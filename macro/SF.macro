*          DATA SET MCSF       AT LEVEL 044 AS OF 08/03/04                      
         MACRO                                                                  
&TAG     SF    &FW1,&FW2                                                        
.*                                                                              
.* IF &FW1 AND &FW2 ARE FULL WORD SYMBOLS OR END WITH '('                       
         AIF   (((T'&FW1 EQ 'F') OR ('&FW1'(K'&FW1,1) EQ ')')) AND     X        
               ((T'&FW2 EQ 'F') OR ('&FW2'(K'&FW2,1) EQ ')'))).FF               
.*IF &FW1 IS NOT A AND &FW2 IS FULLWORD SYMBOL OR ENDS WTH ')'                  
         AIF   (((T'&FW1 NE 'F') OR ('&FW1'(K'&FW1,1) NE ')')) AND     X        
               ((T'&FW2 EQ 'F') OR ('&FW2'(K'&FW2,1) EQ ')'))).RF               
.*IF &FW1 IS A FULL WORD SYMBOL OR ENDS WITH '(' AND &FW2 IS AN                 
.*   IMEDIATE DECIMAL VALUE(SELF DEFINING TERM) <= 4096                         
         AIF   (((T'&FW1 EQ 'F') OR ('&FW1'(K'&FW1,1) EQ ')')) AND     X        
               ((T'&FW2 EQ 'N') AND (&FW2 LE 4096))).FWDI                       
.*IF &FW1 IS NOT A FULL WORD SYMBOL OR ENDS WITH '(' AND &FW2 IS AN             
.*   IMEDIATE DECIMAL VALUE(SELF DEFINING TERM) <= 4096                         
         AIF   (((T'&FW1 NE 'F') OR ('&FW1'(K'&FW1,1) NE ')')) AND     X        
               ((T'&FW2 EQ 'N') AND (&FW2 LE 4096))).REGI                       
.*IF &FW1 IS A FULL WORD SYMBOL OR ENDS WITH '(' AND &FW2 IS AN                 
.*   IMEDIATE DECIMAL VALUE(SELF DEFINING TERM) > 4096                          
         AIF   (((T'&FW1 EQ 'F') OR ('&FW1'(K'&FW1,1) EQ ')')) AND     X        
               ((T'&FW2 EQ 'N') AND (&FW2 GT 4096))).FWDBI                      
.*IF &FW1 IS NOT A FULL WORD SYMBOL OR ENDS WITH '(' AND &FW2 IS AN             
.*   IMEDIATE DECIMAL VALUE(SELF DEFINING TERM) > 4096                          
         AIF   (((T'&FW1 NE 'F') OR ('&FW1'(K'&FW1,1) NE ')')) AND     X        
               ((T'&FW2 EQ 'N') AND (&FW2 GT 4096))).REGBI                      
.*DON'T KNOW HOW TO HANDLE THIS SET OF OPERANDS                                 
         MNOTE 12,'***OPERANDS NOTE SUPPORTED BY SF'                            
         AGO   .END                                                             
.FF      ANOP                                                                   
&TAG     L     R1,&FW1                                                          
         S     R1,&FW2                                                          
         ST    R1,&FW1                                                          
         AGO   .END                                                             
*                                                                               
.RF      ANOP                                                                   
&TAG     L     R1,0(&FW1)                                                       
         S     R1,&FW2                                                          
         ST    R1,0(&FW1)                                                       
         AGO   .END                                                             
*                                                                               
.FWDI    ANOP                                                                   
&TAG     LA    R1,&FW2                                                          
         LNR   R1,R1                                                            
         A     R1,&FW1                                                          
         ST    R1,&FW1                                                          
         AGO   .END                                                             
*                                                                               
.REGI    ANOP                                                                   
&TAG     LA    R1,&FW2                                                          
         LNR   R1,R1                                                            
         A     R1,0(&FW1)                                                       
         ST    R1,0(&FW1)                                                       
         AGO   .END                                                             
*                                                                               
.FWDBI   ANOP                                                                   
&TAG     L     R1,&FW1                                                          
         S     R1,=F'&FW2'                                                      
         ST    R1,&FW1                                                          
         AGO   .END                                                             
*                                                                               
.REGBI   ANOP                                                                   
&TAG     L     R1,0(&FW1)                                                       
         S     R1,=F'&FW2'                                                      
         ST    R1,0(&FW1)                                                       
         AGO   .END                                                             
*                                                                               
.END       ANOP                                                                 
         MEXIT                                                                  
         MEND                                                                   
