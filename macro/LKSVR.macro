*          DATA SET MCLKSVR    AT LEVEL 012 AS OF 10/23/17                      
         MACRO                                                                  
&SERVER  LKSVR &TYPE=,                                                 +        
               &CODE=,                                                 +        
               &IDF=,                                                  +        
               &FILES=,                                                +        
               &FILES2=,                                               +        
               &FACS=,                                                 +        
               &REQUEST=,                                              +        
               &IBLOCK=,                                               +        
               &SBLOCK=,                                               +        
               &ABENDLIST=,                                            +        
               &SLOWLIST=,                                             +        
               &SERVERTYPE=,                                           +        
               &SERVERLIST=,                                           +        
               &SYSTEM=,                                               +        
               &SYSTEM2=,                                              +        
               &WORKERKEY=,                                            +        
               &APPEND=,                                               +        
               &SYSPHASE=,                                             +        
               &LOADFACSOFF=,                                          +        
               &SEGMENT=,                                              +        
               &USERIDS=,                                              +        
               &LINKIO=,                                               +        
               &AUTOCLEAR=,                                            +        
               &EODID=,                                                +        
               &BLOCKS=,                                               +        
               &BLOCKS2=,                                              +        
               &IFROM=,                                                +        
               &ITO=,                                                  +        
               &ILEN=,                                                 +        
               &RLEN=,                                                 +        
               &IDIAL=,                                                +        
               &BIG=,                                                  +        
               &CSECT=,     *** ONLY DDLINK SHOULD USE THIS PARAMETER  +        
               &PROGRAM=,                                              +        
               &FACWRK=,                                               +        
               &FASTASSM=                                                       
.*                                                                              
         GBLC  &SERVER_TYPE        U(PLOAD)/D(OWNLOAD)                          
         GBLB  &SERVER_NATIVE      DDLINK APPLICATION                           
         GBLB  &SERVER_SENDING     UPLOAD AND SEND                              
         GBLB  &SERVER_RECEIVING   RECEIVE AND PROCESS                          
         GBLB  &SERVER_BIG         BIG SERVER                                   
         GBLB  &SERVER_RUNUPL      RUNNER UPLOAD                                
         GBLC  &SERVER_NAME                                                     
         GBLC  &SYSTEM_NAME                                                     
         GBLB  &FIRST_REQUEST_DEFINITION                                        
         GBLB  &FIRST_OUTPUT_DEFINITION                                         
         GBLB  &FAST_ASSEMBLY                                                   
         GBLC  &REQUEST_NAME                                                    
         GBLC  &BLOCK_EQUATES(15)                                               
         GBLC  &BLOCK_LABELS(15)                                                
         GBLA  &#BLOCK_EQUATES                                                  
         GBLA  &#BLOCK_LABELS                                                   
.*                                                                              
         LCLC  &I1,&I2,&I3,&I4,&I5,&I6,&I7,&I8,&INDS                            
         LCLA  &COUNT                                                           
         LCLB  &SWITCH                                                          
         LCLB  &FACWRKN                                                         
.*                                                                              
         AIF   (T'&CSECT EQ 'O').LKLAB00                                        
         AIF   ('&CSECT'(1,1) NE 'N').ERROR13                                   
         AGO   .LKLAB01                                                         
.LKLAB00 ANOP                                                                   
         AIF   (T'&SERVER NE 'O').LKLAB01                                       
&SERVER_NAME                       SETC  '&SYSECT'                              
         AGO   .LKLAB02                                                         
.LKLAB01 ANOP                                                                   
&SERVER_NAME                       SETC  '&SERVER'                              
.LKLAB02 ANOP                                                                   
.*                                                                              
&SERVER_BIG                        SETB  ('&BIG' EQ 'Y')                        
&FACWRKN                           SETB  ('&FACWRK' EQ 'N')                     
&FIRST_REQUEST_DEFINITION          SETB  1                                      
&FIRST_OUTPUT_DEFINITION           SETB  1                                      
.*                                                                              
         AIF   (T'&TYPE EQ 'O').ERROR1                                          
         AIF   (K'&TYPE EQ 1).LKTYP04                                           
         AIF   ('&TYPE'(1,2) NE 'US').LKTYP00                                   
&SERVER_SENDING                    SETB  1                                      
         AGO   .LKFAST                                                          
.*                                                                              
.LKTYP00 ANOP  ,                                                                
         AIF   ('&TYPE'(1,2) NE 'UO').LKTYP02                                   
&SERVER_RUNUPL                     SETB  1                                      
&SERVER_NATIVE                     SETB  1                                      
         AGO   .LKFAST                                                          
.*                                                                              
.LKTYP02 ANOP  ,                                                                
         AIF   ('&TYPE'(1,2) NE 'UR').ERROR1                                    
&SERVER_RECEIVING                  SETB  1                                      
         AGO   .LKFAST                                                          
.*                                                                              
.LKTYP04 ANOP  ,                                                                
&SERVER_NATIVE                     SETB  1                                      
         AIF   ('&TYPE'(1,1) EQ 'D' OR '&TYPE'(1,1) EQ 'U').LKFAST              
         AGO   .ERROR1                                                          
.*                                                                              
.LKFAST  ANOP  ,                                                                
         AIF   (T'&FASTASSM EQ 'O').LKSVR00                                     
&FAST_ASSEMBLY                     SETB  ('&FASTASSM'(1,1) EQ 'Y')              
.*                                                                              
.LKSVR00 ANOP  ,                                                                
&SERVER_TYPE                       SETC  '&TYPE'(1,1)                           
.*                                                                              
         AIF   (T'&CSECT EQ 'O').LKSVR01                                        
         AIF   ('&CSECT'(1,1) NE 'N').ERROR13                                   
&SERVER  DS    0D                                                               
         AGO   .LKSVR02                                                         
.*                                                                              
.LKSVR01 ANOP  ,                                                                
         AIF   (T'&SERVER EQ 'O').LKSVR02                                       
         MNOTE 12,'Label not permitted on LKSVR macro. Provide a CSECT +        
               statement.'                                                      
&SERVER  CSECT                     *** GENERATED BY LKSVR MACRO                 
.*                                                                              
.LKSVR02 ANOP  ,                                                                
         AIF   (&SERVER_RECEIVING).LKSVR97                                      
.*                                                                              
         DC    (RSVRDEFL)X'00'                                                  
         ORG   &SERVER_NAME                                                     
         AIF   (T'&IDF EQ 'O').LKSVR03                                          
         AIF   ('&IDF'(1,1) NE 'Y').LKSVR03                                     
         AIF   (T'&CODE EQ 'O').ERROR2                                          
         J     &CODE                                                            
         DC    XL4'00'                                                          
         DC    C'*SERVER**SERVER**SERVER*'                                      
         AGO   .LKSVR04                                                         
.*                                                                              
.LKSVR03 ANOP  ,                                                                
         DC    C'*SERVER**SERVER**SERVER**SERVER*'                              
.*                                                                              
.LKSVR04 ANOP  ,                                                                
         AIF   (T'&CODE EQ 'O').LKSVR06                                         
         AIF   (T'&IDF EQ 'O').LKSVR05                                          
         AIF   ('&IDF'(1,1) EQ 'Y').LKSVR06                                     
.*                                                                              
.LKSVR05 ANOP  ,                                                                
         DC    AL2(&CODE-&SERVER_NAME)                                          
         AGO   .LKSVR08                                                         
.*                                                                              
.LKSVR06 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR08 ANOP  ,                                                                
         AIF   (T'&FILES EQ 'O').LKSVR10                                        
         DC    AL2(&FILES-&SERVER_NAME)                                         
         AGO   .LKSVR12                                                         
.*                                                                              
.LKSVR10 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR12 ANOP  ,                                                                
         AIF   (T'&FACS EQ 'O').LKSVR14                                         
         DC    AL2(&FACS-&SERVER_NAME)                                          
         AGO   .LKSVR16                                                         
.*                                                                              
.LKSVR14 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR16 ANOP  ,                                                                
         AIF   (T'&REQUEST EQ 'O').LKSVR19                                      
         AIF   ('&REQUEST'(1,1) NE '*').LKSVR17                                 
&REQUEST_NAME                      SETC  'REQUEST'                              
         AGO   .LKSVR18                                                         
.*                                                                              
.LKSVR17 ANOP  ,                                                                
&REQUEST_NAME                      SETC  '&REQUEST'                             
.*                                                                              
.LKSVR18 ANOP  ,                                                                
         DC    AL2(&REQUEST_NAME-&SERVER_NAME)                                  
         AGO   .LKSVR20                                                         
.*                                                                              
.LKSVR19 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR20 ANOP  ,                                                                
         AIF   (T'&IBLOCK EQ 'O').LKSVR22                                       
         AIF   ('&IBLOCK'(1,1) NE '*').LKSVR21                                  
         DC    AL2(IBLOCK-&SERVER_NAME)                                         
         AGO   .LKSVR24                                                         
.*                                                                              
.LKSVR21 ANOP  ,                                                                
         DC    AL2(&IBLOCK-&SERVER_NAME)                                        
         AGO   .LKSVR24                                                         
.*                                                                              
.LKSVR22 ANOP  ,                                                                
         AIF   (T'&RLEN EQ 'O').LKSVR23                                         
         DC    AL2(&RLEN)                                                       
         AGO   .LKSVR24                                                         
.*                                                                              
.LKSVR23 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR24 ANOP  ,                                                                
         AIF   (T'&ABENDLIST EQ 'O').LKSVR26                                    
         DC    AL2(&ABENDLIST-&SERVER_NAME)                                     
         AGO   .LKSVR28                                                         
.*                                                                              
.LKSVR26 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR28 ANOP  ,                                                                
         AIF   (T'&SLOWLIST EQ 'O').LKSVR30                                     
         DC    AL2(&SLOWLIST-&SERVER_NAME)                                      
         AGO   .LKSVR32                                                         
.*                                                                              
.LKSVR30 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR32 ANOP  ,                                                                
         AIF   (T'&SYSTEM2 EQ 'O').LKSVR34                                      
         DC    AL1(&SYSTEM2)                                                    
         AGO   .LKSVR36                                                         
.*                                                                              
.LKSVR34 ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKSVR36 ANOP  ,                                                                
         AIF   (T'&FILES2 EQ 'O').LKSVR38                                       
         AIF   (T'&SYSTEM2 EQ 'O').ERROR3                                       
         DC    AL2(&FILES2-&SERVER_NAME)                                        
         AGO   .LKSVR40                                                         
.*                                                                              
.LKSVR38 ANOP  ,                                                                
         AIF   (T'&SYSTEM2 NE 'O').ERROR3                                       
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR40 ANOP  ,                                                                
         DC    XL3'00'                                                          
.*                                                                              
         AIF   (T'&SERVERLIST EQ 'O').LKSVR42                                   
         AIF   (T'&SERVERTYPE NE 'O').ERROR4                                    
         DC    AL2(&SERVERLIST-&SERVER_NAME)                                    
         AGO   .LKSVR44                                                         
.*                                                                              
.LKSVR42 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR44 ANOP  ,                                                                
         AIF   (T'&SBLOCK EQ 'O').LKSVR46                                       
         DC    AL2(&SBLOCK-&SERVER_NAME)                                        
         AGO   .LKSVR48                                                         
.*                                                                              
.LKSVR46 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR48 ANOP  ,                                                                
         AIF   (T'&SERVERTYPE EQ 'O').LKSVR50                                   
         DC    AL1(&SERVERTYPE)                                                 
         AGO   .LKSVR52                                                         
.*                                                                              
.LKSVR50 ANOP  ,                                                                
         DC    AL1(0)                                                           
                                                                                
.LKSVR52 ANOP  ,                                                                
         AIF   (T'&SYSTEM EQ 'O').ERROR9                                        
&SYSTEM_NAME                       SETC  '&SYSTEM'                              
         DC    AL1(&SYSTEM)                                                     
.*                                                                              
.LKSVR56 ANOP  ,                                                                
         AIF   (T'&WORKERKEY EQ 'O').LKSVR58                                    
         AIF   (K'&WORKERKEY NE 4).ERROR5                                       
         DC    C'&WORKERKEY'                                                    
         DC    C'F'                                                             
         AGO   .LKSVR60                                                         
.*                                                                              
.LKSVR58 ANOP  ,                                                                
         DC    XL5'00'                                                          
.*                                                                              
.LKSVR60 ANOP  ,                                                                
         AIF   (T'&APPEND EQ 'O').LKSVR62                                       
         AIF   ('&APPEND'(1,1) NE 'Y').LKSVR62                                  
         AIF   (T'&WORKERKEY EQ 'O').ERROR6                                     
         DC    AL1(4)                                                           
         AGO   .LKSVR64                                                         
.*                                                                              
.LKSVR62 ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKSVR64 ANOP  ,                                                                
         AIF   (T'&SYSPHASE EQ 'O').LKSVR66                                     
         DC    AL2(&SYSPHASE)                                                   
         AGO   .LKSVR68                                                         
.*                                                                              
.LKSVR66 ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKSVR68 ANOP  ,                                                                
&I1      SETC  '1'                 USES DDLINK (ALWAYS SET)                     
&I2      SETC  '0'                 LOAD COMFACS OFFLINE                         
&I3      SETC  '0'                 MULTI-SYSTEM SERVER                          
&I4      SETC  '0'                 ALLOW FILE SEGMENTS                          
&I5      SETC  '0'                 USER-IDS REQUIRED                            
&I6      SETC  '0'                 USE LINKIO FOR OUTPUT                        
&I7      SETC  '0'                 IDF COMPATIBLE                               
&I8      SETC  '0'                 AUTO CLEAR REQUEST VALUES                    
.*                                                                              
         AIF   (T'&LOADFACSOFF EQ 'O').LKSVR70                                  
         AIF   ('&LOADFACSOFF'(1,1) NE 'Y').LKSVR70                             
&I2      SETC  '1'                                                              
.*                                                                              
.LKSVR70 ANOP  ,                                                                
         AIF   (T'&FILES2 EQ 'O').LKSVR72                                       
&I3      SETC  '1'                                                              
.*                                                                              
.LKSVR72 ANOP  ,                                                                
         AIF   (T'&SEGMENT EQ 'O').LKSVR74                                      
         AIF   ('&SEGMENT'(1,1) NE 'Y').LKSVR74                                 
         AIF   (T'&WORKERKEY EQ 'O').ERROR6                                     
&I4      SETC  '1'                                                              
.*                                                                              
.LKSVR74 ANOP  ,                                                                
         AIF   (T'&USERIDS EQ 'O').LKSVR76                                      
         AIF   ('&USERIDS'(1,1) NE 'Y').LKSVR76                                 
&I5      SETC  '1'                                                              
.*                                                                              
.LKSVR76 ANOP  ,                                                                
         AIF   (T'&LINKIO EQ 'O').LKSVR78                                       
         AIF   ('&LINKIO'(1,1) NE 'Y').LKSVR78                                  
         AIF   (T'&WORKERKEY EQ 'O').ERROR6                                     
&I6      SETC  '1'                                                              
.*                                                                              
.LKSVR78 ANOP  ,                                                                
         AIF   (T'&IDF EQ 'O').LKSVR80                                          
         AIF   ('&IDF'(1,1) NE 'Y').LKSVR80                                     
&I7      SETC  '1'                                                              
.*                                                                              
.LKSVR80 ANOP  ,                                                                
         AIF   (T'&AUTOCLEAR EQ 'O').LKSVR81                                    
         AIF   ('&AUTOCLEAR'(1,1) NE 'Y').LKSVR81                               
&I8      SETC  '1'                                                              
.*                                                                              
.LKSVR81 ANOP  ,                                                                
&INDS    SETC  '&I1'.'&I2'.'&I3'.'&I4'.'&I5'.'&I6'.'&I7'.'&I8'                  
         DC    B'&INDS'                                                         
.*                                                                              
&I1      SETC  '0'                 BIG SERVER                                   
&I2      SETC  '0'                 RUNNER UPLOAD                                
&I3      SETC  '0'                 N/D                                          
&I4      SETC  '0'                 N/D                                          
&I5      SETC  '0'                 N/D                                          
&I6      SETC  '0'                 N/D                                          
&I7      SETC  '0'                 N/D                                          
&I8      SETC  '0'                 N/D                                          
.*                                                                              
         AIF   (NOT &FACWRKN).LKSVR82                                           
&I3      SETC   '1'                                                             
.*                                                                              
.LKSVR82 ANOP  ,                                                                
         AIF   (NOT &SERVER_BIG).LKSVR83                                        
&I1      SETC  '1'                                                              
.*                                                                              
.LKSVR83 ANOP  ,                                                                
         AIF   (NOT &SERVER_RUNUPL).LKSVR84                                     
&I2      SETC  '1'                                                              
.*                                                                              
.LKSVR84 ANOP  ,                   RSVRIND2                                     
&INDS    SETC  '&I1'.'&I2'.'&I3'.'&I4'.'&I5'.'&I6'.'&I7'.'&I8'                  
         DC    B'&INDS'                                                         
.*                                                                              
         AIF   (T'&EODID EQ 'O').LKSVR85                                        
         AIF   (K'&EODID NE 2).ERROR7                                           
         DC    C'&EODID'                                                        
         AGO   .LKSVR86                                                         
.*                                                                              
.LKSVR85 ANOP  ,                                                                
         DC    X'0000'                                                          
.*                                                                              
.LKSVR86 ANOP  ,                                                                
         AIF   (T'&PROGRAM EQ 'O').LKSVR87                                      
         DC    AL1(&PROGRAM)                                                    
         AGO   .LKSVR96                                                         
.*                                                                              
.LKSVR87 ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKSVR96 ANOP  ,                                                                
         AIF   (T'&BLOCKS EQ 'O').LKSVR99                                       
&SWITCH  SETB  0                                                                
&COUNT   SETA  0                                                                
.*                                                                              
.LKSVR97 ANOP  ,                                                                
&COUNT   SETA  &COUNT+1                                                         
         AIF   (&COUNT GT N'&BLOCKS).LKSVR99                                    
         AIF   (&SWITCH).LKSVR98                                                
&#BLOCK_EQUATES                    SETA  &#BLOCK_EQUATES+1                      
&BLOCK_EQUATES(&#BLOCK_EQUATES)    SETC '&BLOCKS(&COUNT)'                       
&SWITCH  SETB  1                                                                
         AGO   .LKSVR97                                                         
.*                                                                              
.LKSVR98 ANOP  ,                                                                
&#BLOCK_LABELS                     SETA  &#BLOCK_LABELS+1                       
&BLOCK_LABELS(&#BLOCK_LABELS)      SETC  '&BLOCKS(&COUNT)'                      
&SWITCH  SETB  0                                                                
         AGO   .LKSVR97                                                         
.*                                                                              
.LKSVR99 ANOP  ,                                                                
         AIF   (T'&BLOCKS2 EQ 'O').LKSVRA2                                      
&SWITCH  SETB  0                                                                
&COUNT   SETA  0                                                                
.*                                                                              
.LKSVRA0 ANOP  ,                                                                
&COUNT   SETA  &COUNT+1                                                         
         AIF   (&COUNT GT N'&BLOCKS2).LKSVRA2                                   
         AIF   (&SWITCH).LKSVRA1                                                
&#BLOCK_EQUATES                    SETA  &#BLOCK_EQUATES+1                      
&BLOCK_EQUATES(&#BLOCK_EQUATES)    SETC '&BLOCKS2(&COUNT)'                      
&SWITCH  SETB  1                                                                
         AGO   .LKSVRA0                                                         
.*                                                                              
.LKSVRA1 ANOP  ,                                                                
&#BLOCK_LABELS                     SETA  &#BLOCK_LABELS+1                       
&BLOCK_LABELS(&#BLOCK_LABELS)      SETC  '&BLOCKS2(&COUNT)'                     
&SWITCH  SETB  0                                                                
         AGO   .LKSVRA0                                                         
.*                                                                              
.LKSVRA2 ANOP  ,                                                                
         AIF   (&#BLOCK_EQUATES NE &#BLOCK_LABELS).ERROR8                       
         AIF   (&SERVER_RECEIVING).LKSVRX                                       
         ORG                                                                    
.*                                                                              
         AIF   (T'&IBLOCK EQ 'O').LKSVRX                                        
         AIF   (T'&IFROM EQ 'O').LKSVRX                                         
         AIF   (N'&IFROM NE 2).ERROR10                                          
         AIF   (K'&IFROM(1) GT 3).ERROR10                                       
         AIF   (K'&IFROM(2) GT 3).ERROR10                                       
         AIF   (N'&ITO NE 2).ERROR12                                            
         AIF   (K'&ITO(1) GT 3).ERROR12                                         
         AIF   (K'&ITO(2) GT 3).ERROR12                                         
         AIF   (T'&ILEN EQ 'O').ERROR11                                         
.*                                                                              
         AIF   ('&IBLOCK'(1,1) NE '*').LKSVRA4                                  
IBLOCK   DS    0X                                                               
         AGO   .LKSVRA6                                                         
.*                                                                              
.LKSVRA4 ANOP  ,                                                                
&IBLOCK  DS    0X                                                               
.*                                                                              
.LKSVRA6 ANOP  ,                                                                
         AIF   (T'&ITO EQ 'O').ERROR12                                          
         DC    AL2(&ILEN)                                                       
         DC    CL3'&IFROM(1)',CL3'&IFROM(2)'                                    
         DC    CL3'&ITO(1)',CL3'&ITO(2)'                                        
.*                                                                              
         AIF   (T'&IDIAL EQ 'O').LKSVRA8                                        
         AIF   ('&IDIAL'(1,1) NE 'Y').LKSVRA8                                   
         DC    X'80'                                                            
         DC    XL15'00'                                                         
         AGO   .LKSVRX                                                          
.*                                                                              
.LKSVRA8 ANOP  ,                                                                
         DC    XL16'00'                                                         
         DS    0D                                                               
.*                                                                              
.LKSVRX  ANOP  ,                                                                
         MEXIT                                                                  
.*                                                                              
.ERROR1  ANOP  ,                                                                
         MNOTE 12,'Type parameter missing or invalid'                           
         MEXIT                                                                  
.*                                                                              
.ERROR2  ANOP  ,                                                                
         MNOTE 12,'Label missing'                                               
         MEXIT                                                                  
.*                                                                              
.ERROR3  ANOP  ,                                                                
         MNOTE 12,'SYSTEM2/FILES2 conflict'                                     
         MEXIT                                                                  
.*                                                                              
.ERROR4  ANOP  ,                                                                
         MNOTE 12,'SERVERTYPE= and SERVERLIST= incompatible'                    
         MEXIT                                                                  
.*                                                                              
.ERROR5  ANOP  ,                                                                
         MNOTE 12,'WORKERKEY= must be 4 characters long'                        
         MEXIT                                                                  
.*                                                                              
.ERROR6  ANOP  ,                                                                
         MNOTE 12,'WORKERKEY= keyword missing'                                  
         MEXIT                                                                  
.*                                                                              
.ERROR7  ANOP  ,                                                                
         MNOTE 12,'EODID= must be 2 characters long'                            
         MEXIT                                                                  
.*                                                                              
.ERROR8  ANOP  ,                                                                
         MNOTE 12,'BLOCKS= parameter invalid'                                   
         MEXIT                                                                  
.*                                                                              
.ERROR9  ANOP  ,                                                                
         MNOTE 12,'SYSTEM= parameter missing'                                   
         MEXIT                                                                  
.*                                                                              
.ERROR10 ANOP  ,                                                                
         MNOTE 12,'IFROM= parameter invalid'                                    
         MEXIT                                                                  
.*                                                                              
.ERROR11 ANOP  ,                                                                
         MNOTE 12,'ILEN= parameter missing'                                     
         MEXIT                                                                  
.*                                                                              
.ERROR12 ANOP  ,                                                                
         MNOTE 12,'ITO= parameter missing or invalid'                           
         MEXIT                                                                  
.*                                                                              
.ERROR13 ANOP  ,                                                                
         MNOTE 12,'CSECT=Y no longer supported'                                 
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
