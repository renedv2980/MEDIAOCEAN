*          DATA SET MCPEQUS    AT LEVEL 008 AS OF 03/03/21                      
         MACRO                                                                  
         PEQUS &DSECT=                                                          
.*                                                                              
.* GENERATE EQU STATEMENTS FOR AVAILABILITY THROUGHOUT ALL SOURCE CODE.         
.*                                                                              
         GBLA  &GAPEQUS                                                         
.*                                                                              
.* ONLY GENERATE THESE EQU STATEMENTS ONCE PER ASSEMBLY.                        
.*                                                                              
         AIF   (&GAPEQUS NE 0).EXIT                                             
&GAPEQUS SETA  1                                                                
.*                                                                              
.* IF A DSECT= PARAMETER IS EXPLICITY PROVIDED, IT IS HONORED. O/W:             
.* IF WE ARE NOT CURRENTLY WITHIN A PROGRAM SECTION (CSECT, RSECT,              
.* DSECT, OR COM), WE GENERATE A DSECT STATEMENT HERE. IF WE DON'T,             
.* THEN THE FIRST EQU STATEMENT WILL FORCE THE CREATION OF AN UNNAMED           
.* "PRIVATE CODE" CSECT (WHICH IBM RECOMMENDS AGAINST).                         
.*                                                                              
         AIF   (T'&DSECT EQ 'O').CHKSTYP                                        
         AIF   ('&DSECT' EQ 'NO').NODSECT                                       
         AIF   ('&DSECT' EQ 'YES').DSECT                                        
         MNOTE 8,'INVALID DSECT= PARAMETER'                                     
         AGO   .EXIT                                                            
.*                                                                              
.CHKSTYP AIF   ('&SYSSTYP' NE '').NODSECT                                       
.*                                                                              
.DSECT   DSECT ,                                                                
.NODSECT ANOP                                                                   
.*                                                                              
.* RESERVED PROGRAM TYPE ATTRIBUTE SYMBOLS                                      
.*                                                                              
.* THE CURRENT PLAN IS TO RESERVE NUMBERS < 512 FOR DATATYPES.                  
.* NUMBERS IN THE 500'S DESIGNATE EQU TYPES FOR DSECT MAPPING (OR FOR           
.* ANY OTHER PURPOSE).                                                          
.* THE HOB IS RESERVED FOR FLAGS (WHICH CAN BE LOGICALLY ADDED TO               
.* DATATYPES, E.G. DT02+DTSPCL).                                                
.*                                                                              
DT02     EQU   1                   TYPE-2 COMPRESSED DATE                       
DT02X    EQU   2                   TYPE-2 COMPRESSED DATE (1'S COMP.)           
DT02N    EQU   5                   TYPE-2 COMPRESSED DATE (2'S COMP.)           
DT14     EQU   3                   TYPE-14 COMPRESSED DATE                      
DT14X    EQU   4                   TYPE-14 COMPRESSED DATE (1'S COMP.)          
DT14N    EQU   6                   TYPE-14 COMPRESSED DATE (2'S COMP.)          
.*                                                                              
DTSPCL   EQU   X'40000000',4,C'X'  TYPE-2 "SPECIAL": PROCESS VIA HOOK           
DTIGNOR  EQU   X'20000000',4,C'X'  IGNORE DURING LAYOUT TBL GENERATION          
DTNOCNV  EQU   X'20000000',4,C'X'  (DEPRECATED: USE DTIGNOR INSTEAD)            
.*                                                                              
PTYPE_ELCODE EQU 512               ELEMENT CODE EQUATE INDICATOR                
         PUSH  PRINT                                                            
         PRINT NOGEN                                                            
.* THE FOLLOWING SYMBOLS ARE GENERATED BY MACROS, AND TYPICALLY NEED            
.* NOT BE SPECIFIED IN OPEN CODE. WE THEREFORE SUPPRESS THEM FROM THE           
.* LISTING. I.E., WE ONLY SHOW PROGRAM TYPES THAT DEVELOPERS MIGHT              
.* ACTUALLY NEED TO KNOW ABOUT.                                                 
PTYPE_RELATED_ELEMENT EQU 513      ELEMENT MAY APPEAR IN THIS RECORD            
PTYPE_ACTIVE          EQU 514      ACTIVE KEY DSECT                             
PTYPE_PASSIVE         EQU 515      PASSIVE KEY DSECT                            
PTYPE_DUPL_HEX_ELCODE EQU 516      DUPLICATE HEX ELEMENT CODE                   
         POP   PRINT                                                            
.*                                                                              
.EXIT    ANOP                                                                   
.*                                                                              
         MEND                                                                   
