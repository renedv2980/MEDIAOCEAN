*          DATA SET MCDMVS     AT LEVEL 004 AS OF 09/19/18                      
         MACRO                                                                  
&FILE    DMVS  &KEYLEN=,&RECSIZE=,&FILABL=,&MERGE=NO,                  X        
               &ACB=YES,&GLOBAL=NO,&FLASH=NO,                          X        
               &MACRF=,&RMODE31=ALL,&BUFND=2,&BUFNI=1,&RPLS=1                   
.*                                                                              
         GBLC  &DDSCTRY                                                         
         AIF   ('&DDSCTRY' NE '').CTRYOK                                        
         MNOTE 8,'DDSCTRY SYMBOL NOT SET: USE REQUS MACRO'                      
.CTRYOK  ANOP                                                                   
.*                                                                              
         GBLC  &OFFLINE                                                         
         LCLC  &ACBNAME,&OFF                                                    
         LCLA  &KEY,&A,&I,&V                                                    
.*                                                                              
&OFF     SETC  '&OFFLINE'.'XX'                                                  
&ACBNAME SETC  '&FILE.Z'                                                        
&ACBNAMT SETC  '&FILE.T'                                                        
.*                                                                              
.******* START OF DTF GENERATION                                                
.*                                                                              
.DTF10   ANOP                                                                   
         DC    0D'0'                                                            
         DC    2F'0'                                                            
&FILE    DC    X'000080',AL1(0),AL1(&RPLS),X'00FFFF' ISFBUFFS=&RPLS             
         DC    XL8'00',X'000000'                                                
.*                                                                              
&I       SETA  128                 VSAM MODE X'80'                              
.*                                                                              
.DTF020  ANOP                                                                   
         AIF   (T'&GLOBAL EQ 'O').DTF020X                                       
         AIF   ('&GLOBAL' EQ 'NO').DTF020X                                      
         AIF   ('&GLOBAL'(1,1) EQ 'Y').DTF0201                                  
         MNOTE 8,'INVALID GLOBAL PARAMETER'                                     
         MEXIT                                                                  
.DTF0201 ANOP                                                                   
&I       SETA  &I+64               SET GLOBAL RESOURCE X'40'                    
.DTF020X ANOP                                                                   
.DTF030  ANOP                                                                   
         AIF   (T'&FLASH EQ 'O').DTF030X                                        
         AIF   ('&FLASH'(1,1) EQ 'N').DTF030X                                   
         AIF   ('&FLASH'(1,1) EQ 'Y').DTF0301                                   
         MNOTE 8,'INVALID FLASH PARAMETER'                                      
         MEXIT                                                                  
.DTF0301 ANOP                                                                   
&I       SETA  &I+02               SET FLASH FLAG X'02'                         
.DTF030X ANOP                                                                   
.*                                                                              
         DC    AL1(&I)             ISFFLAG (ISFVSAM/ISFGLOB/ISFFLSH)            
.*                                                                              
&I       SETA  16                  SET DTF IS FOR AN IS FILE X'10'              
.DTF040  ANOP                                                                   
         AIF   (T'&ACB EQ 'O').DTF040X                                          
         AIF   ('&ACB'(1,1) EQ 'Y').DTF040X                                     
         AIF   ('&ACB' EQ 'NO').DTF0401                                         
         MNOTE 8,'INVALID ACB PARAMETER'                                        
         MEXIT                                                                  
.DTF0401 ANOP                                                                   
&I       SETA  &I+64               SET NO ACB MODE X'40'                        
.DTF040X ANOP                                                                   
.*                                                                              
         DC    AL1(&I)             ISFTYPE (ISFTIS/ISFNOD)                      
.*                                                                              
         DC    AL1(0)              N/D                                          
         DC    CL8'&FILE'          ISFDD                                        
         DC    H'0',AL1(0),AL3(0)  ISFXSEQ/ISFXTAB (VSAM NOT USED)              
.*                                                                              
.DTF050  ANOP                                                                   
         AIF   (T'&ACB EQ 'O').DTF0502                                          
         AIF   ('&ACB' NE 'NO').DTF0502                                         
         DC    A(0)                ISFOPEN/ISFADCB                              
         AIF   (T'&FILABL EQ 'O').DTF0501                                       
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'        ISFDD                                        
         ORG                                                                    
.DTF0501 ANOP                                                                   
         MEXIT                                                                  
.DTF0502 ANOP                                                                   
         DC    A(&ACBNAME)         ISFOPEN/ISFADCB                              
         AIF   (T'&FILABL EQ 'O').DTF050X                                       
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'        ISFDD                                        
         ORG                                                                    
.DTF050X ANOP                                                                   
.*                                                                              
.DTF060  ANOP                                                                   
.*                                                                              
.* ******************************* END OF XL40 DTF GENERATION                   
.*                                                                              
         SPACE 1                                                                
         DC    AL2(0)              *ISSPARE - UNUSED FOR VSAM                   
.DTF070  ANOP                                                                   
         AIF   ('&RECSIZE' EQ 'V').DTF0701                                      
         AIF   (T'&RECSIZE EQ 'N').DTF0702                                      
         MNOTE 8,'INVALID RECSIZE FIELD'                                        
         MEXIT                                                                  
.DTF0701 ANOP                                                                   
&V       SETA  128                                                              
         DC    AL2(0)              ISRECLN - RECORD LENGTH VARIABLE             
         AGO   .DTF070X                                                         
.DTF0702 ANOP                                                                   
&V       SETA  0                                                                
         DC    AL2(&RECSIZE)       ISRECLN - RECORD LENGTH                      
.DTF070X ANOP                                                                   
.*                                                                              
.DTF080  ANOP                                                                   
         AIF   (T'&KEYLEN NE 'O' AND T'&KEYLEN EQ 'N').DTF0802                  
.DTF0801 MNOTE 8,'INVALID KEY LENGTH'                                           
         MEXIT                                                                  
.DTF0802 ANOP                                                                   
&KEY     SETA  &KEYLEN                                                          
         AIF   (&KEY EQ 0).DTF0801                                              
         DC    AL2(&KEY)           ISKEYLN  - KEY LENGTH                        
         DC    AL2(&KEY-1)         ISKEYLN1 - KEY LENGTH - 1                    
.DTF080X ANOP                                                                   
.*                                                                              
         DC    AL2(0)              *ISPDLN - UNUSED FOR VSAM                    
         DC    AL2(0)              *ISTILN - UNUSED FOR VSAM                    
         DC    AL2(0)              *ISCILN - UNUSED FOR VSAM                    
         DC    AL2(0)              *ISTITRK - UNUSED FOR VSAM                   
         DC    A(0)                *ISBUFF1 - UNUSED FOR VSAM                   
         DC    A(0)                *ISBUFF1X - UNUSED FOR VSAM                  
         DC    A(0)                *ISBUFF2 - UNUSED FOR VSAM                   
         DC    A(0)                *ISTIBUFF - UNUSED FOR VSAM                  
         DC    A(0)                *ISINDX - REUSED FOR VSAM TEST FILE          
         AIF   ('&MERGE' EQ 'NO').DTF0901                                       
         ORG   *-4                                                              
         DC    A(&ACBNAMT)         *ISINDX - VSAM TEST FILE ACB                 
.DTF0901 ANOP                                                                   
         DC    AL2(0)              *ISINDXLN - UNUSED FOR VSAM                  
         DC    AL2(0)              *ISIXLN - UNUSED FOR VSAM                    
         DC    AL1(0)              *ISINDXTY - UNUSED FOR VSAM                  
         DC    AL1(&V)             ISCMPRSW (80=V/L)                            
         SPACE 1                                                                
         DC    AL2(0)              *ISTRKS - UNUSED FOR VSAM                    
         DC    AL2(0)              *ISHIPD - UNUSED FOR VSAM                    
         DC    AL2(0)              *ISHITI - UNUSED FOR VSAM                    
         DC    AL2(0)              *ISHICI - UNUSED FOR VSAM                    
         DC    AL2(0)              N/D                                          
         SPACE 1                                                                
         DC    XL4'0'              *ISPDLAST - UNUSED FOR VSAM                  
         DC    XL4'0'              *ISCILAST - UNUSED FOR VSAM                  
         DC    XL4'0'              *ISOVLAST - UNUSED FOR VSAM                  
         DC    XL4'0'              *ISPDDA - UNUSED FOR VSAM                    
         DC    XL4'0'              *ISOVDA - UNUSED FOR VSAM                    
         DC    A(0)                *ISPDPTR - UNUSED FOR VSAM                   
.*                                                                              
         DC    A(0)                *ISPDKEY - UNUSED FOR VSAM                   
         DC    A(0)                *ISPDPRKY - UNUSED FOR VSAM                  
         DC    A(0)                *ISTIHIKY - UNUSED FOR VSAM                  
.*                                                                              
.*       FOR VSAM, READONLY OR UPDATE DETERMINED BY MACRF                       
.*                                                                              
&ACBNAME ACB   AM=VSAM,DDNAME=&FILE,MACRF=&MACRF,RMODE31=&RMODE31,     X        
               BUFND=&BUFND,BUFNI=&BUFNI                                        
.*                                                                              
         AIF   (T'&FILABL EQ 'O').DTF900                                        
         ORG   &ACBNAME+40                                                      
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
         AIF   ('&MERGE' EQ 'NO').DTF900                                        
.*                                                                              
.*       SECOND ACB FOR TEST PART OF TEST PAIR                                  
.*                                                                              
&ACBNAMT ACB   AM=VSAM,DDNAME=&ACBNAMT,MACRF=&MACRF,RMODE31=&RMODE31,  X        
               BUFND=&BUFND,BUFNI=&BUFNI                                        
.*                                                                              
         AIF   ('&MERGE' EQ 'YES').DTF900                                       
         ORG   &ACBNAMT+40                                                      
         DC    CL8'&MERGE'                                                      
         ORG                                                                    
.DTF900  ANOP                                                                   
         MEXIT                                                                  
         MEND                                                                   
