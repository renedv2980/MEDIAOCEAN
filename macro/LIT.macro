*          DATA SET MCLIT      AT LEVEL 003 AS OF 09/08/13                      
         MACRO                                                                  
         LIT   &LIT                                                             
         GBLC  &LITVAL,&LITTYP                                                  
         GBLA  &LITLEN                                                          
         LCLC  &CHAR,&HEX                                                       
         LCLC  &A,&B,&C,&D                                                      
         LCLA  &I,&J,&K,&NUM,&LEN                                               
.*                                                                              
&NUM     SETA  0                                                                
&I       SETA  1                                                                
         AIF   ('&LIT'(1,1) LT '1').M20                                         
&B       SETC  '&LIT'(1,1)                                                      
&I       SETA  2                                                                
         AIF   ('&LIT'(2,1) LT '0').M12                                         
&B       SETC  '&LIT'(1,2)                                                      
&I       SETA  3                                                                
         AIF   ('&LIT'(3,1) LT '0').M12                                         
&B       SETC  '&LIT'(1,3)                                                      
&I       SETA  4                                                                
.M12     ANOP                                                                   
&NUM     SETA  &B                                                               
.*                                 VALIDATE CONSTANT TYPE                       
.M20     ANOP                                                                   
&LITTYP  SETC  '&LIT'(&I,1)                                                     
         AIF   ('&LITTYP' EQ 'C').MCHAR                                         
         AIF   ('&LITTYP' EQ 'X').MHEX                                          
         AGO   .MERR3                                                           
.*                                 CHARACTER CONSTANTS                          
.MCHAR   ANOP                                                                   
&K       SETA  0                                                                
&J       SETA  &I+1                                                             
         AIF   ('&LIT'(&J,1) NE 'L').MCHAR4                                     
&J       SETA  &J+1                                                             
         AIF   ('&LIT'(&J,1) LT '1').MERR6                                      
&B       SETC  '&LIT'(&I+2,1)                                                   
&J       SETA  &J+1                                                             
         AIF   ('&LIT'(&J,1) LT '0').MCHAR2                                     
&B       SETC  '&LIT'(&I+2,2)                                                   
&J       SETA  &J+1                                                             
         AIF   ('&LIT'(&J,1) LT '0').MCHAR2                                     
&B       SETC  '&LIT'(&I+2,3)                                                   
.MCHAR2  ANOP                                                                   
&K       SETA  &B                                                               
         AIF   ('&LIT'(&J,1) NE '''').MERR4                                     
         AIF   ('&LIT'(K'&LIT,1) NE '''').MERR4                                 
&LITVAL  SETC  '&LIT'                                                           
&LITLEN  SETA  &K                                                               
         AGO   .M30                                                             
.MCHAR4  ANOP                                                                   
         AIF   ('&LIT'(&J,1) NE '''').MERR4                                     
         AIF   ('&LIT'(K'&LIT,1) NE '''').MERR4                                 
&LITVAL  SETC  '&LIT'                                                           
&LITLEN  SETA  K'&LIT-&J-1                                                      
         AGO   .M30                                                             
.*                                 HEXADECIMAL CONSTANTS                        
.MHEX    ANOP                                                                   
&K       SETA  0                                                                
&J       SETA  &I+1                                                             
         AIF   ('&LIT'(&J,1) NE 'L').MHEX4                                      
&J       SETA  &J+1                                                             
         AIF   ('&LIT'(&J,1) LT '1').MERR6                                      
&B       SETC  '&LIT'(&I+2,1)                                                   
&J       SETA  &J+1                                                             
         AIF   ('&LIT'(&J,1) LT '0').MHEX2                                      
&B       SETC  '&LIT'(&I+2,2)                                                   
&J       SETA  &J+1                                                             
         AIF   ('&LIT'(&J,1) LT '0').MHEX2                                      
&B       SETC  '&LIT'(&I+2,3)                                                   
.MHEX2   ANOP                                                                   
&K       SETA  &B                                                               
         AIF   ('&LIT'(&J,1) NE '''').MERR4                                     
         AIF   ('&LIT'(K'&LIT,1) NE '''').MERR4                                 
&LITVAL  SETC  '&LIT'                                                           
&LITLEN  SETA  &K                                                               
         AGO   .M30                                                             
.MHEX4   ANOP                                                                   
         AIF   ('&LIT'(&J,1) NE '''').MERR4                                     
         AIF   ('&LIT'(K'&LIT,1) NE '''').MERR4                                 
&LITVAL  SETC  '&LIT'                                                           
&LITLEN  SETA  (K'&LIT-&J)/2                                                    
         AGO   .M30                                                             
.*                                 CALCULATE TOTAL LITERAL LENGTH               
.M30     ANOP                                                                   
         AIF   (&NUM EQ 0).M32                                                  
&LITLEN  SETA  &LITLEN*&NUM                                                     
.M32     ANOP                                                                   
         AIF   (&LITLEN GT 249 OR &LITLEN EQ 0).MERR5                           
         AGO   .MX                                                              
.MERR3   MNOTE 8,'INVALID LITERAL TYPE'                                         
         MEXIT                                                                  
.MERR4   MNOTE 8,'MISSING QUOTES'                                               
         MEXIT                                                                  
.MERR5   MNOTE 8,'LITERAL TOO LONG OR ZERO LENGTH'                              
         MEXIT                                                                  
.MERR6   MNOTE 8,'INVALID LENGTH MODIFIER'                                      
         MEXIT                                                                  
.MERR7   MNOTE 8,'LENGTH MODIFIER REQUIRED FOR PACKED'                          
         MEXIT                                                                  
.MERR8   MNOTE 8,'PACKED HAS A MAXIMUM LENGTH OF 16'                            
         MEXIT                                                                  
.*                                                                              
.MX      ANOP                                                                   
         MEXIT                                                                  
         MEND                                                                   
