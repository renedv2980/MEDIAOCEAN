*          DATA SET DFTIDMCN   AT LEVEL 004 AS OF 03/30/09                      
***********************************************************************         
* THESE MACROS ARE USED IN DEDEMTABOF.                                          
* THEY ARE NEEDED TO DEFINE THE TABLES OF NEW FORMULAS USED FOR                 
* AUDIENCE ESTIMATOR.                                                           
***********************************************************************         
                                                                                
***********************************************************************         
* FMSBK - MACRO DEFINITION USED IN DEDEMTABOF TO DEFINE THE                     
*         FILE/MEDIA/SOURCE/BOOK/TYPE FOR THE NEW FORMULA TABLES.               
*         THE NEW FORMULA TABLES ARE USED BY AUDIENCE ESTIMATOR.                
***********************************************************************         
                                                                                
         MACRO                                                                  
         FMSBK &KEY,&BOOK,&TYPE,&LABEL                                          
                                                                                
         LCLC  &YEAR,&MONWK,&TBID,&TYPEWM                                       
                                                                                
         AIF   (T'&KEY EQ 'O').MISSING                                          
         AIF   (T'&BOOK EQ 'O').MISSING                                         
         AIF   (T'&TYPE EQ 'O').MISSING                                         
         AIF   (T'&LABEL EQ 'O').MISSING                                        
         AIF   (K'&KEY NE 3).BADKEY                                             
         AIF   (K'&BOOK NE 5).BADDATE                                           
         AIF   (K'&LABEL NE 7).BADLBL                                           
         AIF   ('&BOOK'(3,1) NE '/').BADDATE                                    
         AIF   ('&TYPE' EQ 'WEEKLY').OKWEEK                                     
         AIF   ('&TYPE' EQ 'MONTHLY').OKMONTH                                   
         AGO   .BADTYPE                                                         
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
.BADKEY  MNOTE 8,'FIRST PARAMETER MUST BE OF LENGTH 3'                          
         MEXIT                                                                  
.BADDATE MNOTE 8,'DATE EXPRESSION MUST BE MM/YY OR WK/YY'                       
         MEXIT                                                                  
.BADLABL MNOTE 8,'LABEL MUST BE OF LENGTH 7'                                    
         MEXIT                                                                  
.BADTYPE MNOTE 8,'TYPE MUST BE WEEKLY OR MONTHLY'                               
         MEXIT                                                                  
                                                                                
.OKWEEK  ANOP                                                                   
&TYPEWM  SETC  'DEMFWKQ'                                                        
         AGO   .OKAY                                                            
.OKMONTH ANOP                                                                   
&TYPEWM  SETC  'DEMFMKQ'                                                        
         AGO   .OKAY                                                            
                                                                                
.OKAY    ANOP                                                                   
&YEAR    SETC  '&BOOK'(4,2)                                                     
&MONWK   SETC  '&BOOK'(1,2)                                                     
&TBID    SETC  '&KEY&MONWK&YEAR'                                                
                                                                                
&TBID    DC    CL7'&TBID',AL1(&YEAR,&MONWK),AL1(&TYPEWM)                        
         DC    AL4(X&LABEL-&LABEL)    DISPL TO A(NEXT F/M/S/BK TABLE)           
                                                                                
         MEND                                                                   
***********************************************************************         
* MACRO DEFINITIONS FOR TABLES OF FORMULAS BY TABLE ID                          
***********************************************************************         
                                                                                
*---------------------------------------------------------------------*         
* SFTID - MACRO FOR START OF TABLE                                              
*---------------------------------------------------------------------*         
         MACRO                                                                  
         SFTID &TBLID                                                           
                                                                                
         GBLC  &TID                                                             
         GBLA  &STFLAG,&OVFLAG,&SMFLAG                                          
                                                                                
         AIF   (T'&TBLID EQ 'O').MISSING                                        
         AIF   (K'&TBLID NE 7).BADTID                                           
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
.BADTID  MNOTE 8,'TABLE ID MUST BE OF LENGTH 7'                                 
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
&TID     SETC  '&TBLID'                                                         
&STFLAG  SETA  0                                                                
&OVFLAG  SETA  0                                                                
&SMFLAG  SETA  0                                                                
                                                                                
* SEE DSECT DEMFFRMD                                                            
S&TBLID  DS    0X                                                               
         DC    AL4(G&TBLID-S&TBLID)    DISPL TO A(GENERAL MACROS)               
         DC    AL4(E&TBLID-S&TBLID)    DISPL TO A(SPECIFIC MACROS)              
         DC    AL4(F&TBLID-S&TBLID)    DISPL TO A(FORMULAS BY MODIFIER)         
         DC    AL4(O&TBLID-S&TBLID)    DISPL TO A(OVERRIDE RELTNL FOMS)         
         DC    AL4(0)                  SPARE                                    
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* GMACR - MACRO FOR GENERAL MACRO ENTRY                                         
*---------------------------------------------------------------------*         
         MACRO                                                                  
         GMACR &MOD,&FORM                                                       
                                                                                
         GBLC  &TID                                                             
         GBLA  &STFLAG                                                          
         LCLC  &MOD2                                                            
                                                                                
         AIF   (T'&MOD EQ 'O').MISSING                                          
         AIF   (T'&FORM EQ 'O').MISSING                                         
         AIF   (K'&MOD GT 2).BADMOD                                             
         AIF   (K'&FORM GT 50).BADFORM                                          
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
.BADMOD  MNOTE 8,'MODIFIER MUST BE OF LENGTH 1 OR 2'                            
         MEXIT                                                                  
.BADFORM MNOTE 8,'FORMULA MUST BE OF LENGTH AT MOST 50'                         
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
                                                                                
&MOD2    SETC  '&MOD'                                                           
         AIF   (K'&MOD NE 1).GM05                                               
&MOD2    SETC  '&MOD '                                                          
.GM05    ANOP                                                                   
                                                                                
         AIF   (&STFLAG NE 0).GM10                                              
* SEE DSECT DEMFGMCD                                                            
G&TID    DC    C'&MOD2',CL50'&FORM'                                             
&STFLAG  SETA  1                                                                
         AGO   .GM20                                                            
.GM10    ANOP                                                                   
         DC    C'&MOD2',CL50'&FORM'                                             
.GM20    ANOP                                                                   
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* SMACR - MACRO FOR SPECIFIC MACRO ENTRY                                        
*---------------------------------------------------------------------*         
         MACRO                                                                  
         SMACR &MOD,&DEMNO,&FORM                                                
                                                                                
         GBLC  &TID                                                             
         GBLA  &STFLAG,&SMFLAG                                                  
         LCLC  &MOD2                                                            
                                                                                
         AIF   (T'&MOD EQ 'O').MISSING                                          
         AIF   (T'&DEMNO EQ 'O').MISSING                                        
         AIF   (T'&FORM EQ 'O').MISSING                                         
         AIF   (K'&MOD GT 2).BADMOD                                             
         AIF   (K'&FORM GT 50).BADFORM                                          
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
.BADMOD  MNOTE 8,'MODIFIER MUST BE OF LENGTH 1 OR 2'                            
         MEXIT                                                                  
.BADFORM MNOTE 8,'FORMULA MUST BE OF LENGTH AT MOST 50'                         
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
&SMFLAG  SETA  1                                                                
                                                                                
&MOD2    SETC  '&MOD'                                                           
         AIF   (K'&MOD NE 1).SM05                                               
&MOD2    SETC  '&MOD '                                                          
.SM05    ANOP                                                                   
                                                                                
         AIF   (&STFLAG NE 0).SM10                                              
* SEE DSECT DEMFSMCD                                                            
E&TID    DC    C'&MOD2',AL2(&DEMNO),CL50'&FORM'                                 
&STFLAG  SETA  1                                                                
         AGO   .SM20                                                            
.SM10    ANOP                                                                   
         DC    C'&MOD2',AL2(&DEMNO),CL50'&FORM'                                 
.SM20    ANOP                                                                   
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* OVREL - MACRO FOR OVERRIDE RELATIONAL FORMULA ENTRY                           
*---------------------------------------------------------------------*         
         MACRO                                                                  
         OVREL &MOD,&FROMOD,&FORM                                               
                                                                                
         GBLC  &TID                                                             
         GBLA  &STFLAG,&OVFLAG                                                  
         LCLC  &MOD2,&FROMOD2                                                   
                                                                                
         AIF   (T'&MOD EQ 'O').MISSING                                          
         AIF   (T'&FROMOD EQ 'O').MISSING                                       
         AIF   (T'&FORM EQ 'O').MISSING                                         
         AIF   (K'&MOD GT 2).BADMOD                                             
         AIF   (K'&FROMOD GT 2).BADMOD                                          
         AIF   (K'&FORM GT 50).BADFORM                                          
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
.BADMOD  MNOTE 8,'MODIFIER MUST BE OF LENGTH 1 OR 2'                            
         MEXIT                                                                  
.BADFORM MNOTE 8,'FORMULA MUST BE OF LENGTH AT MOST 50'                         
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
&OVFLAG  SETA  1                                                                
                                                                                
&MOD2    SETC  '&MOD'                                                           
         AIF   (K'&MOD NE 1).OR05                                               
&MOD2    SETC  '&MOD '                                                          
.OR05    ANOP                                                                   
                                                                                
&FROMOD2 SETC  '&FROMOD'                                                        
         AIF   (K'&FROMOD NE 1).OR06                                            
&FROMOD2 SETC  '&FROMOD '                                                       
.OR06    ANOP                                                                   
                                                                                
         AIF   (&STFLAG NE 0).OR10                                              
* SEE DSECT DEMFOVRD                                                            
O&TID    DC    C'&MOD2',C'&FROMOD2',CL50'&FORM'                                 
&STFLAG  SETA  1                                                                
         AGO   .OR20                                                            
.OR10    ANOP                                                                   
         DC    C'&MOD2',C'&FROMOD2',CL50'&FORM'                                 
.OR20    ANOP                                                                   
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* FFTID - MACRO FOR START OF FORMULAS                                           
*---------------------------------------------------------------------*         
         MACRO                                                                  
         FFTID                                                                  
                                                                                
         GBLC  &TID                                                             
         GBLA  &STFLAG,&SMFLAG,&OVFLAG                                          
                                                                                
         AIF   (&SMFLAG NE 0).FFT10                                             
E&TID    EQU   S&TID               MAKE A(SPECIFIC MACROS) ZERO                 
.FFT10   ANOP                                                                   
                                                                                
         AIF   (&OVFLAG NE 0).FFT20                                             
O&TID    EQU   S&TID               MAKE A(OVERRIDE RELTNLS) ZERO                
.FFT20   ANOP                                                                   
                                                                                
F&TID    DS    0X                                                               
&STFLAG  SETA  0                                                                
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* FMOD  - MACRO TO DEFINE MODIFIER AND CORRESPONDING TABLES                     
*---------------------------------------------------------------------*         
         MACRO                                                                  
         FMOD  &MOD                                                             
                                                                                
         GBLC  &TID,&TIDM,&C3,&C4                                               
         LCLC  &MOD1,MOD2,&TIDS                                                 
                                                                                
         AIF   (T'&MOD EQ 'O').MISSING                                          
         AIF   (K'&MOD GT 2).BADMOD                                             
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
.BADMOD  MNOTE 8,'MODIFIER MUST BE OF LENGTH 1 OR 2'                            
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
                                                                                
&MOD2    SETC  '&MOD'                                                           
&MOD1    SETC  '&MOD'(1,1)                                                      
         AIF   (K'&MOD NE 1).FM05                                               
&MOD2    SETC  '&MOD '                                                          
         AGO   .FM06                                                            
.FM05    ANOP                                                                   
&MOD1    SETC  '&MOD'(2,1)                                                      
.FM06    ANOP                                                                   
                                                                                
&TIDS    SETC  '&TID'(1,6)                                                      
&TIDM    SETC  '&TIDS&MOD1'                                                     
&C3      SETC  '3'                                                              
&C4      SETC  '4'                                                              
                                                                                
* SEE DSECT DEMFMHDD                                                            
&TIDM    DC    AL4(X&TIDM-&TIDM),CL2'&MOD2'                                     
         DC    AL4(&TIDM&C3-&TIDM)     DISPL TO A(BLD BLKS BY TYPE3 ID)         
         DC    AL4(&TIDM&C4-&TIDM)     DISPL TO A(BLD BLKS BY TYPE4 ID)         
         DC    AL4(F&TIDM-&TIDM)       DISPL TO A(FORMULA TABLE)                
         DC    AL4(0)                  SPARE                                    
         DC    AL4(0)                  SPARE                                    
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* FTYP3 - MACRO TO DEFINE LIST OF BUILDING BLOCKS                     *         
*         IN TYPE3 FORMAT (1 BYTE)                                    *         
*---------------------------------------------------------------------*         
         MACRO                                                                  
         FTYP3 &DEMOS                                                           
                                                                                
         GBLC  &TIDM,&C3                                                        
         LCLA  &ELEMENT,&COUNT                                                  
         LCLC  &LABEL                                                           
                                                                                
         AIF   ('&DEMOS' EQ '').MISSING                                         
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
* SEE DSECT DEMFBL3D                                                            
                                                                                
&COUNT   SETA  N'&DEMOS                                                         
&LABEL   SETC  '&TIDM&C3'                                                       
                                                                                
&LABEL   DC    AL2(&COUNT)         NUMBER OF BUILDING BLOCKS                    
                                                                                
&ELEMENT SETA  0                                                                
.LOOP    ANOP                                                                   
&ELEMENT SETA  &ELEMENT+1                                                       
         DC    AL1(&DEMOS(&ELEMENT).)                                           
         AIF   (&ELEMENT LT &COUNT).LOOP                                        
         DC    X'00'               EOT                                          
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* FTYP4 - MACRO TO DEFINE LIST OF BUILDING BLOCKS                     *         
*         IN TYPE4 FORMAT (2 BYTE)                                    *         
*---------------------------------------------------------------------*         
         MACRO                                                                  
         FTYP4 &DEMOS                                                           
                                                                                
         GBLC  &TIDM,&C4                                                        
         LCLA  &ELEMENT,&COUNT                                                  
         LCLC  &LABEL                                                           
                                                                                
         AIF   ('&DEMOS' EQ '').MISSING                                         
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
* SEE DSECT DEMFBL4D                                                            
                                                                                
&COUNT   SETA  N'&DEMOS                                                         
&LABEL   SETC  '&TIDM&C4'                                                       
                                                                                
&LABEL   DC    AL2(&COUNT)         NUMBER OF BUILDING BLOCKS                    
                                                                                
&ELEMENT SETA  0                                                                
.LOOP    ANOP                                                                   
&ELEMENT SETA  &ELEMENT+1                                                       
         DC    AL2(&DEMOS(&ELEMENT).)                                           
         AIF   (&ELEMENT LT &COUNT).LOOP                                        
         DC    X'FFFF'             EOT                                          
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* DFORM - MACRO TO DEFINE A FORMULA                                   *         
*         FORMULA COMPUTES A NEW DEMO FROM A SUBSET OF BUILDING BLOCKS*         
*                                                                     *         
* EX: DFORM 100,(1,2)                                                 *         
*     TO COMPUTE DEMO #100, ADD THE 1ST AND 2ND BUILDING BLOCKS       *         
*---------------------------------------------------------------------*         
         MACRO                                                                  
         DFORM &NEWD,&DEMOS                                                     
                                                                                
         GBLC  &TIDM                                                            
         GBLA  &STFLAG                                                          
         LCLA  &ELEMENT,&LENGTH                                                 
                                                                                
         AIF   (T'&NEWD EQ 'O').MISSING                                         
         AIF   ('&DEMOS' EQ '').MISSING                                         
         AGO   .OKAY                                                            
                                                                                
.MISSING MNOTE 8,'MISSING REQUIRED PARAMETER'                                   
         MEXIT                                                                  
                                                                                
.OKAY    ANOP                                                                   
         AIF   (&STFLAG NE 0).DF10                                              
* SEE DSECT DEMFDEMD                                                            
F&TIDM   DS    0X                                                               
&STFLAG  SETA  1                                                                
.DF10    ANOP                                                                   
                                                                                
&LENGTH  SETA  2+2+2*N'&DEMOS                                                   
                                                                                
         DC    AL2(&LENGTH)                                                     
         DC    AL2(&NEWD)                                                       
                                                                                
&ELEMENT SETA  0                                                                
.LOOP    ANOP                                                                   
&ELEMENT SETA  &ELEMENT+1                                                       
         DC    AL2(&DEMOS(&ELEMENT).)                                           
         AIF   (&ELEMENT LT N'&DEMOS).LOOP                                      
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* EFTID - MACRO FOR END OF GLOBAL TABLE                                         
*---------------------------------------------------------------------*         
         MACRO                                                                  
         EFTID                                                                  
                                                                                
         DC    AL4(0)                                                           
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* FFEND - MACRO TO CREATE END OF LOCAL TABLE                                    
*---------------------------------------------------------------------*         
         MACRO                                                                  
         FFEND                                                                  
                                                                                
         GBLA  &STFLAG                                                          
                                                                                
&STFLAG  SETA  0                                                                
         DC    X'FF'                                                            
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* EMOD  - END OF TABLES FOR MODIFIER                                            
*---------------------------------------------------------------------*         
         MACRO                                                                  
         EMOD                                                                   
                                                                                
         GBLC  &TIDM                                                            
                                                                                
X&TIDM   DS    0X                                                               
                                                                                
         MEND                                                                   
*---------------------------------------------------------------------*         
* ACT_TABLE MACRO                                                               
*---------------------------------------------------------------------*         
*                                                                               
         MACRO                                                                  
         ACT_TABLE &TABLENUM,&TABLETYPE                                         
.*                                                                              
         GBLA  &TBLNUM,&TBLTYPE                                                 
&TBLNUM  SETA  &TABLENUM                                                        
.*                                                                              
&TBLTYPE SETA  1                                                                
         AIF   ('&TABLETYPE' EQ 'DEFAULT').A10                                  
&TBLTYPE SETA  2                                                                
         AIF   ('&TABLETYPE' EQ 'FORCE').A10                                    
&TBLTYPE SETA  3                                                                
         AIF   ('&TABLETYPE' EQ 'SPECIAL').A10                                  
         MNOTE 8,'INVALID TABLE TYPE'                                           
         MEXIT                                                                  
.*                                                                              
.A10     ANOP                                                                   
         MEXIT                                                                  
         MEND                                                                   
*---------------------------------------------------------------------*         
* SWEEP CLASS MACRO FOR ACT TABLES                                              
*---------------------------------------------------------------------*         
         MACRO                                                                  
         CLASS &SWPCLS,&BOOKS,&OVERRIDE_TABLE_TYPE=                             
         LCLA  &SC,&TTYPE                                                       
         GBLA  &TBLNUM,&TBLTYPE                                                 
         GBLA  &USED                                                            
.*                                                                              
         AIF   ('&SWPCLS' NE 'END').C10                                         
&SPARE   SETA  24-&USED                                                         
         DS    &SPARE.X'00'                                                     
&USED    SETA  0                                                                
         MEXIT                                                                  
.*                                                                              
.C10     ANOP                                                                   
&TTYPE   SETA  &TBLTYPE                                                         
         AIF   (T'&OVERRIDE_TABLE_TYPE EQ 'O').C20                              
&TTYPE   SETA  1                                                                
         AIF   ('&OVERRIDE_TABLE_TYPE' EQ 'DEFAULT').C20                        
&TTYPE   SETA  2                                                                
         AIF   ('&OVERRIDE_TABLE_TYPE' EQ 'FORCE').C20                          
&TTYPE   SETA  3                                                                
         AIF   ('&OVERRIDE_TABLE_TYPE' EQ 'SPECIAL').C20                        
         MNOTE 8,'INVALID OVERRIDE TABLE TYPE'                                  
         MEXIT                                                                  
.*                                                                              
.C20     ANOP                                                                   
         AIF   (N'&SYSLIST NE 2).CBAD                                           
         AIF   (N'&BOOKS NE 12).CBAD                                            
         AGO   .C30                                                             
.CBAD    MNOTE 8,'CLASS MACRO REQUIRES A CLASS FOLLOWED BY 12 BOOKS'            
         MEXIT                                                                  
.*                                                                              
.C30     ANOP                                                                   
.*                                                                              
&SC      SETA  255                                                              
         AIF   ('&SWPCLS' EQ 'DFLT').C40                                        
&SC      SETA  241                                                              
         AIF   ('&SWPCLS' EQ '1').C40                                           
&SC      SETA  242                                                              
         AIF   ('&SWPCLS' EQ '2').C40                                           
&SC      SETA  243                                                              
         AIF   ('&SWPCLS' EQ '3').C40                                           
&SC      SETA  247                                                              
         AIF   ('&SWPCLS' EQ '7').C40                                           
&SC      SETA  248                                                              
         AIF   ('&SWPCLS' EQ '8').C40                                           
&SC      SETA  249                                                              
         AIF   ('&SWPCLS' EQ '9').C40                                           
         MNOTE 8,'INVALID SWEEP CLASS'                                          
         MEXIT                                                                  
.*                                                                              
.C40     ANOP                                                                   
         DC    AL1(&TBLNUM),AL1(&SC),AL1(&TTYPE)                                
.*                                                                              
&LOOPCTR SETA  1                                                                
.*                                                                              
.C50     ANOP                                                                   
.*                                                                              
&FORCE   SETA  0                                                                
&ADJMNTH SETA  0                                                                
         AIF   (K'&BOOKS(&LOOPCTR) NE 1).C60                                    
         AIF   ('&BOOKS(&LOOPCTR)' NE 'W').CBADBK                               
&BOOK    SETA  255                                                              
         AGO   .C80                                                             
.C60     AIF   (K'&BOOKS(&LOOPCTR) EQ 3).C70                                    
         AIF   (K'&BOOKS(&LOOPCTR) NE 4).CBADBK                                 
         AIF   ('&BOOKS(&LOOPCTR)'(4,1) EQ '!').SETFRCE                         
         AIF   ('&BOOKS(&LOOPCTR)'(4,1) NE '-').CBADBK                          
&ADJMNTH SETA  24                                                               
         AGO   .C70                                                             
.SETFRCE ANOP                                                                   
&FORCE   SETA  128                                                              
.*                                                                              
.C70     ANOP                                                                   
&BOOK    SETA  1                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'JAN').C80                           
&BOOK    SETA  2                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'FEB').C80                           
&BOOK    SETA  3                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'MAR').C80                           
&BOOK    SETA  4                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'APR').C80                           
&BOOK    SETA  5                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'MAY').C80                           
&BOOK    SETA  6                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'JUN').C80                           
&BOOK    SETA  7                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'JUL').C80                           
&BOOK    SETA  8                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'AUG').C80                           
&BOOK    SETA  9                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'SEP').C80                           
&BOOK    SETA  10                                                               
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'OCT').C80                           
&BOOK    SETA  11                                                               
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'NOV').C80                           
&BOOK    SETA  12                                                               
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'DEC').C80                           
.CBADBK  MNOTE 8,'INVALID BOOK'                                                 
         MEXIT                                                                  
.*                                                                              
.C80     ANOP                                                                   
&BOOK    SETA  &BOOK+&FORCE+&ADJMNTH                                            
         DC    AL1(&BOOK)                                                       
.*                                                                              
&LOOPCTR SETA  &LOOPCTR+1                                                       
         AIF   ('&LOOPCTR' LE '12').C50                                         
.*                                                                              
         MEXIT                                                                  
         MEND                                                                   
         EJECT                                                                  
*---------------------------------------------------------------------*         
* MACRO TO DEFINE WEEKS IN SWEEP CLASS ENTRY                                    
*---------------------------------------------------------------------*         
         MACRO                                                                  
         WEEKS &MONTH,&BOOKS                                                    
         LCLA  &MNTH                                                            
         GBLA  &USED                                                            
.*                                                                              
         AIF   (N'&SYSLIST NE 2).WBAD                                           
         AIF   (N'&BOOKS NE 5).WBAD                                             
         AGO   .W10                                                             
.WBAD    MNOTE 8,'WEEKS MACRO REQUIRES A MONTH FOLLOWED BY 5 WEEKS'             
         MEXIT                                                                  
.W10     ANOP                                                                   
.*                                                                              
&MNTH    SETA  1                                                                
         AIF   ('&MONTH' EQ 'JAN').W20                                          
&MNTH    SETA  2                                                                
         AIF   ('&MONTH' EQ 'FEB').W20                                          
&MNTH    SETA  3                                                                
         AIF   ('&MONTH' EQ 'MAR').W20                                          
&MNTH    SETA  4                                                                
         AIF   ('&MONTH' EQ 'APR').W20                                          
&MNTH    SETA  5                                                                
         AIF   ('&MONTH' EQ 'MAY').W20                                          
&MNTH    SETA  6                                                                
         AIF   ('&MONTH' EQ 'JUN').W20                                          
&MNTH    SETA  7                                                                
         AIF   ('&MONTH' EQ 'JUL').W20                                          
&MNTH    SETA  8                                                                
         AIF   ('&MONTH' EQ 'AUG').W20                                          
&MNTH    SETA  9                                                                
         AIF   ('&MONTH' EQ 'SEP').W20                                          
&MNTH    SETA  10                                                               
         AIF   ('&MONTH' EQ 'OCT').W20                                          
&MNTH    SETA  11                                                               
         AIF   ('&MONTH' EQ 'NOV').W20                                          
&MNTH    SETA  12                                                               
         AIF   ('&MONTH' EQ 'DEC').W20                                          
         MNOTE 8,'INVALID MONTH'                                                
         MEXIT                                                                  
.*                                                                              
.W20     ANOP                                                                   
         DC    AL1(&MNTH)                                                       
.*                                                                              
&LOOPCTR SETA  1                                                                
.*                                                                              
.W30     ANOP                                                                   
&FORCE   SETA  0                                                                
&ADJMNTH SETA  0                                                                
         AIF   (K'&BOOKS(&LOOPCTR) EQ 3).W40                                    
         AIF   (K'&BOOKS(&LOOPCTR) LT 3).WBADBK                                 
         AIF   (K'&BOOKS(&LOOPCTR) GT 5).WBADBK                                 
         AIF   (K'&BOOKS(&LOOPCTR) EQ 5).CHKFRC5                                
         AIF   ('&BOOKS(&LOOPCTR)'(4,1) NE '!').WBADBK                          
         AGO   .SETFRCE                                                         
.CHKFRC5 AIF   ('&BOOKS(&LOOPCTR)'(5,1) NE '!').WBADBK                          
         AIF   ('&BOOKS(&LOOPCTR)'(4,1) NE '+').WBADBK                          
&ADJMNTH SETA  12                                                               
.SETFRCE ANOP                                                                   
&FORCE   SETA  128                                                              
.*                                                                              
.W40     ANOP                                                                   
&BOOK    SETA  1                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'JAN').W50                           
&BOOK    SETA  2                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'FEB').W50                           
&BOOK    SETA  3                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'MAR').W50                           
&BOOK    SETA  4                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'APR').W50                           
&BOOK    SETA  5                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'MAY').W50                           
&BOOK    SETA  6                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'JUN').W50                           
&BOOK    SETA  7                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'JUL').W50                           
&BOOK    SETA  8                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'AUG').W50                           
&BOOK    SETA  9                                                                
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'SEP').W50                           
&BOOK    SETA  10                                                               
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'OCT').W50                           
&BOOK    SETA  11                                                               
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'NOV').W50                           
&BOOK    SETA  12                                                               
         AIF   ('&BOOKS(&LOOPCTR)'(1,3) EQ 'DEC').W50                           
.WBADBK  MNOTE 8,'INVALID BOOK'                                                 
         MEXIT                                                                  
.*                                                                              
.W50     ANOP                                                                   
&BOOK    SETA  &BOOK+&FORCE+&ADJMNTH                                            
         DC    AL1(&BOOK)                                                       
.*                                                                              
&LOOPCTR SETA  &LOOPCTR+1                                                       
         AIF   ('&LOOPCTR' LE '5').W30                                          
.*                                                                              
&USED    SETA  &USED+6                                                          
         MEXIT                                                                  
         MEND                                                                   
