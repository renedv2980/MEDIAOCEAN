*          DATA SET MCMOVE     AT LEVEL 005 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     MOVE  &A,&B                                                            
         LCLC  &C                                                               
         LCLA  &K                                                               
         LCLC  &MOVEA,&MOVEB,&MOVEX                                             
&MOVEA   SETC  'MOVA&SYSNDX'                                                    
&MOVEB   SETC  'MOVB&SYSNDX'                                                    
&MOVEX   SETC  'MOVX&SYSNDX'                                                    
.*                                                                              
         AIF   (N'&A EQ 2).ONE                                                  
         MNOTE 8,'SHOULD BE TWO TERMS ON FIRST OPERAND'                         
         MEXIT                                                                  
.*                                                                              
.ONE     AIF   (N'&B EQ 1).TWO                                                  
         MNOTE 8,'SHOULD BE ONE TERM ON SECOND OPERAND'                         
         MEXIT                                                                  
.*                                                                              
.TWO     ANOP                                                                   
         AIF   ('&B' EQ '(RE)').THREE                                           
         AIF   ('&B' EQ '(14)').THREE                                           
         AIF   ('&B'(1,1) NE '(').TWOA                                          
&K       SETA  K'&B-2                                                           
&C       SETC  '&B'(2,&K)                                                       
&TAG     LR    RE,&C                                                            
         AGO   .FOUR                                                            
.*                                                                              
.TWOA    ANOP                                                                   
&TAG     LA    RE,&B                                                            
         AGO   .FOUR                                                            
.*                                                                              
.THREE   ANOP                                                                   
&TAG     DS    0H                                                               
.*                                                                              
.FOUR    AIF   ('&A(1)' EQ '(RF)').FIVE                                         
         AIF   ('&A(1)' EQ '(15)').FIVE                                         
         AIF   ('&A(1)'(1,1) NE '(').FOURA                                      
&K       SETA  K'&A(1)-2                                                        
&C       SETC  '&A(1)'(2,&K)                                                    
         LR    RF,&C                                                            
         AGO   .FIVE                                                            
.*                                                                              
.FOURA   ANOP                                                                   
         LA    RF,&A(1)                                                         
.*                                                                              
.FIVE    AIF   ('&A(2)'(1,1) NE '(').SIX                                        
         AIF   ('&A(2)'(2,1) EQ '1').NINE                                       
         AIF   ('&A(2)'(2,2) EQ 'R1').NINE                                      
&K       SETA  K'&A(2)-2                                                        
&C       SETC  '&A(2)'(2,&K)                                                    
         LR    R1,&C                                                            
         AGO   .NINE                                                            
.SIX     AIF   (T'&A(2) EQ 'N').SEVEN                                           
         MNOTE 8,'INVALID LENGTH'                                               
         MEXIT                                                                  
.*                                                                              
.SEVEN   AIF   (&A(2) GT 4095).EIGHT                                            
         LA    R1,&A(2)                                                         
         AGO   .NINE                                                            
.*                                                                              
.EIGHT   ANOP                                                                   
         CNOP  0,4                                                              
         L     R1,*+8                                                           
         J     *+8                                                              
         DC    F'&A(2)'                                                         
.*                                                                              
.NINE    ANOP                                                                   
&MOVEA   DS    0H                                                               
         CHI   R1,250                                                           
         JL    &MOVEB                                                           
         MVC   0(250,RF),0(RE)                                                  
         LA    RE,250(RE)                                                       
         LA    RF,250(RF)                                                       
         SHI   R1,250                                                           
         J     &MOVEA                                                           
.*                                                                              
&MOVEB   DS    0H                                                               
         LTR   R1,R1                                                            
         JZ    &MOVEX                                                           
         BCTR  R1,0                                                             
         EXRL  R1,*+10                                                          
         J     *+10                                                             
         MVC   0(0,RF),0(RE)                                                    
.*                                                                              
&MOVEX   DS    0H                                                               
         MEXIT                                                                  
         MEND                                                                   
