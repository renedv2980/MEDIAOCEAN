*          DATA SET MCFASEN    AT LEVEL 007 AS OF 03/21/12                      
         MACRO                                                                  
&LABEL   FASEN &SE_NAME,&SYS_NUMBER,&SYSOV_NUMBER,                     +        
               &AUTOSTARTED=NO,         (SEISTRT)                      +        
               &RESTRICTED=NO,          (SEIRESA)                      +        
               &OPERATIONAL=YES,        (SEINOP)                       +        
               &PROGRAMLISTADDRESS=,    (SEPGMS)                       +        
               &MAXTASKS=,              (SETASKMX)                     +        
               &SYSFACSLISTNUMBER=,     (SEFACSET)                     +        
               &SYSFILESETNUMBER=,      (SEFILSET)                     +        
               &SYSFILES=,              (SEFILES)                      +        
               &UPDATIVEFACPAKID=ADV0,  (SEFACUP)                      +        
               &READONLYFACPAKID=ADV0,  (SEFACRO)                      +        
               &TRAILINGRCV=NO,         (SEFACFL1)                     +        
               &RCVDTF=                 (SERCVDTF)                              
.*--------------------------------------------------------------------          
         LCLA  &FLAGS              **USED FOR BIT INDICATORS**                  
&FLAGS   SETA  0                                                                
.*--------------------------------------------------------------------          
         AIF   ((T'&SE_NAME NE 'O') AND (T'&SYS_NUMBER NE 'O') AND     +        
               (T'&SYSOV_NUMBER NE 'O')).J2                                     
         MNOTE 4,'ERROR: REQUIRED PARAMETER OMITTED'                            
.J2      ANOP                                                                   
.*--------------------------------------------------------------------          
         AIF   (K'&SE_NAME LE 7).J4                                             
         MNOTE 4,X'WARNING: MAXIMUM CHARACTERS IN PROGRAM NAME IS 7'            
.J4      ANOP                                                                   
.*--------------------------------------------------------------------          
&LABEL   DC    CL7'&SE_NAME'       **SENAME**                                   
.*--------------------------------------------------------------------          
         AIF   (('&SYS_NUMBER'(1,1) EQ 'X') AND                        +        
               (K'&SYS_NUMBER EQ 5)).J6                                         
         MNOTE 4,'ERROR: PROGRAM NUMBER MUST BE A TWO DIGIT HEX NUMBER +        
               (X''**'')'                                                       
.J6      ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    &SYS_NUMBER         **SESYS**                                    
.*--------------------------------------------------------------------          
         AIF   (('&SYSOV_NUMBER'(1,1) EQ 'X') AND                      +        
               (K'&SYSOV_NUMBER EQ 5)).J8                                       
         MNOTE 4,'ERROR: PROGRAM NUMBER MUST BE A TWO DIGIT HEX NUMBER +        
               (X''**'')'                                                       
.J8      ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    &SYSOV_NUMBER       **SEOVSYS**                                  
.*--------------------------------------------------------------------          
         AIF   ('&AUTOSTARTED'(1,1) EQ 'N').SK1                                 
         AIF   ('&AUTOSTARTED'(1,1) EQ 'Y').SK1A                                
         MNOTE 4,'ERROR: AUTOSTARTED PARAMETER NOT YES OR NO'                   
.SK1A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'80'                                                     
.SK1     ANOP                                                                   
.*--------------------------------------------------------------------          
         AIF   ('&RESTRICTED'(1,1) EQ 'N').SK2                                  
         AIF   ('&RESTRICTED'(1,1) EQ 'Y').SK2A                                 
         MNOTE 4,'ERROR: RESTRICTED PARAMETER NOT YES OR NO'                    
.SK2A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'02'                                                     
.SK2     ANOP                                                                   
.*--------------------------------------------------------------------          
         AIF   ('&OPERATIONAL'(1,1) EQ 'Y').SK3                                 
         AIF   ('&OPERATIONAL'(1,1) EQ 'N').SK3A                                
         MNOTE 4,'ERROR: OPERATIONAL PARAMETER NOT YES OR NO'                   
.SK3A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'01'                                                     
.SK3     ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    AL1(&FLAGS)         **SEIND**                                    
.*--------------------------------------------------------------------          
         DC    14X'0'              **SEQLEN,SEFIRST,SELAST,SESIN**              
.*--------------------------------------------------------------------          
         AIF   (T'&PROGRAMLISTADDRESS NE 'O').J10                               
         MNOTE 4,'ERROR: PROGRAMLISTADDRESS PARAMETER NOT ENTERED'              
.J10     ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    A(&PROGRAMLISTADDRESS)     **SEPGMS**                            
.*--------------------------------------------------------------------          
         AIF   (T'&MAXTASKS NE 'O').J12                                         
         MNOTE 4,'ERROR: MAXTASKS PARAMETER NOT ENTERED'                        
.J12     ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    AL1(&MAXTASKS)      **SETASKMX**                                 
.*--------------------------------------------------------------------          
         DC    AL1(0)              **SETASK**                                   
.*--------------------------------------------------------------------          
         AIF   (T'&SYSFACSLISTNUMBER NE 'O').J14                                
         MNOTE 4,'ERROR: SYSFACSLISTNUMBER PARAMETER NOT ENTERED'               
.J14     ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    AL1(&SYSFACSLISTNUMBER)  **SEFACSET**                            
.*--------------------------------------------------------------------          
         AIF   (T'&SYSFILESETNUMBER NE 'O').J16                                 
         MNOTE 4,'ERROR: SYSFILESETNUMBER PARAMETER NOT ENTERED'                
.J16     ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    AL1(&SYSFILESETNUMBER) **SEFILESET**                             
.*--------------------------------------------------------------------          
         AIF   (T'&SYSFILES NE 'O').J18                                         
         MNOTE 4,'ERROR: SYSFILES PARAMETER NOT ENTERED'                        
.J18     ANOP                                                                   
.*--------------------------------------------------------------------          
         AIF   ('&SYSFILES'(1,1) NE '0').J20    **SEFILES**                     
         DC    A(&SYSFILES)        **SEFILES**                                  
         DC    A(&SYSFILES)        **SEFILEX**                                  
         AGO   .J22                                                             
.*--------------------------------------------------------------------          
.J20     AIF   (K'&SYSFILES EQ 8).SK4                                           
         MNOTE 4,'ERROR: SYSFILES ENTRY NOT 8 CHARACTERS LONG'                  
.SK4     ANOP                                                                   
         AIF   ('&SYSFILES'(1,3) EQ 'SYS').SK4A                                 
         MNOTE 4,'ERROR: SYSFILES ENTRY DOES NOT BEGIN WITH ''SYS'''            
.SK4A    ANOP                                                                   
         AIF   ('&SYSFILES'(8,1) EQ 'S').SK4B                                   
         MNOTE 4,'ERROR: SYSFILES ENTRY DOES END WITH ''S'''                    
.SK4B    ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    V(&SYSFILES)        **SEFILES**                                  
.*--------------------------------------------------------------------          
&ENDFILE SETC  '&SYSFILES'(1,7).'X'                                             
         DC    V(&ENDFILE)         **SEFILEX**                                  
.*--------------------------------------------------------------------          
.J22     DC    F'0'                **SERCVSEQ**                                 
         DC    48F'0'              **SERCVSEQ**                                 
.*--------------------------------------------------------------------          
         DC    XL8'00'             **SESSSTRT,SESSSTOP**                        
.*--------------------------------------------------------------------          
         DC    CL8' '              **SESSLUID**                                 
.*--------------------------------------------------------------------          
         DC    CL8' '              **SESSPSWD**                                 
.*--------------------------------------------------------------------          
         DC    CL8' '              **SESSPID**                                  
.*--------------------------------------------------------------------          
         DC    CL16' '             **SESSWHY**                                  
.*--------------------------------------------------------------------          
         DC    AL4(0)              **SESTABUF**                                 
.*--------------------------------------------------------------------          
         DC    AL1(&UPDATIVEFACPAKID) **SEFACUP**                               
.*--------------------------------------------------------------------          
         DC    AL1(&READONLYFACPAKID) **SEFACRO**                               
.*--------------------------------------------------------------------          
&FLAGS   SETA  0                                                                
         AIF   ('&TRAILINGRCV'(1,1) EQ 'Y').DF1                                 
         AIF   ('&TRAILINGRCV'(1,1) EQ 'N').DF1A                                
         MNOTE 4,'ERROR: TRAILINGRCV PARAMETER NOT YES OR NO'                   
.DF1     ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'80'                                                     
.DF1A    ANOP                                                                   
.*--------------------------------------------------------------------          
         DC    AL1(&FLAGS)         *SEFACFL*                                    
         DC    X'00'                                                            
.*--------------------------------------------------------------------          
.RCV     ANOP  ,                                                                
         AIF   (T'&RCVDTF EQ 'O').RCV3                                          
         AIF   (K'&RCVDTF GT 8).RCV2                                            
         DC    V(&RCVDTF)          *SERCVDTF*                                   
         AGO   .XTRA                                                            
.RCV2    ANOP  ,                                                                
         MNOTE 4,'ERROR: RCVDTF LABEL IS TOO LONG'                              
.RCV3    ANOP  ,                                                                
         DC    A(0)                *SERCVDTF*                                   
.XTRA    ANOP                                                                   
         DC    6A(0)               *SPARE*                                      
.*--------------------------------------------------------------------          
         MEND                                                                   
