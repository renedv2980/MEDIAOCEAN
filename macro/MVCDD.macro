*          DATA SET MCMVCDD    AT LEVEL 003 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     MVCDD &FLD,&DDNO,&ALIGN                                                
.*                                                                              
         LCLA  &I,&J,&K,&L,&TYP                                                 
         LCLA  &LPREN,&RPREN,&COMMA                                             
         LCLA  &ESC                                                             
         LCLA  &ESC2,&ESC3,&ESCL,&ESCC,&ESCR,&ESCS,&ESCF                        
         LCLA  &ESU2,&ESU3,&ESUL,&ESUC,&ESUR,&ESUS                              
         LCLC  &SRC,&SRC1,&SRC2,&LEN,&HDG                                       
.*                                                                              
&HDG     SETC  '0'                                                              
.*                                                                              
&ESC2    SETA  32                                                               
&ESC3    SETA  33                                                               
&ESCL    SETA  34                                                               
&ESCC    SETA  35                                                               
&ESCR    SETA  36                                                               
&ESCS    SETA  37                                                               
&ESCF    SETA  39                                                               
.*                                                                              
&ESU2    SETA  40                                                               
&ESU3    SETA  41                                                               
&ESUL    SETA  42                                                               
&ESUC    SETA  43                                                               
&ESUR    SETA  44                                                               
&ESUS    SETA  45                                                               
.*                                                                              
.*********************************************************************          
.FLDVAL  ANOP                                                                   
         AIF   (T'&FLD EQ 'O').FLDERR1                                          
&L       SETA  K'&FLD                                                           
&LPREN   SETA  0                                                                
&RPREN   SETA  0                                                                
&COMMA   SETA  0                                                                
&I       SETA  1                                                                
.*                                                                              
.FLD1    ANOP                                                                   
         AIF   ('&FLD'(&I,1) EQ '(').FLD6                                       
         AIF   ('&FLD'(&I,1) EQ ')').FLD7                                       
         AIF   ('&FLD'(&I,1) EQ ',').FLD8                                       
.FLD2    ANOP                                                                   
&I       SETA  &I+1                                                             
         AIF   (&I GT &L).FLDA                                                  
         AGO   .FLD1                                                            
.FLD6    ANOP                                                                   
         AIF   (&LPREN NE 0).FLD2                                               
&LPREN   SETA  &I                                                               
         AGO   .FLD2                                                            
.FLD7    ANOP                                                                   
&RPREN   SETA  &I                                                               
         AGO   .FLD2                                                            
.FLD8    ANOP                                                                   
&COMMA   SETA  &I                                                               
         AGO   .FLD2                                                            
.*                                                                              
.FLDA    ANOP                                                                   
         AIF   (&LPREN LT 2).FLDD                                               
         AIF   (&LPREN GE &RPREN).FLDERR2                                       
         AIF   (&COMMA EQ 0).FLDB                                               
         AIF   (&COMMA GT &LPREN AND &COMMA LT &RPREN).FLDC                     
         AGO   .FLDERR2                                                         
.*                                                                              
.FLDB    ANOP                                                                   
&TYP     SETA  1                                                                
&SRC     SETC  '&FLD'(1,&LPREN-1)                                               
&LEN     SETC  '&FLD'(&LPREN+1,&RPREN-&LPREN-1)                                 
         AGO   .FLDX                                                            
.*                                                                              
.FLDC    ANOP                                                                   
&TYP     SETA  2                                                                
&SRC1    SETC  '&FLD'(1,&LPREN-1)                                               
&SRC2    SETC  '&FLD'(&COMMA+1,&RPREN-&COMMA-1)                                 
&LEN     SETC  '&FLD'(&LPREN+1,&COMMA-&LPREN-1)                                 
         AGO   .FLDX                                                            
.*                                                                              
.FLDD    ANOP                                                                   
&TYP     SETA  1                                                                
&SRC     SETC  '&FLD'                                                           
&LEN     SETC  'L'''.'&FLD'                                                     
         AGO   .FLDX                                                            
.*                                                                              
.FLDERR1 MNOTE 8,'MISSING SOURCE FIELD'                                         
.FLDERR2 MNOTE 8,'INVALID SOURCE FIELD'                                         
.FLDERR3 MNOTE 8,'INVALID SOURCE FIELD LENGTH'                                  
.FLDX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.DDNVAL  ANOP                                                                   
         AIF   (T'&DDNO EQ 'O').DDNERR                                          
         AGO   .DDNX                                                            
.DDNERR  MNOTE 8,'MISSING DATA DICTIONARY NUMBER'                               
.DDNX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.ESCVAL  ANOP                                                                   
         AIF   ('&LEN' EQ '1').FLDERR3                                          
         AIF   ('&LEN' EQ '2').ESCL2                                            
         AIF   ('&LEN' EQ '3').ESCL3                                            
.*                                                                              
         AIF   (T'&ALIGN EQ 'O').ESCL                                           
.*                                                                              
         AIF   ('&ALIGN' EQ 'L2U').ESUL2                                        
         AIF   ('&ALIGN' EQ 'L3U').ESUL3                                        
         AIF   ('&ALIGN'(1,2) EQ 'LU').ESUL                                     
         AIF   ('&ALIGN'(1,2) EQ 'CU').ESUC                                     
         AIF   ('&ALIGN'(1,2) EQ 'RU').ESUR                                     
         AIF   ('&ALIGN'(1,2) EQ 'SU').ESUS                                     
.*                                                                              
         AIF   ('&ALIGN' EQ 'L2').ESCL2                                         
         AIF   ('&ALIGN' EQ 'L3').ESCL3                                         
         AIF   ('&ALIGN'(1,1) EQ 'L').ESCL                                      
         AIF   ('&ALIGN'(1,1) EQ 'H').ESCH                                      
         AIF   ('&ALIGN'(1,1) EQ 'C').ESCC                                      
         AIF   ('&ALIGN'(1,1) EQ 'R').ESCR                                      
         AIF   ('&ALIGN'(1,1) EQ 'S').ESCS                                      
         AIF   ('&ALIGN'(1,1) EQ 'F').ESCF                                      
         AGO   .ESCERR                                                          
.*                                                                              
.ESCL    ANOP                                                                   
&ESC     SETA  &ESCL                                                            
         AGO   .ESCX                                                            
.ESCL2   ANOP                                                                   
         AIF   ('&ALIGN' EQ 'L2U').ESUL2                                        
         AIF   ('&ALIGN' EQ 'LU').ESUL2                                         
&ESC     SETA  &ESC2                                                            
         AGO   .ESCX                                                            
.ESCL3   ANOP                                                                   
         AIF   ('&ALIGN' EQ 'L3U').ESUL3                                        
         AIF   ('&ALIGN' EQ 'LU').ESUL3                                         
&ESC     SETA  &ESC3                                                            
         AGO   .ESCX                                                            
.*                                                                              
.ESCH    ANOP                                                                   
&ESC     SETA  &ESCL                                                            
&HDG     SETC  '128'                                                            
         AGO   .ESCX                                                            
.ESCC    ANOP                                                                   
&ESC     SETA  &ESCC                                                            
         AGO   .ESCX                                                            
.ESCR    ANOP                                                                   
&ESC     SETA  &ESCR                                                            
         AGO   .ESCX                                                            
.ESCS    ANOP                                                                   
&ESC     SETA  &ESCS                                                            
         AGO   .ESCX                                                            
.ESCF    ANOP                                                                   
&ESC     SETA  &ESCF                                                            
         AGO   .ESCX                                                            
.*                                                                              
.ESUL    ANOP                                                                   
&ESC     SETA  &ESUL                                                            
         AGO   .ESCX                                                            
.ESUL2   ANOP                                                                   
&ESC     SETA  &ESU2                                                            
         AGO   .ESCX                                                            
.ESUL3   ANOP                                                                   
&ESC     SETA  &ESU3                                                            
         AGO   .ESCX                                                            
.ESUC    ANOP                                                                   
&ESC     SETA  &ESUC                                                            
         AGO   .ESCX                                                            
.ESUR    ANOP                                                                   
&ESC     SETA  &ESUR                                                            
         AGO   .ESCX                                                            
.ESUS    ANOP                                                                   
&ESC     SETA  &ESUS                                                            
         AGO   .ESCX                                                            
.ESCERR  MNOTE 8,'ALIGN SHOULD BE L,C,R,S,F,H,L2,L3 OR LU,CU,RU,SU'             
.ESCX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.GEN     ANOP                                                                   
         AIF   (&TYP EQ 1).GEN1                                                 
         AIF   (&TYP EQ 2).GEN2                                                 
         AGO   .XIT                                                             
.*                                                                              
.GEN1    ANOP                                                                   
         AIF   (&ESC EQ &ESC2).GEN1A                                            
         AIF   (&ESC EQ &ESU2).GEN1A                                            
         AIF   (&ESC EQ &ESC3).GEN1A                                            
         AIF   (&ESC EQ &ESU3).GEN1A                                            
&TAG     MVC   &SRC.(4),=AL1(&ESC,&DDNO/256,&DDNO,&LEN+&HDG)                    
         AGO   .XIT                                                             
.GEN1A   ANOP                                                                   
&TAG     MVC   &SRC.(3),=AL1(&ESC,&DDNO/256,&DDNO)                              
         AGO   .XIT                                                             
.*                                                                              
.GEN2    ANOP                                                                   
         AIF   (&ESC EQ &ESC2).GEN2A                                            
         AIF   (&ESC EQ &ESU2).GEN2A                                            
         AIF   (&ESC EQ &ESC3).GEN2A                                            
         AIF   (&ESC EQ &ESU3).GEN2A                                            
&TAG     MVC   &SRC1.(4,&SRC2),=AL1(&ESC,&DDNO/256,&DDNO,&LEN+&HDG)             
         AGO   .XIT                                                             
.GEN2A   ANOP                                                                   
&TAG     MVC   &SRC1.(3,&SRC2),=AL1(&ESC,&DDNO/256,&DDNO)                       
         AGO   .XIT                                                             
.*                                                                              
.XIT     MEXIT                                                                  
         MEND                                                                   
