*          DATA SET MCEDITS    AT LEVEL 003 AS OF 11/12/92                      
         MACRO                                                                  
&TAG     EDIT  &INPUT,&OUTPUT,&DECS,&MINUS=,&CR=,&COMMAS=,&ALIGN=,     X        
               &TRAIL=,&ZERO=,&DROP=,                                  X        
               &WRK=WORK,&DUB=DUB,&FILL=,&FLOAT=,&BRACKET=                      
.*                                                                              
.*                                 GET INPUT INTO R0                            
         LCLA  &OUTDSP,&KA,&KB,&DR,&BLDSP                                       
         LCLA  &INX,&INL                                                        
         LCLC  &INA,&INB                                                        
         LCLA  &POINT                                                           
         LCLC  &CA,&CB,&DRC,&NODSP                                              
         LCLC  &SUBIN                                                           
         AIF   (T'&INPUT NE 'O').INPT2                                          
         MNOTE 8,'MISSING INPUT EXPRESSION'                                     
         AGO   .OUT2                                                            
.*                                                                              
.INPT2   AIF   (N'&INPUT EQ 2).SUBINPT                                          
         AIF   (K'&INPUT GT 8).INPT3                                            
         AIF   (N'&INPUT EQ 1).INPT4                                            
         MNOTE 0,'TOO MANY INPUT TERMS'                                         
         AGO   .OUT2                                                            
.INPT3   ANOP                                                                   
&TAG     DS    0H                                                               
         MNOTE 8,'INPUT  EXPRESSION EXCEEDS 8 CHARACTERS'                       
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 HANDLE TODAY FEATURE HERE                    
.*                                                                              
.INPT4   AIF   ('&INPUT' NE 'TODAY').INPT5                                      
         L     R1,20               COMRG EXPANSION                              
         SVC   33                                                               
         MVC   &WRK.(9),=CL9' '                                                 
         MVC   &WRK+9(8),0(R1)                                                  
         TM    53(R1),X'80'                                                     
         BNO   *+22                                                             
         XC    &WRK+09(2),&WRK+12                                               
         XC    &WRK+12(2),&WRK+09                                               
         XC    &WRK+09(2),&WRK+12                                               
         PACK  &DUB,&WRK+09(2)                                                  
         CVB   R0,&DUB                                                          
         BCTR  R0,0                                                             
         MH    R0,=H'3'                                                         
         LA    R1,=C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                      
         AR    R1,R0                                                            
         MVC   &WRK+9(3),0(R1)                                                  
         AGO   .EDEND                                                           
.*                                                                              
.*                                 IS INPUT ALREADY IN REGISTER                 
.INPT5   AIF   ('&INPUT'(1,1) NE '(').INPT8                                     
         AIF   ('&INPUT(1)' EQ '0').INPT6                                       
         AIF   ('&INPUT(1)' EQ 'R0').INPT6                                      
&TAG     LR    R0,&INPUT(1)                                                     
         AGO   .INPT20                                                          
.*                                 ITS IN R0 - DONT GENERATE INSTRUCT.          
.INPT6   ANOP                                                                   
&TAG     DS    0H                                                               
         AGO   .INPT20                                                          
.*                                 INPUT IS LENGTH-DEFINED PACKED               
.INPT8   AIF   (T'&INPUT NE 'P').INPT10                                         
&TAG     ZAP   &DUB,&INPUT                                                      
         AGO   .OUT2                                                            
.*                                                                              
.INPT10  AIF   (L'&INPUT EQ 1).INPT12                                           
         AIF   (L'&INPUT EQ 2).INPT14                                           
         AIF   (L'&INPUT EQ 3).INPT16                                           
         AIF   (L'&INPUT EQ 4).INPT18                                           
         MNOTE 0,'INPUT LENGTH INCORRECT'                                       
         AGO   .OUT2                                                            
.*                                                                              
.*                                 1 BYTE INPUT                                 
.INPT12  ANOP                                                                   
&TAG     SR    R0,R0                                                            
         IC    R0,&INPUT                                                        
         AGO   .INPT20                                                          
.*                                                                              
.*                                 2 BYTE INPUT                                 
.INPT14  ANOP                                                                   
&TAG     MVC   &DUB.(2),&INPUT                                                  
         LH    R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                 3 BYTE INPUT                                 
.INPT16  ANOP                                                                   
&TAG     MVC   &DUB+1(3),&INPUT                                                 
         MVI   &DUB,0                                                           
         TM    &DUB+1,X'80'                                                     
         BZ    *+8                                                              
         MVI   &DUB,X'FF'                                                       
         L     R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 4 BYTE INPUT                                 
.INPT18  ANOP                                                                   
&TAG     MVC   &DUB.(4),&INPUT                                                  
         L     R0,&DUB                                                          
.*                                 NOW CVD INTO DUB                             
.INPT20  CVD   R0,&DUB                                                          
         AGO   .OUT2                                                            
.*                                                                              
.*                                 SUB-LIST INPUT                               
.SUBINPT ANOP                                                                   
         AIF   ('&INPUT(1)' EQ 'TIME').TIME                                     
         AIF   ('&INPUT(1)' EQ 'TUS').TUS                                       
&SUBIN   SETC  '&INPUT(2)'                                                      
         AIF   (K'&INPUT(2) GT 8).INPT3                                         
         AIF   ('&INPUT(2)'(1,1) NE '(').SUB                                    
&SUBIN   SETC  '0&INPUT(2)'                                                     
.SUB     AIF   ('&INPUT(1)' EQ '1').SUB1                                        
         AIF   ('&INPUT(1)' EQ '2').SUB2                                        
         AIF   ('&INPUT(1)' EQ '3').SUB3                                        
         AIF   ('&INPUT(1)' EQ '4').SUB4                                        
         AIF   ('&INPUT(1)' EQ 'B1').SUB1                                       
         AIF   ('&INPUT(1)' EQ 'B2').SUB2                                       
         AIF   ('&INPUT(1)' EQ 'B3').SUB3                                       
         AIF   ('&INPUT(1)' EQ 'B4').SUB4                                       
         AIF   ('&INPUT(1)'(1,1) EQ 'P').SUB6                                   
         AIF   ('&INPUT(1)'(1,1) EQ 'C').SUB6                                   
         AIF   ('&INPUT(1)' EQ 'X').SUB10                                       
         MNOTE 0,'SUB-LIST INPUT LENGTH NOT 1-4'                                
         AGO   .OUT2                                                            
.*                                                                              
.*                                 1 BYTE INPUT (SUBLIST)                       
.SUB1    ANOP                                                                   
&TAG     SR    R0,R0                                                            
         IC    R0,&SUBIN                                                        
         AGO   .INPT20                                                          
.*                                                                              
.*                                 2 BYTE INPUT (SUB-LIST)                      
.SUB2    ANOP                                                                   
&TAG     MVC   &DUB.(2),&SUBIN                                                  
         LH    R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 3 BYTE INPUT (SUB-LIST)                      
.SUB3    ANOP                                                                   
&TAG     MVC   &DUB+1(3),&SUBIN                                                 
         MVI   &DUB,0                                                           
         TM    &DUB+1,X'80'                                                     
         BZ    *+8                                                              
         MVI   &DUB,X'FF'                                                       
         L     R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 4 BYTE INPUT (SUB-LIST)                      
.SUB4    ANOP                                                                   
&TAG     MVC   &DUB.(4),&SUBIN                                                  
         L     R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 HANDLING NON-BINARY INPUT (SUB-LIST)         
.*                                                                              
.SUB6    ANOP                                                                   
&INX     SETA  K'&INPUT(1)-1                                                    
&INA     SETC  '&INPUT(1)'(2,&INX)                                              
&INL     SETA  &INA                                                             
.*                                 SET UP FOR REGISTER EXPRESSIONS              
&INX     SETA  K'&INPUT(2)                                                      
         AIF   (K'&INPUT(2) EQ 1).SUB6A                                         
&INA     SETC  '0'.'&INPUT(2)'(1,1)                                             
&INB     SETC  '&INPUT(2)'(2,&INX-1)                                            
         AIF   ('&INPUT(2)'(1,1) EQ '(').SUB8                                   
         AIF   (K'&INPUT(2) EQ 2).SUB6A                                         
.*                                                                              
&INA     SETC  '&INPUT(2)'(1,2)                                                 
&INB     SETC  '&INPUT(2)'(3,&INX-2)                                            
         AIF   ('&INPUT(2)'(2,1) EQ '(').SUB8                                   
         AIF   (K'&INPUT(2) EQ 3).SUB6A                                         
.*                                                                              
&INA     SETC  '&INPUT(2)'(1,3)                                                 
&INB     SETC  '&INPUT(2)'(4,&INX-3)                                            
         AIF   ('&INPUT(2)'(3,1) EQ '(').SUB8                                   
         AIF   (K'&INPUT(2) EQ 4).SUB6A                                         
.*                                                                              
&INA     SETC  '&INPUT(2)'(1,4)                                                 
&INB     SETC  '&INPUT(2)'(5,&INX-4)                                            
         AIF   ('&INPUT(2)'(4,1) EQ '(').SUB8                                   
         AIF   (K'&INPUT(2) EQ 5).SUB6A                                         
.*                                                                              
&INA     SETC  '&INPUT(2)'(1,5)                                                 
&INB     SETC  '&INPUT(2)'(6,&INX-5)                                            
         AIF   ('&INPUT(2)'(5,1) EQ '(').SUB8                                   
.*                                                                              
.*                                 GENERATE INPUT INSTRUCTIONS                  
.*                                                                              
.SUB6A   ANOP                                                                   
         AIF   ('&INPUT(1)'(1,1) EQ 'P').SUB7                                   
&TAG     PACK  &DUB,&INPUT(2).(&INL)                                            
         AGO   .OUT2                                                            
.*                                                                              
.SUB7    ANOP                                                                   
&TAG     ZAP   &DUB,&INPUT(2).(&INL)                                            
         AGO   .OUT2                                                            
.*                                                                              
.SUB8    AIF   ('&INPUT(1)'(1,1) EQ 'P').SUB9                                   
&TAG     PACK  &DUB,&INA&INL,&INB                                               
         AGO   .OUT2                                                            
.*                                                                              
.SUB9    ANOP                                                                   
&TAG     ZAP   &DUB,&INA&INL,&INB                                               
         AGO   .OUT2                                                            
.SUB10   ANOP                                                                   
&TAG     CVDX  &DUB,&INPUT(2)                                                   
         AGO   .OUT2                                                            
.*                                                                              
.*                                 HANDLE TIME EXPRESSIONS HERE                 
.*                                                                              
.TIME    ANOP                                                                   
         AIF   ('&INPUT(2)' EQ 'NOW').TIME2                                     
&TAG     MVC   &DUB.(4),&INPUT(2)                                               
         AGO   .TIME4                                                           
.*                                                                              
.TIME2   ANOP                                                                   
&TAG     THMS                                                                   
         ST    R1,&DUB                                                          
.*                                                                              
.TIME4   ANOP                                                                   
         MVC   &WRK.(9),=CL9' '                                                 
         UNPK  &WRK+11(6),&DUB.(4)                                              
         OI    &WRK+16,X'F0'                                                    
         MVC   &WRK+9(2),&WRK+11                                                
         MVC   &WRK+12(2),&WRK+13                                               
         MVI   &WRK+11,C'.'                                                     
         MVI   &WRK+14,C'.'                                                     
         AGO   .EDEND                                                           
.*                                                                              
.*                                 TUS EDITING                                  
.*                                                                              
.TUS     ANOP                                                                   
         AIF   ('&INPUT(2)'(1,1) EQ '(').TUS2                                   
&TAG     MVC   &DUB,&INPUT(2)                                                   
         L     R1,&DUB                                                          
         AGO   .TUS4                                                            
.TUS2    ANOP                                                                   
&TAG     LR    R1,&INPUT(2)                                                     
.TUS4    ANOP                                                                   
         SR    R0,R0                                                            
         D     R0,=F'300'                                                       
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         CVD   R0,&DUB                                                          
         MVC   &WRK.(17),=CL17'         HH.MM.SS'                               
         UNPK  &WRK+15(2),&DUB                                                  
         OI    &WRK+16,X'F0'                                                    
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         CVD   R0,&DUB                                                          
         UNPK  &WRK+12(2),&DUB                                                  
         OI    &WRK+13,X'F0'                                                    
         CVD   R1,&DUB                                                          
         UNPK  &WRK+9(2),&DUB                                                   
         OI    &WRK+10,X'F0'                                                    
         AGO   .EDEND                                                           
.*                                                                              
.*                                 HANDLING OUTPUT OPTIONS (NO DECS)            
.OUT2    AIF   (K'&OUTPUT(2) LE 8).OUT3                                         
         MNOTE 8,'OUTPUT EXPRESSION EXCEEDS 8 CHARACTERS'                       
         SPACE 1                                                                
         MEXIT                                                                  
.OUT3    ANOP                                                                   
&POINT   SETA  17                                                               
         AIF   ('&DECS' GT '0').OUT10                                           
         AIF   ('&COMMAS' EQ 'YES').OUT6                                        
         AIF   ('&MINUS' EQ 'YES').OUT4                                         
         MVC   &WRK.(17),=X'4040404040402020202020202020202020'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+15,X'21'                                                    
         AGO   .OUTEND                                                          
.*                                                                              
.OUT4    MVC   &WRK.(17),=X'4040404040202020202020202020202060'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+14,X'21'                                                    
         AGO   .OUTEND                                                          
.*                                                                              
.OUT6    AIF   ('&MINUS' EQ 'YES').OUT8                                         
         MVC   &WRK.(17),=X'40404020206B2020206B2020206B202020'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+15,X'21'                                                    
         AGO   .OUTEND                                                          
.*                                                                              
.OUT8    MVC   &WRK.(17),=X'404020206B2020206B2020206B20202060'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+14,X'21'                                                    
         AGO   .OUTEND                                                          
.*                                                                              
.OUT10   AIF   ('&DECS' NE '1').OUT14                                           
.*                                                                              
.*                                 1 DECIMAL PLACE                              
&POINT   SETA  15                                                               
         AIF   ('&COMMAS' EQ 'YES').OUT12                                       
         MVC   &WRK.(17),=X'4040404040202020202020202020214B20'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT12   MVC   &WRK.(17),=X'4040206B2020206B2020206B2020214B20'                 
         AGO   .OUTEND                                                          
.*                                                                              
.*                                 2 DECIMAL PLACES                             
.OUT14   AIF   ('&DECS' NE '2').OUT26                                           
         AIF   ('&COMMAS' EQ 'YES').OUT20                                       
         AIF   ('&MINUS' EQ 'YES').OUT18                                        
         AIF   ('&CR' EQ 'YES').OUT16                                           
&POINT   SETA  14                                                               
         MVC   &WRK.(17),=X'40404040402020202020202020214B2020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT16   MVC   &WRK.(17),=X'4040402020202020202020214B2020C3D9'                 
&POINT   SETA  12                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT18   MVC   &WRK.(17),=X'404040402020202020202020214B202060'                 
&POINT   SETA  13                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT20   AIF   ('&MINUS' EQ 'YES').OUT24                                        
         AIF   ('&CR' EQ 'YES').OUT22                                           
&POINT   SETA  14                                                               
         MVC   &WRK.(17),=X'4040402020206B2020206B2020214B2020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT22   MVC   &WRK.(17),=X'402020206B2020206B2020214B2020C3D9'                 
&POINT   SETA  12                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT24   MVC   &WRK.(17),=X'40402020206B2020206B2020214B202060'                 
&POINT   SETA  13                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.*                                 MORE THAN 2 DEC PLACES                       
.OUT26   AIF   ('&DECS' NE '3').OUT28                                           
&POINT   SETA  13                                                               
         MVC   &WRK.(17),=X'404040404020202020202020214B202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT28   AIF   ('&DECS' NE '4').OUT30                                           
         AIF   ('&MINUS' EQ 'YES').OUT29                                        
&POINT   SETA  12                                                               
         MVC   &WRK.(17),=X'4040404040202020202020214B20202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT29   MVC   &WRK.(17),=X'40404040202020202020214B2020202060'                 
&POINT   SETA  11                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT30   AIF   ('&DECS' NE '5').OUT32                                           
&POINT   SETA  11                                                               
         MVC   &WRK.(17),=X'40404040402020202020214B2020202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT32   AIF   ('&DECS' NE '6').OUT34                                           
&POINT   SETA  10                                                               
         MVC   &WRK.(17),=X'404040404020202020214B202020202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT34   MNOTE 0,'TOO MANY DECIMAL POINTS'                                      
.OUTEND  AIF   (T'&FILL EQ 'O').ED4                                             
         AIF   (K'&FILL EQ 1).ED2                                               
         MNOTE 0,'FILL CHARACTER MUST BE ONE BYTE'                              
         AGO   .ED4                                                             
.*                                 FILL CHARACTER OPTION                        
.ED2     MVI   &WRK,C'&FILL'                                                    
.*                                                                              
.*                                 FLOAT CHARACTER OPTION                       
.ED4     AIF   (T'&FLOAT EQ 'O').ED6                                            
         LA    R1,&WRK+&POINT                                                   
         EDMK  &WRK.(17),&DUB+2                                                 
         BCTR  R1,R0                                                            
         AIF   ('&FLOAT' NE '-').ED5                                            
         CP    &DUB,=P'0'                                                       
         BNL   *+8                                                              
.ED5     ANOP                                                                   
         MVI   0(R1),C'&FLOAT'                                                  
         AGO   .EDEND                                                           
.*                                                                              
.*                                 STANDARD EDIT                                
.ED6     ED    &WRK.(17),&DUB+2                                                 
.*                                                                              
.*                                 HANDLE ZERO=BLANK HERE                       
.*                                                                              
         AIF   ('&ZERO' NE 'BLANK').ED8                                         
         CP    &DUB,=P'0'                                                       
         BNE   *+10                                                             
         MVC   &WRK.(17),=CL17' '                                               
.*                                                                              
.*                                 HANDLE TRAIL=C HERE                          
.*                                                                              
.ED8     AIF   (T'&TRAIL EQ 'O').ED10                                           
         CLC   &WRK.(17),=CL17' '                                               
         BE    *+14                                                             
         MVC   &WRK.(16),&WRK+1                                                 
         MVI   &WRK+16,&TRAIL                                                   
.*                                                                              
.*                                 HANDLE DROP=N HERE                           
.*                                                                              
.ED10    AIF   (T'&DROP EQ 'O').ED12                                            
         LA    R1,&WRK+16                                                       
         LA    R0,&DROP                                                         
         CLI   0(R1),C'0'                                                       
         BNE   *+14                                                             
         MVI   0(R1),C' '                                                       
         BCTR  R1,0                                                             
         BCT   R0,*-14                                                          
.*                                                                              
.ED12    ANOP                                                                   
.*                                                                              
.*                                 NOW HANDLE OUTPUT OPTIONS                    
.*                                                                              
.EDEND   AIF   (T'&OUTPUT EQ 'O').OUT35                                         
         AIF   (N'&OUTPUT EQ 2).OUT36                                           
         MNOTE 0,'OUTPUT MUST BE TWO TERM SUB-LIST'                             
.OUT35   ANOP                                                                   
         SPACE 1                                                                
         MEXIT                                                                  
.OUT36   AIF   (T'&OUTPUT(1) EQ 'O').OUT38                                      
         AIF   (&OUTPUT(1) EQ 0).OUT38                                          
         AIF   (&OUTPUT(1) LE 17).OUT40                                         
.*                                                                              
.OUT38   MNOTE 0,'OUTPUT LENGTH MUST BE 1-17'                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.OUT40   ANOP                                                                   
&OUTDSP  SETA  17-&OUTPUT(1)                                                    
.*                                 CHECK FOR ( IN OUTPUT EXPRESSIONS            
         AIF   ('&OUTPUT(2)'(1,1) EQ '(').BRAC1                                 
         AIF   (K'&OUTPUT(2) EQ 1).BRAC0                                        
         AIF   ('&OUTPUT(2)'(2,1) EQ '(').BRAC2                                 
         AIF   (K'&OUTPUT(2) EQ 2).BRAC0                                        
         AIF   ('&OUTPUT(2)'(3,1) EQ '(').BRAC3                                 
         AIF   (K'&OUTPUT(2) EQ 3).BRAC0                                        
         AIF   ('&OUTPUT(2)'(4,1) EQ '(').BRAC4                                 
         AIF   (K'&OUTPUT(2) EQ 4).BRAC0                                        
         AIF   ('&OUTPUT(2)'(5,1) EQ '(').BRAC5                                 
.BRAC0   ANOP                                                                   
.*                                                                              
&KA      SETA  0                   NO REGISTERS-MOVE IN OUTPUT                  
         MVC   &OUTPUT(2).(&OUTPUT(1)),&WRK+&OUTDSP                             
         AGO   .LEFT                                                            
.*                                                                              
.BRAC1   ANOP                                                                   
&KA      SETA  1                                                                
         AGO   .BRACALL                                                         
.*                                                                              
.BRAC2   ANOP                                                                   
&KA      SETA  2                                                                
         AGO   .BRACALL                                                         
.*                                                                              
.BRAC3   ANOP                                                                   
&KA      SETA  3                                                                
         AGO   .BRACALL                                                         
.*                                                                              
.BRAC4   ANOP                                                                   
&KA      SETA  4                                                                
         AGO   .BRACALL                                                         
.*                                                                              
.BRAC5   ANOP                                                                   
&KA      SETA  5                                                                
.*                                                                              
.BRACALL ANOP                                                                   
&KB      SETA  K'&OUTPUT(2)-&KA                                                 
.*                                                                              
.*                                 WE CAN NOW SPLIT EXPRESSION                  
.*                                                                              
&CA      SETC  '&OUTPUT(2)'(1,&KA)                                              
&CB      SETC  '&OUTPUT(2)'(&KA+1,&KB)                                          
         AIF   (&KA NE 1).NOZERO                                                
         MVC   0&CA&OUTPUT(1),&CB,&WRK+&OUTDSP                                  
         AGO   .LEFT                                                            
.NOZERO  ANOP                                                                   
         MVC   &CA&OUTPUT(1),&CB,&WRK+&OUTDSP                                   
.*                                                                              
.*                                 OUTPUT MOVED - CHECK LEFT ALIGNMENT          
.*                                                                              
.LEFT    AIF   (T'&BRACKET EQ 'O').LEFT1                                        
.*                                                                              
.*                                           BRACKET OPTION                     
.*                                                                              
         LA    R1,&OUTPUT(2)                                                    
         LA    R0,&OUTPUT(1)                                                    
         CLI   0(R1),C' '                                                       
         BNE   *+16                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,*-12                                                          
         B     *+34                                                             
         BCTR  R1,0                                                             
         MVI   0(R1),C'('                                                       
         LA    R1,2(R1)                                                         
         CLI   0(R1),C' '                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,*-12                                                          
         MVI   0(R1),C')'                                                       
.*                                                                              
.*                                                                              
.LEFT1   AIF   (T'&ALIGN EQ 'O').MACEND                                         
         AIF   ('&ALIGN'(1,1) NE 'L').MACEND                                    
.*                                                                              
.*                                 NON REGISTER EXPRESSIONS                     
         LA    R0,&OUTPUT(1)                                                    
         AIF   (&KA EQ 1).LEFT2                                                 
         CLI   &OUTPUT(2),C' '                                                  
         BNE   *+18                                                             
         AIF   (&KA NE 0).LEFT2                                                 
         MVC   &OUTPUT(2).(&OUTPUT(1)),&OUTPUT(2)+1                             
         MVI   &OUTPUT(2)+&OUTPUT(1)-1,C' '                                     
         BCT   R0,*-18                                                          
.MACEND  ANOP                                                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 NOW GENERATE FOR REGISTER EXPRESS.           
.*                                                                              
.LEFT2   AIF   (&KA NE 1).LEFT4                                                 
         CLI   0&OUTPUT(2),C' '                                                 
         BNE   *+18                                                             
         MVC   0&CA&OUTPUT(1),&CB,1&OUTPUT(2)                                   
         MVI   &OUTPUT(1)-1&OUTPUT(2),C' '                                      
         BCT   R0,*-18                                                          
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.LEFT4   ANOP                                                                   
&DRC     SETC  '&OUTPUT(2)'(1,&KA-1)                                            
&DR      SETA  &DRC+1                                                           
&NODSP   SETC  '&OUTPUT(2)'(&KA,&KB+1)                                          
&BLDSP   SETA  &DRC-1+&OUTPUT(1)                                                
.*                                                                              
         MVC   &CA&OUTPUT(1),&CB,&DR&NODSP                                      
         MVI   &BLDSP&NODSP,C' '                                                
         BCT   R0,*-18                                                          
         SPACE 1                                                                
         MEXIT                                                                  
         MEND                                                                   
**PAN#1  CSECT                                                                  
         DC    CL21'003MCEDITS   11/12/92'                                      
         END                                                                    
