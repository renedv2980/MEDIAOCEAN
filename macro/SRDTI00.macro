*          DATA SET SRDTI00    AT LEVEL 007 AS OF 03/02/18                      
*&&      SET   NOP=N                                                            
***********************************************************************         
* A BIT OF EXPERIMENTING WITH NO BASE REGISTER CODE IN THIS BOOK      *         
* ONE BASE (RB) IS DEFINED UP FRONT WITH CONSTANTS AND A SPARE        *         
* GAP FOR THE LITERAL POOL.                                           *         
* CODE BEGINS AT RB+4096 JUST AT THE END OF ADDRESSABILITY            *         
* ALSO THE MVCI MACRO TO TRY AND REMOVE                               *         
* THE NEEED FOR LITERALS MOST OF THE TIME.                            *         
***********************************************************************         
         MACRO                                                                  
&NAME    MVCI  &M1,&M2             MVC M1(L2),M2                                
         GBLC  &LBL1                                                            
         LCLA  &LEN1                                                            
&LEN1    SETA  K'&M2-3                                                          
&LBL1    SETC  'LB1&SYSNDX'                                                     
         JAS   R1,&LBL1                                                         
         DC    &M2                                                              
         AIF   ('&M2'(1,1) EQ 'X').PLISTR                                       
&LBL1    MVC   &M1.(&LEN1),0(R1)                                                
         AGO   .RTN                                                             
.PLISTR  ANOP                                                                   
&LBL1    MVC   &M1.((&LEN1)/2),0(R1)                                            
.RTN     MEXIT                                                                  
         MEND                                                                   
                                                                                
*PHASE T16900A                                                                  
         TITLE '$DTI - DATASPACE TASK INFORMATION'                              
DTI      CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRKL,**$DTI**,RR=RE,CLEAR=YES                                    
         USING WRKD,RC             RC=A(W/S)                                    
         ST    RE,RELO                                                          
         J     DTI000              JUMP TO MAIN PROGRAM (NO BASE REGS)          
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERALS UP FRONT - KEEP WITHIN 4K BASE REG           *         
***********************************************************************         
SPACES   DC    CL160' '                                                         
CTFILE   DC    CL8'CTFILE'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
TUHOUR   DC    F'138240000'        60*60*38400                                  
TUMINUTE DC    F'2304000'          60*38400                                     
TUSECOND DC    F'38400'            38400                                        
TUMSEC   DC    F'384'              384                                          
ARZERO   DC    4F'0'                                                            
HELPID   DC    XL10'0169FF00010000000000'                                       
         EJECT                                                                  
***********************************************************************         
* MESSAGES AND HEADERS                                                *         
***********************************************************************         
TASKHDR  DC    C'Adv   #T SE    Prog  I/Os   CPU   Run   Adv   #T '             
         DC    C'SE    Prog  I/Os   CPU   Run '                                 
TASKHDRU DC    C'----  -- ----  ----  ----  Time  Time   ----  -- '             
         DC    C'----- ---- -----  Time  Time '                                 
TASKCPU  DC    C'CPU Terminal Logcl Wait for Resource Wtm'                      
TASKCPUU DC    C'pct --------  I/Os -------- -------- ---'                      
*                                                                               
SRVPFKLN DC    C'PF2=Task PF3=Server PF4=Queue PF5=Brief PF6=LongRun'           
         DC    C' PF7/8 Scroll                 '                                
*                                                                               
RUNRHDR  DC    C'Runner   Type          Stat Time   XXXX '                      
         DC    C'Runner   Type          Stat Time   XXXX '                      
RUNRHDRU DC    C'-------- ------------- ---- ------ ---- '                      
         DC    C'-------- ------------- ---- ------ ---- '                      
RUNRLON  DC    C'Worker file            Status 2  Error     '                   
RUNRLONU DC    C'---------------------- --------- ----------'                   
*                                                                               
MSG1     DC    CL50'Dataspace information displayed'                            
*                                                                               
ERR1     DC    CL60'Facpak ID must be numeric in the range 1 - 32'              
ERR2     DC    CL60'Facpak ID number cannot be found within dataspace'          
ERR3     DC    CL60'Facpak is not initialised'                                  
ERR4     DC    CL60'Facpak has no tasks'                                        
ERR5     DS    0CL60                                                            
         DC    C'Start Task must be numeric in the range 1 - '                  
ERR5NUM  EQU   *-ERR5                                                           
         ORG   ERR5+L'ERR5                                                      
         EJECT                                                                  
***********************************************************************         
* TABLES                                                              *         
***********************************************************************         
TYPETAB  DS    0CL13                                                            
         DC    C'A Accent     '    ACCPAK ACCENT                                
         DC    C'B Scribe     '    ACCPAK SCRIBE                                
         DC    C'C Control    '    CONTROL SYSTEM                               
         DC    C'D Demos      '    US DEMOGRAPHICS (COMPARAGRAPH)               
         DC    C'E Na Demos   '    NA DEMOGRAPHICS (DESKTOP)                    
*&&UK*&& DC    C'S Me Stats   '    UK MEDIA STATS                               
*&&US*&& DC    C'S Steward    '    US NETPAK STEWARD                            
         DC    C'N Me  STATS  '    US NETPAK MATCHMAKER                         
*&&UK*&& DC    C'P Me Pay Upld'    EU MEDIA OFFLINE PAY UPLOAD                  
*&&US*&& DC    C'P Printpak   '    NA PRINTPAK ADBUYER                          
         DC    C'T Na Desktop '    NA DESKTOPS                                  
         DC    C'M Me Explore '    EU MEDIA EXPLORER                            
         DC    C'U Me Upload  '    EU MEDIA OFFLINE UPLOAD                      
         DC    C'X Aud Estimat'    US NETPAK AUDIENCE ESTIMATOR                 
         DC    C'O BOc Time   '    BRAND OCEAN TIME UPLOAD                      
         DC    C'W BOc Downloa'    BRAND OCEAN DOWNLOADS                        
         DC    C'  Unknown    '                                                 
                                                                                
STATTAB  DS    0CL11                                                            
         DC    C'B Actv    '                                                    
         DC    C'E Aben    '                                                    
         DC    C'S Wait    '                                                    
         DC    C'  Unkn    '                                                    
*                                                                               
STATTAB2 DS    0CL11                                                            
         DC    C'W Waiting  '                                                   
         DC    C'R Running  '                                                   
         DC    C'P Snd Pnd  '                                                   
         DC    C'S Snd In Pr'                                                   
         DC    C'C Complete '                                                   
                                                                                
ERRORTAB DS    0CL11                                                            
         DC    AL1(000),CL10'No Errors '                                        
         DC    AL1(255),CL10'No ECB    '                                        
         DC    AL1(254),CL10'NO Server '                                        
         DC    AL1(253),CL10'NO Run que'                                        
         DC    AL1(252),CL10'Inv UserId'                                        
         DC    AL1(251),CL10'Inv System'                                        
         DC    AL1(250),CL10'Inv Worker'                                        
         DC    AL1(249),CL10'I/O Exceed'                                        
         DC    AL1(248),CL10'CPU Exceed'                                        
         DC    AL1(247),CL10'Pages Excd'                                        
         DC    AL1(246),CL10'TCB Exceed'                                        
         DC    AL1(245),CL10'Prg Check '                                        
         DC    AL1(244),CL10'Sys Closed'                                        
         DC    AL1(243),CL10'Cancelled '                                        
         DC    AL1(000),CL10'Unknown   '                                        
         EJECT                                                                  
***********************************************************************         
* CREATE AREA FOR LITERAL POOL UP TO 4K BOUNDARY                      *         
***********************************************************************         
LITERALS DS    0D                                                               
         ORG   DTI+4096                                                         
         EJECT                                                                  
***********************************************************************         
* MAIN PROGRAM - NO BASE REGS SO MUST JUMP EVERYWHERE                 *         
***********************************************************************         
DTI000   EQU   *                                                                
         USING SRPARMD,R1          R1=A(PARMS)                                  
         L     RA,SRPARM6                                                       
         USING SRDTIFFD,RA         RA=A(TWA)                                    
         L     R2,SRPARM1                                                       
         USING SYSFACD,R2          R9=A(SYSFACS)                                
         MVC   ASELIST,VSELIST                                                  
         MVC   ADATAMGR,VDATAMGR                                                
         MVC   ASSB,VSSB                                                        
         DROP  R2                                                               
*                                                                               
         L     R2,SRPARM4                                                       
         USING COMFACSD,R2         R2=A(COMFACS)                                
         MVC   VHEXOUT,CHEXOUT     SAVE USEFUL ENTRY POINTS                     
         MVC   AGETFACT,CGETFACT                                                
         MVC   ASWITCH,CSWITCH                                                  
         MVC   AGETHELP,CGETHELP                                                
*                                                                               
         L     RF,SRPARM3                                                       
         USING UTLD,RF                                                          
         ST    RF,AUTL                                                          
         TM    TSVCREQ,X'01'       TEST FIRST TIME                              
         JNO   *+8                                                              
         OI    TSVCREQ,X'02'       SET =XRSOR FLAG                              
         DROP  RF                                                               
*                                                                               
         L     RF,SRPARM8          SET CURSOR INF FROM TIOB                     
         USING TIOBD,RF                                                         
         SR    R0,R0                                                            
         ICM   R0,3,TIOBCURD                                                    
         AR    R0,R3               ADD TWA ADDRESS                              
         ST    R0,CURSOR                                                        
         MVC   CURADR,TIOBCURS                                                  
         MVC   PFKEY,TIOBAID       SAVE PFKEY DATA                              
         MVC   CURI,TIOBCURI                                                    
*                                                                               
         SR    RE,RE               PF1 HELP                                     
         ICM   RE,3,TIOBCURD                                                    
         AR    RE,RA                                                            
         CLI   PFKEY,1             TEST HELP PFKEY                              
         JNE   *+12                                                             
         ST    RE,AHELP            SAVE A(HELP FIELD)                           
         MVI   PFKEY,0                                                          
                                                                                
         DROP  RF                                                               
*                                                                               
         GOTO1 ADATAMGR,DMCB,(0,=CL8'FILTAB'),0                                 
         MVC   AFILTAB,DMCB+4                                                   
*                                                                               
         GOTO1 CGETFACT,DMCB,0     GET A(SYSLIST)                               
         L     R1,0(R1)                                                         
         L     RE,FASYSLST-FACTSD(R1)                                           
         ST    RE,ASYSLST                                                       
         GOTO1 (RF),DMCB,(X'80',FULL),F#SYSADS                                  
         L     R1,FULL                                                          
         MVC   AORALL,AAORALL-SYSADSD(R1)                                       
         DROP  R2,R1               FINISHED WITH COMFACS                        
*                                                                               
HDR      USING FHD,SRVMSGH                                                      
*                                                                               
         BRAS  RE,INIT             INITIALISE                                   
*                                                                               
         CLI   PFKEY,2             TASK KEY                                     
         JNE   *+10                                                             
         MVC   SRVSRV+1(5),=C'DTI,T'                                            
         CLI   PFKEY,3             SERVER KEY                                   
         JNE   *+10                                                             
         MVC   SRVSRV+1(5),=C'DTI,S'                                            
         CLI   PFKEY,4             QUEUE KEY                                    
         JNE   *+10                                                             
         MVC   SRVSRV+1(5),=C'DTI,Q'                                            
*                                                                               
         CLI   PFKEY,5             BRIEF                                        
         JNE   PFKEY001                                                         
         MVC   SRVP2(8),=C'SHORT   '                                            
         MVI   SRVP2H+5,5                                                       
*                                                                               
PFKEY001 CLI   PFKEY,6             LONG/SHORT                                   
         JNE   PFKEY002                                                         
         MVC   SRVP1(8),=C'LONG    '                                            
         MVI   SRVP1H+5,4                                                       
         MVI   CURI,1              FORCE TO LOOK LIKE DATA ENTRY                
*                                                                               
PFKEY002 MVI   MODE,C'T'                                                        
         CLC   SRVSRV+1(4),=C'DTI,'                                             
         JNE   *+10                                                             
         MVC   MODE,SRVSRV+5                                                    
*                                                                               
DTI001   BRAS  RE,VALIDATE                                                      
*                                                                               
         MVC   SRVPFK,SRVPFKLN                                                  
*                                                                               
DTI010   TIME  TU                                                               
         ST    R0,TIMENOW          TIME IN MVS TU'S (1/38400 SEC)               
*                                                                               
         CLI   MODE,C'T'                                                        
         JNE   DTI012                                                           
         BRAS  RE,NEWDTI           DISPLAY TASK INFORMATION                     
*                                                                               
         CLI   KILLTASK,0          ANY KILLS LEFT UNFULFILLED                   
         JE    DTI012              NO                                           
         MVI   KILLTASK,0          DON'T CARRY KILLS TO NEXT ENTER              
         MVC   KILLADV,SPACES                                                   
         MVC   SRVP3+7(8),=C' NO KILL'                                          
*                                                                               
DTI012   CLI   MODE,C'S'                                                        
         JNE   *+8                                                              
         BRAS  RE,NEWDTIR          DISPLAY RUNNER INFORMATION                   
*                                                                               
         CLI   MODE,C'Q'                                                        
         JNE   *+8                                                              
         BRAS  RE,NEWDTIQ          DISPLAY RUNNER INFORMATION                   
         J     EXIT                                                             
*                                                                               
         GOTO1 AGETFACT,DMCB,(X'80',WORK),F#WRITE                               
         GOTO1 AGETFACT,DMCB,(X'80',=F'38400'),F#WAIT 1 SECOND                  
*                                                                               
         LA    R3,SRVL1H                                                        
         USING TSKLINED,R3                                                      
DTI020   MVC   TSKLINEL,SPACES                                                  
         LA    R3,TSKLENQ(R3)                                                   
         LA    R1,SRVPFKH                                                       
         CR    R3,R1                                                            
         JL    DTI020                                                           
*                                                                               
         L     R1,DISPLOOP                                                      
         LA    R1,1(R1)                                                         
         ST    R1,DISPLOOP         LOOP FOR 60 SECONDS AT 1 SEC                 
         CLC   DISPLOOP,=F'60'                                                  
         JL    DTI010                                                           
*                                                                               
         J     EXIT                                                             
*                                                                               
         MVI   SRVMSG,C'('         SET MESSAGE                                  
         MVC   SRVMSG+1(L'SYSNAME),SYSNAME                                      
         LA    RF,SRVMSG+L'SYSNAME+1                                            
         CLI   0(RF),C' '                                                       
         JH    *+8                                                              
         JCT   RF,*-8                                                           
         MVI   1(RF),C')'                                                       
         LA    RF,3(RF)                                                         
         MVC   0(L'MSG1,RF),MSG1                                                
         OI    HDR.FHOI,FHOITR     TRANSMIT IT                                  
*                                                                               
EXIT     L     R1,CURSOR                                                        
         OI    6(R1),X'40'         AND SET CURSOR                               
         OC    AHELP,AHELP                                                      
         JNZ   HELPOUT                                                          
         XMOD1 ,                                                                
*                                                                               
EXITEQU  CR    RB,RB                                                            
         J     XIT1                                                             
EXITNEQ  LTR   RB,RB                                                            
XIT1     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* INITIALISE ALL VALUES                                               *         
***********************************************************************         
INIT     NTR1  ,                                                                
         L     R2,ASSB                                                          
         USING SSBD,R2                                                          
         MVC   SYSNAME,SSBSYSN4    GET SYSTEM NAME                              
         MVC   LOCORE,SSBLOADR                                                  
         MVC   HICORE,SSBHIADR                                                  
         MVC   AFID,SSBAFID                                                     
*                                                                               
         MVC   MYALET,SSBALET      ALET DMGR                                    
         MVC   MYTBLET,SSBTBLET    ALET TABS                                    
*                                                                               
         GOTO1 AORALL,DMCB         GET ALETS AND AFACALET                       
         MVC   AFACALET,0(R1)                                                   
*                                                                               
         LAM   AR2,AR2,MYALET      CREATE TABLE OF ASID/FACIDS                  
         XR    R2,R2                                                            
         SAC   512                                                              
         USING DMDSHDR,R2                                                       
         L     R2,DHAADVS                                                       
         USING DSJOBD,R2                                                        
         LA    R0,DSJOBMXQ         Max jobs to look at                          
         LA    R3,FACASIDS                                                      
INIT010  CLI   DSJADV,0                                                         
         JE    INIT020                                                          
         MVC   0(2,R3),DSJASID                                                  
         MVC   2(2,R3),DSJADV                                                   
         LA    R3,4(R3)                                                         
INIT020  LA    R2,DSJOBLNQ(,R2)                                                 
         JCT   R0,INIT010                                                       
*                                                                               
         LAM   AR2,AR2,MYTBLET     INIT TABS AREAS                              
         XR    R2,R2                                                            
         USING FATABSD,R2                                                       
         L     R2,TABSRUN                                                       
         USING TABSRUND,R2                                                      
         XR    R1,R1                                                            
         IC    R1,TABSSRWD         SERVER WIDTH                                 
         STH   R1,TABSVRW                                                       
         IC    R1,TABSJBWD         JOB WIDTH                                    
         STH   R1,TABQUEW                                                       
         MVC   TABASVR,TABSASVR                                                 
         MVC   TABAQUE,TABSAQUE                                                 
         MVC   TABNSVR,TABSNSVR                                                 
         MVC   TABNQUE,TABSNQUE                                                 
         SAC   0                                                                
*                                                                               
INITX    XIT1  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* NEW DTI TASK ROUTINE                                                *         
***********************************************************************         
NEWDTI   NTR1  ,                                                                
         MVCI  WORK,X'0102030405060708'                                         
*                                                                               
         L     R6,AFACALET                                                      
         USING FACALETD,R6                                                      
*                                                                               
         MVC   SRVHDR,TASKHDR                                                   
         OI    SRVHDRH+6,X'80'                                                  
         MVC   SRVHDRU,TASKHDRU                                                 
         OI    SRVHDRUH+6,X'80'                                                 
         LA    R3,SRVL1H                                                        
         USING TSKLINED,R3                                                      
*                                                                               
NEWDTI00 DS    0H                                                               
         CLI   FACALSTD,C'Y'                                                    
         JNE   NEWDTI03                                                         
         CLI   FACALAVL,C'Y'                                                    
         JNE   NEWDTI03                                                         
*                                                                               
         LAM   AR2,AR2,FACALALE    ADDRESS TARGET FACPAK                        
         LAM   ARE,AR1,ARZERO                                                   
         LAM   AR8,AR8,FACALALE    ADDRESS TARGET FACPAK R8=WORK REG            
         SAC   512                                                              
         L     R2,FACALTCB         PICK UP A(TCB)                               
*                                                                               
         LH    R4,0(,R2)           BXLE THROUGH TASKS                           
         L     R5,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
         USING TCBD,R2                                                          
*                                                                               
NEWDTI01 CLI   TCBSEN,0            ANYTHING GOING ON                            
         JNE   NEWDTI10                                                         
*                                                                               
NEWDTI02 JXLE  R2,R4,NEWDTI01      NEXT                                         
*                                                                               
NEWDTI03 LA    R6,FACALLEN(R6)     NEXT FAC ENGINE                              
         OC    FACALFAC,FACALFAC                                                
         JNZ   NEWDTI00                                                         
         J     NEWDTIX                                                          
*                                                                               
NEWDTI10 CLI   PAGE,2                                                           
         JE    *+8                                                              
         OI    6(R3),X'80'                                                      
         USING TCBD,R2                                                          
*                                                                               
         BRAS  RE,FILTER           DO WE WANT TO SEE THIS TASK                  
         JNE   NEWDTI02                                                         
*                                                                               
NEWT01   MVC   TSKLINE(10),SPACES                                               
         MVC   TSKADV,FACALFAC                                                  
         MVI   TSKTSK,C'#'                                                      
         MVC   TSKTSK+1(1),TCBID+6                                              
*                                                                               
         CLI   KILLTASK,0          ARE WE LOOKING TO KILL ANYTHING?             
         JE    NEWT01A             NO                                           
         CLC   KILLADV,TSKADV                                                   
         JNE   NEWT01A                                                          
         CLC   KILLTASK,TSKTSK+1                                                
         JNE   NEWT01A                                                          
         OI    TCBFLAG3,TCBKILL    FLAG THIS FOR FORCED ABEND                   
         MVI   KILLTASK,0          LIFE AFTER DEATH                             
         MVC   KILLADV,SPACES                                                   
*                                                                               
NEWT01A  CLI   TCBSEN,0                                                         
         JE    NEWT03                                                           
         MVC   GTSYSSE,TCBSEN                                                   
         BRAS  RE,GETSYS                                                        
         MVC   TSKSESYS,GTSYSN                                                  
         CLC   TSKSESYS(3),=C'SER'                                              
         JNE   *+10                                                             
         MVC   TSKSESYS+3(2),SPACES                                             
         CLC   TSKSESYS(3),=C'CON'                                              
         JNE   *+10                                                             
         MVC   TSKSESYS+3(2),SPACES                                             
         MVC   GTPROG,TCBPRG                                                    
         BRAS  RE,GETPROG                                                       
         MVC   TSKPROG,GTPROGN                                                  
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,7,TCBIOCNT                                                    
         C     R0,=F'99999'                                                     
         JH    NEWT02                                                           
         EDIT  (R0),(5,TSKIOC)                                                  
         J     NEWT03                                                           
*                                                                               
NEWT02   SRL   R0,10                                                            
         EDIT  (R0),(4,TSKIOC)                                                  
         MVI   TSKIOC+4,C'k'                                                    
*                                                                               
NEWT03   EDIT  (B4,TCBCPUTK),(10,WORK),FILL=0                                   
         MVC   WORK+10(3),WORK+1   WORK+00=SSSSCCCCCC S=SECS,C=MICRO            
         MVI   WORK+13,C'.'                                                     
         MVC   WORK+14(6),WORK+4   WORK+10=SSS.CCCCCC                           
         MVC   TSKCPU-1(6),WORK+10 OUTPUT CPU TIME AS SSS.00                    
         CLI   TSKCPU-1,C'0'                                                    
         JNE   NEWT05                                                           
         MVI   TSKCPU-1,C' '       ADJUST TO SS.00 (SECONDS.HUNDRETHS)          
         CLI   TSKCPU,C'0'                                                      
         JNE   NEWT05                                                           
         MVI   TSKCPU,C' '         ADJUST TO S.00                               
*                                                                               
NEWT05   L     R1,TIMENOW          TASK RUN TIME IN TUS (1/38400 SEC)           
         S     R1,TCBSTTM                                                       
         ST    R1,FULL                                                          
         BRAS  RE,TIMEOUT                                                       
         BRAS  RE,T5CHR                                                         
         MVC   TSKTIME,WORK1                                                    
         CLI   PAGE,C'X'                                                        
         JNE   NEWT99                                                           
         EJECT                                                                  
***********************************************************************         
* EXTRA INFO CPU/RIO/WAITS (CPU)                                      *         
***********************************************************************         
         MVC   SRVHDR+39(39),TASKCPU                                            
         OI    SRVHDRH+6,X'80'                                                  
         MVC   SRVHDRU+39(39),TASKCPUU                                          
         OI    SRVHDRUH+6,X'80'                                                 
*                                                                               
         L     RF,TCBCPUTM         ELAPSED CPU IN TUS                           
         M     RE,=F'1000000'                                                   
         D     RE,TUSECOND         RF=ELAPSED CPU TIME IN MICROSECS             
         LTR   R1,RF                                                            
         JZ    NEWT20                                                           
         XR    RE,RE                                                            
         L     RF,TCBCPUTK         RF=TASK USED CPU TIME IN MICROSECS           
         MHI   RF,100                                                           
         DR    RE,R1               RF=USED/ELAPSED AS A PERCENTAGE              
         EDIT  (RF),(3,TSKCPUPC),FILL=0                                         
*                                                                               
NEWT20   MVC   TSKLUID,TCBSYM                                                   
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,15,TCBLIOCT                                                   
         C     R0,=F'99999'                                                     
         JH    NEWT22                                                           
         EDIT  (R0),(5,TSKLIOC)                                                 
         J     NEWT23                                                           
*                                                                               
NEWT22   SRL   R0,10                                                            
         EDIT  (R0),(4,TSKLIOC)                                                 
         MVI   TSKIOC+4,C'k'                                                    
*                                                                               
NEWT23   MVC   TSKWAIT,=C'None    '                                             
*                                                                               
         OC    TCBREGS(64),TCBREGS                                              
         JZ    NEWT23A                                                          
*                                                                               
         MVC   TSKWAIT,=C'TIMERPOP'                                             
         J     NEWT98                                                           
*                                                                               
NEWT23A  ICM   R8,15,TCBSVRD                                                    
         JZ    NEWT98                                                           
         ICM   R8,15,4(R8)                                                      
         JZ    NEWT97                                                           
*                                                                               
         CLC   0(4,R8),=C'+TKW'    TIMER WAIT                                   
         JNE   NEWT24                                                           
         MVC   TSKWAIT(8),=C'TIMER   '                                          
         ICM   R8,15,4(R8)                                                      
         JZ    NEWT98                                                           
NEWT23B  CLC   0(4,R8),=C'ENQD'    FROM ENQDEQ?                                 
         JE    NEWT23C                                                          
*                                                                               
         MVC   TSKRESRC+4(4),0(R8)                                              
         ICM   R8,15,4(R8)                                                      
         CLC   0(4,R8),=C'ENQD'    FROM ENQDEQ?                                 
         JE    NEWT23C                                                          
         JZ    NEWT98                                                           
         MVC   TSKRESRC+0(4),0(R8)                                              
         CLC   TSKRESRC(8),=C'+PEN+IOE'                                         
         JNE   NEWT98                                                           
         MVC   TSKRESRC(8),SPACES                                               
         J     NEWT23B                                                          
*                                                                               
NEWT23C  ICM   R8,15,24(R8)                                                     
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         MVC   TSKRESRC(2),=C'?Q'                                               
         MVC   TSKRESRC(1),0(R8)                                                
         ICM   R8,7,1(R8)                                                       
         JZ    NEWT98                                                           
         MVC   TSKRESRC+3(5),0(R8)                                              
         J     NEWT98                                                           
*                                                                               
NEWT24   CLC   0(4,R8),=C'+ADW'    ADWAIT                                       
         JNE   NEWT30                                                           
*                                                                               
NEWT25   ICM   R8,15,4(R8)         BACK UP TO DMGR                              
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT97                                                           
         CLC   0(4,R8),=C'LOCK'    OR LOCKER                                    
         JE    NEWT29                                                           
*                                                                               
         CLI   TCBSVECB,X'FF'      CHECK FOR ISGENQ WAIT                        
         JNE   *+14                                                             
         CLC   0(4,R8),=C'ISGE'                                                 
         JE    NEWT50                                                           
*                                                                               
         CLI   TCBSVECB,X'FC'      CHECK FOR LOCKSPC IF DSPACE WAIT             
         JL    *+14                                                             
         CLC   0(4,R8),=C'LKSP'                                                 
         JE    NEWT40                                                           
*                                                                               
         CLC   0(4,R8),=C'DMGR'    DMGR                                         
         JE    *+14                                                             
         CLC   0(4,R8),=C'DEMA'    OR MAYBE DEMAND                              
         JNE   NEWT25                                                           
*                                                                               
         LA    R0,16                                                            
NEWT26   ICM   R8,15,8(R8)         THEN FORWARD TO ISDD OR DADD                 
         JZ    NEWT97                                                           
         CLC   0(4,R8),=C'ISDD'                                                 
         JE    NEWT27                                                           
         CLC   0(4,R8),=C'DADD'                                                 
         JE    NEWT27                                                           
         JCT   R0,NEWT26                                                        
         J     NEWT97                                                           
*                                                                               
NEWT27   MVC   TSKWAIT(4),0(R8)                                                 
         MVC   TSKRESRC,=C'UNKNOWN '                                            
         ICM   R8,15,24(R8)                                                     
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         ICM   R8,15,12(R8)                                                     
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         MVC   TSKRESRC,DTFDD-DTFPHD(R8)                                        
         J     NEWT98                                                           
*                                                                               
NEWT29   MVC   TSKWAIT(8),=C'LOCK=   '                                          
         ICM   R8,15,24(R8)                                                     
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
*                                                                               
         MVC   BYTE,0(R8)                                                       
         TM    4(R8),X'20'                                                      
         JNZ   NEWT29A                                                          
         MVC   FULL,0(R8)                                                       
         MVI   FULL,0                                                           
         J     NEWT29B                                                          
*                                                                               
NEWT29A  ICM   R8,7,1(R8)                                                       
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         MVC   FULL,0(R8)                                                       
*                                                                               
NEWT29B  SAC   0                                                                
         GOTO1 VHEXOUT,DMCB,BYTE,TSKWAIT+5,1                                    
         GOTO1 (RF),(R1),FULL,TSKRESRC,4                                        
         SAC   512                                                              
         LLC   R1,BYTE                                                          
         SLL   R1,5                                                             
         A     R1,AFILTAB                                                       
         MVI   TSKWAIT,C'*'                                                     
         MVC   TSKWAIT+1(7),1(R1)                                               
         J     NEWT98                                                           
*                                                                               
NEWT30   CLC   0(4,R8),=C'+SCW'    ADWAIT                                       
         JNE   NEWT40                                                           
         ICM   R8,15,4(R8)                                                      
         JZ    NEWT97                                                           
         MVC   TSKWAIT(4),0(R8)                                                 
         ICM   R8,15,24(R8)                                                     
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         ICM   R8,15,0(R8)                                                      
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         MVC   TSKRESRC,0(R8)                                                   
         J     NEWT98                                                           
*                                                                               
NEWT40   MVC   TSKWAIT(4),0(R8)                                                 
         CLI   TCBSVECB,X'FC'      PGMS                                         
         JNE   *+10                                                             
         MVC   TSKRESRC(4),=C'PGMS'                                             
         CLI   TCBSVECB,X'FD'      TABS                                         
         JNE   *+10                                                             
         MVC   TSKRESRC(4),=C'TABS'                                             
         CLI   TCBSVECB,X'FE'      DMGR                                         
         JNE   *+10                                                             
         MVC   TSKRESRC(4),=C'DMGR'                                             
         XR    R1,R1                                                            
         ICM   R1,7,TCBSVECB+1                                                  
         SRL   R1,6                                                             
         ST    R1,FULL                                                          
         SAC   0                                                                
         GOTO1 VHEXOUT,DMCB,FULL+2,TSKRESRC+4,2                                 
         SAC   512                                                              
         J     NEWT98                                                           
*                                                                               
NEWT50   ICM   R8,15,TCBISGQ       A(ISGENQ FIRST LOCK ENTRY)                   
         JZ    NEWT97                                                           
         BRAS  RE,RANGECHK                                                      
         JNE   NEWT98                                                           
         MVC   TSKWAIT,=C'ISGENQ  '                                             
         MVC   TSKRESRC,10(R8)                                                  
         J     NEWT98                                                           
*                                                                               
NEWT97   MVC   TSKWAIT,=C'UNKNOWN '                                             
*                                                                               
NEWT98   L     RF,TIMENOW                                                       
         S     RF,TCBTIME                                                       
         XR    RE,RE                                                            
         D     RE,TUSECOND                                                      
         CHI   RF,99                                                            
         JH    NEWT98A                                                          
         EDIT  (RF),(2,TSKWTM),FILL=0                                           
         J     NEWT99                                                           
*                                                                               
NEWT98A  MVC   TSKWTM,=C'>>'                                                    
         J     NEWT99                                                           
*                                                                               
RANGECHK SLL   R8,8                MAKE SURE R8 IS IN A GOOD RANGE              
         SRL   R8,8                                                             
         C     R8,LOCORE                                                        
         BLR   RE                                                               
         C     R8,HICORE                                                        
         BHR   RE                                                               
         CR    RE,RE               EXIT EQU IF IT IS                            
         BR    RE                                                               
*                                                                               
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* NEXT LINE                                                           *         
***********************************************************************         
NEWT99   LA    R3,TSKLENQ(R3)                                                   
         LA    R1,SRVPFKH                                                       
         CR    R3,R1                                                            
         JL    NEWDTI02                                                         
*                                                                               
         CLI   PAGE,2              HAVE WE DOUBLED UP                           
         JE    NEWDTIX                                                          
         CLI   PAGE,C'X'           DO WE HAVE EXTRA INFO                        
         JE    NEWDTIX                                                          
         MVI   PAGE,2              DOUBLE UP NOW                                
         LA    R3,SRVL1H                                                        
         LA    R3,40(R3)           DO SECOND LINE                               
         J     NEWDTI02                                                         
*                                                                               
NEWDTIX  SAC   0                                                                
         XIT1  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* NEW DTI RUNNER ROUTINE                                              *         
***********************************************************************         
NEWDTIR  NTR1  ,                                                                
         MVC   SRVHDR,RUNRHDR                                                   
         OI    SRVHDRH+6,X'80'                                                  
         MVC   SRVHDRU,RUNRHDRU                                                 
         OI    SRVHDRUH+6,X'80'                                                 
         CLI   PAGE,C'X'                                                        
         JNE   NEWDTIRA                                                         
*                                                                               
         MVC   SRVHDR+35(43),RUNRLON                                            
         OI    SRVHDRH+6,X'80'                                                  
         MVC   SRVHDRU+35(43),RUNRLONU                                          
         OI    SRVHDRUH+6,X'80'                                                 
*                                                                               
NEWDTIRA LA    R3,SRVL1H                                                        
         USING RUNLINED,R3                                                      
*                                                                               
NEWDTIR0 LAM   AR2,AR2,MYTBLET     ADDRESS TABS DATASPACE                       
         LAM   AR8,AR8,MYTBLET                                                  
         LAM   ARE,AR1,ARZERO                                                   
         SAC   512                                                              
         L     R2,TABASVR          PICK UP A(SERVERS)                           
         USING TABSSVRD,R2                                                      
*                                                                               
         LH    R4,TABNSVR          MAX NUMBER OF SERVERS                        
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,1,MYPAGE         PAGE ADJUSTMENTS                             
         JZ    NEWTR01                                                          
*                                                                               
         MHI   R1,18                                                            
         CLI   PAGE,C'X'                                                        
         JE    NEWTR00                                                          
         SR    R1,R1                                                            
         ICM   R1,1,MYPAGE         PAGE ADJUSTMENTS                             
         MHI   R1,36                                                            
*                                                                               
NEWTR00  SR    R4,R1                                                            
         JNP   NEWDTIRZ                                                         
         MH    R1,TABSVRW                                                       
         AR    R2,R1                                                            
*                                                                               
NEWTR01  MVC   RUNRNAME,=C'Empty   '                                            
         CLI   TSJOB,C' '                                                       
         JNH   NEWTR99                                                          
         MVC   RUNRNAME,TSJOB                                                   
*                                                                               
         LA    R1,TYPETAB                                                       
         MVC   RUNRTYPE(1),TSTYPE                                               
NEWTR02  CLC   0(1,R1),TSTYPE                                                   
         JE    NEWTR02A                                                         
         LA    R1,13(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTR02                                                          
NEWTR02A MVC   RUNRTYPE+1(12),1(R1)                                             
*                                                                               
         LA    R1,STATTAB                                                       
         MVC   RUNRSTAT(1),TSFLAG                                               
NEWTR03  CLC   0(1,R1),TSFLAG                                                   
         JE    NEWTR03A                                                         
         LA    R1,10(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTR03                                                          
*                                                                               
NEWTR03A MVC   RUNRSTAT(4),2(R1)                                                
*                                                                               
         XR    R8,R8                                                            
         CLI   TSFLAG,C'B'         ACTIVE                                       
         JNE   NEWTR05                                                          
*                                                                               
NEWTR04  L     R8,TABAQUE          FIND ACTIVE QUEUE ENTRY                      
         USING TABSQUED,R8                                                      
         LH    R1,TABNQUE                                                       
NEWTR04A C     R2,TQASVR                                                        
         JE    NEWTR04B                                                         
         AH    R8,TABQUEW                                                       
         JCT   R1,NEWTR04A                                                      
         J     NEWTR05                                                          
*                                                                               
NEWTR04B L     R1,TIMENOW          GET ELAPSED TIME (SINCE ARRIVAL)             
         S     R1,TQATIME                                                       
         ST    R1,FULL                                                          
         BRAS  RE,TIMEOUT                                                       
         BRAS  RE,T5CHR                                                         
         MVC   RUNRTIME,WORK1                                                   
*                                                                               
NEWTR05  CLI   PAGE,C'X'                                                        
         JNE   NEWTR99             NO EXTRA INFO                                
         EJECT                                                                  
***********************************************************************         
* EXTRA INFO                                                          *         
***********************************************************************         
NEWTR10  EQU   *                                                                
         LTR   R8,R8                                                            
         JZ    NEWTR99                                                          
*                                                                               
         XC    MYWORK,MYWORK                                                    
         MVC   GIUSER,TQWUSRID                                                  
         SAC   0                                                                
         BRAS  RE,GETUSER                                                       
         LAM   ARE,AR1,ARZERO                                                   
         SAC   512                                                              
         MVC   MYWORK(10),GIUSERID                                              
         LA    R1,10                                                            
         LA    RF,MYWORK+9                                                      
NEWTR10A CLI   0(RF),C' '                                                       
         JH    NEWTR10B                                                         
         SHI   RF,1                                                             
         JCT   R1,NEWTR10A                                                      
         J     NEWTR99                                                          
*                                                                               
NEWTR10B MVI   1(RF),C','                                                       
         MVC   2(4,RF),TQWKEYID+2                                               
         MVC   8(1,RF),TQWKEYID+7     UUUUAAAA16C                               
         EDIT  (B1,TQWKEY+6),(2,6(RF))                                          
         MVI   9(RF),C','                                                       
         EDIT  (B2,TQWKEYSQ),(6,10(RF)),FILL=0                                  
         MVC   RUNRWRKF,MYWORK                                                  
*                                                                               
         LA    R1,STATTAB2                                                      
NEWTR11  CLC   0(1,R1),TQSTATUS                                                 
         JE    NEWTR11A                                                         
         LA    R1,10(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTR11                                                          
NEWTR11A MVC   RUNRSTA2,2(R1)                                                   
*                                                                               
         LA    R1,ERRORTAB                                                      
NEWTR12  CLC   0(1,R1),TQERROR                                                  
         JE    NEWTR12A                                                         
         LA    R1,10(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTR12                                                          
NEWTR12A MVC   RUNRERR,0(R1)                                                    
         EJECT                                                                  
***********************************************************************         
* NEXT LINE                                                           *         
***********************************************************************         
NEWTR99  AH    R2,TABSVRW          NEXT SERVER                                  
         LA    R3,RUNLENQ(R3)                                                   
         LA    R1,SRVPFKH                                                       
         CR    R3,R1                                                            
         JL    NEWTR99A                                                         
*                                                                               
         CLI   PAGE,2              HAVE WE DOUBLED UP                           
         JE    NEWDTIRX                                                         
         CLI   PAGE,C'X'           DO WE HAVE EXTRA INFO                        
         JE    NEWDTIRX                                                         
         MVI   PAGE,2              DOUBLE UP NOW                                
         LA    R3,SRVL1H                                                        
         LA    R3,40(R3)           DO SECOND LINE                               
*                                                                               
NEWTR99A JCT   R4,NEWTR01                                                       
*                                                                               
NEWDTIRZ MVC   RUNLINE,=C'------------ END OF DATA ------------ '               
*                                                                               
NEWDTIRX SAC   0                                                                
         XIT1  ,                                                                
         DROP  R8,R2                                                            
         EJECT                                                                  
***********************************************************************         
* NEW DTI RUNNER QUEUE ROUTINE                                        *         
***********************************************************************         
NEWDTIQ  NTR1  ,                                                                
         MVC   SRVHDR,RUNRHDR                                                   
         OI    SRVHDRH+6,X'80'                                                  
         MVC   SRVHDRU,RUNRHDRU                                                 
         OI    SRVHDRUH+6,X'80'                                                 
*                                                                               
         CLI   PAGE,C'X'                                                        
         JNE   NEWDTIQA                                                         
*                                                                               
         MVC   SRVHDR+35(43),RUNRLON                                            
         OI    SRVHDRH+6,X'80'                                                  
         MVC   SRVHDRU+35(43),RUNRLONU                                          
         OI    SRVHDRUH+6,X'80'                                                 
*                                                                               
NEWDTIQA LA    R3,SRVL1H                                                        
         USING RUNLINED,R3                                                      
*                                                                               
NEWDTIQ0 LAM   AR2,AR2,MYTBLET     ADDRESS TABS DATASPACE                       
         LAM   AR8,AR8,MYTBLET                                                  
         LAM   ARE,AR1,ARZERO                                                   
         SAC   512                                                              
         L     R2,TABAQUE          PICK UP A(SERVERS)                           
         USING TABSQUED,R2                                                      
*                                                                               
         LH    R4,TABNQUE          MAX NUMBER OF QUEUE                          
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,1,MYPAGE         PAGE ADJUSTMENTS                             
         JZ    NEWTQ01                                                          
         MHI   R1,18                                                            
*                                                                               
         CLI   PAGE,C'X'                                                        
         JE    NEWTQ00                                                          
         SR    R1,R1                                                            
         ICM   R1,1,MYPAGE         PAGE ADJUSTMENTS                             
         MHI   R1,36                                                            
*                                                                               
NEWTQ00  SR    R4,R1                                                            
         JNP   NEWDTIQZ                                                         
         MH    R1,TABQUEW                                                       
         AR    R2,R1                                                            
*                                                                               
NEWTQ01  MVC   RUNRNAME,=C'Empty   '                                            
         CLC   TQLOCKID,=F'0'                                                   
         JE    NEWTQ99                                                          
         MVC   RUNRNAME+0(2),TQLOCKID                                           
         MVC   RUNRNAME+2(6),=C'(....)'                                         
         MVC   HALF,TQLFASID                                                    
         SAC   0                                                                
         GOTO1 VHEXOUT,DMCB,HALF,RUNRNAME+3,2                                   
         SAC   512                                                              
*                                                                               
         LA    R1,TYPETAB                                                       
         MVC   RUNRTYPE(1),TQTYPE                                               
NEWTQ02  CLC   0(1,R1),TQTYPE                                                   
         JE    NEWTQ02A                                                         
         LA    R1,13(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTQ02                                                          
NEWTQ02A MVC   RUNRTYPE+1(12),1(R1)                                             
*                                                                               
         MVC   RUNRSTAT,=C'n/a '                                                
         ICM   R8,15,TQASVR                                                     
         JZ    NEWTQ04B                                                         
*                                                                               
         USING TABSSVRD,R8                                                      
         LA    R1,STATTAB                                                       
         MVC   RUNRSTAT(1),TSFLAG                                               
NEWTQ03  CLC   0(1,R1),TSFLAG                                                   
         JE    NEWTQ03A                                                         
         LA    R1,10(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTQ03                                                          
*                                                                               
NEWTQ03A MVC   RUNRSTAT(4),2(R1)                                                
*                                                                               
NEWTQ04B L     R1,TIMENOW          GET ELAPSED TIME (SINCE ARRIVAL)             
         S     R1,TQATIME                                                       
         ST    R1,FULL                                                          
         BRAS  RE,TIMEOUT                                                       
         BRAS  RE,T5CHR                                                         
         MVC   RUNRTIME,WORK1                                                   
*                                                                               
NEWTQ05  CLI   PAGE,C'X'                                                        
         JNE   NEWTQ99             NO EXTRA INFO                                
         EJECT                                                                  
***********************************************************************         
* EXTRA INFO                                                          *         
***********************************************************************         
NEWTQ10  EQU   *                                                                
         XC    MYWORK,MYWORK                                                    
         MVC   GIUSER,TQWUSRID                                                  
         SAC   0                                                                
         BRAS  RE,GETUSER                                                       
         LAM   ARE,AR1,ARZERO                                                   
         SAC   512                                                              
         MVC   MYWORK(10),GIUSERID                                              
         LA    R1,10                                                            
         LA    RF,MYWORK+9                                                      
NEWTQ10A CLI   0(RF),C' '                                                       
         JH    NEWTQ10B                                                         
         SHI   RF,1                                                             
         JCT   R1,NEWTQ10A                                                      
         J     NEWTQ99                                                          
*                                                                               
NEWTQ10B MVI   1(RF),C','                                                       
         MVC   2(4,RF),TQWKEYID+2                                               
         MVC   8(1,RF),TQWKEYID+7     UUUUAAAA16C                               
         EDIT  (B1,TQWKEY+6),(2,6(RF))                                          
         MVI   9(RF),C','                                                       
         EDIT  (B2,TQWKEYSQ),(6,10(RF)),FILL=0                                  
         MVC   RUNRWRKF,MYWORK                                                  
*                                                                               
         LA    R1,STATTAB2                                                      
NEWTQ11  CLC   0(1,R1),TQSTATUS                                                 
         JE    NEWTQ11A                                                         
         LA    R1,10(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTQ11                                                          
NEWTQ11A MVC   RUNRSTA2,2(R1)                                                   
*                                                                               
         LA    R1,ERRORTAB                                                      
NEWTQ12  CLC   0(1,R1),TQERROR                                                  
         JE    NEWTQ12A                                                         
         LA    R1,10(R1)                                                        
         CLI   0(R1),C' '          LAST ONE SO UNKNOWN                          
         JNE   NEWTQ12                                                          
NEWTQ12A MVC   RUNRERR,0(R1)                                                    
         EJECT                                                                  
***********************************************************************         
* NEXT LINE                                                           *         
***********************************************************************         
NEWTQ99  AH    R2,TABQUEW          NEXT SERVER                                  
         LA    R3,RUNLENQ(R3)                                                   
         LA    R1,SRVPFKH                                                       
         CR    R3,R1                                                            
         JL    NEWTQ99A                                                         
*                                                                               
         CLI   PAGE,2              HAVE WE DOUBLED UP                           
         JE    NEWDTIQX                                                         
         CLI   PAGE,C'X'           DO WE HAVE EXTRA INFO                        
         JE    NEWDTIQX                                                         
         MVI   PAGE,2              DOUBLE UP NOW                                
         LA    R3,SRVL1H                                                        
         LA    R3,40(R3)           DO SECOND LINE                               
*                                                                               
NEWTQ99A JCT   R4,NEWTQ01                                                       
*                                                                               
NEWDTIQZ MVC   RUNLINE,=C'------------ END OF DATA ------------ '               
*                                                                               
NEWDTIQX SAC   0                                                                
         XIT1  ,                                                                
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* FILTER ON TASK VALUES                                               *         
***********************************************************************         
FILTER   NTR1                                                                   
         USING TCBD,R2                                                          
         USING FACALETD,R6                                                      
*                                                                               
FILT010  OC    FILTADV,FILTADV                                                  
         JZ    FILT020                                                          
*                                                                               
         CLC   FACALFAC(5),FILTADV   COMPARE FULL LEN                           
         JE    FILT020                                                          
         CLI   FILTADV+4,C'*'                                                   
         JNE   EXITNEQ                                                          
         CLC   FACALFAC(4),FILTADV   COMPARE 4 CHR + *                          
         JNE   EXITNEQ                                                          
*                                                                               
         DROP  R6                                                               
*                                                                               
FILT020  OC    FILTSE,FILTSE                                                    
         JZ    FILT030                                                          
         CLC   TCBSEN,FILTSE                                                    
         JNE   EXITNEQ                                                          
*                                                                               
FILT030  OC    FILTSYS,FILTSYS                                                  
         JZ    FILT040                                                          
         CLC   TCBOVSYS,FILTSYS                                                 
         JNE   EXITNEQ                                                          
*                                                                               
FILT040  OC    FILTPG,FILTPG                                                    
         JZ    FILT050                                                          
         CLC   TCBPRG,FILTPG                                                    
         JNE   EXITNEQ                                                          
*                                                                               
FILT050  CLI   FILTOTH,C'L'        LONG COUNT > 10 SECONDS                      
         JNE   FILT060                                                          
         L     R1,TIMENOW                                                       
         S     R1,TCBSTTM                                                       
         C     R1,=F'384000'       384000 = 10 SEC                              
         JL    FILTERN                                                          
*                                                                               
FILT060  CLI   FILTOTH,C'I'        IO COUNT > 2000                              
         JNE   FILT070                                                          
         CLC   TCBIOCNT,=X'0007D0' DEC 2000                                     
         JL    FILTERN                                                          
*                                                                               
FILT070  CLI   FILTOTH,C'W'        WAIT COUNT IF TCBSVECB IS SET                
         JNE   FILT080                                                          
         OC    TCBSVECB,TCBSVECB                                                
         JZ    FILTERN                                                          
*                                                                               
FILT080  CLI   FILTOTH,C'O'        LOCK                                         
         JNE   FILT090                                                          
         CLI   TCBSVECB,X'FE'      COUNT IF WE ARE WAITING ON DSPACE            
         JNE   FILTERN                                                          
*                                                                               
FILT090  CLI   FILTOTH,C'T'        TIMER                                        
         JNE   FILT100                                                          
         OC    TCBTIMEW,TCBTIMEW   COUNT IF WE HAVE WAKEUP TIME                 
         JZ    FILTERN                                                          
*                                                                               
FILT100  EQU   *                                                                
*                                                                               
FILTERY  J     EXITEQU                                                          
*                                                                               
FILTERN  J     EXITNEQ                                                          
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE PARMS                                                      *         
***********************************************************************         
VALIDATE NTR1                                                                   
         LA    R7,SRVP1H                                                        
         CLI   8(R7),C'?'                                                       
         JE    VAL000                                                           
         LA    R7,SRVP2H                                                        
         CLI   8(R7),C'?'                                                       
         JE    VAL000                                                           
         LA    R7,SRVP3H                                                        
         CLI   8(R7),C'?'                                                       
         JE    VAL000                                                           
         JNE   VAL010                                                           
*                                                                               
VAL000   ST    R7,AHELP                                                         
*                                                                               
VAL010   LA    R7,SRVP1H                                                        
         ST    R7,CURSOR                                                        
         SR    R0,R0                                                            
         ICM   R0,1,5(R7)                                                       
         JZ    VALP2                                                            
*                                                                               
         LA    R1,8(R7)            START OF INPUT AREA                          
VAL014   CHI   R0,1                                                             
         JNH   VALP2                                                            
         MVC   FILTWORK,SPACES                                                  
         LA    RF,FILTWORK         WORK AREA FOR FILTERS                        
VAL015   CLI   0(R1),C','                                                       
         JE    VAL020                                                           
         CLI   0(R1),C' '          EXTRACT WORD FROM FILTER                     
         JNH   VAL020                                                           
         MVC   0(1,RF),0(R1)                                                    
         AHI   R1,1                                                             
         AHI   RF,1                                                             
         JCT   R0,VAL015                                                        
*                                                                               
VAL020   AHI   R1,1                JUMP , OR SPACE                              
         SHI   R0,1                                                             
*                                                                               
         L     R2,=A(FILTAB)                                                    
         A     R2,RELO                                                          
VAL021   CLC   0(7,R2),FILTWORK                                                 
         JE    VALOTHER                                                         
         AHI   R2,8                                                             
         CLI   7(R2),C'X'                                                       
         JNE   VAL021                                                           
*                                                                               
         L     R2,AFID             CHECK AGAINST ADVS                           
VAL022   CLC   0(3,R2),FILTWORK                                                 
         JE    VALADV                                                           
         LA    R2,8(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         JNE   VAL022                                                           
*                                                                               
         L     R5,ASELIST          MUST HAVE SYSFACS                            
         BRAS  RE,SETBXLE                                                       
         USING SELISTD,R5                                                       
*                                                                               
VALSYS0  CLC   SENAME(3),FILTWORK  IF 3 CHR MATCH SAVE SEOV                     
         JNE   *+16                                                             
         MVC   FILTSYS,SEOVSYS                                                  
         MVC   APGMS,SEPGMS        AND A PGMS LIST                              
*                                                                               
         CLC   SENAME,FILTWORK     SE NAME MATCH                                
         JE    VALSESYS                                                         
*                                                                               
         JXLE  R5,R6,VALSYS0       NEXT                                         
*                                                                               
         L     R5,APGMS                                                         
         L     R6,2(R5)                                                         
         LA    R5,6(R5)                                                         
VAL030   CLC   0(7,R5),FILTWORK                                                 
         JE    VALPGM                                                           
         LA    R5,48(R5)                                                        
         CR    R5,R6                                                            
         JL    VAL030                                                           
         J     VAL014                                                           
*                                                                               
VALSESYS MVC   FILTSE,SESYS        SET SE FILTER                                
         J     VAL014                                                           
*                                                                               
VALPGM   MVC   FILTPG,8(R5)        SET PGM FILTER                               
         J     VAL014                                                           
*                                                                               
VALADV   MVC   FILTADV,FILTWORK    SET ADV FILTER                               
         J     VAL014                                                           
*                                                                               
VALOTHER MVC   FILTOTH,7(R2)       SET OTHER FILTER                             
         J     VAL014                                                           
*                                                                               
VALP2    EQU   *                                                                
         MVI   PAGE,C'X'           SHORT PAGE IF REQUESTED                      
VAL210   LA    R7,SRVP2H                                                        
         SR    R0,R0                                                            
         ICM   R0,1,5(R7)                                                       
         JZ    VALP3                                                            
*                                                                               
         CLC   8(2,R7),=C'SH'                                                   
         JNE   *+8                                                              
         MVI   PAGE,1              PAGE 1 OF 2                                  
         EJECT                                                                  
***********************************************************************         
* VALIDATE PARAMETER 3                                                *         
***********************************************************************         
VALP3    MVI   KILLTASK,0          DON'T KILL ANYTHING                          
         MVC   KILLADV,SPACES      SERIOUSLY                                    
*                                                                               
         LA    R7,SRVP3H           PARAMETER 3                                  
         XR    R0,R0                                                            
         ICM   R0,1,5(R7)          ANYTHING IN PARAMETER 3                      
         JZ    VALP4               NO                                           
*                                                                               
         CLC   8(5,R7),=C'KILL,'   TO KILL IS THE ONLY OPTION, FOR NOW          
         JNE   VALP3E                                                           
         LA    R7,SRVP3+5                                                       
*                                                                               
         CHI   R0,10               MUST BE LENGTH 10                            
         JNE   VALP3A                                                           
         MVC   KILLADV(3),0(R7)                                                 
         LA    R7,3(,R7)                                                        
         J     VALP3C                                                           
VALP3A   CHI   R0,11               OR 11                                        
         JNE   VALP3B                                                           
         MVC   KILLADV(4),0(R7)                                                 
         LA    R7,4(,R7)                                                        
         J     VALP3C                                                           
VALP3B   CHI   R0,12               OR 12                                        
         JNE   VALP3E                                                           
         MVC   KILLADV(5),0(R7)                                                 
         LA    R7,5(,R7)                                                        
VALP3C   CLI   0(R7),C','          MAKE SURE TASK IS NEXT                       
         JNE   VALP3E                                                           
         CLI   2(R7),C' '          MAKE SURE SINGLE CHARACTER TASK              
         JH    VALP3E                                                           
         MVC   KILLTASK,1(R7)                                                   
         MVC   SRVP3(7),SRVP3+5                                                 
         MVC   SRVP3+7(7),=C' I KILL'                                           
         J     VALP3X                                                           
*                                                                               
VALP3E   MVC   SRVP3,=CL16'NO UNDERSTAND'                                       
VALP3X   OI    SRVP3H+6,X'80'      TRANSMIT                                     
         EJECT                                                                  
***********************************************************************         
* VALIDATE PARAMETER 4                                                *         
***********************************************************************         
VALP4    CLI   DONESW,C'Y'         HAVE WE DONE PAGE/FILTER SWAP                
         JE    VALXXX                                                           
         CLI   CURI,0              NO SIGN OF ENTERED DATA                      
         JE    VALP4001                                                         
         XC    SRVP4,SRVP4                                                      
         J     VALXXX                                                           
*                                                                               
VALP4001 CLI   PFKEY,7             7 AND 8 CLEAR FILTER                         
         JNE   *+10                                                             
         XC    SRVP1,SRVP1                                                      
         CLI   PFKEY,8                                                          
         JNE   *+10                                                             
         XC    SRVP1,SRVP1                                                      
*                                                                               
         SR    R3,R3               GET NUMBER IN P4                             
         ICM   R3,1,SRVP4H+5                                                    
         LA    R4,SRVP4                                                         
         BRAS  RE,VALNUM                                                        
         CLI   DUB,X'FF'           TEST INVALID                                 
         JNE   VALP400                                                          
         XR    R1,R1                                                            
         J     *+8                                                              
VALP400  CVB   R1,DUB                                                           
*                                                                               
         CLI   PFKEY,7                                                          
         JNE   VALP401                                                          
         SHI   R1,1                                                             
VALP401  CLI   PFKEY,8                                                          
         JNE   VALP402                                                          
         AHI   R1,1                                                             
VALP402  CL    R1,=F'50'           MAX 50 THEN SET TO ZERO                      
         JL    *+6                                                              
         XR    R1,R1                                                            
*                                                                               
         XC    SRVP4,SRVP4                                                      
         LTR   RF,R1                                                            
         JZ    VALXXX                                                           
*                                                                               
         STC   RF,MYPAGE                                                        
         EDIT  (RF),(2,SRVP4),FILL=0                                            
         MVI   SRVP4H+5,2                                                       
*                                                                               
         CLI   MODE,C'T'           FOR MODE=TASK                                
         JNE   VALXXX                                                           
         L     R6,AFACALET                                                      
         SLL   R1,5                R1=R1*32                                     
         AR    R6,R1                                                            
         OC    0(8,R6),0(R6)                                                    
         JZ    VALP405                                                          
         MVC   SRVP1(8),0(R6)                                                   
         MVI   DONESW,C'Y'         HAVE WE DONE PAGE/FILTER SWAP                
         MVI   SRVP1H+5,8                                                       
         J     VAL010                                                           
*                                                                               
VALP405  MVI   MYPAGE,0            RESET TO ZERO                                
         J     VAL010                                                           
*                                                                               
VALXXX   EQU   *                                                                
*                                                                               
VALIDATX J     EXITEQU                                                          
*                                                                               
FILTAB   DC    CL7'LONG    ',C'L'  OTHER FILTERS                                
         DC    CL7'SHORT   ',C'S'                                               
         DC    CL7'IO      ',C'I'                                               
         DC    CL7'WAIT    ',C'W'                                               
         DC    CL7'LOCK    ',C'O'                                               
         DC    CL7'TIMER   ',C'T'                                               
         DC    CL7'XXXXXXXX',C'X'                                               
         EJECT                                                                  
VALNUM   NTR1                                                                   
         LR    R5,R3              SAVE LENGTH OF FIELD                          
         LR    R6,R4              SAVE ADDRESS OF FIELD                         
*                                                                               
VALN2    TM    0(R4),X'F0'        NUMERIC ?                                     
         JO    VALN4              VALID                                         
         CLI   0(R4),X'40'        SPACE ?                                       
         JNE   VALN6              INVALID ZONE                                  
         OI    0(R4),X'F0'        SET TO VALID ZONE                             
*                                                                               
VALN4    LA    R4,1(R4)           BUMP TO NEXT BYTE                             
         JCT   R3,VALN2           LOOP TO LENGTH                                
         BCTR  R5,R0              LENGTH-1 FOR EXECUTE                          
         JAS   R1,EXECIT                                                        
         PACK  DUB,0(0,R6)        ZONED TO PACKED                               
EXECIT   EX    R5,0(R1)           CONVERT TO PACKED (LENGTH MODIFIED)           
         J     VALXIT             VALID EXIT                                    
*                                                                               
VALN6    MVI   DUB,X'FF'          SET ON ERROR CONDITION                        
*                                                                               
VALXIT   XIT1                                                                   
                                                                                
**********************************************************************          
* EDIT TUS IN FULL TO WORK HH:MM:SS.SS                               *          
**********************************************************************          
TIMEOUT  NTR1                                                                   
         MVC   WORK1(11),=C'00:00:00.00'                                        
         SR    RE,RE                                                            
         L     RF,FULL                                                          
         D     RE,TUHOUR                                                        
         EDIT  (RF),(2,WORK1),FILL=0      HRS                                   
         LR    RF,RE                                                            
         SR    RE,RE                                                            
         D     RE,TUMINUTE                                                      
         EDIT  (RF),(2,WORK1+3),FILL=0    MINS                                  
         LR    RF,RE                                                            
         SR    RE,RE                                                            
         D     RE,TUSECOND                                                      
         EDIT  (RF),(2,WORK1+6),FILL=0    SECS                                  
         LR    RF,RE                                                            
         SR    RE,RE                                                            
         D     RE,TUMSEC                                                        
         EDIT  (RF),(2,WORK1+9),FILL=0    100/SEC                               
         J     XIT1                                                             
*                                                                               
T5CHR    NTR1                                                                   
         CLC   WORK1(5),=C'00:00'         ZERO HH:MM                            
         JNE   *+14                                                             
         MVC   WORK1(5),WORK1+6           SET SS.00                             
         J     T5CHRX                                                           
*                                                                               
         CLC   WORK1(2),=C'00'            ZERO HH                               
         JNE   *+14                                                             
         MVC   WORK1(5),WORK1+3           SET MM:SS                             
         J     T5CHRX                                                           
*                                                                               
         MVC   WORK1(5),WORK1+1           SET H:MMH                             
         MVI   WORK1+4,C'H'                                                     
*                                                                               
T5CHRX   CLI   WORK1,C'0'                 REMOVE 1 LEADING ZERO                 
         JNE   *+8                                                              
         MVI   WORK1,C' '                                                       
         J     XIT1                                                             
         EJECT                                                                  
***********************************************************************         
* CALL GETHELP AND EXIT TO MONITOR                                    *         
***********************************************************************         
HELPOUT  L     R1,AHELP                                                         
         OI    6(R1),X'40'         SET CURSOR                                   
         LR    R0,R1                                                            
         MVC   HELPKEY,HELPID                                                   
*                                                                               
         JAS   RE,HELP000          GET A(HELPTAB) IN RE                         
HELPTAB  LA    R1,SRVP1H           POSSIBLE HELP FIELDS                         
         LA    R1,SRVP2H                                                        
         LA    R1,SRVP3H                                                        
         LA    R1,SRVP4H                                                        
         DC    H'0'                                                             
*                                                                               
HELP000  SR    RF,RF                                                            
         LA    RF,1                                                             
HELP010  EX    0,0(RE)             BY TESTING AHELP                             
         CR    R1,R0                                                            
         JE    HELP020                                                          
         LA    RE,4(RE)                                                         
         CLI   0(RE),0                                                          
         JE    HELP020                                                          
         LA    RF,1(RF)                                                         
         J     HELP010                                                          
*                                                                               
HELP020  STC   RF,HELPNUM                                                       
         XC    DMCB(24),DMCB                                                    
         GOTO1 AGETHELP,DMCB,(X'50',HELPKEY),0,(C'B',0)                         
         DC    H'0'                                                             
                                                                                
***********************************************************************         
* GET GTSYS & GTSYSN FROM GTSYSSE                                     *         
***********************************************************************         
GETSYS   NTR1                                                                   
         L     R5,ASELIST          MUST HAVE SYSFACS                            
         BRAS  RE,SETBXLE                                                       
         USING SELISTD,R5                                                       
GETSYS0  CLI   GTSYSSE,0           IF SYSSE IS ZERO                             
         JNE   GETSYS1                                                          
         CLC   GTSYS,SEOVSYS       TEST GTSYS WITH OVSYS                        
         JE    GETSYS2                                                          
GETSYS1  CLC   GTSYSSE,SESYS       TEST SE NUMBER                               
         JE    GETSYS2                                                          
         JXLE  R5,R6,GETSYS0       NEXT                                         
         MVI   GTSYS,0                                                          
         XC    GTSYSN,GTSYSN                                                    
         J     EXITNEQ             ERROR EXIT NOT FOUND                         
*                                                                               
GETSYS2  MVC   GTSYS,SEOVSYS       FOUND                                        
         MVC   GTSYSN,SENAME       SET NAME                                     
         MVC   GTAPGMS,SEPGMS      SAVE A(PGMS)                                 
         J     EXITEQU                                                          
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* GET GTPROGN FROM GTPROG                                             *         
***********************************************************************         
GETPROG  NTR1                                                                   
         ICM   R5,15,GTAPGMS       MUST HAVE A(PRGMS)                           
         JZ    GETPRG2                                                          
         BRAS  RE,SETBXLE          SET BXLE                                     
         USING PGMLSTD,R5                                                       
GETPRG0  CLC   GTPROG,PGMNUM       TEST PROG NUMBER                             
         JNE   GETPRG0A                                                         
         CLI   PGMCTRY,0                                                        
         JE    GETPRG1                                                          
         CLC   PGMCTRY,MYTCTRY     TEST PROG COUNTRY                            
         JE    GETPRG1                                                          
GETPRG0A JXLE  R5,R6,GETPRG0       NEXT                                         
GETPRG2  XC    GTPROGN,GTPROGN     NOT FOUND SO HEXOUT NUMBER                   
         OC    GTPROG,GTPROG                                                    
         JZ    EXITEQU                                                          
         GOTO1 VHEXOUT,DMCB,GTPROG,GTPROGN,1                                    
         J     EXITEQU                                                          
GETPRG1  MVC   GTPROGN(7),PGMNAME  SET NAME                                     
         J     EXITEQU                                                          
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* GET SYSTEM NAME FROM GTSYS                                          *         
***********************************************************************         
GETOVS   NTR1  LABEL=*                                                          
         L     RE,ASYSLST          RE=SYSLST                                    
         LA    RE,6(RE)                                                         
         USING SYSLSTD,RE                                                       
*                                                                               
GETS010  CLI   SYSLNUM,0           TEST FOR EOT                                 
         JE    GETS090                                                          
         CLC   GTSYS,SYSLNUM       TEST SYSTEM NUMBER                           
         JE    GETS990                                                          
         LA    RE,SYSLLEN(RE)      NEXT                                         
         J     GETS010                                                          
*                                                                               
GETS090  J     EXITNEQ             SET CC NEQ NOT FOUND                         
*                                                                               
GETS990  MVC   GTSYSN,SYSLNAME                                                  
         LR    R1,RE               R1=A(SYSLST ENTRY)                           
         J     EXITEQU             SET CC EQU                                   
         DROP  RE                                                               
         EJECT                                                                  
***********************************************************************         
* GET USERID FROM 2 CHR ID NUMBER                                     *         
***********************************************************************         
GETUSER  NTR1                                                                   
         CLC   GIUSER,GIUPREV                                                   
         JE    GETUSRX                                                          
         MVC   GIUPREV,GIUSER                                                   
*                                                                               
         MVC   GIUSERID,SPACES     IGNORE ZERO ID                               
         OC    GIUSER,GIUSER                                                    
         JZ    GETUSR20                                                         
*                                                                               
         LA    R7,CTREC            BUILD ID RECORD KEY                          
         USING CTIREC,R7                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID+8(2),GIUSER                                               
         NI    CTIKID+8,X'7F'      UNSET GENERIC FLAG                           
         GOTO1 ADATAMGR,DMCB,DMREAD,CTFILE,(R7),(R7)                            
         CLI   8(R1),0                                                          
         JNE   GETUSR12            NNNNNN IF REC NOT FOUND                      
         LA    R7,CTIDATA                                                       
         SR    RE,RE                                                            
GETUSR10 AR    R7,RE                                                            
         CLI   0(R7),X'02'         TEST ID ELEMENT                              
         JNE   *+14                                                             
         MVC   GIUSERID,2(R7)      GET ID NAME                                  
         J     GETUSR20                                                         
*                                                                               
         IC    RE,1(R7)            BUMP TO NEXT ELEMENT                         
         CLI   0(R7),0                                                          
         JNE   GETUSR10            BUT DROP THROUGH IF LAST                     
*                                                                               
GETUSR12 EDIT  (B2,GIUSER),(10,GIUSERID),DUB=SDUB,WRK=SWORK1                    
*                                                                               
GETUSR20 LA    RF,0                                                             
         LA    RE,GIUSERID                                                      
GETUSR21 CLI   0(RE),X'40'                                                      
         JE    GETUSR30                                                         
         CLI   0(RE),0                                                          
         JE    GETUSR30                                                         
         LA    RF,1(RF)                                                         
         LA    RE,1(RE)                                                         
         CHI   RF,8                                                             
         JL    GETUSR21                                                         
GETUSR30 STC   RF,GIULEN                                                        
GETUSRX  J     XIT1                                                             
         EJECT                                                                  
***********************************************************************         
* GET PERSON FROM 2 CHR ID PERSON ID                                  *         
***********************************************************************         
GETPERS  NTR1                                                                   
         CLC   GIPERS,GIPPREV                                                   
         JE    GETPERX                                                          
         MVC   GIPPREV,GIPERS                                                   
         LA    R7,CTREC            BUILD ID RECORD KEY                          
         USING CT0REC,R7                                                        
         XC    CT0KEY,CT0KEY                                                    
         MVI   CT0KTYP,CT0KTEQU                                                 
         MVC   CT0KAGY,GIPERS      2CHR AGY                                     
         MVC   CT0KNUM,GIPERS+2    PERSON NUM                                   
         GOTO1 ADATAMGR,DMCB,DMREAD,CTFILE,(R7),(R7)                            
         CLI   8(R1),0                                                          
         JNE   GETPER12            NNNNNN IF REC NOT FOUND                      
         LA    R7,CT0DATA                                                       
         SR    RE,RE                                                            
GETPER10 AR    R7,RE                                                            
         CLI   0(R7),SAPALELQ      TEST PERSON ELEMENT                          
         JNE   *+14                                                             
         MVC   GIPERSON,2(R7)      GET ID NAME                                  
         J     GETPER20                                                         
*                                                                               
         IC    RE,1(R7)            BUMP TO NEXT ELEMENT                         
         CLI   0(R7),0                                                          
         JNE   GETPER10            BUT DROP THROUGH IF LAST                     
*                                                                               
GETPER12 EDIT  (B2,GIPERS+2),(8,GIPERSON),DUB=SDUB,WRK=SWORK1                   
*                                                                               
GETPER20 LA    RF,0                                                             
         LA    RE,GIPERSON                                                      
GETPER21 CLI   0(RE),X'40'                                                      
         JE    GETPER30                                                         
         CLI   0(RE),0                                                          
         JE    GETPER30                                                         
         LA    RF,1(RF)                                                         
         LA    RE,1(RE)                                                         
         CHI   RF,8                                                             
         JL    GETPER21                                                         
GETPER30 STC   RF,GIPLEN                                                        
GETPERX  J     XIT1                                                             
         EJECT                                                                  
***********************************************************************         
* PC PROGRAM CONNECT DATA                                             *         
***********************************************************************         
GETPCVER NTR1                                                                   
         CLC   GIPCDATA,GIPCPREV                                                
         JE    PCVERX                                                           
         MVC   GIPCPREV,GIPCDATA                                                
         MVC   GIPCOUT,SPACES                                                   
*                                                                               
         MVC   GIPCOUT(5),=C'PMGC '                                             
         SR    R1,R1               UNSET FLAGS IF OFF                           
         IC    R1,GIPCDATA                                                      
         LA    RF,GIPCOUT                                                       
         SLL   R1,24                                                            
         LA    R0,4                                                             
PCV010   LTR   R1,R1                                                            
         JM    *+8                                                              
         MVI   0(RF),C'.'                                                       
         LA    RF,1(RF)                                                         
         SLL   R1,1                                                             
         JCT   R0,PCV010                                                        
*                                                                               
         LA    R2,GIPCOUT+5        OUTPUT PC PROGRAM INFO IF DEFINED            
         SR    R0,R0                                                            
         ICM   R0,3,GIPCDATA+1                                                  
         JZ    PCVER94                                                          
*                                                                               
         TM    GIPCDATA,X'80'      PC PROG                                      
         JZ    PCVER94                                                          
*                                                                               
         CVD   R0,DUB              OUTPUT PC APP PROGRAM NUMBER                 
         OI    DUB+7,X'0F'                                                      
         UNPK  0(5,R2),DUB                                                      
         L     RF,=V(FAXPTPC)      POINT TO PC PROGRAM TABLE                    
         A     RF,RELO                                                          
         USING FAXPTABD,RF                                                      
         LA    RF,L'FAXPNTRY(RF)   BUMP PAST FIRST ENTRY                        
*                                                                               
PCVER91  CLI   FAXPTYPE,0          SEARCH FOR NUMBER TO GET NAME                
         JE    PCVER94                                                          
         TM    FAXPTYPE,FAXPTPC                                                 
         JZ    PCVER92                                                          
         CLM   R0,3,FAXPNUM                                                     
         JE    PCVER93                                                          
PCVER92  LA    RF,L'FAXPNTRY(RF)                                                
         J     PCVER91                                                          
PCVER93  MVC   0(L'FAXPNAME,R2),FAXPNAME                                        
         DROP  RF                                                               
*                                                                               
PCVER94  OC    GIPCDATA+3(3),GIPCDATA+3 PROGRAM VERSION NUMBER                  
         JZ    XIT1                                                             
         MVI   GIPCOUT+16,C'V'                                                  
         LA    R2,GIPCOUT+17                                                    
         CLC   GIPCDATA+3(3),=X'FFFFFF'    INVALID VALUE                        
         JNE   *+14                                                             
         MVC   0(7,R2),=C'?.?.?.?'                                              
         J     XIT1                                                             
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,14,GIPCDATA+3    A.B.CC.DD IN LEFT 3 BYTES                    
*                                                                               
         SR    RE,RE               A                                            
         SLDL  RE,4                                                             
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(2,R2),DUB                                                      
         MVI   2(R2),C'.'                                                       
         LA    R2,3(R2)                                                         
*                                                                               
         SR    RE,RE               B                                            
         SLDL  RE,4                                                             
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(2,R2),DUB                                                      
         MVI   2(R2),C'.'                                                       
         LA    R2,3(R2)                                                         
*                                                                               
         SR    RE,RE               CC                                           
         SLDL  RE,8                                                             
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(3,R2),DUB                                                      
         MVI   3(R2),C'.'                                                       
         LA    R2,4(R2)                                                         
*                                                                               
         SR    RE,RE               DD                                           
         SLDL  RE,8                                                             
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(3,R2),DUB                                                      
*                                                                               
PCVERX   J     XIT1                                                             
         EJECT                                                                  
SETBXLE  LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         ORG   LITERALS                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WRKD     DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
DMCB     DS    6F                                                               
FACID    DS    H                                                                
FACMAX   DS    H                                                                
STRTTSK  DS    H                                                                
TSKMAX   DS    H                                                                
VHEXOUT  DS    A                                                                
AGETFACT DS    A                                                                
ASWITCH  DS    A                                                                
AGETHELP DS    A                                                                
AUTL     DS    A                                                                
ASELIST  DS    A                                                                
ADATAMGR DS    A                                                                
ASSB     DS    A                                                                
AFILTAB  DS    A                                                                
TIMENOW  DS    F                                                                
DISPLOOP DS    F                                                                
*                                                                               
RELO     DS    A                                                                
AORALL   DS    A                                                                
ASYSLST  DS    A                                                                
AFACALET DS    A                                                                
TIMENTU  DS    A                   TIME NOW TUS USED FOR TASK CHK               
CURRTOR  DS    A                   A CURRENT TOR                                
CURRFAC  DS    A                   A CURRENT FAC                                
CURRFACN DS    CL8                 CURRENT FAC NAME                             
*                                                                               
AFID     DS    A                                                                
APGMS    DS    A                                                                
AHELP    DS    A                                                                
*                                                                               
LOCORE   DS    F                                                                
HICORE   DS    F                                                                
*                                                                               
MYTCTRY  DS    X                                                                
PAGE     DS    X                                                                
MYPAGE   DS    X                                                                
*                                                                               
CURSOR   DS    A                                                                
CURADR   DS    H                                                                
PFKEY    DS    C                                                                
MODE     DS    C                                                                
DONESW   DS    C                                                                
CURI     DS    C                                                                
*                                                                               
GTSYS    DS    X                   OV SYSTEM                                    
GTSYSSE  DS    X                   SE SYSTEM                                    
GTSYSL   DS    X                   LEN FOR OVSYS NAME SEARCH                    
GTSYSN   DS    CL7                 SYS NAME                                     
GTPROGL  DS    X                   LEN FOR PROG NAME SEARCH                     
GTPROGN  DS    CL8                 PROG NAME                                    
GTPROG   DS    X                   PROG NUMBER                                  
GTLANGN  DS    CL3                 LANGUAGE NAME                                
GTLANG   DS    X                   LANGUAGE CODE                                
GTAPGMS  DS    A                   A(PRGMS LIST FOR SYSTEM)                     
*                                                                               
GIUSER   DS    CL2                 WORK AREA FOR GETUSER                        
GIUPREV  DS    CL2                                                              
GIULEN   DS    CL1                                                              
GIUSERID DS    CL10                                                             
*                                                                               
GIPERS   DS    CL4                 WORK AREA FOR GETPERS                        
GIPPREV  DS    CL4                                                              
GIPLEN   DS    CL1                                                              
GIPERSON DS    CL8                                                              
*                                                                               
GIPCDATA DS    CL6                                                              
GIPCPREV DS    CL6                                                              
GIPCOUT  DS    CL30                PMGC CL10 NAME  V00.00.000.000               
*                                                                               
FACASIDS DS    128XL4              ASID/FACID  XL2/XL2                          
*                                                                               
MYALET   DS    A                                                                
MYTBLET  DS    A                                                                
*                                                                               
TABSVRW  DS    H                   SERVER WIDTH                                 
TABQUEW  DS    H                   JOB WIDTH                                    
TABASVR  DS    A                   SERVER ADDR                                  
TABAQUE  DS    A                   JOB QUEUE ADDR                               
TABNSVR  DS    H                   MAX SERVERS                                  
TABNQUE  DS    H                   MAX JOBS                                     
*                                                                               
SYSNAME  DS    CL(L'SSBSYSNA)                                                   
WORK     DS    CL64                                                             
WORK1    DS    CL64                                                             
MYWORK   DS    CL64                                                             
*                                                                               
FILTWORK DS    CL8                                                              
FILTADV  DS    CL5                 ADV NAME                                     
FILTSE   DS    CL1                 SE SYS                                       
FILTSYS  DS    CL1                 SYSTEM                                       
FILTPG   DS    CL1                 PROG                                         
FILTOTH  DS    CL1                 OTHER CAN BE LONG/IO/WAIT                    
*                                                                               
SDUB     DS    D                                                                
SWORK    DS    CL64                                                             
SWORK1   DS    CL64                                                             
*                                                                               
KILLADV  DS    CL5                                                              
KILLTASK DS    C                                                                
*                                                                               
HELPKEY  DS    0CL10                                                            
         DS    XL3                                                              
HELPNUM  DS    XL1                                                              
HELPPAG  DS    XL1                                                              
         DS    XL5                                                              
*                                                                               
CTREC    DS    1024C                                                            
WRKL     EQU   *-WRKD              SHOULD BE LESS THAN 256                      
                                                                                
***********************************************************************         
* DSECT TO COVER SCREEN DISPLAY LINE                                  *         
***********************************************************************         
TSKLINED DSECT                                                                  
TSKHDR   DS    CL8                 FIELD HEADER                                 
TSKLINE  DS    0CL38                                                            
TSKLINEL DS    0CL78                                                            
TSKADV   DS    CL5                 ADV                                          
         DS    X                                                                
TSKTSK   DS    CL2                 TASK                                         
         DS    C                                                                
TSKSESYS DS    CL5                 SE SYS                                       
         DS    CL1                                                              
TSKPROG  DS    CL4                 PROG                                         
         DS    C                                                                
TSKIOC   DS    CL5                 IO COUNT                                     
         DS    C                                                                
TSKCPU   DS    CL5                 CPU COUNT                                    
         DS    C                                                                
TSKTIME  DS    CL5                 TIME IN TASK                                 
         DS    C                                                                
         DS    CL1                                                              
TSKXTRA  DS    0CL40                                                            
*                                                                               
         ORG   TSKXTRA                                                          
TSKCPUPC DS    CL3                 CPUUSED/ELAPSED PERCENTAGE                   
         DS    C                                                                
TSKLUID  DS    CL8                 LUID                                         
         DS    C                                                                
TSKLIOC  DS    CL5                 LOGICAL IO                                   
         DS    C                                                                
TSKWAIT  DS    CL8                 WAITING TYPE                                 
         DS    C                                                                
TSKRESRC DS    CL8                 WAITING FOR                                  
         DS    C                                                                
TSKWTM   DS    CL2                 WAITI TIME                                   
         ORG                                                                    
*                                                                               
         ORG   TSKLINEL+L'TSKLINEL                                              
TSKLENQ  EQU   *-TSKLINED          SHOULD BE 78 BYTES                           
*                                                                               
RUNLINED DSECT                     DEFINE SCREEN LINE                           
RUNHDR   DS    CL8                 FIELD HEADER                                 
RUNLINE  DS    0CL38                                                            
RUNLINEL DS    0CL78                                                            
RUNRNAME DS    CL8                 RUNNER                                       
         DS    C                                                                
RUNRTYPE DS    CL13                TYPE                                         
         DS    CL1                                                              
RUNRSTAT DS    CL4                 STAT - WAITING - ACTIVE / ADV1A/#1           
         DS    C                                                                
RUNRTIME DS    CL5                 TIME                                         
         DS    C                                                                
RUNRXTRA DS    0CL39                                                            
*                                                                               
         ORG   RUNRXTRA                                                         
RUNRWRKF DS    CL22                WRKF FILE                                    
         DS    C                                                                
RUNRSTA2 DS    CL9                 STATUS                                       
         DS    C                                                                
RUNRERR  DS    CL10                ERROR                                        
         ORG                                                                    
*                                                                               
         ORG   RUNLINEL+L'RUNLINEL                                              
RUNLENQ  EQU   *-RUNLINED          SHOULD BE 78 BYTES                           
         EJECT                                                                  
***********************************************************************         
* SCREEN DSECT                                                        *         
***********************************************************************         
SRDTIFFD DSECT                                                                  
         DS    CL64                                                             
* SRDTIFFD                                                                      
       ++INCLUDE SRDTIFFD                                                       
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* CTGENFILE                                                                     
       ++INCLUDE CTGENFILE                                                      
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
* DDFH                                                                          
       ++INCLUDE DDFH                                                           
* DDTRACED                                                                      
       ++INCLUDE DDTRACED                                                       
* DMDSHDR                                                                       
       ++INCLUDE DMDSHDR                                                        
* DMDTFPHD                                                                      
       ++INCLUDE DMDTFPH                                                        
* DMSPACED                                                                      
       ++INCLUDE DMSPACED                                                       
* FAADRRECA                                                                     
       ++INCLUDE FAADRREC                                                       
* FAFACTS                                                                       
       ++INCLUDE FAFACTS                                                        
* FAPGMLST                                                                      
       ++INCLUDE FAPGMLST                                                       
* FAPIGFACD                                                                     
       ++INCLUDE FAPIGFACD                                                      
* FASELIST                                                                      
       ++INCLUDE FASELIST                                                       
* FASRPARM                                                                      
       ++INCLUDE FASRPARM                                                       
* FASRS                                                                         
       ++INCLUDE FASRS                                                          
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
* FASYSFAC                                                                      
       ++INCLUDE FASYSFAC                                                       
* FASYSLSTD                                                                     
       ++INCLUDE FASYSLSTD                                                      
* FATABSD                                                                       
       ++INCLUDE FATABSD                                                        
* FATABSRUN                                                                     
       ++INCLUDE FATABSRUN                                                      
* FATCB                                                                         
       ++INCLUDE FATCB                                                          
* FATIOB                                                                        
       ++INCLUDE FATIOB                                                         
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* FAXPTABD                                                                      
       ++INCLUDE FAXPTABD                                                       
* SEACSFILE                                                                     
       ++INCLUDE SEACSFILE                                                      
*                                                                               
FACALETD DSECT                                                                  
FACALFAC DS    CL8                                                              
FACALTOK DS    CL8                                                              
FACALALE DS    CL4                                                              
FACALSSB DS    CL4                                                              
FACALTCB DS    CL4                                                              
FACALSTD DS    C                                                                
FACALAVL DS    C                                                                
         DS    CL2                                                              
FACALLEN EQU   *-FACALETD                                                       
*                                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007SRDTI00   03/02/18'                                      
         END                                                                    
