*          DATA SET MCDMDAS    AT LEVEL 002 AS OF 07/29/96                      
         MACRO                                                                  
&FILE    DMDA  &TYPEFLE=INPUT,&DEVICE=3350,&XTNTXIT=,                  X        
               &DSKXTNT=16,&BLKSIZE=0,                                 X        
               &RECSIZE=,&IOAREA1=,&IOAREA2=,&FILABL=,&KEYLEN=,        X        
               &EMU=NO,&DCB=YES,&KEYCMP=NO,&SPARE=,&CACHE=YES,         X        
               &XBUFFS=0,&BIG=NO,&VSAM=NO,&VERMONT=NO,&GLOBAL=NO                
.*                                                                              
         GBLC  &OFFLINE                                                         
         LCLC  &A,&C,&DCBNAME,&OFF                                              
         LCLA  &I,&J,&K,&L,&M,&XNUM                                             
.*                                                                              
&OFF     SETC  '&OFFLINE'.'XX'                                                  
&DCBNAME SETC  '&FILE.Z'                                                        
&C       SETC  'N'                                                              
&XNUM    SETA  &DSKXTNT                                                         
         AIF   (T'&KEYLEN EQ 'O' AND T'&IOAREA2 EQ 'O').DTF1                    
&XNUM    SETA  16                                                               
.*                                                                              
.******* START OF DTF GENERATION                                                
.*                                                                              
.DTF1    ANOP                                                                   
&FILE    DC    0D'0'                                                            
         DC    X'000080',AL1(&XNUM),X'0000FFFF'                                 
         DC    XL8'00',X'0000'                                                  
         DC    AL1(0)                                                           
.*                                                                              
&I       SETA  0                                                                
.DTF3    ANOP                                                                   
         AIF   (T'&VSAM EQ 'O').DTF3B                                           
         AIF   ('&VSAM' EQ 'NO').DTF3B                                          
         AIF   ('&VSAM'(1,1) EQ 'Y').DTF3A                                      
         MNOTE 0,'INVALID VSAM PARAMETER'                                       
         MEXIT                                                                  
.DTF3A   ANOP                                                                   
&I       SETA  &I+128              SET VSAM FILE X'80'                          
.DTF3B   ANOP                                                                   
         AIF   (T'&GLOBAL EQ 'O').DTF3D                                         
         AIF   ('&GLOBAL' EQ 'NO').DTF3D                                        
         AIF   ('&GLOBAL'(1,1) EQ 'Y').DTF3C                                    
         MNOTE 0,'INVALID GLOBAL PARAMETER'                                     
         MEXIT                                                                  
.DTF3C   ANOP                                                                   
&I       SETA  &I+64               SET GLOBAL RESOURCE X'40'                    
.DTF3D   ANOP                                                                   
         DC    AL1(&I)                                                          
.*                                                                              
&I       SETA  0                                                                
.DTF4    ANOP                                                                   
         AIF   (T'&EMU EQ 'O').DTF5                                             
         AIF   ('&EMU' EQ 'NO').DTF5                                            
         AIF   ('&EMU'(1,1) EQ 'Y').DTF4A                                       
         MNOTE 0,'INVALID EMU PARAMETER'                                        
         MEXIT                                                                  
.DTF4A   ANOP                                                                   
&I       SETA  &I+128              SET EMULATION MODE X'80'                     
.DTF5    ANOP                                                                   
         AIF   (T'&DCB EQ 'O').DTF6                                             
         AIF   ('&DCB'(1,1) EQ 'Y').DTF6                                        
         AIF   ('&DCB' EQ 'NO').DTF5A                                           
         MNOTE 0,'INVALID DCB PARAMETER'                                        
         MEXIT                                                                  
.DTF5A   ANOP                                                                   
&I       SETA  &I+64               SET NO DCB MODE X'40'                        
.DTF6    ANOP                                                                   
         AIF   (T'&KEYCMP EQ 'O').DTF10                                         
         AIF   ('&KEYCMP' EQ 'NO').DTF10                                        
         AIF   (T'&KEYLEN NE 'O').DTF6A                                         
         MNOTE 0,'MUST HAVE KEYLEN IF CMPRS SPECIFIED'                          
         MEXIT                                                                  
.DTF6A   ANOP                                                                   
         AIF   ('&KEYCMP' EQ 'LR').DTF7                                         
         AIF   ('&KEYCMP' EQ 'RL').DTF7                                         
         AIF   ('&KEYCMP' EQ 'L').DTF7                                          
         AIF   ('&KEYCMP' EQ 'R').DTF8                                          
         MNOTE 0,'INVALID CMPRS PARAMETER'                                      
         MEXIT                                                                  
.DTF7    ANOP                                                                   
&I       SETA  &I+8                COMPRESS FROM LEFT X'08'                     
&C       SETC  'Y'                                                              
.DTF8    ANOP                                                                   
         AIF   ('&KEYCMP' EQ 'LR').DTF9                                         
         AIF   ('&KEYCMP' EQ 'RL').DTF9                                         
         AIF   ('&KEYCMP' EQ 'R').DTF9                                          
         AGO   .DTF10                                                           
.DTF9    ANOP                                                                   
&I       SETA  &I+4                COMPRESS FROM RIGHT X'04'                    
&C       SETC  'Y'                                                              
.DTF10   ANOP                                                                   
         AIF   ('&CACHE' EQ 'NO').DTF10A                                        
         AIF   ('&CACHE' EQ 'YES').DTF11                                        
         MNOTE 0,'CACHE MUST BE YES OR NO'                                      
         MEXIT                                                                  
.DTF10A  ANOP                                                                   
&I       SETA  &I+2                NO CACHING X'02'                             
.*                                                                              
.DTF11   ANOP                                                                   
         AIF   ('&BIG' EQ 'NO').DTF12                                           
         AIF   ('&BIG' EQ 'YES').DTF11A                                         
         MNOTE 0,'BIG MUST BE YES OR NO'                                        
         MEXIT                                                                  
.DTF11A  ANOP                                                                   
&I       SETA  &I+1                BIG FILE (18 BIT TRACKS) X'01'               
.*                                                                              
.DTF12   ANOP                                                                   
         DC    AL1(&I)                                                          
.*                                                                              
.DTF13   ANOP                                                                   
         DC    AL1(&XBUFFS)                                                     
         DC    CL8'&FILE'                                                       
         DC    AL4(0),AL1(0),AL1(0)                                             
.*                                                                              
.DTF14   ANOP                                                                   
         AIF   (T'&DCB EQ 'O').DTF16                                            
         AIF   ('&DCB' NE 'NO').DTF16                                           
         DC    A(0)                                                             
         AIF   (T'&FILABL EQ 'O').DTF15                                         
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.DTF15   ANOP                                                                   
         MEXIT                                                                  
.DTF16   ANOP                                                                   
         DC    A(&DCBNAME)                                                      
         AIF   (T'&FILABL EQ 'O').DTF17                                         
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.DTF17   ANOP                                                                   
.*                                                                              
.* ******************************* END OF XL40 DTF GENERATION                   
.LAB1    ANOP                                                                   
*                                                                               
         DC    A(0)                LAST RECORD ADDRESS                          
.*                                                                              
.LAB2    ANOP                                                                   
&I       SETA  0                                                                
&J       SETA  0                                                                
&K       SETA  0                                                                
         AIF   (T'&RECSIZE EQ 'O').LAB3                                         
         AIF   ('&RECSIZE'(1,1) NE '(').LAB2A                                   
&I       SETA  &RECSIZE(1)                                                      
&I       SETA  (&I+127)/128                                                     
         AGO   .LAB3                                                            
.LAB2A   ANOP                                                                   
&J       SETA  &RECSIZE                                                         
&K       SETA  32768                                                            
.*                                                                              
.LAB3    ANOP                                                                   
&A       SETC  '0'                                                              
&L       SETA  &BLKSIZE                                                         
         AIF   ('&OFF'(1,1) EQ 'N').LAB3A                                       
         AIF   (&L EQ 0).LAB3A                                                  
&A       SETC  '&FILE.A'                                                        
         AIF   (T'&IOAREA1 EQ 'O').LAB3A                                        
&A       SETC  '&IOAREA1'                                                       
         AIF   ('&IOAREA1'(1,1) NE '(').LAB3A                                   
&A       SETC  '&IOAREA1(1)'                                                    
         AIF   ('&A' NE 'AUTO').LAB3A                                           
&A       SETC  '0'                                                              
.LAB3A   ANOP                                                                   
         DC    AL1(&I)             MAX REC LEN MOD 128 FOR V/L FILE             
         DC    AL3(&A)             A(1ST CORE BUFFER)                           
         DC    A(0)                D/A BLOCK IN 1ST CORE BUFFER                 
.*                                                                              
.LAB4    ANOP                                                                   
&M       SETA  &J                                                               
         AIF   (&L EQ 0).LAB4A                                                  
&M       SETA  &L                                                               
         AIF   (&J EQ 0).LAB4A                                                  
         MNOTE 0,'INVALID RECSIZE FIELD'                                        
&I       SETA  (&J+127)/128                                                     
&J       SETA  0                                                                
&K       SETA  0                                                                
         ORG   *-8                                                              
         DC    AL1(&I),AL3(&A),A(0)                                             
.LAB4A   ANOP                                                                   
         DC    AL2(&K+&M)          BLOCK SIZE OR FIXED RECORD SIZE              
.*                                                                              
&I       SETA  0                                                                
         AIF   (T'&IOAREA2 EQ 'O').LAB4B                                        
&I       SETA  2                                                                
.LAB4B   ANOP                                                                   
         AIF   (T'&KEYLEN EQ 'O').LAB4C                                         
         AIF   (N'&KEYLEN NE 2).LAB4C                                           
&I       SETA  3                                                                
.LAB4C   ANOP                                                                   
         AIF   ('&VERMONT' EQ 'NO').LAB4D                                       
&I       SETA  &I+64                                                            
.LAB4D   ANOP                                                                   
         DC    AL1(&I)             FLAGS                                        
.*                                                                              
.LAB5    ANOP                                                                   
&M       SETA  &XNUM                                                            
         DC    &M.XL14'00',X'00'   EXTENT MATRIX                                
.*                                                                              
         AIF   (T'&KEYLEN EQ 'O' AND T'&IOAREA2 EQ 'O').LAB6                    
&A       SETC  '0'                                                              
         AIF   ('&OFF'(1,1) EQ 'N').LAB5B                                       
         AIF   ('&C' EQ 'Y' AND T'&IOAREA2 EQ 'O').LAB5B                        
&A       SETC  '&FILE.B'                                                        
         AIF   (T'&IOAREA2 EQ 'O' OR '&IOAREA2' EQ 'YES').LAB5B                 
&A       SETC  '&IOAREA2'                                                       
         AIF   ('&IOAREA2'(1,1) NE '(').LAB5B                                   
&A       SETC  '&IOAREA2(1)'                                                    
         AIF   ('&A' NE 'AUTO').LAB5B                                           
&A       SETC  '0'                                                              
.LAB5B   ANOP                                                                   
*                                                                               
         DC    A(&A)               A(2ND CORE BUFFER)                           
         DC    A(0)                D/A BLOCK IN 2ND CORE BUFFER                 
.*                                                                              
         AIF   (T'&KEYLEN EQ 'O').LAB6                                          
         AIF   (N'&KEYLEN EQ 2).LAB5E                                           
         AIF   (N'&KEYLEN NE 1).LAB5ERR                                         
         AIF   (T'&KEYLEN NE 'N').LAB5ERR                                       
         AIF   ('&C' EQ 'Y').LAB5C                                              
         MNOTE 0,'MUST HAVE CMPRS IF KEYLEN SPECIFIED'                          
         MEXIT                                                                  
.LAB5C   ANOP                                                                   
&I       SETA  0                                                                
         AIF   (T'&SPARE EQ 'O').LAB5D                                          
&I       SETA  &SPARE                                                           
         AIF   (T'&SPARE EQ 'N').LAB5D                                          
         MNOTE 0,'INVALID SPARE PARAMETER'                                      
         MEXIT                                                                  
.LAB5D   ANOP                                                                   
*                                  KEY COMPRESSED FILES EXTENSION AREA          
         DC    AL2(&KEYLEN)        KEY LENGTH FOR COMPRESSION                   
         DC    AL2(&I)             SPARE IN BLOCK FOR COMPRESSION               
         AGO   .LAB6                                                            
.LAB5E   ANOP                                                                   
*                                  DANDDX FILES EXTENSION AREA                  
         DC    A(0)                A(CURRENT INDEX ENTRY)                       
         DC    A(0)                A(CURRENT RECORD IN DATA BLOCK)              
         DC    &M.A(0)             ONE DNEXT PER EXTENT MATRIX ENTRY            
         DC    &M.H'0'             ONE DCOUNT PER EXTENT MATRIX ENTRY           
         AIF   (T'&KEYLEN(1) EQ 'N' AND T'&KEYLEN(2) EQ 'N').LAB5F              
.LAB5ERR MNOTE 0,'INVALID KEYLEN FIELD'                                         
         MEXIT                                                                  
.LAB5F   ANOP                                                                   
         DC    H'&KEYLEN(1)'       MAJOR KEY LENGTH (LEFT)                      
         DC    H'&KEYLEN(2)'       MINOR KEY LENGTH (RIGHT)                     
         DC    AL1(0)              RECORD NUMBER IN BLOCK (WAS +34)             
         DC    AL1(0)              EXTENT MATRIX NUMBER   (WAS +35)             
         DC    XL2'00'             N/D                                          
.*                                                                              
.LAB6    ANOP                                                                   
         AIF   ('&OFF'(1,1) EQ 'N').LAB7                                        
         AIF   ('&BLKSIZE' EQ '0').LAB7                                         
         AIF   (T'&IOAREA1 NE 'O').LAB7                                         
&FILE.A  DS    &BLKSIZE.C          CORE BUFFER                                  
.*                                                                              
.LAB7    ANOP                                                                   
         AIF   ('&OFF'(1,1) EQ 'N').LAB8                                        
         AIF   (T'&KEYLEN EQ 'O' AND T'&IOAREA2 EQ 'O').LAB8                    
         AIF   ('&C' EQ 'Y' AND T'&IOAREA2 EQ 'O').LAB8                         
         AIF   (T'&IOAREA2 NE 'O' AND '&IOAREA2(1)' NE 'YES').LAB8              
&FILE.B  DS    &BLKSIZE.C          2ND CORE BUFFER OR INDEX                     
.*                                                                              
.LAB8    ANOP                                                                   
         AIF   ('&VSAM'(1,1) EQ 'Y').LAB9                                       
*                                                                               
&DCBNAME DCB   DDNAME=&FILE,DSORG=DA,MACRF=(RI)                                 
         AGO   .LAB10                                                           
.*                                                                              
.LAB9    ANOP                                                                   
*                                                                               
&DCBNAME ACB   AM=VSAM,DDNAME=&FILE,MACRF=(DIR,SEQ)                             
.*                                                                              
.LAB10   ANOP                                                                   
         AIF   (T'&FILABL EQ 'O').LABA                                          
         ORG   &DCBNAME+40                                                      
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.*                                                                              
.LABA    ANOP                                                                   
         MEND                                                                   
**PAN#1  CSECT                                                                  
         DC    CL21'002MCDMDAS   07/29/96'                                      
         END                                                                    
