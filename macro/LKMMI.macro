*          DATA SET MCLKMMI    AT LEVEL 006 AS OF 10/05/09                      
         MACRO                                                                  
&LABEL   LKMMI &MACTYPE,                                               +        
               &PARM1,                                                 +        
               &PARM2,                                                 +        
               &PARM3,                                                 +        
               &RUNNER=,                                               +        
               &SEND=,                                                 +        
               &RECEIVE=,                                              +        
               &UPDATIVE=,                                             +        
               &ALWAYSRESPOND=,                                        +        
               &WSSVR=                                                          
.*                                                                              
         GBLC  &SYSNAME                                                         
         GBLB  &MMI_HEADER_PRESENT                                              
         LCLA  &C                                                               
         LCLB  &H,&UN,&US,&UO,&E,&ETEXT                                         
         LCLC  &I1(8),&I2(8),&S                                                 
.*                                                                              
         AIF   (T'&MACTYPE EQ 'O').ERROR1                                       
&H       SETB  ('&MACTYPE'(1,1) EQ 'H')                                         
         AIF   (&H).LKMH                                                        
&E       SETB  ('&MACTYPE'(1,1) EQ 'E')                                         
         AIF   (&E).LKME                                                        
         AIF   (K'&MACTYPE GT 1).LKMI02                                         
&UN      SETB  ('&MACTYPE'(1,1) EQ 'U')                                         
         AGO   .LKMI04                                                          
.*                                                                              
.LKMI02  ANOP  ,                                                                
&UN      SETB  ('&MACTYPE'(1,2) EQ 'UN')                                        
&US      SETB  ('&MACTYPE'(1,2) EQ 'US')                                        
&UO      SETB  ('&MACTYPE'(1,2) EQ 'UO')                                        
         AIF   (NOT &UN AND NOT &US AND NOT &UO).ERROR2                         
.*                                                                              
.LKMI04  ANOP  ,                                                                
         AIF   (NOT &MMI_HEADER_PRESENT).ERROR3                                 
.*                                                                              
&C       SETA  0                                                                
.*                                                                              
.LKMI06  ANOP  ,                                                                
&C       SETA  &C+1                                                             
         AIF   (&C GT 8).LKMI08                                                 
&I1(&C)  SETC  '0'                                                              
         AGO   .LKMI06                                                          
.*                                                                              
.LKMI08  ANOP  ,                                                                
&C       SETA  0                                                                
.*                                                                              
.LKMI10  ANOP  ,                                                                
&C       SETA  &C+1                                                             
         AIF   (&C GT 8).LKMI12                                                 
&I2(&C)  SETC  '0'                                                              
         AGO   .LKMI10                                                          
.*                                                                              
.LKMI12  ANOP  ,                                                                
         AIF   (T'&PARM3 EQ 'O').ERROR7                                         
&ETEXT   SETB  ('&PARM3(1,1)' EQ '*')                                           
.*                                                                              
         AIF   (&UN).LKMUN                                                      
         AIF   (&US).LKMUS                                                      
         AIF   (&UO).LKMUO                                                      
.*                                                                              
.LKMD    ANOP  ,                                                                
         AIF   (T'&WSSVR NE 'O').ERROR4                                         
         AIF   (T'&RUNNER EQ 'O').LKMD01                                        
         AIF   ('&RUNNER'(1,1) NE 'Y').LKMD00                                   
&I1(3)   SETC  '1'                                                              
         AGO   .LKMD02                                                          
.*                                                                              
.LKMD00  ANOP  ,                                                                
         AIF   ('&RUNNER'(1,1) NE 'B').ERROR9                                   
&I1(1)   SETC  '1'                                                              
&I1(3)   SETC  '1'                                                              
         AGO   .LKMD02                                                          
.*                                                                              
.LKMD01  ANOP  ,                                                                
&I1(1)   SETC  '1'                                                              
.*                                                                              
.LKMD02  ANOP  ,                                                                
         AIF   (T'&RECEIVE EQ 'O').LKMD06                                       
         AIF   ('&RECEIVE'(1,1) NE 'Y').LKMD06                                  
&I2(3)   SETC  '1'                                                              
.*                                                                              
.LKMD04  ANOP  ,                                                                
         AIF   (T'&SEND EQ 'O').LKMD04                                          
         AIF   ('&SEND'(1,1) NE 'Y').LKMD04                                     
&I2(4)   SETC  '1'                                                              
.*                                                                              
.LKMD06  ANOP  ,                                                                
         AIF   (T'&ALWAYSRESPOND EQ 'O').LKMD08                                 
         AIF   ('&ALWAYSRESPOND'(1,1) NE 'Y').LKMD08                            
&I2(2)   SETC  '1'                                                              
.*                                                                              
.LKMD08  ANOP  ,                                                                
         AIF   (T'&UPDATIVE EQ 'O').LKMD10                                      
         AIF   ('&UPDATIVE'(1,1) NE 'Y').LKMD10                                 
&I2(5)   SETC  '1'                                                              
.*                                                                              
.LKMD10  ANOP  ,                                                                
         AGO   .LKMGEN                                                          
.*                                                                              
.LKMUN   ANOP  ,                                                                
&I1(8)   SETC  '1'                                                              
         AGO   .LKMU02                                                          
.*                                                                              
.LKMUO   ANOP  ,                                                                
&I1(3)   SETC  '1'                                                              
&I1(8)   SETC  '1'                                                              
&I2(5)   SETC  '1'                                                              
         AGO   .LKMU02                                                          
.*                                                                              
.LKMUS   ANOP  ,                                                                
&I1(7)   SETC  '1'                                                              
&I1(8)   SETC  '1'                                                              
.*                                                                              
.LKMU02  ANOP  ,                                                                
         AIF   (T'&RUNNER NE 'O' OR                                    +        
                T'&ALWAYSRESPOND NE 'O' OR                             +        
                T'&SEND NE 'O' OR                                      +        
                T'&RECEIVE NE 'O').ERROR4                                       
         AIF   (T'&WSSVR EQ 'O').LKMU04                                         
         AIF   ('&WSSVR'(1,1) NE 'Y').LKMU04                                    
&I1(6)   SETC  '1'                                                              
.*                                                                              
.LKMU04  ANOP  ,                                                                
         AIF   (T'&UPDATIVE EQ 'O').LKMU06                                      
         AIF   ('&UPDATIVE'(1,1) NE 'Y').LKMU06                                 
&I2(5)   SETC  '1'                                                              
.*                                                                              
.LKMU06  ANOP  ,                                                                
.*                                                                              
.LKMGEN  ANOP  ,                                                                
         AIF   (NOT &ETEXT).LKMGEN2                                             
&I2(8)   SETC  '1'                                                              
.*                                                                              
.LKMGEN2 ANOP  ,                                                                
         AIF   (T'&PARM1 EQ 'O').ERROR5                                         
         DC    AL2(&PARM1)                                                      
         AIF   (T'&PARM2 EQ 'O').ERROR6                                         
         DC    AL1(&PARM2)                                                      
.*                                                                              
&S       SETC  '&I1(1)'.'&I1(2)'.'&I1(3)'.'&I1(4)'.'&I1(5)'.'&I1(6)'.'&+        
               I1(7)'.'&I1(8)'                                                  
         DC    B'&S'                                                            
&S       SETC  '&I2(1)'.'&I2(2)'.'&I2(3)'.'&I2(4)'.'&I2(5)'.'&I2(6)'.'&+        
               I2(7)'.'&I2(8)'                                                  
         DC    B'&S'                                                            
.*                                                                              
         AIF   (N'&PARM3 NE 1).LKMGEN4                                          
         DC    AL1(&SYSNAME)                                                    
         DC    AL2(&PARM3)                                                      
         AGO   .LKMGEN8                                                         
.*                                                                              
.LKMGEN4 ANOP  ,                                                                
         AIF   (N'&PARM3 NE 2).ERROR9                                           
         AIF   (&ETEXT).LKMGEN6                                                 
         DC    AL1(&PARM3(1))                                                   
         DC    AL2(&PARM3(2))                                                   
         AGO   .LKMGEN8                                                         
.*                                                                              
.LKMGEN6 ANOP  ,                                                                
         DC    AL1(L'&PARM3(2))                                                 
         DC    AL2(&PARM3(2)-*)                                                 
.*                                                                              
.LKMGEN8 ANOP  ,                                                                
*                                                                               
         MEXIT                                                                  
.*                                                                              
.LKMH    ANOP  ,                                                                
         AIF   (&MMI_HEADER_PRESENT).ERROR3                                     
&MMI_HEADER_PRESENT                SETB  1                                      
         AIF   (T'&PARM1 EQ 'O').ERROR8                                         
&SYSNAME SETC  '&PARM1'                                                         
&LABEL   DS    0X                                                               
*                                                                               
         MEXIT                                                                  
.*                                                                              
.LKME    ANOP  ,                                                                
         AIF   (NOT &MMI_HEADER_PRESENT).ERROR3                                 
&MMI_HEADER_PRESENT                SETB  0                                      
         DC    AL2(0)                                                           
         MEXIT                                                                  
.*                                                                              
.ERROR1  ANOP  ,                                                                
         MNOTE 12,'Macro type missing'                                          
         MEXIT                                                                  
.*                                                                              
.ERROR2  ANOP  ,                                                                
         MNOTE 12,'Macro type invalid'                                          
         MEXIT                                                                  
.*                                                                              
.ERROR3  ANOP  ,                                                                
         MNOTE 12,'Invalid macro sequence'                                      
         MEXIT                                                                  
.*                                                                              
.ERROR4  ANOP  ,                                                                
         MNOTE 12,'Invalid keyword parameter'                                   
         MEXIT                                                                  
.*                                                                              
.ERROR5  ANOP  ,                                                                
         MNOTE 12,'Map code missing'                                            
         MEXIT                                                                  
.*                                                                              
.ERROR6  ANOP  ,                                                                
         MNOTE 12,'Overlay number missing'                                      
         MEXIT                                                                  
.*                                                                              
.ERROR7  ANOP  ,                                                                
         MNOTE 12,'Text number missing'                                         
         MEXIT                                                                  
.*                                                                              
.ERROR8  ANOP  ,                                                                
         MNOTE 12,'System missing'                                              
         MEXIT                                                                  
.*                                                                              
.ERROR9  ANOP  ,                                                                
         MNOTE 12,'Invalid parameter'                                           
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
