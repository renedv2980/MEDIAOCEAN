*          DATA SET MCDMISS    AT LEVEL 005 AS OF 05/08/03                      
         MACRO                                                                  
&FILE    DMIS  &TYPEFLE=INPUT,&DEVICE=3380,&XTNTXIT=,                  X        
               &DSKXTNT=9,&BLKSIZE=,                                   X        
               &KEYLEN=,&COMPRSS=,&RECSIZE=,&INDAREA=,&INDSIZE=,       X        
               &MASTIND=,&SPARE=,&IOAREA1=,&ADD=,&FILABL=,             X        
               &EMU=NO,&DCB=YES,&CMPRS=NO,&XBUFFS=0,&IXPDOV=,          X        
               &CACHE=YES,&BIG=NO,&VSAM=NO,&GLOBAL=NO,&BLKIND=NO                
.*                                                                              
         GBLC  &DDSCTRY                                                         
         AIF   ('&DDSCTRY' NE '').CTRYOK                                        
         MNOTE 8,'DDSCTRY SYMBOL NOT SET: USE REQUS MACRO'                      
.CTRYOK  ANOP                                                                   
.*                                                                              
         GBLC  &OFFLINE                                                         
         LCLA  &TISIZE,&CISIZE,&CIRT                                            
         LCLA  &A,&N,&DL,&DEV,&SPR,&REC,&KEY,&TRACKS,&PDRT,&TIRT                
         LCLC  &XIT,&C,&DCBNAME,&OFF                                            
         LCLA  &B,&D,&I,&V,&X,&XNUM                                             
.*                                                                              
&OFF     SETC  '&OFFLINE'.'XX'                                                  
&DCBNAME SETC  '&FILE.Z'                                                        
&XNUM    SETA  &DSKXTNT+1                                                       
.*                                                                              
.******* START OF DTF GENERATION                                                
.*                                                                              
.DTF1    ANOP                                                                   
&FILE    DC    0D'0'                                                            
         DC    X'000080',AL1(&XNUM),AL1(&XBUFFS),X'00FFFF'                      
         DC    XL8'00',X'000000'                                                
.*                                                                              
&I       SETA  0                   INIT FOR ISFFLAG AT +19                      
.DTF3A   ANOP                                                                   
         AIF   (T'&VSAM EQ 'O').DTF3AX                                          
         AIF   ('&VSAM' EQ 'NO').DTF3AX                                         
         AIF   ('&VSAM'(1,1) EQ 'Y').DTF3A1                                     
         MNOTE 0,'INVALID VSAM PARAMETER'                                       
         MEXIT                                                                  
.DTF3A1  ANOP                                                                   
&I       SETA  &I+128              SET VSAM FILE MODE X'80'                     
.DTF3AX  ANOP                                                                   
.*                                                                              
.DTF3B   ANOP                                                                   
         AIF   (T'&GLOBAL EQ 'O').DTF3BX                                        
         AIF   ('&GLOBAL' EQ 'NO').DTF3BX                                       
         AIF   ('&GLOBAL'(1,1) EQ 'Y').DTF3B1                                   
         MNOTE 0,'INVALID GLOBAL PARAMETER'                                     
         MEXIT                                                                  
.DTF3B1  ANOP                                                                   
&I       SETA  &I+64               SET GLOBAL RESOURCE X'40'                    
.DTF3BX  ANOP                                                                   
.DTF3E   ANOP                                                                   
         AIF   (T'&BLKIND EQ 'O').DTF3EX                                        
         AIF   ('&BLKIND' EQ 'NO').DTF3EX                                       
         AIF   ('&BLKIND'(1,1) EQ 'Y').DTF3E1                                   
         MNOTE 0,'INVALID BLKIND PARAMETER'                                     
         MEXIT                                                                  
.DTF3E1  ANOP                                                                   
&I       SETA  &I+32               SET BLOCK INDEX X'20'                        
.DTF3EX  ANOP                                                                   
.DTF3F   ANOP                                                                   
         AIF   ('&CACHE' EQ 'NO').DTF3F1                                        
         AIF   ('&CACHE' EQ 'YES').DTF3FX                                       
         MNOTE 0,'CACHE MUST BE YES OR NO'                                      
         MEXIT                                                                  
.DTF3F1  ANOP                                                                   
&I       SETA  &I+4                NO CACHING X'04'                             
.DTF3FX  ANOP                                                                   
.*                                                                              
         DC    AL1(&I)                                                          
.*                                                                              
&I       SETA  16                  SET DTF IS FOR AN IS FILE X'10'              
.DTF4    ANOP                                                                   
         AIF   (T'&EMU EQ 'O').DTF5                                             
         AIF   ('&EMU' EQ 'NO').DTF5                                            
         AIF   ('&EMU'(1,1) EQ 'Y').DTF4A                                       
         MNOTE 0,'INVALID EMU PARAMETER'                                        
         MEXIT                                                                  
.DTF4A   ANOP                                                                   
&I       SETA  &I+128              SET EMULATION MODE X'80'                     
.DTF5    ANOP                                                                   
         AIF   (T'&DCB EQ 'O').DTF6                                             
         AIF   ('&DCB'(1,1) EQ 'Y').DTF6                                        
         AIF   ('&DCB' EQ 'NO').DTF5A                                           
         MNOTE 0,'INVALID DCB PARAMETER'                                        
         MEXIT                                                                  
.DTF5A   ANOP                                                                   
&I       SETA  &I+64               SET NO DCB MODE X'40'                        
.DTF6    ANOP                                                                   
         AIF   ('&DDSCTRY' NE 'US').DTF6AA                                      
         AIF   (T'&IXPDOV EQ 'O').DTF6A                                         
         AGO   .DTF6AB                                                          
.DTF6AA  ANOP                                                                   
         AIF   (T'&IXPDOV EQ 'O').DTF7                                          
.DTF6AB  AIF   ('&IXPDOV' EQ 'N').DTF7                                          
         AIF   ('&IXPDOV'(1,1) EQ 'Y').DTF6A                                    
         MNOTE 0,'INVALID IXPDOV PARAMETER'                                     
         MEXIT                                                                  
.DTF6A   ANOP                                                                   
&I       SETA  &I+32               SET IXPDOV TO ISFTYP X'20'                   
.DTF7    ANOP                                                                   
         AIF   (T'&CMPRS EQ 'O').DTF11                                          
         AIF   ('&CMPRS' EQ 'NO').DTF11                                         
         AIF   ('&CMPRS' EQ 'LR').DTF8                                          
         AIF   ('&CMPRS' EQ 'RL').DTF8                                          
         AIF   ('&CMPRS' EQ 'L').DTF8                                           
         AIF   ('&CMPRS' EQ 'R').DTF9                                           
         MNOTE 0,'INVALID CMPRS PARAMETER'                                      
         MEXIT                                                                  
.DTF8    ANOP                                                                   
&I       SETA  &I+8                COMPRESS FROM LEFT X'08'                     
.DTF9    ANOP                                                                   
         AIF   ('&CMPRS' EQ 'LR').DTF10                                         
         AIF   ('&CMPRS' EQ 'RL').DTF10                                         
         AIF   ('&CMPRS' EQ 'R').DTF10                                          
         AGO   .DTF11                                                           
.DTF10   ANOP                                                                   
&I       SETA  &I+4                COMPRESS FROM RIGHT X'04'                    
.*                                                                              
.DTF11   ANOP                                                                   
         AIF   ('&BIG' EQ 'NO').DTF13                                           
         AIF   ('&BIG' EQ 'YES').DTF11A                                         
         AIF   ('&BIG' EQ '18BIT').DTF11A                                       
         AIF   ('&BIG' EQ 'BIG').DTF11B                                         
         AIF   ('&BIG' EQ '20BIT').DTF11B                                       
         MNOTE 0,'BIG MUST BE YES OR NO OR BIG'                                 
         MEXIT                                                                  
.DTF11A  ANOP                                                                   
&I       SETA  &I+1                BIG FILE (18 BIT TRACKS) X'01'               
         AGO   .DTF11                                                           
.DTF11B  ANOP                                                                   
&I       SETA  &I+2                BIG FILE (20 BIT TRACKS) X'02'               
.*                                                                              
.DTF13   ANOP                                                                   
         DC    AL1(&I)                                                          
.*                                                                              
.DTF15   ANOP                                                                   
         DC    AL1(0)                                                           
         DC    CL8'&FILE'                                                       
.*                                                                              
&TRACKS  SETA  12                                                               
&DEV     SETA  3340                                                             
         AIF   (T'&DEVICE EQ 'O').DTF16                                         
         AIF   ('&DEVICE' EQ '3340').DTF16                                      
&TRACKS  SETA  30                                                               
&DEV     SETA  3350                                                             
         AIF   ('&DEVICE' EQ '3350').DTF16                                      
&TRACKS  SETA  12                                                               
&DEV     SETA  3375                                                             
         AIF   ('&DEVICE' EQ '3375').DTF16                                      
&TRACKS  SETA  15                                                               
&DEV     SETA  3380                                                             
         AIF   ('&DEVICE' EQ '3380').DTF16                                      
&TRACKS  SETA  15                                                               
&DEV     SETA  3390                                                             
         AIF   ('&DEVICE' EQ '3390').DTF16                                      
         MNOTE 0,'INVALID DEVICE STATEMENT'                                     
         MEXIT                                                                  
.DTF16   ANOP                                                                   
         DC    H'0',AL1(0),AL3(0)                                               
.*                                                                              
.DTF17   ANOP                                                                   
         AIF   (T'&DCB EQ 'O').DTF19                                            
         AIF   ('&DCB' NE 'NO').DTF19                                           
         DC    A(0)                                                             
         AIF   (T'&FILABL EQ 'O').DTF18                                         
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.DTF18   ANOP                                                                   
         MEXIT                                                                  
.DTF19   ANOP                                                                   
         DC    A(&DCBNAME)                                                      
         AIF   (T'&FILABL EQ 'O').DTF20                                         
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.DTF20   ANOP                                                                   
.*                                                                              
.* ******************************* END OF XL40 DTF GENERATION                   
.*                                                                              
*                                                                               
.ONE     ANOP                                                                   
&SPR     SETA  100                                                              
         AIF   (T'&SPARE EQ 'O').THREE                                          
&SPR     SETA  &SPARE                                                           
         AIF   (T'&SPARE EQ 'N').THREE                                          
         MNOTE 0,'INVALID SPARE FIELD'                                          
         MEXIT                                                                  
.*                                                                              
.THREE   ANOP                                                                   
         SPACE 1                                                                
         DC    AL2(&SPR)           SPARE BYTES IN PRIME BLOCK                   
         AIF   ('&RECSIZE' EQ 'V').THREEA                                       
         AIF   (T'&RECSIZE EQ 'N').THREEB                                       
         MNOTE 0,'INVALID RECSIZE FIELD'                                        
         MEXIT                                                                  
.THREEA  ANOP                                                                   
&V       SETA  128                                                              
         DC    AL2(0)              RECORD LENGTH VARIABLE                       
         AGO   .FOUR                                                            
.THREEB  ANOP                                                                   
&V       SETA  0                                                                
         DC    AL2(&RECSIZE)       RECORD LENGTH                                
.*                                                                              
.FOUR    ANOP                                                                   
         AIF   (T'&KEYLEN NE 'O' AND T'&KEYLEN EQ 'N').SIX                      
.FIVE    MNOTE 0,'INVALID KEY LENGTH'                                           
         MEXIT                                                                  
.*                                                                              
.SIX     ANOP                                                                   
&KEY     SETA  &KEYLEN                                                          
         AIF   (&KEY EQ 0).FIVE                                                 
         DC    AL2(&KEY)           KEY LENGTH                                   
&A       SETA  &KEY-1                                                           
         DC    AL2(&A)             KEY LENGTH - 1                               
         AIF   (T'&BLKSIZE NE 'O').SEVEN                                        
         MNOTE 0,'MISSING BLKSIZE FIELD'                                        
         MEXIT                                                                  
.*                                                                              
.SEVEN   AIF   (T'&BLKSIZE EQ 'N').EIGHT                                        
         MNOTE 0,'BLKSIZE NOT NUMERIC'                                          
         MEXIT                                                                  
.*                                                                              
.EIGHT   ANOP                                                                   
         DC    AL2(&BLKSIZE)       PRIME DATA BLOCK SIZE                        
         SPACE 1                                                                
.*                                                                              
.*                                 WORK OUT RECORDS PER TRACK (PD)              
.*                                                                              
.NINE    ANOP                                                                   
         AIF   (&DEV NE 3340).NINE1                                             
&PDRT    SETA  8535/(&BLKSIZE+167)                                              
         AGO   .TEN                                                             
.NINE1   ANOP                                                                   
         AIF   (&DEV NE 3350).NINE2                                             
&PDRT    SETA  19254/(&BLKSIZE+185)                                             
         AGO   .TEN                                                             
.NINE2   ANOP                                                                   
         AIF   (&DEV NE 3375).NINE3                                             
&PDRT    SETA  36000/((((&BLKSIZE+31)/32)*32)+384)                              
         AGO   .TEN                                                             
.NINE3   ANOP                                                                   
         AIF   (&DEV NE 3380).NINE4                                             
&PDRT    SETA  47968/((((&BLKSIZE+43)/32)*32)+480)                              
         AGO   .TEN                                                             
.NINE4   ANOP                                                                   
         AIF   (&DEV NE 3390).NINE5                                             
&PDRT    SETA  0                                                                
         AGO   .TEN                                                             
.NINE5   ANOP                                                                   
.*                                                                              
.*                                 WORK OUT TI BLOCKSIZE                        
.*                                                                              
.TEN     ANOP                                                                   
&A       SETA  &TRACKS-1                                                        
&TISIZE  SETA  (&KEY*&PDRT*&A)                                                  
&A       SETA  &KEYLEN+2                                                        
&CISIZE  SETA  (&TISIZE/&A)*&A                                                  
&B       SETA  &TISIZE/2                                                        
         AIF   (&BLKSIZE GT &B).TWELVE                                          
         MNOTE 0,'BLKSIZE MUST BE GREATER THAN HALK TRACK INDEX SIZE'           
.*                                                                              
.*                                 NOW WORK OUT RECORDS/TRACK (TI)              
.*                                                                              
.TWELVE  ANOP                                                                   
         AIF   (&DEV NE 3340).TWELVE1                                           
&TIRT    SETA  8535/(&TISIZE+167)                                               
&CIRT    SETA  8535/(&CISIZE+167)                                               
         AGO   .THRTEEN                                                         
.TWELVE1 ANOP                                                                   
         AIF   (&DEV NE 3350).TWELVE2                                           
&TIRT    SETA  19254/(&TISIZE+185)                                              
&CIRT    SETA  19254/(&CISIZE+185)                                              
         AGO   .THRTEEN                                                         
.TWELVE2 ANOP                                                                   
         AIF   (&DEV NE 3375).TWELVE3                                           
&TIRT    SETA  36000/((((&TISIZE+31)/32)*32)+384)                               
&CIRT    SETA  36000/((((&CISIZE+31)/32)*32)+384)                               
         AGO   .THRTEEN                                                         
.TWELVE3 ANOP                                                                   
         AIF   (&DEV NE 3380).TWELVE4                                           
&TIRT    SETA  47968/((((&TISIZE+43)/32)*32)+480)                               
&CIRT    SETA  47968/((((&CISIZE+43)/32)*32)+480)                               
         AGO   .THRTEEN                                                         
.TWELVE4 ANOP                                                                   
         AIF   (&DEV NE 3390).TWELVE5                                           
&TIRT    SETA   0                                                               
&CIRT    SETA   0                                                               
         AGO    .THRTEEN                                                        
.TWELVE5 ANOP                                                                   
.*                                                                              
.THRTEEN ANOP                                                                   
         DC    AL2(&TISIZE)        TRACK INDEX BLKSIZE                          
         DC    AL2(&CISIZE)        CYLINDER INDEX BLKSIZE                       
         DC    AL2(0)                                                           
         SPACE 1                                                                
&C       SETC  '0'                                                              
         AIF   ('&OFF'(1,1) EQ 'N').THRT4                                       
&C       SETC  '&FILE.D'                                                        
         AIF   (T'&IOAREA1 EQ 'O').THRT4                                        
         AIF   ('&IOAREA1'(1,1) EQ '(').THRT2                                   
&C       SETC  '&IOAREA1'                                                       
         AGO   .THRT4                                                           
.THRT2   ANOP                                                                   
&C       SETC  '&IOAREA1(1)'                                                    
         AIF   ('&C' NE 'AUTO').THRT4                                           
&C       SETC  '0'                                                              
.THRT4   ANOP                                                                   
         DC    A(&C)               A(PRIME DATA BUFFER)                         
&A       SETA  &BLKSIZE-1                                                       
         DC    A(&C+&A)                                                         
.*                                                                              
.FRTEEN  ANOP                                                                   
         AIF   ('&ADD' NE 'NO').FRT2                                            
         DC    A(&C)                                                            
         DC    A(&C+&A)                                                         
         AGO   .FRT4                                                            
.FRT2    ANOP                                                                   
         DC    A(&C+&BLKSIZE)                                                   
&A       SETA  (&BLKSIZE*2)-1                                                   
         DC    A(&C+&A)                                                         
.FRT4    ANOP                                                                   
         SPACE 1                                                                
         AIF   (T'&INDAREA NE 'O').FIFTEEN                                      
         DC    A(&FILE.I)          A(CORE INDEX BUFFER)                         
         AGO   .FIFT6                                                           
.*                                                                              
.FIFTEEN ANOP                                                                   
         AIF   ('&INDAREA'(1,1) NE '(').FIFT4                                   
         AIF   ('&OFF'(1,1) EQ 'Y').FIFT2                                       
         DC    A(&FILE.I)                                                       
         AGO   .FIFT6                                                           
.FIFT2   ANOP                                                                   
*        AIF   (L'&OFF LT 2).FIFT3                                              
         AIF   ('&OFF'(2,1) NE 'I').FIFT3                                       
         DC    A(&FILE.I)                                                       
         AGO   .FIFT6                                                           
.FIFT3   ANOP                                                                   
         DC    A(&INDAREA(1))      A(CORE INDEX BUFFER)                         
         AGO   .FIFT6                                                           
.FIFT4   ANOP                                                                   
         DC    A(&INDAREA)         A(CORE INDEX BUFFER)                         
.FIFT6   ANOP                                                                   
         AIF   (T'&INDSIZE NE 'O').SIXTEEN                                      
         MNOTE 0,'MISSING INDEX SIZE STATEMENT'                                 
         MEXIT                                                                  
.*                                                                              
.SIXTEEN ANOP                                                                   
&D       SETA  0                                                                
         AIF   (T'&INDSIZE NE 'N').SIXTN1                                       
&X       SETA  &INDSIZE                                                         
         AGO   .SIXTN4                                                          
.SIXTN1  ANOP                                                                   
&I       SETA  K'&INDSIZE                                                       
         AIF   ('&INDSIZE'(&I,1) EQ 'D').SIXTN2                                 
         MNOTE 0,'INVALID INDEX SIZE'                                           
         MEXIT                                                                  
.SIXTN2  ANOP                                                                   
&I       SETA  &I-1                                                             
         AIF   (&I NE 0).SIXTN3                                                 
         MNOTE 0,'INVALID INDEX SIZE'                                           
         MEXIT                                                                  
.SIXTN3  ANOP                                                                   
&D       SETA  2                                                                
&C       SETC  '&INDSIZE'(1,&I)                                                 
&X       SETA  &C                                                               
.SIXTN4  ANOP                                                                   
         DC    AL2(&X)             SIZE OF INDEX                                
&A       SETA  &KEYLEN+2                                                        
         DC    AL2(&A)             SIZE OF CI ENTRY  (KEYLEN+2)                 
         SPACE 1                                                                
&A       SETA  0                                                                
         AIF   (T'&MASTIND EQ 'O').SVNTEEN                                      
&A       SETA  1                                                                
.SVNTEEN ANOP                                                                   
&A       SETA  &A+&D                                                            
         DC    AL1(&A)             INDEX TYPE 01=MST,02=LEN IN DUBS             
&A       SETA  1                                                                
         AIF   (T'&COMPRSS EQ 'O').ETEEN                                        
         AIF   ('&COMPRSS' NE 'NO').ETEEN                                       
&A       SETA  0                                                                
.ETEEN   ANOP                                                                   
&A       SETA  &A+&V                                                            
         AIF   ('&ADD' NE 'NO').ETEEN2                                          
&A       SETA  &A+64                                                            
.ETEEN2  ANOP                                                                   
         DC    AL1(&A)             80=V/L,40=NOADD,01=COMPRESS                  
         SPACE 1                                                                
         DC    AL2(&TRACKS)        NUMBER OF TRACKS PER CYLINDER                
         DC    AL2(&PDRT)          HIGH RECORD ON PD TRACK                      
         DC    AL2(&TIRT)          HIGH RECORD ON TI TRACK                      
         DC    AL2(&CIRT)          HIGH RECORD ON CI TRACK                      
         DC    AL2(0)                                                           
         SPACE 1                                                                
         DC    X'00000000'         TTBO OF LAST PD REC                          
         DC    X'00000000'         TTB0 OF LAST CI REC                          
         DC    X'00000000'         TTB0 OF LAST PD OVERFLOW RECORD              
         DC    X'00000000'         TTB0 OF CURRENT PD RECORD                    
         DC    X'00000000'         TTB0 OF CURRENT OVERFLOW RECORD              
         DC    A(0)                A(LAST REC USED IN PD BUFFER)                
         AIF   ('&OFF'(1,1) EQ 'N').ETEEN4                                      
         DC    A(&FILE.A)          A(LAST KEY SAVE AREA)                        
         DC    A(&FILE.B)          A(PRIOR KEY SAVE AREA)                       
         DC    A(&FILE.C)          A(TI ENTRY SAVE AREA)                        
         AGO   .ETEEN6                                                          
.ETEEN4  ANOP                                                                   
         DC    A(0)                A(LAST KEY SAVE AREA)                        
         DC    A(0)                A(PRIOR KEY SAVE AREA)                       
         DC    A(0)                A(TI ENTRY SAVE AREA)                        
.ETEEN6  ANOP                                                                   
         SPACE 1                                                                
&FILE.X  DC    1XL14'00',X'00'     EXTENT MATRIX FOR IX AND OV                  
&A       SETA  &DSKXTNT                                                         
.*                                                                              
.NINTEEN ANOP                                                                   
         DC    &A.XL14'00',X'00'   EXTENT MATRIX FOR PD                         
         AIF   ('&OFF'(1,1) EQ 'N').TWENT2                                      
&FILE.A  DC    &KEYLEN.X'00'                                                    
&FILE.B  DC    &KEYLEN.X'00'                                                    
&FILE.C  DC    &KEYLEN.X'00'                                                    
         AIF   (T'&IOAREA1 EQ 'O').TWENTY                                       
         AGO   .TWENT2                                                          
.TWENTY  ANOP                                                                   
&A       SETA  &BLKSIZE*2                                                       
         AIF   ('&ADD' NE 'NO').TWENT1                                          
&A       SETA  &TISIZE                                                          
         AIF   (&TISIZE GT &BLKSIZE).TWENT1                                     
&A       SETA  &BLKSIZE                                                         
.TWENT1  ANOP                                                                   
&FILE.D  DS    &A.C                                                             
.TWENT2  AIF   (T'&INDAREA EQ 'O').TWENT4                                       
         AIF   ('&INDAREA'(1,1) NE '(').TWENT6                                  
         AIF   ('&OFF'(1,1) NE 'Y').TWENT4                                      
*        AIF   (L'&OFF LT 2).TWENT6                                             
         AIF   ('&OFF'(2,1) NE 'I').TWENT6                                      
.TWENT4  ANOP                                                                   
         AIF   (&D EQ 0).TWENT4A                                                
&FILE.I  DS    &X.XL8                                                           
         AGO   .TWENT6                                                          
.TWENT4A ANOP                                                                   
&FILE.I  DS    &X.XL1                                                           
.*                                                                              
.TWENT6  ANOP                                                                   
         AIF   ('&VSAM'(1,1) EQ 'Y').TWENT7                                     
*                                                                               
&DCBNAME DCB   DDNAME=&FILE,DSORG=DA,MACRF=(RI)                                 
         AGO   .TWENT8                                                          
.TWENT7  ANOP                                                                   
*                                                                               
&DCBNAME ACB   AM=VSAM,DDNAME=&FILE,MACRF=(KEY,DIR,SEQ)                         
.*                                                                              
.TWENT8  ANOP                                                                   
         AIF   (T'&FILABL EQ 'O').THIRTY                                        
         ORG   &DCBNAME+40                                                      
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.THIRTY  ANOP                                                                   
         MEXIT                                                                  
         MEND                                                                   
**PAN#1  CSECT                                                                  
         DC    CL21'005MCDMISS   05/08/03'                                      
         END                                                                    
