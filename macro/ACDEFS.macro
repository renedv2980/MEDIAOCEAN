*          DATA SET MCACDEFS   AT LEVEL 024 AS OF 09/20/95                      
         MACRO                                                                  
&TAG     ACDEF &LINE,&COL,&KEY,&DDLEN,&DDTYP,&WIDE=132                          
         GBLA  &WP                                                              
         LCLA  &PC,&PL,&PN,&PD,&PK,&CN,&PW,&INDEX,&LEN                          
         LCLC  &PCL,&PLENA                                                      
.*                                                                              
         AIF   (T'&WIDE NE 'N').WCERR                                           
&PW      SETA  &WIDE                                                            
         AIF   (&WP EQ 0).L1                                                    
&PW      SETA  &WP                                                              
.*                                                                              
.L1      ANOP                                                                   
         AIF   ('&LINE' EQ 'READ').FS                                           
         AIF   ('&LINE' EQ 'MODE').MS                                           
         AIF   ('&LINE' EQ 'RESET').RE                                          
         AIF   ('&LINE' EQ 'UPDATE').FC                                         
         AIF   ('&LINE' EQ 'SPROG').SC                                          
         AIF   ('&LINE' EQ 'CTRY').CC                                           
         AIF   ('&LINE' EQ 'LANG').LC                                           
         AIF   ('&LINE' EQ 'WIDTH').WC                                          
         AIF   ('&LINE' EQ 'SET').SET                                           
         AIF   ('&LINE' EQ 'PERSEC').PS                                         
         AIF   ('&LINE' EQ 'JOBCOL').JC                                         
         AIF   ('&LINE' EQ 'FJOBCOL').FJ                                        
         AIF   ('&LINE' EQ 'GETOPT').GO                                         
         AIF   ('&LINE' EQ 'JOBBER').JO                                         
         AIF   ('&LINE' EQ 'GENBUCK').GB                                        
         AIF   ('&LINE' EQ 'FILTUSED').FU                                       
         AIF   ('&LINE' EQ 'PROCDIR').PD                                        
         AIF   ('&LINE' EQ 'PROCFIL').PF                                        
         AIF   ('&LINE' EQ 'TRNSTACK').TS                                       
         AIF   ('&LINE' EQ 'WCNAMES').WN                                        
         AIF   ('&LINE' EQ 'PRORATA').PR                                        
         AIF   ('&LINE' EQ 'PTACNV').PT                                         
.*                                                                              
&CN      SETA  1                                                                
         AIF   ('&LINE' EQ 'SINGLE').RS4                                        
&CN      SETA  2                                                                
         AIF   ('&LINE' EQ 'DOUBLE').RS4                                        
&CN      SETA  3                                                                
         AIF   ('&LINE' EQ 'TRIPLE').RS4                                        
&CN      SETA  4                                                                
         AIF   ('&LINE' EQ 'LINEUP').RS4                                        
&CN      SETA  6                                                                
         AIF   ('&LINE'(1,5) EQ 'NOREP').RS4                                    
&CN      SETA  7                                                                
         AIF   ('&LINE' EQ 'SUMMARY').RS4                                       
&CN      SETA  8                                                                
         AIF   ('&LINE'(1,5) EQ 'NOSUM').RS4                                    
&CN      SETA  10                                                               
         AIF   ('&LINE' EQ 'NOFILT').RS4                                        
.*                                                                              
         AIF   ('&LINE' NE 'MAXLINES').PL                                       
&CN      SETA  &COL                                                             
         AIF   (T'&COL NE 'N').RSERR                                            
.*                                                                              
.RS4     ANOP                                                                   
&TAG     DC    AL1(11)                                                          
         DC    AL1(03)                                                          
         DC    AL1(&CN)                                                         
         MEXIT                                                                  
.*                                                                              
.PL      ANOP                                                                   
&PCL     SETC  '&LINE'(2,K'&LINE-1)                                             
&PL      SETA  &PCL                                                             
         AIF   (&PL EQ 0).P2                                                    
         AIF   (&PL GT 12).P2                                                   
&PC      SETA  0                                                                
         AIF   ('&LINE'(1,1) EQ 'H').P4                                         
         AIF   (&PL GT 4).P2                                                    
         AIF   ('&LINE'(1,1) EQ 'P').P3                                         
         AIF   (&PL GT 2).P2                                                    
.P3      ANOP                                                                   
&PL      SETA  &PL+12                                                           
&PC      SETA  8                                                                
         AIF   ('&LINE'(1,1) EQ 'M').P4                                         
&PL      SETA  &PL+2                                                            
&PC      SETA  16                                                               
         AIF   ('&LINE'(1,1) EQ 'P').P4                                         
&PL      SETA  &PL+4                                                            
&PC      SETA  24                                                               
         AIF   ('&LINE'(1,1) EQ 'F').P4                                         
&PL      SETA  &PL+2                                                            
&PC      SETA  132                                                              
         AIF   ('&LINE'(1,1) EQ 'U').P4                                         
.*                                                                              
.P2      MNOTE 0,'INVALID LINE'                                                 
         MEXIT                                                                  
.*                                                                              
.P4      ANOP                                                                   
         AIF   (T'&COL NE 'N').P6                                               
         AIF   (&COL EQ 0).P6                                                   
         AIF   (&COL GT &PW).P6                                                 
&PL      SETA  &PL-1                                                            
&PD      SETA  &PL*&PW                                                          
&PD      SETA  &PD+&COL-1                                                       
&PD      SETA  &PD+&PC                                                          
.*                                                                              
&PL      SETA  K'&KEY-3                                                         
         AIF   ('&KEY'(1,1) GT 'Z').P9                                          
         AIF   ('&KEY'(1,2) EQ 'C''').P12                                       
         AIF   ('&KEY'(1,3) EQ 'AC#').PDD                                       
         AIF   ('&KEY'(1,3) EQ 'CLI').P5                                        
         AIF   ('&KEY'(1,2) EQ 'CL').P10                                        
.P5      ANOP                                                                   
&PK      SETA  0                                                                
         AIF   ('&KEY' EQ 'SPACES').P8                                          
&PK      SETA  1                                                                
         AIF   ('&KEY' EQ 'RUN').P8                                             
&PK      SETA  2                                                                
         AIF   ('&KEY' EQ 'REPORT').P8                                          
&PK      SETA  3                                                                
         AIF   ('&KEY' EQ 'PAGE').P8                                            
&PK      SETA  4                                                                
         AIF   ('&KEY' EQ 'PERIOD').P8                                          
&PK      SETA  5                                                                
         AIF   ('&KEY' EQ 'REQUESTOR').P8                                       
&PK      SETA  6                                                                
         AIF   ('&KEY' EQ 'COMPANY').P8                                         
&PK      SETA  7                                                                
         AIF   ('&KEY' EQ 'COMPADD').P8                                         
&PK      SETA  8                                                                
         AIF   ('&KEY' EQ 'UNIT').P8                                            
&PK      SETA  9                                                                
         AIF   ('&KEY' EQ 'UNITADD').P8                                         
&PK      SETA  10                                                               
         AIF   ('&KEY' EQ 'LEDGER').P8                                          
&PK      SETA  11                                                               
         AIF   ('&KEY' EQ 'ACCOUNT').P8                                         
         AIF   ('&KEY' EQ 'ACCNAME').P8                                         
&PK      SETA  12                                                               
         AIF   ('&KEY' EQ 'ACCADD').P8                                          
&PK      SETA  13                                                               
         AIF   ('&KEY' EQ 'COMPNAME').P8                                        
&PK      SETA  14                                                               
         AIF   ('&KEY' EQ 'MOSFILT').P8                                         
&PK      SETA  21                                                               
         AIF   ('&KEY' EQ 'REQDETS').P8                                         
&PK      SETA  25                                                               
         AIF   ('&KEY' EQ 'ORIGIN').P8                                          
&PK      SETA  26                                                               
         AIF   ('&KEY' EQ 'ORIGADD').P8                                         
         MNOTE 0,'NOT A VALID KEYWORD'                                          
         MEXIT                                                                  
.*                                                                              
.P6      ANOP                                                                   
         MNOTE 0,'COLUMN SHOULD 1-132'                                          
         MEXIT                                                                  
.*                                                                              
.*                                 COMPLETE KEYWORD EXPANSION                   
.*                                                                              
.P8      ANOP                                                                   
&TAG     DC    AL1(12)                                                          
         DC    AL1(05)                                                          
         DC    AL2(&PD)                                                         
         DC    AL1(&PK)                                                         
         MEXIT                                                                  
.*                                                                              
.P9      ANOP                                                                   
&PN      SETA  1                                                                
         AIF   ('&KEY'(2,1) LE 'Z').P9A                                         
&PN      SETA  2                                                                
         AIF   ('&KEY'(3,1) LE 'Z').P9A                                         
&PN      SETA  3                                                                
         AIF   ('&KEY'(4,1) GT 'Z').P14                                         
.P9A     ANOP                                                                   
&PLENA   SETC  '&KEY'(1,&PN)                                                    
         AGO   .P11A                                                            
.P10     ANOP                                                                   
&PN      SETA  1                                                                
         AIF   ('&KEY'(4,1) EQ '''').P11                                        
&PN      SETA  2                                                                
         AIF   ('&KEY'(5,1) EQ '''').P11                                        
&PN      SETA  3                                                                
         AIF   ('&KEY'(6,1) EQ '''').P11                                        
         AGO   .P14                                                             
.*                                                                              
.P11     ANOP                                                                   
&PLENA   SETC  '&KEY'(3,&PN)                                                    
.P11A    ANOP                                                                   
&PL      SETA  &PLENA                                                           
         AIF   (&PL EQ 0).P14                                                   
         AIF   (&PL GT 250).P14                                                 
.*                                                                              
.P12     ANOP                                                                   
&TAG     DC    AL1(13)                                                          
&PL      SETA  &PL+4                                                            
         DC    AL1(&PL)                                                         
         DC    AL2(&PD)                                                         
         DC    &KEY                                                             
         MEXIT                                                                  
.*                                                                              
.*                                                                              
.P14     ANOP                                                                   
         MNOTE 0,'INVALID LITERAL EXPRESSION'                                   
         MEXIT                                                                  
.*                                                                              
.PDD     ANOP                                                                   
         AIF   (T'&DDLEN EQ 'O').PDERR                                          
         AIF   ('&DDLEN' EQ '2').PDD1                                           
         AIF   ('&DDLEN' EQ '3').PDD1                                           
&PL      SETA  &DDLEN+4                                                         
         AGO   .PDD2                                                            
.PDD1    ANOP                                                                   
&PL      SETA  7                                                                
.PDD2    ANOP                                                                   
&TAG     DC    AL1(13)                                                          
         DC    AL1(&PL)                                                         
         DC    AL2(&PD)                                                         
         DCDD  &KEY,&DDLEN,&DDTYP,PAD=BLANKS                                    
         MEXIT                                                                  
.*                                                                              
.SET     ANOP                                                                   
&CN      SETA  17                                                               
         AIF   ('&COL' EQ 'ACCTBAL').FC12                                       
         AGO   .SETERR                                                          
.*                                                                              
.FS      ANOP                                                                   
&CN      SETA  1                                                                
         AIF   ('&COL'(1,5) EQ 'ACCOU').FC12                                    
&CN      SETA  2                                                                
         AIF   ('&COL'(1,5) EQ 'TRANS').FC12                                    
&CN      SETA  3                                                                
         AIF   ('&COL'(1,4) EQ 'HIST').FC12                                     
&CN      SETA  4                                                                
         AIF   ('&COL'(1,4) EQ 'HOLD').FC12                                     
&CN      SETA  5                                                                
         AIF   ('&COL'(1,4) EQ 'WORK').FC12                                     
         AIF   ('&COL'(1,4) EQ 'ANAL').FC12                                     
&CN      SETA  6                                                                
         AIF   ('&COL'(1,4) EQ 'TIME').FC12                                     
&CN      SETA  8                                                                
         AIF   ('&COL'(1,3) EQ 'EST').FC12                                      
&CN      SETA  9                                                                
         AIF   ('&COL'(1,3) EQ 'ORD').FC12                                      
&CN      SETA  10                                                               
         AIF   ('&COL'(1,3) EQ 'ACL').FC12                                      
         AIF   ('&COL'(1,4) EQ 'LIST').FC12                                     
&CN      SETA  11                                                               
         AIF   ('&COL'(1,6) EQ 'FILTER').FC12                                   
&CN      SETA  13                                                               
         AIF   ('&COL'(1,3) EQ 'REQ').FC12                                      
&CN      SETA  14                                                               
         AIF   ('&COL'(1,3) EQ 'OFA').FC12                                      
&CN      SETA  20                                                               
         AIF   ('&COL'(1,7) EQ 'RECOVER').FS2                                   
         AIF   ('&COL'(1,3) EQ 'RCV').FS2                                       
         AGO   .FDERR                                                           
.*                                                                              
.FS2     ANOP                                                                   
         AIF   (T'&KEY EQ 'O').FC12                                             
         AIF   ('&KEY'(1,3) NE 'DEL').FDERR                                     
.*                                                                              
&TAG     DC    AL1(10)                                                          
         DC    AL1(04)                                                          
         DC    AL1(&CN)                                                         
         DC    C'D'                                                             
         MEXIT                                                                  
.*                                                                              
.* THE MODE SPEC HAS NEVER BEEN IMPLEMENTED IN ACMASTER--WE WILL JUST           
.* DO A SYNTAX CHECK AND EXIT                                                   
.*                                                                              
.MS      ANOP                                                                   
         AIF   ('&COL'(1,4) NE 'PROC').FDERR                                    
         MEXIT                                                                  
.*                                                                              
.RE      ANOP                                                                   
&CN      SETA  16                                                               
         AGO   .FC12                                                            
.*                                                                              
.FC      ANOP                                                                   
&CN      SETA  12                                                               
         AIF   ('&COL'(1,6) EQ 'ACCFIL').FC12                                   
         AGO   .FDERR                                                           
.*                                                                              
.FU      ANOP                                                                   
&CN      SETA  21                                                               
         AGO   .FC12                                                            
.*                                                                              
.PD      ANOP                                                                   
&CN      SETA  22                                                               
         AGO   .FC12                                                            
.*                                                                              
.PF      ANOP                                                                   
&CN      SETA  23                                                               
         AGO   .FC12                                                            
.*                                                                              
.TS      ANOP                                                                   
&CN      SETA  24                                                               
         AGO   .FC12                                                            
.*                                                                              
.WN      ANOP                                                                   
&CN      SETA  25                                                               
         AGO   .FC12                                                            
.*                                                                              
.FC12    ANOP                                                                   
&TAG     DC    AL1(10)                                                          
         DC    AL1(03)                                                          
         DC    AL1(&CN)                                                         
         MEXIT                                                                  
.*                                                                              
.GB      ANOP                                                                   
&CN      SETA  15                                                               
&PCL     SETC  'C'                                                              
         AIF   ('&COL'(1,6) EQ 'CONTRA').GB10                                   
&PCL     SETC  'O'                                                              
         AIF   ('&COL'(1,6) EQ 'OFFICE').GB10                                   
         AGO   .BTERR                                                           
.*                                                                              
.GB10    ANOP                                                                   
&TAG     DC    AL1(10)                                                          
         DC    AL1(04)                                                          
         DC    AL1(&CN)                                                         
         DC    C'&PCL'                                                          
         MEXIT                                                                  
.*                                                                              
.SC      ANOP                                                                   
&INDEX   SETA  1                                                                
&TAG     DC    AL1(01)                                                          
         DC    AL1(02)                                                          
.SC2     ANOP                                                                   
&INDEX   SETA  &INDEX+1                                                         
         AIF   (T'&SYSLIST(&INDEX) NE 'O').SC4                                  
         MEXIT                                                                  
.*                                                                              
.SC4     ANOP                                                                   
         AIF   (T'&SYSLIST(&INDEX) NE 'N').SCERR                                
         AIF   (&SYSLIST(&INDEX) GT 255).SCERR                                  
         DC    AL1(02)                                                          
         DC    AL1(03)                                                          
         DC    AL1(&SYSLIST(&INDEX))                                            
         AGO   .SC2                                                             
.*                                                                              
.CC      ANOP                                                                   
&CN      SETA  &COL                                                             
         AIF   (T'&COL NE 'N').CCERR                                            
&TAG     DC    AL1(07)                                                          
         DC    AL1(03)                                                          
         DC    AL1(&CN)                                                         
         MEXIT                                                                  
.*                                                                              
.LC      ANOP                                                                   
&CN      SETA  &COL                                                             
         AIF   (T'&COL NE 'N').LCERR                                            
&TAG     DC    AL1(08)                                                          
         DC    AL1(03)                                                          
         DC    AL1(&CN)                                                         
         MEXIT                                                                  
.*                                                                              
.WC      ANOP                                                                   
         AIF   (T'&COL NE 'N').WCERR                                            
         AIF   (&COL GT 198).WCERR                                              
&WP      SETA  &COL                                                             
         MEXIT                                                                  
.*                                                                              
.PS      ANOP                                                                   
&PCL     SETC  'C'                                                              
         AIF   ('&COL'(1,3) EQ 'CLI').PS2                                       
&PCL     SETC  'E'                                                              
         AIF   ('&COL'(1,3) EQ 'EMP').PS2                                       
&PCL     SETC  'N'                                                              
         AIF   ('&COL'(1,4) EQ 'NONE').PS2                                      
         AGO   .PSERR                                                           
.*                                                                              
.PS2     ANOP                                                                   
&TAG     DC    AL1(09)                                                          
         DC    AL1(03)                                                          
         DC    CL1'&PCL'                                                        
         MEXIT                                                                  
.*                                                                              
.JC      ANOP                                                                   
         AIF   (N'&SYSLIST LE 2).JCERR                                          
&INDEX   SETA  1                                                                
&LEN     SETA  ((N'&SYSLIST-1)*2)+4                                             
         DC    AL1(03)                                                          
         DC    AL1(&LEN)                                                        
.JC2     ANOP                                                                   
&INDEX   SETA  &INDEX+1                                                         
         AIF   (&INDEX GT N'&SYSLIST).JC4                                       
&PCL     SETC  '&SYSLIST(&INDEX)'                                               
         DC    AL2(&PCL)                                                        
         AGO   .JC2                                                             
.JC4     ANOP                                                                   
         DC    AL2(0)                                                           
         MEXIT                                                                  
.*                                                                              
.FJ      ANOP                                                                   
         AIF   (N'&SYSLIST LT 2).JCERR                                          
&INDEX   SETA  1                                                                
&LEN     SETA  0                   INITIALIZE CHAR STRING LENGTH                
.*                                                                              
.FJ2     ANOP                                                                   
&INDEX   SETA  &INDEX+1                                                         
         AIF   (&INDEX GT N'&SYSLIST).FJ6                                       
         AIF   (K'&SYSLIST(&INDEX) EQ 0).JCERR1                                 
.*                                                                              
&PCL     SETC  '&PCL'.'&SYSLIST(&INDEX)'   ATTACH THE OPERAND                   
&LEN     SETA  &LEN+K'&SYSLIST(&INDEX)                                          
         AIF   (&INDEX EQ N'&SYSLIST).FJ4                                       
.*                                                                              
&PCL     SETC  '&PCL'.','          ATTACH A COMMA AFTER OPERAND                 
&LEN     SETA  &LEN+1                                                           
.*                                                                              
.FJ4     ANOP                                                                   
         AGO   .FJ2                                                             
.*                                                                              
.FJ6     ANOP                                                                   
&PL      SETA  &LEN+8+2            COMPUTE ELEMENT LENGTH                       
&PN      SETA  &LEN+8              COMPUTE L'FIELD HEADER + STRING              
.*                                                                              
.* BUILD ELEMENT CODE/LENGTH/DUMMY FIELD HEADER(8)/JOBCOL STRING(FIELD)         
.*                                                                              
         DC    AL1(06)                                                          
         DC    AL1(&PL)                                                         
         DC    AL1(&PN)                                                         
         DC    XL4'00'                                                          
         DC    AL1(&LEN)                                                        
         DC    XL2'00'                                                          
         DC    C'&PCL'                                                          
         MEXIT                                                                  
.*                                                                              
.GO      ANOP                                                                   
         AIF   (N'&SYSLIST NE 2).GOERR                                          
&TAG     DC    AL1(04)                                                          
         DC    AL1(03)                                                          
         AIF   ('&COL'(1,1) NE 'N').GO2                                         
         DC    C'N'                                                             
         MEXIT                                                                  
.GO2     ANOP                                                                   
         AIF   ('&COL'(1,1) NE 'J').GO4                                         
         DC    C'J'                                                             
         MEXIT                                                                  
.GO4     ANOP                                                                   
         AIF   ('&COL'(1,1) NE 'W').GO6                                         
         DC    C'W'                                                             
         MEXIT                                                                  
.GO6     ANOP                                                                   
         AGO   .GOERR1                                                          
.*                                                                              
.JO      ANOP                                                                   
         AIF   (N'&SYSLIST NE 2).JOERR                                          
         DC    AL1(05)                                                          
         DC    AL1(03)                                                          
         AIF   ('&COL'(1,5) NE 'PROCA').JO2                                     
         DC    AL1(21)                                                          
         MEXIT                                                                  
.JO2     ANOP                                                                   
         AIF   ('&COL'(1,4) NE 'LEVC').JO4                                      
         DC    AL1(08)                                                          
         MEXIT                                                                  
.JO4     ANOP                                                                   
         AIF   ('&COL'(1,8) NE 'PROCLEVC').JO6                                  
         DC    AL1(35)                                                          
         MEXIT                                                                  
.JO6     ANOP                                                                   
         AGO   .JOERR1                                                          
.*                                                                              
.PR      ANOP                                                                   
&TAG     DC    AL1(15)                                                          
         DC    AL1(03)                                                          
         AIF   (T'&COL NE 'O').PR2                                              
         DC    C'Y'                                                             
         MEXIT                                                                  
.PR2     ANOP                                                                   
         AIF   ('&COL'(1,4) NE 'BILL').PRERR                                    
         DC    C'B'                                                             
         MEXIT                                                                  
.*                                                                              
.PT      ANOP                                                                   
&TAG     DC    AL1(16)                                                          
         DC    AL1(03)                                                          
         AIF   ('&COL'(1,3) NE 'YES').PRERR                                     
         DC    C'Y'                                                             
         MEXIT                                                                  
.*                                                                              
.JCERR   ANOP                                                                   
         MNOTE 0,'TOO FEW OPERANDS - MUST BE AT LEAST 2'                        
         MEXIT                                                                  
.*                                                                              
.JCERR1  ANOP                                                                   
         MNOTE 0,'DON''T PASS OMITTED OPERAND'                                  
         MEXIT                                                                  
.*                                                                              
.GOERR   ANOP                                                                   
         MNOTE 0,'SHOULD BE 2 OPERANDS (GETOPT,OPTION)'                         
         MEXIT                                                                  
.*                                                                              
.GOERR1  ANOP                                                                   
         MNOTE 0,'INVALID OPTION S/B NO/JOB'                                    
         MEXIT                                                                  
.*                                                                              
.PRERR   ANOP                                                                   
         MNOTE 0,'PARAMETER 2 IS INVALID'                                       
         MEXIT                                                                  
.*                                                                              
.JOERR   ANOP                                                                   
         MNOTE 0,'SHOULD BE 2 OPERANDS (JOBBER,MODE)'                           
         MEXIT                                                                  
.*                                                                              
.JOERR1  ANOP                                                                   
         MNOTE 0,'INVALID MODE-S/B PROCACC/LEVCFRST/PROCLEVC'                   
         MEXIT                                                                  
.*                                                                              
.PDERR   ANOP                                                                   
         MNOTE 0,'DATA DICT MUST HAVE LENGTH'                                   
         MEXIT                                                                  
.*                                                                              
.FDERR   ANOP                                                                   
         MNOTE 0,'INVALID FILE OR RECORD TYPE NAME '                            
         MEXIT                                                                  
.*                                                                              
.SETERR  ANOP                                                                   
         MNOTE 0,'INVALID SET KEYWORD'                                          
         MEXIT                                                                  
.*                                                                              
.RSERR   ANOP                                                                   
         MNOTE 0,'INVALID KEYWORD'                                              
         MEXIT                                                                  
.*                                                                              
.SCERR   ANOP                                                                   
         MNOTE 0,'INVALID SUB-PROGRAM NUMBER'                                   
         MEXIT                                                                  
.*                                                                              
.CCERR   ANOP                                                                   
         MNOTE 0,'INVALID COUNTRY NUMBER'                                       
         MEXIT                                                                  
.*                                                                              
.LCERR   ANOP                                                                   
         MNOTE 0,'INVALID LANGUAGE NUMBER'                                      
         MEXIT                                                                  
.*                                                                              
.WCERR   ANOP                                                                   
         MNOTE 0,'INVALID PRINT WIDTH'                                          
         MEXIT                                                                  
.*                                                                              
.PSERR   ANOP                                                                   
         MNOTE 0,'INVALID PERSONNEL LEDGER SECURITY REPORT TYPE'                
         MEXIT                                                                  
.*                                                                              
.BTERR   ANOP                                                                   
         MNOTE 0,'INVALID BUCKET TYPE'                                          
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
