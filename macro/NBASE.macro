*          DATA SET MCNBASE    AT LEVEL 004 AS OF 09/08/13                      
         MACRO                                                                  
&NAME    NBASE &D,&ID,&WORK=,&CLEAR=NO,&SAVE=YES,&RR=                           
         LCLA  &I,&J,&K,&L,&M,&WSL,&WSNO                                        
         LCLC  &RL                                                              
.*                                                                              
         REQUS                                                                  
.*                                                                              
         AIF   ('&SAVE' EQ 'NO').LNOSV1                                         
&NAME    STM   RE,RC,12(RD)                                                     
         AGO   .L0                                                              
.LNOSV1  ANOP                                                                   
&NAME    NOP   12(RD)                                                           
.L0      ANOP                                                                   
         BASR  RB,0                                                             
         LA    RB,0(RB)                                                         
         SH    RB,12(RB)                                                        
         B     32(RB)                                                           
         DC    AL2(6)                                                           
         AIF   (T'&D NE 'O').L1                                                 
         DC    AL2(0)                                                           
         AGO   .L3                                                              
.L1      AIF   (T'&D NE 'N').L2                                                 
&WSL     SETA  8*&D                                                             
         DC    AL2(&WSL)                                                        
         AGO   .L3                                                              
.L2      ANOP                                                                   
         DC    AL2((&D+7)/8*8)                                                  
.L3      AIF   (T'&ID NE 'O').L4                                                
         DC    CL8' '                                                           
         AGO  .L5                                                               
.L4      ANOP                                                                   
         DC    CL8'&ID'                                                         
.L5      ANOP                                                                   
         DC    AL2(4096)                                                        
.*                                                                              
&K       SETA  32                                                               
&WSNO    SETA  0                                                                
         AIF   (T'&WORK EQ 'O').L6                                              
&I       SETA  2                                                                
         AGO   .LA                                                              
.L6      ANOP                                                                   
&I       SETA  3                                                                
         AIF   (N'&SYSLIST LT 3).L7                                             
         AIF   (T'&SYSLIST(3) EQ 'O').L7                                        
         AIF   (K'&SYSLIST(3) NE 2).LA                                          
         AIF   ('&SYSLIST(3,1)' EQ 'R').L8                                      
         AGO   .LA                                                              
.L7      MNOTE 8,'MISSING WORK AREA - MUST SET RD AND MVS LINKAGES'             
&WSNO    SETA  1                                                                
         AGO   .LA                                                              
.L8      MNOTE 8,'INVALID WORK AREA - RD AND MVS LINKAGES NOT SET'              
&WSNO    SETA  2                                                                
.*                                                                              
.LA      ANOP                                                                   
&J       SETA  N'&SYSLIST-&I                                                    
         USING *-&K,RB                                                          
         AIF   (&J LE 0).LC                                                     
&K       SETA  &K+4                                                             
&L       SETA  0                                                                
&RL      SETC 'RB'                                                              
         LH    RC,30(RB)                                                        
.*                                                                              
.LB      ANOP                                                                   
&I       SETA  &I+1                                                             
&K       SETA  &K+4                                                             
&L       SETA  &L+1                                                             
&M       SETA  4096*&L-&K                                                       
         AIF   ('&SYSLIST(&I)' NE 'RC').LB1                                     
         AIF   (&WSNO EQ 1 AND &J EQ 1).LB1                                     
         MNOTE 8,'CANT USE RC AS A BASE REGISTER'                               
.LB1     ANOP                                                                   
         LA    &SYSLIST(&I),0(RC,&RL)                                           
         USING *+&M,&SYSLIST(&I)                                                
&J       SETA  &J-1                                                             
         AIF   (&J EQ 0).LC                                                     
&RL      SETC  '&SYSLIST(&I)'                                                   
         AGO   .LB                                                              
.*                                                                              
.LC      AIF   (&WSNO NE 0).LF                                                  
         AIF   (T'&WORK EQ 'O').LD                                              
         AIF   ('&WORK'(1,2) EQ 'A(').LC3                                       
         AIF   ('&WORK'(1,2) EQ 'V(').LC3                                       
         AIF   (T'&WORK EQ 'C').LC2                                             
         AIF   (T'&WORK EQ 'H').LC2                                             
         AIF   (T'&WORK EQ 'F').LC2                                             
         AIF   (T'&WORK EQ 'D').LC2                                             
         L     RC,&WORK                                                         
         AGO   .LDR                                                             
.LC2     ANOP                                                                   
         LA    RC,&WORK                                                         
         AGO   .LE                                                              
.LC3     ANOP                                                                   
         L     RC,=&WORK                                                        
         AGO   .LDR                                                             
.*                                                                              
.LD      AIF   ('&SYSLIST(3)'(1,2) EQ 'A(').LD3                                 
         AIF   ('&SYSLIST(3)'(1,2) EQ 'V(').LD3                                 
         AIF   (T'&SYSLIST(3) EQ 'C').LD2                                       
         AIF   (T'&SYSLIST(3) EQ 'H').LD2                                       
         AIF   (T'&SYSLIST(3) EQ 'F').LD2                                       
         AIF   (T'&SYSLIST(3) EQ 'D').LD2                                       
         L     RC,&SYSLIST(3)                                                   
         AGO   .LDR                                                             
.LD2     ANOP                                                                   
         LA    RC,&SYSLIST(3)                                                   
         AGO   .LE                                                              
.LD3     ANOP                                                                   
         L     RC,=&SYSLIST(3)                                                  
         AGO   .LDR                                                             
.LDR     ANOP                                                                   
         CNOP  0,4                                                              
         B     *+8                                                              
         DC    A(*)                                                             
         LA    RF,*-4                                                           
         S     RF,*-8                                                           
         AR    RC,RF                                                            
.*                                                                              
.LE      AIF   ('&SAVE' EQ 'NO').LNOSV2                                         
         SR    RF,RF                                                            
         ICM   RF,3,20(RB)                                                      
         LA    RF,16(RC,RF)                                                     
         ST    RF,8(RD)                                                         
         ST    RD,4(RF)                                                         
         LR    RD,RF                                                            
         MVC   0(4,RC),24(RB)                                                   
         XC    4(4,RC),4(RC)                                                    
         ST    RD,8(RC)                                                         
         ST    R1,12(RC)                                                        
         LA    RC,16(RC)                                                        
         AGO   .LF                                                              
.LNOSV2  ANOP                                                                   
         SR    RF,RF                                                            
         ICM   RF,3,20(RB)                                                      
         LA    RF,16(RC,RF)                                                     
         NOP   8(RD)                                                            
         ST    RC,4(RF)                                                         
         LR    RD,RF                                                            
         MVC   0(4,RC),24(RB)                                                   
         XC    12(4,RC),4(RC)                                                   
         ST    RD,8(RC)                                                         
         NOP   12(RC)                                                           
         LA    RC,16(RC)                                                        
.*                                                                              
.LF      AIF   ('&CLEAR' EQ 'NO').LG                                            
         LR    R0,RC                                                            
         LR    R1,RD                                                            
         SR    R1,RC                                                            
         BZ    *+8                                                              
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
.*                                                                              
.LG      AIF   ('&SAVE' EQ 'NO').LNOSV3                                         
         L     RE,4(RD)                                                         
         LM    RE,R1,12(RE)                                                     
         AGO   .LH                                                              
.LNOSV3  ANOP                                                                   
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         SR    RE,RE                                                            
         LR    RF,RB                                                            
.*                                                                              
.LH      AIF   (T'&RR EQ 'O').LI                                                
         CNOP  0,4                                                              
         B     *+8                                                              
         DC    A(*)                                                             
         LA    &RR,*-4                                                          
         S     &RR,*-8                                                          
.*                                                                              
.LI      ANOP                                                                   
         MEND                                                                   
