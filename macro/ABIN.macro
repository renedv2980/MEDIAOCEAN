*          DATA SET MCABIN     AT LEVEL 047 AS OF 08/03/04                      
.**********************************************************************         
.*      ABIN IS A MACRO TO PERFORM BINARY ARITHMETIC OPERATIONS       *         
.*                                                                    *         
.*PARAMETERS:&OP=A(ADD),S(SUBSTRACT),M(MULTIPLY),D(DIVIDE)  POSITIONAL*         
.*        &I1  = 1ST INPUT                                  POSITIONAL*         
.*        &I2  = 2ND INPUT                                  POSITIONAL*         
.*        &OT  = OUTPUT AREA                                KEYWORD   *         
.*        &RG  = N OR (N,M) ;REGISTER TO USE(0-F)           KEYWORD   *         
.*        &SV  = SAVE AREA FOR THE USED REGISTERS           KEYWORD   *         
.*        &KEEP= N(NORMAL);Y(DON'T RESTORE THE REGISTERS.             *         
.*DON'T SAVE THE RESULTS.INCOMPATIBLE WITH &OT=SOMETHING)   KEYWORD   *         
.*                                                                    *         
.*   FOR DIVISION ONLY:                                               *         
.*        &RD  = N(NO ROUNDING);Y(ROUNDING)                 KEYWORD   *         
.*        &MF  = MULTIPLICATION FACTOR(BEFORE DIVISION)     KEYWORD   *         
.*                                                                    *         
.*  NOTE:&OP,&I1,&I2 AND &RG ARE NECESSARY OPERANDS IN THE MACRO CALL.*         
.**********************************************************************         
         MACRO                                                                  
&NAME    ABIN  &OP,&I1,&I2,&OT=,&SV=,&MF=,&RG=(0,14),&KEEP=N,&RD=Y              
         GBLA  &LI,&FOL                                                         
         GBLC  &O,&T,&M,&V                                                      
         LCLA  &R1,&R2,&R3,&R4,&LI1,&LI2,&LT,&LTT,&LV,&SA                       
         LCLC  &RG1,&RG2,&RG3,&RG4,&ND,&NT,&O1,&O2,&OST,&M1,&M2,&MT,&O3         
         LCLC  &T1,&T2,&TT,&NI,&SAV,&MV,&TV,&OM,&S1,&S2,&Z,&LAB,&SC,&O4         
         LCLB  &SDEF,&FOP1,&FOP2,&FOP3,&IC,&ID,&IT,&L1,&L2,&IOP,&IW             
         AIF   (T'&NAME EQ 'O').LBB                                             
&NAME    ORG                                                                    
.LBB     AIF   (T'&RG EQ 'O' OR T'&I1 EQ 'O' OR T'&I2 EQ 'O').LX1               
         AIF   ('&OP' EQ 'A' OR '&OP' EQ 'S' OR '&OP' EQ 'M').LB                
         AIF   ('&OP' NE 'D').LX1                                               
.LB      AIF   (T'&OT NE 'O' AND '&KEEP' EQ 'Y').LX1                            
.*                                                                              
.*  OPERANDS HOUSEKEEPING                                                       
.*                                                                              
&IOP     SETB  ('&OP' EQ 'M' OR '&OP' EQ 'D')                                   
&LAB     SETC  'X@'.'&SYSNDX'.'#X'                                              
&FOL     SETA  0                                                                
&SA      SETA  31                                                               
&SC      SETC  'L'                                                              
         AIF   ('&OP' EQ 'D' AND '&RD' EQ 'Y').LB15                             
&SA      SETA  32                                                               
.LB15    AIF   ('&OP' NE 'D').LB16                                              
&SC      SETC  'A'                                                              
.LB16    ANOP                                                                   
.*                                                                              
         LOS   &I1,&OP                                                          
         AIF   (&LI EQ 0).LX1                                                   
&O1      SETC  '&O'                                                             
&LI1     SETA  &LI                                                              
&LI      SETA  0                                                                
&T1      SETC  '&T'                                                             
&M1      SETC  '&M'                                                             
&FOP1    SETB  ('&T1' EQ 'N' OR '&T1' EQ 'U' AND '&O1'(1,1) EQ '=')             
         AIF   ('&O1'(1,1) NE '%').LB17                                         
&O3      SETC  '&O1'(2,K'&O1-1)                                                 
.LB17    AIF   (&FOP1).LB11                                                     
&S1      SETC  'O'                                                              
         AGO   .LB12                                                            
.LB11    ANOP                                                                   
&S1      SETC  '&O1'(2,1)                                                       
.*                                                                              
.LB12    LOS   &I2,&OP                                                          
         AIF   (&LI EQ 0).LX1                                                   
&O2      SETC  '&O'                                                             
&LI2     SETA  &LI                                                              
&LI      SETA  0                                                                
&T2      SETC  '&T'                                                             
&M2      SETC  '&M'                                                             
&FOP2    SETB  ('&T2' EQ 'N' OR '&T2' EQ 'U' AND '&O2'(1,1) EQ '=')             
         AIF   ('&O2'(1,1) NE '%').LB18                                         
&O4      SETC  '&O2'(2,K'&O2-1)                                                 
.LB18    AIF   (&FOP2).LB13                                                     
&S2      SETC  'O'                                                              
         AGO   .LB14                                                            
.LB13    ANOP                                                                   
&S2      SETC  '&O2'(2,1)                                                       
.*                                                                              
.LB14    ANOP                                                                   
&FOP3    SETB  ('&S2' NE 'A' AND '&S2' NE 'V')                                  
&FOL     SETA  1                                                                
         AIF   (T'&OT EQ 'O').LB5                                               
         LOS   &OT,&OP                                                          
         AIF   (&LI EQ 0).LX1                                                   
&OST     SETC  '&O'                                                             
&LT      SETA  &LI                                                              
&LI      SETA  0                                                                
&TT      SETC  '&T'                                                             
         AIF   (&LT GT 8).LB6                                                   
         AIF   (&LT GT 4).LB7                                                   
&LTT     SETA  &LT                                                              
         AGO   .LB8                                                             
.LB6     ANOP                                                                   
&LTT     SETA  8                                                                
         AGO   .LB8                                                             
.LB7     ANOP                                                                   
&LTT     SETA  4                                                                
.LB8     ANOP                                                                   
&MT      SETC  '&M'                                                             
.*                                                                              
.LB5     AIF   (T'&SV EQ 'O').LB10                                              
         LOS   &SV,&OP                                                          
         AIF   (&LI EQ 0).LX1                                                   
&SAV     SETC  '&O'                                                             
&LV      SETA  &LI                                                              
&LI      SETA  0                                                                
&TV      SETC  '&T'                                                             
&MV      SETC  '&M'                                                             
.*                                                                              
.LB10    AIF   (T'&OT EQ 'O' OR '&TT' NE 'U').LB1                               
         AIF   ('&OST'(1,1) EQ '=').LB1                                         
&NT      SETC  '&OST'(1,K'&OST-3).'&LTT,'.'&OST'(K'&OST-2,3)                    
.LB1     AIF   ('&T2' NE 'U').LB9                                               
         AIF   ('&O2'(1,1) EQ '=').LB9                                          
&ND      SETC  '&O2'(1,K'&O2-3).'&LI2,'.'&O2'(K'&O2-2,3)                        
.LB9     AIF   ('&T1' NE 'U').LL4                                               
         AIF   ('&O1'(1,1) EQ '=').LL4                                          
&NI      SETC  '&O1'(1,K'&O1-3).'&LI1,'.'&O1'(K'&O1-2,3)                        
.*                                                                              
.* REGISTER DEFINITIONS                                                         
.*                                                                              
.LL4     ANOP                                                                   
&R1      SETA  &RG(1)                                                           
&R2      SETA  &RG(1)+1                                                         
&R3      SETA  &RG(1)+2                                                         
&R4      SETA  &RG(2)                                                           
&RG1     SETC  'R&R1'                                                           
&RG2     SETC  'R&R2'                                                           
&RG3     SETC  'R&R3'                                                           
         AIF   (&R1 LT 0 OR &R1 GT 14 OR &R4 LT 0 OR &R4 GT 15).LX1             
         AIF   (&R1 LT 8).LL11                                                  
.*                                                                              
         AIF   (&R1 NE 8).LL5                                                   
.LL6     REG   &R3                                                              
&RG3     SETC  'R&V'                                                            
         AGO   .LL11                                                            
.LL5     AIF   (&R1 NE 9).LL7                                                   
.LL8     REG   &R2                                                              
&RG2     SETC  'R&V'                                                            
         AIF   (&R1 EQ 14).LL11                                                 
         AGO   .LL6                                                             
.LL7     REG   &R1                                                              
&RG1     SETC  'R&V'                                                            
         AGO   .LL8                                                             
.*                                                                              
.LL11    AIF   (N'&RG GT 1).LL1                                                 
         AIF   (&R1 NE 14).LL2                                                  
&RG3     SETC  'RD'                                                             
         AGO   .LL9                                                             
.*                                                                              
.LL1     ANOP                                                                   
&RG3     SETC  'R&R4'                                                           
         AIF   (&R4 LT 10).LL2                                                  
         REG   &R4                                                              
&RG3     SETC  'R&V'                                                            
.LL9     ANOP                                                                   
&IW      SETB  ('&T1' EQ 'R' AND T'&OT EQ 'O')                                  
         AIF   ('&T2' NE 'R').LL10                                              
&RG3     SETC  '&O2'                                                            
.LL10    AIF   (&IOP).LL2                                                       
         AIF   (NOT &IW).LL2                                                    
&RG2     SETC  '&O1'                                                            
.*                                                                              
.* GENERAL INSTRUCTIONS                                                         
.*                                                                              
.LL2     ANOP                                                                   
&IC      SETB  ('&T1' EQ 'U' AND (&LI1 EQ 2 OR &LI1 EQ 4))                      
&L1      SETB  ('&T1' EQ 'N' OR &IC)                                            
&ID      SETB  ('&T2' EQ 'U' AND (&LI2 EQ 2 OR &LI2 EQ 4))                      
&L2      SETB  ('&T2' EQ 'N' OR &ID)                                            
&IT      SETB  ('&TT' EQ 'U' AND (&LT EQ 2 OR &LT GE 4))                        
         AIF   ('&T2' EQ 'R').LL                                                
         AIF   ('&O2'(1,1) EQ '%').LL30                                         
         AIF   (NOT &IOP AND &L2 AND '&S2' NE 'X').LL                           
         AIF   (&IOP AND &LI2 EQ 4 AND &L2 AND '&S2' NE 'X').LL                 
         AIF   (T'&SV EQ 'O').LL3                                               
         AIF   ('&TV' EQ 'U' AND '&SAV'(1,1) EQ '=').LL3                        
         AIF   (&IOP).LL20                                                      
         AIF   (&LV LT 8).LL3                                                   
         AIF   ('&TV' EQ 'U').LL27                                              
         STCM  &RG3,&MV,&SAV.+4                                                 
.LL3     AIF   (&L2 AND '&S2' NE 'X').LL28                                      
         AIF   (&LI2 EQ 4).LL25                                                 
         SR    &RG3,&RG3                                                        
         AIF   (&LI2 EQ 1).LL24                                                 
.LL25    ICM   &RG3,&M2,&O2                                                     
         AGO   .LL                                                              
.LL24    IC    &RG3,&O2                                                         
         AGO   .LL                                                              
.LL28    AIF   (&LI2 EQ 4).LL29                                                 
         LH    &RG3,&O2                                                         
         AGO   .LL                                                              
.LL29    L     &RG3,&O2                                                         
         AGO   .LL                                                              
.LL30    LA    &RG3,&O4                                                         
         AGO   .LL                                                              
.LL27    ST    &RG3,4+&SAV                                                      
         AGO   .LL3                                                             
.LL20    AIF   (&LV LT 12).LL3                                                  
         AIF   ('&TV' EQ 'U').LL26                                              
         STCM  &RG3,&MV,&SAV.+8                                                 
         AGO   .LL3                                                             
.LL26    ST    &RG3,8+&SAV                                                      
         AGO   .LL3                                                             
.*                                                                              
.*                                                                              
.*                                                                              
.LL      AIF   ('&OP' EQ 'A' OR '&OP' EQ 'S').LAS                               
         AIF   ('&OP' EQ 'M' OR '&OP' EQ 'D').LG                                
         AGO   .LX                                                              
.*                                                                              
.* ADDITION&SUBSTRACTION                                                        
.*                                                                              
.LAS     AIF   (T'&SV EQ 'O' OR &IW).LA1                                        
         AIF   ('&TV' EQ 'U' AND '&SAV'(1,1) EQ '=' OR &LV LT 4).LA1            
         AIF   ('&TV' EQ 'R').LA6                                               
         AIF   ('&TV' EQ 'U').LA27                                              
         STCM  &RG2,&MV,&SAV                                                    
         AGO   .LA1                                                             
.LA27    ST    &RG2,&SAV                                                        
         AGO   .LA1                                                             
.LA6     LR    &SAV,&RG2                                                        
.LA1     AIF   (&IW).LA11                                                       
         AIF   ('&T1' EQ 'R' AND T'&OT NE 'O').LA33                             
         AIF   ('&O1'(1,1) EQ '%').LA32                                         
         AIF   (&L1 AND '&S1' NE 'X').LA16                                      
         AIF   (&LI1 EQ 4).LA18                                                 
         SR    &RG2,&RG2                                                        
         AIF   (&LI1 EQ 1).LA10                                                 
.LA18    ICM   &RG2,&M1,&O1                                                     
         AGO   .LA11                                                            
.LA10    IC    &RG2,&O1                                                         
         AGO   .LA11                                                            
.LA16    AIF   (&LI1 EQ 2).LA9                                                  
         L     &RG2,&O1                                                         
         AGO   .LA11                                                            
.LA9     AIF   ('&T1' EQ 'N').LA30                                              
.LA31    LH    &RG2,&O1                                                         
         AGO   .LA11                                                            
.LA32    LA    &RG2,&O3                                                         
         AGO   .LA11                                                            
.LA33    LR    &RG2,&O1                                                         
         AGO   .LA11                                                            
.LA30    AIF   (&I1 GT 4095).LA31                                               
         LA    &RG2,&I1                                                         
.*                                                                              
.*     ADDITION OR SUBSTRACTION                                                 
.*                                                                              
.LA11    AIF   ('&T2' EQ 'R').AOS3                                              
         AIF   (&L2 AND '&S2' NE 'X' AND '&O2'(1,1) NE '%').AOS1                
.AOS3    &OP.R &RG2,&RG3                                                        
         AGO   .LA3                                                             
.AOS1    AIF   (&LI2 EQ 4).AOS2                                                 
         &OP.H &RG2,&O2                                                         
         AGO   .LA3                                                             
.AOS2    &OP   &RG2,&O2                                                         
.*                                                                              
.*  ADDITION&SUBSTRACTION                                                       
.*                                                                              
.LA3     AIF   (T'&OT NE 'O').LA4                                               
         AIF   ('&KEEP' EQ 'Y' OR &FOP1).LX                                     
         AIF   ('&T1' EQ 'R').LA5                                               
         AIF   (&L1).LA14                                                       
         AIF   (&LI1 EQ 1).LA15                                                 
         STCM  &RG2,&M1,&O1                                                     
         AGO   .LA5                                                             
.LA14    AIF   (&LI1 EQ 4).LA17                                                 
         STH   &RG2,&O1                                                         
         AGO   .LA5                                                             
.LA15    STC   &RG2,&O1                                                         
         AGO   .LA5                                                             
.LA17    ST    &RG2,&O1                                                         
         AGO   .LA5                                                             
.LA4     AIF   ('&TT' EQ 'R').LA34                                              
         AIF   (&IT).LA26                                                       
         AIF   (&LT EQ 1).LA21                                                  
         AGO   .LA23                                                            
.LA26    AIF   (&LT EQ 2).LA22                                                  
         ST    &RG2,&OST                                                        
         AGO   .LA5                                                             
.LA21    STC   &RG2,&OST                                                        
         AGO   .LA5                                                             
.LA22    STH   &RG2,&OST                                                        
         AGO   .LA5                                                             
.LA34    LR    &OST,&RG2                                                        
         AGO   .LA5                                                             
.LA23    STCM  &RG2,&MT,&OST                                                    
.LA5     AIF   (T'&SV EQ 'O' OR &IW).LX                                         
         AIF   ('&TV' EQ 'U' AND '&SAV'(1,1) EQ '=' OR &LV LT 4).LX             
         AIF   ('&TV' EQ 'R').LA7                                               
         AIF   ('&TV' EQ 'U').LA28                                              
         ICM   &RG2,&MV,&SAV                                                    
         AGO   .LA20                                                            
.LA28    L     &RG2,&SAV                                                        
         AGO   .LA20                                                            
.LA7     LR    &RG2,&SAV                                                        
.LA20    AIF   ('&T2' EQ 'R').LX                                                
         AIF   (&L2 AND '&S2' NE 'X' OR &LV LT 8).LX                            
         AIF   (T'&SV EQ 'U').LA29                                              
         ICM   &RG3,&MV,&SAV.+4                                                 
         AGO   .LX                                                              
.LA29    L     &RG3,4+&SAV                                                      
         AGO   .LX                                                              
.*                                                                              
.*  MULTIPLICATION&DIVISION                                                     
.*                                                                              
.LG      AIF   (K'&O2 LE 4).LG33                                                
&OM      SETC  '&O2'(4,K'&O2-4)                                                 
&Z       SETC  (K'&O2-4)'0'                                                     
.LG33    AIF   (T'&SV EQ 'O').LG1                                               
         AIF   ('&TV' EQ 'U' AND '&SAV'(1,1) EQ '=' OR &LV LT 8).LG1            
         AIF   ('&TV' EQ 'U').LG9                                               
         STCM  &RG1,&MV,&SAV                                                    
         STCM  &RG2,&MV,&SAV.+4                                                 
         AGO   .LG1                                                             
.LG9     STM   &RG1,&RG2,&SAV                                                   
.LG1     AIF   ('&RD' EQ 'N' OR '&OP' EQ 'M').LG6                               
         SR    &RG2,&RG2                                                        
.LG6     AIF   ('&T1' EQ 'R').LG8                                               
         AIF   ('&O1'(1,1) EQ '%').LG36                                         
         AIF   (&L1 AND '&S1' NE 'X').LG25                                      
         AIF   (&LI1 EQ 4).LG24                                                 
         SR    &RG1,&RG1                                                        
         AIF   (&LI1 EQ 1).LG11                                                 
.LG24    ICM   &RG1,&M1,&O1                                                     
         AGO   .LG7                                                             
.LG11    IC    &RG1,&O1                                                         
         AGO   .LG7                                                             
.LG25    AIF   (&LI1 EQ 4).LG26                                                 
         LH    &RG1,&O1                                                         
         AGO   .LG7                                                             
.LG26    AIF   ('&T1' EQ 'N').LG34                                              
.LG35    L     &RG1,&O1                                                         
         AGO   .LG7                                                             
.LG36    LA    &RG1,&O3                                                         
         AGO   .LG7                                                             
.LG8     LR    &RG1,&O1                                                         
         AGO   .LG7                                                             
.LG34    AIF   (&I1 GT 4095).LG35                                               
         LA    &RG1,&I1                                                         
.LG7     SRD&SC &RG1,&SA                                                        
         AIF   ('&OP' EQ 'D').LD                                                
.*                                                                              
.*   MULTIPLICATION                                                             
.*                                                                              
         AIF   ('&O2'(1,1) EQ '%' OR '&T2' EQ 'R').LM1                          
         AIF   (&L2 AND &LI2 EQ 4 AND '&S2' NE 'X').LM2                         
.LM1     MR    &RG1,&RG3                                                        
         AGO   .LG2                                                             
.LM2     M     &RG1,&O2                                                         
         AGO   .LG2                                                             
.*                                                                              
.*   DIVISION                                                                   
.*                                                                              
.LD      AIF   (T'&MF EQ 'O').LD1                                               
         M     &RG1,=F'&MF'                                                     
.LD1     AIF   ('&O2'(1,1) EQ '%' OR '&T2' EQ 'R').LD2                          
         AIF   (NOT &L2 OR &LI2 NE 4 OR '&S2' EQ 'X').LD2                       
         AIF   ('&T2' EQ 'U').LD3                                               
         AIF   ('&T2' EQ 'N').LD5                                               
         CLC   &O2.(4),=F'0'                                                    
         AGO   .LD4                                                             
.LD3     AIF   ('&O2'(1,1) EQ '=').LD5                                          
         CLC   &ND,=F'0'                                                        
         AGO   .LD4                                                             
.LD5     AIF   (K'&O2 LE 4).LD6                                                 
         AIF   (&L2 AND '&OM' NE '&Z' AND &FOP3).LD7                            
.LD6     CLC   &O2,=F'0'                                                        
.LD4     BE    &LAB                                                             
.LD7     D     &RG1,&O2                                                         
         AGO   .LD9                                                             
.LD2     AIF   ('&S2' EQ 'X' AND '&OM' NE '&Z').LD8                             
         C     &RG3,=F'0'                                                       
         BE    &LAB                                                             
.LD8     DR    &RG1,&RG3                                                        
.LD9     AIF   ('&RD' EQ 'N').LG2                                               
         LTR   &RG2,&RG2                                                        
         BM    *+8                                                              
         AH    &RG2,=H'1'                                                       
         SR&SC &RG2,1                                                           
.*                                                                              
.*  MULTIPLICATION&DIVISION                                                     
.*                                                                              
.LG2     AIF   (T'&OT NE 'O').LG3                                               
         AIF   ('&KEEP' EQ 'Y' OR &FOP1).LX                                     
         AIF   ('&T1' EQ 'R').LG10                                              
         AIF   (&IC).LG27                                                       
         AIF   (&LI1 EQ 1).LG28                                                 
         STCM  &RG2,&M1,&O1                                                     
         AGO   .LG4                                                             
.LG28    STC   &RG2,&O1                                                         
         AGO   .LG4                                                             
.LG27    AIF   (&LI1 EQ 4).LG18                                                 
         STH   &RG2,&O1                                                         
         AGO   .LG4                                                             
.LG18    ST    &RG2,&O1                                                         
         AGO   .LG4                                                             
.LG10    LR    &O1,&RG2                                                         
         AGO   .LG4                                                             
.LG3     AIF   ('&TT' EQ 'R').LG12                                              
         AIF   (&IT).LG29                                                       
         AIF   (&LT GE 8).LG5                                                   
         AIF   (&LT EQ 1).LG21                                                  
         STCM  &RG2,&MT,&OST                                                    
         AGO   .LG4                                                             
.LG29    AIF   (&LT GE 8).LG5                                                   
         AIF   (&LT EQ 2).LG22                                                  
         ST    &RG2,&OST                                                        
         AGO   .LG4                                                             
.LG21    STC   &RG2,&OST                                                        
         AGO   .LG4                                                             
.LG22    STH   &RG2,&OST                                                        
         AGO   .LG4                                                             
.LG5     AIF   (&IT).LG23                                                       
         STCM  &RG1,&MT,&OST                                                    
         STCM  &RG2,&MT,&OST.+4                                                 
         AGO   .LG4                                                             
.LG12    LR    &OST,&RG2                                                        
         AGO   .LG4                                                             
.LG23    STM   &RG1,&RG2,&OST                                                   
.LG4     AIF   (T'&SV EQ 'O').LX                                                
         AIF   ('&TV' EQ 'U' AND '&SAV'(1,1) EQ '=' OR &LV LT 8).LX             
         AIF   ('&TV' EQ 'U').LG30                                              
         ICM   &RG1,&MV,&SAV                                                    
         ICM   &RG2,&MV,&SAV.+4                                                 
         AGO   .LG31                                                            
.LG30    LM    &RG1,&RG2,&SAV                                                   
.LG31    AIF   ('&T2' EQ 'R').LX                                                
         AIF   (&LV LT 12 OR &LI2 EQ 4 AND &L2 AND '&S2' NE 'X').LX             
         AIF   ('&TV' EQ 'U').LG32                                              
         ICM   &RG3,&MV,&SAV.+8                                                 
         AGO   .LX                                                              
.LG32    L     &RG3,8+&SAV                                                      
.*                                                                              
.*  CLEANING IF DIVISION BY 0                                                   
.*                                                                              
.LX      AIF   ('&OP' NE 'D').LX1                                               
         AIF   ('&T2' EQ 'R').LX8                                               
         AIF   (K'&O2 LE 4).LX7                                                 
         AIF   (&L2 AND '&S2' NE 'O' AND '&OM' NE '&Z' AND &FOP3).LX1           
.LX7     AIF   ('&KEEP' EQ 'Y').LY                                              
         AIF   (T'&OT EQ 'O' AND &FOP1).LY                                      
         AIF   (T'&OT EQ 'O').LX4                                               
         AIF   ('&OST'(1,1) EQ '=').LY                                          
.LX4     B     *+10                                                             
         AIF   (T'&OT EQ 'O').LX2                                               
         AIF   ('&TT' EQ 'U').LX3                                               
&LAB     MVC   &OST.(&LTT),=&LTT.X'00'                                          
         AGO   .LX1                                                             
.LX3     ANOP                                                                   
&LAB     MVC   &NT,=&LTT.X'00'                                                  
         AGO   .LX1                                                             
.LY      ANOP                                                                   
&LAB     B     *+4                                                              
         AGO   .LX1                                                             
.LX2     AIF   ('&T1' NE 'U').LX6                                               
&LAB     MVC   &NI,=F'0'                                                        
         AGO   .LX1                                                             
.LX8     ANOP                                                                   
         B     *+6                                                              
&LAB     SR    &RG3,&RG3                                                        
         AGO   .LX1                                                             
.LX6     ANOP                                                                   
&LAB     MVC   &O1.(&LI1),=F'0'                                                 
.LX1     ANOP                                                                   
         MEND                                                                   
