*          DATA SET MCLKARY    AT LEVEL 003 AS OF 03/11/08                      
         MACRO                                                                  
         LKARY &TYPE,&PARM1,&PARM2                                              
.*                                                                              
         GBLB  &FAST_ASSEMBLY      FAST ASSEMBLY OPTION                         
         GBLA  &NAN#               NUMBER OF ENTRIES IN &NAN ARRAY              
         GBLA  &NRN#               NUMBER OF ENTRIES IN &NRN ARRAY              
         GBLA  &NDN#               NUMBER OF ENTRIES IN &NDN ARRAY              
         GBLB  &ROOTN(1000)        ROOT NODE ARRAY                              
         GBLC  &NAN(1000)          NODE TRACE ARRAY                             
         GBLC  &NRN(500)           ROUTINE NAME ARRAY                           
         GBLC  &NDN(500)           ROW NAME ARRAY                               
.*                                                                              
         LCLA  &L#(25)                                                          
         LCLA  &LL(25)                                                          
         LCLA  &I,&J,&K,&L,&M,&N,&O,&P,&Q,&Y,&Z,&LV                             
         LCLB  &ROOT                                                            
         LCLB  &TRACE                                                           
         LCLB  &TT(1000)                                                        
         LCLB  &TF                                                              
         LCLB  &ANY                                                             
         LCLC  &TN                                                              
         LCLC  &L1(25)                                                          
         LCLC  &L2(25)                                                          
         LCLC  &S1,&S2,&S3                                                      
.*                                                                              
         AIF   (T'&TYPE EQ 'O').ERROR1                                          
         AIF   ('&TYPE'(1,1) NE 'A' AND '&TYPE'(1,1) NE '+').LKA06              
         AIF   (T'&PARM1 EQ 'O' OR T'&PARM2 EQ 'O').ERROR1                      
&ROOT    SETB  ('&TYPE'(1,1) EQ '+')                                            
&S1      SETC  '&PARM1'.'-'.'&PARM2'                                            
&I       SETA  0                                                                
.*                                                                              
         ACTR  2000                                                             
.LKA02   ANOP  ,                   TEST DUPLICATE INDEX ENTRY                   
&I       SETA  &I+1                                                             
         AIF   (&I GT &NAN#).LKA04                                              
         AIF   ('&NAN(&I)' NE '&S1').LKA02                                      
         AGO   .LKAX               YES - IGNORE                                 
.*                                                                              
.LKA04   ANOP  ,                   CREATE NEW INDEX ENTRY                       
&NAN#          SETA  &NAN#+1       BUMP INDEX COUNT                             
&NAN(&NAN#)    SETC  '&S1'         SET INDEX VALUE (A-B)                        
&ROOTN(&NAN#)  SETB  (&ROOT)       SET ROOT NODE FLAG                           
         AIF   (&FAST_ASSEMBLY).LKAX                                            
         AGO   .LKA08              DO INDEX INTEGRITY CHECK                     
.*                                                                              
.LKA06   ANOP  ,                                                                
&TRACE   SETB  ('&TYPE'(1,1) EQ 'T')                                            
         AIF   (NOT &TRACE).LKA60                                               
.*                                 DO ARRAY TRACE/INTEGRITY CHECK               
.LKA08   ANOP  ,                                                                
&I       SETA  0                   INITIALIZE MASTER INDEX VALUE                
.*                                                                              
.LKA10   ANOP  ,                                                                
&LV      SETA  0                   INITIALIZE NESTING LEVEL                     
.*                                                                              
.LKA12   ANOP  ,                   SEARCH FORWARD FOR NEXT ROOT NODE            
&I       SETA  &I+1                                                             
         AIF   (&I GT &NAN#).LKA46                                              
         AIF   (NOT &ROOTN(&I)).LKA12                                           
&N       SETA  &I                  SET INDEX NUMBER FOR NODE TRACE              
.*                                                                              
.LKA14   ANOP  ,                                                                
&LV      SETA  &LV+1               BUMP NESTING LEVEL                           
.*                                 DO NODE TRACE                                
.LKA16   ANOP  ,                                                                
&TT(&N)  SETB  1                   SET ENTRY ACTIVITY SWITCH                    
&ANY     SETB  1                   SET TRACE ACTIVITY SWITCH                    
&L       SETA  K'&NAN(&N)          SET LENGTH OF ENTRY                          
.*                                 DECONSTRUCT THE CURRENT INDEX ENTRY          
&M       SETA  ('&NAN(&N)' INDEX '-')                                           
         AIF   (&M EQ 0).ERROR3                                                 
.*                                                                              
&L       SETA  &L-&M               LENGTH OF 'TO NODE'                          
&P       SETA  &M-1                LENGTH OF 'FROM NODE'                        
&M       SETA  &M+1                DISPLACEMENT TO 'TO NODE'                    
.*                                                                              
&S1      SETC  '&NAN(&N)'(1,&P)    'FROM NODE' NAME                             
&S2      SETC  '&NAN(&N)'(&M,&L)   'TO NODE' NAME                               
.*                                                                              
&L1(&LV) SETC  '&S1'               SAVE CURRENT 'FROM NODE' NAME                
&L2(&LV) SETC  '&S2'               SAVE CURRENT 'TO NODE' NAME                  
&LL(&LV) SETA  &P                  SAVE CURRENT 'FROM NODE' LENGTH              
&L#(&LV) SETA  &N                  SAVE CURRENT INDEX VALUE                     
.*                                                                              
         AIF   (NOT &TRACE).LKA22                                               
         AIF   (&LV NE 1).LKA22                                                 
         AIF   (NOT &TF).LKA20                                                  
         AIF   ('&TN' NE '&S1').LKA18                                           
*                                                                     *         
         AGO   .LKA26                                                           
.*                                                                              
.LKA18   ANOP  ,                                                                
***********************************************************************         
&TN      SETC  '&S1'                                                            
         AGO   .LKA26                                                           
.*                                                                              
.LKA20   ANOP  ,                                                                
         PUSH  PRINT                                                            
         PRINT GEN                                                              
***************************< Output Trace >****************************         
&TF      SETB  1                                                                
&TN      SETC  '&S1'                                                            
         AGO   .LKA26                                                           
.*                                                                              
.LKA22   ANOP  ,                   DO NODE TRACE - ISSUE MNOTE ON ERROR         
&Y       SETA  0                                                                
.LKA24   ANOP  ,                                                                
&Y       SETA  &Y+1                                                             
         AIF   (&Y EQ &LV).LKA26                                                
         AIF   ('&S2' NE '&L1(&Y)').LKA24                                       
         MNOTE 12,'Circular definition ''&NAN(&N)'''                            
&ROOTN(&NAN#)  SETB  0                                                          
&NAN#          SETA  &NAN#-1                                                    
         AGO   .LKAX                                                            
.*                                                                              
.LKA26   ANOP  ,                                                                
         AIF   (NOT &TRACE).LKA36                                               
.*                                 CONSTRUCT TRACE LINE                         
&S3      SETC  '*'                                                              
&S3      SETC  '&S3'.(&LV)' '                                                   
&S3      SETC  '&S3'.'Level#'.'&LV'.' '.'&S1'.' => '.'&S2'                      
.*                                                                              
         ACTR  2000                                                             
&N       SETA  0                                                                
.LKA28   ANOP  ,                                                                
&N       SETA  &N+1                                                             
         AIF   (&N GT &NRN#).LKA30                                              
&K       SETA  ('&NRN(&N)' INDEX '-')                                           
&K       SETA  &K-1                                                             
         AIF   (&K NE K'&S2).LKA28                                              
         AIF   ('&S2' NE '&NRN(&N)'(1,&K)).LKA28                                
&O       SETA  K'&NRN(&N)-&K-1                                                  
&K       SETA  &K+2                                                             
&S3      SETC  '&S3'.',Routine='.'&NRN(&N)'(&K,&O)                              
.*                                                                              
.LKA30   ANOP  ,                                                                
         ACTR  2000                                                             
&N       SETA  0                                                                
.LKA32   ANOP  ,                                                                
&N       SETA  &N+1                                                             
         AIF   (&N GT &NDN#).LKA34                                              
&K       SETA  ('&NDN(&N)' INDEX '-')                                           
&K       SETA  &K-1                                                             
         AIF   (&K NE K'&S2).LKA32                                              
         AIF   ('&S2' NE '&NDN(&N)'(1,&K)).LKA32                                
&O       SETA  K'&NDN(&N)-&K-1                                                  
&K       SETA  &K+2                                                             
&S3      SETC  '&S3'.',Rowname='.'&NDN(&N)'(&K,&O)                              
.*                                                                              
.LKA34   ANOP  ,                                                                
&Q       SETA  K'&S3                                                            
&S3      SETC  '&S3'.(70-&Q)' '.'*'                                             
         MNOTE '&S3'                                                            
.*                                                                              
.LKA36   ANOP  ,                   LOCATE 'TO NODE' IN ARRAY                    
         ACTR  2000                                                             
&N       SETA  0                                                                
.LKA38   ANOP  ,                                                                
&N       SETA  &N+1                                                             
         AIF   (&N GT &NAN#).LKA40                                              
&Z       SETA  ('&NAN(&N)' INDEX '-')                                           
&Z       SETA  &Z-1                                                             
         AIF   (&Z NE K'&S2).LKA38                                              
         AIF   ('&S2' NE '&NAN(&N)'(1,K'&S2)).LKA38                             
         AGO   .LKA14                                                           
.*                                                                              
.LKA40   ANOP  ,                                                                
&N       SETA  &L#(&LV)            RESTORE LEVEL SAVED VALUES                   
&L       SETA  &LL(&LV)                                                         
&S1      SETC  '&L1(&LV)'(1,&L)                                                 
&S2      SETC  '&L2(&LV)'(1,&L)                                                 
.*                                                                              
         AIF   (&LV EQ 1).LKA10    BACK TO MAIN LOOP IF AT ROOT NODE            
.*                                                                              
.LKA42   ANOP  ,                   LOOK FOR NEXT POINTER AT SAME NODE           
&N       SETA  &N+1                                                             
         AIF   (&N GT &NAN#).LKA44                                              
&Z       SETA  ('&NAN(&N)' INDEX '-')                                           
&Z       SETA  &Z-1                                                             
         AIF   (&Z NE K'&S1).LKA42                                              
         AIF   ('&S1' EQ '&NAN(&N)'(1,K'&S1)).LKA16                             
         AGO   .LKA42                                                           
.*                                                                              
.LKA44   ANOP  ,                   CHAIN EXHAUSTED - BACK TO PREVIOUS           
&LV      SETA  &LV-1                                                            
         AGO   .LKA40                                                           
.*                                 END OF ARRAY                                 
.LKA46   ANOP  ,                                                                
         AIF   (NOT &TRACE).LKAX                                                
         AIF   (NOT &ANY).LKA50                                                 
************************< End Of Output Trace >************************         
.*                                                                              
.LKA50   ANOP  ,                   REPORT ON UNCHAINED ENTRIES                  
&I       SETA  0                                                                
         ACTR  2000                                                             
.*                                                                              
.LKA52   ANOP  ,                                                                
&I       SETA  &I+1                                                             
         AIF   (&I GT &NAN#).LKA54                                              
         AIF   (&TT(&I)).LKA52                                                  
         MNOTE 'Entry not processed: ''&NAN(&I)'''                              
         AGO   .LKA52                                                           
.*                                                                              
.LKA54   ANOP  ,                                                                
         POP   PRINT                                                            
         AGO   .LKAX                                                            
.*                                                                              
.LKA60   ANOP  ,                   MAINTAIN ROUTINE NAME ARRAY                  
         AIF   ('&TYPE'(1,1) NE 'R').LKA70                                      
         AIF   (T'&PARM1 EQ 'O' OR T'&PARM2 EQ 'O').ERROR1                      
         ACTR  2000                                                             
.*                                                                              
.LKA62   ANOP  ,                                                                
&I       SETA  &I+1                                                             
         AIF   (&I GT &NRN#).LKA64                                              
&K       SETA  ('&NRN(&I)' INDEX '-')                                           
         AIF   (&K EQ 0).ERROR5                                                 
&K       SETA  &K-1                                                             
         AIF   (&K NE K'&PARM1).LKA62                                           
         AIF   ('&PARM1' EQ '&NRN(&I)'(1,&K)).LKA66                             
         AGO   .LKA62                                                           
.*                                                                              
.LKA64   ANOP  ,                                                                
&NRN#          SETA  &NRN#+1                                                    
.*                                                                              
.LKA66   ANOP  ,                                                                
&NRN(&NRN#)    SETC  '&PARM1'.'-'.'&PARM2'                                      
         AGO   .LKAX                                                            
.*                                                                              
.LKA70   ANOP  ,                   MAINTAIN ROW NAME ARRAY                      
         AIF   ('&TYPE'(1,1) NE 'D').ERROR2                                     
         AIF   (T'&PARM1 EQ 'O' OR T'&PARM2 EQ 'O').ERROR1                      
         ACTR  2000                                                             
.*                                                                              
.LKA72   ANOP  ,                                                                
&I       SETA  &I+1                                                             
         AIF   (&I GT &NDN#).LKA74                                              
&K       SETA  ('&NDN(&I)' INDEX '-')                                           
         AIF   (&K EQ 0).ERROR6                                                 
&K       SETA  &K-1                                                             
         AIF   (&K NE K'&PARM1).LKA72                                           
         AIF   ('&PARM1' EQ '&NDN(&I)'(1,&K)).LKA76                             
         AGO   .LKA72                                                           
.*                                                                              
.LKA74   ANOP  ,                                                                
&NDN#          SETA  &NDN#+1                                                    
.*                                                                              
.LKA76   ANOP  ,                                                                
&NDN(&NDN#)    SETC  '&PARM1'.'-'.'&PARM2'                                      
         AGO   .LKAX                                                            
.*                                                                              
.LKAX    ANOP  ,                                                                
         MEXIT                                                                  
.*                                                                              
.ERROR1  ANOP  ,                                                                
         MNOTE 12,'Missing parameter'                                           
         MEXIT                                                                  
.*                                                                              
.ERROR2  ANOP  ,                                                                
         MNOTE 12,'Invalid parameter type S/B - +,A,R,D or T'                   
         MEXIT                                                                  
.*                                                                              
.ERROR3  ANOP  ,                                                                
         MNOTE 12,'Corrupt array entry ''&NAN(&I)'''                            
         MEXIT                                                                  
.*                                                                              
.ERROR4  ANOP  ,                                                                
         MNOTE 0,'Unreferenced array ''&PARM1'''                                
         MEXIT                                                                  
.*                                                                              
.ERROR5  ANOP  ,                                                                
         MNOTE 12,'Corrupt array entry ''&NRN(&I)'''                            
         MEXIT                                                                  
.*                                                                              
.ERROR6  ANOP  ,                                                                
         MNOTE 12,'Corrupt array entry ''&NDN(&I)'''                            
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
