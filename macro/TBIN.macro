*          DATA SET MCTBIN     AT LEVEL 005 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     TBIN  &UNITS,&DUB,&DDSTIME=NO                                          
.*                                                                              
         AIF   ('&UNITS' EQ 'MICRO').CLOCK                                      
         AIF   ('&UNITS' EQ 'CLOCK').CLOCK                                      
         AGO   .TUNITS                                                          
.*                                                                              
.****************************************************************               
.*                                                                              
.CLOCK   ANOP                                                                   
         AIF   (T'&DUB NE 'O').CLOCK1                                           
.*                                                                              
.ERR0    MNOTE 8,'TBIN - MICRO/CLOCK UNITS NEED SECOND PARM DBLWRD'             
.*                                                                              
.CLOCK1  ANOP                                                                   
&TAG     STCK  &DUB                                                             
         LM    R0,R1,&DUB          R0&R1=CLOCK WITH BIT#51=1 MICROSEC           
         AIF   ('&UNITS' EQ 'MICRO').CLOCK2                                     
         MEXIT                                                                  
.CLOCK2  ANOP                                                                   
         SRDL  R0,12               R0&R1=TIME IN MICRO SECS                     
         STM   R0,R1,&DUB                                                       
         MEXIT                                                                  
.*                                                                              
.****************************************************************               
.*                                                                              
.TUNITS  ANOP                                                                   
&TAG     TIME  TU                                                               
         SRDL  R0,32               R1=TIME IN TU'S (1/38400 SEC)                
.*                                                                              
         AIF   (T'&UNITS EQ 'O').TSECS                                          
         AIF   (K'&UNITS LT 2).ERR1                                             
         AIF   ('&UNITS'(1,2) EQ 'SE').TSECS                                    
         AIF   ('&UNITS'(1,2) EQ 'MI').TMILLI                                   
         AIF   ('&UNITS'(1,2) EQ 'TU').TTUS                                     
.*                                                                              
.ERR1    MNOTE 8,'TBIN MACRO - INVALID UNITS - SECONDS ASSUMED'                 
.*                                                                              
.TSECS   ANOP                                                                   
         D     R0,=F'38400'        R1=TIME IN SECONDS                           
         AIF   ('&DDSTIME' EQ 'NO').EXIT                                        
         LH    R0,=H'21600'        R0=6.00 AM IN SECONDS                        
         MEXIT                                                                  
.*                                                                              
.TMILLI  ANOP                                                                   
         LA    R0,1000                                                          
         MR    R0,R0                                                            
         D     R0,=F'38400'        R1=TIME IN MILLISECS                         
         AIF   ('&DDSTIME' EQ 'NO').EXIT                                        
         L     R0,=F'21600000'     R0=6.00 AM IN MILLISECS                      
         MEXIT                                                                  
.*                                                                              
.TTUS    ANOP                                                                   
         LA    R0,300                                                           
         MR    R0,R0                                                            
         D     R0,=F'38400'        R1=TIME IN 1/300 SEC UNITS                   
         AIF   ('&DDSTIME' EQ 'NO').EXIT                                        
         L     R0,=F'6480000'      R0=6.00 AM IN 1/300 SEC UNITS                
         MEXIT                                                                  
.*                                                                              
.EXIT    ANOP                                                                   
         MEND                                                                   
