*          DATA SET MCSSPEC    AT LEVEL 017 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     SSPEC  &LINE,&COL,&KEY,&DDLEN,&DDTYP                                   
         LCLA  &PL,&PN,&PD,&PK                                                  
         LCLA  &PC                                                              
         LCLC  &PCL,&PLENA                                                      
.*                                                                              
.*                                 &LINE MIGHT BE ANOTHER KEYWORD               
.*                                                                              
         AIF   ('&LINE'(1,4) EQ 'LANG').LC                                      
         AIF   ('&LINE'(1,4) EQ 'CTRY').CC                                      
.*                                                                              
.*                                 WORK OUT THE LINE                            
.*                                                                              
&PCL     SETC  '&LINE'(2,K'&LINE-1)                                             
&PL      SETA  &PCL                                                             
         AIF   (&PL EQ 0).P2                                                    
         AIF   (&PL GT 14).P2                                                   
&PC      SETA  0                                                                
         AIF   ('&LINE'(1,1) EQ 'H').P4                                         
&PL      SETA  &PL+14                                                           
&PC      SETA  8                                                                
         AIF   ('&LINE'(1,1) EQ 'M').P4                                         
&PL      SETA  &PL+2                                                            
&PC      SETA  16                                                               
         AIF   ('&LINE'(1,1) EQ 'P').P4                                         
&PL      SETA  &PL+14                                                           
&PC      SETA  24                                                               
         AIF   ('&LINE'(1,1) EQ 'F').P4                                         
.*                                                                              
.P2      MNOTE 8,'INVALID LINE'                                                 
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 COMPUTE DISPLACEMENT                         
.*                                                                              
.P4      ANOP                                                                   
         AIF   (T'&COL NE 'N').P6                                               
         AIF   (&COL EQ 0).P6                                                   
         AIF   (&COL GT 132).P6                                                 
&PL      SETA  &PL-1                                                            
&PD      SETA  &PL*132                                                          
&PD      SETA  &PD+&COL-1                                                       
&PD      SETA  &PD+&PC                                                          
.*                                                                              
.*                                 HANDLE KEYWORDS                              
.*                                                                              
&PL      SETA  K'&KEY-3                                                         
         AIF   ('&KEY'(1,1) GT 'Z').P9                                          
         AIF   ('&KEY'(1,2) EQ 'C''').P12                                       
         AIF   ('&KEY'(1,3) EQ 'SP#').PDD                                       
         AIF   ('&KEY'(3,1) EQ '#').PDD                                         
         AIF   ('&KEY'(1,3) EQ 'CLI').P5                                        
         AIF   ('&KEY'(1,2) EQ 'CL').P10                                        
.P5      ANOP                                                                   
&PK      SETA  0                                                                
         AIF   ('&KEY' EQ 'SPACES').P8                                          
&PK      SETA  1                                                                
         AIF   ('&KEY' EQ 'RUN').P8                                             
&PK      SETA  2                                                                
         AIF   ('&KEY' EQ 'REPORT').P8                                          
&PK      SETA  3                                                                
         AIF   ('&KEY' EQ 'PAGE').P8                                            
&PK      SETA  4                                                                
         AIF   ('&KEY' EQ 'PERIOD').P8                                          
&PK      SETA  5                                                                
         AIF   ('&KEY' EQ 'REQUESTOR').P8                                       
&PK      SETA  6                                                                
         AIF   ('&KEY' EQ 'MEDIA').P8                                           
         AIF   ('&KEY' EQ 'NETREP').P8                                          
&PK      SETA  7                                                                
         AIF   ('&KEY' EQ 'AGYNAME').P8                                         
&PK      SETA  8                                                                
         AIF   ('&KEY' EQ 'AGYADD').P8                                          
&PK      SETA  9                                                                
         AIF   ('&KEY' EQ 'CLIENT').P8                                          
&PK      SETA  10                                                               
         AIF   ('&KEY' EQ 'CLINAME').P8                                         
&PK      SETA  11                                                               
         AIF   ('&KEY' EQ 'PRDGRP').P8                                          
&PK      SETA  12                                                               
         AIF   ('&KEY' EQ 'PRODUCT').P8                                         
&PK      SETA  13                                                               
         AIF   ('&KEY' EQ 'PRONAME').P8                                         
&PK      SETA  15                                                               
         AIF   ('&KEY' EQ 'ESTIMATE').P8                                        
&PK      SETA  16                                                               
         AIF   ('&KEY' EQ 'ESTNAME').P8                                         
&PK      SETA  17                                                               
         AIF   ('&KEY' EQ 'ESTNUM').P8                                          
&PK      SETA  18                                                               
         AIF   ('&KEY' EQ 'MARKET').P8                                          
&PK      SETA  19                                                               
         AIF   ('&KEY' EQ 'STATION').P8                                         
&PK      SETA  20                                                               
         AIF   ('&KEY' EQ 'MKTGRP').P8                                          
&PK      SETA  21                                                               
         AIF   ('&KEY' EQ 'DAYPART').P8                                         
&PK      SETA  22                                                               
         AIF   ('&KEY' EQ 'RATING').P8                                          
&PK      SETA  23                                                               
         AIF   ('&KEY' EQ 'BOOK').P8                                            
&PK      SETA  24                                                               
         AIF   ('&KEY' EQ 'EQUIV').P8                                           
&PK      SETA  25                                                               
         AIF   ('&KEY' EQ 'COMPARE').P8                                         
&PK      SETA  26                                                               
         AIF   ('&KEY' EQ 'MGROUP').P8                                          
&PK      SETA  27                                                               
         AIF   ('&KEY' EQ 'PGROUP').P8                                          
&PK      SETA  39                                                               
         AIF   ('&KEY' EQ 'REQDETS').P8                                         
&PK      SETA  40                                                               
         AIF   ('&KEY' EQ 'CREATED').P8                                         
         MNOTE 8,'NOT A VALID KEYWORD'                                          
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.P6      ANOP                                                                   
         MNOTE 8,'COLUMN SHOULD 1-132'                                          
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 COMPLETE KEYWORD EXPANSION                   
.*                                                                              
.P8      ANOP                                                                   
&TAG     DC    X'0C05'                                                          
         DC    AL2(&PD)                                                         
         DC    AL1(&PK)                                                         
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 HANDLE LITERALS                              
.*                                                                              
.*                                                                              
.*                                 GET LENGTH OF MULTIPLE CHARACTERS            
.*                                                                              
.P9      ANOP                                                                   
&PN      SETA  1                                                                
         AIF   ('&KEY'(2,1) LE 'Z').P9A                                         
&PN      SETA  2                                                                
         AIF   ('&KEY'(3,1) LE 'Z').P9A                                         
&PN      SETA  3                                                                
         AIF   ('&KEY'(4,1) GT 'Z').P14                                         
.P9A     ANOP                                                                   
&PLENA   SETC  '&KEY'(1,&PN)                                                    
         AGO   .P11A                                                            
.P10     ANOP                                                                   
&PN      SETA  1                                                                
         AIF   ('&KEY'(4,1) EQ '''').P11                                        
&PN      SETA  2                                                                
         AIF   ('&KEY'(5,1) EQ '''').P11                                        
&PN      SETA  3                                                                
         AIF   ('&KEY'(6,1) EQ '''').P11                                        
         AGO   .P14                                                             
.*                                                                              
.*                                 DIG OUT EXPLICIT LENGTH                      
.*                                                                              
.P11     ANOP                                                                   
&PLENA   SETC  '&KEY'(3,&PN)                                                    
.P11A    ANOP                                                                   
&PL      SETA  &PLENA                                                           
         AIF   (&PL EQ 0).P14                                                   
         AIF   (&PL GT 250).P14                                                 
.*                                                                              
.*                                 GENERATE LITERAL CODE                        
.*                                                                              
.P12     ANOP                                                                   
&TAG     DC    X'0D'                                                            
&PL      SETA  &PL+4                                                            
         DC    AL1(&PL)                                                         
         DC    AL2(&PD)                                                         
         DC    &KEY                                                             
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                                                              
.P14     ANOP                                                                   
         MNOTE 8,'INVALID LITERAL EXPRESSION'                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 DATA DICTIONARY SYMBOL                       
.*                                                                              
.PDD     ANOP                                                                   
         AIF   (T'&DDLEN EQ 'O').PDERR                                          
         AIF   ('&DDLEN' EQ '2').PDD1                                           
         AIF   ('&DDLEN' EQ '3').PDD1                                           
&PL      SETA  &DDLEN+4                                                         
         AGO   .PDD2                                                            
.PDD1    ANOP                                                                   
&PL      SETA  7                                                                
.PDD2    ANOP                                                                   
&TAG     DC    X'0D'                                                            
         DC    AL1(&PL)                                                         
         DC    AL2(&PD)                                                         
         DCDD  &KEY,&DDLEN,&DDTYP,PAD=BLANKS                                    
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 SET LANGUAGE NUMBER                          
.*                                                                              
.LC      ANOP                                                                   
&PK      SETA  &COL                                                             
         AIF   (T'&COL NE 'N').LCERR                                            
&TAG     DC    X'0603'                                                          
         DC    AL1(&PK)                                                         
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 SET COUNTRY NUMBER                           
.*                                                                              
.CC      ANOP                                                                   
&PK      SETA  &COL                                                             
         AIF   (T'&COL NE 'N').CCERR                                            
&TAG     DC    X'0703'                                                          
         DC    AL1(&PK)                                                         
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.PDERR   ANOP                                                                   
         MNOTE 8,'DATA DICT MUST HAVE LENGTH'                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.LCERR   ANOP                                                                   
         MNOTE 8,'INVALID LANGUAGE NUMBER'                                      
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.CCERR   ANOP                                                                   
         MNOTE 8,'INVALID COUNTRY NUMBER'                                       
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
