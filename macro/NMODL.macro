*          DATA SET MCNMODL    AT LEVEL 007 AS OF 05/30/18                      
         MACRO                                                                  
&NAME    NMODL &D,&ID,&CLEAR=NO,&RR=,&END=                                      
         LCLA  &I,&J,&K,&L,&M                                                   
         LCLC  &RL                                                              
.*                                                                              
         REQUS                                                                  
.*                                                                              
&NAME    STM   RE,RC,12(RD)                                                     
         BASR  RB,0                                                             
         LA    RB,0(RB)                                                         
         SH    RB,12(RB)                                                        
         B     32(RB)                                                           
         DC    AL2(6)                                                           
         AIF   (T'&D NE 'O').L1                                                 
         DC    AL2(0)                                                           
         AGO   .L3                                                              
.L1      AIF   (T'&D NE 'N').L2                                                 
         DC    AL2(&D)                                                          
         AGO   .L3                                                              
.L2      ANOP                                                                   
         DC    AL2((&D+7)/8)                                                    
.L3      AIF   (T'&ID NE 'O').L4                                                
         DC    CL8' '                                                           
         AGO  .L5                                                               
.L4      ANOP                                                                   
         DC    CL8'&ID'                                                         
.L5      ANOP                                                                   
         DC    AL2(4096)                                                        
.*                                                                              
&K       SETA  32                                                               
&I       SETA  2                                                                
.*                                                                              
.LA      ANOP                                                                   
&J       SETA  N'&SYSLIST-&I                                                    
.*                                                                              
         AIF   (&J EQ 0 AND T'&END NE 'O').LK  TEST FOR *ONLY 1* BASE           
         USING *-&K,RB                                                          
         AGO   .LL                                                              
.LK      ANOP                                                                   
         USING (*-&K,&END),RB                                                   
.LL      ANOP                                                                   
.*                                                                              
         AIF   (&J EQ 0).LC                                                     
&K       SETA  &K+4                                                             
&L       SETA  0                                                                
&RL      SETC 'RB'                                                              
         LH    RC,30(RB)                                                        
.*                                                                              
.LB      ANOP                                                                   
&I       SETA  &I+1                                                             
&K       SETA  &K+4                                                             
&L       SETA  &L+1                                                             
&M       SETA  4096*&L-&K                                                       
         LA    &SYSLIST(&I),0(RC,&RL)                                           
.*                                                                              
         AIF   (&J EQ 1 AND T'&END NE 'O').LM  TEST FOR *LAST* BASE REG         
         USING *+&M,&SYSLIST(&I)                                                
         AGO   .LN                                                              
.LM      ANOP                                                                   
         USING (*+&M,&END),&SYSLIST(&I)                                         
.LN      ANOP                                                                   
.*                                                                              
&J       SETA  &J-1                                                             
         AIF   (&J EQ 0).LC                                                     
&RL      SETC  '&SYSLIST(&I)'                                                   
         AGO   .LB                                                              
.*                                                                              
.LC      ANOP                                                                   
         SR    RC,RC                                                            
         ICM   RC,3,20(RB)                                                      
         SLL   RC,3                                                             
         LA    RC,72(RC,RD)                                                     
         ST    RD,4(RC)                                                         
         ST    RC,8(RD)                                                         
         MVC   0(4,RD),24(RB)                                                   
.*                                                                              
.LF      AIF   ('&CLEAR' EQ 'NO').LG                                            
         LA    R0,72(RD)                                                        
         LR    R1,RC                                                            
         SR    R1,R0                                                            
         BZ    *+8                                                              
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         LM    RE,R1,12(RD)                                                     
.*                                                                              
.LG      ANOP                                                                   
         LR    RD,RC                                                            
         L     RC,4(RD)                                                         
         LA    RC,72(RC)                                                        
.*                                                                              
.LH      AIF   (T'&RR EQ 'O').LI                                                
         CNOP  0,4                                                              
         B     *+8                                                              
         DC    A(*)                                                             
         LA    &RR,*-4                                                          
         S     &RR,*-8                                                          
.*                                                                              
.LI      ANOP                                                                   
         MEND                                                                   
