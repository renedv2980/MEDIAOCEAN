*          DATA SET MCXCEF     AT LEVEL 004 AS OF 03/22/12                      
         MACRO                                                                  
&NAME    XCEF  &E,&F                                                            
         LCLC  &XCEFB,&XCEFS,&XCEFX                                             
&XCEFB   SETC  'XCFB&SYSNDX'                                                    
&XCEFS   SETC  'XCFS&SYSNDX'                                                    
&XCEFX   SETC  'XCFX&SYSNDX'                                                    
         AIF   (T'&NAME EQ 'O').XC2                                             
&NAME    DS    0H                                                               
.*                                                                              
.XC2     ANOP                                                                   
         AIF   (T'&E EQ 'O').XC6                                                
         AIF   ('&E' EQ '(RE)').XC6                                             
         AIF   ('&E'(1,2) NE '(R').XC4                                          
         LR    RE,&E                                                            
         AGO   .XC6                                                             
.XC4     ANOP                                                                   
         LA    RE,&E                                                            
.*                                                                              
.XC6     ANOP                                                                   
         AIF   (T'&F EQ 'O').XC14                                               
         AIF   ('&F' EQ '(RF)').XC14                                            
         AIF   ('&F'(1,1) EQ '''').XC7A                                         
         AIF   ('&F'(1,2) EQ 'L''').XC7B                                        
         AIF   ('&F'(1,2) NE '(R').XC8                                          
         LR    RF,&F                                                            
         AGO   .XC14                                                            
.*                                                                              
.XC7A    ANOP                                                                   
&EQU     SETC  '&F'(2,K'&F-2)                                                   
         L     RF,=A(&EQU)                                                      
         AGO   .XC14                                                            
.*                                                                              
.XC7B    ANOP                                                                   
         L     RF,=A(&F)                                                        
         AGO   .XC14                                                            
.*                                                                              
.XC8     ANOP                                                                   
         AIF   (T'&F EQ 'N').XC10                                               
         L     RF,&F                                                            
         AGO   .XC14                                                            
.*                                                                              
.XC10    ANOP                                                                   
         AIF   (&F GT 4095).XC12                                                
         LA    RF,&F                                                            
         AGO   .XC14                                                            
.*                                                                              
.XC12    ANOP                                                                   
         L     RF,=F'&F'                                                        
.*                                                                              
.* RE = A(AREA TO BE CLEARED)                                                   
.* RF = LENGTH OF AREA                                                          
.*                                                                              
.XC14    ANOP                                                                   
&XCEFB   DS    0H                  CLEAR ONE 256-BYTE CHUNK AT A TIME           
         CHI   RF,256                                                           
         JL    &XCEFS                                                           
         XC    0(256,RE),0(RE)                                                  
         LA    RE,256(RE)                                                       
         SHI   RF,256                                                           
         J     &XCEFB                                                           
.*                                                                              
&XCEFS   DS    0H                  CLEAR REMAINDER OF LAST CHUNK                
         CIJE  RF,0,&XCEFX                                                      
         BCTR  RF,0                                                             
         EXRL  RF,*+10                                                          
         J     *+10                                                             
         XC    0(0,RE),0(RE)                                                    
.*                                                                              
&XCEFX   DS    0H                                                               
         MEND                                                                   
