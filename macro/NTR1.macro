*          DATA SET MCNTR1     AT LEVEL 003 AS OF 05/31/07                      
         MACRO                                                                  
&NAME    NTR1  &BASE=,&WORK=,&LABEL=Y                                           
         LCLC  &BLKS,&LIT,&K,&L,&BASEADR                                        
         LCLA  &I,&J                                                            
&I       SETA  72                                                               
&J       SETA  76                                                               
.*                                                                              
         AIF   ('&LABEL'(1,1) NE '*').NID    IF LOCAL ID                        
&BLKS    SETC  '        '                                                       
         AIF   (T'&NAME EQ 'O').ID2                                             
         AIF   (K'&NAME GE 7).ID1                                               
&LIT     SETC  'CL8''+'.'&NAME'.'&BLKS'(1,7-K'&NAME).''''                       
         AGO   .ID3                                                             
.ID1     ANOP                                                                   
&LIT     SETC  'CL8''+'.'&NAME'(1,7).''''                                       
         AGO   .ID3                                                             
.ID2     ANOP                                                                   
&LIT     SETC  'CL8''++++++++'''                                                
.ID3     ANOP                                                                   
         AGO   .L3                                                              
.*                                                                              
.NID     ANOP                                                                   
.*                                                                              
&BLKS    SETC  '++++'                                                           
         AIF   (T'&NAME EQ 'O').L2                                              
         AIF   (K'&NAME GE 3).L1                                                
&LIT     SETC  '=C''+'.'&NAME'.'&BLKS'(1,3-K'&NAME).''''                        
         AGO   .L3                                                              
.L1      ANOP                                                                   
&LIT     SETC  '=C''+'.'&NAME'(1,3).''''                                        
         AGO   .L3                                                              
.L2      ANOP                                                                   
&LIT     SETC  '=C''++++'''                                                     
.*                                                                              
.L3      ANOP                                                                   
         AIF   (T'&WORK EQ 'O').L4B                                             
         AIF   (T'&WORK(2) NE 'N').L4A                                          
&I       SETA  &I+8*&WORK(2)                                                    
&J       SETA  &I+4                                                             
         AGO   .L4B                                                             
.L4A     ANOP                                                                   
&K       SETC  '((('.'&WORK(2)'.'+7)/8)*8)+72'                                  
&L       SETC  '((('.'&WORK(2)'.'+7)/8)*8)+76'                                  
.L4B     ANOP                                                                   
.*                                                                              
&NAME    STM   RE,RC,12(RD)                                                     
         AIF   (T'&BASE EQ 'O').SETRBE                                          
         AIF   (N'&BASE NE 2).SETRB10                                           
         AIF   ('&BASE(1)' EQ '*').SETRB05                                      
         MNOTE 8,'INVALID BASE= EXPRESSION'                                     
         MEXIT                                                                  
.*                                                                              
.SETRB05 ANOP                                                                   
&BASEADR SETC  '(&NAME,&BASE(2))'                                               
         AGO   .SETRBA                                                          
.*                                                                              
.SETRB10 ANOP                                                                   
&BASEADR SETC  '&NAME'                                                          
         AIF   ('&BASE' EQ '*').SETRBA                                          
         AIF   ('&BASE'(1,1) EQ '(').SETRBD                                     
         L     RB,&BASE                                                         
         AGO   .SETRBE                                                          
.SETRBA  ANOP                                                                   
         BASR  RB,0                                                             
         AHI   RB,-6                                                            
         USING &BASEADR,RB                                                      
         AGO   .SETRBE                                                          
.SETRBD  ANOP                                                                   
         LR    RB,&BASE(1)                                                      
.SETRBE  ANOP                                                                   
         AIF   (T'&WORK NE 'O' AND T'&WORK(2) NE 'N').L4C                       
         ST    RD,&J.(RD)                                                       
         AGO   .L4D                                                             
.L4C     ANOP                                                                   
         ST    RD,&L.(RD)                                                       
.L4D     ANOP                                                                   
         AIF   (T'&WORK EQ 'O').L5                                              
         LA    &WORK(1),72(RD)                                                  
.L5      ANOP                                                                   
         AIF   ('&LABEL'(1,1) EQ '*').IDL                                       
         AIF   ('&LABEL'(1,1) NE 'Y').L7A                                       
         MVC   0(4,RD),&LIT                                                     
         AGO   .L7A                                                             
.IDL     ANOP                                                                   
         BASR  RE,0                                                             
         J     *+12                                                             
         DC    &LIT                                                             
         MVC   0(4,RD),4(RE)                                                    
.*                                                                              
.L7A     ANOP                                                                   
         AIF   (T'&WORK NE 'O' AND T'&WORK(2) NE 'N').L7B                       
         LA    RE,&I.(RD)                                                       
         AGO   .L7C                                                             
.L7B     ANOP                                                                   
         LA    RE,&K.(RD)                                                       
.L7C     ANOP                                                                   
         ST    RE,8(RD)                                                         
         LR    RD,RE                                                            
         MEND                                                                   
