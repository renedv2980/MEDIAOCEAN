*          DATA SET MCGT1      AT LEVEL 011 AS OF 09/08/13                      
         MACRO                                                                  
&NAME    GT1   &RTN,&PLIST,&RR=                                                 
.*                                                                              
         LCLA  &I,&J,&FQ,&LQ,&LITL                                              
         LCLC  &LTY                                                             
.*                                                                              
.*                                                                              
.PLIST   ANOP                                                                   
         AIF   (N'&SYSLIST NE 1).PLIST1                                         
         AIF   (T'&NAME EQ 'O').RTN                                             
&NAME    DC    0H'0'                                                            
         AGO   .RTN                                                             
.PLIST1  AIF   (T'&PLIST EQ 'O').PLISTER                                        
         AIF   ('&PLIST'(1,1) EQ '(').PLISTR                                    
         AGO   .PLISTLA                                                         
.PLISTER MNOTE 8,'INVALID PARAMETER LIST ADR'                                   
         MEXIT                                                                  
.PLISTR  AIF   ('&PLIST(1)' EQ 'R1').PLISTR1                                    
         AIF   ('&PLIST(1)' EQ '1').PLISTR1                                     
&NAME    LR    R1,&PLIST(1)                                                     
         AGO   .PLISTX                                                          
.PLISTR1 AIF   (T'&NAME EQ 'O').PLISTX                                          
&NAME    DC    0H'0'                                                            
         AGO   .PLISTX                                                          
.PLISTLA ANOP                                                                   
&NAME    LA    R1,&PLIST                                                        
         AGO   .PLISTX                                                          
.PLISTL  ANOP                                                                   
&NAME    L     R1,&PLIST                                                        
         AGO   .PLISTX                                                          
.PLISTX  ANOP                                                                   
.*                                                                              
&I       SETA  3                                                                
&J       SETA  0                                                                
.*                                                                              
.*                                                                              
.LOOP    ANOP                                                                   
         AIF   (T'&SYSLIST(&I) EQ 'O').NEXT                                     
         AIF   (K'&SYSLIST(&I) EQ 1).LOOP1                                      
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'C''').PISDT2                            
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'X''').PISDT2                            
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'F''').PISDT2                            
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'V(').PISDT2                             
         AIF   ('&SYSLIST(&I)'(1,2) EQ 'A(').PISDT2                             
.LOOP1   ANOP                                                                   
         AIF   ('&SYSLIST(&I)'(1,1) EQ '(').PISL                                
         AIF   ('&SYSLIST(&I)'(1,1) EQ '=').PILIT                               
         AIF   (T'&SYSLIST(&I) EQ 'N').PISDT                                    
         AIF   (T'&SYSLIST(&I) EQ 'A').PIL                                      
         AIF   (T'&SYSLIST(&I) EQ 'V').PIL                                      
         AGO   .PILA                                                            
.*                                                                              
.PILA    ANOP                                                                   
         LA    RE,&SYSLIST(&I)                                                  
         ST    RE,&J.(R1)                                                       
         AGO   .NEXT                                                            
.*                                                                              
.PIL     ANOP                                                                   
         MVC   &J.(4,R1),&SYSLIST(&I)                                           
         AGO   .NEXT                                                            
.*                                                                              
.PIR     ANOP                                                                   
         ST    &SYSLIST(&I,1),&J.(R1)                                           
         AGO   .NEXT                                                            
.*                                                                              
.PISDT   ANOP                                                                   
         AIF   ('&SYSLIST(&I)'(1,1) LT '0').PIERR                               
         AIF   (&SYSLIST(&I) GT 4095).PISDT1                                    
         LA    RE,&SYSLIST(&I)                                                  
         ST    RE,&J.(R1)                                                       
         AGO   .NEXT                                                            
.PISDT1  ANOP                                                                   
         MVC   &J.(4,R1),=F'&SYSLIST(&I)'                                       
         AGO   .NEXT                                                            
.PISDT2  ANOP                                                                   
         MVC   &J.(4,R1),=&SYSLIST(&I)                                          
         AGO   .NEXT                                                            
.*                                                                              
.PISL    ANOP                                                                   
         AIF   (N'&SYSLIST(&I) EQ 1).PIR                                        
         AIF   (N'&SYSLIST(&I) NE 2).PIERR                                      
         AIF   (T'&SYSLIST(&I,2) EQ 'O').PIERR                                  
         AIF   (K'&SYSLIST(&I,2) EQ 1).MELOOP                                   
         AIF   ('&SYSLIST(&I,2)'(1,2) EQ 'C''').PISL2V                          
         AIF   ('&SYSLIST(&I,2)'(1,2) EQ 'X''').PISL2V                          
         AIF   ('&SYSLIST(&I,2)'(1,2) EQ 'F''').PISL2V                          
         AIF   ('&SYSLIST(&I,2)'(1,2) EQ 'V(').PISL2V                           
         AIF   ('&SYSLIST(&I,2)'(1,2) EQ 'A(').PISL2V                           
.MELOOP  ANOP                                                                   
         AIF   ('&SYSLIST(&I,2)'(1,1) EQ '(').PISL2R                            
         AIF   (T'&SYSLIST(&I,2) EQ 'N').PISL2I                                 
         AIF   (T'&SYSLIST(&I,2) EQ 'A').PISL2L                                 
         AIF   (T'&SYSLIST(&I,2) EQ 'V').PISL2L                                 
.PISL2LA ANOP                                                                   
         LA    RE,&SYSLIST(&I,2)                                                
         ST    RE,&J.(R1)                                                       
         AGO   .PISL1                                                           
.PISL2I  ANOP                                                                   
         MVC   &J.(4,R1),=F'&SYSLIST(&I,2)'                                     
         AGO   .PISL1                                                           
.PISL2L  ANOP                                                                   
         MVC   &J.(4,R1),&SYSLIST(&I,2)                                         
         AGO   .PISL1                                                           
.PISL2R  ANOP                                                                   
&LTY     SETC  '&SYSLIST(&I,2)'(2,K'&SYSLIST(&I,2)-2)                           
         ST    &LTY,&J.(R1)                                                     
         AGO   .PISL1                                                           
.PISL2V  ANOP                                                                   
         MVC   &J.(4,R1),=&SYSLIST(&I,2)                                        
         AGO   .PISL1                                                           
.PISL1   ANOP                                                                   
         AIF   (T'&SYSLIST(&I,1) EQ 'O').PIERR                                  
         AIF   ('&SYSLIST(&I,1)'(1,1) EQ '(').PISL1R                            
         AIF   (T'&SYSLIST(&I,1) EQ '*').PISL1I                                 
         AIF   (T'&SYSLIST(&I,1) NE 'N').PISL1C                                 
         AIF   (&SYSLIST(&I,1) GT 255).PIERR                                    
.PISL1I  ANOP                                                                   
         MVI   &J.(R1),&SYSLIST(&I,1)                                           
         AGO   .NEXT                                                            
.PISL1C  ANOP                                                                   
         MVC   &J.(1,R1),&SYSLIST(&I,1)                                         
         AGO   .NEXT                                                            
.PISL1R  ANOP                                                                   
&LTY     SETC  '&SYSLIST(&I,1)'(2,K'&SYSLIST(&I,1)-2)                           
         STC   &LTY,&J.(R1)                                                     
         AGO   .NEXT                                                            
.*                                                                              
.PILIT   ANOP                                                                   
         AIF   ('&SYSLIST(&I)'(2,1) EQ 'V').PILITA                              
         AIF   ('&SYSLIST(&I)'(2,1) EQ 'A').PILITA                              
&LQ      SETA  K'&SYSLIST(&I)                                                   
&FQ      SETA  1                                                                
.PILIT1  ANOP                                                                   
         AIF   ('&SYSLIST(&I)'(&FQ,1) EQ '''').PILIT2                           
&FQ      SETA  &FQ+1                                                            
         AIF   (&FQ GT 7).PIERR                                                 
         AGO   .PILIT1                                                          
.PILIT2  ANOP                                                                   
&LITL    SETA  &LQ-&FQ-1                                                        
&LTY     SETC  '&SYSLIST(&I)'(2,1)                                              
         AIF   (&FQ EQ 3 AND '&LTY'  EQ 'H').PILITH                             
         AIF   (&FQ EQ 3 AND '&LTY'  EQ 'F').PILITF                             
         AIF   (&FQ EQ 3 AND '&LTY'  EQ 'D').PILITD                             
         AIF   (&FQ EQ 3 AND '&LTY'  EQ 'X').PILITX                             
         AIF   (&FQ EQ 3 AND '&LTY'  EQ 'C').PILIT4                             
         AIF   ('&LTY' EQ 'C' OR '&LTY' EQ 'X' OR '&LTY' EQ 'P').PILIT3         
         AGO   .PIERR                                                           
.PILIT3  ANOP                                                                   
         AIF   ('&SYSLIST(&I)'(3,1) NE 'L').PIERR                               
&LTY     SETC  '&SYSLIST(&I)'(4,&FQ-4)                                          
&LITL    SETA  0+&LTY                                                           
         AGO   .PILIT4                                                          
.PILITH  ANOP                                                                   
&LITL    SETA  2                                                                
         AGO   .PILIT4                                                          
.PILITF  ANOP                                                                   
&LITL    SETA  4                                                                
         AGO   .PILIT4                                                          
.PILITD  ANOP                                                                   
&LITL    SETA  8                                                                
         AGO   .PILIT4                                                          
.PILITX  ANOP                                                                   
&LITL    SETA  &LITL/2                                                          
         AGO   .PILIT4                                                          
.PILITA  ANOP                                                                   
&LITL    SETA  4                                                                
.PILIT4  ANOP                                                                   
         LA    RE,&SYSLIST(&I)                                                  
         ST    RE,&J.(R1)                                                       
         MVI   &J.(R1),&LITL                                                    
         AGO   .NEXT                                                            
.*                                                                              
.*                                                                              
.NEXT    ANOP                                                                   
&I       SETA  &I+1                                                             
&J       SETA  &J+4                                                             
         AIF   (&I GT N'&SYSLIST).RTN                                           
         AGO   .LOOP                                                            
.*                                                                              
.PIERR   MNOTE 8,'INVALID PARAMETER'                                            
         MEXIT                                                                  
.*                                                                              
.*                                                                              
.RTN     AIF   (T'&RTN EQ 'O').EXIT                                             
         AIF   ('&RTN'(1,1) EQ '(').RTNR                                        
         AIF   ('&RTN'(1,2) EQ '=A' OR '&RTN'(1,2) EQ '=V').RTNL                
         AIF   (T'&RTN EQ 'I' OR T'&RTN EQ 'M').RTNLA                           
         AIF   (T'&RTN EQ 'J').RTNCS                                            
         AGO   .RTNL                                                            
.RTNR    ANOP                                                                   
         AIF   ('&RTN(1)' EQ 'RF').RTNX                                         
         AIF   ('&RTN(1)' EQ '15').RTNX                                         
         LR    RF,&RTN(1)                                                       
         AGO   .RTNX                                                            
.RTNL    ANOP                                                                   
         L     RF,&RTN                                                          
         AGO   .RTNX                                                            
.RTNLA   ANOP                                                                   
         LA    RF,&RTN                                                          
         AGO   .RTNX                                                            
.RTNCS   ANOP                                                                   
         L     RF,=A(&RTN)                                                      
         AGO   .RTNX                                                            
.RTNX    ANOP                                                                   
.*                                                                              
.*                                                                              
.RELO    ANOP                                                                   
         AIF   (T'&RR EQ 'O').RELOX                                             
         AIF   ('&RR' EQ 'RB').RELO1                                            
         AIF   ('&RR' EQ 'Y').RELO1                                             
         AIF   ('&RR' EQ 'YES').RELO1                                           
         AGO   .RELO2                                                           
.RELO1   ANOP                                                                   
         CNOP  0,4                                                              
         B     *+8                                                              
         DC    A(*)                                                             
         LA    RE,*-4                                                           
         S     RE,*-8                                                           
         AR    RF,RE                                                            
         AGO   .RELOX                                                           
.RELO2   AIF   (K'&RR NE 2).RELO3                                               
         AIF   ('&RR'(1,1) NE 'R').RELO3                                        
         AR    RF,&RR                                                           
         AGO   .RELOX                                                           
.RELO3   ANOP                                                                   
         A     RF,&RR                                                           
         AGO   .RELOX                                                           
.RELOX   ANOP                                                                   
         BASR  RE,RF                                                            
.*                                                                              
.*                                                                              
.EXIT    ANOP                                                                   
         MEND                                                                   
