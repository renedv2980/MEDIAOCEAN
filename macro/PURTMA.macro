*          DATA SET PURTMA     AT LEVEL 030 AS OF 05/01/02                      
           MACRO                                                                
&NAME      CLCT                                                                 
&NAME      SR        RW1,RW1                                                    
           ST        RW1,$COUNT                                                 
           MEND                                                                 
           MACRO                                                                
&NAME      PFPCT     &PCT,&F1,&F2,&CODE=00                                      
&NAME      DS        0H                                                         
           AIF       (T'&F1 EQ 'O').P1                                          
           BAS       RRFF,&F1                                                   
           B         $E&SYSNDX                                                  
.P1        AIF       (T'&F2 EQ 'O').P2                                          
           BAS       RRFF,&F2                                                   
           B         $E&SYSNDX                                                  
.P2        L         RW1,$C&SYSNDX                                              
           ST        RW1,$CURPCT                                                
           A         RW1,$ACCPCT                                                
           ST        RW1,$ACCPCT                                                
           LA        RW1,100                  100 %                             
           A         RW1,$CURPCT                                                
           L         RW2,$EARNRT                                                
           MR        RW1,RW1                                                    
           LA        RW3,50                   SCALE RESULT                      
           AR        RW2,RW3                   *                                
           LA        RW3,100                   *                                
           DR        RW1,RW3                   *                                
           ST        RW2,$EARNRT                                                
           LA        RW1,$APPDIS-1                                              
           LA        RW1,1(RW1)                                                 
           TM        0(RW1),X'FF'                                               
           BNZ       *-8                                                        
           MVI       0(RW1),X'&CODE'                                            
           B         $E&SYSNDX                                                  
$C&SYSNDX  DC        F'&PCT'                                                    
$E&SYSNDX  EQU       *                                                          
           MEND                                                                 
           MACRO                                                                
&NAME      CTIN      &SPR,&F1,&F2                                               
&NAME      DS        0H                                                         
           AIF       (T'&F1 EQ 'O').P1                                          
           BAS       RRFF,&F1                                                   
           B         $E&SYSNDX                                                  
.P1        AIF       (T'&F2 EQ 'O').P2                                          
           BAS       RRFF,&F1                                                   
           B         $E&SYSNDX                                                  
.P2        L         RW1,$COUNT                                                 
           CLI       $TYPE,$$SPR              TEST FOR SPREAD TYPE              
           BNE       *+12                                                       
           LA        RW1,&SPR.(RW1)               ADD COUNT IF SPREAD           
           B         $O&SYSNDX                                                  
.*                                   TESTS FOR MORE SPREAD TYPE GO HERE         
           LA        RW1,1(RW1,0)                                               
$O&SYSNDX  ST        RW1,$COUNT                                                 
$E&SYSNDX  EQU       *                                                          
           MEND                                                                 
           MACRO                                                                
&NAME      PVPCT     &F1,&F2,&CODE=00                                           
&NAME      DS        0H                                                         
           AIF       (T'&F1 EQ 'O').P1                                          
           BAS       RRFF,&F1                                                   
           B         $E&SYSNDX                                                  
.P1        AIF       (T'&F2 EQ 'O').P2                                          
           BAS       RRFF,&F2                                                   
           B         $E&SYSNDX                                                  
.P2        L         RW1,$LUELEM                                                
           LCR       RW1,RW1                                                    
           ST        RW1,$CURPCT                                                
           A         RW1,$ACCPCT                                                
           ST        RW1,$ACCPCT                                                
           LA        RW1,100                  100 %                             
           A         RW1,$CURPCT                                                
           L         RW2,$EARNRT                                                
           MR        RW1,RW1                                                    
           LA        RW3,50                   SCALE RESULT                      
           AR        RW2,RW3                   *                                
           LA        RW3,100                   *                                
           DR        RW1,RW3                   *                                
           ST        RW2,$EARNRT                                                
           LA        RW1,$APPDIS-1                                              
           LA        RW1,1(RW1)                                                 
           TM        0(RW1),X'FF'                                               
           BNZ       *-8                                                        
           MVI       0(RW1),X'&CODE'                                            
$E&SYSNDX  EQU       *                                                          
           MEND                                                                 
           MACRO                                                                
&NAME      ITEMS     &F1,&F2,&F3,&CLCT=Y                                        
           GBLC      &$LBL(5)                                                   
&$LBL(5)   SETC      '&$LBL(4)'                                                 
&$LBL(4)   SETC      '&$LBL(3)'                                                 
&$LBL(3)   SETC      '&$LBL(2)'                                                 
&$LBL(2)   SETC      '&$LBL(1)'                                                 
&$LBL(1)   SETC      '&SYSNDX'                                                  
&NAME      L         RIT,$IF                                                    
           AIF       ('&CLCT' EQ 'N').PC                                        
           SR        RW1,RW1                                                    
           ST        RW1,$COUNT                                                 
.PC        ANOP                                                                 
           B         $G&SYSNDX                                                  
$N&$LBL(1) LA        RIT,$ITEMLEN(RIT,0)                                        
           C         RIT,$IL                                                    
           BH        $E&$LBL(1)                                                 
$G&SYSNDX  EQU       *                                                          
           AIF       (T'&F1 EQ 'O').P1                                          
           BAS       RRFF,&F1                                                   
           B         $N&$LBL(1)                                                 
.P1        AIF       (T'&F2 EQ 'O').P2                                          
           BAS       RRFF,&F2                                                   
           B         $N&$LBL(1)                                                 
.P2        AIF       (T'&F3 EQ 'O').P3                                          
           BAS       RRFF,&F3                                                   
           B         $N&$LBL(1)                                                 
.P3        ANOP                                                                 
           MEND                                                                 
           MACRO                                                                
&NAME      CPDGRP                                                               
           GBLC      &$LBL(5)                                                   
&$LBL(5)   SETC      '&$LBL(4)'                                                 
&$LBL(4)   SETC      '&$LBL(3)'                                                 
&$LBL(3)   SETC      '&$LBL(2)'                                                 
&$LBL(2)   SETC      '&$LBL(1)'                                                 
&$LBL(1)   SETC      '&SYSNDX'                                                  
&NAME      LM        RW1,RW4,$IF              SET UP                            
           STM       RW1,RW4,$ISAV1           *                                 
$N&$LBL(1) L         RIT,$IF                  *                                 
$L0&SYSNDX MVC       $WRKCH,$MGRP             FIND GRP                          
$L1&SYSNDX CLC       $WRKCH,$MGRP             *                                 
           BNE       $L2&SYSNDX               *                                 
           LA        RIT,$ITEMLEN(RIT,0)      *                                 
           C         RIT,$ISAV1+4             *                                 
           BNH       $L1&SYSNDX               *                                 
           B         $O&SYSNDX                *                                 
$L2&SYSNDX CLI       $MGRP,X'00'              *                                 
           BE        $L0&SYSNDX               *                                 
           ST        RIT,$IF                  *                                 
           MVC       $WRKCH,$MGRP             FIND END OF GRP                   
$L3&SYSNDX C         RIT,$ISAV1+4             *                                 
           BE        $L4&SYSNDX               *                                 
           CLC       $WRKCH,$MGRP+$ITEMLEN    *                                 
           BNE       $L4&SYSNDX               *                                 
           LA        RIT,$ITEMLEN(RIT,0)      *                                 
           B         $L3&SYSNDX               *                                 
$O&SYSNDX  LM        RW1,RW4,$ISAV1           CLEAN UP AND EXIT                 
           STM       RW1,RW4,$IF              *                                 
           B         $E&$LBL(1)               *                                 
$L4&SYSNDX ST        RIT,$IL                  READY TO PROCESS                  
           MEND                                                                 
           MACRO                                                                
&NAME      LOOP                                                                 
           GBLC      &$LBL(3)                                                   
&NAME      B         $N&$LBL(1)                                                 
$E&$LBL(1) EQU       *                                                          
&$LBL(1)   SETC      '&$LBL(2)'                                                 
&$LBL(2)   SETC      '&$LBL(3)'                                                 
&$LBL(3)   SETC      '&$LBL(4)'                                                 
&$LBL(4)   SETC      '&$LBL(5)'                                                 
           MEND                                                                 
           MACRO                                                                
&NAME      LUBT      &TABLE                                                     
&NAME      L         RW1,&TABLE               COUNT OF TABLE ENTRIES            
           LA        RW2,&TABLE+4             START OF ENTRIES                  
           L         RW3,$COUNT               LU ARG                            
$N&SYSNDX  C         RW3,0(RW2)               FIND                              
           BL        $O&SYSNDX                  BREAK                           
           LA        RW2,8(RW2)                 *                               
           BCT       RW1,$N&SYSNDX              *                               
$O&SYSNDX  LA        RW3,&TABLE+4             CHECK                             
           CR        RW2,RW3                    IF                              
           BE        $E&SYSNDX                  FIRST                           
           LA        RW3,8                    BACK                              
           SR        RW2,RW3                    UP                              
$E&SYSNDX  L         RW3,4(RW2)               GET VALUE                         
           ST        RW3,$LUELEM                AND SAVE                        
           MEND                                                                 
           MACRO                                                                
&NAME      GBRB      &TABLE,&F1,&F2                                             
&NAME      DS        0H                                                         
           AIF       (T'&F1 EQ 'O').P1                                          
           BAS       RRFF,&F1                                                   
           B         $E&SYSNDX                                                  
.P1        AIF       (T'&F2 EQ 'O').P2                                          
           BAS       RRFF,&F2                                                   
           B         $E&SYSNDX                                                  
.P2        LA        RW2,&TABLE                                                 
$L&SYSNDX  OC        2(2,RW2),2(RW2)                                            
           BZ        $E&SYSNDX                NOT FOUND                         
           CLC       $TYPE,0(RW2)                                               
           BNE       *+14                                                       
           CLC       $COLOR,1(RW2)                                              
           BE        *+12                                                       
           AH        RW2,2(RW2)               GET NEXT                          
           B         $L&SYSNDX                                                  
           LA        RW2,4(RW2)               FOUND                             
           L         RW1,0(RW2)                                                 
           ST        RW1,$OPENRT                                                
           A         RW2,$LUELEM                                                
           L         RW1,0(RW2)                                                 
           ST        RW1,$EARNRT                                                
           SR        RW1,RW1                                                    
           ST        RW1,$ACCPCT                                                
           ST        RW1,$CURPCT                                                
           LH        RW1,$FLAGS                                                 
           LA        RW1,4(RW1)                                                 
           STH       RW1,$FLAGS                                                 
$E&SYSNDX  EQU       *                                                          
           MEND                                                                 
           MACRO                                                                
&NAME      GBRA                                                                 
&NAME      DS        0H                                                         
           TM        $FLAGS+1,4               SKIP IF ALREADY                   
           BO        $E&SYSNDX                  RATED                           
           L         RW1,$OPENRT                                                
           ST        RW1,$EARNRT                                                
           SR        RW1,RW1                                                    
           ST        RW1,$ACCPCT                                                
           ST        RW1,$CURPCT                                                
           LH        RW1,$FLAGS                                                 
           LA        RW1,4(RW1)                                                 
           STH       RW1,$FLAGS                                                 
$E&SYSNDX  EQU       *                                                          
           MEND                                                                 
