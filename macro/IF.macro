*          DATA SET MCIF       AT LEVEL 009 AS OF 07/10/14                      
         MACRO                                                                  
&TAG     IF    &P                                                               
.**********************************************************************         
.*                                                                              
.* THIS MACRO IS NOW OBSOLETE. USE THE "JIF" MACRO INSTEAD.                     
.* THE PARAMETER LIST TO "JIF" IS IDENTICAL TO THE OLD "IF" MACRO.              
.* NOTE THAT "JIF" GENERATES JUMP INSTRUCTIONS. IF YOU WISH TO GENERATE         
.* ORDINARY BRANCHES INSTEAD, ADD A "JUMP=N" PARAMETER TO THE JIF CALL.         
.*                                                                              
.**********************************************************************         
         LCLA  &A,&B,&C,&K,&X,&L                                                
&K       SETA  N'&SYSLIST                                                       
         AIF   (T'&TAG EQ 'O').NOTAG                                            
&TAG     DS    0H                                                               
.NOTAG   ANOP                                                                   
         AIF   (&K EQ 4).OR                                                     
         AIF   ('&SYSLIST(4)' EQ 'AND').AND                                     
         AIF   ('&SYSLIST(4)' EQ 'OR').OR                                       
.*                                                                              
.INVAL   ANOP                                                                   
         MNOTE 02,'INVALID IF EXPRESSION'                                       
         MEXIT                                                                  
.*                                                                              
.*                                           OR HANDLING                        
.*                                                                              
.OR      ANOP                                                                   
&A       SETA  1                                                                
&B       SETA  2                                                                
&C       SETA  3                                                                
.*                                                                              
.*                                                                              
.OR2     ANOP                                                                   
         AIF   ('&SYSLIST(&B)' EQ 'ON').OR19                                    
         AIF   ('&SYSLIST(&B)' EQ 'OFF').OR19B                                  
         AIF   ('&SYSLIST(&B)' EQ 'NO').OR19B                                   
         AIF   ('&SYSLIST(&B)' EQ 'MIX').OR19                                   
         AIF   ('&SYSLIST(&A)'(1,2) EQ '(R').OR5                                
         AIF   ('&SYSLIST(&C)'(1,1) EQ '=').OR3                                 
         AIF   (T'&SYSLIST(&C) EQ 'U').OR4                                      
         AIF   (T'&SYSLIST(&C) EQ 'N').OR4                                      
         AIF   ('&SYSLIST(&C)'(2,1) EQ '''').OR4                                
.OR3     ANOP                                                                   
         CLC   &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .OR6                                                             
.*                                                                              
.OR4     ANOP                                                                   
         CLI   &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .OR6                                                             
.OR5     AIF   ('&SYSLIST(&C)'(1,2) EQ '(R').OR5A                               
         AIF   ('&SYSLIST(&C)'(1,2) EQ '=H').OR5B                               
         AIF   (T'&SYSLIST(&C) EQ 'N').OR5C                                     
         C     &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .OR6                                                             
.OR5A    ANOP                                                                   
         CR    &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .OR6                                                             
.OR5B    ANOP                                                                   
         CH    &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .OR6                                                             
.OR5C    ANOP                                                                   
         CH    &SYSLIST(&A),=H'&SYSLIST(&C)'                                    
.*                                                                              
.*                                           CHECK CONDITIONS                   
.*                                                                              
.OR6     ANOP                                                                   
         AIF   ('&SYSLIST(&B)' EQ '=').OR8                                      
         AIF   ('&SYSLIST(&B)' EQ 'EQ').OR8                                     
         AIF   ('&SYSLIST(&B)' EQ 'NE').OR10                                    
         AIF   ('&SYSLIST(&B)' EQ 'GE').OR12                                    
         AIF   ('&SYSLIST(&B)' EQ 'NL').OR12                                    
         AIF   ('&SYSLIST(&B)' EQ 'LE').OR14                                    
         AIF   ('&SYSLIST(&B)' EQ 'NH').OR14                                    
         AIF   ('&SYSLIST(&B)' EQ '>').OR16                                     
         AIF   ('&SYSLIST(&B)' EQ 'HT').OR16                                    
         AIF   ('&SYSLIST(&B)' EQ 'GT').OR16                                    
         AIF   ('&SYSLIST(&B)' EQ '<').OR18                                     
         AIF   ('&SYSLIST(&B)' EQ 'LT').OR18                                    
.INVCON  ANOP                                                                   
         MNOTE 02,'INVALID CONDITION IN IF'                                     
         MEXIT                                                                  
.OR8     ANOP                                                                   
         BE    &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.OR10    ANOP                                                                   
         BNE   &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.OR12    ANOP                                                                   
         BNL   &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.OR14    ANOP                                                                   
         BNH   &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.OR16    ANOP                                                                   
         BH    &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.OR18    ANOP                                                                   
         BL    &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.*                                           TM INSTRUCTIONS (OR)               
.*                                                                              
.OR19    ANOP                                                                   
         TM    &SYSLIST(&A),&SYSLIST(&C)                                        
         BO    &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.OR19B   ANOP                                                                   
         TM    &SYSLIST(&A),&SYSLIST(&C)                                        
         BNO   &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.OR19C   ANOP                                                                   
         TM    &SYSLIST(&A),&SYSLIST(&C)                                        
         BM    &SYSLIST(&K)                                                     
         AGO   .OR20                                                            
.*                                                                              
.*                                           SEE WHAT TO GENERATE NEXT          
.*                                                                              
.OR20    ANOP                                                                   
&X       SETA  &C+1                                                             
         AIF   (&X EQ &K).DONE                                                  
         AIF   ('&SYSLIST(&X)' NE 'OR').INVAL                                   
&X       SETA  &X+2                                                             
         AIF   (&X EQ &K).OR22                                                  
         AIF   ('&SYSLIST(&X)' EQ 'OR').OR22                                    
&A       SETA  &X-1                                                             
&B       SETA  &X                                                               
&C       SETA  &X+1                                                             
         AGO   .OR2                                                             
.OR22    ANOP                                                                   
&C       SETA  &C+2                                                             
         AGO   .OR2                                                             
.*                                                                              
.*                                           FOR AND EXPRESSIONS, WE            
.*                                           NEED TO SEE HOW BIG THE            
.*                                           GENERATED CODE WILL BE             
.*                                                                              
.AND     ANOP                                                                   
&A       SETA  1                                                                
&B       SETA  2                                                                
&C       SETA  3                                                                
&L       SETA  8                                                                
.*                                                                              
.AND2    ANOP                                                                   
&L       SETA  &L+6                                                             
         AIF   ('&SYSLIST(&C)'(1,2) EQ '(R').AND4                               
&L       SETA  &L+2                                                             
         AIF   ('&SYSLIST(&A)'(1,2) EQ '(R').AND4                               
         AIF   ('&SYSLIST(&C)'(1,1) NE '=').AND3                                
&L       SETA  &L+2                                                             
         AGO   .AND4                                                            
.AND3    ANOP                                                                   
         AIF   (T'&SYSLIST(&C) EQ 'U').AND4                                     
         AIF   (T'&SYSLIST(&C) EQ 'N').AND4                                     
         AIF   ('&SYSLIST(&C)'(2,1) EQ '''').AND4                               
&L       SETA  &L+2                                                             
.*                                                                              
.AND4    ANOP                                                                   
&B       SETA  &B+2                                                             
         AIF   (&B EQ &K).AND10                                                 
         AIF   ('&SYSLIST(&B)' NE 'AND').INVAL                                  
&A       SETA  &B+1                                                             
&B       SETA  &B+2                                                             
&C       SETA  &B+1                                                             
         AGO   .AND2                                                            
.*                                                                              
.*                                           CHECK FOR CLC OR CLI               
.*                                                                              
.AND10   ANOP                                                                   
&A       SETA  1                                                                
&B       SETA  2                                                                
&C       SETA  3                                                                
.*                                                                              
.AND12   ANOP                                                                   
.*                                                                              
.*                                           GENERATE COMPARES                  
.*                                                                              
         AIF   ('&SYSLIST(&B)' EQ 'ON').AND34                                   
         AIF   ('&SYSLIST(&B)' EQ 'NO').AND36                                   
         AIF   ('&SYSLIST(&B)' EQ 'OFF').AND36                                  
         AIF   ('&SYSLIST(&B)' EQ 'MIX').AND38                                  
         AIF   ('&SYSLIST(&A)'(1,2) EQ '(R').AND16                              
         AIF   ('&SYSLIST(&C)'(1,1) EQ '=').AND13                               
         AIF   (T'&SYSLIST(&C) EQ 'U').AND14                                    
         AIF   (T'&SYSLIST(&C) EQ 'N').AND14                                    
         AIF   ('&SYSLIST(&C)'(2,1) EQ '''').AND14                              
.*                                                                              
.AND13   ANOP                                                                   
&L       SETA  &L-10                                                            
         CLC   &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .AND20                                                           
.*                                                                              
.AND14   ANOP                                                                   
&L       SETA  &L-8                                                             
         CLI   &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .AND20                                                           
.*                                                                              
.AND16   ANOP                                                                   
&L       SETA  &L-6                                                             
         AIF   ('&SYSLIST(&C)'(1,2) EQ '(R').AND17                              
&L       SETA  &L-2                                                             
         AIF   ('&SYSLIST(&C)'(1,2) EQ '=H').AND18                              
         AIF   (T'&SYSLIST(&C) EQ 'N').AND19                                    
         C     &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .AND20                                                           
.*                                                                              
.AND17   ANOP                                                                   
         CR    &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .AND20                                                           
.*                                                                              
.AND18   ANOP                                                                   
         CH    &SYSLIST(&A),&SYSLIST(&C)                                        
         AGO   .AND20                                                           
.*                                                                              
.AND19   ANOP                                                                   
         CH    &SYSLIST(&A),=H'&SYSLIST(&C)'                                    
.*                                                                              
.*                                                                              
.AND20   ANOP                                                                   
.*                                                                              
.*                                           CHECK CONDITION                    
.*                                                                              
         AIF   ('&SYSLIST(&B)' EQ '=').AND22                                    
         AIF   ('&SYSLIST(&B)' EQ 'EQ').AND22                                   
         AIF   ('&SYSLIST(&B)' EQ 'NE').AND24                                   
         AIF   ('&SYSLIST(&B)' EQ 'GE').AND26                                   
         AIF   ('&SYSLIST(&B)' EQ 'NL').AND26                                   
         AIF   ('&SYSLIST(&B)' EQ 'LE').AND28                                   
         AIF   ('&SYSLIST(&B)' EQ 'NH').AND28                                   
         AIF   ('&SYSLIST(&B)' EQ '>').AND30                                    
         AIF   ('&SYSLIST(&B)' EQ 'HT').AND30                                   
         AIF   ('&SYSLIST(&B)' EQ 'GT').AND30                                   
         AIF   ('&SYSLIST(&B)' EQ '<').AND32                                    
         AIF   ('&SYSLIST(&B)' EQ 'LT').AND32                                   
         AGO   .INVCON                                                          
.*                                           GENERATE BRANCHES                  
.AND22   ANOP                                                                   
         BNE   *+&L                                                             
         AGO   .AND40                                                           
.*                                                                              
.AND24   ANOP                                                                   
         BE    *+&L                                                             
         AGO   .AND40                                                           
.*                                                                              
.AND26   ANOP                                                                   
         BL    *+&L                                                             
         AGO   .AND40                                                           
.AND28   ANOP                                                                   
         BH    *+&L                                                             
         AGO   .AND40                                                           
.*                                                                              
.AND30   ANOP                                                                   
         BNH   *+&L                                                             
         AGO   .AND40                                                           
.*                                                                              
.AND32   ANOP                                                                   
         BNL   *+&L                                                             
         AGO   .AND40                                                           
.*                                                                              
.*                                           TM INSTRUCTIONS (AND)              
.*                                                                              
.AND34   ANOP                                                                   
         TM    &SYSLIST(&A),&SYSLIST(&C)                                        
         BNO   &SYSLIST(&K)                                                     
         AGO   .AND40                                                           
.AND36   ANOP                                                                   
         TM    &SYSLIST(&A),&SYSLIST(&C)                                        
         BO    &SYSLIST(&K)                                                     
         AGO   .AND40                                                           
.AND38   ANOP                                                                   
         TM    &SYSLIST(&A),&SYSLIST(&C)                                        
         BO    &SYSLIST(&K)                                                     
         AGO   .AND40                                                           
.*                                                                              
.AND40   ANOP                                                                   
&A       SETA  &A+4                                                             
&B       SETA  &B+4                                                             
&C       SETA  &C+4                                                             
         AIF   (&L NE 8).AND12                                                  
         B     &SYSLIST(&K)                                                     
.DONE    ANOP                                                                   
         MEXIT                                                                  
         MEND                                                                   
