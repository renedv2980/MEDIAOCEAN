*          DATA SET MCNMOD1    AT LEVEL 024 AS OF 05/22/18                      
         MACRO                                                                  
&NAME    NMOD1 &D,&ID,&CLEAR=NO,&RR=,&END=                                      
         LCLA  &I,&J,&K,&L,&M                                                   
         LCLC  &RL                                                              
         LCLC  &NMOD1A                                                          
&NMOD1A  SETC  'NMDA&SYSNDX'                                                    
.*                                                                              
         REQUS                                                                  
.*                                                                              
&NAME    STM   RE,RC,12(RD)                                                     
         BASR  RB,0                                                             
         AHI   RB,-6                                                            
         J     &NMOD1A                                                          
         DC    XL4'00'             FILLER                                       
         AIF   (T'&D NE 'O').L1                                                 
         DC    AL4(0)                                                           
         AGO   .L3                                                              
.L1      AIF   (T'&D NE 'N').L2                                                 
         DC    AL4(&D*8)                                                        
         AGO   .L3                                                              
.L2      ANOP                                                                   
         DC    AL4((&D+7)/8*8)                                                  
.L3      AIF   (T'&ID NE 'O').L4                                                
         DC    CL8' '                                                           
         AGO  .L5                                                               
.L4      ANOP                                                                   
         DC    CL8'&ID'                                                         
.L5      ANOP                                                                   
         DC    AL2(0)                                                           
.*                                                                              
&NMOD1A  EQU   *                                                                
&K       SETA  32                                                               
&I       SETA  2                                                                
.*                                                                              
&J       SETA  N'&SYSLIST-&I                                                    
.*                                                                              
         AIF   (&J EQ 0 AND T'&END NE 'O').LK  TEST FOR *ONLY 1* BASE           
         USING *-&K,RB                                                          
         AGO   .LL                                                              
.LK      ANOP                                                                   
         USING (*-&K,&END),RB                                                   
.LL      ANOP                                                                   
.*                                                                              
         AIF   (&J EQ 0).LC                                                     
&K       SETA  &K+4                                                             
&L       SETA  0                                                                
&RL      SETC 'RB'                                                              
         LHI   RC,4096                                                          
.*                                                                              
.LB      ANOP                                                                   
&I       SETA  &I+1                                                             
&K       SETA  &K+4                                                             
&L       SETA  &L+1                                                             
&M       SETA  4096*&L-&K                                                       
         LA    &SYSLIST(&I),0(RC,&RL)                                           
.*                                                                              
         AIF   (&J EQ 1 AND T'&END NE 'O').LM  TEST FOR *LAST* BASE REG         
         USING *+&M,&SYSLIST(&I)                                                
         AGO   .LN                                                              
.LM      ANOP                                                                   
         USING (*+&M,&END),&SYSLIST(&I)                                         
.LN      ANOP                                                                   
.*                                                                              
&J       SETA  &J-1                                                             
         AIF   (&J EQ 0).LC                                                     
&RL      SETC  '&SYSLIST(&I)'                                                   
         AGO   .LB                                                              
.*                                                                              
.LC      ANOP                                                                   
         ICM   RC,15,18(RB)                                                     
         LA    RC,72(RC,RD)                                                     
         ST    RD,4(RC)                                                         
         ST    RC,8(RD)                                                         
         MVC   0(4,RD),24(RB)                                                   
.*                                                                              
         AIF   ('&CLEAR' EQ 'NO').LG                                            
         LA    R0,72(RD)                                                        
         LR    R1,RC                                                            
         SR    R1,R0                                                            
         JZ    *+8                                                              
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         LM    RE,R1,12(RD)                                                     
.*                                                                              
.LG      ANOP                                                                   
         LR    RD,RC                                                            
         S     RC,18(RB)                                                        
.*                                                                              
         AIF   (T'&RR EQ 'O').LI                                                
         CNOP  0,4                                                              
         J     *+8                                                              
         DC    A(*)                                                             
         LA    &RR,*-4                                                          
         S     &RR,*-8                                                          
.*                                                                              
.LI      ANOP                                                                   
         MEND                                                                   
