*          DATA SET MCFOUT     AT LEVEL 004 AS OF 04/18/12                      
         MACRO                                                                  
&NAME    FOUT  &FLD,&MSG,&LMSG,&MDT=                                            
         LCLC  &R1,&R2,&R3,&C                                                   
         LCLA  &I,&L                                                            
.FLD     ANOP                                                                   
         AIF   (T'&FLD EQ 'O').ERR                                              
         AIF   ('&FLD'(1,1) EQ '(').FLDR                                        
.FLDN    ANOP                                                                   
&R1      SETC  '  '                                                             
         AIF   ('&MDT' EQ 'Y').FLDNM                                            
&NAME    OI    &FLD+6,OI1T                                                      
         AGO   .MSG                                                             
.FLDNM   ANOP                                                                   
&NAME    OI    &FLD+6,OI1MT                                                     
         AGO   .MSG                                                             
.FLDR    ANOP                                                                   
&R1      SETC  '&FLD(1)'                                                        
         AIF   ('&MDT' EQ 'Y').FLDRM                                            
&NAME    OI    6(&R1),OI1T                                                      
         AGO   .MSG                                                             
.FLDRM   ANOP                                                                   
&NAME    OI    6(&R1),OI1MT                                                     
         AGO   .MSG                                                             
.MSG     ANOP                                                                   
         AIF   (N'&SYSLIST EQ 1).END                                            
         AIF   (T'&MSG EQ 'O').ERR                                              
         AIF   ('&MSG'(1,1) EQ '(').MSGR                                        
&R2      SETC  '  '                                                             
         AIF   ('&MSG'(1,2) EQ '=C').MSGL                                       
         AIF   (T'&MSG EQ 'C').MSGC                                             
         AGO   .LMSG                                                            
.MSGR    ANOP                                                                   
&R2      SETC  '&MSG(1)'                                                        
         AGO   .LMSG                                                            
.MSGC    ANOP                                                                   
&L       SETA  L'&MSG                                                           
         AGO   .LMSG                                                            
.MSGL    ANOP                                                                   
         AIF   ('&MSG'(3,1) EQ '''').MSGL1                                      
         AIF   ('&MSG'(3,1) EQ 'L').MSGL2                                       
         AGO   .ERR                                                             
.MSGL1   ANOP                                                                   
&L       SETA  K'&MSG-4                                                         
         AGO   .LMSG                                                            
.MSGL2   ANOP                                                                   
&I       SETA  4                                                                
.MSGL3   ANOP                                                                   
         AIF   ('&MSG'(&I,1) EQ '''').MSGL4                                     
&I       SETA  &I+1                                                             
         AIF   (&I GT 6).ERR                                                    
         AGO   .MSGL3                                                           
.MSGL4   ANOP                                                                   
&C       SETC  '&MSG'(4,&I-4)                                                   
&L       SETA  0+&C                                                             
.LMSG    ANOP                                                                   
&R3      SETC  '  '                                                             
         AIF   (T'&LMSG EQ 'O' AND &L EQ 0).ERR                                 
         AIF   (T'&LMSG EQ 'O').FOUT1                                           
         AIF   ('&LMSG'(1,1) EQ '(').LMSGR                                      
         AIF   (T'&LMSG EQ 'N' AND '&LMSG'(1,1) GE '0').LMSGN                   
         AGO   .ERR                                                             
.LMSGR   ANOP                                                                   
&R3      SETC  '&LMSG(1)'                                                       
         AGO   .FOUT1                                                           
.LMSGN   ANOP                                                                   
&L       SETA  &LMSG                                                            
.FOUT1   ANOP                                                                   
         AIF   ('&R1' NE '  ' AND '&R3' EQ '  ').FOUT2                          
         AIF   ('&R1' NE '  ' AND '&R3' NE '  ').FOUT3                          
         AIF   ('&R1' EQ '  ' AND '&R3' EQ '  ').FOUT4                          
         AIF   ('&R1' EQ '  ' AND '&R3' NE '  ').FOUT5                          
.FOUT2   ANOP                                                                   
         NI    7(&R1),X'80'                                                     
         OI    7(&R1),&L                                                        
         AGO   .FOUT6                                                           
.FOUT3   ANOP                                                                   
         TM    7(&R1),X'80'                                                     
         JNO   *+8                                                              
         LA    &R3,128(&R3)                                                     
         STC   &R3,7(&R1)                                                       
         SLL   &R3,25                                                           
         SRL   &R3,25                                                           
         AGO   .FOUT6                                                           
.FOUT4   ANOP                                                                   
         NI    &FLD+7,X'80'                                                     
         OI    &FLD+7,&L                                                        
         AGO   .FOUT6                                                           
.FOUT5   ANOP                                                                   
         TM    &FLD+7,X'80'                                                     
         JNO   *+8                                                              
         LA    &R3,128(&R3)                                                     
         STC   &R3,&FLD+7                                                       
         SLL   &R3,25                                                           
         SRL   &R3,25                                                           
.FOUT6   ANOP                                                                   
         AIF   ('&R3' NE '  ').FOUTC                                            
.FOUT7   ANOP                                                                   
         AIF   ('&R1' EQ '  ' AND '&R2' EQ '  ').FOUT8                          
         AIF   ('&R1' EQ '  ' AND '&R2' NE '  ').FOUT9                          
         AIF   ('&R1' NE '  ' AND '&R2' EQ '  ').FOUTA                          
         AIF   ('&R1' NE '  ' AND '&R2' NE '  ').FOUTB                          
.FOUT8   ANOP                                                                   
         MVC   &FLD+8(&L),&MSG                                                  
         AGO   .END                                                             
.FOUT9   ANOP                                                                   
         MVC   &FLD+8(&L),0(&R2)                                                
         AGO   .END                                                             
.FOUTA   ANOP                                                                   
         MVC   8(&L,&R1),&MSG                                                   
         AGO   .END                                                             
.FOUTB   ANOP                                                                   
         MVC   8(&L,&R1),0(&R2)                                                 
         AGO   .END                                                             
.FOUTC   ANOP                                                                   
         BCTR  &R3,0                                                            
         EXRL  &R3,*+14                                                         
         LA    &R3,1(&R3)                                                       
         J     *+10                                                             
&L       SETA  0                                                                
         AGO   .FOUT7                                                           
.ERR     ANOP                                                                   
         MNOTE 1,'INVALID PARAMETERS TO FOUT'                                   
.END     ANOP                                                                   
         MEND                                                                   
