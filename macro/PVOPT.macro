*          DATA SET PVOPT      AT LEVEL 001 AS OF 05/26/78                      
*          DATA SET PA100023S  AT LEVEL 003 AS OF 02/08/78                      
*          DATA SET V10PAN23   AT LEVEL 017 AS OF 01/31/78                      
         PRINT OFF                                                              
         MACRO                                                                  
         PVOPT                                                         +        
               &BUSYLIB=EOJT,      DOS BUSY LIB OPTION                 +        
               &CCWEXIT=YES,       DOS CCWEXIT USE                     +        
               &COBCMPR=YES,       COBOL COMPRESSION                   +        
               &DATFMT=MMDDYY,     DATE FORMAT                         +        
               &DELVERS=99,        MAX DELETE VERSIONS KEPT ON BACKUP  +        
               &DOSREL=26,         DOS RELEASE NUMBER                  +        
               &ENQDEQ=NO,         FOR PAMS                            +        
               &FILSTAT=NO,        PRINT UNIT RECORD STATISTICS        +        
               &FTP=NO,            FOR LIBRARY FOOTPRINTING            +        
               &INCLVLS=2,         NESTED ++INCLUDE LEVELS             +        
               &INPUT81=NO,        81 BYTE INPUT                       +        
               &INSTSEC=0,         INSTALLATION SECURITY CODE          +        
               &LEVEL0=YES,        LEVEL NUMBER OF 0 ON UPDATE ALL     +        
               &LINECNT=56,        LINES PER PAGE                      +        
               &LISTDEL=YES,       LIST DELETED STATEMENTS             +        
               &LVLSTMP=YES,       LEVEL STAMP                         +        
               &OTHER=OTHER,       USER NAME FOR OTHER                 +        
               &PANDD1=PANDD1,     USER NAME FOR LIBRARY FILE NAME     +        
               &PANDD2=PANDD2,     USER NAME FOR OUTPUT FILE NAME      +        
               &PANDD3=PANDD3,     USER NAME FOR INPUT FILE NAME       +        
               &PCHINCL=YES,       PUNCH ++INCLUDES EXPANDED           +        
               &POWERVS=NO,        USING POWER VS                      +        
               &PRINTER=SYSPRINT,  OS PRINTER DCB NAME                 +        
               &PUNCH=SYSPUNCH,    OS PUNCH DCB NAME                   +        
               &READER=SYSIN,      OS READER DCB NAME                  +        
               &RSTSTAT=TEA,       RESTORE STATUS ON RESTORE BY NAME   +        
               &SECURTY=NO,        PAMS SECURITY CODE                  +        
               &SHRDASD=NO,        SHARED DASD                         +        
               &SKIPAGE=NO,        PAGE SKIP IN PAN#8                  +        
               &STACKER=V,         STACKER SELECT CHARACTER            +        
               &SSYS4=4,           DOS INPUT FILE SYSNO                +        
               &SSYS5=5,           DOS OUTPUT FILE SYSNO               +        
               &SSYS6=6,           DOS LIBRARY SYSNO                   +        
               &TSOTYPS=(USER1,USER2),  TSO QUALIFIERS                 +        
               &USRFMT1=(USER180,0,73,8,1,80,0,0,84),  USER FORMAT 1   +        
               &USRFMT2=(USER780,0,1,6,7,74,0,0,1),    USER FORMAT 2   +        
               &WAIT=CANCEL,       DOS MULTIPLE PARTITION PROTECTION   +        
               &ZUPTYPS=(USER1,USER2),  0-UP ROUTINES AND EXT FEATURES +        
               &AUDITDD=PANAUD,    AUDIT FILE DD OR DTF NAME           +        
               &CONFIG=,           OPERATING SYSTEM CONFIGURATION      +        
               &DESTID1=PNCL,      DESTINATION ID                      +        
               &LINEND=@,          END OF LINE CHARACTER               +        
               &PCLASS=A,          DOS PUNCH JOB CLASS                 +        
               &PSBUFRS=0,         POWER/VS PUTSPOOL BUFFER COUNT      +        
               &TRANSID=PANS,      SECURITY TRANSACTION ID             +        
               &SAVE=ONLINE,       BATCH OR ONLINE SAVE                +        
               &SUBMTDD=PANSUB,    SUBMIT FILE DD OR DTF NAME          +        
               &TABCHAR=;,         TAB CHARACTER                       +        
               &VERIFY=YES,        VERIFY OPTION                       +        
               &RENAME=YES,        RENAME PROD OR DISABLED DATA SETS   +        
               &SVCNUM=0            AUTHORIZATION SVC                           
.*                                                                              
         LCLA  &A1,&A2,&A3                                                      
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6,&B7                                      
         LCLC  &C1                                                              
.*                                                                              
.BUSYLIB AIF   ('&BUSYLIB' EQ 'TIMER').CCWEXIT                                  
         AIF   ('&BUSYLIB' EQ 'SYSLOG').CCWEXIT                                 
         AIF   ('&BUSYLIB' EQ 'EOST').CCWEXIT                                   
         AIF   ('&BUSYLIB' EQ 'EOJT').CCWEXIT                                   
&B1      SETB  1                                                                
         MNOTE 12,'BUSYLIB IS NOT TIMER OR SYSLOG OR EOST OR EOJT'              
.*                                                                              
.CCWEXIT AIF   ('&CCWEXIT' EQ 'YES' OR '&CCWEXIT' EQ 'NO').COBCMPR              
&B1      SETB  1                                                                
         MNOTE 12,'CCWEXIT IS NOT YES OR NO'                                    
.*                                                                              
.COBCMPR AIF   ('&COBCMPR' EQ 'YES' OR '&COBCMPR' EQ 'NO').DATFMT               
&B1      SETB  1                                                                
         MNOTE 12,'COBCMPR IS NOT YES OR NO'                                    
.*                                                                              
.DATFMT  AIF   ('&DATFMT' EQ 'MMDDYY' OR '&DATFMT' EQ 'DDMMYY').DELVERS         
         AIF   ('&DATFMT' EQ 'YYMMDD').DELVERS                                  
&B1      SETB  1                                                                
         MNOTE 12,'DATFMT IS NOT MMDDYY OR DDMMYY OR YYMMDD'                    
.*                                                                              
.DELVERS AIF   (T'&DELVERS NE 'N').DELVERE                                      
         AIF   (&DELVERS GE 1 AND &DELVERS LE 9999).DOSREL                      
.DELVERE MNOTE 12,'DELVERS IS NOT IN 1-9999 RANGE'                              
&B1      SETB  1                                                                
.*                                                                              
.DOSREL  AIF   ('&DOSREL' EQ 'EDOS').ENQDEQ                                     
         AIF   (T'&DOSREL NE 'N').DOSRELE                                       
         AIF   (&DOSREL GE 15 AND &DOSREL LE 99).ENQDEQ                         
.DOSRELE MNOTE 12,'DOSREL IS NOT IN 15-99 RANGE'                                
&B1      SETB  1                                                                
.*                                                                              
.ENQDEQ  AIF   ('&ENQDEQ' EQ 'YES' OR '&ENQDEQ' EQ 'NO').FILSTAT                
&B1      SETB  1                                                                
         MNOTE 12,'ENQDEQ IS NOT YES OR NO'                                     
.*                                                                              
.FILSTAT AIF   ('&FILSTAT' EQ 'YES' OR '&FILSTAT' EQ 'NO').FTP                  
&B1      SETB  1                                                                
         MNOTE 12,'FILSTAT IS NOT YES OR NO'                                    
.*                                                                              
.FTP     AIF   ('&FTP' EQ 'YES' OR '&FTP' EQ 'NO').INCLVLS                      
         AIF   ('&FTP' EQ 'SID').INCLVLS                                        
&B1      SETB  1                                                                
         MNOTE 12,'FTP IS NOT YES,SID OR NO.    DOS DOES NOT USE SID.'          
.*                                                                              
.INCLVLS AIF   (T'&INCLVLS NE 'N').INCLVLE                                      
         AIF   (&INCLVLS GE 1 AND &INCLVLS LE 6).INPUT81                        
.INCLVLE MNOTE 12,'INCLVLS IS NOT IN 1-6 RANGE'                                 
&B1      SETB  1                                                                
.*                                                                              
.INPUT81 AIF   ('&INPUT81' EQ 'YES' OR '&INPUT81' EQ 'NO').INSTSEC              
&B1      SETB  1                                                                
         MNOTE 12,'INPUT81 IS NOT YES OR NO'                                    
.*                                                                              
.INSTSEC AIF   (T'&INSTSEC NE 'N').INSTSEE                                      
         AIF   (&INSTSEC GE 0 AND &INSTSEC LE 32767).LEVEL0                     
.INSTSEE MNOTE 12,'INSTSEC IS NOT IN 0-32767 RANGE'                             
&B1      SETB  1                                                                
.*                                                                              
.LEVEL0  AIF   ('&LEVEL0' EQ 'YES' OR '&LEVEL0' EQ 'NO').LINECNT                
&B1      SETB  1                                                                
         MNOTE 12,'LEVEL0 IS NOT YES OR NO'                                     
.*                                                                              
.LINECNT AIF   (T'&LINECNT NE 'N').LINECNE                                      
         AIF   (&LINECNT GE 10 AND &LINECNT LE 99).LISTDEL                      
.LINECNE MNOTE 12,'LINECNT IS NOT IN 10-99 RANGE'                               
&B1      SETB  1                                                                
.*                                                                              
.LISTDEL AIF   ('&LISTDEL' EQ 'YES' OR '&LISTDEL' EQ 'NO').LVLSTMP              
&B1      SETB  1                                                                
         MNOTE 12,'LISTDEL IS NOT YES OR NO'                                    
.*                                                                              
.LVLSTMP AIF   ('&LVLSTMP' EQ 'YES' OR '&LVLSTMP' EQ 'NO').OTHER                
&B1      SETB  1                                                                
         MNOTE 12,'LVLSTMP IS NOT YES OR NO'                                    
.*                                                                              
.OTHER   AIF   (K'&OTHER EQ 5).PANDD1                                           
&B1      SETB  1                                                                
         MNOTE 12,'OTHER IS NOT 5 CHARACTERS LONG'                              
.*                                                                              
.PANDD1  AIF   (K'&PANDD1 GE 1 AND K'&PANDD1 LE 8).PANDD2                       
&B1      SETB  1                                                                
         MNOTE 12,'PANDD1 IS NOT IN 1-8 CHARACTER RANGE'                        
.*                                                                              
.PANDD2  AIF   (K'&PANDD2 GE 1 AND K'&PANDD2 LE 8).PANDD3                       
&B1      SETB  1                                                                
         MNOTE 12,'PANDD2 IS NOT IN 1-8 CHARACTER RANGE'                        
.*                                                                              
.PANDD3  AIF   (K'&PANDD3 GE 1 AND K'&PANDD3 LE 8).PCHINCL                      
&B1      SETB  1                                                                
         MNOTE 12,'PANDD3 IS NOT IN 1-8 CHARACTER RANGE'                        
.*                                                                              
.PCHINCL AIF   ('&PCHINCL' EQ 'YES' OR '&PCHINCL' EQ 'NO').POWERVS              
&B1      SETB  1                                                                
         MNOTE 12,'PCHINCL IS NOT YES OR NO'                                    
.*                                                                              
.POWERVS AIF   ('&POWERVS' EQ 'YES' OR '&POWERVS' EQ 'NO').PRINTER              
&B1      SETB  1                                                                
         MNOTE 12,'POWERVS IS NOT YES OR NO'                                    
.*                                                                              
.PRINTER AIF   (K'&PRINTER GE 1 AND K'&PRINTER LE 8).PUNCH                      
&B1      SETB  1                                                                
         MNOTE 12,'PRINTER IS NOT IN 1-8 CHARACTER RANGE'                       
.*                                                                              
.PUNCH   AIF   (K'&PUNCH GE 1 AND K'&PUNCH LE 8).READER                         
&B1      SETB  1                                                                
         MNOTE 12,'PUNCH IS NOT IN 1-8 CHARACTER RANGE'                         
.*                                                                              
.READER  AIF   (K'&READER GE 1 AND K'&READER LE 8).RSTSTAT                      
&B1      SETB  1                                                                
         MNOTE 12,'READER IS NOT IN 1-8 CHARACTER RANGE'                        
.*                                                                              
.RSTSTAT AIF   ('&RSTSTAT' EQ 'TEA' OR '&RSTSTAT' EQ 'TEI').SECURTY             
         AIF   ('&RSTSTAT' EQ 'TDA' OR '&RSTSTAT' EQ 'TDI').SECURTY             
         AIF   ('&RSTSTAT' EQ 'PEA' OR '&RSTSTAT' EQ 'PEI').SECURTY             
         AIF   ('&RSTSTAT' EQ 'PDA' OR '&RSTSTAT' EQ 'PDI').SECURTY             
&B1      SETB  1                                                                
         MNOTE 12,'RSTSTAT IS NOT A VALID STATUS COMBINATION'                   
.*                                                                              
.SECURTY AIF   ('&SECURTY' EQ 'YES' OR '&SECURTY' EQ 'NO').SHRDASD              
&B1      SETB  1                                                                
         MNOTE 12,'SECURTY IS NOT YES OR NO'                                    
.*                                                                              
.SHRDASD AIF   ('&SHRDASD' EQ 'YES' OR '&SHRDASD' EQ 'NO').SKIPAGE              
&B1      SETB  1                                                                
         MNOTE 12,'SHRDASD IS NOT YES OR NO'                                    
.*                                                                              
.SKIPAGE AIF   ('&SKIPAGE' EQ 'YES' OR '&SKIPAGE' EQ 'NO').STACKER              
&B1      SETB  1                                                                
         MNOTE 12,'SKIPAGE IS NOT YES OR NO'                                    
.*                                                                              
.STACKER AIF   (K'&STACKER EQ 1).SSYS4                                          
&B1      SETB  1                                                                
         MNOTE 12,'STACKER REQUIRES ONLY 1 CHARACTER'                           
.*                                                                              
.SSYS4   AIF   (T'&SSYS4 NE 'N').SSYS4E                                         
         AIF   (&SSYS4 GE 0 AND &SSYS4 LE 255).SSYS5                            
.SSYS4E  MNOTE 12,'SSYS4 IS NOT IN 0-255 RANGE'                                 
&B1      SETB  1                                                                
.*                                                                              
.SSYS5   AIF   (T'&SSYS5 NE 'N').SSYS5E                                         
         AIF   (&SSYS5 GE 0 AND &SSYS5 LE 255).SSYS6                            
.SSYS5E  MNOTE 12,'SSYS5 IS NOT IN 0-255 RANGE'                                 
&B1      SETB  1                                                                
.*                                                                              
.SSYS6   AIF   (T'&SSYS6 NE 'N').SSYS6E                                         
         AIF   (&SSYS6 GE 0 AND &SSYS6 LE 255).TSOTYPS                          
.SSYS6E  MNOTE 12,'SSYS6 IS NOT IN 0-255 RANGE'                                 
&B1      SETB  1                                                                
.*                                                                              
.TSOTYPS AIF   (T'&TSOTYPS(1) EQ 'O' AND T'&TSOTYPS(2) EQ 'O').TSOTYPE          
         AIF   (T'&TSOTYPS(1) EQ 'O').TSOTYP2                                   
         AIF   (K'&TSOTYPS(1) GT 5).TSOTYPE                                     
.TSOTYP2 AIF   (T'&TSOTYPS(2) EQ 'O').USRFMT1                                   
         AIF   (K'&TSOTYPS(2) LE 5).USRFMT1                                     
.TSOTYPE MNOTE 12,'TSOTYPS IS NOT IN 1-5 CHARACTER RANGE'                       
&B1      SETB  1                                                                
.*                                                                              
.USRFMT1 AIF   (K'&USRFMT1(1) GE 1 AND K'&USRFMT1(1) LE 8).USRF11               
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 NAME IS NOT IN 1-8 CHARACTER RANGE'                  
.USRF11  AIF   (T'&USRFMT1(2) NE 'N' OR T'&USRFMT1(3) NE 'N').USRF11E           
         AIF   (T'&USRFMT1(4) NE 'N' OR T'&USRFMT1(5) NE 'N').USRF11E           
         AIF   (T'&USRFMT1(6) NE 'N' OR T'&USRFMT1(7) NE 'N').USRF11E           
         AIF   (T'&USRFMT1(8) NE 'N').USRF11E                                   
         AIF   (T'&USRFMT1(9) EQ 'N').USRF12                                    
.USRF11E MNOTE 12,'USRFMT1 POSITIONAL OPERANDS AFTER NAME NOT NUMERIC'          
&B1      SETB  1                                                                
         AGO   .USRFMT2                                                         
.USRF12  AIF   (&USRFMT1(2) LT 77).USRF13                                       
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 LEVEL STAMP COLUMN NOT LESS THAN 77'                 
.USRF13  AIF   (&USRFMT1(3)+&USRFMT1(4) LE 81).USRF14                           
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 SEQUENCE FIELDS ADD UP TO OVER 81'                   
.USRF14  AIF   (&USRFMT1(5)+&USRFMT1(6) LE 81).USRF15                           
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 DATA FIELDS ADD UP TO OVER 81'                       
.USRF15  AIF   (&USRFMT1(7)+&USRFMT1(8) LE 81).USRF16                           
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 ID FIELDS ADD UP TO OVER 81'                         
.USRF16  AIF   (&USRFMT1(9) LT 77).USRF17                                       
         AIF   (&USRFMT1(9) GT 80 AND &USRFMT1(9) LE 84).USRF17                 
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 SEQUENCE RETRIEVE FIELD IN 77-80 OR OVER 84'         
.USRF17  AIF   (&USRFMT1(4) LE 8).USRF18                                        
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 SEQUENCE FIELD LENGTH OVER 8'                        
.USRF18  AIF   (&USRFMT1(8) LE 8).USRF19                                        
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 ID FIELD LENGTH OVER 8'                              
.USRF19  AIF   (&USRFMT1(7) NE 0 AND &USRFMT1(8) NE 0).USRF20                   
         AIF   (&USRFMT1(7) EQ 0 AND &USRFMT1(8) EQ 0).USRF20                   
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT1 BOTH ID FIELDS MUST BE 0 OR NOT 0'                   
.USRF20  AIF   (&USRFMT1(3) EQ 0 OR &USRFMT1(4) EQ 0).USRF19E                   
         AIF   (&USRFMT1(5) EQ 0 OR &USRFMT1(6) EQ 0).USRF19E                   
         AIF   (&USRFMT1(9) NE 0).USRFMT2                                       
.USRF19E MNOTE 12,'USRFMT1 0 ALLOWED ONLY IN LEVEL STAMP AND ID FIELDS'         
&B1      SETB  1                                                                
.*                                                                              
.USRFMT2 AIF   (K'&USRFMT2(1) GE 1 AND K'&USRFMT2(1) LE 8).USRF21               
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 NAME IS NOT IN 1-8 CHARACTER RANGE'                  
.USRF21  AIF   (&USRFMT2(2) LT 77).USRF21A                                      
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 LEVEL STAMP COLUMN NOT LESS THAN 77'                 
.USRF21A AIF   (T'&USRFMT2(2) NE 'N' OR T'&USRFMT2(3) NE 'N').USRF21E           
         AIF   (T'&USRFMT2(4) NE 'N' OR T'&USRFMT2(5) NE 'N').USRF21E           
         AIF   (T'&USRFMT2(6) NE 'N' OR T'&USRFMT2(7) NE 'N').USRF21E           
         AIF   (T'&USRFMT2(8) NE 'N').USRF21E                                   
         AIF   (T'&USRFMT2(9) EQ 'N').USRF22                                    
.USRF21E MNOTE 12,'USRFMT2 POSITIONAL OPERANDS AFTER NAME NOT NUMERIC'          
&B1      SETB  1                                                                
         AGO   .WAIT                                                            
.USRF22  AIF   (&USRFMT2(3)+&USRFMT2(4) LE 81).USRF23                           
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 SEQUENCE FIELDS ADD UP TO OVER 81'                   
.USRF23  AIF   (&USRFMT2(5)+&USRFMT2(6) LE 81).USRF24                           
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 DATA FIELDS ADD UP TO OVER 81'                       
.USRF24  AIF   (&USRFMT2(7)+&USRFMT2(8) LE 81).USRF25                           
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 ID FIELDS ADD UP TO OVER 81'                         
.USRF25  AIF   (&USRFMT2(9) LT 77).USRF26                                       
         AIF   (&USRFMT2(9) GT 80 AND &USRFMT2(9) LE 84).USRF26                 
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 SEQUENCE RETRIEVE FIELD IN 77-80 OR OVER 84'         
.USRF26  AIF   (&USRFMT2(4) LE 8).USRF27                                        
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 SEQUENCE FIELD LENGTH OVER 8'                        
.USRF27  AIF   (&USRFMT2(8) LE 8).USRF28                                        
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 ID FIELD LENGTH OVER 8'                              
.USRF28  AIF   (&USRFMT2(7) NE 0 AND &USRFMT2(8) NE 0).USRF29                   
         AIF   (&USRFMT2(7) EQ 0 AND &USRFMT2(8) EQ 0).USRF29                   
&B1      SETB  1                                                                
         MNOTE 12,'USRFMT2 BOTH ID FIELDS MUST BE 0 OR NOT 0'                   
.USRF29  AIF   (&USRFMT2(3) EQ 0 OR &USRFMT2(4) EQ 0).USRF29E                   
         AIF   (&USRFMT2(5) EQ 0 OR &USRFMT2(6) EQ 0).USRF29E                   
         AIF   (&USRFMT2(9) NE 0).WAIT                                          
.USRF29E MNOTE 12,'USRFMT2 0 ALLOWED ONLY IN LEVEL STAMP AND ID FIELDS'         
&B1      SETB  1                                                                
.*                                                                              
.WAIT    AIF   ('&WAIT' EQ 'CANCEL' OR '&WAIT' EQ 'WAIT').ZUPTYPS               
&B1      SETB  1                                                                
         MNOTE 12,'WAIT IS NOT CANCEL OR WAIT'                                  
.*                                                                              
.ZUPTYPS AIF   (T'&ZUPTYPS(1) EQ 'O' AND T'&ZUPTYPS(2) EQ 'O').ZUPTYPE          
         AIF   (T'&ZUPTYPS(1) EQ 'O').ZUPTYP2                                   
         AIF   (K'&ZUPTYPS(1) GT 5).ZUPTYPE                                     
.ZUPTYP2 AIF   (T'&ZUPTYPS(2) EQ 'O').CRCK1                                     
         AIF   (K'&ZUPTYPS(2) LE 5).CRCK1                                       
.ZUPTYPE MNOTE 12,'ZUPTYPS IS NOT IN 1-5 CHARACTER RANGE'                       
&B1      SETB  1                                                                
.*                                                                              
.CRCK1   AIF   ('&FTP' EQ 'NO').AUDITDD                                         
         AIF   ('&SHRDASD' EQ 'YES').AUDITDD                                    
&B1      SETB   1                                                               
         MNOTE 12,'FTP=YES OR SID REQUIRES SHRDASD=YES'                         
.*                                                                              
.AUDITDD AIF   ('&AUDITDD' EQ 'NO').CONFIG                                      
         AIF   (K'&AUDITDD GE 1 AND K'&AUDITDD LE 8).CONFIG                     
&B1      SETB   1                                                               
         MNOTE 12,'AUDITDD IS NOT NO OR IN 1-8 CHARACTER RANGE'                 
.*                                                                              
.CONFIG  AIF   (T'&CONFIG EQ 'O').DESTID1                                       
         AIF   ('&CONFIG' EQ 'DOS' OR '&CONFIG' EQ 'POWER').DESTID1             
         AIF   ('&CONFIG' EQ 'POWER/VS').DESTID1                                
         AIF   ('&CONFIG' EQ 'OS' OR '&CONFIG' EQ 'HASP').DESTID1               
&B1      SETB  1                                                                
         MNOTE 12,'CONFIG ISNT DOS OR POWER OR POWER/VS OR OS OR HASP'          
.*                                                                              
.DESTID1 AIF   (K'&DESTID1 GE 1 AND K'&DESTID1 LE 4).LINEND                     
&B1      SETB  1                                                                
         MNOTE 12,'DESTID1 IS NOT IN 1-4 CHARACTER RANGE'                       
.*                                                                              
.LINEND  AIF   (K'&LINEND EQ 1).PCLASS                                          
&B1      SETB  1                                                                
         MNOTE 12,'LINEND IS NOT 1 CHARACTER'                                   
.*                                                                              
.PCLASS  AIF   ('&PCLASS' GE 'A' AND '&PCLASS' LE 'I').SAVE                     
         AIF   ('&PCLASS' GE 'J' AND '&PCLASS' LE 'R').SAVE                     
         AIF   ('&PCLASS' GE 'S' AND '&PCLASS' LE 'Z').SAVE                     
&B1      SETB  1                                                                
         MNOTE 12,'PCLASS IS NOT ALPHABETIC'                                    
.*                                                                              
.SAVE    AIF   ('&SAVE' EQ 'ONLINE' OR '&SAVE' EQ 'BATCH').PSBUFRS              
&B1      SETB  1                                                                
         MNOTE 12,'SAVE IS NOT ONLINE OR BATCH'                                 
.*                                                                              
.PSBUFRS ANOP                                                                   
         AIF   (T'&PSBUFRS NE 'N').PSBUFRE                                      
         AIF   (&PSBUFRS GE 0 AND &PSBUFRS LE 99).PSBUFRX                       
.PSBUFRE ANOP                                                                   
&B1      SETB  1                                                                
         MNOTE 12,'PSBUFRS NOT IN 0-99 RANGE'                                   
.PSBUFRX ANOP                                                                   
.**********************************************************************         
.SUBMTDD AIF   (K'&SUBMTDD GE 1 AND K'&SUBMTDD LE 8).TABCHAR                    
&B1      SETB  1                                                                
         MNOTE 12,'SUBMTDD IS NOT IN 1-8 CHARACTER RANGE'                       
.*                                                                              
.TABCHAR AIF   (K'&TABCHAR EQ 1).TRANSID                                        
&B1      SETB  1                                                                
         MNOTE 12,'TABCHAR IS NOT 1 CHARACTER'                                  
.*                                                                              
.TRANSID ANOP                                                                   
         AIF   (K'&TRANSID EQ 4).TRANSIX                                        
&B1      SETB  1                                                                
         MNOTE 12,'TRANSID IS NOT 4 CHARACTERS'                                 
.TRANSIX ANOP                                                                   
.**********************************************************************         
.VERIFY  AIF   ('&VERIFY' EQ 'YES' OR '&VERIFY' EQ 'NO').REN                    
&B1      SETB  1                                                                
         MNOTE 12,'VERIFY IS NOT YES OR NO'                                     
.*                                                                              
.REN     AIF   ('&RENAME' EQ 'YES' OR '&RENAME' EQ 'NO').CRCK2                  
&B1      SETB  1                                                                
         MNOTE 12,'RENAME IS NOT YES OR NO'                                     
.*                                                                              
.CRCK2   AIF   ('&LINEND' NE '&TABCHAR').NUMSVC                                 
&B1      SETB  1                                                                
         MNOTE 12,'LINEND AND TABCHAR ARE THE SAME'                             
.*                                                                              
.NUMSVC  AIF   ('&SVCNUM' EQ '0').DOWEGO                                        
         AIF   ('&SVCNUM' GE '200' AND '&SVCNUM' LE '255').DLBCK2               
&B1      SETB  1                                                                
         MNOTE 12,'SVCNUM IS NOT 0 OR 200-255'                                  
.*                                                                              
.DLBCK2  ANOP                                                                   
         AIF   ('&LINEND' NE '&TABCHAR').DBLCK2X                                
&B1      SETB  1                                                                
         MNOTE 12,'LINEND AND TABCHAR ARE THE SAME'                             
.DBLCK2X ANOP                                                                   
.**********************************************************************         
.DOWEGO  AIF   (&B1 EQ 0).GEN                                                   
.NOGO    MNOTE 12,'ERRORS ENCOUNTERED - MACRO GENERATION TERMINATED'            
         MEXIT                                                                  
.*                                                                              
.GEN     MNOTE *,'THE FOLLOWING ARE SELECTED AND DEFAULT OPTIONS'               
         MNOTE *,'&BUSYLIB,FOR DOS BUSY LIBRARY OPTION'                         
         MNOTE *,'&CCWEXIT,FOR DOS CCWEXIT USE'                                 
         MNOTE *,'&COBCMPR,FOR COBOL COMPRESSION'                               
         MNOTE *,'&DATFMT,FOR DATE FORMAT'                                      
         MNOTE *,'&DELVERS,FOR MAX DELETE VERSIONS KEPT ON BACKUP'              
         MNOTE *,'&DOSREL,FOR DOS RELEASE NUMBER'                               
         MNOTE *,'&FILSTAT,FOR PRINT UNIT RECORD STATISTICS'                    
         MNOTE *,'&FTP,FOR LIBRARY FOOTPRINTING'                                
         MNOTE *,'&INCLVLS,FOR NESTED ++INCLUDE LEVELS'                         
         MNOTE *,'&INSTSEC,FOR AN INSTALLATION SECURITY CODE'                   
         MNOTE *,'&LEVEL0,FOR LEVEL 0 USE ON UPDATE ALL'                        
         MNOTE *,'&LINECNT,FOR LINES PER PAGE'                                  
         MNOTE *,'&LISTDEL,FOR LISTING DELETE STATEMENTS'                       
         MNOTE *,'&LVLSTMP,FOR LEVEL STAMP'                                     
         MNOTE *,'&OTHER,FOR OTHER FORMAT NAME'                                 
         MNOTE *,'&PANDD1,FOR THE LIBRARY FILE NAME'                            
         MNOTE *,'&PANDD2,FOR THE OUTPUT FILE NAME'                             
         MNOTE *,'&PANDD3,FOR THE INPUT FILE NAME'                              
         MNOTE *,'&PCHINCL,FOR PUNCHING ++INCLUDES EXPANDED'                    
         MNOTE *,'&PRINTER,FOR THE OS PRINTER DCB NAME'                         
         MNOTE *,'&PUNCH,FOR THE OS PUNCH DCB NAME'                             
         MNOTE *,'&READER,FOR THE OS READER DCB NAME'                           
         MNOTE *,'&RSTSTAT,FOR RESTORE STATUS ON RESTORE BY NAME'               
         MNOTE *,'&SHRDASD,FOR SHARED DASD'                                     
         MNOTE *,'&SKIPAGE,FOR PAGE SKIPPING IN PAN#8'                          
         MNOTE *,'&STACKER,FOR STACKER CONTROL CHARACTER'                       
         MNOTE *,'&SSYS4,FOR DOS INPUT FILE SYSNO'                              
         MNOTE *,'&SSYS5,FOR DOS OUTPUT FILE SYSNO'                             
         MNOTE *,'&SSYS6,FOR DOS LIBRARY SYSNO'                                 
         MNOTE *,'&TSOTYPS,FOR TSO QUALIFIERS'                                  
         MNOTE *,'&USRFMT1,FOR USER FORMAT 1'                                   
         MNOTE *,'&USRFMT2,FOR USER FORMAT 2'                                   
         MNOTE *,'&WAIT,FOR DOS MULTIPLE PARTITION PROTECTION'                  
         MNOTE *,'&ZUPTYPS,FOR 0-UP ROUTINES AND EXTENDED FEATURES'             
         MNOTE *,'&AUDITDD,FOR THE AUDIT FILE NAME'                             
         MNOTE *,'&CONFIG,FOR OPERATING SYSTEM CONFIGURATION'                   
         MNOTE *,'&DESTID1,FOR THE DESTINATION ID'                              
         MNOTE *,'&LINEND,FOR LINE END CHARACTER'                               
         MNOTE *,'&PCLASS,FOR DOS PUNCH JOB CLASS'                              
         MNOTE *,'&PSBUFRS,FOR NUMBER OF PUTSPOOL BUFFERS'                      
         MNOTE *,'&SAVE,FOR TYPE OF SAVE'                                       
         MNOTE *,'&SUBMTDD,FOR THE SUBMIT FILE NAME'                            
         MNOTE *,'&TABCHAR,FOR TAB CHARACTER'                                   
         MNOTE *,'&TRANSID,FOR SECURITY TRANSACTION ID'                         
         MNOTE *,'&VERIFY,FOR VERIFY OPTION'                                    
         MNOTE *,'&RENAME,FOR RENAME PROD OR DIS OPTION'                        
         MNOTE *,'&SVCNUM,FOR SVC AUTHORIZATION - CICS'                         
.*                                                                              
FGPAN23  CSECT                                                                  
&B1      SETB  ('&DATFMT' EQ 'DDMMYY')                                          
&B2      SETB  ('&DATFMT' EQ 'YYMMDD')                                          
         DC    B'&B1&B2'                 DATE FORMAT                            
&B1      SETB  ('&FTP' EQ 'YES')                                                
&B2      SETB  ('&SHRDASD' EQ 'YES')                                            
&B3      SETB  ('&BUSYLIB' EQ 'TIMER')                                          
&B4      SETB  ('&BUSYLIB' EQ 'SYSLOG')                                         
&B5      SETB  ('&BUSYLIB' EQ 'EOST')                                           
&B6      SETB  ('&BUSYLIB' EQ 'EOJT')                                           
&B7      SETB  ('&FTP' EQ 'SID')                                                
         DC    B'&B1&B2&B3&B4&B5&B6&B7.0' FTP OPTION                            
&B1      SETB  ('&ENQDEQ' EQ 'YES')                                             
         DC    X'&B1'                    ENQDEQ                                 
&B1      SETB  ('&RSTSTAT'(2,1) EQ 'D')                                         
&B2      SETB  ('&RSTSTAT'(3,1) EQ 'I')                                         
&B3      SETB  ('&RSTSTAT'(1,1) EQ 'P')                                         
         DC    B'&B1&B2&B3'              RESTORE STATUS                         
&B1      SETB  ('&SECURTY' EQ 'YES')                                            
         DC    X'&B1'                    SECURITY                               
&B1      SETB  ('&SKIPAGE' EQ 'YES')                                            
         DC    X'&B1'                     SKIP1                                 
         DC    X'&B1'                     SKIP2                                 
         DC    X'0'                       EXPINCL                               
         DC    C'&STACKER'                STACKER SELECT CHAR                   
         AIF   ('&DOSREL' NE 'EDOS').ARND0                                      
         DC    AL1(0)                     DOS RELEASE                           
         AGO   .ARND1                                                           
.ARND0   DC    AL1(&DOSREL)                                                     
.ARND1   ANOP                                                                   
&B1      SETB  ('&INPUT81' EQ 'YES')                                            
         DC    X'&B1'                     81 BYTE INPUT                         
&B1      SETB  ('&POWERVS' EQ 'YES')                                            
&B2      SETB  ('&CCWEXIT' EQ 'YES')                                            
         DC    B'&B1&B2'                  CCW EXIT                              
&A1      SETA  K'&USRFMT1(1)                                                    
&A2      SETA  K'&USRFMT2(1)                                                    
&A3      SETA  20-4-&A1-&A2                                                     
         DC    X'E00&A1'                  USER FMT1 CODE                        
         DC    C'&USRFMT1(1)'             USER FMT1 NAME                        
         DC    X'F00&A2'                  USER FMT2 CODE                        
         DC    C'&USRFMT2(1)'             USER FMT2 NAME                        
         DC    &A3.X'0'                                                         
         DC    CL5'&OTHER'                                                      
&B1      SETB  ('&LEVEL0' EQ 'YES')                                             
         DC    X'&B1'                     LEVEL 0 UPDATE                        
         DS    0CL26                                                            
         DC    X'4'                       Z UP   TSO TYPE                       
&C1      SETC  'USER1'                                                          
         AIF   (T'&ZUPTYPS(1) EQ 'O').ARND2                                     
&C1      SETC  '&ZUPTYPS(1)'                                                    
.ARND2   ANOP                                                                   
         DC    CL5'&C1'                   Z UP  TSO TYPE                        
         DC    X'4'                       Z UP  TSO TYPE                        
&C1      SETC  'USER2'                                                          
         AIF   (T'&ZUPTYPS(2) EQ 'O').ARND3                                     
&C1      SETC  '&ZUPTYPS(2)'                                                    
.ARND3   ANOP                                                                   
         DC    CL5'&C1'                   Z UP  TSO TYPE                        
         DC    X'4'                       Z UP  TSO TYPE                        
&C1      SETC  'USER1'                                                          
         AIF   (T'&TSOTYPS(1) EQ 'O').ARND4                                     
&C1      SETC  '&TSOTYPS(1)'                                                    
.ARND4   ANOP                                                                   
         DC    CL5'&C1'                   Z UP  TSO TYPE                        
         DC    X'4'                       Z UP  TSO TYPE                        
&C1      SETC  'USER2'                                                          
         AIF   (T'&TSOTYPS(2) EQ 'O').ARND5                                     
&C1      SETC  '&TSOTYPS(2)'                                                    
.ARND5   ANOP                                                                   
         DC    CL5'&C1'                   Z UP  TSO TYPE                        
         DC    2X'0'                                                            
         DC    H'&USRFMT1(2),&USRFMT1(3),&USRFMT1(4),&USRFMT1(5),&USRFM+        
               T1(6),&USRFMT1(7),&USRFMT1(8),&USRFMT1(9)'                       
         DC    H'&USRFMT2(2),&USRFMT2(3),&USRFMT2(4),&USRFMT2(5),&USRFM+        
               T2(6),&USRFMT2(7),&USRFMT2(8),&USRFMT2(9)'                       
         DC    P'&LINECNT'                LINE COUNT                            
         DC    AL1(&SSYS4)                SYS4                                  
         DC    AL1(&SSYS5)                SYS5                                  
         DC    AL1(&SSYS6)                SYS6                                  
         DC    C' '                       INCLUDE PUNCH                         
&C1      SETC  '&WAIT'(1,1)                                                     
         DC    C'&C1'                     WAIT OPT                              
         DC    CL8'&READER'               READER                                
         DC    CL8'&PRINTER'              PRINTER                               
         DC    CL8'&PANDD1'               PANDD1                                
         DC    CL8'&PANDD2'               PANDD2                                
         DC    CL8'&PANDD3'               PANDD3                                
         DC    CL8'&PUNCH'                PUNCH                                 
&C1      SETC  '&LVLSTMP'(1,1)                                                  
         DC    C'&C1'                     LEVEL STAMP                           
         DC    X'&INCLVLS'                INCLUDE LEVELS                        
&C1      SETC  '&COBCMPR'(1,1)                                                  
         DC    C'&C1'                     COBOL COMP                            
&C1      SETC  '&FILSTAT'(1,1)                                                  
         DC    C'&C1'                     FILE STATS                            
&A1      SETA  0                                                                
         AIF   ('&PCHINCL' NE 'YES').CKDEL                                      
&A1      SETA  1                                                                
.CKDEL   AIF   ('&LISTDEL' NE 'YES').ARND6                                      
&A1      SETA  &A1+2                                                            
.ARND6   ANOP                                                                   
         DC    X'&A1'                                                           
         DC    H'&DELVERS'                DELETE VERSIONS                       
         DC    H'&INSTSEC'                I                                     
         AIF   ('&AUDITDD' NE 'NO').ARND7                                       
         DC    CL8' '                     AUDIT DD                              
         AGO   .ARND8                                                           
.ARND7   ANOP                                                                   
         DC    CL8'&AUDITDD'              AUDIT DD                              
.ARND8   ANOP                                                                   
&B1      SETB  ('&CONFIG' EQ 'HASP')                                            
&B2      SETB  ('&CONFIG' EQ 'OS')                                              
&B3      SETB  ('&CONFIG' EQ 'POWER/VS')                                        
&B4      SETB  ('&CONFIG' EQ 'POWER' OR '&CONFIG' EQ 'DOS')                     
         DC    B'&B1&B2&B3&B4'          OPSYS                                   
         DC    CL4'&DESTID1'            DEST ID                                 
         DC    CL4'&TRANSID'            TRANS ID                                
         DC    CL1'&PCLASS'             P CLASS                                 
         DC    AL1(&PSBUFRS)            PSBUFFERS                               
         DC    AL1(&SVCNUM)             SVCNUM                                  
         DC    X'00'              *****UNUSED*****                              
         DC    CL1'&LINEND'             LINE END CHAR                           
&C1      SETC  '&SAVE'(1,1)                                                     
         DC    CL1'&C1'                 SAVE                                    
         DC    CL8'&SUBMTDD'            SUBMIT DD                               
         DC    CL1'&TABCHAR'            TAB CHAR                                
&C1      SETC  '&VERIFY'(1,1)                                                   
         DC    CL1'&C1'                 VERIFY OPT                              
&C1      SETC  '&RENAME'(1,1)                                                   
         DC    CL1'&C1'                 PROD/DIS RENAME OPT                     
         MEND                                                                   
         PRINT ON                                                               
