*          DATA SET REREQMAC   AT LEVEL 035 AS OF 06/12/02                      
*                                                                               
*                                                                               
***********************************************************************         
*                                                                     *         
*    REREQMAC (INCLUDE DATA SET) --- MACRO EXPANSIONS USED IN         *         
*                                    REREQ10 AND REREQ11              *         
*                                                                     *         
*---------------------------------------------------------------------*         
*                                                                     *         
* UPDATE HISTORY:                                                     *         
* --------------                                                      *         
* AUG21/90 (MRR) --- INITIAL RELEASE, MOVED FROM REREQ10 AND 11       *         
*                    CREATE SIO AND SIS AS SHORT FORM OF HIO AND HIS  *         
*                                                                     *         
* OCT12/90 (BU ) --- ADD DOWNLOAD=Y/N KEYWORD                         *         
*                                                                     *         
* OCT19/90 (BU ) --- ADD EXT=XX KEYWORD, WHERE XX IS THE NUMBER       *         
*                    FOR HELP SCREEN                                  *         
*                                                                     *         
* JAN21/91 (BU ) --- ADD RRGEN=X KEYWORD, WHERE X IS EITHER Y OR N    *         
*                                                                     *         
* JAN08/92 (BU ) --- ADD MASTR=X KEYWORD, WHERE X IS EITHER Y OR N    *         
*                    TO ALLOW/PROHIBIT REQUEST FOR A MASTER REP       *         
*                                                                     *         
* SEP03/92 (BU ) --- ADD FRIDAY=X KEYWD,  WHERE X IS EITHER Y OR N    *         
*                    TO REQUIRE OFFICE AND/OR STATION FILTER IF DAY   *         
*                    OF REQUEST NOT FRIDAY                            *         
*                                                                     *         
* OCT29/93 (SKU) --- CHANGE GROUP/SUBGROUP, SALESPERSON AND ADVERTISER*         
*                    TO 5 CHARS TO ACCOMMODATE RRG-ONLY SET           *         
*                    IDENTIFIERS                                      *         
*                                                                     *         
* JAN26/94 (BU ) --- ADD FOUR NEW FILTER FIELDS TO REQUEST FILTERS.   *         
*                                                                     *         
* MAR23/95 (BU ) --- REARRANGE FILTER FIELDS                          *         
*                                                                     *         
* MAR28/95 (BU ) --- RESEQUENCE FILTER FIELDS PER NEW DESIGN          *         
*                                                                     *         
* OCT26/95 (BU ) --- CHANGE OFFICE/ CON TYPE, DEV CONTYPE, POINT      *         
*                    PERSON FIELDS TO ACCOMMODATE RRG-ONLY SET        *         
*                    IDENTIFIERS                                      *         
*                                                                     *         
* JUL12/99 (BU ) --- CHANGE MARKET TO ACCOMMODATE RRG-ONLY SET ID'S   *         
*                                                                     *         
* OCT12/01 (BU ) --- ADD BUYRD=X  KEYWD,  WHERE X IS EITHER Y OR N    *         
*                    TO REQUIRE SETTING OF 'USE BUYREADER LOGIC'      *         
*                    FLAG IN RRG                                      *         
*                                                                     *         
*                                                                     *         
*                    ***  END TOMBSTONE  ***                          *         
***********************************************************************         
*                                                                               
*                                                                               
*                                                                               
         TITLE 'REREQMAC -- GFLD MACRO DEFINITION'                              
*                                                                               
*- MACRO TO GENERATE 1 FIELD DEFINITION ENTRY                                   
         MACRO                                                                  
         GFLD  &FLD,&ROW,&COL,&FMT=0,&CNTL=0,&RTN=NONE,&EXT=N                   
.*                                                                              
         LCLA  &ERROR                                                           
.*                                                                              
&ERROR   SETA  0                                                                
.*                                                                              
.* VALIDATE NAME PARAMETER                                                      
         AIF   (T'&FLD NE 'O').VAL10                                            
         MNOTE 12,'MISSING FIELD NAME.  1-7 CHARACTER EQU NAME'                 
&ERROR   SETA  1                                                                
.*                                                                              
.* VALIDATE ROW.  REQUIRED NUMERIC 1-24                                         
.VAL10   ANOP                                                                   
         AIF   (T'&ROW NE 'O').VAL15                                            
         MNOTE 12,'MISSING ROW.  NUMERIC, 1-24'                                 
&ERROR   SETA  1                                                                
         AGO   .VAL20                                                           
.*                                                                              
.VAL15   ANOP                                                                   
         AIF   (&ROW GE 01).VAL20                                               
         AIF   (&ROW LE 24).VAL20                                               
         MNOTE 12,'INVALID ROW.  NUMERIC, 1-24'                                 
&ERROR   SETA  1                                                                
.*                                                                              
.* VALIDATE COLUMN.  REQUIRED NUMERIC 1-80                                      
.VAL20   ANOP                                                                   
         AIF   (T'&ROW NE 'O').VAL25                                            
         MNOTE 12,'MISSING ROW.  NUMERIC, 1-80'                                 
&ERROR   SETA  1                                                                
         AGO   .VAL30                                                           
.*                                                                              
.VAL25   ANOP                                                                   
         AIF   (&ROW GE 01).VAL30                                               
         AIF   (&ROW LE 80).VAL30                                               
         MNOTE 12,'INVALID ROW.  NUMERIC, 1-80'                                 
&ERROR   SETA  1                                                                
.*                                                                              
.* EXIT IF WE HAD ERRORS                                                        
.VAL30   ANOP                                                                   
         AIF   (&ERROR NE 0).GETOUT                                             
.*                                                                              
.* DEFINE FIELD ENTRY                                                           
         AIF   ('&RTN' EQ 'NONE').DEF20                                         
         DC    AL2(&FLD),AL1(&ROW,&COL),AL2(&RTN)                               
         AGO   .DEF30                                                           
.DEF20   ANOP                                                                   
         DC    AL2(&FLD),AL1(&ROW,&COL),AL2(0)                                  
.DEF30   ANOP                                                                   
         AIF   ('&EXT' NE 'N').DEF35                                            
         DC    XL1'&FMT',AL1(&CNTL),AL1(0)                                      
         AGO   .GETOUT                                                          
.DEF35   ANOP                                                                   
         AIF   ('&EXT' NE 'Y').DEF36                                            
         DC    XL1'&FMT',AL1(&CNTL),AL1(&RTN)                                   
         AGO   .GETOUT                                                          
.DEF36   ANOP                                                                   
         DC    XL1'&FMT',AL1(&CNTL),AL1(&EXT)                                   
.*                                                                              
.GETOUT  ANOP                                                                   
         MEND                                                                   
         TITLE 'REREQMAC --- GENLN MACRO DEFINITION'                            
*                                                                               
*- MACRO TO DEFINE 1 SCREEN LINE WITH LABEL, IPT FIELD, EXPANSION               
*                                                                               
         MACRO                                                                  
&TAG     GENLN &NAME,&ROW,&COL=0,&LBL=Y,&IPT=0,&XP=0,&OV=N,&SOON=N,    X        
               &FMT=0,&NUM=N,&LAST=N,&VAL=N,&EXT=N                              
.*                                                                              
.*                                                                              
         LCLC  &QNAME,&IN,&EXP,&VNAME,&CNTL                                     
         LCLA  &I                                                               
.*                                                                              
.*                                                                              
&QNAME   SETC  'Q&NAME'                                                         
&VNAME   SETC  'V&NAME'                                                         
.*                                                                              
&I       SETA  1                                                                
.*                                                                              
         AIF   ('&LAST' EQ 'N').LBL00                                           
         DC    XL2'00'                                                          
         AGO   .OUT                                                             
.*                                                                              
.LBL00   ANOP                                                                   
*                                                                               
         AIF   ('&LBL' NE 'Y').IPT00                                            
.*                                                                              
.* CALL GENFLD MACRO FOR LABEL                                                  
.*                                                                              
&CNTL    SETC  'FCCMT'                                                          
.*                                                                              
         AIF   ('&OV' EQ 'N').LBL10                                             
&CNTL    SETC  'FCRQOVRN+&CNTL'                                                 
.*                                                                              
.LBL10   ANOP                                                                   
         AIF   ('&SOON' EQ 'N').LBL20                                           
&CNTL    SETC  'FCRQSOON+&CNTL'                                                 
.*                                                                              
.LBL20   ANOP                                                                   
         GFLD  &QNAME,&ROW,&COL(&I),CNTL=&CNTL                                  
.*                                                                              
&I       SETA  &I+1                                                             
.*                                                                              
.*                                                                              
.IPT00   ANOP                                                                   
         AIF   (&IPT EQ 0).XPN00                                                
.*                                                                              
.* CALL GENFLD MACRO FOR INPUT FIELD                                            
.*                                                                              
&CNTL    SETC  '0'                                                              
.*                                                                              
         AIF   ('&NUM' NE 'Y').IPT10                                            
&CNTL    SETC  'FCNUM+&CNTL'                                                    
.*                                                                              
.IPT10   ANOP                                                                   
         AIF   (&XP EQ 0).IPT20                                                 
&CNTL    SETC  'FCXPAN+&CNTL'                                                   
.*                                                                              
.IPT20   ANOP                                                                   
         AIF   ('&OV' EQ 'N').IPT30                                             
&CNTL    SETC  'FCRQOVRN+&CNTL'                                                 
.*                                                                              
.IPT30   ANOP                                                                   
         AIF   ('&SOON' EQ 'N').IPT40                                           
&CNTL    SETC  'FCRQSOON+&CNTL'                                                 
.*                                                                              
.IPT40   ANOP                                                                   
         AIF   ('&VAL' EQ 'N').IPT50                                            
&CNTL    SETC  'FCVAL+&CNTL'                                                    
.*                                                                              
.IPT50   ANOP                                                                   
&IN      SETC  'IFLD3&IPT'                                                      
         GFLD  &IN,&ROW,&COL(&I),FMT=&FMT,RTN=&VNAME,                  X        
               CNTL=&CNTL,EXT=&EXT                                              
.*                                                                              
&I       SETA  &I+1                                                             
.*                                                                              
.*                                                                              
.XPN00   ANOP                                                                   
         AIF   (&XP EQ 0).OUT                                                   
.*                                                                              
.* CALL GENFLD MACRO FOR EXPANSION FIELD                                        
.*                                                                              
&CNTL    SETC  'FCCMT'                                                          
&EXP     SETC  'IFLD3&XP'                                                       
         GFLD  &EXP,&ROW,&COL(&I),CNTL=&CNTL                                    
.*                                                                              
.OUT     ANOP                                                                   
         MEND                                                                   
         TITLE 'REREQMAC --- STANDARD FILTER SCREEN MACRO'                      
*                                                                               
*- GENERATE STANDARD FILTER SCREEN                                              
*                                                                               
         MACRO                                                                  
         STDFT &HIO=NNNNNNNNNNNNNNNNNNNN,&HIS=NNNNNNNNNNNNNNNNNNNN              
.*                  00000000011111111112      00000000011111111112              
.*                  12345678901234567890      12345678901234567890              
.*                                                                              
         LCLA  &I                                                               
         LCLC  &O,&S,&TEMP                                                      
.*                                                                              
         GENLN CMT0130,05,COL=26                                                
.*                                                                              
.* FIELD 01 -- OFFICE/REGION                                                    
&O       SETC  '&HIO'(1,1)                                                      
&S       SETC  '&HIS'(1,1)                                                      
         GENLN OFFREGN,06,COL=(02,14,20),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 02 -- OFFICE                                                           
&O       SETC  '&HIO'(2,1)                                                      
&S       SETC  '&HIS'(2,1)                                                      
         GENLN OFFICEC,06,COL=(42,51,59),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 03 -- GROUP + SUBGROUP                                                 
&O       SETC  '&HIO'(3,1)                                                      
&S       SETC  '&HIS'(3,1)                                                      
         GENLN GRPSUBG,07,COL=(02,14,20),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 04 -- STATION                                                          
&O       SETC  '&HIO'(4,1)                                                      
&S       SETC  '&HIS'(4,1)                                                      
         GENLN STATION,07,COL=(42,51,59),IPT=06,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 05 -- SALESPERSON                                                      
&O       SETC  '&HIO'(5,1)                                                      
&S       SETC  '&HIS'(5,1)                                                      
         GENLN SALPERS,08,COL=(02,14,20),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 06 -- DIVISION + TEAM                                                  
&O       SETC  '&HIO'(6,1)                                                      
&S       SETC  '&HIS'(6,1)                                                      
         GENLN DIVTEAM,08,COL=(42,51,59),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 07 -- ADVERTISER CODE                                                  
&O       SETC  '&HIO'(7,1)                                                      
&S       SETC  '&HIS'(7,1)                                                      
         GENLN ADVERTC,09,COL=(02,14,20),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 08 -- AGENCY CODE                                                      
&O       SETC  '&HIO'(8,1)                                                      
&S       SETC  '&HIS'(8,1)                                                      
         GENLN AGENCYC,09,COL=(42,51,59),IPT=07,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 09 -- PRODUCT CODE                                                     
&O       SETC  '&HIO'(9,1)                                                      
&S       SETC  '&HIS'(9,1)                                                      
         GENLN PRODUCT,10,COL=(02,14,20),IPT=03,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 10 -- CLASS CODE                                                       
&O       SETC  '&HIO'(10,1)                                                     
&S       SETC  '&HIS'(10,1)                                                     
         GENLN CLASSCD,10,COL=(42,51,59),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 11 -- CATEGORY CODE                                                    
&O       SETC  '&HIO'(11,1)                                                     
&S       SETC  '&HIS'(11,1)                                                     
         GENLN CATGORY,11,COL=(02,11,12),IPT=02,                       X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 12 -- CONTRACT TYPE                                                    
&O       SETC  '&HIO'(12,1)                                                     
&S       SETC  '&HIS'(12,1)                                                     
         GENLN CONTYPE,11,COL=(22,31,32),IPT=05,                       X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 13 -- NEW BUSINESS DEVELOPMENTAL TYPE                                  
&O       SETC  '&HIO'(13,1)                                                     
&S       SETC  '&HIS'(13,1)                                                     
         GENLN BUSTYP,11,COL=(42,51,52),IPT=05,                        X        
               EXT=Y                                                            
.*                                                                              
.* FIELD 14 -- DEVELOPMENTAL SALESPERSON                                        
&O       SETC  '&HIO'(14,1)                                                     
&S       SETC  '&HIS'(14,1)                                                     
         GENLN DEVSP,11,COL=(62,71,72),IPT=03,                         X        
               EXT=Y                                                            
.*                                                                              
.* FIELD 15 -- STATION TYPE                                                     
&O       SETC  '&HIO'(15,1)                                                     
&S       SETC  '&HIS'(15,1)                                                     
         GENLN STATYPE,12,COL=(02,11,12),IPT=01,                       X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 16 -- OWNER                                                            
&O       SETC  '&HIO'(16,1)                                                     
&S       SETC  '&HIS'(16,1)                                                     
         GENLN OWNER,12,COL=(22,31,32),IPT=03,                         X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 17 -- TVB REGION                                                       
&O       SETC  '&HIO'(17,1)                                                     
&S       SETC  '&HIS'(17,1)                                                     
         GENLN TVB,12,COL=(42,51,52),IPT=02,                           X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 18 -- RANK                                                             
&O       SETC  '&HIO'(18,1)                                                     
&S       SETC  '&HIS'(18,1)                                                     
         GENLN RANK,12,COL=(62,71,72),IPT=01,                          X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 19 -- MARKET                                                           
&O       SETC  '&HIO'(19,1)                                                     
&S       SETC  '&HIS'(19,1)                                                     
         GENLN MARKET,13,COL=(02,11,12),IPT=05,                        X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 20 -- NETWORK AFFILIATION                                              
&O       SETC  '&HIO'(20,1)                                                     
&S       SETC  '&HIS'(20,1)                                                     
         GENLN AFFILAT,13,COL=(22,31,32),IPT=03,                       X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 21 -- POINT PERSON                                                     
         GENLN PPSON,13,COL=(42,51,52),IPT=05,                         X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 22 -- NETWORK CONTRACT NUMBER                                          
         GENLN NETCON,13,COL=(62,71,72),IPT=08,                        X        
               EXT=Y                                                            
.*                                                                              
         GENLN CMT0132,16,COL=26                                                
         MEND                                                                   
         TITLE 'REREQMAC --- SHORT FILTER SCREEN MACRO'                         
*                                                                               
*- GENERATE SHORT STANDARD FILTER SCREEN                                        
*                                                                               
         MACRO                                                                  
         SHDFT &SIO=NNNNNNNNNNNNN,&SIS=NNNNNNNNNNNNN                            
.*                  0000000001111      0000000001111                            
.*                  1234567890123      1234567890123                            
.*                                                                              
         LCLA  &I                                                               
         LCLC  &O,&S,&TEMP                                                      
.*                                                                              
         GENLN CMT0130,05,COL=26                                                
.*                                                                              
.* FIELD 01 -- OFFICE/REGION                                                    
&O       SETC  '&SIO'(1,1)                                                      
&S       SETC  '&SIS'(1,1)                                                      
         GENLN OFFREGN,06,COL=(02,14,20),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 02 -- OFFICE                                                           
&O       SETC  '&SIO'(2,1)                                                      
&S       SETC  '&SIS'(2,1)                                                      
         GENLN OFFICEC,06,COL=(42,51,59),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 03 -- GROUP + SUBGROUP                                                 
&O       SETC  '&SIO'(3,1)                                                      
&S       SETC  '&SIS'(3,1)                                                      
         GENLN GRPSUBG,07,COL=(02,14,20),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 04 -- STATION                                                          
&O       SETC  '&SIO'(4,1)                                                      
&S       SETC  '&SIS'(4,1)                                                      
         GENLN STATION,07,COL=(42,51,59),IPT=06,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 05 -- SALESPERSON                                                      
&O       SETC  '&SIO'(5,1)                                                      
&S       SETC  '&SIS'(5,1)                                                      
         GENLN SALPERS,08,COL=(02,14,20),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 06 -- DIVISION + TEAM                                                  
&O       SETC  '&SIO'(6,1)                                                      
&S       SETC  '&SIS'(6,1)                                                      
         GENLN DIVTEAM,08,COL=(42,51,59),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 07 -- ADVERTISER CODE                                                  
&O       SETC  '&SIO'(7,1)                                                      
&S       SETC  '&SIS'(7,1)                                                      
         GENLN ADVERTC,09,COL=(02,14,20),IPT=05,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 08 -- AGENCY CODE                                                      
&O       SETC  '&SIO'(8,1)                                                      
&S       SETC  '&SIS'(8,1)                                                      
         GENLN AGENCYC,09,COL=(42,51,59),IPT=07,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 09 -- PRODUCT CODE                                                     
&O       SETC  '&SIO'(9,1)                                                      
&S       SETC  '&SIS'(9,1)                                                      
         GENLN PRODUCT,10,COL=(02,14,20),IPT=03,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 10 -- CLASS CODE                                                       
&O       SETC  '&SIO'(10,1)                                                     
&S       SETC  '&SIS'(10,1)                                                     
         GENLN CLASSCD,10,COL=(42,51,59),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 11 -- CATEGORY CODE                                                    
&O       SETC  '&SIO'(11,1)                                                     
&S       SETC  '&SIS'(11,1)                                                     
         GENLN CATGORY,11,COL=(02,14,20),IPT=02,XP=20,                 X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 12 -- CONTRACT TYPE                                                    
&O       SETC  '&SIO'(12,1)                                                     
&S       SETC  '&SIS'(12,1)                                                     
         GENLN CONTYPE,11,COL=(42,51,59),IPT=02,                       X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
.* FIELD 13 -- STATION TYPE                                                     
&O       SETC  '&SIO'(13,1)                                                     
&S       SETC  '&SIS'(13,1)                                                     
         GENLN STATYPE,12,COL=(02,14,20),IPT=01,                       X        
               OV=&O,SOON=&S,EXT=Y                                              
.*                                                                              
         GENLN CMT0132,13,COL=26                                                
         MEND                                                                   
         TITLE 'REREQMAC --- RQDEF MACRO / REQUEST DEFINITION'                  
*                                                                               
*- MACRO TO DEFINE BASIC REQTBL ENTRY                                           
*                                                                               
         MACRO                                                                  
         RQDEF &ID=,&NAME=,&PROC=0,&OVR=Y,&SOON=Y,&DDS=N,&MENU=Y,      X        
               &MARK=N,&XREQ=N,&OFFLMT=Y,&PREFIX=R,&CID=,&RRG=,        X        
               &ENDEF=N,&DOWN=Y,&RRGEN=N,&MASTR=Y,&FRIDAY=N,&BUYRD=N            
.*                                                                              
         LCLC  &LBL,&CNTL,&CDID,&DFLT,&CNTL2                                    
.*                                                                              
&LBL     SETC  '&PREFIX.&ID'                                                    
&DFLT    SETC  'DF&LBL'                                                         
.*                                                                              
.* END OF DEFINITION?                                                           
.* GENERATE LAST FIELD ENTRY THEN END OF RQST DEF LABEL.                        
         AIF   ('&ENDEF' EQ 'N').RQD00                                          
         GENLN LAST=Y                                                           
&LBL.X   EQU   *                                                                
         MEXIT                                                                  
.*                                                                              
.* ANALYZE CALL                                                                 
.*                                                                              
.RQD00   ANOP                                                                   
.*                                                                              
.* SET REQUEST CONTROL1 BITS                                                    
&CNTL    SETC  '0'                                                              
         AIF   ('&OVR' EQ 'N').RQD20                                            
&CNTL    SETC  'RQ1OVRNT+&CNTL'                                                 
.*                                                                              
.RQD20   ANOP                                                                   
         AIF   ('&SOON' EQ 'N').RQD30                                           
&CNTL    SETC  'RQ1SOON+&CNTL'                                                  
.*                                                                              
.RQD30   ANOP                                                                   
         AIF   ('&DDS' EQ 'N').RQD40                                            
&CNTL    SETC  'RQ1DDS+&CNTL'                                                   
.*                                                                              
.RQD40   ANOP                                                                   
         AIF   ('&MENU' EQ 'Y').RQD50                                           
&CNTL    SETC  'RQ1XMENU+&CNTL'                                                 
.*                                                                              
.RQD50   ANOP                                                                   
         AIF   ('&MARK' EQ 'N').RQD60                                           
&CNTL    SETC  'RQ1FMARK+&CNTL'                                                 
.*                                                                              
.RQD60   ANOP                                                                   
         AIF   ('&XREQ' EQ 'N').RQD70                                           
&CNTL    SETC  'RQ1XREQ+&CNTL'                                                  
.*                                                                              
.RQD70   ANOP                                                                   
         AIF   ('&OFFLMT' EQ 'N').RQD80                                         
&CNTL    SETC  'RQ1OFLMT+&CNTL'                                                 
.*                                                                              
.RQD80   ANOP                                                                   
.*                                                                              
.* SET REQUEST CONTROL2 BITS                                                    
&CNTL2   SETC  '0'                                                              
         AIF   ('&DOWN' EQ 'N').RQD82                                           
&CNTL2   SETC  'RQ2DWNLD+&CNTL2'                                                
.*                                                                              
.RQD82   ANOP                                                                   
.*                                                                              
         AIF   ('&RRGEN' EQ 'N').RQD84                                          
&CNTL2   SETC  'RQ2RRG+&CNTL2'                                                  
.*                                                                              
.RQD84   ANOP                                                                   
.*                                                                              
         AIF   ('&MASTR' EQ 'Y').RQD86A                                         
         AIF   ('&MASTR' EQ 'O').RQD86B                                         
         AGO   .RQD86Z                                                          
.*                                                                              
.RQD86A  ANOP                                                                   
&CNTL2   SETC  'RQ2MASTR+&CNTL2'                                                
         AGO   .RQD86Z                                                          
.*                                                                              
.RQD86B  ANOP                                                                   
&CNTL2   SETC  'RQ2MASTR+RQ2MASOV+&CNTL2'                                       
.*                                                                              
.RQD86Z  ANOP                                                                   
.*                                                                              
         AIF   ('&FRIDAY' EQ 'N').RQD87                                         
&CNTL2   SETC  'RQ2FRIDY+&CNTL2'                                                
.*                                                                              
.RQD87   ANOP                                                                   
.*                                                                              
         AIF   ('&BUYRD' EQ 'N').RQD88                                          
&CNTL2   SETC  'RQ2BYRED+&CNTL2'                                                
.*                                                                              
.RQD88   ANOP                                                                   
.*                                                                              
.* DEFINE TABLE ENTRY                                                           
&LBL     DC    AL2(&LBL.X-*)       LENGTH OF ENTRY                              
         DC    CL1'&PREFIX'        PREFIX CHARACTER                             
         DC    CL2'&ID'            ALPHA ID                                     
         DC    AL3(&DFLT-REQTBL)   DEFAULT VALUES RELATIVE TO REQTBL            
         DC    AL1(&PROC)          POST-VALIDATION PROCESS RTN NUMBER           
         DC    AL1(&CNTL)                       CONTROL 1                       
         DC    AL1(&CNTL2)                      CONTROL 2                       
.*                                                                              
&CDID    SETC  '&ID'                                                            
         AIF   (T'&CID EQ 'O').RQD90                                            
&CDID    SETC  '&CID'                                                           
.*                                                                              
.RQD90   ANOP                                                                   
         DC    CL2'&CDID'          RQST CARD ID                                 
.*                                                                              
         AIF   (T'&RRG EQ 'O').RQD100                                           
         AIF   ('&RRG' EQ 'Y').RQD95                                            
.*                                                                              
         DC    CL2'&RRG'           RRG ID                                       
         AGO   .RQD120                                                          
.*                                                                              
.RQD95   ANOP                                                                   
         DC    CL2'&ID'            RRG ID                                       
         AGO   .RQD120                                                          
.*                                                                              
.RQD100  ANOP                                                                   
         DC    XL2'00'             NO RRG ID                                    
.*                                                                              
.RQD120  ANOP                                                                   
         DC    CL20&NAME           MENU NAME                                    
         MEND                                                                   
