*          DATA SET MCPWRTB    AT LEVEL 070 AS OF 08/03/04                      
         MACRO                                                                  
.**********************************************************************         
.*                                                                    *         
.*       PWRTB MACRO                                                  *         
.*                                                                    *         
.*       BUILDS A TABLE FOR PRVAL THAT IS USED BY THE                 *         
.*       PRINTWRITER (PWR) TO VALIDATE INPUT AND TO                   *         
.*       PRODUCE HELP MENUS                                           *         
.*                                                                    *         
.*       PARAMETERS                                                   *         
.*       &LABEL    INTERNAL ID - MAX  5 CHS                           *         
.*       &TYPE     INITIAL - START OF TABLE                           *         
.*                 ENTRY   - TABLE ENTRY - DEFAULT                    *         
.*                 END     - END OF TABLE                             *         
.*       &NXTRA    NUMBER OF EXTRA BYTES PER ENTRY                    *         
.*       &NAME     (MINL,MAXL,SHORT,TEXT)                             *         
.*         MINL    MINIMUM INPUT  LENGTH - DEFAULT = TOTAL LENGTH     *         
.*         MAXL    MAXIMUM OUTPUT LENGTH - DEFAULT = TOTAL LENGTH     *         
.*         SHORT   ABBREVIATION - MAX 4 CHS                           *         
.*         TEXT    TEXT FOR ENTRY                                     *         
.*       &CNTRL    DDVAL CONTROL INFORMATION                          *         
.*       &AORM     AOR    TYPE MASK                                   *         
.*       &MEDIAM   MEDIA  TYPE MASK                                   *         
.*       &MENUM    MENU   TYPE MASK                                   *         
.*       &FLAVORM  FLAVOR TYPE MASK - (CONTRACT,BUY,...)              *         
.*       &FIELDM   FIELD  TYPE MASK - (HEADLINE,MIDLINE,ROW,COLUMN)   *         
.*       &XTRA     EXTRA DATA FOR TABLE                               *         
.*       &PREFIX   SYSTEM PREFIX                                      *         
.*                                                                    *         
.*                                                                    *         
.*                                                                    *         
.*                                                                    *         
.*                                                                    *         
.*                                                                    *         
.*                                                                    *         
.*                                                                    *         
.**********************************************************************         
.*                                                                              
.*                                                                              
&LABEL   PWRTB &TYPE=ENTRY,                                            X        
               &NAME=,                                                 X        
               &CNTRL=,                                                X        
               &AORM=,                                                 X        
               &MEDIAM=,                                               X        
               &MENUM=,                                                X        
               &FLAVORM=,                                              X        
               &FIELDM=,                                               X        
               &DOCM=Y,                                                X        
               &NXTRA=0,                                               X        
               &PREFIX=PK,                                             X        
               &EXTRA=                                                          
.*                                                                              
         GBLA  &NXTR               NUMBER OF EXTRA BYTES PER ENTRY              
         GBLC  &PFIX               PREFIX                                       
.*                                                                              
         LCLA  &I                  WORK COUNTER                                 
         LCLA  &ENTLN              ENTRY LENGTH                                 
         LCLA  &LANGL              LANGUAGE ENTRY LENGTH                        
         LCLA  &MINL               MINIMUM INPUT  LENGTH                        
         LCLA  &MAXL               MAXIMUM OUTPUT LENGTH                        
.*                                                                              
         LCLC  &LABLC              LABEL WRKAREA                                
         LCLC  &QUOTE              SINGLE QUOTE                                 
         LCLC  &SHORT              ABBREVIATION                                 
         LCLC  &TEXT               TEXT                                         
         LCLC  &CTRL               CONTROL BYTE                                 
         LCLC  &MINLC              MINIMUM INPUT  LENGTH                        
         LCLC  &MAXLC              MAXIMUM OUTPUT LENGTH                        
         LCLC  &MSKAOR             AOR    TYPE MASK                             
         LCLC  &MSKMD              MEDIA  TYPE MASK                             
         LCLC  &MSKMN              RECORD TYPE MASK                             
         LCLC  &MSKFL              FLAVOR TYPE MASK                             
         LCLC  &MSKFD              FIELD  TYPE MASK                             
         LCLC  &MSKDOC             DOCUMENTED  MASK                             
.*                                                                              
.*       INITIALIZATION                                                         
.*                                                                              
&QUOTE   SETC ''''                 SINGLE QUOTE                                 
.*                                                                              
.*       VALIDATE LABEL                                                         
.*                                                                              
         AIF   (T'&LABEL EQ 'O').LABELE1                                        
.*                                                                              
.*       VALIDATE TYPE                                                          
.*                                                                              
         AIF   ('&TYPE' EQ 'INITIAL').INITIAL   START OF TABLE                  
         AIF   ('&TYPE' EQ 'ENTRY').ENTRY       ENTRY IN TABLE                  
         AIF   ('&TYPE' EQ 'END').END           END   OF TABLE                  
.*                                                                              
         AGO   .TYPEE1                                                          
.*                                                                              
.*       TYPE=INITIAL - START OF TABLE                                          
.*       NXTRA = NUMBER OF EXTRA BYTES IN EACH ENTRY                            
.*                                                                              
.INITIAL ANOP                                                                   
.*                                                                              
&NXTR    SETA  &NXTRA              SAVE NUMBER OF EXTRA BYTES                   
.*                                                                              
&LABLC   SETC  '&LABEL'            SAVE LABEL                                   
&LABLC   SETC  '&LABLC.TBL'        ADD 'TAB'                                    
&LABLC   DS    0F                  START OF TABLE                               
         DC    AL1(&NXTR)          NUMBER OF EXTRA BYTES PER ENTRY              
&PFIX    SETC  '&PREFIX'           SAVE PREFIX                                  
.*                                                                              
         AGO   .PWRTBX                                                          
.*                                                                              
.*       TYPE=END - END OF TABLE                                                
.*                                                                              
.END     ANOP                                                                   
.*                                                                              
*                                                                               
         DC    AL1(0)              END OF TABLE                                 
*                                                                               
.*                                                                              
         AGO   .PWRTBX                                                          
.*                                                                              
.*       TYPE=ENTRY - ENTRY IN TABLE                                            
.*                                                                              
.ENTRY   ANOP                                                                   
.*                                                                              
.*       AOR   MASK - Y                                                         
.*                                                                              
         AIF   (T'&AORM NE 'O').MSKAOR1 IF AOR MASK NOT ENTERED                 
&MSKAOR  SETC  'Y'                 DEFAULT TO 'Y'                               
         AGO   .MSKAORX                                                         
.*                                                                              
.MSKAOR1 ANOP                      ELSE                                         
.*                                                                              
&I       SETA  1                   INIT MASKS COUNTER                           
.*                                                                              
.MSKAORL ANOP                                                                   
.*                                                                              
         AIF   (T'&AORM(&I) EQ 'O').MSKAORC    IGNORE IF NOT ENTERED            
.*                                                                              
&MSKAOR  SETC  '&PFIX.MAOR&AORM(&I)'      BUILD MASK                            
.*                                                                              
.MSKAORC ANOP                                                                   
.*                                                                              
&I       SETA  &I+1                INCREMENT COUNTER                            
         AIF   (&I GT N'&AORM).MSKAORX        DONE                              
&MSKAOR  SETC  '&MSKAOR.+'         ADD ON NEXT MASK                             
         AGO   .MSKAORL                                                         
.*                                                                              
.MSKAORX ANOP                                                                   
.*                                                                              
.*       MEDIA MASK - MAGAZINE, NEWSPAPER, TRADE ETC                            
.*                                                                              
         AIF   (T'&MEDIAM NE 'O').MSKMD1 IF MEDIA MASK NOT ENTERED              
&MSKMD   SETC  'Y'                 DEFAULT TO 'Y'                               
         AGO   .MSKMDX                                                          
.*                                                                              
.MSKMD1  ANOP                      ELSE                                         
.*                                                                              
&I       SETA  1                   INIT MASKS COUNTER                           
.*                                                                              
.MSKMDL  ANOP                                                                   
.*                                                                              
         AIF   (T'&MEDIAM(&I) EQ 'O').MSKMDC   IGNORE IF NOT ENTERED            
.*                                                                              
&MSKMD   SETC  '&PFIX.MMD&MEDIAM(&I)'    BUILD MASK                             
.*                                                                              
.MSKMDC  ANOP                                                                   
.*                                                                              
&I       SETA  &I+1                INCREMENT COUNTER                            
         AIF   (&I GT N'&MEDIAM).MSKMDX       DONE                              
&MSKMD   SETC  '&MSKMD.+'          ADD ON NEXT MASK                             
         AGO   .MSKMDL                                                          
.*                                                                              
.MSKMDX  ANOP                                                                   
.*                                                                              
.*       MENU MASK                                                              
.*                                                                              
         AIF   (T'&MENUM NE 'O').MSKMN1 IF MENU MASK NOT ENTERED                
&MSKMN   SETC  'Y'                 DEFAULT TO 'Y'                               
         AGO   .MSKMNX                                                          
.*                                                                              
.MSKMN1  ANOP                      ELSE                                         
.*                                                                              
&I       SETA  1                   INIT MASKS COUNTER                           
.*                                                                              
.MSKMNL  ANOP                                                                   
.*                                                                              
         AIF   (T'&MENUM(&I) EQ 'O').MSKMNC   IGNORE IF NOT ENTERED             
.*                                                                              
&MSKMN   SETC  '&MSKMN.&PFIX.MMN&MENUM(&I)' BUILD MASK                          
.*                                                                              
.MSKMNC  ANOP                                                                   
.*                                                                              
&I       SETA  &I+1                INCREMENT COUNTER                            
         AIF   (&I GT N'&MENUM).MSKMNX       DONE                               
&MSKMN   SETC  '&MSKMN.+'          ADD ON NEXT MASK                             
         AGO   .MSKMNL                                                          
.*                                                                              
.MSKMNX  ANOP                                                                   
.*                                                                              
.*       FLAVOR MASK - BUY, CONTRACT, CONTONLY, BILLONLY, AOR                   
.*               XCLDE - MEANS EXCLUDE FROM MENU                                
.*                                                                              
         AIF   (T'&FLAVORM NE 'O').MSKFL1 IF FLAVOR MASK NOT ENTERED            
&MSKFL   SETC  'Y'                 DEFAULT TO 'Y'                               
         AGO   .MSKFLX                                                          
.*                                                                              
.MSKFL1  ANOP                      ELSE                                         
.*                                                                              
&I       SETA  1                   INIT LOOP COUNTER                            
.*                                                                              
.MSKFLL  ANOP                                                                   
.*                                                                              
         AIF   (T'&FLAVORM(&I) EQ 'O').MSKFLC   IGNORE IF NOT ENTERED           
.*                                                                              
.MSKFLL2 ANOP               ELSE                                                
.*                                                                              
&MSKFL   SETC  '&MSKFL.&PFIX.MFL&FLAVORM(&I)' BUILD MASK                        
.*                                                                              
.MSKFLC  ANOP                                                                   
.*                                                                              
&I       SETA  &I+1                DECREMENT COUNTER                            
         AIF   (&I GT N'&FLAVORM).MSKFLX  DONE                                  
&MSKFL   SETC  '&MSKFL.+'          ADD ON NEXT MASK                             
         AGO   .MSKFLL                                                          
.*                                                                              
.MSKFLX  ANOP                                                                   
.*                                                                              
.*       FIELD TYPE MASK - HEADLINE,MIDLINE,ROW,COLUMN                          
.*                                                                              
         AIF   (T'&FIELDM NE 'O').MSKFD1 IF FIELD MASK NOT ENTERED              
&MSKFD   SETC  'Y'                 DEFAULT TO 'Y'                               
         AGO   .MSKFDX                                                          
.*                                                                              
.MSKFD1  ANOP                      ELSE                                         
.*                                                                              
&I       SETA  1                   INIT MASKS COUNTER                           
.*                                                                              
.MSKFDL  ANOP                                                                   
.*                                                                              
         AIF   (T'&FIELDM(&I) EQ 'O').MSKFDC   IGNORE IF NOT ENTERED            
.*                                                                              
&MSKFD   SETC  '&MSKFD&PFIX.M&FIELDM(&I)' BUILD MASK                            
.*                                                                              
.MSKFDC  ANOP                                                                   
.*                                                                              
&I       SETA  &I+1                INCREMENT COUNTER                            
         AIF   (&I GT N'&FIELDM).MSKFDX       DONE                              
&MSKFD   SETC  '&MSKFD.-'          ADD ON NEXT MASK                             
         AGO   .MSKFDL                                                          
.*                                                                              
.MSKFDX  ANOP                                                                   
.*                                                                              
.*       DOCUMENTED MASK - YES(DEFAULT)/NO                                      
.*                                                                              
&I       SETA  1                   INIT MASKS COUNTER                           
.*                                                                              
.MSKDOCL ANOP                                                                   
.*                                                                              
         AIF   (T'&DOCM(&I) EQ 'O').MSKDOCC    IGNORE IF NOT ENTERED            
.*                                                                              
&MSKDOC  SETC  '&MSKDOC&PFIX.MDOC&DOCM(&I)' BUILD MASK                          
.*                                                                              
.MSKDOCC ANOP                                                                   
.*                                                                              
&I       SETA  &I+1                INCREMENT COUNTER                            
         AIF   (&I GT N'&DOCM).MSKDOCX        DONE                              
&MSKDOC  SETC  '&MSKDOC.-'         ADD ON NEXT MASK                             
         AGO   .MSKDOCL                                                         
.*                                                                              
.MSKDOCX ANOP                                                                   
.*                                                                              
.*       NAME KEYWORD - (MINL, MAXL, SHORT, TEXT)                               
.*                                                                              
         AIF   (T'&NAME(4) EQ 'O').TEXTE1 MUST HAVE TEXT ENTRY                  
.*                                                                              
&TEXT    SETC  '&NAME(4)'          GET TEXT                                     
         AIF   ('&TEXT'(1,1) EQ '&QUOTE').TEXTX  IF TEXT HAS NO QUOTES          
&TEXT    SETC  '&QUOTE&TEXT&QUOTE'   ADD THEM                                   
.TEXTX   ANOP                                                                   
.*                                                                              
&CTRL    SETC  '0'                 INIT CONTROL BYTE                            
         AIF   (T'&CNTRL EQ 'O').CTRLX    IGNORE IF NOT ENTERED                 
.*                                                                              
&CTRL    SETC  'VLT&CNTRL.Q'                                                    
.*                                                                              
.CTRLX   ANOP                                                                   
.*                                                                              
         AIF   (T'&NAME(3) NE 'O').SHORT1 IF ABBREVIATION OMITTED               
&SHORT   SETC  ''' '''             SET TO BLANK                                 
         AGO   .SHORTX                                                          
.*                                                                              
.SHORT1  ANOP                      ELSE                                         
&SHORT   SETC  '&NAME(3)'          GET SHORT NAME                               
.*                                                                              
         AIF   ('&SHORT'(1,1) EQ '&QUOTE').SHORTX IF SHORT HAS                  
&SHORT   SETC  '&QUOTE&SHORT&QUOTE'   NO QUOTES ADD THEM                        
.*                                                                              
.SHORTX  ANOP                                                                   
.*                                                                              
&LANGL   SETA  8                   STANDARD LANGUAGE ENTRY LENGTH               
&LANGL   SETA  &LANGL+K'&TEXT-2    ADD ON LENGTH OF TEXT                        
.*                                                                              
&ENTLN   SETA  16                  STANDARD ENTRY LENGTH                        
&ENTLN   SETA  &ENTLN+&NXTR        ADD ON EXTRA BYTES LENGTH                    
&ENTLN   SETA  &ENTLN+&LANGL       ADD ON LANGUAGE ENTRY LENGTH                 
.*                                                                              
         AIF   (T'&NAME(1) EQ 'O').MINL1 IF MINIMUM LENGTH NOT GIVEN            
&MINLC   SETC  '&NAME(1)'                                                       
         AGO   .MINLX                                                           
.*                                                                              
.MINL1   ANOP                      ELSE                                         
&MINL    SETA  K'&TEXT-2           SET TO TEXT LENGTH                           
&MINLC   SETC  '&MINL'             SET TO TEXT LENGTH                           
.*                                                                              
.MINLX   ANOP                                                                   
.*                                                                              
         AIF   (T'&NAME(2) EQ 'O').MAXL1 IF MAXIMUM LENGTH NOT GIVEN            
&MAXLC   SETC  '&NAME(2)'                                                       
         AGO   .MAXLX                                                           
.*                                                                              
.MAXL1   ANOP                      ELSE                                         
&MAXL    SETA  K'&TEXT-2           SET TO TEXT LENGTH                           
&MAXLC   SETC  '&MAXL'             SET TO TEXT LENGTH                           
.*                                                                              
.MAXLX   ANOP                                                                   
.*                                                                              
&LABEL   DC    AL1(&ENTLN)              ENTRY LENGTH                            
         DC    AL1(&CTRL)               CONTROL BYTE                            
         DC    AL2(&PFIX.Q&LABEL)       INTERNAL CODE                           
.*****   DC    AL2(&PFIX.H&LABEL)       HELP PANEL NUMBER                       
         DC    AL2(0)                   HELP PANEL NUMBER                       
         DC    AL2(&MSKAOR)             AOR    TYPE MASK                        
         DC    AL2(&MSKMD)              MEDIA  TYPE MASK                        
         DC    AL2(&MSKMN)              MENU   TYPE MASK                        
         DC    AL2(&MSKFL)              FLAVOR TYPE MASK                        
         DC    AL1(&MSKFD)              FIELD  TYPE MASK                        
         DC    AL1(&MSKDOC)             DOCUMENTED  MASK                        
*        TEXT DATA IN TABLE ENTRY                                               
         DC    AL1(&LANGL)              LANGUAGE ENTRY LENGTH                   
         DC    AL1(LANGENG)             ENGLISH                                 
         DC    AL1(&MINLC,&MAXLC)       MIN INPUT, MAX OUTPUT LENGTHS           
         DC    CL4&SHORT                ABBREVIATION                            
         DC    C&TEXT                                                           
.*                                                                              
         AGO   .PWRTBX                                                          
.*                                                                              
.*       ERROR MESSAGES                                                         
.*                                                                              
.IDE1    ANOP                                                                   
         MNOTE 4,'MUST HAVE INTERNAL ID SPECIFIED'                              
         AGO   .PWRTBX                                                          
.*                                                                              
.TEXTE1  ANOP                                                                   
         MNOTE 4,'MUST HAVE TEXT SPECIFIED'                                     
         AGO   .PWRTBX                                                          
.*                                                                              
.LABELE1 ANOP                                                                   
         MNOTE 4,'MUST HAVE LABEL FIELD COLS 1-8'                               
         AGO   .PWRTBX                                                          
.*                                                                              
.TYPEE1  ANOP                                                                   
         MNOTE 4,'TYPE ENTRY NOT RECOGNIZED'                                    
         AGO   .PWRTBX                                                          
.*                                                                              
.PWRTBX  ANOP                                                                   
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
