*          DATA SET MCLKRUN    AT LEVEL 001 AS OF 03/05/05                      
         MACRO                                                                  
&LABEL   LKRUN &MACTYPE,                                               +        
               &MAPCODE,                                               +        
               &INPUT,                                                 +        
               &OUTPUT,                                                +        
               &OLEN=,                                                 +        
               &FILTROUT=,                                             +        
               &PCVERSION=                                                      
.*                                                                              
         GBLA  &TYPNUM                                                          
         GBLB  &FIRST_OUTPUT_DEFINITION                                         
         GBLB  &TABLE_IS_OPEN                                                   
         GBLB  &SERVER_NATIVE                                                   
         GBLB  &OARRAY_IS_OPEN                                                  
         GBLB  &RUN_ON_EXIT_OPEN                                                
         GBLC  &FLTROUT                                                         
         GBLC  &SERVER_NAME                                                     
         GBLC  &CURRENT_TABLE_NAME                                              
         GBLC  &CURRENT_RECORD_NAME                                             
         GBLC  &CURRENT_ROW_NAME                                                
         GBLC  &BLOCK_LABEL                                                     
         LCLC  &MAP                                                             
         LCLC  &DATAVAL                                                         
         LCLC  &DATABLK                                                         
         LCLC  &EDTROUT                                                         
         LCLC  &NDXROUT                                                         
         LCLC  &NDXOVER                                                         
.*                                                                              
         AIF   (&FIRST_OUTPUT_DEFINITION).ERROR1                                
         AIF   (NOT &TABLE_IS_OPEN).ERROR1                                      
         AIF   (T'&MACTYPE EQ 'O').ERROR2                                       
&RUNX    SETB  ('&MACTYPE'(1,1) EQ 'H')                                         
&QVAL    SETB  ('&MACTYPE'(1,1) EQ 'C')                                         
&ERUN    SETB  ('&MACTYPE'(1,1) EQ 'E')                                         
.*                                                                              
&MAP     SETC  '1'                                                              
&DATABLK SETC  '0'                                                              
&DATAVAL SETC  ''                                                               
&EDTROUT SETC  ''                                                               
&NDXROUT SETC  ''                                                               
&NDXOVER SETC  ''                                                               
.*                                                                              
&TYPNUM  SETA  34                  LD_RUNXQ                                     
         AIF   (&RUNX).LKRH                                                     
&TYPNUM  SETA  35                  LD_QVALQ                                     
         AIF   (&QVAL).LKRC                                                     
&TYPNUM  SETA  36                  LD_ERUNQ                                     
         AIF   (&ERUN).LKRE                                                     
         AGO   .ERROR2                                                          
.*                                                                              
.LKRH    ANOP  ,                                                                
         AIF   (&RUN_ON_EXIT_OPEN).ERROR3                                       
         AIF   (T'&OLEN NE 'O' OR                                      +        
                T'&INPUT NE 'O' OR                                     +        
                T'&OUTPUT NE 'O' OR                                    +        
                T'&PCVERSION NE 'O').ERROR5                                     
         AIF   (T'&MAPCODE EQ 'O').ERROR4                                       
&MAP     SETC  '&MAPCODE'                                                       
&RUN_ON_EXIT_OPEN                  SETB  1                                      
&FLTROUT SETC  ''                                                               
         AIF   (T'&FILTROUT EQ 'O').LKRGEN                                      
&FLTROUT SETC  '&FILTROUT'                                                      
         AGO   .LKRGEN                                                          
.*                                                                              
.LKRC    ANOP  ,                                                                
         AIF   (NOT &RUN_ON_EXIT_OPEN).ERROR1                                   
         AIF   (T'&MAPCODE EQ 'O').ERROR4                                       
         AIF   (T'&INPUT EQ 'O').ERROR6                                         
         AIF   (N'&INPUT NE 2).ERROR6                                           
.*                                                                              
         LKBLK &INPUT(1)                                                        
&DATABLK SETC  '&INPUT(1)'                                                      
&DATAVAL SETC  '&INPUT(2)'                                                      
.*                                                                              
&MAP     SETC  '&MAPCODE'                                                       
         AIF   (T'&OUTPUT EQ 'O').LKRGEN                                        
         AIF   (T'&FILTROUT NE 'O').ERROR5                                      
         AIF   (T'&OUTPUT EQ 'O').LKRGEN                                        
         AIF   ('&OUTPUT(1)'(1,1) EQ 'U').LKRCU                                 
         AIF   ('&OUTPUT(1)'(1,1) EQ 'R').LKRCR                                 
         AGO   .ERROR7                                                          
.*                                                                              
.LKRCU   ANOP  ,                   INDEX ROUTINE                                
         AIF   (NOT &OARRAY_IS_OPEN).ERROR8                                     
         AIF   (N'&OUTPUT NE 3).ERROR7                                          
&NDXROUT SETC  '&OUTPUT(2)'                                                     
&NDXOVER SETC  '&OUTPUT(3)'                                                     
         AGO   .LKRGEN                                                          
.*                                                                              
.LKRCR   ANOP  ,                   INTERNAL EDIT ROUTINE                        
         AIF   (NOT &OARRAY_IS_OPEN).ERROR8                                     
         AIF   (N'&OUTPUT NE 2).ERROR7                                          
&EDTROUT SETC  '&OUTPUT(2)'                                                     
         AGO   .LKRGEN                                                          
.*                                                                              
.LKRE    ANOP  ,                                                                
         AIF   (NOT &RUN_ON_EXIT_OPEN).ERROR1                                   
&RUN_ON_EXIT_OPEN                  SETB  0                                      
         AIF   (T'&OLEN NE 'O' OR                                      +        
                T'&INPUT NE 'O' OR                                     +        
                T'&OUTPUT NE 'O' OR                                    +        
                T'&MAPCODE NE 'O' OR                                   +        
                T'&FILTROUT NE 'O' OR                                  +        
                T'&PCVERSION NE 'O').ERROR5                                     
.*                                                                              
.LKRGEN  ANOP  ,                                                                
         AIF   (&OARRAY_IS_OPEN).LKRX                                           
         DC    AL2(&MAP)                                                        
         DC    X'FF'                                                            
         AIF   ('&FLTROUT' EQ '').LKRO02                                        
         DC    AL2(&FLTROUT-&SERVER_NAME)                                       
         AGO   .LKRO04                                                          
.*                                                                              
.LKRO02  ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKRO04  ANOP  ,                                                                
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
.*                                                                              
         AIF   ('&NDXROUT' EQ '').LKRO06                                        
&DATABLK SETC  '&DATABLK'.'+X''40'''                                            
.*                                                                              
.LKRO06  ANOP  ,                                                                
         DC    AL1(&DATABLK)                                                    
.*                                                                              
         AIF   ('&FLTROUT' EQ '').LKRO08                                        
         DC    X'80'                                                            
         AGO   .LKRO10                                                          
.*                                                                              
.LKRO08  ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKRO10  ANOP  ,                                                                
         AIF   ('&NDXOVER' EQ '').LKRO12                                        
         DC    AL1(&NDXOVER)                                                    
         AGO   .LKRO14                                                          
.*                                                                              
.LKRO12  ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKRO14  ANOP  ,                                                                
         AIF   ('&DATAVAL' EQ '').LKRO16                                        
         DC    AL2(&DATAVAL-&BLOCK_LABEL)                                       
         AGO   .LKRO18                                                          
.*                                                                              
.LKRO16  ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKRO18  ANOP  ,                                                                
         AIF   ('&NDXROUT' EQ '').LKRO20                                        
         DC    AL1(&NDXROUT)                                                    
         AGO   .LKRO22                                                          
.*                                                                              
.LKRO20  ANOP  ,                                                                
         DC    AL1(&TYPNUM)                                                     
.*                                                                              
.LKRO22  ANOP  ,                                                                
         AIF   (T'&OLEN EQ 'O').LKRO24                                          
         DC    AL1(&OLEN)                                                       
         AGO   .LKRO28                                                          
.*                                                                              
.LKRO24  ANOP  ,                                                                
         AIF   ('&DATAVAL' EQ '').LKRO26                                        
         DC    AL1(L'&DATAVAL)                                                  
         AGO   .LKRO28                                                          
.*                                                                              
.LKRO26  ANOP  ,                                                                
         DC    AL1(1)                                                           
.*                                                                              
.LKRO28  ANOP  ,                                                                
         LKVER &PCVERSION                                                       
*                                                                               
         MEXIT                                                                  
.*                                                                              
.LKRX    ANOP  ,                                                                
         DC    AL2(&MAP)                                                        
         DC    X'FF'                                                            
         DC    AL2(0)                                                           
         DC    AL1(0)                                                           
         DC    AL1(0)                                                           
         AIF   ('&EDTROUT' EQ '' AND '&NDXROUT' EQ '').LKRX02                   
&DATABLK SETC  '&DATABLK'.'+X''40'''                                            
.*                                                                              
.LKRX02  ANOP  ,                                                                
         DC    AL1(&DATABLK)                                                    
         AIF   ('&FLTROUT' EQ '').LKRX04                                        
         DC    X'08'                                                            
         AGO   .LKRX06                                                          
.*                                                                              
.LKRX04  ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKRX06  ANOP  ,                                                                
         AIF   ('&NDXOVER' EQ '').LKRX08                                        
         DC    AL1(&NDXOVER)                                                    
         AGO   .LKRX10                                                          
.*                                                                              
.LKRX08  ANOP  ,                                                                
         DC    AL1(0)                                                           
.*                                                                              
.LKRX10  ANOP  ,                                                                
         AIF   ('&DATAVAL' EQ '').LKRX12                                        
         DC    AL2(&DATAVAL-&BLOCK_LABEL)                                       
         AGO   .LKRX14                                                          
.*                                                                              
.LKRX12  ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKRX14  ANOP  ,                                                                
         AIF   (T'&OLEN EQ 'O').LKRX16                                          
         DC    AL1(&OLEN)                                                       
         AGO   .LKRX20                                                          
.*                                                                              
.LKRX16  ANOP  ,                                                                
         AIF   ('&DATAVAL' EQ '').LKRX18                                        
         DC    AL1(L'&DATAVAL)                                                  
         AGO   .LKRX20                                                          
.*                                                                              
.LKRX18  ANOP  ,                                                                
         DC    AL1(1)                                                           
.*                                                                              
.LKRX20  ANOP  ,                                                                
         AIF   ('&FLTROUT' EQ '').LKRX22                                        
         DC    AL2(&FLTROUT-&SERVER_NAME)                                       
         AGO   .LKRX24                                                          
.*                                                                              
.LKRX22  ANOP  ,                                                                
         DC    AL2(0)                                                           
.*                                                                              
.LKRX24  ANOP  ,                                                                
         AIF   ('&EDTROUT' EQ '').LKRX26                                        
         DC    AL2(&EDTROUT-&SERVER_NAME)                                       
         AGO   .LKRX30                                                          
.*                                                                              
.LKRX26  ANOP  ,                                                                
         AIF   ('&NDXROUT' EQ '').LKRX28                                        
         DC    AL1(&NDXROUT)                                                    
         DC    AL1(0)                                                           
         AGO   .LKRX30                                                          
.*                                                                              
.LKRX28  ANOP  ,                                                                
         DC    AL1(&TYPNUM)                                                     
         DC    AL1(0)                                                           
.*                                                                              
.LKRX30  ANOP  ,                                                                
         LKVER &PCVERSION                                                       
*                                                                               
         MEXIT                                                                  
.*                                                                              
.ERROR1  ANOP  ,                                                                
         MNOTE 12,'Invalid macro sequence'                                      
         MEXIT                                                                  
.*                                                                              
.ERROR2  ANOP  ,                                                                
         MNOTE 12,'Missing or invalid type - S/B H, C or E'                     
         MEXIT                                                                  
.*                                                                              
.ERROR3  ANOP  ,                                                                
         MNOTE 12,'Missing end run macro'                                       
         MEXIT                                                                  
.*                                                                              
.ERROR4  ANOP  ,                                                                
         MNOTE 12,'Missing map code'                                            
         MEXIT                                                                  
.*                                                                              
.ERROR5  ANOP  ,                                                                
         MNOTE 12,'Extraneous parameters'                                       
         MEXIT                                                                  
.*                                                                              
.ERROR6  ANOP  ,                                                                
         MNOTE 12,'Input value missing or invalid'                              
         MEXIT                                                                  
.*                                                                              
.ERROR7  ANOP  ,                                                                
         MNOTE 12,'Output value missing or invalid'                             
         MEXIT                                                                  
.*                                                                              
.ERROR8  ANOP  ,                                                                
         MNOTE 12,'Cannot use edit routine for LO_D entries'                    
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
