*          DATA SET MCDICT     AT LEVEL 023 AS OF 09/08/13                      
         MACRO                                                                  
&KEY     DICT  &TYPE,&EL,&DISP,&LEN,&COMPULS,&SCREEN=,                 X        
               &H1=,&H2=,&OK=,&H3=,&OUTLEN=                                     
         GBLA  &N                                                               
         LCLA  &K,&T,&L,&D,&FLAG,&OL                                            
         LCLC  &C,&E                                                            
         AIF   (T'&KEY NE 'O').KEY2                                             
         MNOTE 8,'MUST HAVE KEY FIELD COLS 1-8'                                 
         MEXIT                                                                  
.KEY2    ANOP                                                                   
&N       SETA  &N+1                                                             
         DC    X'01',AL1(20),CL8'&KEY',AL1(&N)                                  
.*                                           CHECK TYPE/SET DEFAULT LEN         
         AIF   ('&TYPE' EQ 'A').T02                                             
         AIF   ('&TYPE' EQ 'ALPHA').T02                                         
         AIF   ('&TYPE' EQ 'C').T04                                             
         AIF   ('&TYPE' EQ 'CHAR').T04                                          
         AIF   ('&TYPE' EQ 'CHARS').T04                                         
         AIF   ('&TYPE' EQ 'HEX').T06                                           
         AIF   ('&TYPE' EQ 'H').T06                                             
&OL      SETA  8                                                                
&FLAG    SETA  2                                                                
         AIF   ('&TYPE' EQ 'N').T12                                             
         AIF   ('&TYPE' EQ 'NUM').T12                                           
         AIF   ('&TYPE' EQ 'NUMERIC').T12                                       
         AIF   ('&TYPE' EQ 'BIN').T14                                           
         AIF   ('&TYPE' EQ 'B').T14                                             
         AIF   ('&TYPE' EQ 'BINARY').T14                                        
         AIF   ('&TYPE' EQ 'P').T16                                             
         AIF   ('&TYPE' EQ 'PACK').T16                                          
         AIF   ('&TYPE' EQ 'PACKED').T16                                        
         AIF   ('&TYPE' EQ 'PWOS').T18                                          
         AIF   ('&TYPE' EQ '1DEC').T21                                          
         AIF   ('&TYPE' EQ '2DEC').T22                                          
         AIF   ('&TYPE' EQ 'CASH').T22                                          
         AIF   ('&TYPE' EQ '3DEC').T23                                          
         AIF   ('&TYPE' EQ '4DEC').T24                                          
         AIF   ('&TYPE' EQ '5DEC').T25                                          
&FLAG    SETA  0                                                                
         AIF   ('&TYPE' EQ 'D').T40                                             
         AIF   ('&TYPE' EQ 'DATE').T40                                          
         AIF   ('&TYPE' EQ 'YMD').T40                                           
         AIF   ('&TYPE' EQ 'PDATE').T41                                         
         AIF   ('&TYPE' EQ 'PD').T41                                            
         AIF   ('&TYPE' EQ 'CD').T42                                            
         AIF   ('&TYPE' EQ 'CDATE').T42                                         
         AIF   ('&TYPE' EQ 'BDATE').T43                                         
         AIF   ('&TYPE' EQ 'BD').T43                                            
         AIF   ('&TYPE' EQ 'IDATE').T44                                         
         AIF   ('&TYPE' EQ 'ID').T44                                            
&OL      SETA  6                                                                
         AIF   ('&TYPE' EQ 'YM').T50                                            
         AIF   ('&TYPE' EQ 'PYM').T51                                           
         AIF   ('&TYPE' EQ 'BYM').T53                                           
         AIF   ('&TYPE' EQ 'MD').T60                                            
         AIF   ('&TYPE' EQ 'PMD').T61                                           
         AIF   ('&TYPE' EQ 'BMD').T63                                           
&OL      SETA  17                                                               
         AIF   ('&TYPE' EQ 'PERIOD').T70                                        
         AIF   ('&TYPE' EQ 'PPERIOD').T71                                       
         AIF   ('&TYPE' EQ 'CPERIOD').T72                                       
         AIF   ('&TYPE' EQ 'BPERIOD').T73                                       
&OL      SETA  11                                                               
         AIF   ('&TYPE' EQ 'TIME').T80                                          
&OL      SETA  3                                                                
         AIF   ('&TYPE' EQ 'DAY').T90                                           
         MNOTE 8,'TYPE EXPRESSION NOT RECOGNIZED'                               
&T       SETA  0                                                                
         MEXIT                                                                  
.*                                 ALPHA                                        
.T02     ANOP                                                                   
&C       SETC  '02'                                                             
         AGO   .TYP1                                                            
.*                                 ALPHA-NUMERIC                                
.T04     ANOP                                                                   
&C       SETC  '04'                                                             
         AGO   .TYP1                                                            
.*                                 HEX                                          
.T06     ANOP                                                                   
&C       SETC  '06'                                                             
         AGO   .TYP1                                                            
.*                                 NUMERIC EBCDIC                               
.T12     ANOP                                                                   
&C       SETC  '12'                                                             
         AGO   .TYP1                                                            
.*                                 NUMERIC BINARY                               
.T14     ANOP                                                                   
&C       SETC  '14'                                                             
         AGO   .TYP1                                                            
.*                                 NUMERIC PACKED                               
.T16     ANOP                                                                   
&C       SETC  '16'                                                             
         AGO   .TYP1                                                            
.*                                 NUMERIC PWOS                                 
.T18     ANOP                                                                   
&C       SETC  '18'                                                             
         AGO   .TYP1                                                            
.*                                 1 DEC                                        
.T21     ANOP                                                                   
&C       SETC  '21'                                                             
         AGO   .TYP4                                                            
.*                                 CASH & 2 DEC                                 
.T22     ANOP                                                                   
&C       SETC  '22'                                                             
         AGO   .TYP4                                                            
.*                                 3 DEC                                        
.T23     ANOP                                                                   
&C       SETC  '23'                                                             
         AGO   .TYP4                                                            
.*                                 4 DEC                                        
.T24     ANOP                                                                   
&C       SETC  '24'                                                             
         AGO   .TYP4                                                            
.*                                 5 DEC                                        
.T25     ANOP                                                                   
&C       SETC  '25'                                                             
         AGO   .TYP4                                                            
.*                                 DATA EBCDIC                                  
.T40     ANOP                                                                   
&C       SETC  '40'                                                             
         AGO   .TYP6                                                            
.*                                 DATA PWOS                                    
.T41     ANOP                                                                   
&C       SETC  '41'                                                             
         AGO   .TYP3                                                            
.*                                 DATE COMPRESSED                              
.T42     ANOP                                                                   
&C       SETC  '42'                                                             
         AGO   .TYP2                                                            
.*                                 DATE BINARY                                  
.T43     ANOP                                                                   
&C       SETC  '43'                                                             
         AGO   .TYP3                                                            
.*                                 DATE IPL                                     
.T44     ANOP                                                                   
&C       SETC  '44'                                                             
&L       SETA  8                                                                
         AGO   .TYPEND                                                          
.*                                 YM EBCDIC                                    
.T50     ANOP                                                                   
&C       SETC  '50'                                                             
         AGO   .TYP4                                                            
.*                                 YM PWOS                                      
.T51     ANOP                                                                   
&C       SETC  '51'                                                             
         AGO   .TYP2                                                            
.*                                 YM BINARY                                    
.T53     ANOP                                                                   
&C       SETC  '53'                                                             
         AGO   .TYP2                                                            
.*                                 MD EBCDIC                                    
.T60     ANOP                                                                   
&C       SETC  '60'                                                             
         AGO   .TYP4                                                            
.*                                 MD PWOS                                      
.T61     ANOP                                                                   
&C       SETC  '61'                                                             
         AGO   .TYP2                                                            
.*                                 MD BINARY                                    
.T63     ANOP                                                                   
&C       SETC  '63'                                                             
         AGO   .TYP2                                                            
.*                                 PERIOD EBCDIC                                
.T70     ANOP                                                                   
&C       SETC  '70'                                                             
&L       SETA  12                                                               
         AGO   .TYPEND                                                          
.*                                 PERIOD PWOS                                  
.T71     ANOP                                                                   
&C       SETC  '71'                                                             
         AGO   .TYP6                                                            
.*                                 PERIOD COMPRESSED                            
.T72     ANOP                                                                   
&C       SETC  '72'                                                             
         AGO   .TYP4                                                            
.*                                 PERIOD BINARY                                
.T73     ANOP                                                                   
&C       SETC  '73'                                                             
         AGO   .TYP6                                                            
.*                                 MILITARY TIME                                
.T80     ANOP                                                                   
&C       SETC  '80'                                                             
         AGO   .TYP4                                                            
.*                                                                              
.T90     ANOP                                                                   
&C       SETC  '90'                                                             
         AGO   .TYP1                                                            
.*                                                                              
.*                                 SET DEFAULT LENGTHS                          
.*                                                                              
.TYP1    ANOP                                                                   
&L       SETA  1                                                                
         AGO   .TYPEND                                                          
.TYP2    ANOP                                                                   
&L       SETA  2                                                                
         AGO   .TYPEND                                                          
.TYP3    ANOP                                                                   
&L       SETA  3                                                                
         AGO   .TYPEND                                                          
.TYP4    ANOP                                                                   
&L       SETA  4                                                                
         AGO   .TYPEND                                                          
.TYP6    ANOP                                                                   
&L       SETA  6                                                                
.TYPEND  ANOP                                                                   
.*                                                                              
.*                                           CHECK ELEMENT CODE                 
.*                                                                              
&E       SETC  '00'                                                             
         AIF   (T'&EL EQ 'O').ELEND                                             
         AIF   ('&EL' EQ 'KEY').ELEND                                           
&E       SETC  '&EL'                                                            
         AIF   ('&EL'(1,2) NE 'X''').ELEND                                      
&E       SETC  '&EL'(3,2)                                                       
.*                                                                              
.ELEND   ANOP                                                                   
.*                                                                              
.*                                           CHECK DISPLACEMENT                 
.*                                                                              
&D       SETA  2                                                                
         AIF   (T'&DISP EQ 'O').DISPEND                                         
&D       SETA  &DISP                                                            
.*                                                                              
.DISPEND ANOP                                                                   
.*                                                                              
         AIF   (T'&LEN EQ 'O').LENEND                                           
&L       SETA  &LEN                                                             
.*                                                                              
.LENEND  ANOP                                                                   
.*                                                                              
         DC    X'&C',X'&E',AL1(&D),AL1(&L)                                      
.*                                                                              
.*                                           SET FLAGS                          
.*                                                                              
.*                                           COMPULSORY=X'80'                   
         AIF   (T'&COMPULS EQ 'O').FLAG2                                        
         AIF   ('&COMPULS' EQ 'N').FLAG2                                        
&FLAG    SETA  128+&FLAG                                                        
         AIF   ('&COMPULS' EQ 'Y').FLAG2                                        
         MNOTE 8,'FIELD 5 S/B Y N OR OMITTED'                                   
.FLAG2   ANOP                                                                   
.*                                           SCREEN COMING=X'40'                
         AIF   (T'&SCREEN EQ 'O').FLAG4                                         
&FLAG    SETA  &FLAG+64                                                         
.FLAG4   ANOP                                                                   
.*                                           OKVALS COMING=X'20'                
         AIF   (T'&OK EQ 'O').FLAG6                                             
&FLAG    SETA  &FLAG+32                                                         
.FLAG6   ANOP                                                                   
.*                                           H1 COMING LATER=X'10'              
.*                                           H2 COMING LATER=X'08'              
.*                                           H3 COMING LATER=X'04'              
         AIF   (T'&H1 EQ 'O').FLAG10                                            
&FLAG    SETA  &FLAG+16                                                         
         AIF   (T'&H2 EQ 'O').FLAG10                                            
&FLAG    SETA  &FLAG+8                                                          
         AIF   (T'&H3 EQ 'O').FLAG10                                            
&FLAG    SETA  &FLAG+4                                                          
.FLAG10  ANOP                                                                   
.*                                                                              
.*                                 PICK UP OUTPUT LENGTH                        
.*                                                                              
         AIF   (&OL NE 0).OL2                                                   
&OL      SETA  &L                                                               
.OL2     AIF   (T'&OUTLEN EQ 'O').OL4                                           
&OL      SETA  &OUTLEN                                                          
.OL4     ANOP                                                                   
         DC    AL1(&FLAG),AL1(&OL),AL1(0,0,0)                                   
.*                                           HANDLE SCREEN=                     
         AIF   (T'&SCREEN EQ 'O').SCREND                                        
         AIF   (T'&SCREEN NE 'R').SCRCHR                                        
&K       SETA  3                                                                
         DC    X'13',AL1(&K),AL1(&SCREEN)                                       
         AGO   .SCREND                                                          
.SCRCHR  ANOP                                                                   
&K       SETA  K'&SCREEN                                                        
         DC    X'03',AL1(&K),C&SCREEN                                           
.SCREND  ANOP                                                                   
.*                                           HANDLE OK=                         
.*                                                                              
         AIF   (T'&OK EQ 'O').OKEND                                             
&K       SETA  K'&OK                                                            
         DC    X'05',AL1(&K),C&OK                                               
.OKEND   ANOP                                                                   
.*                                           HANDLE HEADINGS                    
         AIF   (T'&H1 EQ 'O').HEADEND                                           
&K       SETA  K'&H1                                                            
         DC    X'07',AL1(&K),C&H1                                               
         AIF   (T'&H2 EQ 'O').HEADEND                                           
&K       SETA  K'&H2                                                            
         DC    X'08',AL1(&K),C&H2                                               
         AIF   (T'&H3 EQ 'O').HEADEND                                           
&K       SETA  K'&H3                                                            
         DC    X'09',AL1(&K),C&H3                                               
.HEADEND ANOP                                                                   
         SPACE 1                                                                
         MEXIT                                                                  
         MEND                                                                   
