*          DATA SET MCFAPGM    AT LEVEL 017 AS OF 06/15/05                      
         MACRO                                                                  
&LABEL   FAPGM &PGM_NAME,&PGM_NUMBER,                                  +        
               &OPERATIONAL=YES,   (PGMINOP)                           +        
               &AUTHHALF=NO,       (PGMIAHW)                           +        
               &RESTRICTED=NO,     (PGMIACC)                           +        
               &IOBOUND=NO,        (PGMIIOB)                           +        
               &AUTHORED=NO,       (PGMIAOR)                           +        
               &READFORUPDATE=YES, (PGMIRFU)                           +        
               &READONLY=NO,       (PGMIROP)                           +        
               &OLAI=YES,          (PGMIOLA)                           +        
               &CALLOVSYS=X'00',   (PGMCOSYS: TWO-DIGIT HEX SYSTEM NO. +        
               &PRIORITY=0,        (PGMPRTY: DECIMAL INTEGER <256)     +        
               &FASWITCH=NO,       (PGMISWT)                           +        
               &HELPV1=NO,         (PGMIHV1)                           +        
               &INPUTREQUIRED=YES, (PGMINOD)                           +        
               &PF1HELP=NO,        (PGMIHPF1)                          +        
               &GOCONNECT=NO,      (PGMIGOCT)                          +        
               &SECURITY=OLD,      (PGMISECA AND PGMISECB)             +        
               &READONLYACCESS=YES, (PGMINORA)                         +        
               &MAXTASKS=0,        (PGMTSKMX: DECIMAL INTEGER < 256)   +        
               &ACCESSNUMBER=X'00', (PGMALNUM: TWO-DIGIT HEX PROG.NO.) +        
               &LINK=X'00',        (PGMLNUM AND PGMILINK)              +        
               &COUNTRY=0,         (PGMCTRY: DECIMAL COUNTRY CODE)     +        
               &TEXTKEY=0,         (PGMTEXT: DECIMAL INTEGER < 256)    +        
               &PCPROGRAM=NO,      (PGMIPC)                            +        
               &WEBPROGRAM=NO,     (PGMIWEB)                           +        
               &STEREOOPEN=NO,     (PGMISTRO)                          +        
               &TESTFACPAKONLY=NO, (PGMITSTF)                          +        
               &AGENCYACCESSLIST=0, (PGMAGYLA:AGENCY ACCESS LIST LABEL +        
               &UNP=NO,            (PGMIUNP)                           +        
               &DARE=NO,           (PGMIDARE)                          +        
               &CANRUN=STD,        (PGMITSKS)                          +        
               &SPROTECT=NO,       (PGMIPROT)                          +        
               &NOAUTH=NO,         (PGMINAUT)                          +        
               &INDIRECT=NO,       (PGMIACCI)                          +        
               &PC32=NO            (PGMIPC32)                                   
.*                                                                              
.*                       (  &SECURITY=OLD  NEITHER BIT IS ON  )                 
.*                       (  &SECURITY=NEW  SETS ON PGMISECA   )                 
.*                       (  &SECURITY=BOTH SETS ON BOTH BITS  )                 
.* ___________________________________________________________________          
         LCLA  &FLAGS                                                           
&FLAGS  SETA  0                                                                 
.* ___________________________________________________________________          
         AIF   ((T'&PGM_NAME NE 'O') AND (T'&PGM_NUMBER NE 'O')).J1             
         MNOTE 8,'ERROR: REQUIRED PARAMETER OMITTED'                            
.J1      ANOP                                                                   
.*  ------------------   PGMNAME   ----------------------------------           
         AIF   (K'&PGM_NAME LE 7).J2                                            
         MNOTE 8,'ERROR: MAXIMUM CHARACTERS IN PROGRAM NAME IS 7'               
.J2      ANOP                                                                   
.*  -----------------------------------------------------------------           
&LABEL   DC    CL7'&PGM_NAME'      **PGMNAME**                                  
.* --------------------   PGMIND   ----------------------------------           
         AIF   ('&OPERATIONAL'(1,1) EQ 'Y').SK1                                 
         AIF   ('&OPERATIONAL'(1,1) EQ 'N').SK1A                                
         MNOTE 8,'ERROR: OPERATIONAL PARAMETER NOT YES OR NO'                   
.SK1A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'80'                                                     
.SK1     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&AUTHHALF'(1,1) EQ 'N').SK2                                    
         AIF   ('&AUTHHALF'(1,1) EQ 'Y').SK2A                                   
         MNOTE 8,'ERROR: AUTHHALF PARAMETER NOT YES OR NO'                      
.SK2A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'40'                                                     
.SK2     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&RESTRICTED'(1,1) EQ 'N').SK3                                  
         AIF   ('&RESTRICTED'(1,1) EQ 'Y').SK3A                                 
         MNOTE 8,'ERROR: AUTHHALF PARAMETER NOT YES OR NO'                      
.SK3A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'20'                                                     
.SK3     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&IOBOUND'(1,1) EQ 'N').SK4                                     
         AIF   ('&IOBOUND'(1,1) EQ 'Y').SK4A                                    
         MNOTE 8,'ERROR: IOBOUND PARAMETER NOT YES OR NO'                       
.SK4A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'10'                                                     
.SK4     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&AUTHORED'(1,1) EQ 'N').SK5                                    
         AIF   ('&AUTHORED'(1,1) EQ 'Y').SK5A                                   
         MNOTE 8,'ERROR: AUTHORED PARAMETER NOT YES OR NO'                      
.SK5A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'08'                                                     
.SK5     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&READONLY'(1,1) EQ 'N').SK6                                    
         AIF   ('&READONLY'(1,1) EQ 'Y').SK6A                                   
         MNOTE 8,'ERROR: READONLY PARAMETER NOT YES OR NO'                      
.SK6A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'02'                                                     
         AGO   .SK7                                                             
.SK6     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&READFORUPDATE'(1,1) EQ 'N').SK7                               
         AIF   ('&READFORUPDATE'(1,1) EQ 'Y').SK7A                              
         MNOTE 8,'ERROR: READFORUPDATE PARAMETER NOT YES OR NO'                 
.SK7A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'04'                                                     
.SK7     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&OLAI'(1,1) EQ 'N').SK8                                        
         AIF   ('&OLAI'(1,1) EQ 'Y').SK8A                                       
         MNOTE 8,'ERROR: OLAI PARAMETER NOT YES OR NO'                          
.SK8A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'01'                                                     
.SK8     ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    AL1(&FLAGS)         **PGMIND**                                   
.*   ------------------   PGMNUM   ---------------------------------            
         AIF  (('&PGM_NUMBER'(1,1) EQ 'X') AND (K'&PGM_NUMBER EQ 5)).J3         
         MNOTE 8,'ERROR: PROGRAM NUMBER MUST BE A TWO DIGIT HEX NUMBER +        
               (X''**'')'                                                       
.J3      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    &PGM_NUMBER         **PGMNUM**                                   
.*   ------------------   PGMCOSYS   -------------------------------            
         AIF   (('&CALLOVSYS'(1,1) EQ 'X') AND (K'&CALLOVSYS EQ 5)).J4          
         MNOTE 8,'ERROR: PGM CALLOV SYS NO. MUST BE A TWO DIGIT HEX NO.+        
               (X''**'')'                                                       
.J4      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    &CALLOVSYS          **PGMCOSYS**                                 
.*   --------------------   PGMPRTY   ------------------------------            
         AIF   ((K'&PRIORITY LE 3) AND (&PRIORITY LT 256)).J5                   
         MNOTE 8,'ERROR: PROGRAM PRIORITY IS A DECIMAL NUMBER < 256'            
.J5      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    AL1(&PRIORITY)      **PGMPRTY**                                  
.*   ---------------------------------------------------------------            
&FLAGS   SETA  0                                                                
.* --------------------   PGMIND2  ----------------------------------           
         AIF   ('&FASWITCH'(1,1) EQ 'N').SK9                                    
         AIF   ('&FASWITCH'(1,1) EQ 'Y').SK9A                                   
         MNOTE 8,'ERROR: FASWITCH PARAMETER NOT YES OR NO'                      
.SK9A    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'80'                                                     
.SK9     ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&HELPV1'(1,1) EQ 'N').SK10                                     
         AIF   ('&HELPV1'(1,1) EQ 'Y').SK10A                                    
         MNOTE 8,'ERROR: HELPV1 PARAMETER NOT YES OR NO'                        
.SK10A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'40'                                                     
.SK10    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&INPUTREQUIRED'(1,1) EQ 'Y').SK11                              
         AIF   ('&INPUTREQUIRED'(1,1) EQ 'N').SK11A                             
         MNOTE 8,'ERROR: INPUTREQUIRED PARAMETER NOT YES OR NO'                 
.SK11A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'20'                                                     
.SK11    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&PF1HELP'(1,1) EQ 'N').SK12                                    
         AIF   ('&PF1HELP'(1,1) EQ 'Y').SK12A                                   
         MNOTE 8,'ERROR: PF1HELP PARAMETER NOT YES OR NO'                       
.SK12A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'10'                                                     
.SK12    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&GOCONNECT'(1,1) EQ 'N').SK13                                  
         AIF   ('&GOCONNECT'(1,1) EQ 'Y').SK13A                                 
         MNOTE 8,'ERROR: READFORUPDATE PARAMETER NOT YES OR NO'                 
.SK13A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'08'                                                     
.SK13    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&SECURITY' EQ 'OLD').SK15                                      
         AIF   ('&SECURITY' EQ 'NEW').SK14                                      
         AIF   ('&SECURITY' EQ 'BOTH').SK14A                                    
         MNOTE 8,'ERROR: SECURITY PARAMETER NOT OLD, NEW, OR BOTH'              
.SK14A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'02'                                                     
.SK14    ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'04'                                                     
.SK15    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&READONLYACCESS'(1,1) EQ 'Y').SK16                             
         AIF   ('&READONLYACCESS'(1,1) EQ 'N').SK16A                            
         MNOTE 8,'ERROR: READONLYACCESS PARAMETER NOT YES OR NO'                
.SK16A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'01'                                                     
.SK16    ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    AL1(&FLAGS)         **PGMIND2**                                  
.*   -------------------   PGMTSKMX  -------------------------------            
         AIF   ((K'&MAXTASKS LE 3) AND (&MAXTASKS LT 256)).J6                   
         MNOTE 8,'ERROR: PROGRAM MAX NUM OF TASKS IS AN INTEGER < 256'          
.J6      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    AL1(&MAXTASKS)      **PGMTSKMX**                                 
.*   -------------------   PGMTSK   --------------------------------            
         DC    XL1'00'             **PGMTSK**                                   
.*   -------------------   PGMALNUM   ------------------------------            
         AIF   (('&ACCESSNUMBER'(1,1) EQ 'X') AND                      +        
               (K'&ACCESSNUMBER EQ 5)).J7                                       
         MNOTE 8,'ERROR: PGM ACCESS LIST NO. MUST BE A ONE BYTE HEX NO.+        
               (X''**'')'                                                       
.J7      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    &ACCESSNUMBER       **PGMALNUM**                                 
.*   --------------------   PGMCTRY   ------------------------------            
         AIF   ((K'&COUNTRY LE 3) AND (&COUNTRY LT 256)).J8                     
         MNOTE 8,'ERROR: PGM COUNTRY CODE IS A DECIMAL COUNTRY CODE'            
.J8      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    AL1(&COUNTRY)       **PGMCTRY**                                  
.*   ---------------------   PGMTEXT   -----------------------------            
         AIF   (('&TEXTKEY'(1,1) NE 'X') AND (&TEXTKEY LT 65536)).J9            
         MNOTE 8,'ERROR: TEXTKEY IS AN INTEGER < 65536'                         
.J9      ANOP                                                                   
.*   ---------------------------------------------------------------            
         DC    AL2(&TEXTKEY)       **PGMTEXT**                                  
.*   ---------------------------------------------------------------            
&FLAGS   SETA  0                                                                
.* --------------------   PGMIND3  ----------------------------------           
         AIF   ('&PCPROGRAM'(1,1) EQ 'N').SK17                                  
         AIF   ('&PCPROGRAM'(1,1) EQ 'Y').SK17A                                 
         MNOTE 8,'ERROR: PCPROGRAM PARAMETER NOT YES OR NO'                     
.SK17A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'80'                                                     
.SK17    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&WEBPROGRAM'(1,1) EQ 'N').SK18                                 
         AIF   ('&WEBPROGRAM'(1,1) EQ 'Y').SK18A                                
         MNOTE 8,'ERROR: WEBPROGRAM PARAMETER NOT YES OR NO'                    
.SK18A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'40'                                                     
.SK18    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&STEREOOPEN'(1,1) EQ 'N').SK19                                 
         AIF   ('&STEREOOPEN'(1,1) EQ 'Y').SK19A                                
         MNOTE 8,'ERROR: STEREOOPEN PARAMETER NOT YES OR NO'                    
.SK19A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'20'                                                     
.SK19    ANOP                                                                   
.*   ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''               
         AIF   ('&TESTFACPAKONLY'(1,1) EQ 'N').SK20                             
         AIF   ('&TESTFACPAKONLY'(1,1) EQ 'Y').SK20A                            
         MNOTE 8,'ERROR: TESTFACPAKONLY PARAMETER NOT YES OR NO'                
.SK20A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'10'                                                     
.SK20    ANOP                                                                   
         AIF   ('&UNP'(1,1) EQ 'N').SK22                                        
         AIF   ('&UNP'(1,1) EQ 'Y').SK22A                                       
         MNOTE 8,'ERROR: UNP PARAMETER NOT YES OR NO'                           
.SK22A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'08'                                                     
.SK22    ANOP                                                                   
         AIF   ('&DARE'(1,1) EQ 'N').SK24                                       
         AIF   ('&DARE'(1,1) EQ 'Y').SK24A                                      
         MNOTE 8,'ERROR: DARE PARAMETER NOT YES OR NO'                          
.SK24A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'04'                                                     
.SK24    ANOP                                                                   
         AIF   ('&CANRUN' EQ 'STD').SK26                                        
         AIF   ('&CANRUN' EQ 'TORONLY').SK26A                                   
         AIF   ('&CANRUN' EQ 'AORONLY').SK26B                                   
         AIF   ('&CANRUN' EQ 'BOTH').SK26C                                      
         MNOTE 8,'ERROR: CANRUN MUST BE TORONLY, AORONLY OR BOTH'               
.SK26A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'01'        PROGRAM ONLY RUNS IN TOR                     
         AGO   .SK26                                                            
.SK26B   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'02'        PROGRAM ONLY RUNS IN AOR                     
         AGO   .SK26                                                            
.SK26C   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'03'        PROGRAM RUNS IN AOR AND TOR                  
         AGO   .SK26                                                            
.SK26    ANOP                                                                   
.*     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -              
         DC    AL1(&FLAGS)         **PGMIND3**                                  
.*   ---------------------------------------------------------------            
&FLAGS   SETA  0                                                                
.* --------------------   PGMIND4  ----------------------------------           
         AIF   ('&SPROTECT'(1,1) EQ 'N').SK27                                   
         AIF   ('&SPROTECT'(1,1) EQ 'Y').SK27A                                  
         MNOTE 8,'ERROR: SPROTECT PARAMETER NOT YES OR NO'                      
.SK27A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'80'                                                     
.SK27    ANOP                                                                   
         AIF   ('&PC32'(1,1) EQ 'N').SK28                                       
         AIF   ('&PC32'(1,1) EQ 'Y').SK28A                                      
         MNOTE 8,'ERROR: PC32 PARAMETER NOT YES OR NO'                          
.SK28A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'20'                                                     
.SK28    ANOP                                                                   
         AIF   (('&LINK'(1,1) EQ 'X') AND (K'&LINK EQ 5)).SK29                  
         MNOTE 8,'ERROR: LINK NO. MUST BE A ONE BYTE HEX NO. (X''**'')'         
         AGO   .SK29A                                                           
.SK29    ANOP                                                                   
         AIF   ('&LINK'(3,2) EQ '00').SK29A                                     
&FLAGS   SETA  &FLAGS+X'10'        PGMILINK                                     
.SK29A   ANOP                                                                   
         AIF   ('&NOAUTH'(1,1) EQ 'N').SK31                                     
         AIF   ('&NOAUTH'(1,1) EQ 'Y').SK30A                                    
         MNOTE 8,'ERROR: NOAUTH PARAMETER NOT YES OR NO'                        
.SK30A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'08'                                                     
.SK31    ANOP                                                                   
         AIF   ('&INDIRECT'(1,1) EQ 'N').SK32                                   
         AIF   ('&INDIRECT'(1,1) EQ 'Y').SK31A                                  
         MNOTE 8,'ERROR: INDIRECT PARAMETER NOT YES OR NO'                      
.SK31A   ANOP                                                                   
&FLAGS   SETA  &FLAGS+X'04'                                                     
.SK32    ANOP                                                                   
.*     - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -              
         DC    AL1(&FLAGS)         **PGMIND4**                                  
.*   ---------------------  PGMCNT1  -------------------------------            
         DC    XL4'00'             **PGMCNT1**                                  
.*   ---------------------  PGMLNUM  -------------------------------            
         DC    &LINK               **PGMLNUM**                                  
.*   ---------------------  PGMAGYLA  ------------------------------            
         DC    AL3(&AGENCYACCESSLIST) **PGMAGYLA**                              
.*   --------------------  PGMVINFO  -------------------------------            
         DC    XL14'00'            **PGMINFO**                                  
.*   --------------------  N/D  ------------------------------------            
         DC    XL6'00'             **N/D**                                      
.*   ---------------------------------------------------------------            
.*   ---------------------------------------------------------------            
         MEND                                                                   
