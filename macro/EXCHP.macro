*          DATA SET MCEXCHP    AT LEVEL 005 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     EXCHP &INPUT,&RULE,&WRK=WORK,&DUB=DUB                                  
.*                                                                              
.*                                 GET INPUT INTO RE                            
         AIF   (T'&INPUT NE 'O').INPT2                                          
         MNOTE 8,'MISSING INPUT EXPRESSION'                                     
         AGO   .OUT2                                                            
.*                                                                              
.INPT2   AIF   (N'&INPUT EQ 1).INPT3                                            
         MNOTE 8,'TOO MANY INPUT TERMS'                                         
         AGO   .OUT2                                                            
.*                                                                              
.INPT3   ANOP                                                                   
&TAG     DS    0H                                                               
.*                                 GET A(RULE) INTO RF                          
.OUT2    AIF   (T'&RULE NE 'O').OUT3                                            
         MNOTE 8,'MISSING EXCHANGE RULE'                                        
         MEXIT                                                                  
.*                                                                              
.OUT3    AIF   ('&RULE'(1,1) NE '(').OUT4                                       
         AIF   ('&RULE'(2,2) EQ 'RF').OUT5                                      
         LR    RF,&RULE(1)                                                      
         AGO   .OUT5                                                            
.*                                                                              
.OUT4    ANOP                                                                   
         LA    RF,&RULE                                                         
.*                                 GET VALUE, DIVISOR & MULTIPLIER              
.*                                 INTO WRK(16), WRK+16(8), DUB                 
.OUT5    ANOP                                                                   
         XC    &WRK.(8),&WRK                                                    
         AIF   ('&DUB' EQ '&INPUT').OUT6                                        
         ZAP   &DUB,&INPUT                                                      
.*                                                                              
.OUT6    ANOP                                                                   
         ZAP   &WRK+8(8),&DUB                                                   
         ZAP   &WRK+16(8),=P'0'                                                 
         MVO   &WRK+16(8),1(5,RF)                                               
         ZAP   &DUB,=PL4'100000'                                                
.*                                 TRANSPOSE MULTIPLIER & DIVISOR               
.*                                 IF RULE SAYS DONT DIVIDE BY RATE             
         TM    0(RF),X'01'                                                      
         BO    *+16                                                             
         MVC   &DUB,&WRK+16                                                     
         ZAP   &WRK+16(8),=PL4'100000'                                          
.*                                 ADJUST FOR # DP'S DIFFERENCE                 
.*                                 NOW IF LEFTWARD SHIFT                        
         ZIC   RE,6(RF)                                                         
         TM    6(RF),X'80'                                                      
         BO    *+10                                                             
         SRP   &WRK.(16),0(RE),5                                                
.*                                 CONVERT CURRENCY                             
         MP    &WRK.(16),&DUB                                                   
         MP    &WRK.(16),=PL2'10'                                               
         DP    &WRK.(16),&WRK+16(8)                                             
         SRP   &WRK.(8),63,5       ROUND                                        
.*                                 ADJUST FOR # DP'S DIFFERENCE                 
.*                                 NOW IF NOT LEFTWARD SHIFT                    
         TM    6(RF),X'80'                                                      
         BNO   *+10                                                             
         SRP   &WRK.(8),0(RE),5                                                 
.*                                 RETURN VALUE IN RE                           
         ZAP   &DUB,&WRK.(8)                                                    
         MEXIT                                                                  
         MEND                                                                   
