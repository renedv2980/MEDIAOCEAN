*          DATA SET MCPROT     AT LEVEL 004 AS OF 03/06/20                      
         MACRO                                                                  
&NAME    PROT  &INPUT                                                           
         GBLC  &PONX,&POFFX,&PCLRX                                              
&PONX    SETC  'NX&SYSNDX'                                                      
&POFFX   SETC  'FX&SYSNDX'                                                      
&PCLRX   SETC  'CX&SYSNDX'                                                      
.*                                                                              
         AIF   (D'SSB).SKIP1       ONLY GENERATE "WXTRN SSB" ONCE               
         WXTRN SSB                                                              
.SKIP1   ANOP                                                                   
.*                                                                              
         AIF   ('&INPUT(1)' EQ 'ON').ON                                         
         AIF   ('&INPUT(1)' EQ 'OFF').OFF                                       
         AIF   ('&INPUT(1)' EQ 'CLEAR').CLR                                     
         AGO   .EX                                                              
.**********************************************************************         
.* THIS MACRO IS INVOKED IN THE FAPROT COMFACS ROUTINE                *         
.* FAPROT ASSUMES THAT THE PROT MACRO USES ONLY RE AND RF             *         
.**********************************************************************         
.*                                                                              
.*       SET STORAGE PROTECTION ON IF VALID                                     
.*                                                                              
.ON      ANOP                                                                   
&NAME    ICM   RE,15,=V(SSB)       IS V(SSB) RESOLVED?                          
         BZ    &PONX               NO: JUST EXIT                                
         ICM   RF,3,0(RE)          ARE WE OFFLINE? (SSOCNTL = 0 ?)              
         BZ    &PONX               YES: JUST EXIT                               
         TM    X'A8'(RE),X'80'     TEST STORAGE PROTECTION ENABLED              
         BZ    &PONX                                                            
         ICM   RE,15,X'14'(RE)     GET A(TASK) FROM SSB                         
         BZ    &PONX                                                            
         TM    X'294'(RE),X'80'    TEST APPLIC SPKEY SET IN TCBPROT             
         BZ    &PONX                                                            
         TM    X'294'(RE),X'40'    TEST FACPAK SPKEY OVERRIDE SET               
         BZ    &PONX                                                            
         L     RF,4(RD)                                                         
         TM    4(RF),X'80'                                                      
         BZ    &PONX                                                            
         NI    4(RF),X'FF'-X'80'                                                
         NI    X'294'(RE),X'FF'-X'40'                                           
         SPKA  144                                                              
&PONX    DS    0H                                                               
         AGO   .EX                                                              
.*                                                                              
.*       SET STORAGE PROTECTION OFF IF VALID                                    
.*                                                                              
.OFF     ANOP                                                                   
&NAME    ICM   RE,15,=V(SSB)       IS V(SSB) RESOLVED?                          
         BZ    &POFFX              NO: JUST EXIT                                
         ICM   RF,3,0(RE)          ARE WE OFFLINE? (SSOCNTL = 0 ?)              
         BZ    &POFFX              YES: JUST EXIT                               
         TM    X'A8'(RE),X'80'     TEST STORAGE PROTECTION ENABLED              
         BZ    &POFFX                                                           
         ICM   RE,15,X'14'(RE)     GET A(TASK) FROM SSB                         
         BZ    &POFFX                                                           
         TM    X'294'(RE),X'80'    TEST APPLIC SPKEY SET IN TCBPROT             
         BZ    &POFFX                                                           
         L     RF,4(RD)                                                         
         NI    4(RF),X'FF'-X'80'                                                
         TM    X'294'(RE),X'40'    TEST FACPAK SPKEY OVERRIDE SET               
         BO    &POFFX                                                           
         SPKA  128                                                              
         OI    X'294'(RE),X'40'                                                 
         OI    4(RF),X'80'                                                      
&POFFX   DS    0H                                                               
         AGO   .EX                                                              
.*                                                                              
.*       CLEAR STORAGE PROTECTION                                               
.*                                                                              
.CLR     ANOP                                                                   
&NAME    ICM   RE,15,=V(SSB)       IS V(SSB) RESOLVED?                          
         BZ    &PCLRX              NO: JUST EXIT                                
         ICM   RF,3,0(RE)          ARE WE OFFLINE? (SSOCNTL = 0 ?)              
         BZ    &PCLRX              YES: JUST EXIT                               
         TM    X'A8'(RE),X'80'     TEST STORAGE PROTECTION ENABLED              
         BZ    &PCLRX                                                           
         ICM   RE,15,X'14'(RE)     GET A(TASK) FROM SSB                         
         BZ    &PCLRX                                                           
         SPKA  128                                                              
         MVI   X'294'(RE),X'00'    CLEAR TCBPROT BYTE                           
         L     RF,4(RD)                                                         
         NI    4(RF),X'FF'-X'80'                                                
&PCLRX   DS    0H                                                               
         AGO   .EX                                                              
.*                                                                              
.EX      ANOP                                                                   
         MEND                                                                   
