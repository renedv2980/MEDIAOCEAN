*          DATA SET MCASPEC    AT LEVEL 011 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     ASPEC &LINE,&COL,&KEY,&DDLEN,&DDTYP,&WIDE=132                          
         LCLA  &PL,&PN,&PD,&PK                                                  
         LCLA  &PC                                                              
         LCLC  &PCL,&PLENA                                                      
.*                                                                              
.*                                 WORK OUT THE LINE                            
.*                                                                              
&PCL     SETC  '&LINE'(2,K'&LINE-1)                                             
&PL      SETA  &PCL                                                             
         AIF   (&PL EQ 0).P2                                                    
         AIF   (&PL GT 12).P2                                                   
&PC      SETA  0                                                                
         AIF   ('&LINE'(1,1) EQ 'H').P4                                         
         AIF   (&PL GT 4).P2                                                    
         AIF   ('&LINE'(1,1) EQ 'P').P3                                         
         AIF   (&PL GT 2).P2                                                    
.P3      ANOP                                                                   
&PL      SETA  &PL+12                                                           
&PC      SETA  8                                                                
         AIF   ('&LINE'(1,1) EQ 'M').P4                                         
&PL      SETA  &PL+2                                                            
&PC      SETA  16                                                               
         AIF   ('&LINE'(1,1) EQ 'P').P4                                         
&PL      SETA  &PL+4                                                            
&PC      SETA  24                                                               
         AIF   ('&LINE'(1,1) EQ 'F').P4                                         
&PL      SETA  &PL+2                                                            
&PC      SETA  132                                                              
         AIF   ('&LINE'(1,1) EQ 'U').P4                                         
.*                                                                              
.P2      MNOTE 8,'INVALID LINE'                                                 
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 COMPUTE DISPLACEMENT                         
.*                                                                              
.P4      ANOP                                                                   
         AIF   (T'&COL NE 'N').P6                                               
         AIF   (&COL EQ 0).P6                                                   
         AIF   (&COL GT &WIDE).P6                                               
&PL      SETA  &PL-1                                                            
&PD      SETA  &PL*&WIDE                                                        
&PD      SETA  &PD+&COL-1                                                       
&PD      SETA  &PD+&PC                                                          
.*                                                                              
.*                                 HANDLE KEYWORDS                              
.*                                                                              
&PL      SETA  K'&KEY-3                                                         
         AIF   ('&KEY'(1,1) GT 'Z').P9                                          
         AIF   ('&KEY'(1,2) EQ 'C''').P12                                       
         AIF   ('&KEY'(1,3) EQ 'AC#').PDD                                       
         AIF   ('&KEY'(1,3) EQ 'CLI').P5                                        
         AIF   ('&KEY'(1,2) EQ 'CL').P10                                        
.P5      ANOP                                                                   
&PK      SETA  0                                                                
         AIF   ('&KEY' EQ 'SPACES').P8                                          
&PK      SETA  1                                                                
         AIF   ('&KEY' EQ 'RUN').P8                                             
&PK      SETA  2                                                                
         AIF   ('&KEY' EQ 'REPORT').P8                                          
&PK      SETA  3                                                                
         AIF   ('&KEY' EQ 'PAGE').P8                                            
&PK      SETA  4                                                                
         AIF   ('&KEY' EQ 'PERIOD').P8                                          
&PK      SETA  5                                                                
         AIF   ('&KEY' EQ 'REQUESTOR').P8                                       
&PK      SETA  6                                                                
         AIF   ('&KEY' EQ 'COMPANY').P8                                         
&PK      SETA  7                                                                
         AIF   ('&KEY' EQ 'COMPADD').P8                                         
&PK      SETA  8                                                                
         AIF   ('&KEY' EQ 'UNIT').P8                                            
&PK      SETA  9                                                                
         AIF   ('&KEY' EQ 'UNITADD').P8                                         
&PK      SETA  10                                                               
         AIF   ('&KEY' EQ 'LEDGER').P8                                          
&PK      SETA  11                                                               
         AIF   ('&KEY' EQ 'ACCOUNT').P8                                         
&PK      SETA  12                                                               
         AIF   ('&KEY' EQ 'ACCADD').P8                                          
&PK      SETA  13                                                               
         AIF   ('&KEY' EQ 'RENUM').P8                                           
&PK      SETA  14                                                               
         AIF   ('&KEY' EQ 'REP').P8                                             
&PK      SETA  15                                                               
         AIF   ('&KEY' EQ 'BASIS').P8                                           
&PK      SETA  16                                                               
         AIF   ('&KEY' EQ 'TEAM').P8                                            
&PK      SETA  17                                                               
         AIF   ('&KEY' EQ 'OFFICE').P8                                          
&PK      SETA  18                                                               
         AIF   ('&KEY' EQ 'SALESMAN').P8                                        
&PK      SETA  19                                                               
         AIF   ('&KEY' EQ 'ASAT').P8                                            
&PK      SETA  20                                                               
         AIF   ('&KEY' EQ 'GROUP').P8                                           
&PK      SETA  21                                                               
         AIF   ('&KEY' EQ 'REQDETS').P8                                         
&PK      SETA  22                                                               
         AIF   ('&KEY' EQ 'SUBGROUP').P8                                        
&PK      SETA  23                                                               
         AIF   ('&KEY' EQ 'STATION').P8                                         
&PK      SETA  24                                                               
         AIF   ('&KEY' EQ 'DIVISION').P8                                        
&PK      SETA  25                                                               
         AIF   ('&KEY' EQ 'ORIGIN').P8                                          
&PK      SETA  26                                                               
         AIF   ('&KEY' EQ 'ORIGADD').P8                                         
&PK      SETA  27                                                               
         AIF   ('&KEY' EQ 'REP1').P8                                            
         MNOTE 8,'NOT A VALID KEYWORD'                                          
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.P6      ANOP                                                                   
         MNOTE 8,'COLUMN SHOULD 1-132'                                          
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 COMPLETE KEYWORD EXPANSION                   
.*                                                                              
.P8      ANOP                                                                   
&TAG     DC    X'0C05'                                                          
         DC    AL2(&PD)                                                         
         DC    AL1(&PK)                                                         
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 HANDLE LITERALS                              
.*                                                                              
.*                                                                              
.*                                 GET LENGTH OF MULTIPLE CHARACTERS            
.*                                                                              
.P9      ANOP                                                                   
&PN      SETA  1                                                                
         AIF   ('&KEY'(2,1) LE 'Z').P9A                                         
&PN      SETA  2                                                                
         AIF   ('&KEY'(3,1) LE 'Z').P9A                                         
&PN      SETA  3                                                                
         AIF   ('&KEY'(4,1) GT 'Z').P14                                         
.P9A     ANOP                                                                   
&PLENA   SETC  '&KEY'(1,&PN)                                                    
         AGO   .P11A                                                            
.P10     ANOP                                                                   
&PN      SETA  1                                                                
         AIF   ('&KEY'(4,1) EQ '''').P11                                        
&PN      SETA  2                                                                
         AIF   ('&KEY'(5,1) EQ '''').P11                                        
&PN      SETA  3                                                                
         AIF   ('&KEY'(6,1) EQ '''').P11                                        
         AGO   .P14                                                             
.*                                                                              
.*                                 DIG OUT EXPLICIT LENGTH                      
.*                                                                              
.P11     ANOP                                                                   
&PLENA   SETC  '&KEY'(3,&PN)                                                    
.P11A    ANOP                                                                   
&PL      SETA  &PLENA                                                           
         AIF   (&PL EQ 0).P14                                                   
         AIF   (&PL GT 250).P14                                                 
.*                                                                              
.*                                 GENERATE LITERAL CODE                        
.*                                                                              
.P12     ANOP                                                                   
&TAG     DC    X'0D'                                                            
&PL      SETA  &PL+4                                                            
         DC    AL1(&PL)                                                         
         DC    AL2(&PD)                                                         
         DC    &KEY                                                             
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                                                              
.P14     ANOP                                                                   
         MNOTE 8,'INVALID LITERAL EXPRESSION'                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 DATA DICTIONARY CODE                         
.*                                                                              
.PDD     ANOP                                                                   
         AIF   (T'&DDLEN EQ 'O').PDDERR                                         
         AIF   ('&DDLEN' EQ '2').PDD1                                           
         AIF   ('&DDLEN' EQ '3').PDD1                                           
&PL      SETA  &DDLEN+4                                                         
         AGO   .PDD2                                                            
.PDD1    ANOP                                                                   
&PL      SETA  7                                                                
.PDD2    ANOP                                                                   
&TAG     DC    X'0D'                                                            
         DC    AL1(&PL)                                                         
         DC    AL2(&PD)                                                         
         DCDD  &KEY,&DDLEN,&DDTYP,PAD=BLANKS                                    
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                                                              
.PDDERR  ANOP                                                                   
         MNOTE 8,'DATA DICT MUST HAVE LENGTH'                                   
         SPACE 1                                                                
         MEXIT                                                                  
         MEND                                                                   
