*          DATA SET MCNMODNEW  AT LEVEL 022 AS OF 01/28/04                      
         MACRO                                                                  
&NAME    NMOD1 &D,&ID,&CLEAR=NO,&RR=                                            
         LCLA  &I,&J,&K,&L,&M                                                   
         LCLC  &RL                                                              
.*                                                                              
         REQUS                                                                  
.*                                                                              
&NAME    STM   RE,RC,12(RD)                                                     
         BASR  RB,0                                                             
         AHI   RB,-6                                                            
         B     32(RB)                                                           
         DC    XL4'00'             FILLER                                       
         AIF   (T'&D NE 'O').L1                                                 
         DC    AL4(0)                                                           
         AGO   .L3                                                              
.L1      AIF   (T'&D NE 'N').L2                                                 
         DC    AL4(&D*8)                                                        
         AGO   .L3                                                              
.L2      ANOP                                                                   
         DC    AL4((&D+7)/8*8)                                                  
.L3      AIF   (T'&ID NE 'O').L4                                                
         DC    CL8' '                                                           
         AGO  .L5                                                               
.L4      ANOP                                                                   
         DC    CL8'&ID'                                                         
.L5      ANOP                                                                   
         DC    AL2(0)                                                           
.*                                                                              
&K       SETA  32                                                               
&I       SETA  2                                                                
.*                                                                              
.LA      ANOP                                                                   
&J       SETA  N'&SYSLIST-&I                                                    
         USING *-&K,RB                                                          
         AIF   (&J EQ 0).LC                                                     
&K       SETA  &K+4                                                             
&L       SETA  0                                                                
&RL      SETC 'RB'                                                              
         LHI   RC,4096                                                          
.*                                                                              
.LB      ANOP                                                                   
&I       SETA  &I+1                                                             
&K       SETA  &K+4                                                             
&L       SETA  &L+1                                                             
&M       SETA  4096*&L-&K                                                       
         LA    &SYSLIST(&I),0(RC,&RL)                                           
         USING *+&M,&SYSLIST(&I)                                                
&J       SETA  &J-1                                                             
         AIF   (&J EQ 0).LC                                                     
&RL      SETC  '&SYSLIST(&I)'                                                   
         AGO   .LB                                                              
.*                                                                              
.LC      ANOP                                                                   
         ICM   RC,15,18(RB)                                                     
         LA    RC,72(RC,RD)                                                     
         ST    RD,4(RC)                                                         
         ST    RC,8(RD)                                                         
         MVC   0(4,RD),24(RB)                                                   
.*                                                                              
.LF      AIF   ('&CLEAR' EQ 'NO').LG                                            
         LA    R0,72(RD)                                                        
         LR    R1,RC                                                            
         SR    R1,R0                                                            
         BZ    *+8                                                              
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         LM    RE,R1,12(RD)                                                     
.*                                                                              
.LG      ANOP                                                                   
         LR    RD,RC                                                            
         S     RC,18(RB)                                                        
.*                                                                              
.LH      AIF   (T'&RR EQ 'O').LI                                                
         CNOP  0,4                                                              
         B     *+8                                                              
         DC    A(*)                                                             
         LA    &RR,*-4                                                          
         S     &RR,*-8                                                          
.*                                                                              
.LI      ANOP                                                                   
         MEND                                                                   
