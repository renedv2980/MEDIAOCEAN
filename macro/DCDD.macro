*          DATA SET MCDCDD     AT LEVEL 003 AS OF 09/08/13                      
         MACRO                                                                  
&TAG     DCDD  &DDI,&LEN,&ALIGN,&PAD=SPACES                                     
.*                                                                              
         LCLA  &ESC                                                             
         LCLA  &ESC2,&ESC3,&ESCL,&ESCC,&ESCR,&ESCS,&ESCF                        
         LCLA  &ESU2,&ESU3,&ESUL,&ESUC,&ESUR,&ESUS                              
         LCLC  &HDG                                                             
.*                                                                              
&HDG     SETC  '0'                                                              
.*                                                                              
&ESC2    SETA  32                                                               
&ESC3    SETA  33                                                               
&ESCL    SETA  34                                                               
&ESCC    SETA  35                                                               
&ESCR    SETA  36                                                               
&ESCS    SETA  37                                                               
&ESCF    SETA  39                                                               
.*                                                                              
&ESU2    SETA  40                                                               
&ESU3    SETA  41                                                               
&ESUL    SETA  42                                                               
&ESUC    SETA  43                                                               
&ESUR    SETA  44                                                               
&ESUS    SETA  45                                                               
.*                                                                              
.*********************************************************************          
.DDIVAL  ANOP                                                                   
         AIF   (T'&DDI EQ 'O').DDIERR1                                          
         AGO   .DDIX                                                            
.DDIERR1 MNOTE 8,'MISSING DATA DCTNRY ID'                                       
         MEXIT                                                                  
.DDIX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.LENVAL  ANOP                                                                   
         AIF   (T'&LEN EQ 'O').LENERR1                                          
         AIF   ('&LEN' EQ '0').LENERR2                                          
         AIF   ('&LEN' EQ '1').LENERR2                                          
         AGO   .LENX                                                            
.LENERR1 MNOTE 8,'MISSING FIELD LENGTH'                                         
         MEXIT                                                                  
.LENERR2 MNOTE 8,'INVALID FIELD LENGTH'                                         
         MEXIT                                                                  
.LENX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.ALNVAL  ANOP                                                                   
         AIF   ('&LEN' EQ '2').ALNL2                                            
         AIF   ('&LEN' EQ '3').ALNL3                                            
.*                                                                              
         AIF   (T'&ALIGN EQ 'O').ALNL                                           
.*                                                                              
         AIF   ('&ALIGN' EQ 'L2U').ALNL2U                                       
         AIF   ('&ALIGN' EQ 'L3U').ALNL3U                                       
         AIF   ('&ALIGN'(1,2) EQ 'LU').ALNLU                                    
         AIF   ('&ALIGN'(1,2) EQ 'CU').ALNCU                                    
         AIF   ('&ALIGN'(1,2) EQ 'RU').ALNRU                                    
         AIF   ('&ALIGN'(1,2) EQ 'SU').ALNSU                                    
.*                                                                              
         AIF   ('&ALIGN' EQ 'L2').ALNL2                                         
         AIF   ('&ALIGN' EQ 'L3').ALNL3                                         
         AIF   ('&ALIGN'(1,1) EQ 'L').ALNL                                      
         AIF   ('&ALIGN'(1,1) EQ 'H').ALNH                                      
         AIF   ('&ALIGN'(1,1) EQ 'C').ALNC                                      
         AIF   ('&ALIGN'(1,1) EQ 'R').ALNR                                      
         AIF   ('&ALIGN'(1,1) EQ 'S').ALNS                                      
         AIF   ('&ALIGN'(1,1) EQ 'F').ALNF                                      
         AGO   .ALNERR                                                          
.*                                                                              
.ALNL    ANOP                                                                   
&ESC     SETA  &ESCL                                                            
         AGO   .ALNX                                                            
.ALNL2   ANOP                                                                   
         AIF   ('&ALIGN' EQ 'L2U').ALNL2U                                       
         AIF   ('&ALIGN' EQ 'LU').ALNL2U                                        
&ESC     SETA  &ESC2                                                            
         AGO   .ALNX                                                            
.ALNL3   ANOP                                                                   
         AIF   ('&ALIGN' EQ 'L3U').ALNL3U                                       
         AIF   ('&ALIGN' EQ 'LU').ALNL3U                                        
&ESC     SETA  &ESC3                                                            
         AGO   .ALNX                                                            
.ALNH    ANOP                                                                   
&ESC     SETA  &ESCL                                                            
&HDG     SETC  '128'                                                            
         AGO   .ALNX                                                            
.ALNC    ANOP                                                                   
&ESC     SETA  &ESCC                                                            
         AGO   .ALNX                                                            
.ALNR    ANOP                                                                   
&ESC     SETA  &ESCR                                                            
         AGO   .ALNX                                                            
.ALNS    ANOP                                                                   
&ESC     SETA  &ESCS                                                            
         AGO   .ALNX                                                            
.ALNF    ANOP                                                                   
&ESC     SETA  &ESCF                                                            
         AGO   .ALNX                                                            
.*                                                                              
.ALNLU   ANOP                                                                   
&ESC     SETA  &ESUL                                                            
         AGO   .ALNX                                                            
.ALNL2U  ANOP                                                                   
&ESC     SETA  &ESU2                                                            
         AGO   .ALNX                                                            
.ALNL3U  ANOP                                                                   
&ESC     SETA  &ESU3                                                            
         AGO   .ALNX                                                            
.ALNCU   ANOP                                                                   
&ESC     SETA  &ESUC                                                            
         AGO   .ALNX                                                            
.ALNRU   ANOP                                                                   
&ESC     SETA  &ESUR                                                            
         AGO   .ALNX                                                            
.ALNSU   ANOP                                                                   
&ESC     SETA  &ESUS                                                            
         AGO   .ALNX                                                            
.ALNERR  MNOTE 8,'ALIGN SHOULD BE L,C,R,S,F,H,L2,L3 OR LU,CU,RU,SU'             
         MEXIT                                                                  
.ALNX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.PADVAL  ANOP                                                                   
         AIF   ('&PAD'(1,1) EQ 'B').PADX                                        
         AIF   ('&PAD'(1,1) EQ 'S').PADX                                        
         AIF   ('&PAD'(1,1) EQ 'N').PADX                                        
         AIF   ('&PAD'(1,1) EQ 'Z').PADX                                        
.PADERR1 MNOTE 8,'PAD SHOULD BE BLANKS/SPACES/ZEROS/NULLS OR NONE'              
         MEXIT                                                                  
.PADX    ANOP                                                                   
.*                                                                              
.*********************************************************************          
.GEN     ANOP                                                                   
         AIF   (&ESC EQ &ESC2).GEN3                                             
         AIF   (&ESC EQ &ESC3).GEN3                                             
         AIF   ('&PAD' NE 'NONE').GEN2                                          
.GEN1    ANOP                                                                   
&TAG     DC    AL1(&ESC),AL2(&DDI),AL1(&LEN+&HDG)                               
         AGO   .XIT                                                             
.GEN2    ANOP                                                                   
         AIF   ('&LEN' EQ '2').GEN3                                             
         AIF   ('&LEN' EQ '3').GEN3                                             
         AIF   ('&LEN' EQ '4').GEN1                                             
         AIF   ('&PAD'(1,1) EQ 'B').GEN2A                                       
         AIF   ('&PAD'(1,1) EQ 'S').GEN2A                                       
         AGO   .GEN2B                                                           
.GEN2A   ANOP                                                                   
&TAG     DC    AL1(&ESC),AL2(&DDI),AL1(&LEN+&HDG),(&LEN-4)C' '                  
         AGO   .XIT                                                             
.GEN2B   ANOP                                                                   
&TAG     DC    AL1(&ESC),AL2(&DDI),AL1(&LEN+&HDG),(&LEN-4)X'00'                 
         AGO   .XIT                                                             
.GEN3    ANOP                                                                   
&TAG     DC    AL1(&ESC),AL2(&DDI)                                              
         AGO   .XIT                                                             
.*                                                                              
.XIT     MEXIT                                                                  
         MEND                                                                   
