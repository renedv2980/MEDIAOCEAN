*          DATA SET MCDMDA     AT LEVEL 005 AS OF 10/30/18                      
         MACRO                                                                  
&FILE    DMDA  &TYPEFLE=INPUT,&DEVICE=3350,&XTNTXIT=,                  X        
               &DSKXTNT=16,&BLKSIZE=0,                                 X        
               &RECSIZE=,&IOAREA1=,&IOAREA2=,&FILABL=,&KEYLEN=,        X        
               &EMU=NO,&DCB=YES,&KEYCMP=NO,&SPARE=,&CACHE=YES,         X        
               &XBUFFS=0,&BIG=NO,&VSAM=NO,&VERMONT=NO,&GLOBAL=NO,      X        
               &BIGDEMOS=NO,&ADD=YES,&FLASH=NO,&RO=,&SHRMEM=NO                  
.*                                                                              
         GBLC  &OFFLINE                                                         
         LCLC  &A,&C,&DCBNAME,&OFF,&HIXT                                        
         LCLA  &I,&J,&K,&L,&M,&XNUM,&Q                                          
.*                                                                              
&OFF     SETC  '&OFFLINE'.'XX'                                                  
&DCBNAME SETC  '&FILE.Z'                                                        
&C       SETC  'N'                                                              
&HIXT    SETC  'N'                 LOW CORE EXTENT MATRIX                       
&XNUM    SETA  &DSKXTNT                                                         
.*                                                                              
         AIF   ('&XNUM' EQ '0').DTF1A                                           
         AIF   (T'&KEYLEN EQ 'O' AND T'&IOAREA2 EQ 'O').DTF1                    
&XNUM    SETA  16                                                               
.*                                                                              
.DTF1    ANOP                                                                   
         AIF   ('&BIGDEMOS'(1,1) EQ 'N').DTF1B                                  
         AIF   (T'&KEYLEN NE 'O').DTF1A                                         
         MNOTE 12,'MUST HAVE SET KEYLEN FOR BIGDEMOS'                           
         MEXIT                                                                  
.*                                                                              
.DTF1A   ANOP                                                                   
&XNUM    SETA  48                  SET 48 EXTENT MATRIX                         
&HIXT    SETC  'Y'                 SET HIGH CORE EXTENT MATRIX                  
         AGO   .DTF1C                                                           
.*                                                                              
.DTF1B   ANOP                                                                   
         AIF   (&XNUM LE 16).DTF1C                                              
         MNOTE 12,'USE DSKXTNT=0 FOR >16 EXTENTS'                               
         MEXIT                                                                  
.*                                                                              
.******* START OF DTF GENERATION                                                
.*                                                                              
.DTF1C   ANOP                                                                   
&FILE    DC    0D'0'                                                            
         DC    X'000080',AL1(&XNUM),X'0000FFFF'                                 
         DC    XL8'00',X'0000'                                                  
         DC    AL1(0)                                                           
.*                                                                              
&I       SETA  0                                                                
.DTF3    ANOP                                                                   
         AIF   (T'&VSAM EQ 'O').DTF3B                                           
         AIF   ('&VSAM' EQ 'NO').DTF3B                                          
         AIF   ('&VSAM'(1,1) EQ 'Y').DTF3A                                      
         MNOTE 8,'INVALID VSAM PARAMETER'                                       
         MEXIT                                                                  
.DTF3A   ANOP                                                                   
&I       SETA  &I+128              SET VSAM FILE X'80'                          
.DTF3B   ANOP                                                                   
         AIF   (T'&GLOBAL EQ 'O').DTF3D                                         
         AIF   ('&GLOBAL' EQ 'NO').DTF3D                                        
         AIF   ('&GLOBAL'(1,1) EQ 'Y').DTF3C                                    
         MNOTE 8,'INVALID GLOBAL PARAMETER'                                     
         MEXIT                                                                  
.DTF3C   ANOP                                                                   
&I       SETA  &I+64               SET GLOBAL RESOURCE X'40'                    
.DTF3D   ANOP                                                                   
         AIF   ('&CACHE' EQ 'NO').DTF3D1                                        
         AIF   ('&CACHE' EQ 'YES').DTF3E                                        
         MNOTE 8,'CACHE MUST BE YES OR NO'                                      
         MEXIT                                                                  
.DTF3D1  ANOP                                                                   
&I       SETA  &I+4                NO CACHING X'04'                             
.DTF3E   ANOP                                                                   
         AIF   (T'&ADD EQ 'O').DTF3F                                            
         AIF   ('&ADD'(1,1) EQ 'Y').DTF3F                                       
         AIF   ('&ADD'(1,1) EQ 'N').DTF3E1                                      
         MNOTE 8,'INVALID ADD PARAMETER'                                        
         MEXIT                                                                  
.DTF3E1  ANOP                                                                   
&I       SETA  &I+32               ADD=NO X'20'                                 
.DTF3F   ANOP                                                                   
         AIF   (T'&FLASH EQ 'O').DTF3G                                          
         AIF   ('&FLASH'(1,1) EQ 'N').DTF3G                                     
         AIF   ('&FLASH'(1,1) EQ 'Y').DTF3F1                                    
         MNOTE 8,'INVALID FLASH PARAMETER'                                      
         MEXIT                                                                  
.DTF3F1  ANOP                                                                   
&I       SETA  &I+02               FLASH=YES X'02'                              
.DTF3G   ANOP                                                                   
         AIF   (T'&SHRMEM EQ 'O').DTF3H                                         
         AIF   ('&SHRMEM'(1,1) EQ 'N').DTF3H                                    
         AIF   ('&SHRMEM'(1,1) EQ 'Y').DTF3G1                                   
         MNOTE 8,'INVALID SHRMEM PARAMETER'                                     
         MEXIT                                                                  
.DTF3G1  ANOP                                                                   
&I       SETA  &I+01               SHRMEM=YES X'01'                             
.DTF3H   ANOP                                                                   
         DC    AL1(&I)             DTFFLAG                                      
.*                                                                              
&I       SETA  0                                                                
.DTF4    ANOP                                                                   
         AIF   (T'&EMU EQ 'O').DTF5                                             
         AIF   ('&EMU' EQ 'NO').DTF5                                            
         AIF   ('&EMU'(1,1) EQ 'Y').DTF4A                                       
         MNOTE 8,'INVALID EMU PARAMETER'                                        
         MEXIT                                                                  
.DTF4A   ANOP                                                                   
&I       SETA  &I+128              SET EMULATION MODE X'80'                     
.DTF5    ANOP                                                                   
         AIF   (T'&DCB EQ 'O').DTF6                                             
         AIF   ('&DCB'(1,1) EQ 'Y').DTF6                                        
         AIF   ('&DCB' EQ 'NO').DTF5A                                           
         MNOTE 8,'INVALID DCB PARAMETER'                                        
         MEXIT                                                                  
.DTF5A   ANOP                                                                   
&I       SETA  &I+64               SET NO DCB MODE X'40'                        
.DTF6    ANOP                                                                   
         AIF   (T'&KEYCMP EQ 'O').DTF10                                         
         AIF   ('&KEYCMP' EQ 'NO').DTF10                                        
         AIF   (T'&KEYLEN NE 'O').DTF6A                                         
         MNOTE 8,'MUST HAVE KEYLEN IF CMPRS SPECIFIED'                          
         MEXIT                                                                  
.DTF6A   ANOP                                                                   
         AIF   ('&KEYCMP' EQ 'LR').DTF7                                         
         AIF   ('&KEYCMP' EQ 'RL').DTF7                                         
         AIF   ('&KEYCMP' EQ 'L').DTF7                                          
         AIF   ('&KEYCMP' EQ 'R').DTF8                                          
         MNOTE 8,'INVALID CMPRS PARAMETER'                                      
         MEXIT                                                                  
.DTF7    ANOP                                                                   
&I       SETA  &I+8                COMPRESS FROM LEFT X'08'                     
&C       SETC  'Y'                                                              
.DTF8    ANOP                                                                   
         AIF   ('&KEYCMP' EQ 'LR').DTF9                                         
         AIF   ('&KEYCMP' EQ 'RL').DTF9                                         
         AIF   ('&KEYCMP' EQ 'R').DTF9                                          
         AGO   .DTF10                                                           
.DTF9    ANOP                                                                   
&I       SETA  &I+4                COMPRESS FROM RIGHT X'04'                    
&C       SETC  'Y'                                                              
.*                                                                              
.DTF10   ANOP                                                                   
         AIF   ('&BIG'(1,1) EQ 'N').DTF12                                       
         AIF   ('&BIG'(1,2) EQ '16').DTF12                                      
         AIF   ('&BIG'(1,1) EQ 'Y').DTF10A                                      
         AIF   ('&BIG'(1,2) EQ '18').DTF10A                                     
         AIF   ('&BIG'(1,1) EQ 'B').DTF10B                                      
         AIF   ('&BIG'(1,2) EQ '20').DTF10B                                     
         AIF   ('&BIG'(1,1) EQ 'G').DTF10C                                      
         AIF   ('&BIG'(1,2) EQ '22').DTF10C                                     
         MNOTE 8,'BIG MUST BE N,Y,B,G OR 16,18,20,22'                           
         MEXIT                                                                  
.DTF10A  ANOP                                                                   
&I       SETA  &I+1                BIG FILE Y (18 BIT TRACKS) X'01'             
         AGO   .DTF12                                                           
.DTF10B  ANOP                                                                   
&I       SETA  &I+2                BIG FILE B (20 BIT TRACKS) X'02'             
         AGO   .DTF12                                                           
.DTF10C  ANOP                                                                   
&I       SETA  &I+3                BIG FILE G (22 BIT TRACKS) X'03'             
.*                                                                              
.DTF12   ANOP                                                                   
         DC    AL1(&I)             DTFTYPE                                      
.*                                                                              
.DTF13   ANOP                                                                   
         DC    AL1(&XBUFFS)                                                     
         DC    CL8'&FILE'                                                       
         DC    AL4(0),AL1(0),AL1(0)                                             
.*                                                                              
.DTF14   ANOP                                                                   
         AIF   (T'&DCB EQ 'O').DTF16                                            
         AIF   ('&DCB' NE 'NO').DTF16                                           
         DC    A(0)                                                             
         AIF   (T'&FILABL EQ 'O').DTF15                                         
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.DTF15   ANOP                                                                   
         MEXIT                                                                  
.*                                                                              
.DTF16   ANOP                                                                   
         DC    A(&DCBNAME)         A(DTF)                                       
         AIF   (T'&FILABL EQ 'O').DTF17                                         
         ORG   &FILE+22                                                         
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.*                                                                              
.DTF17   ANOP                                                                   
         AIF   (T'&RO EQ 'O').DTF18                                             
         AIF   ('&RO' EQ 'Y').DTF17A                                            
         AIF   ('&RO' NE 'YES').DTF18                                           
.DTF17A  ORG   &FILE+36            DTFOPEN                                      
         DC    X'80'               READ-ONLY (DTF_RO)                           
         ORG                                                                    
.*                                                                              
.DTF18   ANOP                                                                   
.*                                                                              
.* ******************************* END OF XL40 DTF GENERATION                   
.LAB1    ANOP                                                                   
*                                                                               
         DC    A(0)                LAST RECORD ADDRESS                          
.*                                                                              
.LAB2    ANOP                                                                   
&I       SETA  0                                                                
&J       SETA  0                                                                
&K       SETA  0                                                                
         AIF   (T'&RECSIZE EQ 'O').LAB3                                         
         AIF   ('&RECSIZE'(1,1) NE '(').LAB2A                                   
&I       SETA  &RECSIZE(1)                                                      
&I       SETA  (&I+127)/128                                                     
         AGO   .LAB3                                                            
.LAB2A   ANOP                                                                   
&J       SETA  &RECSIZE                                                         
&K       SETA  32768                                                            
.*                                                                              
.LAB3    ANOP                                                                   
&A       SETC  '0'                                                              
&L       SETA  &BLKSIZE                                                         
         AIF   ('&OFF'(1,1) EQ 'N').LAB3A                                       
         AIF   (&L EQ 0).LAB3A                                                  
&A       SETC  '&FILE.A'                                                        
         AIF   (T'&IOAREA1 EQ 'O').LAB3A                                        
&A       SETC  '&IOAREA1'                                                       
         AIF   ('&IOAREA1'(1,1) NE '(').LAB3A                                   
&A       SETC  '&IOAREA1(1)'                                                    
         AIF   ('&A' NE 'AUTO').LAB3A                                           
&A       SETC  '0'                                                              
.LAB3A   ANOP                                                                   
         DC    AL1(&I)             MAX REC LEN MOD 128 FOR V/L FILE             
         DC    AL3(&A)             A(1ST CORE BUFFER)                           
         DC    A(0)                D/A BLOCK IN 1ST CORE BUFFER                 
.*                                                                              
.LAB4    ANOP                                                                   
&M       SETA  &J                                                               
         AIF   (&L EQ 0).LAB4A                                                  
&M       SETA  &L                                                               
         AIF   (&J EQ 0).LAB4A                                                  
         MNOTE 8,'INVALID RECSIZE FIELD'                                        
&I       SETA  (&J+127)/128                                                     
&J       SETA  0                                                                
&K       SETA  0                                                                
         ORG   *-8                                                              
         DC    AL1(&I),AL3(&A),A(0)                                             
.LAB4A   ANOP                                                                   
         DC    AL2(&K+&M)          BLOCK SIZE OR FIXED RECORD SIZE              
.*                                                                              
&I       SETA  0                                                                
         AIF   (T'&IOAREA2 EQ 'O').LAB4B                                        
&I       SETA  2                                                                
.LAB4B   ANOP                                                                   
         AIF   (T'&KEYLEN EQ 'O').LAB4C                                         
         AIF   (N'&KEYLEN NE 2).LAB4C                                           
&I       SETA  3                                                                
.LAB4C   ANOP                                                                   
         AIF   ('&VERMONT'(1,1) EQ 'N').LAB4D                                   
&I       SETA  &I+64               VERMONT  X'40'                               
         AIF   ('&VERMONT'(1,1) EQ 'Y').LAB4D                                   
         AIF   ('&VERMONT'(1,1) EQ 'E').LAB4CE                                  
         MNOTE 8,'INVALID VERMONT FIELD - N(O)/Y(ES)/E(XTENDED)'                
.LAB4CE  ANOP                                                                   
&I       SETA  &I+4                EXTENDED VERMONT  X'44'                      
.LAB4D   ANOP                                                                   
         AIF   ('&HIXT' NE 'Y').LAB4F                                           
&I       SETA  &I+8                HIGH CORE EXTENT MATRIX X'08'                
.LAB4F   ANOP                                                                   
         DC    AL1(&I)             DIND                                         
.*                                                                              
.LAB5    ANOP                                                                   
&M       SETA  &XNUM                                                            
         AIF   ('&HIXT' NE 'Y').LAB5A                                           
         DC    AL4(0)              A(HIGH CORE EXTENT MATRIX)                   
         DC    XL10'00'            SPARE                                        
         DC    X'FF',CL12'**HIGHCORE**',X'FF'                                   
         DC    14XL14'00',X'00'                                                 
         AGO   .LAB5B                                                           
                                                                                
.LAB5A   ANOP                                                                   
         AIF   ('&BIGDEMOS'(1,1) EQ 'N').LAB5AB                                 
&M       SETA  16                                                               
         DC    14X'FF'             BIGDEMOS DUMMY EXTENT MATRIX                 
         DC    X'00',CL12'BIGDEMOS=YES',X'00'                                   
         DC    14X'FF'                                                          
         DC    13XL14'00',X'00'                                                 
         AGO   .LAB5B                                                           
.*                                                                              
.LAB5AB  ANOP                                                                   
         DC    &M.XL14'00',X'00'   EXTENT MATRIX                                
.*                                                                              
.LAB5B   ANOP                                                                   
         AIF   (T'&KEYLEN EQ 'O' AND T'&IOAREA2 EQ 'O').LAB6                    
&A       SETC  '0'                                                              
         AIF   ('&OFF'(1,1) EQ 'N').LAB5C                                       
         AIF   ('&C' EQ 'Y' AND T'&IOAREA2 EQ 'O').LAB5C                        
&A       SETC  '&FILE.B'                                                        
         AIF   (T'&IOAREA2 EQ 'O' OR '&IOAREA2' EQ 'YES').LAB5C                 
&A       SETC  '&IOAREA2'                                                       
         AIF   ('&IOAREA2'(1,1) NE '(').LAB5C                                   
&A       SETC  '&IOAREA2(1)'                                                    
         AIF   ('&A' NE 'AUTO').LAB5C                                           
&A       SETC  '0'                                                              
.LAB5C   ANOP                                                                   
*                                                                               
         DC    A(&A)               A(2ND CORE BUFFER)                           
         DC    A(0)                D/A BLOCK IN 2ND CORE BUFFER                 
.*                                                                              
         AIF   (T'&KEYLEN EQ 'O').LAB6                                          
         AIF   (N'&KEYLEN EQ 2).LAB5E                                           
         AIF   (N'&KEYLEN NE 1).LAB5ERR                                         
         AIF   (T'&KEYLEN NE 'N').LAB5ERR                                       
         AIF   ('&C' EQ 'Y').LAB5CC                                             
         MNOTE 8,'MUST HAVE CMPRS IF KEYLEN SPECIFIED'                          
         MEXIT                                                                  
.*                                                                              
.LAB5CC  ANOP                                                                   
&I       SETA  0                                                                
         AIF   (T'&SPARE EQ 'O').LAB5D                                          
&I       SETA  &SPARE                                                           
         AIF   (T'&SPARE EQ 'N').LAB5D                                          
         MNOTE 8,'INVALID SPARE PARAMETER'                                      
         MEXIT                                                                  
.LAB5D   ANOP                                                                   
*                                  KEY COMPRESSED FILES EXTENSION AREA          
         DC    AL2(&KEYLEN)        KEY LENGTH FOR COMPRESSION                   
         DC    AL2(&I)             SPARE IN BLOCK FOR COMPRESSION               
         AGO   .LAB6                                                            
.LAB5E   ANOP                                                                   
*                                  DANDX FILES EXTENSION AREA                   
&Q       SETA  48                                                               
.LAB5F   ANOP                                                                   
         DC    A(0)                A(CURRENT INDEX ENTRY)                       
         DC    A(0)                A(CURRENT RECORD IN DATA BLOCK)              
         DC    &Q.A(0)             ONE DNEXT PER EXTENT MATRIX ENTRY            
         DC    &Q.H'0'             ONE DCOUNT PER EXTENT MATRIX ENTRY           
         AIF   (T'&KEYLEN(1) EQ 'N' AND T'&KEYLEN(2) EQ 'N').LAB5G              
.LAB5ERR MNOTE 8,'INVALID KEYLEN FIELD'                                         
         MEXIT                                                                  
.LAB5G   ANOP                                                                   
         DC    H'&KEYLEN(1)'       MAJOR KEY LENGTH (LEFT)                      
         DC    H'&KEYLEN(2)'       MINOR KEY LENGTH (RIGHT)                     
         DC    AL1(0)              RECORD NUMBER IN BLOCK (WAS +34)             
         DC    AL1(0)              EXTENT MATRIX NUMBER   (WAS +35)             
         DC    XL2'00'             N/D                                          
.*                                                                              
.LAB6    ANOP                                                                   
         AIF   ('&OFF'(1,1) EQ 'N').LAB7                                        
         AIF   ('&BLKSIZE' EQ '0').LAB7                                         
         AIF   (T'&IOAREA1 NE 'O').LAB7                                         
&FILE.A  DS    &BLKSIZE.C          CORE BUFFER                                  
.*                                                                              
.LAB7    ANOP                                                                   
         AIF   ('&OFF'(1,1) EQ 'N').LAB8                                        
         AIF   (T'&KEYLEN EQ 'O' AND T'&IOAREA2 EQ 'O').LAB8                    
         AIF   ('&C' EQ 'Y' AND T'&IOAREA2 EQ 'O').LAB8                         
         AIF   (T'&IOAREA2 NE 'O' AND '&IOAREA2(1)' NE 'YES').LAB8              
&FILE.B  DS    &BLKSIZE.C          2ND CORE BUFFER OR INDEX                     
.*                                                                              
.LAB8    ANOP                                                                   
         AIF   ('&VSAM'(1,1) EQ 'Y').LAB9                                       
*                                                                               
&DCBNAME DCB   DDNAME=&FILE,DSORG=DA,MACRF=(RI)                                 
         AGO   .LAB10                                                           
.*                                                                              
.LAB9    ANOP                                                                   
*                                                                               
&DCBNAME ACB   AM=VSAM,DDNAME=&FILE,MACRF=(DIR,SEQ)                             
.*                                                                              
.LAB10   ANOP                                                                   
         AIF   (T'&FILABL EQ 'O').LABA                                          
         ORG   &DCBNAME+40                                                      
         DC    CL8'&FILABL'                                                     
         ORG                                                                    
.*                                                                              
.LABA    ANOP                                                                   
         MEND                                                                   
