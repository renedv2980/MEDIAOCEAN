*          DATA SET MCSPEC     AT LEVEL 003 AS OF 09/08/13                      
         MACRO                                                                  
         SPEC  &LINE,&COL,&KEYLIT,&DDLEN,&DDTYP                                 
         GBLA  &KEYNUM,&KEYLEN,&LITLEN                                          
         GBLC  &LITVAL,&LITTYP                                                  
         LCLC  &A,&B,&C,&CVH(6)                                                 
         LCLA  &I,&J,&K                                                         
&CVH(1)  SETC  'A'                                                              
&CVH(2)  SETC  'B'                                                              
&CVH(3)  SETC  'C'                                                              
&CVH(4)  SETC  'D'                                                              
&CVH(5)  SETC  'E'                                                              
&CVH(6)  SETC  'F'                                                              
.*                                                                              
         AIF   (T'&LINE EQ 'O').MERR1                                           
         AIF   (K'&LINE GT 3).M21                                               
         AIF   ('&LINE' EQ 'END').M20                                           
         AIF   (K'&LINE LT 2).MERR2                                             
&B       SETC  '&LINE'(1,1)                                                     
.*                                                                              
&A       SETC  '1'                                                              
&J       SETA  16                                                               
         AIF   ('&B' EQ 'H').M4                                                 
&A       SETC  '2'                                                              
&J       SETA  16                                                               
         AIF   ('&B' EQ 'M').M4                                                 
&A       SETC  '3'                                                              
&J       SETA  16                                                               
         AIF   ('&B' EQ 'P').M4                                                 
&A       SETC  '4'                                                              
&J       SETA  16                                                               
         AIF   ('&B' EQ 'F').M4                                                 
         AGO   .MERR2                                                           
.*                                                                              
.M4      ANOP                                                                   
&B       SETC  '&LINE'(2,1)                                                     
         AIF   ('&LINE'(2,1) LT '1').MERR2                                      
         AIF   (K'&LINE EQ 2).M6                                                
&B       SETC  '&LINE'(2,2)                                                     
         AIF   ('&LINE'(3,1) LT '0').MERR2                                      
.M6      ANOP                                                                   
&I       SETA  &B                                                               
         AIF   (&I GT &J).MERR2                                                 
         AIF   (&I LE 9).M8                                                     
&B       SETC  '&CVH(&I-9)'                                                     
.*                                                                              
.M8      ANOP                                                                   
         AIF   (T'&COL EQ 'O').MERR3                                            
         AIF   (T'&COL NE 'N').MERR4                                            
&I       SETA  &COL                                                             
         AIF   (&I GT 165 OR &I EQ 0).MERR4                                     
.*                                                                              
         AIF   (T'&KEYLIT EQ 'O').MERR5                                         
         AIF   ('&KEYLIT'(K'&KEYLIT,1) EQ '''').M10                             
         AIF   ('&KEYLIT'(3,1) EQ '#').MDD                                      
         LIB   &KEYLIT                                                          
.*                                 GENERATE SPEC KEYWORD ELEMENT                
         DC    AL1(3)              SPEC KEYWORD ELEMENT CODE                    
         DC    AL1(5)              SPEC KEYWORD ELEMENT LENGTH                  
         DC    X'&A&B'             SPEC LINE TYPE/LINE NUMBER                   
         DC    AL1(&I)             SPEC COLUMN NUMBER                           
         DC    AL1(&KEYNUM)        SPEC KEYWORD NUMBER                          
         AGO   .MX                                                              
.*                                                                              
.M10     ANOP                                                                   
         LIT   &KEYLIT                                                          
&J       SETA  &LITLEN+4                                                        
.*                                 GENERATE SPEC LITERAL ELEMENT                
         DC    AL1(4)              SPEC LITERAL ELEMENT CODE                    
         DC    AL1(&J)             SPEC LITERAL ELEMENT LENGTH                  
         DC    X'&A&B'             SPEC LINE TYPE/LINE NUMBER                   
         DC    AL1(&I)             SPEC COLUMN NUMBER                           
         DC    &LITVAL                                                          
         AGO   .MX                                                              
.*                                                                              
.M20     ANOP                                                                   
         DC    AL1(0)              END OF SPEC POOL                             
         AGO   .MX                                                              
.*                                                                              
.M21     ANOP                                                                   
&I       SETA  0                                                                
         AIF   ('&LINE' NE 'HOOK').M22                                          
         AIF   (T'&COL EQ 'O').MERR8                                            
         AIF   (T'&COL NE 'N').MERR8                                            
&I       SETA  &COL                                                             
         AIF   (&I GT 16 OR &I EQ 0).MERR8                                      
         AGO   .M30                                                             
.*                                                                              
.M22     ANOP                                                                   
&I       SETA  17                                                               
         AGO   .MERR7                                                           
.*                                                                              
.M30     ANOP                      GENERATE SPEC ACTION ELEMENT                 
         DC    AL1(5)              SPEC ACTION ELEMENT CODE                     
         DC    AL1(3)              SPEC ACTION ELEMENT LENGTH                   
         DC    AL1(&I)             SPEC ACTION NUMBER/HOOK NUMBER               
         AGO   .MX                                                              
.*                                                                              
.MDD     ANOP                                                                   
         AIF   (T'&DDLEN EQ 'O' OR &DDLEN LT 2).MDDERR                          
         AIF   ('&DDLEN' EQ '2').MDD1                                           
&ML      SETA  &DDLEN+4                                                         
         AGO   .MDD2                                                            
.MDD1    ANOP                                                                   
&ML      SETA  7                                                                
.MDD2    ANOP                                                                   
         DC    AL1(04)                                                          
         DC    AL1(&ML)                                                         
         DC    X'&A&B'                                                          
         DC    AL1(&I)                                                          
         DCDD  &KEYLIT,&DDLEN,&DDTYP,PAD=BLANKS                                 
         AGO   .MX                                                              
.*                                                                              
.MERR1   MNOTE 8,'MISSING LINE PARAMETER'                                       
         MEXIT                                                                  
.MERR2   MNOTE 8,'INVALID LINE PARAMETER'                                       
         MEXIT                                                                  
.MERR3   MNOTE 8,'MISSING COLUMN PARAMETER'                                     
         MEXIT                                                                  
.MERR4   MNOTE 8,'INVALID COLUMN PARAMETER'                                     
         MEXIT                                                                  
.MERR5   MNOTE 8,'MISSING KEYWORD/LITERAL PARAMETER'                            
         MEXIT                                                                  
.MERR6   MNOTE 8,'ONLY ONE PARAMETER REQUIRED FOR ACTION'                       
         MEXIT                                                                  
.MERR7   MNOTE 8,'INVALID ACTION'                                               
         MEXIT                                                                  
.MERR8   MNOTE 8,'MISSING OR INVALID HOOK NUMBER'                               
         MEXIT                                                                  
.MDDERR  MNOTE 8,'MISSING OR INVALID DICTIONARY LENGTH'                         
         MEXIT                                                                  
.*                                                                              
.MX      ANOP                                                                   
         MEXIT                                                                  
         MEND                                                                   
