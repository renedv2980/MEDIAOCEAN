*          DATA SET MCDTYPES   AT LEVEL 026 AS OF 10/17/90                      
         MACRO                                                                  
&LABEL   DTYPE &DTYP,&REP=,&BASE=,&IRTN=,&ORTN=,&XRTN=,&NRTN=,         +        
               &IARGS=,&OARGS=,&XARGS=,&NARGS=                                  
.*                                                                              
         LCLC  &IROUT,&OROUT,&XROUT,&NROUT                                      
         LCLB  &IFOUND,&OFOUND,&XFOUND,&NFOUND                                  
.*                                                                              
         AIF   (T'&DTYP NE 'O').DTYPE                                           
         AIF   (T'&LABEL NE 'O').LABEL                                          
         MNOTE 4,'MACRO IGNORED -- ''DTYPE'' WITH NO PARAMETERS MUST HA+        
               VE A LABEL'                                                      
         MEXIT                                                                  
.LABEL   DS    0D                                                               
&LABEL   DS    XL64                                                             
         MEXIT                                                                  
.DTYPE   AIF   ((K'&DTYP EQ 5) AND ('&DTYP'(1,5) EQ 'START')).START             
         AGO   .GEN                                                             
.START   AIF   (T'&LABEL NE 'O').ALIGN                                          
         MNOTE 4,'MACRO IGNORED -- ''DTYPE START'' MUST HAVE A LABEL'           
         MEXIT                                                                  
.ALIGN   ANOP                                                                   
&LABEL   DS    0D                                                               
         MEXIT                                                                  
.*                                                                              
.GEN     AIF   (T'&LABEL EQ 'O').NOLABEL                                        
         MNOTE 4,'MACRO IGNORED -- NO LABEL PERMITTED'                          
         MEXIT                                                                  
.NOLABEL AIF   (K'&DTYP LE 7).DTYPEOK                                           
         MNOTE 4,'MACRO IGNORED -- DATATYPE EXCEEDS SEVEN CHARACTERS'           
         MEXIT                                                                  
.DTYPEOK DS    0D                                                               
         DC    A(0)                A(DATA WITHIN DATA TABLE)                    
         DC    CL7'&DTYP'          DATATYPE NAME                                
.*                                                                              
         AIF   (T'&REP EQ 'O').NOREP                                            
         AIF   ((&REP GE 1) AND (&REP LE 255)).REPOK                            
         MNOTE 4,'REP IGNORED -- MUST BE BETWEEN 1 AND 255'                     
.NOREP   DC    AL1(0)              NO REPETITION ID                             
         AGO   .SKIP10                                                          
.REPOK   DC    AL1(&REP)           REPETITION ID                                
.*                                                                              
.SKIP10  DC    XL8'00'             FILLED IN BY SCROLLER                        
.*                                                                              
&IROUT   SETC  'I&DTYP'                                                         
         AIF   (T'&IRTN EQ 'O').SKIP20                                          
         AIF   ((K'&IRTN EQ 4) AND ('&IRTN'(1,4) EQ 'NONE')).SKIP30             
&IROUT   SETC  '&IRTN'                                                          
.SKIP20  ANOP                                                                   
&IFOUND  SETB  1                                                                
         AIF   (T'&BASE NE 'O').IBASE                                           
         DC    A(&IROUT)           A(INPUT ROUTINE)                             
         AGO   .IARGS                                                           
.IBASE   DC    AL2(&IROUT-&BASE)   DISPLACEMENT TO INPUT ROUTINE                
         DC    AL2(0)              SPARE                                        
         AGO   .IARGS                                                           
.SKIP30  DC    A(0)                NO INPUT ROUTINE                             
.*                                                                              
.IARGS   AIF   (T'&IARGS EQ 'O').NOIARGS                                        
         AIF   (&IFOUND EQ 1).SKIP40                                            
         MNOTE 4,'IARGS INVALID WITHOUT INPUT ROUTINE'                          
         AGO   .NOIARGS                                                         
.SKIP40  AIF   (K'&IARGS EQ 8).IARGSOK                                          
         MNOTE 4,'IARGS IGNORED -- MUST BE EXACTLY EIGHT CHARACTERS'            
         AGO   .NOIARGS                                                         
.IARGSOK DC    XL4'&IARGS'         INPUT ARGUMENTS                              
         AGO   .OROUT                                                           
.NOIARGS DC    F'0'                NO INPUT ARGUMENTS                           
.*                                                                              
.OROUT   ANOP                                                                   
&OROUT   SETC  'O&DTYP'                                                         
         AIF   (T'&ORTN EQ 'O').SKIP70                                          
         AIF   ((K'&ORTN EQ 4) AND ('&ORTN'(1,4) EQ 'CHAR')).SKIP50             
         AGO   .SKIP60                                                          
.SKIP50  DC    C'CHAR'             KEYWORD -- SCROLLER ROUTINE 'CHAR'           
         AGO   .OARGS                                                           
.SKIP60  ANOP                                                                   
&OROUT   SETC  '&ORTN'                                                          
.SKIP70  ANOP                                                                   
&OFOUND  SETB  1                                                                
         AIF   (T'&BASE NE 'O').OBASE                                           
         DC    A(&OROUT)           A(OUTPUT ROUTINE)                            
         AGO   .OARGS                                                           
.OBASE   DC    AL2(&OROUT-&BASE)   DISPLACEMENT TO OUTPUT ROUTINE               
         DC    AL2(0)              SPARE                                        
.*                                                                              
.OARGS   AIF   (T'&OARGS EQ 'O').NOOARGS                                        
         AIF   (&OFOUND EQ 1).SKIP80                                            
         MNOTE 4,'OARGS INVALID WITH KEYWORD OUTPUT ROUTINE'                    
         AGO   .NOOARGS                                                         
.SKIP80  AIF   (K'&OARGS EQ 8).OARGSOK                                          
         MNOTE 4,'OARGS IGNORED -- MUST BE EXACTLY EIGHT CHARACTERS'            
         AGO   .NOOARGS                                                         
.OARGSOK DC    XL4'&OARGS'         OUTPUT ARGUMENTS                             
         AGO   .XROUT                                                           
.NOOARGS DC    F'0'                NO OUTPUT ARGUMENTS                          
.*                                                                              
.XROUT   ANOP                                                                   
&XROUT   SETC  'X&DTYP'                                                         
         AIF   (T'&XRTN EQ 'O').SKIP90                                          
         AIF   ((K'&XRTN EQ 4) AND ('&XRTN'(1,4) EQ 'NONE')).SKIP100            
&XROUT   SETC  '&XRTN'                                                          
.SKIP90  ANOP                                                                   
&XFOUND  SETB  1                                                                
         AIF   (T'&BASE NE 'O').XBASE                                           
         DC    A(&XROUT)           A(TRANSFER ROUTINE)                          
         AGO   .XARGS                                                           
.XBASE   DC    AL2(&XROUT-&BASE)   DISPLACEMENT TO TRANSFER ROUTINE             
         DC    AL2(0)              SPARE                                        
         AGO   .XARGS                                                           
.SKIP100 AIF   (&IFOUND EQ 1).SKIP110                                           
         MNOTE 4,'MUST HAVE EITHER INPUT OR TRANSFER ROUTINE'                   
.SKIP110 DC    A(0)                NO TRANSFER ROUTINE                          
.*                                                                              
.XARGS   AIF   (T'&XARGS EQ 'O').NOXARGS                                        
         AIF   (&XFOUND EQ 1).SKIP120                                           
         MNOTE 4,'XARGS INVALID WITHOUT TRANSFER ROUTINE'                       
         AGO   .NOXARGS                                                         
.SKIP120 AIF   (K'&XARGS EQ 8).XARGSOK                                          
         MNOTE 4,'XARGS IGNORED -- MUST BE EXACTLY EIGHT CHARACTERS'            
         AGO   .NOXARGS                                                         
.XARGSOK DC    XL4'&XARGS'         TRANSFER ARGUMENTS                           
         AGO   .NROUT                                                           
.NOXARGS DC    F'0'                NO TRANSFER ARGUMENTS                        
.*                                                                              
.NROUT   ANOP                                                                   
&NROUT   SETC  'N&DTYP'                                                         
         AIF   (T'&NRTN EQ 'O').SKIP130                                         
         AIF   ((K'&NRTN EQ 4) AND ('&NRTN'(1,4) EQ 'NONE')).SKIP140            
&NROUT   SETC  '&NRTN'                                                          
.SKIP130 ANOP                                                                   
&NFOUND  SETB  1                                                                
         AIF   (T'&BASE NE 'O').NBASE                                           
         DC    A(&NROUT)           A(NAME ROUTINE)                              
         AGO   .NARGS                                                           
.NBASE   DC    AL2(&NROUT-&BASE)   DISPLACEMENT TO NAME ROUTINE                 
         DC    AL2(0)              SPARE                                        
         AGO   .NARGS                                                           
.SKIP140 DC    A(0)                NO NAME ROUTINE                              
.*                                                                              
.NARGS   AIF   (T'&NARGS EQ 'O').NONARGS                                        
         AIF   (&NFOUND EQ 1).SKIP150                                           
         MNOTE 4,'NARGS INVALID WITHOUT NAME ROUTINE'                           
         AGO   .NONARGS                                                         
.SKIP150 AIF   (K'&NARGS EQ 8).NARGSOK                                          
         MNOTE 4,'NARGS IGNORED -- MUST BE EXACTLY EIGHT CHARACTERS'            
         AGO   .NONARGS                                                         
.NARGSOK DC    XL4'&NARGS'         NAME ARGUMENTS                               
         AGO   .SKIP160                                                         
.NONARGS DC    F'0'                NO NAME ARGUMENTS                            
.*                                                                              
.SKIP160 DC    XL12'00'            SPARE                                        
         MEND                                                                   
**PAN#1  CSECT                                                                  
         DC    CL21'026MCDTYPES  10/17/90'                                      
         END                                                                    
