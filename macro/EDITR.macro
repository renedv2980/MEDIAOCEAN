*          DATA SET MCEDITR    AT LEVEL 003 AS OF 05/04/16                      
         MACRO                                                                  
&LABEL   EDITR &INPUT,&OUTPUT,&DECS,&MINUS=,&CR=,&COMMAS=,&ALIGN=,     +        
               &TRAIL=,&ZERO=,&DROP=,                                  +        
               &WRK=WORK,&DUB=DUB,&FILL=,&FLOAT=,&BRACKET=,&INLINE=Y,  +        
               &IZERO=N                                                         
.*                                                                              
         LCLB  &IZEROY,&ZEROBL,&ZERONB,&MINUSY,&COMMAY,&CRY,&FLOATM             
         LCLA  &KA,&KB                                                          
         LCLA  &INX,&INL                                                        
         LCLC  &INA,&INB,&BLDSP                                                 
         LCLA  &POINT                                                           
         LCLC  &MASK                                                            
         LCLC  &CA,&CB,&DRC,&NODSP,&DR                                          
         LCLC  &SUBIN,&OUTLEN,&OUTDSP,&OUTBUF                                   
         LCLC  &EDITL0,&EDITL1                                                  
&EDITL0  SETC  'EDT0&SYSNDX'                                                    
&EDITL1  SETC  'EDT1&SYSNDX'                                                    
.*                                                                              
&IZEROY  SETB  ('&IZERO' EQ 'Y')                                                
&ZERONB  SETB  ('&ZERO' EQ 'NOBLANK')                                           
&ZEROBL  SETB  ('&ZERO' EQ 'BLANK')                                             
&FLOATM  SETB  ('&FLOAT' EQ '-')                                                
&MINUSY  SETB  ('&MINUS' EQ 'YES')                                              
&COMMAY  SETB  ('&COMMAS' EQ 'YES')                                             
&CRY     SETB  ('&CR' EQ 'YES')                                                 
.*                                                                              
         AIF   (T'&INPUT EQ 'O').ERROR1                                         
         AIF   (N'&INPUT EQ 2).EDITR09                                          
         AIF   (N'&INPUT NE 1).EROROR2                                          
.*                                 IS INPUT ALREADY IN REGISTER                 
         AIF   ('&INPUT'(1,1) NE '(').EDITR02                                   
         AIF   ('&INPUT(1)' EQ '0').EDITR01                                     
         AIF   ('&INPUT(1)' EQ 'R0').EDITR01                                    
&LABEL   LR    R0,&INPUT(1)                                                     
         AGO   .EDITR08                                                         
.*                                 INPUT IS IN R0                               
.EDITR01 ANOP  ,                                                                
&LABEL   CVD   R0,&DUB                                                          
         AGO   .EDITR30                                                         
.*                                 INPUT IS LENGTH-DEFINED PACKED               
.EDITR02 ANOP  ,                                                                
         AIF   (T'&INPUT NE 'P').EDITR03                                        
&LABEL   ZAP   &DUB,&INPUT                                                      
         AGO   .EDITR28                                                         
.*                                                                              
.EDITR03 ANOP  ,                                                                
         AIF   (L'&INPUT EQ 1).EDITR04                                          
         AIF   (L'&INPUT EQ 2).EDITR05                                          
         AIF   (L'&INPUT EQ 3).EDITR06                                          
         AIF   (L'&INPUT EQ 4).EDITR07                                          
         AGO    .ERROR3                                                         
.*                                 1 BYTE BINARY INPUT (UNSIGNED)               
.EDITR04 ANOP  ,                                                                
&LABEL   SR    R0,R0                                                            
         IC    R0,&INPUT                                                        
         AGO   .EDITR08                                                         
.*                                 2 BYTE BINARY INPUT (SIGNED)                 
.EDITR05 ANOP  ,                                                                
&LABEL   MVC   &DUB.(2),&INPUT                                                  
         LH    R0,&DUB                                                          
         AGO   .EDITR08                                                         
.*                                 3 BYTE BINARY INPUT (SIGNED)                 
.EDITR06 ANOP  ,                                                                
&LABEL   MVI   &DUB,0                                                           
         TM    &INPUT,X'80'                                                     
         JZ    *+8                                                              
         MVI   &DUB,X'FF'                                                       
         MVC   &DUB+1(3),&INPUT                                                 
         ICM   R0,15,&DUB                                                       
         AGO   .EDITR08                                                         
.*                                 4 BYTE BINARY INPUT (SIGNED)                 
.EDITR07 ANOP  ,                                                                
&LABEL   ICM   R0,15,&INPUT                                                     
.*                                 NOW CONVERT TO DECIMAL IN DUB                
.EDITR08 ANOP  ,                                                                
         CVD   R0,&DUB                                                          
         AGO   .EDITR30                                                         
.*                                 SUB-LIST INPUT                               
.EDITR09 ANOP  ,                                                                
&SUBIN   SETC  '&INPUT(2)'                                                      
         AIF   ('&INPUT(2)'(1,1) NE '(').EDITR10                                
&SUBIN   SETC  '0&INPUT(2)'                                                     
.*                                                                              
.EDITR10 ANOP  ,                                                                
         AIF   ('&INPUT(1)' EQ '1').EDITR11                                     
         AIF   ('&INPUT(1)' EQ '2').EDITR12                                     
         AIF   ('&INPUT(1)' EQ '3').EDITR13                                     
         AIF   ('&INPUT(1)' EQ '4').EDITR14                                     
         AIF   ('&INPUT(1)' EQ 'B1').EDITR11                                    
         AIF   ('&INPUT(1)' EQ 'B2').EDITR12                                    
         AIF   ('&INPUT(1)' EQ 'B3').EDITR13                                    
         AIF   ('&INPUT(1)' EQ 'B4').EDITR14                                    
         AIF   ('&INPUT(1)'(1,1) EQ 'P').EDITR15                                
         AIF   ('&INPUT(1)'(1,1) EQ 'C').EDITR15                                
         AIF   ('&INPUT(1)' EQ 'X').EDITR24                                     
         AGO   .ERROR4                                                          
.*                                 1 BYTE BINARY INPUT (UNSIGNED)               
.EDITR11 ANOP  ,                                                                
&LABEL   SR    R0,R0                                                            
         IC    R0,&SUBIN                                                        
         AGO   .EDITR08                                                         
.*                                 2 BYTE BINARY INPUT (SIGNED)                 
.EDITR12 ANOP  ,                                                                
&LABEL   MVC   &DUB.(2),&SUBIN                                                  
         LH    R0,&DUB                                                          
         AGO   .EDITR08                                                         
.*                                 3 BYTE BINARY INPUT (SIGNED)                 
.EDITR13 ANOP  ,                                                                
&LABEL   MVI   &DUB,0                                                           
         TM    &DUB+1,X'80'                                                     
         JZ    *+8                                                              
         MVI   &DUB,X'FF'                                                       
         MVC   &DUB+1(3),&SUBIN                                                 
         L     R0,&DUB                                                          
         AGO   .EDITR08                                                         
.*                                 4 BYTE BINARY INPUT (SIGNED)                 
.EDITR14 ANOP  ,                                                                
&LABEL   ICM   R0,15,&INPUT                                                     
         AGO   .EDITR08                                                         
.*                                 HANDLING NON-BINARY INPUT (SUB-LIST)         
.EDITR15 ANOP  ,                                                                
&INX     SETA  K'&INPUT(1)-1                                                    
&INA     SETC  '&INPUT(1)'(2,&INX)                                              
&INL     SETA  &INA                                                             
.*                                 SET UP FOR REGISTER EXPRESSIONS              
&INX     SETA  K'&INPUT(2)                                                      
         AIF   (K'&INPUT(2) EQ 1).EDITR20                                       
&KA      SETA  K'&INPUT(2)                                                      
.*                                 CHECK FOR ( IN INPUT EXPRESSIONS             
.EDITR16 ANOP  ,                                                                
         AIF   (&KA EQ 0).EDITR17                                               
         AIF   ('&INPUT(2)'(&KA,1) EQ '(').EDITR17                              
&KA      SETA  &KA-1                                                            
         AGO   .EDITR16                                                         
.*                                                                              
.EDITR17 ANOP  ,                                                                
         AIF   (&KA EQ 0).EDITR20                                               
&KB      SETA  K'&INPUT(2)-&KA                                                  
         AIF   (&KA EQ 1).EDITR18                                               
&INA     SETC  '&INPUT(2)'(1,&KA)                                               
         AGO   .EDITR19                                                         
.*                                                                              
.EDITR18 ANOP  ,                                                                
&INA     SETC  '0'.'&INPUT(2)'(&KA,1)                                           
.*                                                                              
.EDITR19 ANOP  ,                                                                
&INB     SETC  '&INPUT(2)'(&KA+1,&KB)                                           
         AGO   .EDITR22                                                         
.*                                 GENERATE INPUT INSTRUCTIONS                  
.EDITR20 ANOP  ,                                                                
         AIF   ('&INPUT(1)'(1,1) EQ 'P').EDITR21                                
&LABEL   PACK  &DUB,&INPUT(2).(&INL)                                            
         AGO   .EDITR28                                                         
.*                                                                              
.EDITR21 ANOP  ,                                                                
&LABEL   ZAP   &DUB,&INPUT(2).(&INL)                                            
         AGO   .EDITR28                                                         
.*                                                                              
.EDITR22 ANOP  ,                                                                
         AIF   ('&INPUT(1)'(1,1) EQ 'P').EDITR23                                
&LABEL   PACK  &DUB,&INA&INL,&INB                                               
         AGO   .EDITR28                                                         
.*                                                                              
.EDITR23 ANOP  ,                                                                
&LABEL   ZAP   &DUB,&INA&INL,&INB                                               
         AGO   .EDITR28                                                         
.*                                                                              
.EDITR24 ANOP  ,                                                                
&LABEL   CVDX  &DUB,&INPUT(2)                                                   
.*                                                                              
.EDITR28 ANOP  ,                                                                
         AIF   (NOT &ZEROBL AND NOT &FLOATM).EDITR30                            
         CVB   R0,&DUB                                                          
.*                                 HANDLING OUTPUT OPTIONS (NO DECS)            
.EDITR30 ANOP  ,                                                                
&POINT   SETA  17                                                               
         AIF   ('&DECS' GT '0').EDITR34                                         
         AIF   (&COMMAY).EDITR32                                                
         AIF   (&MINUSY).EDITR31                                                
&MASK    SETC  '4040404040402020202020202020202020'                             
         AIF   (NOT &ZERONB).EDITR50                                            
&MASK    SETC  '4040404040402020202020202020202120'                             
&POINT   SETA  16                                                               
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR31 ANOP  ,                                                                
&MASK    SETC  '4040404040202020202020202020202060'                             
         AIF   (NOT &ZERONB).EDITR50                                            
&MASK    SETC  '4040404040202020202020202020212060'                             
&POINT   SETA  15                                                               
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR32 ANOP  ,                                                                
         AIF   (&MINUSY).EDITR33                                                
&MASK    SETC  '40404020206B2020206B2020206B202020'                             
         AIF   (NOT &ZERONB).EDITR50                                            
&MASK    SETC  '40404020206B2020206B2020206B202120'                             
&POINT   SETA  16                                                               
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR33 ANOP  ,                                                                
&MASK    SETC  '404020206B2020206B2020206B20202060'                             
         AIF   (NOT &ZERONB).EDITR50                                            
&POINT   SETA  15                                                               
&MASK    SETC  '404020206B2020206B2020206B20212060'                             
         AGO   .EDITR50                                                         
.*                                 1 DECIMAL PLACE                              
.EDITR34 ANOP  ,                                                                
         AIF   ('&DECS' NE '1').EDITR36                                         
&POINT   SETA  15                                                               
         AIF   (&COMMAY).EDITR35                                                
&MASK    SETC  '4040404040202020202020202020214B20'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '4040404040202020202020202021204B20'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR35 ANOP  ,                                                                
&MASK    SETC  '4040206B2020206B2020206B2020214B20'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '4040206B2020206B2020206B2021204B20'                             
         AGO   .EDITR50                                                         
.*                                 2 DECIMAL PLACES                             
.EDITR36 ANOP  ,                                                                
         AIF   ('&DECS' NE '2').EDITR42                                         
         AIF   (&COMMAY).EDITR39                                                
         AIF   (&MINUSY).EDITR38                                                
         AIF   (&CRY).EDITR37                                                   
&POINT   SETA  14                                                               
&MASK    SETC  '40404040402020202020202020214B2020'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '40404040402020202020202021204B2020'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR37 ANOP  ,                                                                
&POINT   SETA  12                                                               
&MASK    SETC  '4040402020202020202020214B2020C3D9'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '4040402020202020202021204B2020C3D9'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR38 ANOP  ,                                                                
&POINT   SETA  13                                                               
&MASK    SETC  '404040402020202020202020214B202060'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '404040402020202020202021204B202060'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR39 ANOP  ,                                                                
         AIF   (&MINUSY).EDITR41                                                
         AIF   (&CRY).EDITR40                                                   
&POINT   SETA  14                                                               
&MASK    SETC  '4040402020206B2020206B2020214B2020'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '4040402020206B2020206B2021204B2020'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR40 ANOP  ,                                                                
&POINT   SETA  12                                                               
&MASK    SETC  '402020206B2020206B2020214B2020C3D9'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '402020206B2020206B2021204B2020C3D9'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR41 ANOP  ,                                                                
&POINT   SETA  13                                                               
&MASK    SETC  '40402020206B2020206B2020214B202060'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '40402020206B2020206B2021204B202060'                             
         AGO   .EDITR50                                                         
.*                                 MORE THAN 2 DEC PLACES                       
.EDITR42 ANOP  ,                                                                
         AIF   ('&DECS' NE '3').EDITR43                                         
&POINT   SETA  13                                                               
&MASK    SETC  '404040404020202020202020214B202020'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '404040404020202020202021204B202020'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR43 ANOP  ,                                                                
         AIF   ('&DECS' NE '4').EDITR45                                         
         AIF   (&MINUSY).EDITR44                                                
&POINT   SETA  12                                                               
&MASK    SETC  '4040404040202020202020214B20202020'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '4040404040202020202021204B20202020'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR44 ANOP  ,                                                                
&POINT   SETA  11                                                               
&MASK    SETC  '40404040202020202020214B2020202060'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '40404040202020202021204B2020202060'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR45 ANOP  ,                                                                
         AIF   ('&DECS' NE '5').EDITR46                                         
&POINT   SETA  11                                                               
&MASK    SETC  '40404040402020202020214B2020202020'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '40404040402020202021204B2020202020'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR46 ANOP  ,                                                                
         AIF   ('&DECS' NE '6').ERROR5                                          
&POINT   SETA  10                                                               
&MASK    SETC  '404040404020202020214B202020202020'                             
         AIF   (NOT &IZEROY).EDITR50                                            
&MASK    SETC  '404040404020202021204B202020202020'                             
         AGO   .EDITR50                                                         
.*                                                                              
.EDITR50 ANOP  ,                                                                
         AIF   ('&INLINE'(1,1) EQ 'N').EDITR51                                  
         BASR  R1,0                                                             
         J     &EDITL1                                                          
         DC    X'&MASK'                                                         
         DC    X'00'                                                            
&EDITL1  DS    0H                                                               
         MVC   &WRK.(17),4(R1)                                                  
         AGO   .EDITR52                                                         
.*                                                                              
.EDITR51 ANOP  ,                                                                
         MVC   &WRK.(17),=X'&MASK'                                              
.*                                                                              
.EDITR52 ANOP  ,                                                                
         AIF   (T'&FILL EQ 'O').EDITR53                                         
         AIF   (K'&FILL NE 1).ERROR6                                            
         MVI   &WRK,C'&FILL'                                                    
.*                                 FLOAT CHARACTER OPTION                       
.EDITR53 ANOP  ,                                                                
         AIF   (T'&FLOAT EQ 'O').EDITR55                                        
         LA    R1,&WRK+&POINT                                                   
         EDMK  &WRK.(17),&DUB+2                                                 
         BCTR  R1,0                                                             
         AIF   (NOT &FLOATM).EDITR54                                            
         LTR   R0,R0                                                            
         AIF   ('&DECS' EQ '0').EDITR5A                                         
         JNM   *+18                                                             
         CLI   1(R1),C'.'                                                       
         JNE   *+6                                                              
         BCTR  R1,0                                                             
         AGO   .EDITR54                                                         
.*                                                                              
.EDITR5A ANOP  ,                                                                
         JNM   *+8                                                              
.*                                                                              
.EDITR54 ANOP  ,                                                                
         MVI   0(R1),C'&FLOAT'                                                  
         AGO   .EDITR58                                                         
.*                                 STANDARD EDIT                                
.EDITR55 ANOP  ,                                                                
         ED    &WRK.(17),&DUB+2                                                 
.*                                 HANDLE ZERO=BLANK HERE                       
         AIF   (NOT &ZEROBL).EDITR56                                            
         LTR   R0,R0                                                            
         JNZ   *+14                                                             
         MVI   &WRK,C' '                                                        
         MVC   &WRK.+1(16),&WRK                                                 
.*                                 HANDLE TRAIL=C HERE                          
.EDITR56 ANOP  ,                                                                
         AIF   (T'&TRAIL EQ 'O').EDITR57                                        
         CLI   &WRK,C' '                                                        
         JNE   *+10                                                             
         CLC   &WRK.+1(16),&WRK                                                 
         JE    *+14                                                             
         MVC   &WRK.(16),&WRK.+1                                                
         MVI   &WRK.+16,C'&TRAIL'                                               
.*                                 HANDLE DROP=N HERE                           
.EDITR57 ANOP  ,                                                                
         AIF   (T'&DROP EQ 'O').EDITR58                                         
         LA    R1,&WRK+16                                                       
         LHI   R0,&DROP                                                         
         CLI   0(R1),C'0'                                                       
         JNE   *+14                                                             
         MVI   0(R1),C' '                                                       
         BCTR  R1,0                                                             
         BRCT  R0,*-14                                                          
.*                                 NOW HANDLE OUTPUT OPTIONS                    
.EDITR58 ANOP  ,                                                                
         AIF   (N'&OUTPUT EQ 2).EDITR59                                         
         AIF   (T'&OUTPUT EQ 'O').ERROR7                                        
         AGO   .EDITR60                                                         
.*                                                                              
.EDITR59 ANOP  ,                                                                
         AIF   (T'&OUTPUT(1) EQ 'O').EDITR61                                    
&OUTLEN  SETC  '&OUTPUT(1)'                                                     
&OUTBUF  SETC  '&OUTPUT(2)'                                                     
         AGO   .EDITR62                                                         
.*                                                                              
.EDITR60 ANOP  ,                                                                
&OUTLEN  SETC  'L''&OUTPUT'                                                     
&OUTBUF  SETC  '&OUTPUT'                                                        
         AGO   .EDITR62                                                         
.*                                                                              
.EDITR61 ANOP  ,                                                                
&OUTLEN  SETC  'L''&OUTPUT(2)'                                                  
&OUTBUF  SETC  '&OUTPUT(2)'                                                     
.*                                                                              
.EDITR62 ANOP  ,                                                                
&OUTDSP  SETC  '17-(&OUTLEN.)'                                                  
.*                                 CHECK FOR ( IN OUTPUT EXPRESSIONS            
&KA      SETA  K'&OUTBUF                                                        
.*                                                                              
.EDITR63 ANOP  ,                                                                
         AIF   (&KA EQ 0).EDITR64                                               
         AIF   ('&OUTBUF'(&KA,1) EQ '(').EDITR64                                
&KA      SETA  &KA-1                                                            
         AGO   .EDITR63                                                         
.*                                                                              
.EDITR64 ANOP  ,                                                                
         AIF   (&KA GT 0).EDITR65                                               
         MVC   &OUTBUF.(&OUTLEN),&WRK+&OUTDSP                                   
         AGO   .EDITR67                                                         
.*                                                                              
.EDITR65 ANOP  ,                                                                
&KB      SETA  K'&OUTBUF-&KA       WE CAN NOW SPLIT EXPRESSION                  
&CA      SETC  '&OUTBUF'(1,&KA)                                                 
&CB      SETC  '&OUTBUF'(&KA+1,&KB)                                             
         AIF   (&KA NE 1).EDITR66                                               
         MVC   0&CA&OUTLEN,&CB,&WRK+&OUTDSP                                     
         AGO   .EDITR67                                                         
.*                                                                              
.EDITR66 ANOP  ,                                                                
         MVC   &CA&OUTLEN,&CB,&WRK+&OUTDSP                                      
.*                                 OUTPUT MOVED - CHECK EDIT ALIGNMENT          
.EDITR67 ANOP  ,                                                                
         AIF   (T'&BRACKET EQ 'O').EDITR68                                      
.*                                 BRACKET OPTION                               
         LA    R1,&OUTBUF                                                       
         LHI   R0,&OUTLEN                                                       
         CLI   0(R1),C' '                                                       
         JNE   *+16                                                             
         AHI   R1,1                                                             
         BRCT  R0,*-12                                                          
         J     &EDITL0                                                          
         BCTR  R1,0                                                             
         MVI   0(R1),C'('                                                       
         AHI   R1,2                                                             
         CLI   0(R1),C' '                                                       
         JE    *+12                                                             
         AHI   R1,1                                                             
         BRCT  R0,*-12                                                          
         MVI   0(R1),C')'                                                       
&EDITL0  DS    0H                                                               
.*                                                                              
.EDITR68 ANOP  ,                                                                
         AIF   (T'&ALIGN EQ 'O').EDITRX                                         
         AIF   ('&ALIGN'(1,1) NE 'L').EDITRX                                    
.*                                 NON REGISTER EXPRESSIONS                     
         LHI   R0,&OUTLEN                                                       
         AIF   (&KA EQ 1).EDITR69                                               
         CLI   &OUTBUF,C' '                                                     
         JNE   *+18                                                             
         AIF   (&KA NE 0).EDITR69                                               
         MVC   &OUTBUF.(&OUTLEN-1),&OUTBUF+1                                    
         MVI   &OUTBUF+&OUTLEN-1,C' '                                           
         BRCT  R0,*-18                                                          
         AGO   .EDITRX                                                          
.*                                 NOW GENERATE FOR REGISTER EXPRESS.           
.EDITR69 ANOP  ,                                                                
         AIF   (&KA NE 1).EDITR70                                               
         CLI   0&OUTBUF,C' '                                                    
         JNE   *+18                                                             
         MVC   0&CA&OUTLEN-1,&CB,1&OUTBUF                                       
         MVI   &OUTLEN-1&OUTBUF,C' '                                            
         BRCT  R0,*-18                                                          
         AGO   .EDITRX                                                          
.*                                                                              
.EDITR70 ANOP  ,                                                                
&DRC     SETC  '&OUTBUF'(1,&KA-1)                                               
&DR      SETC  '&DRC.+1'                                                        
&NODSP   SETC  '&OUTBUF'(&KA,&KB+1)                                             
&BLDSP   SETC  '&DRC.-1+&OUTLEN'                                                
.*                                                                              
         MVC   &CA&OUTLEN,&CB,&DR&NODSP                                         
         MVI   &BLDSP&NODSP,C' '                                                
         BRCT  R0,*-18                                                          
.*                                                                              
.EDITRX  ANOP  ,                                                                
         MEXIT                                                                  
.*                                                                              
.ERROR1  ANOP  ,                                                                
         MNOTE 8,'Missing input expression'                                     
         MEXIT                                                                  
.*                                                                              
.ERROR2  ANOP  ,                                                                
         MNOTE 8,'Too many input terms'                                         
         MEXIT                                                                  
.*                                                                              
.ERROR3  ANOP  ,                                                                
         MNOTE 8,'Input length incorrect'                                       
         MEXIT                                                                  
.*                                                                              
.ERROR4  ANOP  ,                                                                
         MNOTE 8,'Sub-list input length not 1-4'                                
         MEXIT                                                                  
.*                                                                              
.ERROR5  ANOP  ,                                                                
         MNOTE 8,'Too many decimal points'                                      
         MEXIT                                                                  
.*                                                                              
.ERROR6  ANOP  ,                                                                
         MNOTE 8,'Fill character must be one byte'                              
         MEXIT                                                                  
.*                                                                              
.ERROR7  ANOP  ,                                                                
         MNOTE 8,'No output expression given'                                   
         MEXIT                                                                  
.*                                                                              
         MEND                                                                   
