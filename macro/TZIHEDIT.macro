*          DATA SET TZIHEDIT   AT LEVEL 006 AS OF 01/09/96                      
         MACRO                                                                  
&TAG     EDIT  &INPUT,&OUTPUT,&DECS,&MINUS=,&CR=,&COMMAS=,&ALIGN=,     X        
               &TRAIL=,&ZERO=,&DROP=,                                  X        
               &WRK=WORK,&DUB=DUB,&FILL=,&FLOAT=,&BRACKET=                      
.*                                                                              
.*                                 GET INPUT INTO R0                            
         LCLA  &KA,&KB                                                          
         LCLA  &INX,&INL                                                        
         LCLC  &INA,&INB,&BLDSP                                                 
         LCLA  &POINT                                                           
         LCLC  &CA,&CB,&DRC,&NODSP,&DR                                          
         LCLC  &SUBIN,&OUTLEN,&OUTDSP,&OUTBUF                                   
         AIF   (T'&INPUT NE 'O').INPT2                                          
         MNOTE 8,'MISSING INPUT EXPRESSION'                                     
         AGO   .OUT2                                                            
.*                                                                              
.INPT2   AIF   (N'&INPUT EQ 2).SUBINPT                                          
         AIF   (N'&INPUT EQ 1).INPT5                                            
         MNOTE 0,'TOO MANY INPUT TERMS'                                         
         AGO   .OUT2                                                            
.*                                                                              
.*                                 IS INPUT ALREADY IN REGISTER                 
.INPT5   AIF   ('&INPUT'(1,1) NE '(').INPT8                                     
         AIF   ('&INPUT(1)' EQ '0').INPT6                                       
         AIF   ('&INPUT(1)' EQ 'R0').INPT6                                      
&TAG     LR    R0,&INPUT(1)                                                     
         AGO   .INPT20                                                          
.*                                 ITS IN R0 - DONT GENERATE INSTRUCT.          
.INPT6   ANOP                                                                   
&TAG     DS    0H                                                               
         AGO   .INPT20                                                          
.*                                 INPUT IS LENGTH-DEFINED PACKED               
.INPT8   AIF   (T'&INPUT NE 'P').INPT10                                         
&TAG     ZAP   &DUB,&INPUT                                                      
         AGO   .OUT2                                                            
.*                                                                              
.INPT10  AIF   (L'&INPUT EQ 1).INPT12                                           
         AIF   (L'&INPUT EQ 2).INPT14                                           
         AIF   (L'&INPUT EQ 3).INPT16                                           
         AIF   (L'&INPUT EQ 4).INPT18                                           
         MNOTE 0,'INPUT LENGTH INCORRECT'                                       
         AGO   .OUT2                                                            
.*                                                                              
.*                                 1 BYTE INPUT                                 
.INPT12  ANOP                                                                   
&TAG     SR    R0,R0                                                            
         IC    R0,&INPUT                                                        
         AGO   .INPT20                                                          
.*                                                                              
.*                                 2 BYTE INPUT                                 
.INPT14  ANOP                                                                   
&TAG     MVC   &DUB.(2),&INPUT                                                  
         LH    R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                 3 BYTE INPUT                                 
.INPT16  ANOP                                                                   
&TAG     MVC   &DUB+1(3),&INPUT                                                 
         MVI   &DUB,0                                                           
         TM    &DUB+1,X'80'                                                     
         BZ    *+8                                                              
         MVI   &DUB,X'FF'                                                       
         L     R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 4 BYTE INPUT                                 
.INPT18  ANOP                                                                   
&TAG     MVC   &DUB.(4),&INPUT                                                  
         L     R0,&DUB                                                          
.*                                 NOW CVD INTO DUB                             
.INPT20  CVD   R0,&DUB                                                          
         AGO   .OUT2                                                            
.*                                                                              
.*                                 SUB-LIST INPUT                               
.SUBINPT ANOP                                                                   
         AIF   ('&INPUT(1)' EQ 'TIME').TIME                                     
         AIF   ('&INPUT(1)' EQ 'TUS').TUS                                       
&SUBIN   SETC  '&INPUT(2)'                                                      
         AIF   ('&INPUT(2)'(1,1) NE '(').SUB                                    
&SUBIN   SETC  '0&INPUT(2)'                                                     
.SUB     AIF   ('&INPUT(1)' EQ '1').SUB1                                        
         AIF   ('&INPUT(1)' EQ '2').SUB2                                        
         AIF   ('&INPUT(1)' EQ '3').SUB3                                        
         AIF   ('&INPUT(1)' EQ '4').SUB4                                        
         AIF   ('&INPUT(1)' EQ 'B1').SUB1                                       
         AIF   ('&INPUT(1)' EQ 'B2').SUB2                                       
         AIF   ('&INPUT(1)' EQ 'B3').SUB3                                       
         AIF   ('&INPUT(1)' EQ 'B4').SUB4                                       
         AIF   ('&INPUT(1)'(1,1) EQ 'P').SUB6                                   
         AIF   ('&INPUT(1)'(1,1) EQ 'C').SUB6                                   
         AIF   ('&INPUT(1)' EQ 'X').SUB10                                       
         MNOTE 0,'SUB-LIST INPUT LENGTH NOT 1-4'                                
         AGO   .OUT2                                                            
.*                                                                              
.*                                 1 BYTE INPUT (SUBLIST)                       
.SUB1    ANOP                                                                   
&TAG     SR    R0,R0                                                            
         IC    R0,&SUBIN                                                        
         AGO   .INPT20                                                          
.*                                                                              
.*                                 2 BYTE INPUT (SUB-LIST)                      
.SUB2    ANOP                                                                   
&TAG     MVC   &DUB.(2),&SUBIN                                                  
         LH    R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 3 BYTE INPUT (SUB-LIST)                      
.SUB3    ANOP                                                                   
&TAG     MVC   &DUB+1(3),&SUBIN                                                 
         MVI   &DUB,0                                                           
         TM    &DUB+1,X'80'                                                     
         BZ    *+8                                                              
         MVI   &DUB,X'FF'                                                       
         L     R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 4 BYTE INPUT (SUB-LIST)                      
.SUB4    ANOP                                                                   
&TAG     MVC   &DUB.(4),&SUBIN                                                  
         L     R0,&DUB                                                          
         AGO   .INPT20                                                          
.*                                                                              
.*                                 HANDLING NON-BINARY INPUT (SUB-LIST)         
.*                                                                              
.SUB6    ANOP                                                                   
&INX     SETA  K'&INPUT(1)-1                                                    
&INA     SETC  '&INPUT(1)'(2,&INX)                                              
&INL     SETA  &INA                                                             
.*                                 SET UP FOR REGISTER EXPRESSIONS              
&INX     SETA  K'&INPUT(2)                                                      
         AIF   (K'&INPUT(2) EQ 1).SUB6A                                         
.*                                                                              
.*                                 CHECK FOR ( IN INPUT EXPRESSIONS             
.*                                                                              
&KA      SETA  K'&INPUT(2)                                                      
.FNDLPI  AIF   (&KA EQ 0).FOUNDI                                                
         AIF   ('&INPUT(2)'(&KA,1) EQ '(').FOUNDI                               
&KA      SETA  &KA-1                                                            
         AGO   .FNDLPI                                                          
.*                                                                              
.FOUNDI  AIF   (&KA EQ 0).SUB6A                                                 
.*                                                                              
&KB      SETA  K'&INPUT(2)-&KA                                                  
         AIF   (&KA EQ 1).FNDI2                                                 
&INA     SETC  '&INPUT(2)'(1,&KA)                                               
         AGO   .FNDI3                                                           
.FNDI2   ANOP                                                                   
&INA     SETC  '0'.'&INPUT(2)'(&KA,1)                                           
.FNDI3   ANOP                                                                   
&INB     SETC  '&INPUT(2)'(&KA+1,&KB)                                           
.*                                                                              
         AGO   .SUB8                                                            
.*                                                                              
.*                                 GENERATE INPUT INSTRUCTIONS                  
.*                                                                              
.SUB6A   ANOP                                                                   
         AIF   ('&INPUT(1)'(1,1) EQ 'P').SUB7                                   
&TAG     PACK  &DUB,&INPUT(2).(&INL)                                            
         AGO   .OUT2                                                            
.*                                                                              
.SUB7    ANOP                                                                   
&TAG     ZAP   &DUB,&INPUT(2).(&INL)                                            
         AGO   .OUT2                                                            
.*                                                                              
.SUB8    AIF   ('&INPUT(1)'(1,1) EQ 'P').SUB9                                   
&TAG     PACK  &DUB,&INA&INL,&INB                                               
         AGO   .OUT2                                                            
.*                                                                              
.SUB9    ANOP                                                                   
&TAG     ZAP   &DUB,&INA&INL,&INB                                               
         AGO   .OUT2                                                            
.SUB10   ANOP                                                                   
&TAG     CVDX  &DUB,&INPUT(2)                                                   
         AGO   .OUT2                                                            
.*                                                                              
.*                                 HANDLE TIME EXPRESSIONS HERE                 
.*                                                                              
.TIME    ANOP                                                                   
         AIF   ('&INPUT(2)' EQ 'NOW').TIME2                                     
&TAG     MVC   &DUB.(4),&INPUT(2)                                               
         AGO   .TIME4                                                           
.*                                                                              
.TIME2   ANOP                                                                   
&TAG     THMS                                                                   
         ST    R1,&DUB                                                          
.*                                                                              
.TIME4   ANOP                                                                   
         MVC   &WRK.(9),=CL9' '                                                 
         UNPK  &WRK+11(6),&DUB.(4)                                              
         OI    &WRK+16,X'F0'                                                    
         MVC   &WRK+9(2),&WRK+11                                                
         MVC   &WRK+12(2),&WRK+13                                               
         MVI   &WRK+11,C'.'                                                     
         MVI   &WRK+14,C'.'                                                     
         AGO   .EDEND                                                           
.*                                                                              
.*                                 TUS EDITING                                  
.*                                                                              
.TUS     ANOP                                                                   
         AIF   ('&INPUT(2)'(1,1) EQ '(').TUS2                                   
&TAG     MVC   &DUB,&INPUT(2)                                                   
         L     R1,&DUB                                                          
         AGO   .TUS4                                                            
.TUS2    ANOP                                                                   
&TAG     LR    R1,&INPUT(2)                                                     
.TUS4    ANOP                                                                   
         SR    R0,R0                                                            
         D     R0,=F'300'                                                       
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         CVD   R0,&DUB                                                          
         MVC   &WRK.(17),=CL17'         HH.MM.SS'                               
         UNPK  &WRK+15(2),&DUB                                                  
         OI    &WRK+16,X'F0'                                                    
         SR    R0,R0                                                            
         D     R0,=F'60'                                                        
         CVD   R0,&DUB                                                          
         UNPK  &WRK+12(2),&DUB                                                  
         OI    &WRK+13,X'F0'                                                    
         CVD   R1,&DUB                                                          
         UNPK  &WRK+9(2),&DUB                                                   
         OI    &WRK+10,X'F0'                                                    
         AGO   .EDEND                                                           
.*                                                                              
.*                                 HANDLING OUTPUT OPTIONS (NO DECS)            
.*                                                                              
.OUT2    ANOP                                                                   
.OUT3    ANOP                                                                   
&POINT   SETA  17                                                               
         AIF   ('&DECS' GT '0').OUT10                                           
         AIF   ('&COMMAS' EQ 'YES').OUT6                                        
         AIF   ('&MINUS' EQ 'YES').OUT4                                         
         MVC   &WRK.(17),=X'4040404040402020202020202020202020'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+15,X'21'                                                    
&POINT   SETA  16                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT4    MVC   &WRK.(17),=X'4040404040202020202020202020202060'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+14,X'21'                                                    
&POINT   SETA  15                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT6    AIF   ('&MINUS' EQ 'YES').OUT8                                         
         MVC   &WRK.(17),=X'40404020206B2020206B2020206B202020'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+15,X'21'                                                    
&POINT   SETA  16                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT8    MVC   &WRK.(17),=X'404020206B2020206B2020206B20202060'                 
         AIF   ('&ZERO' NE 'NOBLANK').OUTEND                                    
         MVI   &WRK+14,X'21'                                                    
&POINT   SETA  15                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT10   AIF   ('&DECS' NE '1').OUT14                                           
.*                                                                              
.*                                 1 DECIMAL PLACE                              
&POINT   SETA  15                                                               
         AIF   ('&COMMAS' EQ 'YES').OUT12                                       
         MVC   &WRK.(17),=X'4040404040202020202020202020214B20'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT12   MVC   &WRK.(17),=X'4040206B2020206B2020206B2020214B20'                 
         AGO   .OUTEND                                                          
.*                                                                              
.*                                 2 DECIMAL PLACES                             
.OUT14   AIF   ('&DECS' NE '2').OUT26                                           
         AIF   ('&COMMAS' EQ 'YES').OUT20                                       
         AIF   ('&MINUS' EQ 'YES').OUT18                                        
         AIF   ('&CR' EQ 'YES').OUT16                                           
&POINT   SETA  14                                                               
         MVC   &WRK.(17),=X'40404040402020202020202020214B2020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT16   MVC   &WRK.(17),=X'4040402020202020202020214B2020C3D9'                 
&POINT   SETA  12                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT18   MVC   &WRK.(17),=X'404040402020202020202020214B202060'                 
&POINT   SETA  13                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT20   AIF   ('&MINUS' EQ 'YES').OUT24                                        
         AIF   ('&CR' EQ 'YES').OUT22                                           
&POINT   SETA  14                                                               
         MVC   &WRK.(17),=X'4040402020206B2020206B2020214B2020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT22   MVC   &WRK.(17),=X'402020206B2020206B2020214B2020C3D9'                 
&POINT   SETA  12                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT24   MVC   &WRK.(17),=X'40402020206B2020206B2020214B202060'                 
&POINT   SETA  13                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.*                                 MORE THAN 2 DEC PLACES                       
.OUT26   AIF   ('&DECS' NE '3').OUT28                                           
&POINT   SETA  13                                                               
         MVC   &WRK.(17),=X'404040404020202020202020214B202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT28   AIF   ('&DECS' NE '4').OUT30                                           
         AIF   ('&MINUS' EQ 'YES').OUT29                                        
&POINT   SETA  12                                                               
         MVC   &WRK.(17),=X'4040404040202020202020214B20202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT29   MVC   &WRK.(17),=X'40404040202020202020214B2020202060'                 
&POINT   SETA  11                                                               
         AGO   .OUTEND                                                          
.*                                                                              
.OUT30   AIF   ('&DECS' NE '5').OUT32                                           
&POINT   SETA  11                                                               
         MVC   &WRK.(17),=X'40404040402020202020214B2020202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT32   AIF   ('&DECS' NE '6').OUT34                                           
&POINT   SETA  10                                                               
         MVC   &WRK.(17),=X'404040404020202020214B202020202020'                 
         AGO   .OUTEND                                                          
.*                                                                              
.OUT34   MNOTE 0,'TOO MANY DECIMAL POINTS'                                      
.OUTEND  AIF   (T'&FILL EQ 'O').ED4                                             
         AIF   (K'&FILL EQ 1).ED2                                               
         MNOTE 0,'FILL CHARACTER MUST BE ONE BYTE'                              
         AGO   .ED4                                                             
.*                                 FILL CHARACTER OPTION                        
.ED2     MVI   &WRK,C'&FILL'                                                    
.*                                                                              
.*                                 FLOAT CHARACTER OPTION                       
.ED4     AIF   (T'&FLOAT EQ 'O').ED6                                            
         LA    R1,&WRK+&POINT                                                   
         EDMK  &WRK.(17),&DUB+2                                                 
         BCTR  R1,R0                                                            
         AIF   ('&FLOAT' NE '-').ED5                                            
         CP    &DUB,=P'0'                                                       
         BNL   *+8                                                              
.ED5     ANOP                                                                   
         MVI   0(R1),C'&FLOAT'                                                  
         AGO   .EDEND                                                           
.*                                                                              
.*                                 STANDARD EDIT                                
.ED6     ED    &WRK.(17),&DUB+2                                                 
.*                                                                              
.*                                 HANDLE ZERO=BLANK HERE                       
.*                                                                              
         AIF   ('&ZERO' NE 'BLANK').ED8                                         
         CP    &DUB,=P'0'                                                       
         BNE   *+10                                                             
         MVC   &WRK.(17),=CL17' '                                               
.*                                                                              
.*                                 HANDLE TRAIL=C HERE                          
.*                                                                              
.ED8     AIF   (T'&TRAIL EQ 'O').ED10                                           
         CLC   &WRK.(17),=CL17' '                                               
         BE    *+14                                                             
         MVC   &WRK.(16),&WRK+1                                                 
         MVI   &WRK+16,&TRAIL                                                   
.*                                                                              
.*                                 HANDLE DROP=N HERE                           
.*                                                                              
.ED10    AIF   (T'&DROP EQ 'O').ED12                                            
         LA    R1,&WRK+16                                                       
         LA    R0,&DROP                                                         
         CLI   0(R1),C'0'                                                       
         BNE   *+14                                                             
         MVI   0(R1),C' '                                                       
         BCTR  R1,0                                                             
         BCT   R0,*-14                                                          
.*                                                                              
.ED12    ANOP                                                                   
.*                                                                              
.*                                 NOW HANDLE OUTPUT OPTIONS                    
.*                                                                              
.EDEND   ANOP                                                                   
         AIF   (N'&OUTPUT EQ 2).OUT36                                           
         AIF   (T'&OUTPUT EQ 'O').OUT35                                         
         AGO   .OUT38                                                           
.*                                                                              
.OUT35   ANOP                                                                   
         MNOTE 0,'NO OUTPUT EXPRESSION GIVEN'                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.OUT36   AIF   (T'&OUTPUT(1) EQ 'O').OUT39                                      
&OUTLEN  SETC  '&OUTPUT(1)'                                                     
&OUTBUF  SETC  '&OUTPUT(2)'                                                     
         AGO   .OUT40                                                           
.*                                                                              
.OUT38   ANOP                                                                   
&OUTLEN  SETC  'L''&OUTPUT'                                                     
&OUTBUF  SETC  '&OUTPUT'                                                        
         AGO   .OUT40                                                           
.*                                                                              
.OUT39   ANOP                                                                   
&OUTLEN  SETC  'L''&OUTPUT(2)'                                                  
&OUTBUF  SETC  '&OUTPUT(2)'                                                     
.*                                                                              
.OUT40   ANOP                                                                   
&OUTDSP  SETC  '17-(&OUTLEN.)'                                                  
.*                                                                              
.*                                 CHECK FOR ( IN OUTPUT EXPRESSIONS            
.*                                                                              
&KA      SETA  K'&OUTBUF                                                        
.FNDLPJ  AIF   (&KA EQ 0).FOUNDJ                                                
         AIF   ('&OUTBUF'(&KA,1) EQ '(').FOUNDJ                                 
&KA      SETA  &KA-1                                                            
         AGO   .FNDLPJ                                                          
.*                                                                              
.FOUNDJ  AIF   (&KA GT 0).BRACALL                                               
.*                                                                              
         MVC   &OUTBUF.(&OUTLEN),&WRK+&OUTDSP                                   
         AGO   .LEFT                                                            
.*                                                                              
.BRACALL ANOP                                                                   
&KB      SETA  K'&OUTBUF-&KA                                                    
.*                                                                              
.*                                 WE CAN NOW SPLIT EXPRESSION                  
.*                                                                              
&CA      SETC  '&OUTBUF'(1,&KA)                                                 
&CB      SETC  '&OUTBUF'(&KA+1,&KB)                                             
         AIF   (&KA NE 1).NOZERO                                                
         MVC   0&CA&OUTLEN,&CB,&WRK+&OUTDSP                                     
         AGO   .LEFT                                                            
.NOZERO  ANOP                                                                   
         MVC   &CA&OUTLEN,&CB,&WRK+&OUTDSP                                      
.*                                                                              
.*                                 OUTPUT MOVED - CHECK LEFT ALIGNMENT          
.*                                                                              
.LEFT    AIF   (T'&BRACKET EQ 'O').LEFT1                                        
.*                                                                              
.*                                           BRACKET OPTION                     
.*                                                                              
         LA    R1,&OUTBUF                                                       
         LA    R0,&OUTLEN                                                       
         CLI   0(R1),C' '                                                       
         BNE   *+16                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,*-12                                                          
         B     *+34                                                             
         BCTR  R1,0                                                             
         MVI   0(R1),C'('                                                       
         LA    R1,2(R1)                                                         
         CLI   0(R1),C' '                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,*-12                                                          
         MVI   0(R1),C')'                                                       
.*                                                                              
.*                                                                              
.LEFT1   AIF   (T'&ALIGN EQ 'O').MACEND                                         
         AIF   ('&ALIGN'(1,1) NE 'L').MACEND                                    
.*                                                                              
.*                                 NON REGISTER EXPRESSIONS                     
         LA    R0,&OUTLEN                                                       
         AIF   (&KA EQ 1).LEFT2                                                 
         CLI   &OUTBUF,C' '                                                     
         BNE   *+18                                                             
         AIF   (&KA NE 0).LEFT2                                                 
         MVC   &OUTBUF.(&OUTLEN),&OUTBUF+1                                      
         MVI   &OUTBUF+&OUTLEN-1,C' '                                           
         BCT   R0,*-18                                                          
.MACEND  ANOP                                                                   
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.*                                 NOW GENERATE FOR REGISTER EXPRESS.           
.*                                                                              
.LEFT2   AIF   (&KA NE 1).LEFT4                                                 
         CLI   0&OUTBUF,C' '                                                    
         BNE   *+18                                                             
         MVC   0&CA&OUTLEN,&CB,1&OUTBUF                                         
         MVI   &OUTLEN-1&OUTBUF,C' '                                            
         BCT   R0,*-18                                                          
         SPACE 1                                                                
         MEXIT                                                                  
.*                                                                              
.LEFT4   ANOP                                                                   
&DRC     SETC  '&OUTBUF'(1,&KA-1)                                               
&DR      SETC  '&DRC.+1'                                                        
&NODSP   SETC  '&OUTBUF'(&KA,&KB+1)                                             
&BLDSP   SETC  '&DRC.-1+&OUTLEN'                                                
.*                                                                              
         MVC   &CA&OUTLEN,&CB,&DR&NODSP                                         
         MVI   &BLDSP&NODSP,C' '                                                
         BCT   R0,*-18                                                          
         SPACE 1                                                                
         MEXIT                                                                  
         MEND                                                                   
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006TZIHEDIT  01/09/96'                                      
         END                                                                    
