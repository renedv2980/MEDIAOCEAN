*          DATA SET ACCHOPCON  AT LEVEL 005 AS OF 05/01/02                      
*CATALP CHOPCON                                                                 
         TITLE 'CHOPS UP CONTRA-ACCOUNT CODE'                                   
CHOPCON  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 135,**CHOP**                                                     
         USING WORKD,RC            RC=A(W/S)                                    
         LR    RA,R1                                                            
         USING PARMD,RA            RA=A(PARAMETER LIST)                         
         L     R9,PASUBAC                                                       
         USING ACKEYD,R9           R9=A(SUB-ACCOUNT HEADER)                     
         L     R8,PABLOCK                                                       
         XC    0(256,R8),0(R8)     R8=A(OUTPUT BLOCK)                           
         MVI   READSW,C'N'                                                      
         MVC   HEIR(2),ACKEYCON+1                                               
         MVC   HEIR+2(5),=X'0C00000000'                                         
*                                  CHECK IF SPECIAL LEDGER                      
         LA    R1,SPECTAB                                                       
CHOP2    CLI   0(R1),0                                                          
         BE    CHOP4                                                            
         CLC   0(2,R1),ACKEYACC+1                                               
         BE    2(R1)                                                            
         LA    R1,L'SPECTAB(R1)                                                 
         B     CHOP2                                                            
*                                                                               
SPECTAB  DS    0CL6                                                             
         DC    C'SR'               RECEIVABLES                                  
         B     SPEC2                                                            
         DC    C'2P'               EXPENSE ANALYSIS                             
         B     SPEC4                                                            
         DC    C'29'                                                            
         B     SPEC4                                                            
         DC    H'0'                                                             
         EJECT                                                                  
*              GET CONTRA-LEDGER HEIRARCHY                                      
*                                                                               
CHOP4    CLC   ACKEYACC(1),ACKEYCON                                             
         BNE   SPEC6                                                            
         CLI   POPTION,C'N'        OPTION NOT TO CHOP                           
         BE    CHOP14                                                           
*                                  CHECK THAT UNIT AND LEDGER ARE THERE         
         CLI   ACKEYCON+1,C' '                                                  
         BE    CHOPX                                                            
         CLI   ACKEYCON+2,C' '                                                  
         BE    CHOPX                                                            
*                                  CHECK VALUES EXTRACTED BEFORE                
         L     R2,PASAVE                                                        
         ZIC   R3,0(R2)            R3=NUMBER OF ENTRIES                         
         LA    R4,1(R2)            R4=A(FIRST SAVE ELEMENT)                     
         LA    R5,1(R3)                                                         
         LTR   R3,R3                                                            
         BZ    CHOP8                                                            
CHOP6    CLC   0(2,R4),ACKEYCON+1                                               
         BNE   *+14                                                             
         MVC   HEIR(6),0(R4)                                                    
         B     CHOP14                                                           
         LA    R4,6(R4)                                                         
         BCT   R3,CHOP6                                                         
*                                  READ CONTRA-LEDGER & EXTRACT VALUES          
CHOP8    MVC   KEY,SPACES                                                       
         MVC   KEY(3),ACKEYCON                                                  
         GOTO1 PADTAMGR,DMCB,(0,=C'DMREAD'),=C'ACCOUNT',KEY,IOAREA              
         MVI   READSW,C'Y'                                                      
         CLI   DMCB+8,0                                                         
         BNE   CHOP12                                                           
         LA    RE,IOAREA+ACRECORD-ACKEYD                                        
         SR    RF,RF                                                            
CHOP10   CLI   0(RE),0             FIND HIERARCHY ELEMENT                       
         BE    CHOP12                                                           
         CLI   0(RE),X'16'                                                      
         BE    *+14                                                             
         IC    RF,1(RE)                                                         
         AR    RE,RF                                                            
         B     CHOP10                                                           
         USING ACHEIRD,RE                                                       
         MVC   HEIR+2(1),ACHRLEVA                                               
         MVC   HEIR+3(1),ACHRLEVB                                               
         MVC   HEIR+4(1),ACHRLEVC                                               
         MVC   HEIR+5(1),ACHRLEVD                                               
         DROP  RE                                                               
*                                  ADD ENTRY TO SAVE LIST                       
CHOP12   MVC   0(6,R4),HEIR                                                     
         STC   R5,0(R2)                                                         
         B     CHOP14                                                           
         EJECT                                                                  
*              FORMAT OUTPUT BLOCK                                              
*                                                                               
CHOP14   LA    R7,HEIR+2           R7=A(LEVA LENGTH)                            
         SR    R1,R1                                                            
         SR    RF,RF               RF=NUMBER OF OUTPUT ENTRIES                  
         SR    R2,R2               R2=L'LAST KEY ELEMENT                        
         B     CHOP16                                                           
CHOP15   IC    R1,0(R7)                                                         
         SR    R1,R2                                                            
         BCTR  R1,0                R1=L'CURRENT KEY ELEMENT                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),SPACES      CHECK FOR SOMETHING THERE                    
         BE    CHOP18                                                           
         LA    R8,51(R8)                                                        
CHOP16   MVC   0(15,R8),SPACES                                                  
         IC    R1,0(R7)                                                         
         LA    R1,2(R1)            R1=L'LEVN ACCOUNT + 2                        
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),ACKEYCON    MOVE KEY TO OUTPUT BLOCK                     
         LA    RF,1(RF)                                                         
         CLI   1(R7),0             IS THIS THE LAST ENTRY                       
         BE    CHOP18                                                           
         IC    R2,0(R7)            NO - BUMP POINTERS                           
         LA    RE,ACKEYCON+3(R2)                                                
         LA    R7,1(R7)                                                         
         B     CHOP15                                                           
CHOP18   BAS   RE,GETNAME          YES - EXTRACT NAME                           
         STC   RF,POPTION          SET NUMBER OF KEY LEVELS                     
*                                  RE-READ SUB-ACCOUNT HEADER IF READ           
CHOP20   CLI   READSW,C'Y'         SEQUENCE HAS BEEN BROKEN                     
         BNE   CHOPX                                                            
         GOTO1 PADTAMGR,DMCB,(0,=C'DMREAD'),=C'ACCOUNT',(R9),IOAREA             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
CHOPX    XMOD1 1                                                                
         EJECT                                                                  
*              HANDLE RECEIVABLE LEDGER CONTRA-ACCOUNT KEYS                     
*                                                                               
SPEC2    MVC   0(15,R8),SPACES                                                  
         MVC   1(12,R8),ACKEYCON+3                                              
         CLC   ACKEYCON(3),SPACES  BILLING SOURCE ?                             
         BE    *+10                                                             
         MVC   0(15,R8),ACKEYCON   NO - MUST BE BAD                             
         LA    RF,1                                                             
         B     CHOP18                                                           
*                                                                               
*              HANDLE EXPENSE ANALYSIS CONTRA-ACCOUNT KEYS                      
*                                                                               
SPEC4    CLI   ACKEYCON,C'*'       IGNORE IF BAD CONTRA-ACCOUNT KEY             
         BNE   CHOP14                                                           
         CLI   POPTION,C'N'        OR NOT SPLITTING KEYS                        
         BE    CHOP14                                                           
         LA    RF,ACKEYCON+1                                                    
         LA    R0,6                                                             
SPEC4A   CLI   0(RF),C'-'          FIND KEY DELIMITER                           
         BE    SPEC4B                                                           
         LA    RF,1(RF)                                                         
         BCT   R0,SPEC4A                                                        
         B     CHOP14                                                           
SPEC4B   LA    RE,ACKEYCON                                                      
         SR    RF,RE                                                            
         BCTR  RF,0                RF=L'EXPENSE PORTION OF KEY                  
         MVC   0(15,R8),SPACES                                                  
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R8),ACKEYCON                                                 
         LA    RF,5                                                             
         SR    RF,R0                                                            
         LA    RF,ACKEYCON(RF)                                                  
         MVC   WORKCODE,0(RF)      EXTRACT WORK CODE FROM EXPENSE A/C           
         BAS   RE,GETWORK                                                       
         LA    R8,51(R8)                                                        
         MVC   0(15,R8),ACKEYCON                                                
         LA    RF,2                                                             
         B     CHOP18                                                           
*                                                                               
*              HANDLE BAD CONTRA-ACCOUNT KEYS                                   
*                                                                               
SPEC6    MVC   0(15,R8),ACKEYCON                                                
         LA    R0,14                                                            
         LA    RF,1                                                             
SPEC6B   CLI   1(R8),C' '          LEFT-ALIGN KEY INTO OUTPUT BLOCK             
         BNE   CHOP18                                                           
         MVC   0(14,R8),1(R8)                                                   
         MVI   14(R8),C' '                                                      
         BCT   R0,SPEC6B                                                        
         B     CHOP18                                                           
         EJECT                                                                  
*              EXTRACT NAME FROM SUB-ACCOUNT HEADER ELEMENT                     
*                                                                               
GETNAME  NTR1                                                                   
         LA    RE,ACRECORD                                                      
         MVC   15(36,R8),SPACES                                                 
         CLI   0(RE),X'43'                                                      
         BNE   CHOPX                                                            
         USING TRSUBHD,RE                                                       
         ZIC   R1,TRSBLEN                                                       
         SH    R1,=H'18'                                                        
         BM    CHOPX               BRANCH IF ANSWER IS MINUS                    
         EX    R1,*+8                                                           
         B     CHOPX                                                            
         MVC   15(0,R8),TRSBNAME   MOVE NAME TO OUTPUT BLOCK                    
         DROP  RE                                                               
*                                                                               
*              GET WORK CODE NAME FROM LEDGER RECORD/ACCOUNT FILE               
*                                                                               
GETWORK  NTR1                                                                   
         L     RE,PALEDGER         A(LEDGER RECORD) PASSED                      
         LTR   RE,RE                                                            
         BNZ   GETWK4                                                           
GETWK2   MVC   KEY,SPACES          NO - READ ANALYSIS RECORD                    
         MVI   KEY,X'0A'                                                        
         MVC   KEY+1(3),ACKEYACC                                                
         MVC   KEY+4(2),WORKCODE                                                
         GOTO1 PADTAMGR,DMCB,(0,=C'DMREAD'),=C'ACCOUNT',KEY,IOAREA              
         MVI   READSW,C'Y'                                                      
         CLI   DMCB+8,0                                                         
         BNE   GETWKC                                                           
         LA    RE,IOAREA                                                        
*                                  FIND WORK-CODE ELEMENT                       
GETWK4   LA    RE,ACRECORD-ACKEYD(RE)                                           
         SR    R1,R1                                                            
GETWK6   CLI   0(RE),0                                                          
         BE    GETWKC                                                           
         CLI   0(RE),X'12'                                                      
         BE    GETWKA                                                           
GETWK8   IC    R1,1(RE)                                                         
         AR    RE,R1                                                            
         B     GETWK6                                                           
         USING ACANALD,RE                                                       
GETWKA   CLC   ACANCODE,WORKCODE   CHECK IF OUR WORK CODE                       
         BNE   GETWK8                                                           
         MVC   15(36,R8),SPACES                                                 
         MVC   15(L'ACANDESC,R8),ACANDESC                                       
         B     CHOPX                                                            
         DROP  RE                                                               
*                                                                               
GETWKC   MVC   15(36,R8),SPACES                                                 
         MVC   15(11,R8),=C'**UNKNOWN**'                                        
         B     CHOPX                                                            
         EJECT                                                                  
*              LITERALS ETC.                                                    
*                                                                               
         LTORG                                                                  
SPACES   DC    CL64' '                                                          
*                                  DSECT TO COVER WORKING-STORAGE               
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
READSW   DS    C                                                                
HEIR     DS    CL7                                                              
WORKCODE DS    CL2                                                              
KEY      DS    CL42                                                             
IOAREA   DS    1000C                                                            
*                                  DSECT TO COVER PARAMETER LIST                
PARMD    DSECT                                                                  
PASUBAC  DS    A                                                                
POPTION  DS    0C                                                               
PABLOCK  DS    A                                                                
PASAVE   DS    A                                                                
PADTAMGR DS    A                                                                
PALEDGER DS    A                                                                
* ACGENBOTH                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005ACCHOPCON 05/01/02'                                      
         END                                                                    
