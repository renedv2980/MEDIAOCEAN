*          DATA SET SPGETFILM  AT LEVEL 029 AS OF 05/01/02                      
*CATALP GETFILM                                                                 
         TITLE 'FIND VALID FILM DISTRIBUTION ELEMENTS'                          
         PRINT NOGEN                                                            
GETFILM  CSECT                                                                  
         NMOD1 300,GETFILM                                                      
         LR    R3,RC                                                            
         USING GFWORKD,R3                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         MVC   GFPARAM,0(R1)                                                    
         L     R8,4(R1)                                                         
         LA    R4,PRDDTE                                                        
         XC    PRDDTE,PRDDTE                                                    
         ZIC   RF,0(R8)                                                         
         BCTR  RF,0                                                             
         MH    RF,PRDBUFLN                                                      
         A     RF,PRDBUFF                                                       
         MVC   0(2,R4),1(RF)                                                    
         MVC   THISPRD,1(RF)                                                    
         CLI   3(RF),C' '                                                       
         BE    GEQUX                                                            
         XC    KEY,KEY                                                          
         LA    R2,KEY                                                           
         USING TEKEY,R2                                                         
         MVC   TEFCODE(2),=X'0A04'                                              
         MVC   TEAGY,BAGY                                                       
         MVC   TEMED,QMED                                                       
         MVC   TECLT,BCLT                                                       
         BAS   R9,READHI                                                        
         CLC   KEY(13),KEYSAVE                                                  
         BNE   NOFLMERR                                                         
         L     R1,AREC                                                          
         L     R2,AREC                                                          
         LA    R1,TEQELEM                                                       
         USING TEQELEM,R1                                                       
GEQU1    CLI   0(R1),X'30'                                                      
         BE    GEQU2                                                            
         CLI   0(R1),0                                                          
         BE    NOFLMERR                                                         
         ZIC   R0,1(R1)                                                         
         AR    R1,R0                                                            
         B     GEQU1                                                            
GEQU2    CLC   TEQACTP,THISPRD                                                  
         BE    *+12                                                             
         LA    R1,7(R1)                                                         
         B     GEQU2                                                            
         MVC   0(2,R4),TEQISCP                                                  
         DROP  R2                                                               
         DROP  R1                                                               
GEQUX    DS    0H'0'                                                            
         MVC   2(1,R4),1(R8)                                                    
         MVC   6(1,R4),4(R8)                                                    
         MVC   7(4,R4),5(R8)                                                    
NEWBRND  CLI   3(R4),X'00'         PIGGYBACK ENTRY                              
         BE    PIGGYOK              NO - SOLO HAS NO SEQUENCE                   
         CLC   0(2,R4),3(R4)        PIGGY BRANDS IN SEQUENCE                    
         BL    PIGGYOK              YES - READ FILM DISTRIBUTION REC            
         MVC   FULL(3),0(R4)                                                    
         MVC   0(3,R4),3(R4)       (O - SEQUENCE BRANDS                         
         MVC   3(3,R4),FULL                                                     
PIGGYOK  LA    R2,KEY                                                           
         USING FDSTRREC,R2                                                      
         MVC   FDPRDA(3),0(R4)      SET DISTRIBUTION RECORD KEY                 
         MVI   HOLDSC,C' '                                                      
         MVC   FDCODE,6(R4)                                                     
         CLI   FDCODE,0                                                         
         BNE   *+8                                                              
         MVI   FDCODE,C' '                                                      
         MVC   FDPRDB(3),3(R4)                                                  
         MVC   FDFCODE(2),=X'0A02'                                              
         MVC   FDAGY,BAGY                                                       
         MVC   FDMED,QMED                                                       
         MVC   FDCLT,BCLT                                                       
READDSTR BAS   R9,READHI                                                        
RDADDR   BAS   R9,READDIR                                                       
         CLC   KEY(13),KEYSAVE                                                  
         BNE   NOFLMERR            NO-PRINT ERROR                               
         MVI   HALF2,0                                                          
*                                                                               
*        BUILD A LIST OF FILM DISTRIBUTION ELEMENTS WHICH                       
*          QUALIFY FOR THIS MARKET                                              
*                                                                               
SETFDA   LA    R2,REC+24                                                        
         XC    FIDELAD(160),FIDELAD  CLEAR DISTR. ADDRESS TABLE                 
         XC    SISTORE(255),SISTORE                                             
FMGETDIS CLI   3(R4),0             PIGGYBACK ENTRY                              
         BE    FMSOLO              NO - SEARCH FOR SOLO ELEMENT                 
         CLI   FACODE,X'02'        PIGGY FOUND                                  
         BE    FMDTECHK            YES - COMPARE DATES                          
         B     FMELINCR            NO - NEXT ELEMENT                            
FMSOLO   CLI   FACODE,X'01'        SOLO ELEMENT                                 
         BE    FMDTECHK            YES - COMPARE DATES                          
FMELINCR SR    R0,R0               NO - GET NEXT ELEMENT                        
         IC    R0,FALEN                                                         
         AR    R2,R0                                                            
         CLI   0(R2),0             END OF RECORD                                
         BE    SRTDIS1             SORT ELEMENTS                                
         B     FMGETDIS                                                         
FMDTECHK CLI   FSSCL,C' '          DEFAULT STATION TYPE                         
         BE    *+14                YES - PROCESS                                
         CLC   HOLDSC,FSSCL        CURRENT STATION TYPE                         
         BNE   FMELINCR            NO - NEXT ELEMENT                            
         CLI   FMGRCL,0                                                         
         BE    PROCESS                                                          
         CLI   FMGRCL,C'0'         ALL MARKET ELEMENT                           
         BE    PROCESS             YES - PROCESS ELEMENT                        
         ST    R2,STORE2           SAVE ELEMENT ADDRESS                         
         MVC   MELEMKEY,FASTART    SAVE ELEMENT KEY                             
         SR    R0,R0                                                            
         LA    R2,REC+24                                                        
FINDMM   CLC   2(8,R2),MELEMKEY    FIND MARKET EL                               
         BE    FONDMM                                                           
MMINCR   IC    R0,1(R2)            NEXT ELEMENT                                 
         AR    R2,R0                                                            
         CLI   0(R2),0                                                          
         BE    NOMRKT                                                           
         B     FINDMM                                                           
FONDMM   CLI   FACODE,X'03'        MARKET EL                                    
         BNE   MMINCR              NO - NEXT EL                                 
         LR    R7,R2               SEARCH FOR MARKET                            
         IC    R0,1(R2)                                                         
         AR    R7,R0                                                            
         SH    R7,=H'14'                                                        
MNXTMKT  NI    FMMRKT,X'7F'        CLEAR ADD BIT                                
         CLC   FMMRKT,BMKT                                                      
         BE    MRKTOK              HAVE MARKET                                  
         LA    R2,2(R2)                                                         
         CR    R2,R7                                                            
         BL    MNXTMKT                                                          
NOMRKT   L     R2,STORE2           MARKET NOT FOUND                             
         B     FMELINCR            NEXT ELEMENT                                 
MRKTOK   L     R2,STORE2                                                        
PROCESS  LA    R5,7(R4)                                                         
         LA    R6,WORK                                                          
         BAS   R9,CNVDTE23                                                      
         LA    R5,9(R4)                                                         
         LA    R6,WORK+3                                                        
         BAS   R9,CNVDTE23                                                      
         CLC   WORK+3(3),FASTART   CHECK ELEMENT DATES                          
         BL    FMELINCR                                                         
         OC    FAEND,FAEND                                                      
         BZ    *+14                                                             
         CLC   WORK(3),FAEND                                                    
         BH    FMELINCR                                                         
         LA    RE,SISTORE          STORE ELEMENT                                
         CLI   0(RE),0             FIND OPEN SLOT                               
         BE    *+12                                                             
         LA    RE,12(RE)                                                        
         B     *-12                                                             
         ST    R2,STORE2           STORE ELEMENT KEY AND ADDRESS                
         MVC   0(2,RE),FSSCL                                                    
         CLI   1(RE),0                                                          
         BNE   *+8                                                              
         MVI   1(RE),C'0'                                                       
         CLI   1(RE),C'0'                                                       
         BE    *+8                                                              
         MVI   1(RE),C'1'                                                       
         MVC   2(6,RE),FASTART                                                  
         MVC   8(4,RE),STORE2                                                   
         B     FMELINCR                                                         
SRTDIS1  LA    RE,SISTORE                                                       
         CLI   SISTORE,0                                                        
         BE    NOFLMERR                                                         
         LA    R6,1                                                             
SRTDIS   CLI   12(RE),0            SORT DISTRIBUTION ELEMENTS IN                
         BE    *+16                DESCENDING ORDER                             
         LA    R6,1(R6)                                                         
         LA    RE,12(RE)                                                        
         B     SRTDIS                                                           
         GOTO1 XSORT,DMCB,(X'FF',SISTORE),(R6),12,8,0                           
         XC    RATES,RATES                                                      
         LA    RE,RATES            BUILD STATION TYPE/GROUP CODE                
         LA    RF,SISTORE          LIST IN DESCENDING ORDER                     
         MVC   0(2,RE),0(RF)                                                    
BLDSTGC  CLI   0(RF),0             END OF BUILD                                 
         BE    ALLOCF              ALLOCATE FILMS                               
         CLC   0(2,RE),0(RF)                                                    
         BE    *+14                                                             
         LA    RE,2(RE)                                                         
         MVC   0(2,RE),0(RF)                                                    
         LA    RF,12(RF)                                                        
         B     BLDSTGC                                                          
ALLOCF   LA    RE,RATES                                                         
         ST    RE,FULL                                                          
ALLOCF1  LA    RF,SISTORE                                                       
         L     RE,FULL                                                          
ALLOCF2  CLC   0(2,RF),0(RE)                                                    
         BL    ALLOCF3                                                          
         CLI   0(RF),0                                                          
         BE    ALLOCF3                                                          
         LA    RF,12(RF)                                                        
         MVI   HALF2,1                                                          
         B     ALLOCF2                                                          
ALLOCF3  SH    RF,=H'12'           DECREMENT 1 SLOT                             
         CLC   0(2,RE),0(RF)       ST-GC EQUAL                                  
         BNE   PASSDON             NO- END OF PASS                              
         CLC   WORK+3(3),2(RF)                                                  
         BL    ALLOCF3                                                          
         OC    5(3,RF),5(RF)                                                    
         BZ    *+14                                                             
         CLC   WORK(3),5(RF)                                                    
         BH    ALLOCF3                                                          
         LA    R7,FIDELAD                                                       
         CLI   4(R7),0             FIND OPEN SLOT IN                            
         BE    *+12                DISTRIBUTION TABLE                           
         LA    R7,8(R7)                                                         
         B     *-12                                                             
         MVI   HALF2,0                                                          
         MVC   0(4,R7),8(RF)                                                    
         LA    R6,4(R7)                                                         
         LA    R5,WORK                                                          
         CLC   WORK(3),2(RF)                                                    
         BH    *+8                                                              
         LA    R5,2(RF)                                                         
         BAS   R9,CNVDTE32                                                      
         OC    5(3,RF),5(RF)                                                    
         BZ    CHKLAST             NO END DATE                                  
         LA    R6,6(R7)                                                         
         LA    R5,5(RF)                                                         
         CLC   WORK+3(3),5(RF)                                                  
         BH    *+8                                                              
ADEND    LA    R5,WORK+3           ADD END DATE TO PATTERN                      
         BAS   R9,CNVDTE32                                                      
         B     ALLOCF3                                                          
CHKLAST  LA    R5,9(R4)            CONVERT STATION END DATE                     
         LA    R6,WORK+6                                                        
         BAS   R9,CNVDTE23                                                      
         CLC   WORK+3(3),WORK+6    SEARCH DATES = STATION END                   
         BE    ALLOCF3             YES - LEAVE UFN                              
         LA    R6,6(R7)            NO - INSERT END DATE                         
         B     ADEND                                                            
PASSDON  LA    RE,FIDELAD                                                       
         CLI   HALF2,1                                                          
         BE    INCSCGC                                                          
         LA    R6,1                                                             
SRTDATE  CLI   12(RE),0            SORT BY DATES                                
         BE    *+16                                                             
         LA    R6,1(R6)                                                         
         LA    RE,8(RE)                                                         
         B     SRTDATE                                                          
         GOTO1 XSORT,DMCB,(X'00',FIDELAD),(R6),8,4,4                            
         CLI   FIDELAD+4,0         NOTHING FOUND                                
         BE    INCSCGC             TRY DEFAULT                                  
         CLC   FIDELAD+4,7(R4)                                                  
         BNH   CHKMID                                                           
         MVC   HALF,=H'-1'                                                      
         LA    R5,FIDELAD+4                                                     
         BAS   R9,ADDAY1+4                                                      
         LA    R5,WORK                                                          
         LA    R6,WORK+3                                                        
         BAS   R9,CNVDTE23                                                      
         LA    R5,7(R4)                                                         
         LA    R6,WORK                                                          
         BAS   R9,CNVDTE23                                                      
         MVI   HALF2,1                                                          
         B     ALLOCF1                                                          
CHKMID   LA    R7,FIDELAD          CHECK FOR MIDDLE DATES                       
         CLI   6(R7),0             COVERED                                      
         BE    CHKINV                                                           
         CLC   6(2,R7),12(R7)                                                   
         BL    SETMID                                                           
CMRTRN   CLI   12(R7),0                                                         
         BE    CHKED                                                            
         LA    R7,8(R7)                                                         
         B     CHKMID+4                                                         
SETMID   MVC   HALF,=H'1'          SET MIDDLE DATES                             
         BAS   R9,ADDAY1                                                        
         CLC   12(2,R7),WORK                                                    
         BE    CMRTRN                                                           
         LA    R5,WORK                                                          
         LA    R6,WORK                                                          
         BAS   R9,CNVDTE23                                                      
         CLI   12(R7),0                                                         
         BE    CHKED2                                                           
         MVC   WORK+13(3),WORK     DECREMENT END DATE                           
         MVC   HALF,=H'-1'                                                      
         LA    R5,12(R7)                                                        
         BAS   R9,ADDAY1+4                                                      
         LA    R5,WORK                                                          
         LA    R6,WORK+3                                                        
         BAS   R9,CNVDTE23                                                      
         MVC   WORK(3),WORK+13                                                  
         MVI   HALF2,1                                                          
         B     ALLOCF1                                                          
CHKED    CLC   6(2,R7),9(R4)                                                    
         BNL   CHKINV                                                           
         B     SETMID                                                           
CHKED2   LA    R5,9(R4)                                                         
         LA    R6,WORK+3                                                        
         BAS   R9,CNVDTE23                                                      
         MVI   HALF2,1                                                          
         B     ALLOCF1                                                          
INCSCGC  MVI   HALF2,1             INCREMENT STATION CLASS GROUP                
         L     RE,FULL              CODE                                        
         LA    RE,2(RE)                                                         
         ST    RE,FULL                                                          
         CLI   0(RE),0             END                                          
         BE    NOFLMERR            YES - ERROR                                  
         B     ALLOCF1                                                          
ADDAY1   LA    R5,6(R7)                                                         
         LA    R6,WORK                                                          
         STM   R5,R6,DMCB                                                       
         OI    DMCB,X'02'                                                       
         LA    R1,DMCB                                                          
         L     RF,DATCON                                                        
         BASR  RE,RF                                                            
         ST    R6,DMCB                                                          
         LA    R5,WORK+6                                                        
         LH    R6,HALF                                                          
         STM   R5,R6,DMCB+4                                                     
         GOTO1 ADDAY,DMCB                                                       
         LA    R5,WORK+6                                                        
         LA    R6,WORK                                                          
         STM   R5,R6,DMCB                                                       
         OI    DMCB+4,X'02'                                                     
         LA    R1,DMCB                                                          
         L     RF,DATCON                                                        
         BASR  RE,RF                                                            
         BR    R9                                                               
*                                                                               
*        CHECK FOR END OF STATION ON REVISIONS                                  
*                                                                               
CHKINV   LA    R1,GFPARAM                                                       
         L     R9,8(R1)                                                         
         LA    R4,FIDELAD                                                       
OUTPUT   CLI   4(R4),0                                                          
         BE    SP76EX                                                           
OUTPUT1  L     R2,0(R4)                                                         
         USING FAELEM,R2                                                        
         ZIC   RE,1(R2)                                                         
         AR    RE,R2                                                            
         SH    RE,=H'18'                                                        
         MVC   0(4,R9),4(R4)                                                    
         CLI   6(R4),0                                                          
         BNE   *+10                                                             
         MVC   2(2,R9),PRDDTE+9                                                 
         LA    R9,4(R9)                                                         
OUTPUT2  TM    FASFILM,X'40'       FILM DELETED                                 
         BZ    *+12                                                             
         LA    R2,4(R2)                                                         
         B     OUTPUT3                                                          
         MVC   0(2,R9),FASFILM                                                  
         NI    0(R9),X'3F'                                                      
         MVC   2(1,R9),FASRWT                                                   
         LA    R2,4(R2)                                                         
         LA    R9,3(R9)                                                         
OUTPUT3  CR    R2,RE                                                            
         BL    OUTPUT2                                                          
         XC    0(3,R9),0(R9)                                                    
         LA    R9,3(R9)                                                         
         LA    R4,8(R4)                                                         
         B     OUTPUT                                                           
         B     SP76EX                                                           
SP76EX   XMOD1 1                                                                
         SPACE 2                                                                
NOFLMERR LA    R1,GFPARAM                                                       
         L     R9,8(R1)                                                         
         XC    0(10,R9),0(R9)                                                   
         B     SP76EX                                                           
         TITLE 'COMMON READ/WRITE ROUTINES'                                     
CNVDTE23 STM   R5,R6,DUB                                                        
         OI    DUB,X'02'                                                        
         OI    DUB+4,X'03'                                                      
         B     GODTCNV                                                          
CNVDTE32 STM   R5,R6,DUB                                                        
         OI    DUB,X'03'                                                        
         OI    DUB+4,X'02'                                                      
GODTCNV  LA    R1,DUB                                                           
         STM   RE,RF,DMCB                                                       
         L     RF,DATCON                                                        
         BASR  RE,RF                                                            
         LM    RE,RF,DMCB                                                       
         BR    R9                                                               
CNVDTE26 STM   R4,R5,DUB                                                        
         OI    DUB,X'02'                                                        
         B     GODTCNV                                                          
CNVDTE62 STM   R4,R5,DUB                                                        
         OI    DUB+4,X'02'                                                      
         B     GODTCNV                                                          
CNVDTE63 STM   R4,R5,DUB                                                        
         B     GODTCNV                                                          
READHI   GOTO1 HIGH                                                             
READDIR  LA    RE,REC                                                           
         ST    RE,AREC                                                          
         GOTO1 GET                                                              
         BR    R9                                                               
GFWORKD  DSECT                                                                  
FIDELAD  DS    CL160                                                            
SISTORE  DS    CL255                                                            
MELEMKEY DS    CL20                                                             
STORE2   DS    F                                                                
THISPRD  DS    CL3                                                              
GFPARAM  DS    CL12                                                             
HOLDSC   DS    C                                                                
SAVER1   DS    F                                                                
PRDDTE   DS    CL23                                                             
REC      DS    1800C                                                            
        TITLE 'STATION INVENTORY MAINTIANENCE'                                  
         PRINT ON                                                               
       ++INCLUDE FDKEY                                                          
         ORG   FDSTRREC                                                         
       ++INCLUDE FAELEM                                                         
         ORG   FDSTRREC                                                         
       ++INCLUDE FMELEM                                                         
         ORG   FDSTRREC                                                         
       ++INCLUDE FCELEM                                                         
         ORG   FDSTRREC                                                         
       ++INCLUDE FSMELEM                                                        
TKEYD    DS    0C                                                               
       ++INCLUDE TEKEY                                                          
       ++INCLUDE TEQELEM                                                        
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
