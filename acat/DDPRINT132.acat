*          DATA SET DDPRINT132 AT LEVEL 001 AS OF 11/26/13                      
*PROCESS USING(WARN(15))                                                        
***********************************************************************         
*                                                                     *         
* DEIS NOV/2013:                                                      *         
* THIS MODULE WAS COPIED FROM DDPRINTERL. THE CONDITIONAL ASSEMBLY    *         
* CODE IN DDPRINTERL WAS REMOVED, AND ONLY THE CODE PERTAINING TO     *         
* THE "ER" VERSION WAS KEPT. I.E., THIS MODULE IS EQUIVALENT TO       *         
* ASSEMBLING DDPRINTERL WITH A "SET ER=Y" FLAG.                       *         
* IF YOU *INCLUDE PRINT132 INSTEAD OF PRINTER, YOU'LL HAVE SUPPORT    *         
* FOR A SECOND SYSPRINT DATASET WITH 132-BYTE PRINT LINES.            *         
* TO USE THIS FEATURE, YOU MUST CALL V(PRINTER) WITH A NON-ZERO VALUE *         
* IN THE HOB OF RF. E.G.:                                             *         
*        L    RF,=V(PRINTER)                                          *         
*        ICM  RF,B'1000',=X'01'                                       *         
*        BASR RE,RF                                                   *         
* ALSO, LAY THE DPRINT DSECT OVER V(CPRINT2) INSTEAD OF V(CPRINT).    *         
*                                                                     *         
***********************************************************************         
*CATALP PRINT132                                                                
         TITLE 'PRINTER - PRINT CONTROLLING MODULE - WIDTH 132'                 
         PRINT NOGEN                                                            
PRINTER  CSECT                                                                  
         NMOD1 WORKX-WORKD,**PRTL**                                             
         USING WORKD,RC                                                         
*                                                                               
         STCM  RF,B'1000',DUB                                                   
         MVC   VTYPES(VTYPESL),ADCONS1                                          
         CLI   DUB,0                                                            
         JE    INIT                                                             
         MVC   VTYPES(VTYPESL),ADCONS2                                          
*                                                                               
INIT     L     RA,VCPRINT          POINT TO CSECT CONTAINING PRINT DATA         
         USING DPRINT,RA                                                        
         ICM   R5,15,VBOXAREA      TEST CALLER RESETS LANGUAGE                  
         JZ    INIT2                                                            
         USING BOXD,R5                                                          
         TM    BOXDDCTL,X'80'                                                   
         JZ    INIT2                                                            
         CLC   BOXLANG,LANGLAST                                                 
         JE    INIT2                                                            
         MVC   TIMENOW,=4X'FF'     SET TO RESET LANGUAGE STUFF                  
*                                                                               
INIT2    CLC   TIMENOW,=4X'FF'     TEST FIRST TIME                              
         JNE   INITX                                                            
*                                                                               
         THMS                                                                   
         ST    R1,TIMENOW          SAVE TIME=P'0HHMMSS+'                        
         DATE  WORK,FUNNY=NO                                                    
         MVC   TODAY,WORK          SAVE DATE=C'YYMMDD'                          
*&&US                                                                           
         CP    MAXLINE,=P'60'      IBM LASER CANT DO 60 LINES ON PAGE           
         JL    *+10                                                             
         ZAP   MAXLINE,=P'59'                                                   
         MVC   COLUMNS(5),COLS132  DEFAULT IS 132 CHR LINE                      
         J     INITX                                                            
*&&                                                                             
*&&UK                                                                           
         MVC   COLUMNS(5),COLS132  DEFAULT IS 132 CHR LINE                      
         ICM   R5,15,VBOXAREA                                                   
         JZ    INITX                                                            
         TM    BOXDDCTL,X'80'      TEST IF DATA DICTIONARY REQUIRED             
         JZ    INITB                                                            
         MVC   LANGLAST,BOXLANG                                                 
         LA    RE,LANGDATA                                                      
INITA1   CLI   0(RE),X'FF'         SEARCH FOR LANGUAGE                          
         JE    INITB                                                            
         CLC   0(1,RE),LANGLAST                                                 
         JE    *+12                                                             
         LA    RE,L'LANGDATA(RE)                                                
         J     INITA1                                                           
*                                                                               
         MVC   HEADDATE(8),04(RE)  SET DATE/TIME/PAGE                           
         MVC   HEADTIME(8),12(RE)                                               
         MVC   HEADPAGE(8),20(RE)                                               
         MVC   MONTHS(36),28(RE)   SET MONTHS                                   
INITB    J     INITX                                                            
*&&                                                                             
INITX    LA    R2,PBYTE            SET PARAMETER LIST FOR V(PRINT)              
         LA    R3,PCONTROL                                                      
         STM   R2,R3,PARAS                                                      
         EJECT                                                                  
* CONTROL OF HEADLINE PRINTING                                                  
*                                                                               
         CP    LINE,MAXLINE        HEADLINES NEEDED                             
         JL    PRDATA                                                           
         MVC   PLINE,SPACES        SKIP TO CHANNEL 1                            
         MVC   PCONTROL,=C'BC01'                                                
         BRAS  R9,PRINTEM                                                       
*                                                                               
         MVC   WORK(8),TODAY       GET TODAYS DATE C'YYMMDD'                    
         CLC   SPECDATE(5),=C'DATE='                                            
         JNE   *+10                UNLESS OVERRIDEN                             
         MVC   WORK(6),SPECDATE+5                                               
         CLI   WORK,C'9'           TEST IF PASSED DDS FUNNY DATE                
         JNH   PRHEAD1                                                          
         SR    RE,RE               ADJUST FOR YR2000+ DATES                     
         IC    RE,WORK                                                          
         SHI   RE,10                                                            
         STC   RE,WORK                                                          
PRHEAD1  LA    RE,WORK+16                                                       
         MVC   0(9,RE),SPACES                                                   
         ICM   R5,15,VBOXAREA      TEST IF WE KNOW WHERE WE BE                  
         JZ    PRHEAD1A                                                         
         TM    BOXDDCTL,X'80'                                                   
         JZ    PRHEAD1A                                                         
         CLI   BOXLANG,2           ENGLISH HAS DD MMM YY                        
         JH    PRHEAD1B                                                         
         DROP  R5                                                               
*                                                                               
PRHEAD1A DS    0H                                                               
         MVC   0(2,RE),WORK+4                                                   
         PACK  DUB,WORK+2(2)                                                    
         CVB   R2,DUB                                                           
         BCTR  R2,0                                                             
         MHI   R2,3                                                             
         LA    R2,MONTHS(R2)                                                    
         MVC   3(3,RE),0(R2)                                                    
         MVC   7(2,RE),WORK                                                     
         J     PRHEAD1C                                                         
*                                                                               
PRHEAD1B MVC   0(2,RE),WORK+4      EUROPE HAS DD.MM.YY                          
         MVI   2(RE),C'.'                                                       
         MVC   3(2,RE),WORK+2                                                   
         MVI   5(RE),C'.'                                                       
         MVC   6(2,RE),WORK                                                     
*                                                                               
PRHEAD1C LA    RF,HEADDATE                                                      
         CLI   0(RF),C' '                                                       
         JNH   *+12                                                             
         LA    RF,1(RF)                                                         
         J     *-12                                                             
         MVC   1(9,RF),0(RE)       SET DATE AFTER DATE TEXT                     
*                                                                               
         MVC   DUB(4),TIMENOW      GET TIME                                     
         UNPK  WORK(6),DUB(4)                                                   
         LA    RE,WORK+16                                                       
         MVC   0(2,RE),WORK        SET HH.MM                                    
         MVI   2(RE),C'.'                                                       
         MVC   3(2,RE),WORK+2                                                   
         LA    RF,HEADTIME                                                      
         CLI   0(RF),C' '                                                       
         JNH   *+12                                                             
         LA    RF,1(RF)                                                         
         J     *-12                                                             
         MVC   1(5,RF),0(RE)       SET TIME AFTER TIME TEXT                     
*                                                                               
         LA    R2,HEADPAGE         SET PAGE NUMBER AFTER PAGE TEXT              
         CLI   0(R2),C' '                                                       
         JNH   *+12                                                             
         LA    R2,1(R2)                                                         
         J     *-12                                                             
         EDIT  (P4,PAGE),(4,1(R2)),ALIGN=LEFT                                   
*                                                                               
         ZAP   PACK,=P'0'          TEST IF NO HEADLINE REQUIRED                 
         OC    PCCDATE(4),PCCDATE                                               
         JZ    PRMID                                                            
*                                                                               
         LLC   R7,PCCDATE          GET COLUMN FOR DATE                          
         SHI   R7,1                                                             
         JM    PRHEAD5                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(16,R7),HEADDATE                                                
PRHEAD5  LLC   R7,PCCTIME          GET COLUMN FOR TIME                          
         SHI   R7,1                                                             
         JM    PRHEAD6                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(12,R7),HEADTIME                                                
PRHEAD6  LLC   R7,PCCTITLE         GET COLUMN FOR TITLE                         
         SHI   R7,1                                                             
         JM    PRHEAD7                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(L'TITLE,R7),TITLE                                              
PRHEAD7  LLC   R7,PCCPAGE          GET COLUMN FOR PAGE NUMBER                   
         SHI   R7,1                                                             
         JM    PRHEADX                                                          
         LA    R7,PLINE(R7)                                                     
         MVC   0(11,R7),HEADPAGE                                                
*                                                                               
PRHEADX  MVC   PCONTROL,=C'BL01'   PRINT HEADLINE                               
         BRAS  R9,PRINTEM                                                       
         AP    PACK,=P'1'          BUMP HEADLINE COUNTER                        
         SPACE 2                                                                
* ROUTINE TO UNDERLINE TITLE                                                    
*                                                                               
         MVC   PLINE,SPACES        CLEAR OUT HEADLINE                           
         MVC   PCONTROL,=C'BL01'                                                
         LA    R2,TITLE                                                         
         LLC   R5,PCCTITLE                                                      
         SHI   R5,1                                                             
         JM    ULHEAD5             TEST IF NO TITLE DEFINED                     
         LA    R5,PLINE(R5)                                                     
         LA    R4,L'TITLE                                                       
*                                                                               
ULHEAD1  CLI   0(R2),C' '          A(FIRST NON-SPACE) TO R2                     
         JNE   ULHEAD2                                                          
         LA    R2,1(R2)                                                         
         LA    R5,1(R5)                                                         
         BRCT  R4,ULHEAD1                                                       
         J     ULHEAD5             EXIT IF NO DATA IN TITLE                     
*                                                                               
ULHEAD2  LA    R3,TITLE+59         A(LAST NON-SPACE) TO R3                      
         LLC   R6,PCCTITLE                                                      
         SHI   R6,1                                                             
         LA    R6,PLINE+59(R6)                                                  
*                                                                               
ULHEAD3  CLI   0(R3),C' '                                                       
         JNE   ULHEAD4                                                          
         BCTR  R6,0                                                             
         BRCT  R3,ULHEAD3                                                       
*                                                                               
ULHEAD4  MVC   0(1,R5),ULCHR       FILL WITH DASHES                             
         CR    R5,R6                                                            
         JE    ULHEAD5                                                          
         LA    R5,1(R5)                                                         
         J     ULHEAD4                                                          
*                                                                               
ULHEAD5  MVC   PCONTROL,=C'BL02'   SET TO UNDERLINE TITLE AND SPACE             
         AP    PACK,=P'1'                                                       
*                                                                               
         BRAS  R9,PRINTEM          PRINT LINE(S) AFTER TITLE                    
         AP    PACK,=P'1'                                                       
         EJECT                                                                  
* CONTROL OF MIDLINE PRINTING                                                   
*                                                                               
PRMID    ZAP   LINE,=P'0'          SET LINE COUNTER                             
         AP    LINE,PACK                                                        
         AP    PAGE,=P'1'          SET PAGE COUNTER                             
*                                                                               
         CLC   MID1,SPACES         EXIT IF NO MIDLINES                          
         JE    PRSUB                                                            
         MVC   PLINE,MID1                                                       
         CLC   MID2,SPACES                                                      
         JE    PRMIDX                                                           
         MVI   PCONTROL+3,C'1'                                                  
         BRAS  R9,PRINTEM          PRINT MIDLINE 1                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID2                                                       
         CLC   MID3,SPACES                                                      
         JE    PRMIDX                                                           
         BRAS  R9,PRINTEM          PRINT MIDLINE 2                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID3                                                       
         CLC   MID4,SPACES                                                      
         JE    PRMIDX                                                           
         BRAS  R9,PRINTEM          PRINT MIDLINE 3                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,MID4                                                       
*                                                                               
PRMIDX   MVI   PCONTROL+3,C'2'     PRINT LAST MIDLINE AND SPACE 2               
         BRAS  R9,PRINTEM                                                       
         AP    LINE,=P'2'                                                       
         SPACE 2                                                                
* CONTROL OF SUBLINE PRINTING                                                   
*                                                                               
PRSUB    CLC   SUB1,SPACES         EXIT IF NO SUBLINES                          
         JE    PRDATA                                                           
         MVC   PLINE,SUB1                                                       
         CLC   SUB2,SPACES                                                      
         JE    PRSUBX                                                           
         MVI   PCONTROL+3,C'1'                                                  
         BRAS  R9,PRINTEM          PRINT SUBLINE 1                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,SUB2                                                       
         CLC   SUB3,SPACES                                                      
         JE    PRSUBX                                                           
         BRAS  R9,PRINTEM          PRINT SUBLINE 2                              
         AP    LINE,=P'1'                                                       
         MVC   PLINE,SUB3                                                       
         SPACE 2                                                                
PRSUBX   MVI   PCONTROL+3,C'2'     PRINT LAST SUBLINE AND SPACE 2               
         BRAS  R9,PRINTEM                                                       
         AP    LINE,=P'2'                                                       
         EJECT                                                                  
* PRINT USERS LINE AND RETURN                                                   
*                                                                               
PRDATA   MVC   PLINE,P             USER PRINT LINE IS IN P                      
         MVC   PCONTROL,SPACING                                                 
         BRAS  R9,PRINTEM                                                       
         PACK  DUB,SPACING+2(2)                                                 
         AP    LINE,DUB                                                         
         MVC   P,SPACES            CLEAR USER PRINT LINE AFTER PRINTING         
*                                                                               
         XMOD1                                                                  
         SPACE 2                                                                
PRINTEM  GOTO1 VPRINT,PARAS                                                     
         BR    R9                                                               
         EJECT                                                                  
         DS    0F                                                               
TIMENOW  DC    XL4'FFFFFFFF'       TIME                                         
TODAY    DC    CL8'YYMMDDCC'       TODAYS DATE AND COUNTRY CODE                 
ULCHR    DC    C'-'                UNDERLINE CHARACTER                          
LANGLAST DC    X'FF'               LAST LANGUAGE CODE                           
*                                                                               
COLUMNS  DC    AL1(000)            COLUMN WIDTH                                 
PCCDATE  DC    AL1(000)            COLUMN FOR DATE                              
PCCTIME  DC    AL1(000)            COLUMN FOR TIME                              
PCCTITLE DC    AL1(000)            COLUMN FOR TITLE                             
PCCPAGE  DC    AL1(000)            COLUMN FOR PAGE NUMBER                       
*                                                                               
COLS080  DC    AL1(080,000,000,001,064)                                         
COLS110  DC    AL1(110,001,000,031,100)                                         
COLS132  DC    AL1(132,001,020,035,100)                                         
         SPACE 1                                                                
LANGDATA DS    0CL64                                                            
*                                                                               
LANGENG  DC    AL1(0),X'000000'                                                 
         DC    C'DATE    ',C'TIME    ',C'PAGE    '                              
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
LANGENG1 DC    AL1(1),X'000000'                                                 
         DC    C'DATE    ',C'TIME    ',C'PAGE    '                              
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
LANGENG2 DC    AL1(2),X'000000'                                                 
         DC    C'DATE    ',C'TIME    ',C'PAGE    '                              
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                          
*                                                                               
LANGGER  DC    AL1(3),X'000000'                                                 
         DC    C'DATUM   ',C'ZEIT    ',C'SEITE   '                              
         DC    C'JANFEBMAEAPRMAIJUNJULAUGSEPOKTNOVDEZ'                          
*                                                                               
LANGFRE  DC    AL1(4),X'000000'                                                 
         DC    C'DATE    ',C'HEURE   ',C'PAGE    '                              
         DC    C'JANFEVMARAVRMAIJUNJULAOUSEPOCTNOVDEC'                          
*                                                                               
LANGDUT  DC    AL1(7),X'000000'                                                 
         DC    C'DATUM   ',C'TIJD    ',C'PAGINA  '                              
         DC    C'JANFEBMRTAPRMEIJUNJULAUGSEPOKTNOVDEC'                          
*                                                                               
LANGTABX DC    X'FFFFFFFF'                                                      
         SPACE 1                                                                
         LTORG                                                                  
                                                                                
ADCONS1  DC    V(PRINT,CPRINT,BOXAREA)                                          
ADCONS2  DC    V(PRINT2,CPRINT2,BOXAREA2)                                       
         EJECT                                                                  
* DSECT FOR PRINT CONTROLLER                                                    
*                                                                               
WORKD    DSECT                                                                  
                                                                                
VTYPES   DS    0V                                                               
VPRINT   DS    V                                                                
VCPRINT  DS    V                                                                
VBOXAREA DS    V                                                                
VTYPESL  EQU   *-VTYPES                                                         
                                                                                
DUB      DS    D                                                                
PARAS    DS    CL8                                                              
PACK     DS    PL4                                                              
WORK     DS    CL32                                                             
PCONTROL DS    CL4                                                              
PBYTE    DS    CL1                                                              
PLINE    DS    CL132                                                            
         DS    CL66                                                             
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE DDCPRINT                                                       
       ++INCLUDE DDCPRINT2                                                      
         EJECT                                                                  
       ++INCLUDE DDBIGBOX                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DDPRINT13211/26/13'                                      
         END                                                                    
