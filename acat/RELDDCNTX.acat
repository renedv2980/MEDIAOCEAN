*          DATA SET RELDDCNTX  AT LEVEL 022 AS OF 05/01/02                      
*CATALP RELDDCNT                                                                
         TITLE 'LDCOUNT - REP - DIRECTORY REC TYPE COUNT/PRINT'                 
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'00'=COUNT  X'FF'=PRINT                            
*        AL3   A(RECORD)                                                        
*        AL4   A(FILE NAME)                                                     
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 4,RELDDCNT                                                       
         USING WRKD,RC                                                          
*                                                                               
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
         EJECT                                                                  
COUNT    L     R2,0(R1)            R2=A(RECORD)                                 
         LA    R3,CNTRS+6          R3=A(COUNTERS ENTRY)                         
         LH    R4,CNTRS                                                         
         L     R5,CNTRS+2                                                       
*                                                                               
         TM    0(R2),X'80'         TEST MINOR PTR                               
         BO    C1                                                               
         CLI   27(R2),X'FF'        KEY CHANGE?                                  
         BNE   C1                                                               
         LA    R3,FFTOTALS                                                      
         B     C2                                                               
*                                                                               
C1       CLC   0(1,R2),0(R3)       REC TYPES AGREE                              
         BE    C2                  YES                                          
         BXLE  R3,R4,C1            NO LOOP                                      
         SR    R3,R4               NOT FOUND BACK UP TO UNKNOWN                 
         CLI   0(R2),0             TEST HEADER                                  
         BNE   *+6                 NO                                           
         SR    R3,R4               YES - BACK UP TO CONTROL                     
*                                                                               
C2       L     RE,20(R3)           UPDATE TOTAL                                 
         LA    RE,1(RE)                                                         
         ST    RE,20(R3)                                                        
         SPACE 2                                                                
XIT      XMOD1 1                                                                
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         L     RE,4(R1)                                                         
         MVC   HLINE(7),0(RE)                                                   
*                                                                               
         LA    RE,L'HLINE          HEADING                                      
         LA    RF,HLINE                                                         
         BAS   R9,PUT                                                           
         LA    RF,ULINE                                                         
         BAS   R9,PUT                                                           
*                                                                               
         LA    R3,CNTRS+6          RECORD TYPE LINES                            
         LH    R4,CNTRS                                                         
PR1      CLI   0(R3),0                                                          
         BE    PR2                                                              
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         AR    R3,R4                                                            
         B     PR1                                                              
*                                                                               
PR2      LA    RE,L'ULINE          TOTALS                                       
         LA    RF,ULINE                                                         
         BAS   R9,PUT                                                           
         LA    R3,MJTOTALS                                                      
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         LA    R3,MNTOTALS                                                      
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         LA    R3,FFTOTALS                                                      
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
*                                                                               
         L     R0,FFTOTALS+20                                                   
         A     R0,TOTALS+20                                                     
         ST    R0,TOTALS+20                                                     
*                                                                               
         LA    R3,TOTALS                                                        
         BAS   RE,SET                                                           
         LA    RE,L'PLINE                                                       
         LA    RF,PLINE                                                         
         BAS   R9,PUT                                                           
         LA    RF,ULINE                                                         
         BAS   R9,PUT                                                           
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
PUT      STM   RE,RF,DUB                                                        
         MVC   P,SPACES                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(RF)                                                       
         GOTO1 =V(PRINTER)                                                      
         LM    RE,RF,DUB                                                        
PUTX     BR    R9                                                               
         EJECT                                                                  
SET      NTR1                                                                   
         CLI   0(R3),0             IS THIS A TOTAL CTR                          
         BE    SET2                                                             
         L     R5,TOTALS+20        UPDATE GRAND TOTAL                           
         A     R5,20(R3)                                                        
         ST    R5,TOTALS+20                                                     
         CLI   0(R3),X'FF'         ASSUME CONTROL/UNKNOWN ARE MAJ PTRS          
         BE    *+12                                                             
         TM    0(R3),X'80'         TEST MAJOR                                   
         BO    SETMN               NO                                           
         L     R5,MJTOTALS+20      UPDATE MAJOR PTR TOTAL                       
         A     R5,20(R3)                                                        
         ST    R5,MJTOTALS+20                                                   
         B     SET2                                                             
*                                                                               
SETMN    L     R5,MNTOTALS+20      UPDATE MINOR PTR TOTALS                      
         A     R5,20(R3)                                                        
         ST    R5,MNTOTALS+20                                                   
*                                                                               
SET2     MVI   PLINE,C' '                                                       
         MVC   PLINE+1(L'PLINE-1),PLINE                                         
         MVC   PLTYPE,1(R3)                                                     
         LA    R6,20(R3)                                                        
         LA    R7,PLTOT                                                         
         BAS   R9,SETCVD                                                        
         B     SETX                                                             
*                                                                               
SETCVD   L     R0,0(R6)                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  0(8,R7),DUB                                                      
         BR    R9                                                               
*                                                                               
SETX     XIT1                                                                   
         EJECT                                                                  
HLINE    DC    C'XXXXXXX RECORD TYPE  TOTAL   '                                 
ULINE    DC    C'-------------------  --------'                                 
PLINE    DS    0CL29                                                            
PLTYPE   DS    CL19                                                             
         DS    CL2                                                              
PLTOT    DS    CL8                                                              
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
         CNOP  2,4                                                              
CNTRS    DC    H'24'                                                            
         DC    A(CNTRSX-1)                                                      
         DC    X'01',CL19'01 - REPRESENTATIVE',F'0'                             
         DC    X'02',CL19'02 - STATION       ',F'0'                             
         DC    X'03',CL19'03 - REGION        ',F'0'                             
         DC    X'04',CL19'04 - OFFICE        ',F'0'                             
         DC    X'05',CL19'05 - SALES TEAM    ',F'0'                             
         DC    X'06',CL19'06 - SALESMAN      ',F'0'                             
         DC    X'07',CL19'07 - GROUP         ',F'0'                             
         DC    X'08',CL19'08 - ADVERTISER    ',F'0'                             
         DC    X'09',CL19'09 - PRODUCT       ',F'0'                             
         DC    X'0A',CL19'0A - AGENCY        ',F'0'                             
         DC    X'0B',CL19'0B - BUY           ',F'0'                             
         DC    X'0C',CL19'0C - CONTRACT      ',F'0'                             
         DC    X'0D',CL19'0D - CLASS         ',F'0'                             
         DC    X'0F',CL19'0F - CATEGORY      ',F'0'                             
         DC    X'12',CL19'12 - INVENTORY     ',F'0'                             
         DC    X'13',CL19'13 - BUDGET        ',F'0'                             
         DC    X'14',CL19'14 - AVAIL         ',F'0'                             
         DC    X'16',CL19'16 - PROPOSAL      ',F'0'                             
         DC    X'18',CL19'18 - EOM           ',F'0'                             
         DC    X'19',CL19'19 - OFFICE BUDGET ',F'0'                             
         DC    X'1A',CL19'1A - AGENCY 2ND    ',F'0'                             
         DC    X'1B',CL19'1B - EOP ADVERTISER',F'0'                             
         DC    X'1C',CL19'1C - EOP AGENCY    ',F'0'                             
         DC    X'1D',CL19'1D - EOP OFFICE    ',F'0'                             
         DC    X'1E',CL19'1E - EOP SALESPER  ',F'0'                             
         DC    X'22',CL19'22 - OVERNIGHT UPLD',F'0'                             
         DC    X'23',CL19'23 - DEMO MENU     ',F'0'                             
         DC    X'24',CL19'24 - DAYPART       ',F'0'                             
         DC    X'25',CL19'25 - PROGRAM TYPE  ',F'0'                             
         DC    X'26',CL19'26 - STATION DEFN  ',F'0'                             
         DC    X'27',CL19'27 - ATHENA        ',F'0'                             
         DC    X'28',CL19'28 - SWITCH        ',F'0'                             
         DC    X'29',CL19'29 - COMMISSION    ',F'0'                             
         DC    X'2A',CL19'2A - OWNERSHIP     ',F'0'                             
         DC    X'2B',CL19'2B - MARKET        ',F'0'                             
         DC    X'2C',CL19'2C - AUR           ',F'0'                             
         DC    X'2D',CL19'2D - FORECAST      ',F'0'                             
         DC    X'2E',CL19'2E - COMMENT       ',F'0'                             
         DC    X'2F',CL19'2F - PAPER WORK    ',F'0'                             
         DC    X'30',CL19'30 - TYPE          ',F'0'                             
         DC    X'31',CL19'31 - POINT PERSON  ',F'0'                             
         DC    X'32',CL19'32 - CONTRACT TYPE ',F'0'                             
         DC    X'33',CL19'33 - RADAR         ',F'0'                             
         DC    X'34',CL19'34 - OFFICE COMMENT',F'0'                             
         DC    X'35',CL19'35 - DIRECT RESP   ',F'0'                             
         DC    X'36',CL19'36 - CONTRACT LABEL',F'0'                             
         DC    X'37',CL19'37 - GOALN/GOAL    ',F'0'                             
         DC    X'38',CL19'38 - SET           ',F'0'                             
         DC    X'39',CL19'39 - STRATEGY      ',F'0'                             
         DC    X'3A',CL19'3A - DEVLMNTL SALES',F'0'                             
         DC    X'3B',CL19'3B - DEVLMNTL K TYP',F'0'                             
         DC    X'41',CL19'41 - DARE DATA FILE',F'0'                             
         DC    X'42',CL19'42 - STA PREV COPY ',F'0'                             
         DC    X'44',CL19'44 - OFFICE 2ND    ',F'0'                             
         DC    X'82',CL19'82 * STATION CALLS ',F'0'                             
         DC    X'88',CL19'88 * ADVRTISER NAME',F'0'                             
         DC    X'89',CL19'89 * PRODUCT/NET # ',F'0'                             
         DC    X'8A',CL19'8A * AGENCY NAME   ',F'0'                             
         DC    X'8B',CL19'8B * MARKET STATION',F'0'                             
         DC    X'8C',CL19'8C * CONTRACT NUM  ',F'0'                             
         DC    X'9A',CL19'9A * DARE AGENCY EQ',F'0'                             
         DC    X'9B',CL19'9B * BUY SPOT DATA ',F'0'                             
         DC    X'9C',CL19'9C * CONTRACT STATN',F'0'                             
         DC    X'AC',CL19'AC * CONTRACT SALES',F'0'                             
         DC    X'BC',CL19'BC * CONTRACT CATGY',F'0'                             
         DC    X'CC',CL19'CC * CONTRACT SPL  ',F'0'                             
         DC    X'DC',CL19'DC * CONTRACT BOP  ',F'0'                             
         DC    X'EC',CL19'EC * CONTRACT SAR  ',F'0'                             
         DC    X'92',CL19'92 * INVENTORY DAYP',F'0'                             
         DC    X'FF',CL19'   - CONTROL       ',F'0'                             
         DC    X'FF',CL19'?? - UNKNOWN       ',F'0'                             
CNTRSX   DS    0C                                                               
*                                                                               
MJTOTALS DC    X'00',CL19'MAJOR POINTERS     ',F'0'                             
MNTOTALS DC    X'00',CL19'MINOR POINTERS     ',F'0'                             
FFTOTALS DC    X'00',CL19'CHANGED KEY PTRS   ',F'0'                             
TOTALS   DC    X'00',CL19'TOTALS             ',F'0'                             
         EJECT                                                                  
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'022RELDDCNTX 05/01/02'                                      
         END                                                                    
