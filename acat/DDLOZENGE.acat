*          DATA SET DDLOZENGE  AT LEVEL 003 AS OF 05/01/02                      
*CATALP LOZENGE                                                                 
         TITLE 'EXPANDS EIGHT CHR WORD AND PRINTS'                              
         PRINT NOGEN                                                            
LOZENGE  CSECT                                                                  
         NMOD1 0,LOZENGE                                                        
         LA    R2,RELO                                                          
         S     R2,RELO                                                          
         L     R3,=V(PRINT)                                                     
         AR    R3,R2                                                            
         ST    R3,APRINT                                                        
         L     R1,0(R1)                                                         
         MVC   CHAR(8),0(R1)        PARM1 IS A(EIGHT CHR WORD)                  
         B     LARGEPRT                                                         
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
* THE EIGHT CHRS STORED IN CHAR WILL BE PRINTED IN LARGE BLOCK LETTERS.         
* ONLY LETTERS AND NUMBERS WILL BE ENLARGED.                                    
* THE PRINT TAKES 12 LINES FOLLOWED BY 3 SPACE LINES.                           
*                                                                               
LARGEPRT EQU   *                                                                
         LA    WORK1,CHAR          PT TO FIRST CHAR                             
         LA    WORK2,8             NUMBER OF CHARACTERS                         
         LA    WORK3,CHARADDR      SET ADDRESS TABLE                            
LCHK     EQU   *                   VALIDATE AND SET CHARADDR                    
         CLI   0(WORK1),CHAR$      DOLLAR SIGN AS A VALID CHAR                  
         BNE   LCHKA               NO--CHECK FOR NORMAL CHARS                   
         LA    WORK4,L$            YES--PT TO ITS MATRIX                        
         B     LCHKSET2            PROCEED                                      
LCHKA    DC    0Y(0)               CHECK FOR VALID CHARS ONLY                   
         CLI   0(WORK1),CHARA      VALIDATE THE CHARACTER                       
         BNL   LCHKI                                                            
LCHKBLNK EQU   *                   DEFAULT TO BLANK                             
         XC    0(L'CHARADDR,WORK3),0(WORK3)  INDICATE A BLANK (NULL)            
LCHKLOOP EQU   *                   LOOP THRU ALL CHARACTERS                     
         LA    WORK1,1(WORK1)      NEXT CHARACTER                               
         LA    WORK3,4(WORK3)      NEXT MATRIX ADDRESS                          
         BCT   WORK2,LCHK          BACK FOR MORE                                
         B     LPRNT               GO AND PRINT THESE CHARACTERS                
LCHKI    EQU   *                   ACTUAL VALIDATE                              
         SR    WORK4,WORK4         CLEAR FOR INSERT                             
         IC    WORK4,0(WORK1)      GET THE CHARACTER                            
         LA    WORK5,ADJ1          ADJUST IF A-I                                
         CLI   0(WORK1),CHARI      ALL READY IF NOT GREATER THAN I              
         BNH   LCHKSET                                                          
         CLI   0(WORK1),CHARJ      IF IT FALLS BETWEEN I AND J                  
         BL    LCHKBLNK            ASSUME BLANK FOR NON-ALFA                    
         LA    WORK5,ADJ2          ADJUST IF J-R                                
         CLI   0(WORK1),CHARR      ALL READY IF J-R                             
         BNH   LCHKSET                                                          
         CLI   0(WORK1),CHARS      BAD (ASSUME BLANK) IF NOT J-R                
         BL    LCHKBLNK                                                         
         LA    WORK5,ADJ3          ADJUST IF S-Z                                
         CLI   0(WORK1),CHARZ      OK IF S-Z                                    
         BNH   LCHKSET                                                          
         CLI   0(WORK1),CHAR0      ASSUME BLANK IF Z-0                          
         BL    LCHKBLNK                                                         
         LA    WORK5,ADJ4          ADJUST FOR 0-9                               
         CLI   0(WORK1),CHAR9      ASSUME BLANK IF NOT ALFA OR NUMERIC          
         BH    LCHKBLNK                                                         
LCHKSET  EQU   *                   VALIDATION COMPLETE                          
         SR    WORK4,WORK5         PERFORM THE ADJUSTMENT                       
         MH    WORK4,=Y(L'LA)      MULTIPLY BY TABLE-ENTRY SIZE                 
         LA    WORK4,LA(WORK4)     GET PTR TO CHARACTER MATRIX                  
LCHKSET2 DC    0Y(0)               SET PTRS TO PROPER CHAR                      
         ST    WORK4,0(WORK3)      STORE IT TO LATER PRINT THE MATRIX           
         MVI   0(WORK3),MATMASK    INITIAL MATRIX MASK                          
         B     LCHKLOOP            KEEP PREPARING                               
         EJECT                                                                  
* PRINT THE LARGE LETTERS                                                       
*                                                                               
LPRNT    EQU   *                                                                
         SR    WORK3,WORK3         CLEAR FOR IC OF THE MASK                     
         LA    WORK4,12            LINES PER CHARACTER                          
         MVI   CONTROL+3,C'1'                                                   
LPRTNLIN EQU   *                                                                
         LA    WORK1,CHAR-1        THE CHARACTERS                               
         LA    WORK2,CHARADDR      PTR TO TABLE ENTRY                           
         LA    LINPTR,LINEHDR      PT TO BEGINNING OF THE LINE                  
         LA    RC,CHAR+7                                                        
         LA    R0,7                                                             
         SPACE 2                                                                
CNTR1    CLI   0(RC),C' '          CENTRE THE CHARACTERS                        
         BC    7,CNTR2                                                          
         LA    LINPTR,7(LINPTR)                                                 
         BCTR  RC,R0                                                            
         BCT   R0,CNTR1                                                         
         SPACE 2                                                                
CNTR2    EQU   *                                                                
         LA    WORK7,8             NUMBER OF CHARACTERS                         
LPRTLOOP EQU   *                                                                
         LA    WORK1,1(WORK1)                                                   
         LA    WORK5,12            POSITIONS PER CHARACTER                      
         L     WORK6,0(WORK2)      GET THE ENTRY                                
         LTR   WORK6,WORK6                                                      
         BZ    LPRTBLNK            SKIP OVER FOR NULL CHARS                     
         IC    WORK3,0(WORK2)      GET THE MASK                                 
LPRTCHAR EQU   *                                                                
         EX    WORK3,LPRTTM        PUT THIS CHAR AT THIS POSITION               
         BNO   LPRTNO                                                           
         MVC   0(1,LINPTR),0(WORK1)     PUT THE CHARACTER OUT                   
LPRTNO   EQU   *                                                                
         LA    LINPTR,1(LINPTR)    NEXT PRINT POSITION                          
         SRA   WORK3,1             NEXT MASK POSITION                           
         BNZ   LPRTSAME            STILL SAME MATRIX BYTE                       
         LA    WORK3,MATMASK       RESET MASK                                   
         LA    WORK6,1(WORK6)      GO TO NEXT BYTE IN TABLE                     
LPRTSAME EQU   *                                                                
         BCT   WORK5,LPRTCHAR      KEEP GOING SAME CHARACTER                    
         ST    WORK6,0(WORK2)      PUT IT BACK INTO TABLE FOR NEXT LINE         
         STC   WORK3,0(WORK2)      MASK ALSO                                    
         LA    LINPTR,2(LINPTR)    PUT SOME SPACE BETWEEN LETTERS               
LPRTNEXT EQU   *                   NEXT CHARACTER                               
         LA    WORK2,4(WORK2)      MOVE TO NEXT MATRIX PTR                      
         BCT   WORK7,LPRTLOOP      NEXTCHARACTER THIS LINE                      
         BAS   RE,PRINT            PRINT A LINE                                 
         BCT   WORK4,LPRTNLIN      ALL 12 LINES                                 
         MVI   CONTROL+3,C'2'                                                   
         BAS   RE,PRINT                                                         
         B     EXIT                EXIT                                         
LPRTBLNK EQU   *                                                                
         LA    LINPTR,14(LINPTR)   JUST ADVANCE TO NEXT CHARACTER               
         B     LPRTNEXT            AND DO NEXT CHARACTER                        
LPRTTM   TM    0(WORK6),0          TEST THE MATRIX TABLE                        
         SPACE 2                                                                
PRINT    NTR1                      PRINT A LINE AND CLEAR IT                    
         LA    R1,LINE-1                                                        
         ST    R1,PARAS                                                         
         LA    R1,CONTROL                                                       
         ST    R1,PARAS+4                                                       
         LA    R1,PARAS                                                         
         L     RF,APRINT                                                        
         BASR  RE,RF                                                            
         MVI   LINE,C' '                                                        
         MVC   LINE+1(119),LINE                                                 
         XIT1                                                                   
         EJECT                                                                  
CHAR     DC    CL8' '    INPUT TO LARGEPRT - LETTERS TO BE PRINTED              
CHARADDR DC    8F'0'     ABOVE CONVERTED TO PTRS TO THE MATRIX                  
RELO     DC    A(*)                                                             
         SPACE 1                                                                
SENSE    DC    X'FF'                RECEIVE SYSLST SENSE                        
LINE     DS    0CL120               THE PRINT LINE                              
LINEHDR  EQU   *                    PLACE FOR BLOCK LETTERS TO START            
LINEMSG  DC    CL120' '                                                         
CHICKEN  DC    CL120' '                                                         
         DC    C' '                                                             
         SPACE 1                                                                
APRINT   DS    F                                                                
CONTROL  DC    C'BL01'                                                          
PARAS    DS    6F                                                               
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
* EACH ENTRY IN THIS TABLE IS 144 BITS IN LENGTH.                               
* EACH BLOCK LETTER IS DEFINED BY 12 SETS OF 12 BITS.                           
* EACH BIT (IF ON INDICATES PRINT A CHARACTER) REPRESENTS                       
* ONE PRINT POSITION (12 PER CHARACTER PER LINE).                               
* EACH SET OF 12 BITS DENOTES EACH OF 12 LINES.                                 
*                                                                               
L$       DC    X'0603FE7FFE61C607FC3FE063867FFE7FC060'                          
LA       DC    X'7FEFFFC03C03C03FFFFFFC03C03C03C03C03'                          
LB       DC    X'FFEFFFC03C03C06FFCFFCC06C03C03FFFFFE'                          
LC       DC    X'7FEFFFC03C03C00C00C00C00C03C03FFF7FE'                          
LD       DC    X'FFEFFFC03C03C03C03C03C03C03C03FFFFFE'                          
LE       DC    X'FFFFFFC00C00C00FFCFFCC00C00C00FFFFFF'                          
LF       DC    X'FFFFFFC00C00C00FFCFFCC00C00C00C00C00'                          
LG       DC    X'7FEFFFC03C00C00C00C1FC1FC03C03FFF7FE'                          
LH       DC    X'C03C03C03C03C03FFFFFFC03C03C03C03C03'                          
LI       DC    X'FFFFFF060060060060060060060060FFFFFF'                          
LJ       DC    X'003003003003003003003003003C03FFF7FE'                          
LK       DC    X'C03C06C0CC18C30FE0FE0C30C18C0CC06C03'                          
LL       DC    5X'C00C00'                                                       
         DC    X'FFFFFF'                                                        
LM       DC    X'C03E07F0FD9BCF3C63C03C03C03C03C03C03'                          
LN       DC    X'C03E03F03D83CC3C63C33C1BC0FC07C03C01'                          
LO       DC    X'7FEFFF'                                                        
         DC    4X'C03C03'                                                       
         DC    X'FFF7FE'                                                        
LP       DC    X'FFEFFFC03C03C03FFFFFEC00C00C00C00C00'                          
LQ       DC    X'7FEFFFC03C03C03C03C03C03C0FC0FFFF7FF'                          
LR       DC    X'FFEFFFC03C03C03FFFFFEC30C18C0CC06C03'                          
LS       DC    X'7FEFFFC03C00E007FC3FE007003C03FFF7FE'                          
LT       DC    X'FFFFFF'                                                        
         DC    5X'060060'                                                       
LU       DC    5X'C03C03'                                                       
         DC    X'FFF7FE'                                                        
LV       DC    X'C03C0360660630C30C1981980F00F0060060'                          
LW       DC    3X'C03C03'                                                       
         DC    X'C63CF3D9BF0FE07C03'                                            
LX       DC    X'C03C0360630C1980F00F019830C606C03C03'                          
LY       DC    X'C03C0360630C1980F0060060060060060060'                          
LZ       DC    X'FFFFFF00600C0180300600C01803007FFFFF'                          
N0       DC    X'3FC7FEC03C03C03C03C03C03C03C037FE3FC'                          
N1       DC    X'0300700F00300300300300300300303FF3FF'                          
N2       DC    X'7FEFFFC030030077FEFFCC00C00C00FFFFFF'                          
N3       DC    X'FFFFFE00C01803007E0FF003003C03FFF7FE'                          
N4       DC    X'0780D8198318618C18FFFFFF018018018018'                          
N5       DC    X'FFFFFFC00C00C00FFCFFE007003C03FFF7FE'                          
N6       DC    X'0180300600C01803FC7FEE07C03C03FFF7FE'                          
N7       DC    X'FFFFFF00600C0180300600C0180300600C00'                          
N8       DC    X'7FEFFFC03C03E077FE7FEE07C03C03FFF7FE'                          
N9       DC    X'7FEFFFC03C03E037FE3FC0180300600C0180'                          
         EJECT                                                                  
WORK1    EQU   2                   WORK REGISTERS                               
WORK2    EQU   3                                                                
WORK3    EQU   4                                                                
WORK4    EQU   5                                                                
WORK5    EQU   6                                                                
WORK6    EQU   7                                                                
WORK7    EQU   8                                                                
LINPTR   EQU   9                   CURRENT POSITION ON THE LINE                 
         SPACE 1                                                                
CHARA    EQU   C'A'                FOR LARGEPRT INPUT VALIDATION                
CHARI    EQU   C'I'                                                             
CHARJ    EQU   C'J'                                                             
CHARR    EQU   C'R'                                                             
CHARS    EQU   C'S'                                                             
CHARZ    EQU   C'Z'                                                             
CHAR0    EQU   C'0'                                                             
CHAR9    EQU   C'9'                                                             
CHAR$    EQU   C'$'                                                             
         SPACE 1                                                                
ADJ1     EQU   X'C1'               USED BY LARGEPRT TO ADJUST AN INPUT          
ADJ2     EQU   X'C8'                                                            
ADJ3     EQU   X'D0'                                                            
ADJ4     EQU   X'D6'                                                            
MATMASK  EQU   X'80'               MASK TO PRINT BLOCK LETTERS                  
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDLOZENGE 05/01/02'                                      
         END                                                                    
