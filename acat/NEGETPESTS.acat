*          DATA SET NEGETPESTS AT LEVEL 005 AS OF 05/01/02                      
*CATALP GETPEST                                                                 
         TITLE 'GETPEST - GETS PRODUCT/ESTIMATE TARGETS'                        
         SPACE 2                                                                
************************************************************                    
*                                                                               
*                                                                               
* DATE CHECK REMOVED FROM ESTIMATE RECORD READ                                  
* ELSE IT WOULD REJECT ESTIMATES IF START/END WERE < EST DATE RANGE             
*  11/10/92 PXZ                                                                 
*                                                                               
*                                                                               
******************************************************************              
         SPACE 2                                                                
GETPEST  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 200,**PEST**                                                     
         USING MYD,RC                                                           
         MVC   USERRD,4(RD)                                                     
         L     R9,0(R1)            PARAMETER 1 A(GETPEST) BLOCK                 
         USING GETPESTD,R9                                                      
         L     RA,4(R1)            PARAMETER 2 A(NETBLOCK)                      
         USING NETBLOCK,RA                                                      
         SPACE 1                                                                
         CLC   GTPBAMC,NBACTAM     IF WE HAVE NOT YET GOT THIS                  
         BE    *+8                 CLIENT INTO THE BLOCK                        
         BAS   RE,FILL             GO AND FILL                                  
         MVC   GTPBAMC,NBACTAM                                                  
         BAS   RE,LOOK             NOW LOOK UP FOR ENTRY                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO FILL GETPEST BUFFER                                   
         SPACE 3                                                                
FILL     NTR1                                                                   
         LA    R1,GTPBDATA         INTIALIZE FOR USER'S BUFFER                  
         ST    R1,ATHISENT                                                      
         MVI   0(R1),0                                                          
         L     R1,GTPBLEN                                                       
         SH    R1,=H'12'                                                        
         ST    R1,LEFT                                                          
         SPACE 1                                                                
         MVC   THISPROD,=C'POL'    DO POOL FIRST                                
         MVI   THISPNO,255                                                      
         BAS   RE,FILLEST                                                       
         XC    THISPROD,THISPROD                                                
         SPACE 1                                                                
FILL2    LA    R4,KEY              GET NEXT PRODUCT                             
         USING PKEY,R4                                                          
         XC    KEY,KEY                                                          
         MVC   PKEYAM(3),NBACTAM                                                
         AI    THISPROD+2,1                                                     
         MVC   PKEYPRD,THISPROD                                                 
         BAS   RE,HIGH                                                          
         CLC   KEY(4),KEYSAVE      CHECK FOR SAME CLIENT                        
         BNE   XIT                                                              
         MVC   THISPROD,PKEYPRD                                                 
         CLC   THISPROD,=C'POL'    POOL WE HAVE DONE ALREADY                    
         BE    FILL2                                                            
         LA    R4,IO                                                            
         BAS   RE,GETREC                                                        
         MVC   THISPNO,PCODE+1     PICK UP PRODUCT NUMBER                       
         ZIC   R1,THISPNO                                                       
         LA    R2,NBPRDMSK                                                      
         BAS   RE,TESTMASK         MAY FILTER PRODUCT                           
         BNE   FILL2                                                            
         BAS   RE,FILLEST                                                       
         B     FILL2                                                            
         EJECT                                                                  
*              ROUTINE TO GET ESTIMATES FOR THISPROD                            
         SPACE 3                                                                
*              INPUT               THISPROD = PRESENT PRODUCT ALPHA             
*                                  THISPNO  = PRESENT PRODUCT NUMBER            
         SPACE 1                                                                
FILLEST  NTR1                                                                   
         XC    DEFAULT,DEFAULT                                                  
         MVC   THISEST,NBSELEST                                                 
         CLI   THISEST,0                                                        
         BE    FILLEST2                                                         
         ZIC   R1,NBSELEST                                                      
         BCTR  R1,0                                                             
         STC   R1,THISEST          START AT REQ-1                               
         SPACE 1                                                                
FILLEST2 LA    R4,KEY              BUILD ESTIMATE KEY                           
         USING EKEY,R4                                                          
         CLI   THISEST,255                                                      
         BE    XIT                                                              
         AI    THISEST,1                                                        
         XC    KEY,KEY                                                          
         MVC   EKEYAM(3),NBACTAM                                                
         MVC   EKEYPRD,THISPROD                                                 
         MVC   EKEYEST,THISEST                                                  
         BAS   RE,HIGH                                                          
         CLC   KEY(7),KEYSAVE      CHECK FOR SAME PRODUCT                       
         BNE   XIT                                                              
         MVC   THISEST,EKEYEST                                                  
         ZIC   R1,THISEST                                                       
         LA    R2,NBESTMSK                                                      
         BAS   RE,TESTMASK         MAY FILTER ESTIMATE                          
         BNE   FILLEST2                                                         
         LA    R4,IO                                                            
         BAS   RE,GETREC                                                        
**       CLC   EEND,NBSELSTR                                                    
**       BL    FILLEST2                                                         
**       CLC   ESTART,NBSELEND                                                  
**       BH    FILLEST2                                                         
         BAS   RE,POSTEST                                                       
         CLI   NBSELEST,0          IF ONLY 1 REQUESTED                          
         BE    FILLEST2                                                         
         CLI   NBSELESE,0                                                       
         BNE   FILLEST2                                                         
         B     XIT                 WE'RE DONE                                   
         EJECT                                                                  
*              ROUTINE TO POST TARGETS FOR THIS ESTIMATE                        
         SPACE 3                                                                
*              INPUT               THISPNO = PRESENT PRODUCT NUMBER             
*                                  THISEST = PRESENT ESTIMATE NUMBER            
         SPACE 1                                                                
POSTEST  NTR1                                                                   
         USING GTPBENTD,R2                                                      
         MVC   THISENT(1),THISPNO  BUILD PROD/EST/TARGET ENTRY                  
         MVC   THISENT+1(1),THISEST                                             
         MVC   THISENT+2(3),EDEMLST                                             
         MVC   THISENT+5(3),EDEMLST+3                                           
         CLC   THISPNO,DEFAULT     IS THIS THE FIRST FOR PRODUCT                
         BE    POSTEST2                                                         
         MVI   THISENT+1,0                                                      
         MVC   DEFAULT,THISENT     YES SO SAVE THE FIRST ENTRY                  
         B     POSTEST4                                                         
         SPACE 1                                                                
POSTEST2 CLC   THISENT+2(6),DEFAULT+2   CHECK IF TARGETS THE SAME               
         BE    XIT                 YES - SO NO NEED TO POST                     
         SPACE 1                                                                
POSTEST4 L     R1,LEFT                                                          
         SH    R1,=H'8'            IS THERE ANY ROOM FOR ANOTHER ENTRY          
         BNP   XIT                                                              
         ST    R1,LEFT                                                          
         L     R1,ATHISENT                                                      
         MVC   0(8,R1),THISENT                                                  
         LA    R1,8(R1)                                                         
         MVI   0(R1),0                                                          
         ST    R1,ATHISENT                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO LOOK UP BUFFER                                        
         SPACE 3                                                                
LOOK     NTR1                                                                   
         LA    R2,GTPBDATA                                                      
         USING GTPBENTD,R2                                                      
         SPACE 1                                                                
LOOK2    CLI   0(R2),0                                                          
         BE    XIT                                                              
*                                  NEED TO MATCH ON PRODUCT                     
         CLI   GTPBPRO,X'FF'       ACCEPT POOL                                  
         BE    LOOK4                                                            
         CLC   GTPBPRO,NBSPLPRN           OR MATCH ON PRODUCT                   
         BNE   LOOKNEXT                                                         
         SPACE 1                                                                
*                                  NEED TO MATCH ON ESTIMATE                    
LOOK4    CLI   GTPBEST,0           ACCEPT DEFAULT                               
         BE    LOOK6                                                            
         CLC   GTPBEST,NBACTEST           OR MATCH ON ESTIMATE                  
         BNE   LOOKNEXT                                                         
         SPACE 1                                                                
LOOK6    ST    R2,GTPBAENT         FOUND AN ENTRY - SAVE ADDRESS                
         SPACE 1                                                                
LOOKNEXT LA    R2,8(R2)                                                         
         B     LOOK2                                                            
         DROP  R2                                                               
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'SPTDIR  '         DIRECTORIES                         
         LA    R2,IO                                                            
         GOTO1 NBDM,DMCB,COMMAND,FILE,KEY,(R2),0                                
         BAS   RE,DMCHECK                                                       
         MVC   KEY,0(R2)                                                        
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         LA    R3,KEY+14                                                        
         MVC   FILE(8),=C'SPTFILE '     FILE                                    
         MVC   NBDTADSP,=H'24'                                                  
         SPACE 1                                                                
GETREC2  LA    R2,IO                                                            
         GOTO1 NBDM,DMCB,(X'80',=C'GETREC'),FILE,(R3),(R2),DMWORK               
         BAS   RE,DMCHECK                                                       
         B     XIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0                                                          
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
         GETEL (R6),NBDTADSP,ELCODE                                             
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO TEST MASK                                             
         SPACE 3                                                                
TESTMASK NTR1                                                                   
*                                  R1=TEST NUMBER                               
*                                  R2=A(MASK)                                   
         SR    R0,R0                                                            
         SLDL  R0,29                                                            
         SRL   R1,29                                                            
         AR    R2,R0                                                            
         LA    R1,BITLIST(R1)                                                   
         MVC   BITTEST,0(R2)                                                    
         NC    BITTEST,0(R1)                                                    
         CLI   BITTEST,0                                                        
         BE    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
BITLIST  DC    X'8040201008040201'                                              
         SPACE 1                                                                
RELO     DS    A                                                                
         EJECT                                                                  
*              DSECTS ETC                                                       
         SPACE 3                                                                
*                                                                               
MYD      DSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARAS    DS    0F                                                               
DMCB     DS    6F                                                               
USERRD   DS    F                                                                
KEY      DS    CL18                                                             
KEYSAVE  DS    CL18                                                             
ELCODE   DS    CL1                                                              
BITTEST  DS    CL1                                                              
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
DMWORK   DS    CL96                                                             
ATHISENT DS    A                                                                
LEFT     DS    F                                                                
THISPROD DS    CL3                                                              
THISPNO  DS    XL1                                                              
THISEST  DS    XL1                                                              
THISENT  DS    XL8                                                              
DEFAULT  DS    XL8                                                              
IO       DS    CL1000                                                           
         EJECT                                                                  
       ++INCLUDE NEGETPESTD                                                     
         SPACE 1                                                                
*        INCLUDE NETBLOCKD                                                      
*        INCLUDE SPGENCLT                                                       
*        INCLUDE SPGENPRD                                                       
*        INCLUDE SPGENEST                                                       
         PRINT OFF                                                              
       ++INCLUDE NETBLOCKD                                                      
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENEST                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005NEGETPESTS05/01/02'                                      
         END                                                                    
