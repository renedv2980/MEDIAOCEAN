*          DATA SET CPREPRUNM  AT LEVEL 032 AS OF 11/15/07                      
*CATALP CPRUNMST                                                                
         TITLE 'MODULE TO CONTROL COPPER RUN'                                   
CPRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,CPRUNCON,=V(CPSAVE)                                            
         L     RA,=V(CPWORKC)                                                   
         USING CPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING CPWORKD+4096,R9                                                  
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R7,=V(MASTC)                                                     
         USING MASTD,R7                                                         
         EJECT                                                                  
*              INITIAL CONTROL CARDS                                            
         SPACE 2                                                                
READCARD MVC   MCAPHAS2,APPLIC                                                  
         MVC   MCAPHAS1,SPECS                                                   
         ST    RA,MCAWORK                                                       
         GOTO1 =V(RUNSTART)                                                     
         MVC   APPLIC,MCAPHAS2                                                  
         MVC   SPECS,MCAPHAS1                                                   
         MVC   RCTRACE,MCTRACE     TRANSFER MASTER VALUES                       
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCAGYCRD,MCUSER                                                  
         MVC   RCAGENCY,MCUSER                                                  
         MVC   ORIGINUM,MCORIGID                                                
         SPACE 2                                                                
         MVI   RCFLONLY,C'Y'       GO TO EXECUTE I/O AND RUN SPECS              
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,C'N'                                                    
         B     OPEN2                                                            
         EJECT                                                                  
*              OPEN FILE ROUTINES                                               
         SPACE 3                                                                
OPEN2    L     RE,UTL                                                           
         MVI   4(RE),X'0C'       THERE IS ONLY ONE CPP SYSTEM                   
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'CPP',FILELIST                           
         SPACE 2                                                                
* OPEN SPOT XXX DEMO FILES AS PER MEL                                           
         LA    RE,P1                                                            
         LA    RF,1320                                                          
         XCEF                                                                   
         LA    RE,P1                                                            
         USING CND,RE                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,RCAGENCY                                                   
         XC    DMCB+8,DMCB+8                                                    
         GOTO1 =V(CONFID),DMCB,(0,P1),(30,DMCB+8)                               
         LA    RE,P1                                                            
         L     RF,UTL              SAVE CURRENT UTL                             
         CLI   CNSSE,0                                                          
         BNE   *+12                                                             
         LA    RE,L'CNLEN(RE)                                                   
         B     *-12                                                             
         MVC   4(1,RF),CNSSE       SET UTL FOR SPOT                             
         MVC   RCSSE,CNSSE         SET SPOT SE                                  
         MVC   RCBAGYMD,CNSCD      SET SPOT AGY NUMBER                          
         OI    RCBAGYMD,X'01'      SET MEDIA TO TV                              
         LA    RE,P1                                                            
         LA    RF,1320                                                          
         XCEF                                                                   
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'SPOT',DEMOFILE                          
         L     RE,UTL                                                           
         MVI   4(RE),X'0C'       THERE IS ONLY ONE CPP SYSTEM                   
         LA    RE,P1                                                            
         LA    R0,10                                                            
         MVC   0(L'P1,RE),P12                                                   
         LA    RE,L'P1(RE)                                                      
         BCT   R0,*-10                                                          
*                                                                               
         L     R2,=V(SSB)                                                       
         MVI   2(R2),X'FF'         SET OFFLINE FLAG IN SSB                      
*                                                                               
         MVI   BYTE,QDEMAND                                                     
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMAND-COMFACSD(4,RF),4(R1)                                     
*                                                                               
         MVI   BYTE,QDEMOUT                                                     
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMOUT-COMFACSD(4,RF),4(R1)                                     
*                                                                               
         MVI   BYTE,QDEMADDR                                                    
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMADDR-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMAINT                                                    
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMAINT-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMAINT                                                    
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMAINT-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMEL                                                      
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMEL-COMFACSD(4,RF),4(R1)                                      
*                                                                               
         MVI   BYTE,QDEMOVAL                                                    
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMOVAL-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMOMTH                                                    
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMOMTH-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEFINE                                                     
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEFINE-COMFACSD(4,RF),4(R1)                                     
*                                                                               
         MVI   BYTE,QDEMTABS                                                    
         BAS   RE,CPLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMTABS-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         B     ANYDIV                                                           
         SPACE 2                                                                
FILELIST DC    CL8'NCPFILE'                                                     
         DC    CL8'NCTFILE'                                                     
         DC    C'X'                                                             
*                                                                               
DEMOFILE DC    CL8'NDEMDIRN'                                                    
         DC    CL8'NL=DEMFN'                                                    
         DC    CL8'NDEMDIRA'                                                    
         DC    CL8'NL=DEMFA'                                                    
         DC    CL8'NSPTFILE'                                                    
         DC    CL8'NSPTDIR'                                                     
         DC    CL8'NSTAFILE'                                                    
         DC    C'X'                                                             
         SPACE 3                                                                
ANYDIV   DS    0H                                                               
         CLI   RCREQSUM,C'Y'       IS A DIVIDER WANTED                          
         BNE   GOREQS                                                           
         GOTO1 LOGO,DMCB,LOGOC                                                  
         SPACE 2                                                                
GOREQS   DS    0H                                                               
         GOTO1 FILCON                                                           
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
         SPACE 2                                                                
FINALISE CLI   RCREQSUM,C'Y'                                                    
         BNE   CLOSE1                                                           
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC     PRINT END LOGOS                              
         SPACE 2                                                                
CLOSE1   GOTO1 PRINT,DMCB,=C'CLOSE'                                             
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'CPP'                                  
         SPACE 2                                                                
GOEND    DS    0H                                                               
         LTR   R2,R2                                                            
         BZ    NORMEND                                                          
         ABEND 99,DUMP                                                          
         SPACE 2                                                                
NORMEND  DS    0H                                                               
         XBASE                                                                  
         SPACE 3                                                                
CPLOAD   LR    R0,RE                                                            
         MVC   MCDUB,SPACES                                                     
         MVC   MCDUB(4),=C'T00A'                                                
         GOTO1 MCVHEXOU,DMCB,BYTE,MCDUB+4,1,0                                   
         GOTO1 MCVLOADM,DMCB,0                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
         PRINT OFF                                                              
         EJECT                                                                  
       ++INCLUDE CPREPWORKD                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDCNTRL                                                        
       ++INCLUDE DDCOREQUS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'032CPREPRUNM 11/15/07'                                      
         END                                                                    
