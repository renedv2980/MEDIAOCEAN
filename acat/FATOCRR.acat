*          DATA SET FATOCRR    AT LEVEL 005 AS OF 05/01/02                      
*CATALP FATOCRR                                                                 
         TITLE 'TOCRR - 2260 COURIER TERMINAL OUTPUT TRANSLATOR'                
TOCRR    CSECT                                                                  
         DS    1600C                                                            
         ORG   *-1600                                                           
         NMOD1 16,**TOCRR                                                       
         USING OUTD,RC                                                          
         LM    R2,R3,0(R1)         P1 A(TWA)                                    
*                                  P2 A(TIA)                                    
*                                     AFTER                                     
*                                  P1 BYTE 0 X'FF' = TWAERROR                   
         EJECT                                                                  
*                  CHECK INTEGRITY OF TWA                                       
         SPACE 3                                                                
         XC    CHECK,CHECK                                                      
         XC    FORMLINE,FORMLINE                                                
         LA    R4,64(R2)                                                        
         SR    R5,R5                                                            
         LR    R6,R4               SAVE LAST FIELD ADDRESS                      
         SPACE 2                                                                
TC2      CLI   0(R4),0                                                          
         BE    TC2A                                                             
         IC    R5,0(R4)                                                         
         CH    R5,=H'9'                                                         
         BNL   *+6                                                              
         DC    H'0'                                                             
         CLC   2(2,R4),=H'1920'    CHECK ADDRESS NOT GREATER THAN               
         BNH   *+6                                                              
         DC    H'0'                                                             
         CLC   2(2,R4),CHECK       CHECK THAT ADDRESSES ASCEND                  
         BH    *+6                                                              
         DC    H'0'                                                             
         TM    7(R4),X'80'         IS THIS FIELD TO BE FORMATTED                
         BNO   TCNOFORM                                                         
         NI    7(R4),X'7F'                                                      
         LH    RE,2(R4)            WORK OUT WHAT LINE ITS ON                    
         SRDA  RE,32                                                            
         D     RE,=F'80'                                                        
         LA    RF,FORMLINE(RF)     LEAVE A NOTE OF THIS                         
         MVI   0(RF),1                                                          
         SPACE 2                                                                
TCNOFORM MVC   CHECK(2),2(R4)                                                   
         LR    R6,R4               SAVE LAST FIELD ADDR                         
         AR    R4,R5                                                            
         B     TC2                                                              
         SPACE 2                                                                
TWAERROR MVI   0(R1),X'FF'                                                      
         B     TCEXT                                                            
*                                                                               
* TEST LAST FIELD ON LINE 24 - IF SO ADD EOM TO MESSAGE                         
*                                                                               
TC2A     TM    1(R6),X'20'         TEST PROTECTED FIELD                         
         BZ    TC3                 NO                                           
         CLC   2(2,R6),=H'1840'    LINE 24 STARTS AT 1840                       
         BL    TC3                                                              
         IC    R5,0(R6)                                                         
         AR    R6,R5                                                            
         BCTR  R6,0                                                             
         MVI   0(R6),EOM                                                        
         EJECT                                                                  
*                  CHECK IF ALL FIELDS ARE TO BE TRANSMITTED                    
         SPACE 3                                                                
TC3      MVC   INDICS(2),1(R4)     SAVE GENCER INDICS                           
         OC    FORMLINE,FORMLINE                                                
         BZ    *+8                                                              
         BAS   RE,REFORM                                                        
         CLI   0(R1),1             CHECK OPTION TO OUTPUT ALL                   
         BNE   CHKIND                                                           
         MVI   INDICS,1                                                         
         B     TRANSALL                                                         
         SPACE 2                                                                
CHKIND   EQU   *                                                                
         OC    1(2,R4),1(R4)       ARE GENCER INDICS ON                         
         BZ    TC3A                                                             
         XC    1(2,R4),1(R4)                                                    
         MVI   0(R1),1             TELL LCM TO WRITE ERASE                      
         MVI   0(R2),0                                                          
         SPACE 2                                                                
TRANSALL LA    R4,64(R2)                                                        
         SPACE 2                                                                
ALLOOP   CLI   0(R4),0             NOW LOOP AND TURN ON TRANSMIT BITS           
         BE    TC3B                FOR ALL UNPROTECTED FIELDS AND               
         NI    6(R4),X'7F'         PROTECTED FIELDS WITH SIGNIFICANT            
         TM    1(R4),X'20'         DATA (NOT ZEROS OR BLANKS)                   
         BZ    YES                                                              
         SR    R6,R6                                                            
         IC    R6,7(R4)                                                         
         LTR   R6,R6                                                            
         BZ    NO                                                               
         BCTR  R6,R0                                                            
         EX    R6,ZERCOMP                                                       
         BE    NO                                                               
         CLI   8(R4),C' '                                                       
         BNE   YES                                                              
         LTR   R6,R6                                                            
         BZ    NO                                                               
         BCTR  R6,R0                                                            
         EX    R6,BLNKCOMP                                                      
         BE    NO                                                               
         SPACE 2                                                                
YES      OI    6(R4),X'80'                                                      
         SPACE 2                                                                
NO       EQU   *                                                                
         IC    R5,0(R4)                                                         
         AR    R4,R5                                                            
         B     ALLOOP                                                           
         SPACE 3                                                                
TC3A     LR    R5,R3               LCM WILL WRITE AT LINE 1                     
         MVI   0(R5),STX                                                        
         MVI   1(R5),AL1                                                        
         LA    R5,1(R5)                                                         
         B     TC4                                                              
         SPACE 2                                                                
TC3B     LR    R5,R3                                                            
         MVI   0(R5),STX                                                        
         EJECT                                                                  
*                  TWA HEADER HANDLING                                          
         SPACE 3                                                                
TC4      LA    R4,64(R2)           INITIALISE                                   
         ZAP   LINE,=P'1'                                                       
         ZAP   COL,=P'2'                                                        
         MVI   1(R5),ESC           SET FORMAT MODE                              
         MVI   2(R5),C'C'                                                       
         MVI   3(R5),FF            SKIP PAGE FOR LOCAL PRINTING                 
         LA    R5,4(R5)                                                         
         ST    R5,CHECK                                                         
         ST    R5,LOL                                                           
         B     TC8                                                              
         SPACE 2                                                                
TC6      SR    R6,R6               BUMP TO NEXT TWA HEADER                      
         IC    R6,0(R4)                                                         
         AR    R4,R6                                                            
         SPACE 2                                                                
TC8      CLI   0(R4),0             CHECK TWA END                                
         BE    TC11                                                             
         MVC   DUB(2),2(R4)        LINE/COL ADDRESS                             
         LH    R7,DUB                                                           
         SR    R6,R6                                                            
         D     R6,=F'80'           CALCULATE LINE & COLUMN FOR FIELD            
         LA    R6,1(R6)                                                         
         LA    R7,1(R7)                                                         
         CVD   R6,DUB                                                           
         ZAP   FCOL,DUB                                                         
         CVD   R7,DUB                                                           
         ZAP   FLINE,DUB                                                        
         TM    1(R4),X'20'                                                      
         BO    TC30                                                             
         SPACE 2                                                                
TC10     ZAP   DUB,LINE            IF PREVIOUS FIELD WAS BEING                  
         CVB   RF,DUB              REFORMATTED, SKIP IS NOT ALLOWED             
         LA    RF,FORMLINE-1(RF)                                                
         CLI   0(RF),0                                                          
         BNE   TC30                                                             
         OC    INDICS(2),INDICS    THIS IS AN UNPROTECTED FIELD - WE            
         BNZ   TC30                CAN BENEFIT FROM AN HT AS LONG AS            
*                                  THIS IS NOT THE FIRST TRANSMISSION           
         L     R5,CHECK            YES - MOVE HT OVER NON-TRANS FIELD           
         MVI   0(R5),HT                                                         
         LA    R5,1(R5)                                                         
         ST    R5,CHECK                                                         
         ST    R5,LOL                                                           
         ZAP   LINE,FLINE          WE HAVE JUST SKIPPED TO AN                   
         ZAP   COL,FCOL            UNPROTECTED FIELD - SET LINE,COL             
         B     TC40                                                             
         EJECT                                                                  
*                  ROUTINES FOR END OF TWA                                      
         SPACE 3                                                                
TC11     BAS   RE,ANYFILL                                                       
         C     R5,CHECK                                                         
         BNE   TC12                                                             
         CP    LINE,=P'24'         WE NEED A TAB UNLESS SCREEN IS               
         BH    TC16                ALREADY FILLED AND CURSOR WILL               
         BL    TC11B               BE POSITIONED AT HOME ADDRESS.               
         CP    COL,=P'80'          IF IT'S ON ROW 24 COLUMN 80 WE               
         BNE   TC11B               CAN SEND A SPACE AS HT DOES'NT               
         MVI   0(R5),C' '          SEEM TO WORK                                 
         LA    R5,1(R5)                                                         
         B     TC16                                                             
         SPACE 2                                                                
TC11B    DS    0H                                                               
         SPACE 2                                                                
TC12     L     R5,CHECK            YES - MOVE IN OVER NON-TRANS FIELD           
         MVI   0(R5),HT                                                         
         LA    R5,1(R5)                                                         
         SPACE 2                                                                
TC14     CLI   0(R4),0                                                          
         BE    TC16                                                             
         ZAP   LINE,FLINE          WE HAVE JUST SKIPPED TO AN                   
         ZAP   COL,FCOL            UNPROTECTED FIELD - SET LINE,COL             
         B     TC40                                                             
         SPACE 2                                                                
TC16     LA    R4,64(R2)           RESET TO START                               
         ZAP   DUB,LINE            KEEP TRACK OF LAST LINE FORMATTED            
         CVB   R1,DUB                                                           
         CP    COL,=P'1'                                                        
         BNE   *+6                                                              
         BCTR  R1,0                                                             
         STC   R1,0(R2)            SAVE THIS IN TWA                             
         SPACE 2                                                                
TC18     CLI   0(R4),0                                                          
         BNE   TC24                                                             
         MVI   0(R5),HT            NO IC FOUND - SKIP TO TOP                    
         MVI   1(R5),HT                        - SKIP TO FIRST                  
         LA    R5,1(R5)                                                         
         B     TC22                            AND BYPASS LOCK TEST             
         SPACE 2                                                                
TC20     MVI   0(R5),HT            LOCK KEYBOARD FEATURE                        
         TM    6(R4),X'02'                                                      
         BZ    TC22                                                             
         MVI   1(R5),ESC                                                        
         MVI   2(R5),C'I'                                                       
         LA    R5,2(R5)                                                         
         SPACE 2                                                                
TC22     MVI   1(R5),ETX                                                        
         LA    R5,2(R5)                                                         
         LR    R6,R3                                                            
         SR    R5,R6                                                            
         SH    R3,=H'2'                                                         
         STH   R5,0(R3)            STORE L'OUTPUT BEHIND BUFFER                 
         SPACE 2                                                                
TCEXT    XMOD1 1                                                                
         SPACE 2                                                                
TC24     TM    1(R4),X'20'         LOOK FOR IC ON UNPROTECTED FIELDS            
         BO    TC28                                                             
         TM    6(R4),X'40'                                                      
         BZ    TC26                                                             
         NI    6(R4),X'BF'         FOUND - TURN BIT OFF                         
         B     TC20                                                             
         SPACE 2                                                                
TC26     MVI   0(R5),HT            UNPROTECTED - SET AN HT                      
         LA    R5,1(R5)                                                         
         SPACE 2                                                                
TC28     SR    R6,R6                                                            
         IC    R6,0(R4)                                                         
         AR    R4,R6                                                            
         B     TC18                                                             
         EJECT                                                                  
*                  HANDLE LINE FEED AND FILLERS                                 
         SPACE 3                                                                
TC30     CP    FLINE,LINE          IS IT ON THE SAME LINE                       
         BNL   *+6                 PREVIOUS - ERROR                             
         DC    H'0'                                                             
         BE    TC32                                                             
         BAS   RE,ANYFILL                                                       
         CLI   FORMSW,C'Y'                                                      
         BE    TC30                                                             
         C     R5,LOL              WE MAY BE ABLE TO SAVE SOME                  
         BE    TC31                TRANSMISSION BY PUTTING A LF                 
         ST    R5,ANYROOM          INTO THE PREVIOUS LINE - SO                  
         L     R5,LOL              WE WILL SCAN FOR A SPACE/X'00'               
         SPACE 2                                                                
TC30A    CLI   0(R5),C' '                                                       
         BE    TC31                                                             
         CLI   0(R5),0                                                          
         BE    TC31                                                             
         LA    R5,1(R5)                                                         
         C     R5,ANYROOM                                                       
         BNE   TC30A                                                            
         SPACE 2                                                                
TC31     MVI   0(R5),LF            WE NOW KNOW WHERE TO PUT THE LF              
         LA    R5,1(R5)                                                         
         ST    R5,LOL                                                           
         SPACE 2                                                                
TC31B    AP    LINE,=P'1'          AND RESET LINE AND COLUMN                    
         ZAP   COL,=P'1'                                                        
         B     TC30                                                             
         SPACE 2                                                                
TC32     CP    FCOL,COL            NOW COMPARE COLUMNS                          
         BNL   *+6                                                              
         DC    H'0'                                                             
         BE    TC36                                                             
         ZAP   DUB,FCOL            WE NEED A FILLER - GENERATE SPACES           
         SP    DUB,COL                                                          
         CVB   R6,DUB                                                           
         MVC   COL,FCOL                                                         
         MVI   0(R5),C' '                                                       
         CH    R6,=H'1'            CHECK FOR 1 SPACE ONLY                       
         BE    TC34                                                             
         SH    R6,=H'2'                                                         
         EX    R6,VARFILL          MORE THAN ONE                                
         LA    R5,2(R6,R5)                                                      
         B     TC36                                                             
         SPACE 2                                                                
TC34     LA    R5,1(R5)                                                         
         SPACE 2                                                                
TC36     TM    1(R4),X'20'         WE NEED TO INSERT AN SOF                     
         BO    TC40                BEFORE EACH UNPROTECTED FIELD                
         BCTR  R5,R0                                                            
         MVI   0(R5),SOF                                                        
         LA    R5,1(R5)                                                         
         B     TC40                                                             
         SPACE 2                                                                
VARFILL  MVC   1(0,R5),0(R5)                                                    
         EJECT                                                                  
*                  NOW HANDLE MESSAGE OUTPUT                                    
         SPACE 3                                                                
TC40     SR    R6,R6                                                            
         IC    R6,0(R4)                                                         
         SH    R6,=H'8'            TOTAL MESSAGE LENGTH                         
         LTR   R6,R6                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LR    R7,R6                                                            
         BCTR  R6,R0                                                            
         EX    R6,VARCLEAR                                                      
         SPACE 2                                                                
         EX    R6,VARMESS                                                       
         AR    R5,R7               BUMP OUTPUT POINTER                          
         CVD   R7,DUB                                                           
         AP    COL,DUB             AND COLUMN NUMBER                            
         B     TC50                                                             
         SPACE 2                                                                
ZERCOMP  OC    8(0,R4),8(R4)                                                    
BLNKCOMP CLC   9(0,R4),8(R4)                                                    
         SPACE 2                                                                
VARMESS  MVC   0(0,R5),8(R4)                                                    
VARCLEAR XC    0(0,R5),0(R5)                                                    
         EJECT                                                                  
*                  COMPLETE UNPROTECTED, SET LINE, HANDLE TRANSMITS             
         SPACE 2                                                                
TC50     TM    1(R4),X'20'         UNPROTECTED FIELDS                           
         BO    TC52                                                             
         CP    COL,=P'80'          MUST HAVE SPACE                              
         BNH   *+6                                                              
         DC    H'0'                                                             
         MVI   0(R5),EOF           FOR EOF CHARACTER                            
         AP    COL,=P'1'                                                        
         LA    R5,1(R5)                                                         
         ST    R5,LOL              DONT ALLOW LF IN UNPROTECTED FIELDS          
         SPACE 2                                                                
TC52     CP    COL,=P'80'          FIND WHAT LINE WE ARE ON                     
         BNH   TC54                                                             
         SP    COL,=P'80'                                                       
         AP    LINE,=P'1'                                                       
         TM    6(R4),X'80'         TEST XMT THIS TIME                           
         BO    TC53                YES                                          
         C     R5,LOL              AS WE'RE NOT TRANSMITTING THIS FIELD         
         BE    TC53                WE CAN PUT IN AN LF.                         
         ST    R5,ANYROOM                                                       
         L     R5,LOL                                                           
         SPACE 2                                                                
TC52A    CLI   0(R5),C' '          IF WE CAN FIND A BLANK OR A NULL             
         BE    TC52B                                                            
         CLI   0(R5),0                                                          
         BE    TC52B                                                            
         LA    R5,1(R5)                                                         
         C     R5,ANYROOM                                                       
         BNE   TC52A                                                            
         SPACE 2                                                                
TC52B    MVI   0(R5),LF                                                         
         LA    R5,1(R5)                                                         
         ST    R5,LOL                                                           
         SPACE 2                                                                
TC53     ST    R5,LOL                                                           
         B     TC52                                                             
         SPACE 2                                                                
TC54     TM    6(R4),X'80'         DOES THIS FIELD HAVE                         
         BZ    TC6                 TO BE TRANSMITTED                            
         NI    6(R4),X'7F'                                                      
         ST    R5,LOL                                                           
         ST    R5,CHECK            THEN THIS IS THE LAST POSITION               
         B     TC6                 FROM WHICH WE CAN SKIP LATER                 
         SPACE 3                                                                
*                                  IF REQUESTED LENGTH IS ZERO                  
*                                  SET LENGTHS TO NUMBER                        
*                                  OF SIGNIFICANT BYTES IN DATA FIELD           
         EJECT                                                                  
*              ROUTINE TO REFORMAT TWA FOR NEW FIELDS                           
         SPACE 3                                                                
REFORM   NTR1                                                                   
*                                  R2=A(TWA)                                    
         LA    R4,64(R2)                                                        
         SR    R5,R5                                                            
         SPACE 2                                                                
RE2      CLI   0(R4),0             END OF SCREEN                                
         BNE   XIT                                                              
         LH    RE,2(R4)                                                         
         SRDA  RE,32                                                            
         D     RE,=F'80'                                                        
         LA    RF,FORMLINE(RF)     IS THIS FIELD ON A LINE THAT IS              
         CLI   0(RF),0             GOING TO BE REFORMATTED                      
         BE    RE4                                                              
         OI    6(R4),X'80'         TURN ON TRANSMIT BIT                         
         SPACE 2                                                                
RE4      IC    R5,0(R4)                                                         
         AR    R4,R5                                                            
         B     RE2                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO INSERT SPACES FOR REFORMATTING                        
         SPACE 3                                                                
ANYFILL  DS    0H                                                               
         MVI   FORMSW,C'N'                                                      
         ZAP   DUB,LINE            IS LINE BEING REFORMATTED                    
         CVB   RF,DUB                                                           
         LA    RF,FORMLINE-1(RF)                                                
         CLI   0(RF),1                                                          
         BNE   ANYFILL5                                                         
         MVI   FORMSW,C'Y'                                                      
         SPACE 2                                                                
ANYFILL2 CP    COL,=P'80'          COMPLETE LINE WITH SPACES                    
         BH    ANYFILL4                                                         
         MVI   0(R5),C' '                                                       
         LA    R5,1(R5)                                                         
         AP    COL,=P'1'                                                        
         B     ANYFILL2                                                         
         SPACE 2                                                                
ANYFILL4 AP    LINE,=P'1'                                                       
         ZAP   COL,=P'1'                                                        
         ST    R5,CHECK                                                         
         SPACE 2                                                                
ANYFILL5 CLI   0(R4),0                                                          
         BE    ANYFILL6                                                         
         BR    RE                                                               
         SPACE 2                                                                
ANYFILL6 ZAP   DUB,LINE            AT END OF SCREEN, WE MAY NEED TO             
         CVB   R1,DUB              FILL OUT EXTRA LINES WITH SPACES             
         BCTR  R1,0                                                             
         OC    FORMLINE,FORMLINE                                                
         BZR   RE                                                               
         ZIC   R0,0(R2)            SAVE LAST HIGHEST IN TWA HEADER              
         CH    R0,=H'24'                                                        
         BL    *+8                                                              
         LH    R0,=H'24'                                                        
         CR    R1,R0                                                            
         BNLR  RE                                                               
         SR    R0,R1                                                            
         SPACE 2                                                                
ANYFILL8 MVI   0(R5),C' '                                                       
         MVC   1(79,R5),0(R5)                                                   
         LA    R5,80(R5)                                                        
         BCT   R0,ANYFILL8                                                      
         ST    R5,CHECK                                                         
         BR    RE                                                               
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
         EJECT                                                                  
*                  DSECT FOR OUTPUT TRANSLATOR                                  
         SPACE 3                                                                
OUTD     DSECT                                                                  
DUB      DS    D                                                                
CHECK    DS    F                                                                
LOL      DS    F                                                                
ANYROOM  DS    F                                                                
LINE     DS    PL2                                                              
COL      DS    PL2                                                              
FLINE    DS    PL2                                                              
FCOL     DS    PL2                                                              
INDICS   DS    CL2                                                              
FORMSW   DS    CL1                                                              
FORMLINE DS    CL24                                                             
         EJECT                                                                  
*FASCREQUS                                                                      
       ++INCLUDE FASCREQUS                                                      
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005FATOCRR   05/01/02'                                      
         END                                                                    
