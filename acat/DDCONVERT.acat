*          DATA SET DDCONVERT  AT LEVEL 012 AS OF 11/18/93                      
*CATALP CONVERT                                                                 
CONVERT  TITLE 'GENERAL TRANSLATE/VALIDATE CONVERSIONS'                         
*                                                                               
CONVERT  CSECT                                                                  
         PRINT GEN                                                              
         NMOD1 CONWRKX-CONWRKD,**CONV**,RA,R9,CLEAR=YES,RR=RE                   
         USING CONWRKD,RC          RC=A(LOCAL W/S)                              
         ST    RE,CONRELO          SAVE RELO FACTOR                             
         LR    R8,R1                                                            
         USING CONBLKD,R8          R8=A(PARAMETER BLOCK)                        
         ICM   RF,15,CONCOMF       RF=A(COMFACS)                                
         BNZ   *+6                                                              
         DC    H'0'                A(COMFACS) MISSING IN PARM BLOCK             
         USING COMFACSD,RF                                                      
         MVC   CONCURED,CCUREDIT                                                
         MVC   CONCSHVL,CCASHVAL                                                
         MVC   CONGETFT,CGETFACT                                                
         MVC   CONPERVL,CPERVAL                                                 
         MVC   CONDATCN,CDATCON                                                 
         MVC   CONADDAY,CADDAY                                                  
         DROP  RF                                                               
*                                                                               
         L     RF,=V(MASTC)                                                     
         LTR   RF,RF                                                            
         BZ    CON4                                                             
         USING MASTD,RF                                                         
         MVC   CONAGYC,MCCTRY                                                   
         MVC   CONLANG,MCLANG                                                   
         B     CON5                                                             
*                                                                               
CON4     GOTO1 CONGETFT,CONPARM,0                                               
         L     RF,0(R1)            RF=A(SYSTEM DEFINTION BLOCK)                 
         MVC   CONAGYC,FACTRY-FACTSD(RF) GET COUNTRY AND LANGUAGE               
         MVC   CONLANG,FALANG-FACTSD(RF)                                        
CON5     CLI   CONAGYC,0                                                        
         BNE   *+8                                                              
*&&UK*&& MVI   CONAGYC,CTRYGBR                                                  
*&&US*&& MVI   CONAGYC,CTRYUSA                                                  
         CLI   CONLANG,0                                                        
         BNE   *+8                                                              
*&&UK*&& MVI   CONLANG,LANGEUK                                                  
*&&US*&& MVI   CONLANG,LANGEUS                                                  
*                                                                               
         ICM   R2,15,CONIADD       R2=INPUT ADDRESS                             
         ICM   R3,15,CONOADD       R3=OUTPUT ADDRESS                            
         MVI   CONERR,0            CLEAR ERROR NUMBER                           
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,1,CONFLD                                                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         BCTR  RF,0                                                             
         SLL   RF,4                RF=FIELD TYPE*16                             
         TM    CONACTN,CONAVALQ    TEST VALIDATE                                
         BNZ   CON6                                                             
         TM    CONACTN,CONAVGTQ    TEST VALIDATE AND GET                        
         BNZ   CON6                                                             
         LA    RF,4(RF)            NO - MUST BE TRANSLATE                       
         TM    CONACTN,CONATRAQ    TEST TRANSLATE                               
         BNZ   CON6                                                             
         LA    RF,4(RF)                                                         
         TM    CONACTN,CONAGETQ    TEST TABLE REQUEST                           
         BNZ   CON6                                                             
         DC    H'0'                                                             
*                                                                               
CON6     CH    RF,=Y(CONBTABL)     TEST BAD FIELD NUMBER                        
         BL    *+6                                                              
         DC    H'0'                                                             
         B     CONBTAB(RF)         GO TO VAL/TRAN/ALT TRAN ROUTINE              
         SPACE 1                                                                
CONBTAB  DS    0H                                                               
         B     VALIDUE             VALIDATE INVOICE DUE DATE                    
         B     TRAIDUE             TRANSLATE INVOICE DUE DATE                   
         B     GETIDUE             GET ACTUAL DUE DATE                          
         DC    AL4(0)                                                           
CONBTABL EQU   *-CONBTAB                                                        
         SPACE 1                                                                
CONX     CLI   CONERR,0            SET CC=NOT EQUAL ON ERROR                    
         XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* VALIDATE INVOICE DUE DATE FOR DEBTORS AND CONVERT TO INTERNAL FORMAT*         
***********************************************************************         
         SPACE 1                                                                
         USING IIDUTABD,R3         INVOICE DUE DATES FOR DEBTORS                
VALIDUE  TM    CONACTN,CONAVGTQ    TEST VALIDATE AND GET                        
         BNO   *+8                                                              
         LA    R3,CONWORK1                                                      
         L     R4,AIDUTAB                                                       
         A     R4,CONRELO                                                       
         USING IDUTABD,R4          R4=A(INVOICE DUE DATE TABLE)                 
*                                                                               
VALID10  CLI   IDUHCLN,EOT        TEST E-O-T                                    
         BE    ERRINVI                                                          
         CLI   IDUHCTRY,CTRYALL TEST ALL COUNTRIES VALID                        
         BE    VALID20                                                          
         MVC   CONBYTE,IDUHCTRY                                                 
         NI    CONBYTE,CTRYALL-CTRYBUT                                          
         LA    RE,X'70'            SET BRANCH NOT EQUAL                         
         TM    IDUHCTRY,CTRYBUT                                                 
         BZ    *+8                                                              
         LA    RE,X'80'            SET BRANCH EQUAL                             
         CLC   CONLANG,CONBYTE     MATCH COUNTRY                                
         EX    RE,*+4                                                           
         NOP   VALID30                                                          
*                                                                               
VALID20  LA    R4,IDUHLNQ(R4)  POINT TO FIRST TABLE ENTRY                       
         B     VALID40                                                          
*                                                                               
VALID30  SR    RE,RE               BUMP TO NEXT TABLE HEADER                    
         IC    RE,IDUHCLN                                                       
         LA    R4,1(RE,R4)                                                      
         B     VALID10                                                          
*                                                                               
VALID40  SR    R0,R0                                                            
         ICM   R0,1,CONILEN        R0=L'(INPUT)                                 
         BZ    ERRMISS                                                          
         LR    RF,R2               RF=A(INPUT AREA)                             
         SR    R1,R1               R1=L'(KEYWORD)                               
VALID50  CLI   0(RF),C'+'          CHECK FOR END OF KEYWORD                     
         BE    VALID60                                                          
         CLI   0(RF),C'0'                                                       
         BL    *+12                                                             
         CLI   0(RF),IIDUMMTH      MAX MONTH NUMBER                             
         BNH   VALID60                                                          
         LA    RF,1(RF)            BUMP TO NEXT CHARACTER OF KEYWORD            
         LA    R1,1(R1)            INCREMENT KEY WORD LENGTH                    
         BCT   R0,VALID50                                                       
         B     VALID80             NO MATCH SO ASSUME STRAIGHT DATE             
*                                                                               
VALID60  LTR   R1,R1               TEST LENGTH OF KEYWORD                       
         BZ    VALID80                                                          
         CH    R1,=Y(L'IDUNKEYW)                                                
         BH    VALID80                                                          
         BCTR  R1,0                                                             
*                                                                               
VALID70  CLI   IDUNCODE,EOT        TEST END OF SUB-TABLE                        
         BE    VALID80             ASSUME DATE ENTERED                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   IDUNKEYW(0),0(R2)   MATCH KEYWORD TO TABLE                       
         BE    *+12                                                             
         LA    R4,IDUNLNQ(R4)      NO MATCH - BUMP TO NEXT ENTRY                
         B     VALID70                                                          
*                                                                               
         MVC   IIDUTYPE,IDUNCODE   SET INTERNAL VALUE                           
         CLI   IDUNCODE,IDUIDQ     INVOICE DATE RULE?  (ID+NN)                  
         BE    VALID90                                                          
         CLI   IDUNCODE,IDUBDQ     BILL DATE RULE?  (BD+NN)                     
         BE    VALID90                                                          
         CLI   IDUNCODE,IDUIMQ     INVOICE MONTH RULE? (IM+NN)                  
         BE    VALID100                                                         
         CLI   IDUNCODE,IDUBMQ     BILL MONTH RULE? (IM+NN)                     
         BE    VALID100                                                         
         DROP  R4                                                               
         DC    H'0'                                                             
*                                  STRAIGHT DUE DATE                            
VALID80  MVI   IIDUTYPE,IDUDATQ    SET INTERNAL VALUE                           
         MVI   IIDULEN,IIDULN1Q    SET LENGTH                                   
         MVC   CONDATFM,CONLANG    SET LANGUAGE                                 
         OI    CONDATFM,PVINSGLS+PVINSGLO SET FOR SINGLE DATE                   
         SR    R4,R4                                                            
         ICM   R4,1,CONILEN        R4=L'(INPUT)                                 
         GOTO1 CONPERVL,CONPARM,((R4),(R2)),(CONDATFM,CONPERB) VAL DAT          
         L     RF,4(R1)                                                         
         CLI   4(R1),PVRCONE       SINGLE DATE FOUND?                           
         BNE   ERRINVI                                                          
         MVC   IIDUDATE,PVALCSTA-PERVALD(RF) PUT DATE IN OUTPUT AREA            
         MVI   CONOLEN,L'IIDUTYPE+L'IIDUDATE+L'IIDULEN                          
         B     VALID130                                                         
*                                  DAY OFFSET RULE                              
VALID90  CLI   0(RF),C'+'          CHECK FOR CORRECT DELIMITER                  
         BNE   ERRINVI                                                          
         MVI   IIDULEN,IIDULN2Q    SET LENGTH                                   
         LR    R4,R0                                                            
         SH    R4,=H'2'            R4=L'(DAYS OFFSET-1)                         
         BM    ERRINVI                                                          
         MVC   CONDUB,ZEROS                                                     
         EX    R4,*+4                                                           
         MVZ   CONDUB(0),1(RF)                                                  
         CLC   CONDUB,ZEROS                                                     
         BNE   ERRINVI                                                          
         EX    R4,*+8                                                           
         B     *+10                                                             
         PACK  CONDUB,1(0,RF)                                                   
         CVB   RF,CONDUB                                                        
         CH    RF,=Y(IIDUMDYS)     WITHIN RANGE?                                
         BH    ERRHIGH                                                          
         STC   RF,IIDUDAYS         PUT DAYS OFFSET IN OUPUT AREA                
         MVI   CONOLEN,L'IIDUTYPE+L'IIDUDAYS+L'IIDULEN                          
         B     VALID130                                                         
*                                  MONTH RULE                                   
VALID100 MVI   IIDULEN,IIDULN3Q    SET LENGTH                                   
         MVI   IIDUMTH,0           SET FOR SAME MONTH AS INVOICE                
         LR    R4,R0               R4=L'(INPUT MINUS KEYWORD)                   
         CLI   0(RF),C'0'          MONTH NUMBER INPUT?                          
         BL    ERRINVI                                                          
         CLI   0(RF),IIDUMMTH      MAX MONTH NUMBER                             
         BH    ERRHIGH                                                          
         PACK  CONDUB,0(1,RF)                                                   
         CVB   RE,CONDUB                                                        
         STC   RE,IIDUMTH          PUT MONTH NUMBER IN OUTPUT AREA              
         LA    RF,1(RF)            BUMPT TO NEXT INPUT CHAR                     
         BCTR  R4,0                REDUCE LENGTH OF UNCHECKED INPUT             
VALID110 MVI   IIDUMDAY,0          SET FOR SAME DAY IN MONTH AS INVOICE         
         CLI   0(RF),C' '          CHECK CORRECT DELIMITER                      
         BNH   VALID120                                                         
         CLI   0(RF),C'/'          CHECK CORRECT DELIMITER                      
         BNE   ERRINVI                                                          
         LA    RF,1(RF)            BUMP TO NEXT INPUT CHAR                      
         SH    R4,=H'2'            REDUCE LENGTH OF UNCHECKED INPUT             
         BM    VALID120            NO DAY ENTERED?                              
         MVC   CONDUB,ZEROS                                                     
         EX    R4,*+4                                                           
         MVZ   CONDUB(0),0(RF)                                                  
         CLC   CONDUB,ZEROS                                                     
         BNE   ERRINVI                                                          
         EX    R4,*+8                                                           
         B     *+10                                                             
         PACK  CONDUB,0(0,RF)                                                   
         CVB   RF,CONDUB                                                        
         CH    RF,=Y(IIDUMDAT)                                                  
         BH    ERRHIGH                                                          
         STC   RF,IIDUMDAY         PUT DAY NUMBER IN OUTPUT AREA                
VALID120 MVI   CONOLEN,L'IIDUTYPE+L'IIDUMTH+L'IIDUMDAY+L'IIDULEN                
         B     VALID130                                                         
*                                                                               
VALID130 TM    CONACTN,CONAVGTQ    TEST VALIDATE AND GET                        
         BNO   CONX                                                             
         LA    R2,CONWORK1                                                      
         ICM   R3,15,CONOADD       R3=OUTPUT ADDRESS                            
         B     GETIDUE                                                          
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* TRANSLATE INVOICE DUE DATE FOR DEBTORS FROM INTERNAL FORMAT TO      *         
* DISPLAY/PRINT FORMAT                                                *         
***********************************************************************         
         SPACE 1                                                                
         USING IIDUTABD,R2         INVOICE DUE DATES FOR DEBTORS                
TRAIDUE  CLI   IIDUTYPE,IDUDATQ    STRAIGHT DATE?                               
         BE    TRAID50                                                          
         L     R4,AIDUTAB                                                       
         A     R4,CONRELO                                                       
         USING IDUTABD,R4          R4=A(INVOICE DUE DATE TABLE)                 
*                                                                               
TRAID10  CLI   IDUHCLN,EOT        TEST E-O-T                                    
         BE    ERRINVI                                                          
         CLI   IDUHCTRY,CTRYALL TEST ALL COUNTRIES VALID                        
         BE    TRAID20                                                          
         MVC   CONBYTE,IDUHCTRY                                                 
         NI    CONBYTE,CTRYALL-CTRYBUT                                          
         LA    RE,X'70'            SET BRANCH NOT EQUAL                         
         TM    IDUHCTRY,CTRYBUT                                                 
         BZ    *+8                                                              
         LA    RE,X'80'            SET BRANCH EQUAL                             
         CLC   CONLANG,CONBYTE     MATCH COUNTRY                                
         EX    RE,*+4                                                           
         NOP   TRAID30                                                          
*                                                                               
TRAID20  LA    R4,IDUHLNQ(R4)  POINT TO FIRST TABLE ENTRY                       
         B     TRAID40                                                          
*                                                                               
TRAID30  SR    RE,RE               BUMP TO NEXT TABLE HEADER                    
         IC    RE,IDUHCLN          BUMP TO NEXT TABLE HEADER                    
         LA    R4,1(RE,R4)                                                      
         B     TRAID10                                                          
*                                                                               
TRAID40  CLI   IDUNCODE,EOT        TEST END OF SUB-TABLE                        
         BE    ERRINVI             MUST BE DATE                                 
         CLC   IDUNCODE,IIDUTYPE   MATCH ON INTERNAL CODE?                      
         BE    *+12                                                             
         LA    R4,IDUNLNQ(R4)      NO MATCH - BUMP TO NEXT ENTRY                
         B     TRAID40                                                          
*                                                                               
         MVC   0(2,R3),IDUNKEYW                                                 
         CLI   IDUNCODE,IDUIDQ     INVOICE DATE RULE?  (ID+NN)                  
         BE    TRAID60                                                          
         CLI   IDUNCODE,IDUBDQ     BILL DATE RULE?  (BD+NN)                     
         BE    TRAID60                                                          
         CLI   IDUNCODE,IDUIMQ     INVOICE MONTH RULE? (IM+NN)                  
         BE    TRAID70                                                          
         CLI   IDUNCODE,IDUBMQ     BILL MONTH RULE? (IM+NN)                     
         BE    TRAID70                                                          
         DROP  R4                                                               
         DC    H'0'                                                             
*                                                                               
TRAID50  MVI   CONDATFM,X'02'      COMPRESSED DATE                              
         OI    CONDATFM,X'80'      RETURN OUTPUT DATE LENGTH                    
         GOTO1 CONDATCN,CONPARM,(CONDATFM,IIDUDATE),(17,(R3))                   
         MVC   CONOLEN,4(R1)                                                    
         B     CONX                                                             
*                                                                               
TRAID60  LA    R4,2(R3)            R4=A(NEXT FREE CHAR IN OUTPUT AREA)          
         MVI   0(R4),C'+'                                                       
         LA    R4,1(R4)                                                         
         SR    RF,RF                                                            
         IC    RF,IIDUDAYS                                                      
         CURED (RF),(3,(R4)),0,ALIGN=LEFT,MODADDR=CONCURED,            X        
               DMCB=CONPARM                                                     
         SR    R4,R3               R4=L'(KEYWORD+DELIMITER)                     
         AR    R4,R0                                                            
         STC   R4,CONOLEN          LENGTH OF OUTPUT EXPRESSION                  
         B     CONX                                                             
*                                                                               
TRAID70  LA    R4,2(R3)            R4=A(NEXT FREE CHAR IN OUTPUT AREA)          
         CURED (B1,IIDUMTH),(2,(R4)),0,ALIGN=LEFT,MODADDR=CONCURED,DMCB*        
               =CONPARM                                                         
         AR    R4,R0                                                            
         MVI   0(R4),C'/'                                                       
         LA    R4,1(R4)                                                         
         CURED (B1,IIDUMDAY),(2,(R4)),0,ALIGN=LEFT,MODADDR=CONCURED,DMC*        
               B=CONPARM                                                        
TRAID80  SR    R4,R3               R4=L'(KEYWORD+DELIMITER)                     
         AR    R4,R0                                                            
         STC   R4,CONOLEN          LENGTH OF OUTPUT EXPRESSION                  
         B     CONX                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* GET ACTUAL DUE DATE BY APPLYING DUE DATE RULE (INTERNAL FORMAT)     *         
* TO INVOICE DATE                                                     *         
***********************************************************************         
         SPACE 1                                                                
         USING IIDUTABD,R2         INVOICE DUE DATES FOR DEBTORS                
GETIDUE  CLI   IIDUTYPE,IDUDATQ    STRAIGHT DUE DATE?  (DUE=DDMMMYY)            
         BE    GETID50                                                          
         CLI   IIDUTYPE,IDUIDQ     INVOICE DATE RULE?  (ID+NN)                  
         BE    GETID60                                                          
         CLI   IIDUTYPE,IDUBDQ     BILL DATE RULE?  (BD+NN)                     
         BE    GETID60                                                          
         CLI   IIDUTYPE,IDUIMQ     INVOICE MONTH RULE? (IM+NN)                  
         BE    GETID70                                                          
         CLI   IIDUTYPE,IDUBMQ     BILL MONTH RULE? (IM+NN)                     
         BE    GETID70                                                          
         DC    H'0'                                                             
*                                                                               
GETID50  GOTO1 CONDATCN,CONPARM,(1,CONIDATE),(2,CONHALF)                        
         CLC   IIDUDATE,CONHALF    DUE DATE LESS THAN BILL DATE?                
         BL    ERRLOW                                                           
         MVC   0(L'IIDUDATE,R3),IIDUDATE COMPRESSED DATE                        
         MVI   CONOLEN,L'IIDUDATE  LENGTH OF OUTPUT EXPRESSION                  
         B     CONX                                                             
*                                                                               
GETID60  GOTO1 CONDATCN,CONPARM,(1,CONIDATE),(0,CONDAT)                         
         SR    RF,RF                                                            
         IC    RF,IIDUDAYS         NUM OF DAYS TO ADD ON TO INV DATE            
         GOTO1 CONADDAY,CONPARM,CONDAT,CONDAT,(RF)                              
         MVI   CONDATFM,0          EBCDIC DATE                                  
         OI    CONDATFM,X'80'      RETURN OUTPUT DATE LENGTH                    
         GOTO1 CONDATCN,CONPARM,(CONDATFM,CONDAT),(2,(R3))                      
         MVC   CONOLEN,4(R1)                                                    
         B     CONX                                                             
*                                                                               
GETID70  GOTO1 CONDATCN,CONPARM,(1,CONIDATE),(0,CONDAT)                         
         MVC   CONHALF,CONDDD      SAVE DAY IN MONTH OF INVOICE                 
         MVC   CONDDD,MIDMONTH     SET TO MIDDLE OF MONTH                       
         SR    RF,RF                                                            
         ICM   RF,1,IIDUMTH        RF=(NUMBER OF MONTHS PAST INV MONTH)         
         BZ    GETID80                                                          
         MH    RF,=H'30'                                                        
         GOTO1 CONADDAY,CONPARM,CONDAT,CONDAT,(RF)                              
*                                                                               
GETID80  SR    RF,RF                                                            
         ICM   RF,1,IIDUMDAY                                                    
         BNZ   GETID85                                                          
         PACK  CONDUB,CONHALF                                                   
         CVB   RF,CONDUB                                                        
GETID85  PACK  CONDUB,CONDMM                                                    
         CVB   RE,CONDUB                                                        
         BCTR  RE,0                                                             
         LA    RE,MNUMTAB(RE)      RE=A(MAX DAYS IN MONTH)                      
         CLM   RF,1,0(RE)          MORE DAYS THAN ARE IN THE MONTH?             
         BL    GETDI90                                                          
         BE    *+10                                                             
         SR    RF,RF                                                            
         IC    RF,0(RE)            RF=(MAX DAYS IN MONTH)                       
         CLC   CONDMM,CONFEB       FEBRUARY?                                    
         BNE   GETDI90                                                          
*                                                                               
         PACK  CONDUB,CONDYY                                                    
         CVB   RE,CONDUB                                                        
         STC   RE,CONBYTE          INVOICE YEAR                                 
         TM    CONBYTE,X'03'       TEST FOR LEAP YEAR                           
         BNZ   GETDI90                                                          
         LA    RF,1(RF)            BUMP MAX DAYS BY 1                           
GETDI90  CVD   RF,CONDUB                                                        
         UNPK  CONHALF,CONDUB                                                   
*                                                                               
GETDI100 MVC   CONDDD,CONHALF                                                   
         MVI   CONDATFM,0          EBCDIC DATE                                  
         OI    CONDATFM,X'80'      RETURN OUTPUT DATE LENGTH                    
         GOTO1 CONDATCN,CONPARM,(CONDATFM,CONDAT),(2,(R3))                      
         GOTO1 CONDATCN,CONPARM,(1,CONIDATE),(2,CONHALF)                        
         CLC   0(2,R3),CONHALF     DUE DATE LESS THAN BILL DATE?                
         BL    ERRLOW                                                           
         MVC   CONOLEN,4(R1)                                                    
         B     CONX                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SET ERROR NUMBER AND EXIT                                           *         
***********************************************************************         
         SPACE 1                                                                
ERRMISS  MVI   CONERR,1            MISSING INPUT (NO LENGTH FOUND)              
         B     CONX                                                             
ERRINVI  MVI   CONERR,2            INVALID INPUT                                
         B     CONX                                                             
ERRHIGH  MVI   CONERR,3            INPUT VALUE TOO HIGH                         
         B     CONX                                                             
ERRLOW   MVI   CONERR,4            INPUT VALUE TOO LOW                          
         B     CONX                                                             
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
AIDUTAB  DC    A(IDUTAB)                                                        
         SPACE 1                                                                
CTRYALL  EQU   X'FF'                                                            
CTRYBUT  EQU   X'80'                                                            
LANGALL  EQU   X'FF'                                                            
LANGDFT  EQU   X'80'                                                            
EOT      EQU   X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
* TABLE OF INVOICE DUE DATE SYNTAX FOR DEBTORS                        *         
***********************************************************************         
         SPACE 1                                                                
IDUTAB   DS    0X                                                               
*                                                                               
IDUGBR   DC    AL1(IDUGBRX-IDUGBR),AL1(LANGEUK)                                 
         DC    AL1(IDUIDQ),CL8'IDATE'  INV DATE (DAYS AFTER INV DATE)           
         DC    AL1(IDUIMQ),CL8'IMONTH' INV MONTH (MNTH/DAY AFTER INV)           
         DC    AL1(IDUBDQ),CL8'BDATE'  BILL DATE (DAYS AFTER INV DATE)          
         DC    AL1(IDUBMQ),CL8'BMONTH' BILL MONTH (MNTH/DAY AFTER INV)          
IDUGBRX  DC    AL1(EOT)                                                         
*                                                                               
IDUUSA   DC    AL1(IDUUSAX-IDUUSA),AL1(LANGEUS)                                 
         DC    AL1(IDUIDQ),CL8'IDATE'  INV DATE (DAYS AFTER INV DATE)           
         DC    AL1(IDUIMQ),CL8'IMONTH' INV MONTH (MNTH/DAY AFTER INV)           
         DC    AL1(IDUBDQ),CL8'BDATE'  BILL DATE (DAYS AFTER INV DATE)          
         DC    AL1(IDUBMQ),CL8'BMONTH' BILL MONTH (MNTH/DAY AFTER INV)          
IDUUSAX  DC    AL1(EOT)                                                         
*                                                                               
IDUGER   DC    AL1(IDUGERX-IDUGER),AL1(LANGGER)                                 
         DC    AL1(IDUIDQ),CL8'RDATUM' INV DATE (DAYS AFTER INV DATE)           
         DC    AL1(IDUIMQ),CL8'RMONAT' INV MONTH (MNTH/DAY AFTER INV)           
         DC    AL1(IDUBDQ),CL8'BDATE'  BILL DATE (DAYS AFTER INV DATE)          
         DC    AL1(IDUBMQ),CL8'BMONTH' BILL MONTH (MNTH/DAY AFTER INV)          
IDUGERX  DC    AL1(EOT)                                                         
*                                                                               
IDUHOL   DC    AL1(IDUHOLX-IDUHOL),AL1(LANGDUT)                                 
         DC    AL1(IDUIDQ),CL8'FDATUM' INV DATE (DAYS AFTER INV DATE)           
         DC    AL1(IDUIMQ),CL8'FMAAND' INV MONTH (MNTH/DAY AFTER INV)           
         DC    AL1(IDUBDQ),CL8'BDATE'  BILL DATE (DAYS AFTER INV DATE)          
         DC    AL1(IDUBMQ),CL8'BMONTH' BILL MONTH (MNTH/DAY AFTER INV)          
IDUHOLX  DC    AL1(EOT)                                                         
*                                                                               
IDUTABX  DC    AL1(EOT)                                                         
         EJECT                                                                  
***********************************************************************         
* TABLE OF DAYS FOR EACH MONTH WHEN NOT A LEAP YEAR                   *         
***********************************************************************         
         SPACE 1                                                                
MNUMTAB  DC    AL1(31,28,31,30,31,30)                                           
         DC    AL1(31,31,30,31,30,31)                                           
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
MIDMONTH DC    C'15'                                                            
CONFEB   DC    C'02'                                                            
ZEROS    DC    C'00000000'                                                      
***********************************************************************         
* DSECT TO COVER CONVERT LOCAL WORKING STORAGE                        *         
***********************************************************************         
         SPACE 1                                                                
CONWRKD  DSECT                                                                  
CONDUB   DS    D                                                                
CONHALF  DS    H                                                                
CONPARM  DS    8F                                                               
CONBYTE  DS    X                                                                
CONPERB  DS    0XL(L'PVALOUTB)     GENERAL WORK AREA                            
CONWORK  DS    0XL96               GENERAL WORK AREA                            
CONWORK1 DS    XL32                                                             
CONWORK2 DS    XL32                                                             
CONWORK3 DS    XL32                                                             
CONDAT   DS    0CL6                EBCDIC YYMMDD DATE                           
CONDYY   DS    CL2                 EBCDIC YY                                    
CONDMM   DS    CL2                 EBCDIC MM                                    
CONDDD   DS    CL2                 EBCDIC DD                                    
CONCURED DS    A                   A(CURED)                                     
CONCSHVL DS    A                   A(CASHVAL)                                   
CONGETFT DS    A                   A(GETFACT)                                   
CONPERVL DS    A                   A(PERVAL)                                    
CONDATCN DS    A                   A(DATCON)                                    
CONADDAY DS    A                   A(ADDAY)                                     
CONRELO  DS    A                   RELOCATE OFFSET                              
CONHEAD  DS    A                   A(TABLE HEADER)                              
CONDATFM DS    X                   DATE FORMAT                                  
CONAGYC  DS    X                   COUNTRY CODE                                 
CONLANG  DS    X                   LANGUAGE CODE                                
CONWRKX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
*        DSECTS FOR KEYWORD -> INTERNAL CODE TABLES                   *         
***********************************************************************         
         SPACE 1                                                                
IDUTABD  DSECT                     INVOICE DUE DATES FOR DEBTORS                
IDUHEAD  DS    0X                  ** TABLE HEADER **                           
IDUHCLN  DS    XL1                 LENGTH OF ENTRY(-1) FOR COUNTRY              
IDUHCTRY DS    XL1                 COUNTY CODE                                  
IDUHLNQ  EQU   *-IDUHEAD           LENGTH OF HEADER                             
         ORG   IDUHEAD                                                          
IDUNTRY  DS    0X                  ** TABLE ENTRY **                            
IDUNCODE DS    XL1                 INTERNAL INVOICE DUE DATE CODE               
IDUDATQ  EQU   1                   STRAIGHT DATE                                
IDUIDQ   EQU   2                   IDATE RULE                                   
IDUIMQ   EQU   3                   IMONTH RULE                                  
IDUBDQ   EQU   4                   BDATE RULE                                   
IDUBMQ   EQU   5                   BMONTH RULE                                  
IDUNKEYW DS    CL8                 KEY WORD                                     
IDUNLNQ  EQU   *-IDUNTRY           LENGTH OF ENTRY                              
         EJECT                                                                  
***********************************************************************         
*        DSECTS FOR INTERNAL REPRESENTATIONS                          *         
***********************************************************************         
         SPACE 1                                                                
IIDUTABD DSECT                     INVOICE DUE DATES FOR DEBTORS                
IIDUTYPE DS    X                   OUTPUT TYPE (SEE IDUNCODE)                   
IIDULEN  DS    X                   OUTPUT TYPE (SEE IDUNCODE)                   
IIDUVAL  DS    0X                                                               
*                                                                               
IIDUDATE DS    PL(L'PVALCSTA)      STRAIGHT DUE DATE                            
IIDULN1Q EQU   *-IIDUTABD                                                       
         ORG   IIDUVAL                                                          
IIDUDAYS DS    X                   NUMBER OF DAYS PAST INV DATE/BILL DT         
IIDUMDYS EQU   180                 (RANGE 0-180)                                
IIDULN2Q EQU   *-IIDUTABD                                                       
         ORG   IIDUVAL                                                          
IIDUMTH  DS    X                   NUMBER OF MONTHS PAST INVOICE DATE           
IIDUMDAY DS    X                   DATE OF MONTH (0 MEANS SAME AS INV)          
IIDULN3Q EQU   *-IIDUTABD                                                       
IIDUMMTH EQU   C'4'                (RANGE 1-4)                                  
IIDUMDAT EQU   31                  MAX DAYS IN MONTH                            
         ORG                                                                    
CONBLKD  DSECT                                                                  
       ++INCLUDE DDCONBLK                                                       
         EJECT                                                                  
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* FAFACTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DDPERVALD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCTRYEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCTRYEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDLANGEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDLANGEQUS                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012DDCONVERT 11/18/93'                                      
         END                                                                    
