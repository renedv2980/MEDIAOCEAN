*          DATA SET SPGETBF    AT LEVEL 027 AS OF 05/01/02                      
*CATALP SPGETBF                                                                 
         TITLE 'SPGETBF - RETRIEVE AND APPLY BILL FORMULAE'                     
SPGETBF  CSECT                                                                  
*                                                                               
*---------------------------------------------------------------------*         
*                                                                     *         
* LEVEL 15 3/2/94  GPLA   USE SPGETBFR TO READ BILL FORMULA RECS                
*                                                                               
* SPGETBF ONLY DOES ANYTHING IF RQGETBF=C'Y'                          *         
*                                                                     *         
* SPGETBF CAN BE CALLED ON MODE=ESTFRST OR MODE=PROCBUY               *         
*                                                                     *         
* ON ESTFRST, IT BUILDS A TABLE OF BILL FORMULAE FOR EACH PRODUCT     *         
*             FOR THIS CLIENT/ESTIMATE                                *         
*                                                                     *         
* ON PROCBUY, IT APPLIES THE APPROPRIATE BILL FORMULA FOR THE PRODUCT *         
*             IN PARM1 AND APPLIES IT TO THE GROSS DOLLARS IN PARM1.  *         
*             THE BILL FORMULA IS RETURNED AT THE ADDRESS IN PARM2.   *         
*                                                                     *         
* PARM 1  PRODUCT CODE / A(COST AREA)  WHERE COST AREA = GROSS(4)     *         
* ------                                                 NET(4)       *         
*                                                        TAX(4)       *         
*                                                                     *         
* PARM 2  A(BILL FORMULA)                                             *         
* ------                                                              *         
*                                                                     *         
* NOTE THAT TAX IS SUBTRACTED BEFORE APPLYING BILL FORMULA AND        *         
* THEN ADDED BACK                                                     *         
*---------------------------------------------------------------------*         
         EJECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**GTBF**                                                       
*                                                                               
         L     RA,=V(SPWORKC)                                                   
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
*                                                                               
         MVC   PARMS(24),0(R1)     SAVE PARMS                                   
         MVC   PPROD,0(R1)         SET PRODUCT CODE                             
         MVC   AGROSS,0(R1)        A(GROSS, ETC.)                               
*                                                                               
         CLI   RQGETBF,C'Y'                                                     
         BNE   XIT                                                              
         CLI   FIRST,C'Y'          IF FIRST TIME, READ B1X PROFILE              
         BNE   BF1                                                              
         MVI   FIRST,C'N'                                                       
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SB1X'                                                 
         NI    WORK,X'BF'          MAKE SYSTEM LOWER CASE                       
         MVC   WORK+4(2),AGY                                                    
         MVC   WORK+6(1),MED                                                    
         MVC   WORK+7(3),CLT                                                    
         L     RF,ADCLT                                                         
         LA    RF,COFFICE-CLTHDR(RF)                                            
         CLI   0(RF),C' '                                                       
         BNH   *+14                                                             
         MVI   WORK+10,C'*'        SET OFFICE CODE                              
         MVC   WORK+11(1),0(RF)                                                 
         GOTO1 GETPROF,DMCB,WORK,SB1XPROF,DATAMGR                               
*                                                                               
         MVC   BFROPT,SB1XPROF+11  BFR OPTION                                   
         CLI   BFROPT,C' '                                                      
         BH    *+8                                                              
         MVI   BFROPT,C'N'                                                      
*                                                                               
BF1      DS    0H                                                               
         CLI   BFROPT,C'N'         BFR RECORD OPTION                            
         BNE   BF40                                                             
*                                                                               
         CLI   MODE,ESTFRST                                                     
         BE    BF2                                                              
         CLI   MODE,PROCBUY                                                     
         BE    BF10                                                             
*                                                                               
XIT      XMOD1 1                                                                
         EJECT                                                                  
* ESTIMATE FIRST PROCESSING - BUILD TABLE OF BILL FORMULAE                      
*                                                                               
BF2      MVC   BFSAVE,KEY          SAVE SPTDIR KEY                              
         LA    R0,255              CLEAR BILL FORMULAE TABLE                    
         LA    R1,BFTAB                                                         
         XC    0(5,R1),0(R1)                                                    
         LA    R1,5(R1)                                                         
         BCT   R0,*-10                                                          
*                                                                               
         MVI   SEPES,C'Y'          SET SEPERATE ESTIMATES SWITCH                
         CLI   BEST,0                                                           
         BE    *+12                                                             
         CLI   BESTEND,0                                                        
         BE    *+8                                                              
         MVI   SEPES,C'N'                                                       
*                                                                               
         LA    R2,BFTAB            GET BILL FORMULA FOR EACH PRODUCT            
         LA    R4,255                                                           
         MVI   BFPRD,1                                                          
*                                                                               
BF4      CLI   BPRD,X'FF'                                                       
         BE    *+14                                                             
         CLC   BFPRD,BPRD                                                       
         BNE   *+14                                                             
         BAS   RE,GETBILFO                                                      
         MVC   0(5,R2),BILLFORM                                                 
         LA    R2,5(R2)                                                         
         ZIC   R3,BFPRD                                                         
         LA    R3,1(R3)                                                         
         STC   R3,BFPRD                                                         
         BCT   R4,BF4                                                           
*                                                                               
         MVC   KEY,BFSAVE          RESTORE KEY                                  
         GOTO1 HIGH                                                             
         B     XIT                                                              
         EJECT                                                                  
* GET BILL FORMULA                                                              
*                                                                               
GETBILFO NTR1  ,                                                                
         XC    KEY,KEY                                                          
         L     R1,ADCLT                                                         
         MVC   KEY+1(3),1(R1)      A/M,CLT                                      
         MVC   KEY+4(3),=C'AAA'    DEFAULT PRODUCT = AAA                        
         CLC   DEFKEY,KEY          TEST DEFAULT BILL FORMULA READ YET           
         BE    GB1                 YES                                          
         MVC   DEFKEY,KEY          NO-READ PRD=AAA RECORD                       
         BAS   RE,GETPRDF                                                       
         MVC   DEFBILFO,BILLFORM                                                
*                                                                               
GB1      CLI   BFPRD,X'FF'         TEST PRD=POL                                 
         BE    GB6                 YES-USE PRD=AAA                              
         LA    R0,255              NO-FIND ALPHA PRODUCT CODE                   
         LA    R1,CLIST-CLTHDRD(R1)                                             
*                                                                               
GB2      CLI   0(R1),C' '                                                       
         BNH   GB6                                                              
         CLC   BFPRD,3(R1)                                                      
         BE    GB4                                                              
         LA    R1,4(R1)                                                         
         BCT   R0,GB2                                                           
         B     GB6                                                              
*                                                                               
GB4      MVC   KEY+4(3),0(R1)                                                   
*                                                                               
GB6      CLI   SEPES,C'Y'          TEST SEPERATE ESTIMATES                      
         BNE   *+10                                                             
         MVC   KEY+7(1),BEST       YES-READ ESTIMATE RECORD                     
         CLC   DEFKEY,KEY          TEST DEFAULT                                 
         BNE   *+14                                                             
         MVC   BILLFORM,DEFBILFO   YES-SET DEFAULT BILL FORMULA                 
         B     GBX                                                              
         CLI   KEY+7,0                                                          
         BE    GB8                                                              
         BAS   RE,GETESTF                                                       
         OC    BILLFORM,BILLFORM                                                
         BNZ   GBX                                                              
         MVC   KEY,KEYSAVE                                                      
         MVI   KEY+7,0                                                          
*                                                                               
GB8      BAS   RE,GETPRDF                                                       
         OC    BILLFORM,BILLFORM                                                
         BNZ   GBX                                                              
         MVC   BILLFORM,DEFBILFO                                                
         B     GBX                                                              
*                                                                               
GBX      XIT1  ,                                                                
         EJECT                                                                  
GETPRDF  LR    R0,RE                                                            
         XC    BILLFORM,BILLFORM                                                
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   GETPRDX                                                          
         LA    R6,RECORD                                                        
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
         USING PRDHDRD,R6                                                       
         MVC   BILLBAS,PBILLBAS                                                 
         MVC   BILLCOM,PBILLCOM                                                 
GETPRDX  LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R6                                                               
         SPACE 2                                                                
GETESTF  LR    R0,RE                                                            
         XC    BILLFORM,BILLFORM                                                
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   GETESTX                                                          
         LA    R6,RECORD                                                        
         ST    R6,AREC                                                          
         GOTO1 GET                                                              
         USING ESTHDRD,R6                                                       
         MVC   BILLBAS,EBILLBAS                                                 
         MVC   BILLCOM,EBILLCOM                                                 
GETESTX  LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R6                                                               
         EJECT                                                                  
* PROCBUY - APPLY THE APROPRIATE BILL FORMULA                                   
*                                                                               
BF10     DS    0H                                                               
         ICM   R1,15,PARMS+4       CLEAR THE RETURNED BILL FORMULA              
         BZ    *+10                                                             
         XC    0(5,R1),0(R1)                                                    
         SR    R2,R2               FIND THIS PRODUCT'S BILL FORMULA             
         ICM   R2,1,PPROD                                                       
         BZ    XIT                                                              
         BCTR  R2,0                                                             
         MH    R2,=H'5'                                                         
         LA    R2,BFTAB(R2)                                                     
*                                                                               
BF11     DS    0H                                                               
         L     R1,AGROSS           SET A(GROSS DOLLARS)                         
         MVC   BFGROSS,0(R1)       SET GROSS DOLLARS                            
         MVC   BFNET,4(R1)         SET NET DOLLARS                              
         MVC   BFTAX,8(R1)         SET TAX DOLLARS                              
*                                                                               
         LM    R0,R1,BFGROSS       ADJUST BFGROSS/BFNET FOR TAX                 
         S     R0,BFTAX                                                         
         S     R1,BFTAX                                                         
         STM   R0,R1,BFGROSS                                                    
*                                                                               
         OC    0(5,R2),0(R2)       TEST WHETHER THERE IS A BILL FORMULA         
         BZ    XIT                 NO                                           
         MVC   BILLFORM,0(R2)      YES-                                         
         SR    RF,RF               CALCULATE ADJUSTED COST                      
         ICM   R1,15,BILLCOM                                                    
         BZ    BF12                                                             
         SR    RE,RE                                                            
         L     RF,BFGROSS          USING THE BILL FORMULA                       
         TM    BILLBAS,X'01'                                                    
         BZ    *+8                                                              
         L     RF,BFNET                                                         
         MR    RE,R1                                                            
         SLDA  RE,1                                                             
         D     RE,=F'1000000'                                                   
         LTR   RF,RF                                                            
         BM    *+8                                                              
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
*                                                                               
BF12     L     RE,BFGROSS                                                       
         TM    BILLBAS,X'10'                                                    
         BZ    *+8                                                              
         L     RE,BFNET                                                         
         AR    RE,RF                                                            
         A     RE,BFTAX            NOW ADD BACK TAX                             
         L     R1,AGROSS                                                        
         ST    RE,0(R1)                                                         
*                                                                               
         ICM   R1,15,PARMS+4       RETURN THE BILL FORMULA                      
         BZ    XIT                                                              
         MVC   0(5,R1),BILLFORM                                                 
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BILL FORMULA RECORD CODE                                               
*                                                                               
***********************************************************************         
         SPACE 2                                                                
BF40     DS    0H                                                               
         CLI   MODE,PROCBUY        ONLY AT PROCBUY                              
         BNE   XIT                                                              
         MVC   BFSAVE,KEY          SAVE SPTDIR KEY                              
*                                                                               
         LA    R4,GETBFRW          GETBFRW IS INITIALIZED TO NULLS              
         USING SPGBFRDD,R4                                                      
         L     R3,ADBUY            HOPE BUY IS THERE!                           
         USING BUYKEY,R3                                                        
*                                                                               
         MVC   SPGBAM,BAGYMD                                                    
         MVC   SPGBCLT,BCLT                                                     
         LA    R0,255              GET PRODUCT CODE                             
         L     R1,ADCLT                                                         
         LA    R1,CLIST-CLTHDRD(R1)                                             
*                                                                               
BF42     CLI   0(R1),C' '                                                       
         BNH   BF43                                                             
         CLC   PPROD,3(R1)                                                      
         BE    BF44                                                             
         LA    R1,4(R1)                                                         
         BCT   R0,BF42                                                          
BF43     DS    0H                                                               
         DC    H'0'                                                             
*                                                                               
BF44     DS    0H                                                               
         MVC   SPGBPRD,0(R1)                                                    
         MVC   SPGBEST,BUYKEST                                                  
         MVC   SPGBMKT,BUYMSTA                                                  
         MVC   SPGBMOS,=X'BC41'    MONTH OF SERVICE **FEB94 FOR TEST            
         MVC   SPGBACOM,ACOMFACS                                                
         MVC   SPGBLODR,LOADER                                                  
**NOP**  MVI   SPGBMODE,C'B'      BUFFER MODE                                   
         GOTO1 =V(SPGETBFR),DMCB,SPGBFRDD                                       
*                                                                               
         LA    R2,SPGBFORM         R2 TO FORMULA                                
         MVC   KEY,BFSAVE          RESTORE SPTDIR KEY                           
         GOTO1 HIGH                                                             
         B     BF11                BRANCH TO CALC CODE                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* STORAGE                                                                       
*                                                                               
PARMS    DS    6F                                                               
AGROSS   DS    A                                                                
BFGROSS  DS    F                                                                
BFNET    DS    F                                                                
BFTAX    DS    F                                                                
PPROD    DS    XL1                                                              
BFSAVE   DS    CL32                                                             
BFPRD    DS    XL1                                                              
SEPES    DS    CL1                                                              
BILLFORM DS    0XL5                                                             
BILLBAS  DS    XL1                                                              
BILLCOM  DS    XL4                                                              
DEFBILFO DS    XL5                                                              
DEFKEY   DC    XL13'00'                                                         
*                                                                               
FIRST    DC    C'Y'                                                             
SB1XPROF DS    XL16                                                             
BFROPT   DS    C                                                                
GETBFRW  DC    XL(SPGBFRDL)'00'                                                 
*                                                                               
BFTAB    DS    255XL5                                                           
*                                                                               
RECORD   DS    2000X                                                            
         EJECT                                                                  
* SPREPWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* SPREPMODES                                                                    
         PRINT OFF                                                              
       ++INCLUDE SPREPMODES                                                     
         PRINT ON                                                               
         SPACE 1                                                                
* SPGENCLT                                                                      
         PRINT OFF                                                              
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* SPGENPRD                                                                      
         PRINT OFF                                                              
PRDHDRD  DSECT                                                                  
       ++INCLUDE SPGENPRD                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* SPGENEST                                                                      
         PRINT OFF                                                              
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* SPGENBUY                                                                      
         PRINT OFF                                                              
BUYKEYD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
         PRINT ON                                                               
         SPACE 1                                                                
SPGBFRDD DSECT                                                                  
       ++INCLUDE SPGETBFRD                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027SPGETBF   05/01/02'                                      
         END                                                                    
