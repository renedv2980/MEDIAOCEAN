*          DATA SET REGENPTR   AT LEVEL 025 AS OF 09/30/99                      
*CATALP REGENPTR                                                                
         TITLE 'REGENPTR' - REPPAK CONTRACT PASSIVE PTR BUILD ROUTINE           
*              THIS ROUTINE BUILDS CONTRACT RECORD PASSIVE POINTERS             
*                                                                               
*              PARAMETER 1 =       A(CONREC)                                    
*                                                                               
*              PARAMETER 2 =       A(OUTPUT AREA)  SUGGESTED 1000 BYTES         
*                                                                               
*              PARAMETER 3 =       ADDRESS OF BLOCK CONTAINING:                 
*                                     A(DATCON)                                 
*                                                                               
*              OUTPUT AREA FORMATTED AS FOLLOWS:                                
*                 BYTE 0-1   LENGTH OF OUTPUT                                   
*                 BYTE 2     # OF POINTERS IN OUTPUT                            
*                 BYTE 3-?   PASSIVE POINTERS                                   
*                                                                               
REGENPTR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),**PTR**                                           
         USING WORKD,RC                                                         
         L     RA,0(R1)                 CONTRACT RECORD                         
S        USING CONREC,RA                                                        
         MVC   AOUTPUT,4(R1)            OUTPUT AREA                             
         L     R9,8(R1)                                                         
         MVC   DATCON,0(R9)                                                     
*                                                                               
MAIN     DS    0H                                                               
         L     R9,AOUTPUT               INITIALIZE OUTPUT AREA                  
         MVC   0(3,R9),=X'000300'                                               
*                                                                               
*                                       PREPARE K NUMBER FORMATS                
         MVC   FULL,S.RCONKCON                                                  
         L     R0,=X'99999999'                                                  
         S     R0,FULL                                                          
         STCM  R0,15,CON9S                                                      
         PACK  CON9REV+0(1),CON9S+3(1)                                          
         PACK  CON9REV+1(1),CON9S+2(1)                                          
         PACK  CON9REV+2(1),CON9S+1(1)                                          
         PACK  CON9REV+3(1),CON9S+0(1)                                          
*                                                                               
         BAS   RE,P8C                   CREATE 8C POINTER                       
         BAS   RE,P9C                   CREATE 9C POINTER                       
         BAS   RE,PAC                   CREATE AC POINTER                       
         BAS   RE,PBC                   CREATE BC POINTER                       
         BAS   RE,PCC                   CREATE CC POINTER                       
         BAS   RE,PDC                   CREATE DC POINTER                       
         BAS   RE,PEC                   CREATE EC POINTER                       
         BAS   RE,P8D                   CREATE 8D POINTERS                      
         BAS   RE,P8E                   CREATE 8E POINTERS                      
         BAS   RE,P8F                   CREATE 8F POINTER                       
         BAS   RE,P9D                   CREATE 9D POINTER                       
         BAS   RE,P9E                   CREATE 9E POINTER                       
         BAS   RE,PAB01                 CREATE AB01 POINTER                     
*                                                                               
         XMOD1                                                                  
*                                                                               
P8C      DS    0H                       CREATE 8C POINTER                       
         NTR1                                                                   
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVI   RCONPTYP,X'8C'                                                   
         MVC   RCONPREP,S.RCONKREP                                              
         MVC   RCONPCON,CON9S                                                   
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
         DROP  R9                                                               
*                                                                               
P9C      DS    0H                       CREATE 9C POINTER                       
         NTR1                                                                   
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVI   RCONQTYP,X'9C'                                                   
         MVC   RCONQREP,S.RCONKREP                                              
         MVC   RCONQOFF,S.RCONKOFF                                              
         MVC   RCONQGRP,S.RCONKGRP                                              
         MVC   RCONQSTA,S.RCONKSTA                                              
         MVC   RCONQADV,S.RCONKADV                                              
         MVC   RCONQAGY,S.RCONKAGY                                              
         MVC   RCONQAOF,S.RCONKAOF                                              
         MVC   RCONQCON,S.RCONKCON                                              
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
         DROP  R9                                                               
*                                                                               
PAC      DS    0H                       CREATE AC POINTER                       
         NTR1                                                                   
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVI   RCONRTYP,X'AC'                                                   
         MVC   RCONRREP,S.RCONKREP                                              
         MVC   RCONROFF,S.RCONKOFF                                              
         MVC   RCONRTEM,S.RCONTEM                                               
         MVC   RCONRSAL(1),S.RCONSAL+2                                          
         MVC   RCONRSAL+1(1),S.RCONSAL                                          
         MVC   RCONRSAL+2(1),S.RCONSAL+1                                        
         MVC   RCONRSTA,S.RCONKSTA                                              
         MVC   RCONRAGY,S.RCONKAGY                                              
         MVC   RCONRADV,S.RCONKADV                                              
         MVC   RCONRCON,S.RCONKCON                                              
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
         DROP  R9                                                               
*                                                                               
PBC      DS    0H                       CREATE BC POINTER                       
         NTR1                                                                   
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVI   RCONDTYP,X'BC'                                                   
         MVC   RCONDREP,S.RCONKREP                                              
         MVC   RCONDCTG,S.RCONCTGY                                              
         MVC   RCONDOFF,S.RCONKOFF                                              
         MVC   RCONDSTA,S.RCONKSTA                                              
         MVC   RCONDAGY,S.RCONKAGY                                              
         MVC   RCONDAOF,S.RCONKAOF                                              
         MVC   RCONDADV,S.RCONKADV                                              
         MVC   RCONDCON,S.RCONKCON                                              
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
         DROP  R9                                                               
*                                                                               
PCC      DS    0H                       CREATE CC POINTER                       
         NTR1                                                                   
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVI   RCONSTYP,X'CC'                                                   
         MVC   RCONSREP,S.RCONKREP                                              
         MVC   RCONSSTA,S.RCONKSTA                                              
         MVC   RCONSOFF,S.RCONKOFF                                              
         MVC   RCONSTEM,S.RCONTEM                                               
         MVC   RCONSSAL(1),S.RCONSAL+2                                          
         MVC   RCONSSAL+1(1),S.RCONSAL                                          
         MVC   RCONSSAL+2(1),S.RCONSAL+1                                        
         MVC   RCONSADV,S.RCONKADV                                              
         MVC   RCONSAGY,S.RCONKAGY                                              
         MVC   RCONSCON,S.RCONKCON                                              
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
         DROP  R9                                                               
*                                                                               
PDC      DS    0H                       CREATE DC POINTER                       
         NTR1                                                                   
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                 K HAS BOP INFORMATION?                  
         BNE   PDCX                     NO - DON'T ADD POINTER                  
*                                                                               
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         USING RCONBPEL,R6                                                      
         MVI   RCONBTYP,X'DC'                                                   
         MVC   RCONBREP,S.RCONKREP                                              
         MVC   RCONBADV,S.RCONKADV                                              
         MVC   RCONBDTE,RCONBPDT                                                
         MVC   RCONBREF,RCONBPRF                                                
         MVC   RCONBSTA,S.RCONKSTA                                              
         MVC   RCONBCON,S.RCONKCON                                              
         BAS   RE,UPCOUNT                                                       
PDCX     B     XIT                                                              
         DROP  R9,R6                                                            
*                                                                               
PEC      DS    0H                       CREATE EC POINTER                       
         NTR1                                                                   
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'12'                                                     
         BAS   RE,GETEL                 K HAS SAR INFORMATION?                  
         BNE   PECX                     NO - DON'T ADD POINTER                  
*                                                                               
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVI   RCONATYP,X'EC'                                                   
         MVC   RCONAREP,S.RCONKREP                                              
         MVC   RCONACON,CON9REV                                                 
         BAS   RE,UPCOUNT                                                       
PECX     B     XIT                                                              
         DROP  R9                                                               
*                                                                               
* CREATE '8D' PTRS                                                              
P8D      DS    0H                       CREATE 8D POINTER                       
         NTR1                                                                   
*                                                                               
* - GET FLIGHT START/END DATES AND SAVE IN WORK+40                              
         GOTO1 DATCON,DMCB,(3,S.RCONDATE),(2,WORK+40)    START DATE             
         GOTO1 DATCON,DMCB,(3,S.RCONDATE+3),(2,WORK+42)  END DATE               
* - GET DEMO FROM BOP OR SAR ELEMENT AND SAVE IN WORK+50                        
         XC    WORK+50(3),WORK+50                                               
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'12'        SAR ELEMENT                                  
         BAS   RE,GETEL                                                         
         BE    P8D10                                                            
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'10'        BOP ELEMENT                                  
         BAS   RE,GETEL                                                         
         BE    P8D20                                                            
         USING RSAREL,R6                                                        
P8D10    DS    0H                                                               
         LA    RE,RSARDEM            DEMO                                       
         LA    RF,8                                                             
         MVC   WORK+50(3),RSARDEM    DEMO                                       
P8D12    TM    0(RE),X'40'         IS IT MARKED AS PRIMARY ?                    
         BO    P8D14                 YES                                        
         LA    RE,3(RE)                                                         
         MVC   WORK+50(3),0(RE)                                                 
         BCT   RF,P8D12                                                         
         MVC   WORK+50(3),RSARDEM    NO/USE 1ST AS DEFAULT                      
P8D14    NI    WORK+50,X'FF'-X'40'          CLEAR MAIN DEMO INDICATOR           
         B     P8D50                                                            
         DROP R6                                                                
         USING RCONBPEL,R6                                                      
P8D20    DS    0H                                                               
         LA    RE,RCONBPDM+1                                                    
         LA    RF,6                                                             
         MVC   WORK+50(3),RCONBPDM+1                                            
P8D22    TM    0(RE),X'40'              IS IT MARKED AS PRIMARY DEMO?           
         BO    P8D24                    YES                                     
         LA    RE,3(RE)                 NO/BUMP TO NEXT DEMO                    
         MVC   WORK+50(3),0(RE)                                                 
         BCT   RF,P8D22                                                         
P8D24    NI    WORK+50,X'FF'-X'40'           CLEAR MAIN DEMO INDICATOR          
         DROP R6                                                                
                                                                                
* BUILD BASIC KEY IN WORK                                                       
P8D50    XC    WORK(32),WORK                                                    
         LA    R3,WORK                                                          
         MVI   0(R3),X'8D'                                                      
         MVC   1(2,R3),S.RCONKREP                                               
         MVC   8(2,R3),WORK+40    START DATE                                    
         MVC   10(2,R3),WORK+42    END DATE                                     
         MVC   12(4,R3),S.RCONKCON   CONTRACT NUMBER                            
*                                                                               
* ID = 1 = AGENCY / ADVERTISER                                                  
         BAS   RE,NEXTP                                                         
         MVC   0(27,R9),WORK                                                    
         MVI   16(R9),1                                                         
         MVC   17(6,R9),S.RCONKAGY                                              
         MVC   23(4,R9),S.RCONKADV                                              
         BAS   RE,UPCOUNT                                                       
                                                                                
* ID = 2 = SALESPERSON/CONTRACT TYPE/GROUP-SUBG/CATEGORY/TEAM                   
         BAS   RE,NEXTP                                                         
         MVC   0(27,R9),WORK                                                    
         MVI   16(R9),2                                                         
         MVC   17(3,R9),S.RCONSAL                                               
         MVC   20(1,R9),S.RCONTYPE                                              
         MVC   21(2,R9),S.RCONKGRP                                              
         MVC   23(2,R9),S.RCONCTGY                                              
         MVC   25(2,R9),S.RCONTEM                                               
         BAS   RE,UPCOUNT                                                       
                                                                                
* ID = 3 = OFFICE(SALESPERSON)/DEMO/CREATION DATE                               
         BAS   RE,NEXTP                                                         
         MVC   0(27,R9),WORK                                                    
         MVI   16(R9),3                                                         
         MVC   17(2,R9),S.RCONKOFF                                              
* CREATION DATE YMD -> BINARY                                                   
         GOTO1 DATCON,DMCB,(3,S.RCONHDRD),(2,22(R9))                            
         MVC   19(3,R9),WORK+50       DEMO                                      
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
*                                                                               
* CREATE '8E' PTRS                                                              
*                                                                               
P8E      DS    0H                                                               
         NTR1                                                                   
*   SIMILAR TO X'8D' POINTERS BUT HAVE STATION IN KEY INSTEAD OF 0'S            
*                                                                               
* WORK HAS BASIC KEY- REPLACE ZEROS OF X'8D' WITH STATION                       
         MVI   WORK,X'8E'                                                       
         MVC   WORK+3(5),S.RCONKSTA                                             
                                                                                
* ID = 1 = AGENCY / ADVERTISER                                                  
         BAS   RE,NEXTP                                                         
         MVC   0(27,R9),WORK                                                    
         MVI   16(R9),1                                                         
         MVC   17(6,R9),S.RCONKAGY                                              
         MVC   23(4,R9),S.RCONKADV                                              
         BAS   RE,UPCOUNT                                                       
                                                                                
* ID = 2 = SALESPERSON/CONTRACT TYPE/GROUP-SUBG/CATEGORY/TEAM                   
         BAS   RE,NEXTP                                                         
         MVC   0(27,R9),WORK                                                    
         MVI   16(R9),2                                                         
         MVC   17(3,R9),S.RCONSAL                                               
         MVC   20(1,R9),S.RCONTYPE                                              
         MVC   21(2,R9),S.RCONKGRP                                              
         MVC   23(2,R9),S.RCONCTGY                                              
         MVC   25(2,R9),S.RCONTEM                                               
         BAS   RE,UPCOUNT                                                       
                                                                                
* ID = 3 = OFFICE(SALESPERSON)/DEMO/CREATION DATE                               
         BAS   RE,NEXTP                                                         
         MVC   0(27,R9),WORK                                                    
         MVI   16(R9),3                                                         
         MVC   17(2,R9),S.RCONKOFF                                              
* CREATION DATE YMD -> BINARY                                                   
         GOTO1 DATCON,DMCB,(3,S.RCONHDRD),(2,22(R9))                            
         MVC   19(3,R9),WORK+50    DEMO                                         
         BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
*                                                                               
* DELETE X'8F' POINTER                                                          
*                                                                               
P8F      DS    0H                       CREATE 8F POINTER                       
         NTR1                                                                   
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'A3'                                                     
         BAS   RE,GETEL                                                         
         BNE   P8FX                                                             
         USING RCONSON,R6                                                       
         CLI   RCONSNST,X'FF'           CYCLE COMPLETED?                        
         BE    P8FX                     YES - DON'T BUILD KEY                   
*                                                                               
         BAS   RE,NEXTP                                                         
K        USING RCON8FTP,R9                                                      
         MVI   K.RCON8FTP,X'8F'                                                 
         MVC   K.RCON8FRP,S.RCONKREP                                            
         MVC   K.RCON8FSA,S.RCONKSTA                                            
         MVC   K.RCON8FDT,RCONSDTE         DATE SONNNET ELEM CREATED            
         MVC   K.RCON8FIN,=C'NEW'          DEF 'SENT TO' ID (BOXID)             
         MVC   K.RCON8FCN,S.RCONKCON       CONTRACT NUMBER                      
*                                                                               
         ZIC   R1,RCONSONL         LENGTH                                       
         CHI   R1,RCONSOVQ         ,,IF = 40 = 'NEW CONTRACT'                   
         BE    P8F050              ,,THEN WE'RE DONE                            
         AHI   R1,-RCONSOVQ        ID/LEN COMMENT LENGTH                        
         CHI   R1,L'RCONSONM       MUST BE AT LEAST 0NE MINI ELEM               
         BNL   *+6                                                              
         DC    H'0'                                                             
                                                                                
         SR    R0,R0                                                            
         D     R0,=A(L'RCONSONM)   DIV BY LENGTH OF MINI ELEM                   
         LA    RE,RCONSONM         MEMBER MINI ELEM                             
P8F010   OC    3(2,RE),3(RE)       DATE ADDED ?                                 
         BZ    P8F015              NO = 'SENT TO' THIS MEMBER                   
         LA    RE,L'RCONSONM(RE)   YES/BUMP TO NEXT MINI ELEM                   
         BCT   R1,P8F010                                                        
         DC    H'0'                                                             
P8F015   MVC   K.RCON8FIN,0(RE)    SET ID TO KEY                                
*                                                                               
P8F050   DS    0H                                                               
         LA    R6,S.RCONREC                                                     
         CLC   S.RCONKREP,=C'B4'   FOR TESTING                                  
         BE    *+14                                                             
         CLC   S.RCONKREP,=C'NB'   NBC?                                         
         BNE   P8F060                                                           
         MVI   ELCODE,X'1E'                                                     
         BAS   RE,GETEL                                                         
         BNE   P8F060                                                           
         TM    RCONRF1-RCONRFEL(R6),X'20' NBC HOME MKT K?                       
         BZ    P8F060               NO                                          
         MVI   K.RCON8FTP,X'9F'     YES - USE 9F NBC HOME MKT KEY               
*                                                                               
P8F060   DS    0H                                                               
         LA    R6,S.RCONREC                                                     
         BAS   RE,UPCOUNT                                                       
         DROP  K                                                                
P8FX     B     XIT                                                              
         DROP  R6                                                               
*                                                                               
* CREATE X'9D' POINTER                                                          
*                                                                               
P9D      DS    0H                       CREATE 9D POINTER                       
         NTR1                                                                   
*                                                                               
         BAS   RE,NEXTP                                                         
         MVI   0(R9),X'9D'                                                      
         MVC   1(2,R9),S.RCONKREP                                               
         MVC   3(5,R9),S.RCONKSTA                                               
         MVC   8(4,R9),S.RCONKADV                                               
         MVC   23(4,R9),S.RCONKCON                                              
* USE PROD CODE OR NAME                                                         
         MVI   12(R9),X'FF'            DEFAULT IS PROD CODE                     
         MVC   13(3,R9),S.RCONPRD                                               
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'05'                                                     
         BAS   RE,GETEL                                                         
         BNE   P9DX                                                             
         USING RCONEXEL,R6                                                      
         MVC   12(9,R9),RCONEXPR        SET PRODUCT NAME                        
         DROP  R6                                                               
*                                                                               
P9DX     BAS   RE,UPCOUNT                                                       
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
P9E      DS    0H                       CREATE 9E POINTER                       
         NTR1                                                                   
         LA    R6,S.RCONREC                                                     
         MVI   ELCODE,X'2A'                                                     
         BAS   RE,GETEL                 MOVE HISTORY ELEM?                      
         BNE   P9EX                     NO - DON'T ADD POINTER                  
*                                                                               
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         USING RCONMMEL,R6                                                      
         MVI   RCON9ETP,X'9E'                                                   
         MVC   RCON9ERP,S.RCONKREP                                              
         MVC   RCON9EDT,RCONMMDT                                                
         MVC   RCON9ESR,RCONMMOR                                                
         MVC   RCON9EST,S.RCONKSTA                                              
         MVC   RCON9ESC,RCONMMOC                                                
         BAS   RE,UPCOUNT                                                       
P9EX     B     XIT                                                              
         DROP  R9,R6                                                            
*                                                                               
PAB01    DS    0H                       CREATE AB01 POINTER                     
         NTR1                                                                   
         CLC   S.RCONPRD,=3C' '      IS THERE A PRODUCT?                        
         BNH   PAB01X              NO, DON'T MAKE AB01 POINTER                  
*                                                                               
         BAS   RE,NEXTP                                                         
         USING RCONKEY,R9                                                       
         MVC   RCONPCTP(2),=X'AB01'                                             
         MVC   RCONPCRP,S.RCONKREP                                              
         MVC   RCONPCAG,S.RCONKAGY                                              
         MVC   RCONPCAD,S.RCONKADV                                              
         MVC   RCONPCPC,S.RCONPRD                                               
         MVC   RCONPCCN,S.RCONKCON                                              
         BAS   RE,UPCOUNT                                                       
PAB01X   B     XIT                                                              
         DROP  R9                                                               
*                                                                               
*                                                                               
NEXTP    DS    0H          LITTLE ROUTINE TO FIND THE NEXT AVAILABLE            
         L     R9,AOUTPUT  SPOT IN THE OUTPUT AREA. A(NEXT SPOT) PUT            
         ZICM  R0,0(R9),2  IN R9. ROUTINE USES R0,R9                            
         AR    R9,R0                                                            
         XC    0(L'RCONKEY,R9),0(R9)                                            
         BR    RE                                                               
*                                                                               
UPCOUNT  DS    0H          LITTLE ROUTINE INCREASES THE LENGTH OF THE           
         L     RF,AOUTPUT  OUTPUT AREA BY KEYLEN AND THE KEY COUNTER IN         
         ZICM  R0,0(RF),2  OUTPUT AREA BY 1. ROUINE USES R0,RF                  
         AH    R0,=AL2(L'RCONKEY)                                               
         STCM  R0,3,0(RF)                                                       
         CH    R0,=H'500'          MORE THAN 500 BYTE OUTPUT?                   
         BL    *+6                 NO - OK                                      
         DC    H'0'                CONTRACT PROG CAN'T HANDLE IT!               
         ZIC   R0,2(RF)                                                         
         AH    R0,=H'1'                                                         
         STC   R0,2(RF)                                                         
         BR    RE                                                               
*                                                                               
XIT      XIT1                                                                   
*                                                                               
         GETEL R6,34,ELCODE                                                     
*                                                                               
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
FULL     DS    F                                                                
DUB      DS    D                                                                
DATCON   DS    A                                                                
ELCODE   DS    X                                                                
AOUTPUT  DS    A                        A(A OUTPUT AREA)                        
CON9S    DS    CL4                                                              
CON9REV  DS    CL4                                                              
WORK     DS    CL64                                                             
WORKDX   EQU   *                                                                
CONREC   DSECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025REGENPTR  09/30/99'                                      
         END                                                                    
