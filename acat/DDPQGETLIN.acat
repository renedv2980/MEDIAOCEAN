*          DATA SET DDPQGETLIN AT LEVEL 003 AS OF 02/27/91                      
*CATALP PQGETLIN                                                                
*                                                                               
***********************************************************************         
*                                                                     *         
*  TITLE:        DDPQGETLIN -- GET PRINT QUEUE LINES                  *         
*                                                                     *         
*  COMMENTS:     OPEN THE GIVEN PRINT QUEUE REPORT AND RETURN ONE     *         
*                LINE AT A TIME TO THE CALLING PROGRAM.               *         
*                                                                     *         
*  PARAMETERS    ON INPUT:                                            *         
*                 P1 = A(7-BYTE PRINT QUEUE REPORT KEY)               *         
*                      2-BYTE HEX USERID                              *         
*                      3-BYTE CHARACTER SUB-ID                        *         
*                      2-BYTE REPORT NUMBER (OR NULLS -- WHICH MEANS  *         
*                         RETURN ALL REPORTS FOR THIS USERID/SUB-ID)  *         
*                 P2 = A(200-BYTE REPORT LINE OUTPUT AREA)            *         
*                                                                     *         
*                ON OUTPUT:                                           *         
*                 P1 BYTE 1     = 0 IF A LINE WAS RETURNED            *         
*                               = 1 IF NO (MORE) REPORT(S) FOUND      *         
*                               = 2 IF END OF REPORT                  *         
*                                                                     *         
***********************************************************************         
         TITLE 'DDPQGETLIN -- READ PRINT QUEUE REPORT LINES'                    
PQGETLIN CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,PQGETLIN                                                       
*                                                                               
         LR    R9,R1               R9 = A(PARAMETERS)                           
         LM    R4,R5,0(R9)         LOAD PARAMETERS                              
*                                                                               
         OC    UKREPNO-UKRECD(,R4),UKREPNO-UKRECD(R4)                           
         BZ    ALLREPS             NO REPORT NUMBER GIVEN                       
*                                                                               
         CLC   SVKEY,0(R4)         HAS USER CHANGED THE KEY?                    
         BE    READLINE            NO                                           
*                                                                               
         XC    KEY,KEY             INITIALIZE FOR NEW REPORT                    
         LA    R3,KEY                                                           
         USING UKRECD,R3                                                        
         MVC   UKKEY,0(R4)         PUT USER KEY INTO MY KEY AREA                
         MVC   SVKEY,UKKEY         SAVE THE NEW KEY                             
         XCEFL PQBUFF,14336        CLEAR PQ BUFFER                              
*                                                                               
         GOTO1 DATAMGR,DMCB,(X'08',GFILE),PRTQUE,UKKEY,(R5),PQBUFF,0            
         MVC   PRTQID,UKUSRINF                                                  
*                                                                               
         GOTO1 DATAMGR,DMCB,(X'08',INDEX),PRTQID,UKKEY,(R5),PQBUFF,0            
         CLI   DMCB+8,0                                                         
         BE    READLINE            WE FOUND THE REPORT                          
*                                                                               
         TM    DMCB+8,X'80'                                                     
         BO    *+6                                                              
         DC    H'0'                                                             
         MVI   0(R9),1             REPORT NOT FOUND                             
         XC    SVKEY,SVKEY                                                      
         B     GOODBYE                                                          
*                                                                               
READLINE GOTO1 DATAMGR,DMCB,(X'08',READ),PRTQID,0,(R5),PQBUFF,0                 
         CLI   DMCB+8,0                                                         
         BE    GOODBYE             WE GOT A REPORT LINE                         
*                                                                               
         MVI   0(R9),2             END OF REPORT                                
*                                                                               
GOODBYE  XMOD1                                                                  
         DROP  R3                                                               
         EJECT                                                                  
ALLREPS  CLC   SVKEY,0(R4)         HAS USER CHANGED KEY?                        
         BE    READLIN2            NO                                           
*                                                                               
         XC    KEY,KEY             INITIALIZE FOR NEW REPORT                    
         LA    R3,KEY                                                           
         USING UKRECD,R3                                                        
         MVC   UKKEY,0(R4)         SAVE THE NEW KEY                             
         MVC   SVKEY,UKKEY                                                      
         XCEFL PQBUFF,14336        CLEAR PQ BUFFER                              
*                                                                               
         GOTO1 DATAMGR,DMCB,(X'08',GFILE),PRTQUE,UKKEY,(R5),PQBUFF,0            
         MVC   PRTQID,UKUSRINF                                                  
         XC    UKINDEX,UKINDEX     READ INDEX FROM BEGINNING                    
*                                                                               
NEXTREP  GOTO1 DATAMGR,DMCB,(X'08',INDEX),PRTQID,UKKEY,(R5),PQBUFF,0            
         CLI   DMCB+8,0                                                         
         BNE   NOTFOUND                                                         
*                                                                               
         CLC   UKKEY(L'UKSRCID+L'UKSUBID),SVKEY                                 
         BE    READLIN2            WE FOUND A REPORT                            
         B     NEXTREP             KEEP LOOKING                                 
*                                                                               
NOTFOUND TM    DMCB+8,X'80'                                                     
         BO    *+6                                                              
         DC    H'0'                                                             
         MVI   0(R9),1             REPORT(S) NOT FOUND                          
         XC    SVKEY,SVKEY                                                      
         B     GOODBYE                                                          
*                                                                               
READLIN2 GOTO1 DATAMGR,DMCB,(X'08',READ),PRTQID,0,(R5),PQBUFF,0                 
         CLI   DMCB+8,0                                                         
         BE    GOODBYE             WE GOT A REPORT LINE                         
*                                                                               
         LA    R3,KEY                                                           
         B     NEXTREP             FIND NEXT REPORT                             
         DROP  R3                                                               
         EJECT                                                                  
DATAMGR  DC    V(DATAMGR)                                                       
PRTQUE   DC    C'PRTQUE '                                                       
PRTQID   DC    C'PRTQ   '                                                       
GFILE    DC    C'GFILE  '                                                       
INDEX    DC    C'INDEX  '                                                       
READ     DC    C'READ   '                                                       
*                                                                               
DMCB     DS    6F                                                               
KEY      DC    XL40'00'            PRINT QUEUE KEY                              
SVKEY    DC    XL(L'UKKEY)'00'     SAVED PRINT QUEUE KEY                        
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
         DS    0D                                                               
         DC    C'*PQBUFF*'                                                      
PQBUFF   DC    14336X'00'          PRINT QUEUE BUFFER                           
         EJECT                                                                  
       ++INCLUDE DMPRTQK                                                        
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDPQGETLIN02/27/91'                                      
         END                                                                    
