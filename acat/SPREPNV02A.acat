*          DATA SET SPREPNV02A AT LEVEL 085 AS OF 08/22/13                      
*CATALP SPNV02A                                                                 
         TITLE 'SPREPNV02A - INVOICE CONTROL REPORT - INPUT ROUTINES'           
***********************************************************************         
*        AKAT - 8/22/13 - INCREASE TSAR BUFFER FOR BUY-ID LIST                  
*        AKAT - 8/13/10 - INCREASE TSAR BUFFER FOR BUY-ID LIST                  
*        AKAT - 4/22/10 - SUPPORT NEW BUY-ID LIST                               
*        AKAT - SET USRSW1 TO A "Y" AT REQFRST                                  
*        EJOR - INCREASE ESTATABL                                               
*        EJOR - FIX STATION FILTER FOR CABLE                                    
*             - FIX ETYPE FILTER TEST                                           
*        EJOR - FIX FOR 2 CHAR MGRP                                             
*        EJOR - INCREASE &^%$ ESTATABL AGAIN                                    
*        EJOR - INCREASE ESTATABL                                               
*        EJOR - NEW CABLE TESTS                                                 
*        AKAT - USE Q2USER TO FILTER OUT ETYPE=BARTER/STEWARDSHIP               
*        AKAT - TAKE OUT FILTER FOR ETYPE=BARTER/STEWARDSHIP FOR AGY MC         
*        EJOR - FOR AGY MC, NO START PERIOD EARLIER THAN 4/1/02                 
*        SEAN - QOPT5 ACCEPTS Y, B, T     1/27/2000                             
*               Y = TOTAL SPOTS, GROSS COST AND NET COST                        
*               B = TOTAL AND TRACE                                             
*               T = TRACE NO TOTAL                                              
*               N = DEFAULT! NO TOTAL, NO TRACE, CAN BE BLANK                   
***********************************************************************         
SPNV02A  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,SPNV02A                                                        
*                                                                               
         LA    RC,2048(RB)                                                      
         LA    RC,2048(RC)                                                      
         USING SPNV02A+4096,RC                                                  
*                                                                               
         L     R9,0(R1)                                                         
         LA    RA,2048(R9)                                                      
         LA    RA,2048(RA)                                                      
         USING SPWORKD,R9,RA                                                    
*                                                                               
         L     R8,=A(SPNVWORK)                                                  
         USING NVWORKD,R8                                                       
         USING OFFICED,OFCBLK                                                   
*                                                                               
         CLI   MODE,CLTFRST                                                     
         BE    NV1                                                              
         CLI   MODE,REQLAST                                                     
         BE    NV200                                                            
         CLI   MODE,REQFRST                                                     
         BNE   EXIT                                                             
         MVI   USRSW1,C'Y'         RESET THIS AT REQFRST!                       
         MVI   FIRSTCLI,C'Y'       INDICATE NO CLIENTS YET                      
         MVI   ESTFILT,0           RESET FLAG                                   
         MVI   GRPTABSW,0          INDICATE MGRTAB AND PGRTAB NOT BUILT         
         XC    PGRKEY,PGRKEY                                                    
         MVI   QBPRD,0                                                          
         XC    QBMKT(5),QBMKT                                                   
         MVI   LUMPPOL,C'N'                                                     
         CLC   QPRD,=C'POL'        TEST POL REQUEST                             
         BNE   *+16                                                             
         CLI   QRECORD+29,C'N'     AND POL BREAKOUT DENIED                      
         BNE   *+8                                                              
         MVI   LUMPPOL,C'Y'        YES-LUMP ALL PRODUCTS TO POL                 
*                                                                               
         MVC   NVREC-8(8),=CL8'**NVREC*'     SET EYECATCHERS                    
         MVC   NVSPECS-8(8),=CL8'**SPECS*'                                      
         MVC   NVDSP-8(8),=CL8'**NVDSP*'                                        
         MVC   NVSORTER,=V(SORTER)                                              
*                                                                               
         GOTO1 =V(SORTER),DMCB,=C'END'                                          
*                                                                               
         LA    R0,NVKEYLEN                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SORTCARD+15(3),DUB                                               
*                                                                               
         LA    R0,NVRECLEN                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  RECCARD+21(3),DUB                                                
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD,0                               
*                                                                               
         MVI   QSKIPLN,SPECSTA     CREATE THE SPEC LIST                         
         OI    QSKIPLN,X'F0'                                                    
         MVC   SPECLST,SPACES                                                   
         MVC   SPECLST(L'SEQUENCE),SEQUENCE                                     
         LA    RE,SPECLST+3        LAST HEADLINE = MARKET GROUP                 
         ST    RE,AHDSPEC                                                       
         CLI   PROGPROF+5,C'Y'     PROFILE TO FORCE CLT=ALL REQ                 
         BE    *+12                                                             
         CLI   QOPT6,C'Y'          OPTION TO FORCE CLT=ALL REQUEST              
         BNE   *+12                                                             
         MVI   SPECLST,C' '                                                     
         B     *+14                                                             
         CLC   QCLT,=C'ALL'        TEST FOR ALL CLIENTS                         
         BNE   *+12                                                             
         MVI   SPECLST+6,SPECCLT   YES - CLIENT IN DETAIL                       
         MVI   SPECLST+1,C' '                                                   
         CLI   QCLT,C'*'           TEST FOR OFFICES                             
         BE    *+16                                                             
         CLI   QCLOFFC,C'Y'                                                     
         BE    *+8                                                              
         MVI   SPECLST,C' '        NO - REMOVE OFFICE                           
         CLI   QPGR,C' '           TEST FOR PRODUCT GROUPS                      
         BNE   *+8                                                              
         MVI   SPECLST+2,C' '      NO - REMOVE PRODUCT GROUP                    
         CLI   QMGR,C' '           TEST FOR MARKET GROUPS                       
         BNE   *+8                                                              
         MVI   SPECLST+3,C' '      NO - REMOVE MARKET GROUP                     
         CLC   =C'NO',QEST         TEST FOR ESTIMATE=NO                         
         BNE   *+8                                                              
         MVI   SPECLST+9,C' '      YES - REMOVE ESTIMATE                        
*                                                                               
         CLI   PROGPROF+6,C'Y'     PRINT BUY CONTRACT ID?                       
         BNE   EXIT                NO                                           
         ICM   R0,15,=A(TSARBUFL)  LENGTH OF BUFFER                             
         GETMAIN  RU,LV=(0),LOC=(ANY,ANY)    31-BIT STORAGE                     
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,ATSARBF          A(BUFFER)                                    
*                                                                               
         OC    ATSARBF,ATSARBF     BUFFER PRESENT?                              
         BNZ   *+6                 YES                                          
         DC    H'0'                WHERE ARE WE SUPPOSED TO KEEP RECS?          
*                                                                               
         XC    TSAREA,TSAREA                                                    
         LA    R2,TSAREA                                                        
         USING TSARD,R2                                                         
*                                                                               
         MVI   TSOFFACT,TSAINI     ACTION = INIT                                
         MVC   TSABUF,ATSARBF      SET A(BUFFER)                                
         MVC   TSAREC,=A(TSARBUFL) SET BUFFLEN HERE ON INIT CALL                
         MVI   TSKEYL,TSARKEYL     KEY LEN                                      
         OI    TSRECI,TSRVAR       SET VARIABLE LENGTH RECS                     
         MVC   TSRECL,=Y(TSARRECL) MAX REC LEN                                  
         OI    TSIND2,TSI2MANY     N'RECS IS FULLWORD                           
         L     RF,ADCONLST         GET A(TSAROFF)                               
         L     RF,TSAROFF-SPADCONS(RF)                                          
         GOTO1 (RF),(R2)           CALL TSAROFF                                 
         CLI   TSERRS,0            ANY ERRORS                                   
         BE    *+6                 NO                                           
         DC    H'0'                YES - DEATH                                  
         B     EXIT                                                             
         DROP  R2                                                               
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,XXX,BI,A),WORK=1'                            
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=XXX'                                   
*                                                                               
SEQUENCE DS    0CL10               BASIC SEQUENCE                               
         DC    C'1'                OFFICE                                       
         DC    C'2'                CLIENT (QCLT NE ALL)                         
         DC    C'7'                PRODUCT GROUP                                
         DC    C'3'                MARKET GROUP                                 
         DC    C'4'                MARKET                                       
         DC    C'5'                STATION                                      
         DC    C' '                CLIENT (QCLT=ALL)                            
         DC    C'6'                PERIOD                                       
         DC    C'8'                PRODUCT                                      
         DC    C'9'                ESTIMATE                                     
         EJECT                                                                  
* CLTFRST *                                                                     
         SPACE 2                                                                
* PROCESS SPEC LIST *                                                           
         SPACE 1                                                                
NV1      CLI   QOPT7,C' '          TEST CLIENT EXCLUSION CODE GIVEN             
         BNH   NV2                                                              
         L     R1,ADCLT                                                         
         MVC   BYTE,CPROF+13-CLTHDRD(R1)                                        
         NI    BYTE,X'0F'                                                       
         LLC   RE,QOPT7                                                         
         EX    RE,*+8              TEST TO EXCLUDE THIS CLIENT                  
         B     *+8                                                              
         TM    BYTE,0                                                           
         BZ    NV2                                                              
         MVI   MODE,CLTLAST        YES                                          
         B     EXIT                                                             
*                                                                               
NV2      CLI   QCBLSYS,C' '        TEST CABLE SYSTEM OPTION SET                 
         BH    *+10                                                             
         MVC   QCBLSYS,PROGPROF+2  NO-SET FROM NV PROFILE                       
*                                                                               
         CLI   FIRSTCLI,C'Y'                                                    
         BNE   NV70                                                             
         MVI   FIRSTCLI,C'N'                                                    
         XC    NVSPECS,NVSPECS                                                  
         LA    R0,L'SPECLST                                                     
         LA    R1,NVSPECS                                                       
         LA    R4,SPECLST                                                       
*                                                                               
         LA    RE,HROW1                                                         
         ST    RE,FULL                                                          
NV3      CLI   0(R4),C' '                                                       
         BE    NV4                                                              
         BAS   RE,SETSPEC                                                       
NV4      C     R4,AHDSPEC          TEST LAST HEADLINE SPEC                      
         BE    NV5                                                              
         LA    R4,1(R4)                                                         
         BCT   R0,NV3                                                           
         DC    H'0'                                                             
NV5      LA    RE,PROW1                                                         
         ST    RE,FULL                                                          
         B     NV8                                                              
NV6      CLI   0(R4),C' '                                                       
         BE    NV8                                                              
         BAS   RE,SETSPEC                                                       
NV8      LA    R4,1(R4)                                                         
         BCT   R0,NV6                                                           
         SPACE 1                                                                
* NOW SET UP COLUMNS *                                                          
         SPACE 1                                                                
         LAY   RE,COLSPT                                                        
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         LAY   RE,COLGRS                                                        
         CLI   QOPT1,C'G'          TEST PRINT GROSS COST                        
         BE    NV10                                                             
         CLI   QOPT1,C'B'          OR GROSS AND NET                             
         BNE   NV12                                                             
NV10     ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
NV12     LAY   RE,COLNET                                                        
         CLI   QOPT1,C'N'          TEST PRINT NET COST                          
         BE    NV14                                                             
         CLI   QOPT1,C'B'                                                       
         BNE   NV16                                                             
NV14     ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
NV16     LAY   RE,COLMAT                                                        
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         LAY   RE,COLCLR                                                        
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         LAY   RE,PROWACT          SET ACTIVITY DATE AS LAST ROW                
         ST    RE,0(R1)                                                         
         LA    R1,4(R1)                                                         
         MVI   0(R1),X'FF'         SET EOL FLAG                                 
         B     NV20                                                             
         EJECT                                                                  
* SUBR TO MOVE SPEC ADDRESSES TO LIST *                                         
* RE IS RETURN                                                                  
* R1 POINTS TO OPEN SLOT IN LIST                                                
* R4 POINTS TO QSPEC                                                            
* FULL HAS BASE SPEC LIST ADDRESS                                               
         SPACE 1                                                                
SETSPEC  DS    0H                                                               
         LLC   RF,0(R4)            GET SPEC NUMBER                              
         N     RF,=X'0000000F'                                                  
         BCTR  RF,0                                                             
         MHI   RF,12               X ENTRY WIDTH                                
         A     RF,FULL             POINT TO ENTRIES                             
*                                                                               
         USING SPECD,R2                                                         
*                                                                               
         L     R2,0(RF)            GET FIRST SPEC ADDRESS                       
         ST    R2,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         ICM   R2,15,4(RF)         GET SECOND ENTRY                             
         BZR   RE                                                               
*                                                                               
         CLI   SPECDATA,SPECMGR2                                                
         BNE   *+12                                                             
         CLC   MGR1LEN,MGR2LEN     TEST 2 LEVELS OF MKTGRP                      
         BER   RE                  NO - DONE                                    
         CLI   SPECDATA,SPECPGR2                                                
         BNE   *+12                                                             
         CLC   PGR1LEN,PGR2LEN     TEST 2 LEVELS OF PRDGRP                      
         BER   RE                  NO                                           
         ST    R2,0(R1)                                                         
         LA    R1,4(R1)                                                         
*                                                                               
         ICM   R2,15,8(RF)         GET THIRD ENTRY                              
         BZR   RE                                                               
         CLI   SPECDATA,SPECMGR3                                                
         BNE   *+12                                                             
         CLC   MGR2LEN,MGR3LEN     TEST 3 LEVELS OF MKTGRP                      
         BER   RE                  NO - DONE                                    
         ST    R2,0(R1)                                                         
         LA    R1,4(R1)                                                         
         BR    RE                                                               
         EJECT                                                                  
* NOW SET DISPLACEMENTS TO DATA VALUES IN RECORD *                              
         SPACE 1                                                                
NV20     DS    0H                                                               
         LA    R1,NVSPECS                                                       
         SR    RE,RE               INITIAL ROW VALUE                            
         LA    RF,NVKEYLEN         INITIAL COL VALUE                            
*                                                                               
NV22     L     R2,0(R1)                                                         
         USING SPECD,R2                                                         
*                                                                               
         CLI   SPECDATA,128        TEST ROW OR COL                              
         BH    NV24                                                             
         STC   RE,0(R1)            SET ROW DATA DSPL                            
         LA    RE,4(RE)                                                         
*        CLI   SPECDATA,SPECMGR1                                                
*        BNE   NV26                                                             
*        CLC   MGR1LEN,MGR2LEN                                                  
*        BE    NV26                                                             
*        LA    RE,4(RE)                                                         
*        CLC   MGR2LEN,MGR3LEN                                                  
*        BE    NV26                                                             
*        LA    RE,4(RE)                                                         
         B     NV26                                                             
*                                                                               
NV24     STC   RF,0(R1)            SET COL DATA DSPL                            
         LA    RF,4(RF)                                                         
*                                                                               
NV26     LA    R1,4(R1)            NEXT LIST ENTRY                              
         CLI   0(R1),X'FF'                                                      
         BNE   NV22                                                             
         SPACE 1                                                                
* SET SIGNIFCANT DATA ABOUT SPECS *                                             
         SPACE 1                                                                
         XC    NVDSP,NVDSP         CLEAR DISPLACEMENT TABLE                     
         LA    R1,NVSPECS                                                       
NV30     L     R2,0(R1)                                                         
         USING SPECD,R2                                                         
         CLI   SPECDATA,SPECACT    TEST ACTIVITY DATE                           
         BE    NV32                                                             
         LA    R1,4(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   NV30                                                             
         DC    H'0'                                                             
NV32     LLC   R0,0(R1)                                                         
         STH   R0,NVDSPACT         SET DSPL TO ACTIVITY DATE                    
         BCTR  R0,0                                                             
         STH   R0,NVCMPRLN         AND SET LEN FOR ADD RECORDS COMPARE          
*                                                                               
*                                                                               
         SR    R0,R0                                                            
         LA    R1,NVSPECS                                                       
*                                                                               
NV34     L     R2,0(R1)                                                         
         USING SPECD,R2                                                         
*                                                                               
         CLI   SPECDATA,SPECCLT                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPCLT                                                      
*                                                                               
         CLI   SPECDATA,SPECPRD                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPPRD                                                      
*                                                                               
         CLI   SPECDATA,SPECMGR1                                                
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPMG1                                                      
*                                                                               
         CLI   SPECDATA,SPECMKT                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPMKT                                                      
*                                                                               
         CLI   SPECDATA,SPECSTA                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPSTA                                                      
*                                                                               
         CLI   SPECDATA,SPECEST                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPEST                                                      
*                                                                               
         CLI   SPECDATA,SPECPER                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPPER                                                      
*                                                                               
         CLI   SPECDATA,SPECSPT                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPSPT                                                      
*                                                                               
         CLI   SPECDATA,SPECGRS                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPGRS                                                      
*                                                                               
         CLI   SPECDATA,SPECNET                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPNET                                                      
*                                                                               
         CLI   SPECDATA,SPECMAT                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPMAT                                                      
*                                                                               
         CLI   SPECDATA,SPECCLR                                                 
         BNE   *+12                                                             
         IC    R0,0(R1)                                                         
         STH   R0,NVDSPCLR                                                      
*                                                                               
         LA    R1,4(R1)                                                         
         CLI   0(R1),X'FF'                                                      
         BNE   NV34                                                             
         EJECT                                                                  
* BUILD EASI STA LIST                                                           
         MVC   AREC,ADBUY                                                       
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING CT3REC,R6                                                        
         MVI   CT3KTYP,CT3KTYPQ                                                 
         MVI   CT3ESUB,CT3EASIQ                                                 
         MVC   CT3EMED,QMED        MEDIA                                        
         GOTO1 DATAMGR,DMCB,DMRDHI,CTFILE,KEY,AREC,0                            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R6,AREC                                                          
         L     R5,AESTATAB                                                      
         LHI   R0,ESTATABL                                                      
*                                                                               
NV34A    CLC   0(CT3ESTAT-CT3REC,R6),KEY                                        
         BNE   NV34B                                                            
         MVC   0(L'ESTATAB,R5),CT3ESTAT                                         
         LA    R5,L'ESTATAB(R5)                                                 
         GOTO1 DATAMGR,DMCB,=CL8'DMRSEQ',=CL8'CTFILE',KEY,AREC,0                
         BCT   R0,NV34A                                                         
         DC    H'0'                INCREASE ESTATABL                            
NV34B    MVI   0(R5),X'00'         EOT                                          
         DROP  R6                                                               
         EJECT                                                                  
* BUILD DATE LIST *                                                             
         SPACE 1                                                                
         XC    NVDATES,NVDATES     CLEAR DATE TABLE                             
         MVI   NVPARTMN,C'N'                                                    
         CLC   QEND+4(2),=C'00'    TEST FOR DAYS IN REQUEST DATES               
         BNH   NV35                                                             
         CLC   QSTART(4),QEND      YES - TEST FOR START/END SAME MONTH          
         BNE   NV35                                                             
         PACK  DUB,QSTART+4(2)     YES - TEST FOR PARTIAL MONTH                 
         CVB   R4,DUB                                                           
         PACK  DUB,QEND+4(2)                                                    
         CVB   R5,DUB                                                           
         SR    R5,R4                                                            
         LA    R5,1(R5)                                                         
         GOTO1 GETBROAD,DMCB,QSTART,WORK                                        
         CLI   DMCB,X'FF'                                                       
         BNE   *+6                                                              
         DC    H'0'                                                             
         LA    R1,28                                                            
         CLI   DMCB,5                                                           
         BNE   *+8                                                              
         LA    R1,35                                                            
         CR    R5,R1                                                            
         BNL   NV35                                                             
         MVI   NVPARTMN,C'Y'       PARTIAL MONTH                                
         GOTO1 DATCON,DMCB,(0,QSTART),(2,NVDATES)                               
         MVC   BQSTARTP,NVDATES                                                 
         GOTO1 DATCON,DMCB,QEND,(2,NVDATES+2)                                   
         MVC   BQENDP,NVDATES+2                                                 
         GOTO1 DATCON,DMCB,QEND,(3,FULL)                                        
         MVC   NVDATES+4(2),FULL                                                
         B     NV40                                                             
CTFILE   DC    CL8'CTFILE'                                                      
*                                                                               
NV35     MVC   WORK(12),SPACES                                                  
         MVC   WORK(4),QEND                                                     
         MVC   WORK+4(2),=C'01'                                                 
         MVC   WORK+6(4),QEND                                                   
***      MVC   WORK+10(2),=C'01'   FUCKING HELL                                 
         GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK,-2                                   
*                                                                               
         CLC   QAGY,=C'MC'                                                      
         BNE   NV36                                                             
         CLC   QSTART,SPACES                                                    
         BH    NV36                                                             
         MVC   QSTART,=X'FAF2F0F4F0F1'   C'020401'  Y2K DATE                    
*                                                                               
NV36     L     R5,ADBUY                                                         
         CLC   WORK(4),QSTART      TEST FOR LATER REQUESTED START DATE          
         BL    *+12                                                             
         LA    R5,4(R5)            NO - 25 MNTHS SO SKIP 1ST MOBILE MON         
         B     *+10                                                             
         MVC   WORK(4),QSTART      YES - SET REQ START YEAR/MONTH               
         MVI   SPOTPROF+8,1        MAKE SURE MONTHS START ON MONDAY             
         GOTO1 MOBILE,DMCB,(25,WORK),(1,ADBUY)                                  
         SPACE 1                                                                
* NOW CONSTRUCT 6 BYTE ENTRIES - START(2),END(2),Y/M(2)                         
         SPACE 1                                                                
         LA    R0,24                                                            
         LA    R4,NVDATES                                                       
         MVC   BQSTARTP,0(R5)      SET START DATE OF FIRST MONTH                
*                                                                               
NV37     MVC   0(4,R4),0(R5)       MOVE 2 BYTE DATES                            
         GOTO1 DATCON,DMCB,(2,2(R5)),(3,FULL)                                   
         MVC   4(2,R4),FULL        MOVE BINARY Y/M                              
         LA    R4,6(R4)                                                         
         LA    R5,4(R5)                                                         
         CLI   0(R5),X'FF'                                                      
         BE    NV38                                                             
         BCT   R0,NV37                                                          
*                                                                               
NV38     SHI   R4,6                                                             
         MVC   BQENDP,2(R4)        SET END MONTH END DATE                       
         EJECT                                                                  
* DETERMINE REQUEST FILTERS *                                                   
         SPACE 1                                                                
NV40     CLC   QPRD,=C'ALL'        PRODUCT                                      
         BE    NV50                                                             
         MVI   QBPRD,X'FF'                                                      
         CLC   QPRD,=C'POL'                                                     
         BE    NV50                                                             
         CLI   QPRD,C'0'                                                        
         BNL   NV46                                                             
         L     RF,ADCLT            FIND PRODUCT CODE FROM CLTHDR                
         LA    RF,CLIST-CLTHDRD(RF)                                             
*                                                                               
NV42     CLC   0(3,RF),QPRD                                                     
         BE    NV44                                                             
         LA    RF,4(RF)                                                         
         CLI   0(RF),C' '                                                       
         BH    NV42                                                             
         DC    H'0'                NOT FOUND                                    
*                                                                               
NV44     MVC   QBPRD,3(RF)         PRODUCT CODE                                 
         B     NV50                                                             
*                                                                               
NV46     LA    R1,QPRD+2    WORK OUT NO OF DIGITS FOR PRDGRP FILTERING          
         LA    R0,2                                                             
*                                                                               
NV48     CLI   0(R1),C'*'                                                       
         BE    *+12                                                             
         CLI   0(R1),C' '                                                       
         BNE   *+10                                                             
         BCTR  R1,0                                                             
         BCT   R0,NV48                                                          
         STH   R0,QPGRLEN                                                       
*                                                                               
NV50     MVI   QBEST,1             DETERMINE ESTIMATE RANGE                     
         MVI   QBESTEND,255                                                     
         MVI   ESTFILT,0                                                        
         CLC   QEST,SPACES                                                      
         BNH   NV60                                                             
         CLC   QEST,=C'ALL'                                                     
         BE    NV51                                                             
         CLC   QEST(2),=C'NO'                                                   
         BNE   NV52                                                             
NV51     CLC   QESTEND,SPACES                                                   
         BE    NV60                                                             
         MVI   ESTFILT,C'Y'        ESTIMATE FILTERING                           
         B     NV60                                                             
*                                                                               
NV52     PACK  DUB,QEST                                                         
         CVB   RE,DUB                                                           
         STC   RE,QBEST                                                         
         STC   RE,QBESTEND                                                      
         CLC   QESTEND,SPACES                                                   
         BE    NV60                                                             
         PACK  DUB,QESTEND                                                      
         CVB   RE,DUB                                                           
         STC   RE,QBESTEND                                                      
*                                                                               
NV60     CLC   =C'ALL',QMKT        MARKET                                       
         BE    NV61                                                             
         PACK  DUB,QMKT                                                         
         CVB   R0,DUB                                                           
         STCM  R0,3,QBMKT                                                       
*                                                                               
NV61     XC    QBSTA,QBSTA                                                      
*************************************************                               
*                                               *                               
* CALLING MSPACK FOR A CABLE STATION FROM QSTA  *                               
* IS DEATH IF REQ IS FOR A SINGLE EST, SINCE    *                               
* EST WILL BECOME THE NETWORK ON THE STAPACK    *                               
* CALL - QSTANET WILL HAVE QSTA(5) + QCBLNET(3) *                               
*                                               *                               
*************************************************                               
         MVC   QSTANET(5),QSTA                                                  
         MVC   QSTANET+5(3),QCBLNET                                             
*                                                                               
         CLI   QSTA+4,C'/'         TEST INCLUDE CABLE                           
         BE    NV63                THEN CAN'T HAVE MKTGRP                       
         CLI   QSTA+4,C'-'         TEST EXCLUDE CABLE                           
         BE    NV63                THEN CAN'T HAVE MKTGRP                       
*                                                                               
         CLI   QSTA,C'Z'           NO-TEST ALPHA STA                            
         BH    NV65                HIGH IS MKTGRP                               
NV63     CLC   =C'ALL',QSTA                                                     
         BE    NV70                                                             
         CLI   QMGR,C' '           MKT GROUP REQUEST FOR CABLE ONLY             
         BNE   NV65                                                             
* SINGLE STATION REQUEST                                                        
         GOTO1 MSPACK,DMCB,QMKT,QSTANET,QBMKTSTA                                
         B     NV70                                                             
         SPACE 1                                                                
* WORK OUT NUMBER OF DIGITS FOR MKTGRP FILTERING *                              
         SPACE 1                                                                
NV65     LA    R1,QSTA+3           POINT TO LAST CHAR                           
         LA    R0,3                PRESET TO MAX-1                              
*                                                                               
NV66     CLI   0(R1),C'*'                                                       
         BE    *+12                                                             
         CLI   0(R1),C' '                                                       
         BNE   NV68                                                             
         BCTR  R1,0                                                             
         BCT   R0,NV66                                                          
*                                                                               
NV68     STH   R0,QMGRLEN                                                       
         B     NV70                                                             
         EJECT                                                                  
NV70     DS    0H                                                               
         BAS   RE,BLDESTAB         BUILD ESTIMATE TABLE                         
*                                                                               
         L     R0,AMGRTAB          SET ADDRESS CLEARED BY NXTCLT                
         ST    R0,SVMKTADR         PASS IN UNUSED ADCON                         
*                                                                               
         CLI   GRPTABSW,C'Y'       TEST MGRTAB/PGRTAB BUILT YET                 
         BE    *+12                YES                                          
         BAS   RE,BLDGRP           NO - BUILD MGRTAB/PGRTAB NOW                 
         MVI   GRPTABSW,C'Y'       AND SET FLAG                                 
*                                                                               
         CLI   QPGR,C' '                                                        
         BNH   NV71X                                                            
         OC    PGRKEY,PGRKEY                                                    
         BNZ   NV71                                                             
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D81'                                                  
         MVC   KEY+2(1),BAGYMD                                                  
         MVC   KEY+3(2),BCLT                                                    
         MVC   KEY+5(1),QPGR                                                    
         CLI   QPRD,C'0'                                                        
         BL    NV70A                                                            
         MVI   FULL,C'0'                                                        
         MVC   FULL+1(3),FULL                                                   
         LH    RE,QPGRLEN                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   FULL(0),QPRD                                                     
         PACK  DUB,FULL(5)                                                      
         MVC   KEY+6(2),DUB+5                                                   
*                                                                               
NV70A    GOTO1 HIGH                                                             
         B     NV71A                                                            
*                                                                               
NV71     MVC   KEY(13),PGRKEY                                                   
         GOTO1 HIGH                                                             
         GOTO1 SEQ                                                              
*                                                                               
NV71A    CLC   KEY(6),KEYSAVE                                                   
         BNE   EXIT                                                             
         CLI   QPRD,C'0'                                                        
         BL    NV71B                                                            
         MVC   FULL(2),KEY+6                                                    
         UNPK  DUB(5),FULL(3)                                                   
         LH    RE,QPGRLEN                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   DUB(0),QPRD                                                      
         BNE   EXIT                                                             
*                                                                               
NV71B    MVC   PGRKEY,KEY                                                       
         L     RF,ADCLT            FIND PRODUCT CODE FROM CLTHDR                
         LA    RF,CLIST-CLTHDRD(RF)                                             
*                                                                               
NV71C    CLC   0(3,RF),KEY+8                                                    
         BE    NV71D                                                            
         LA    RF,4(RF)                                                         
         CLI   0(RF),C' '                                                       
         BH    NV71C                                                            
         DC    H'0'                                                             
*                                                                               
NV71D    MVC   QBPRD,3(RF)         PRODUCT CODE                                 
         SPACE 1                                                                
* READ FOR BUY RECORDS *                                                        
         SPACE 1                                                                
NV71X    L     R6,ADCLT                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(3),1(R6)                                                     
         MVC   KEY+3(1),QBPRD                                                   
         CLI   QMED,C'C'           TEST CANAD COMBINED                          
         BNE   *+8                                                              
         MVI   KEY+3,X'FF'         YES - PROCESS POL ONLY                       
*                                                                               
NV72     GOTO1 HIGH                                                             
*                                                                               
NV74     CLC   KEY(3),KEYSAVE      SAME A-M/C                                   
         BE    NV75                                                             
         CLI   QBPRD,0             NO - TEST FOR PRODUCT GROUP                  
         BE    EXIT                                                             
         OC    PGRKEY,PGRKEY                                                    
         BZ    EXIT                                                             
         B     NV71                     YES - NEXT PRODUCT                      
*                                                                               
NV75     CLI   QBPRD,0             TEST PRODUCT FILTERING                       
         BE    NV76                                                             
         CLC   KEY+3(1),QBPRD      YES - MATCH THE PRODUCT                      
         BNE   NV78                      NOT EQUAL - NEXT PRODUCT               
*                                                                               
NV76     OC    QBMKT,QBMKT         TEST ALL MARKETS                             
         BZ    NV80                YES                                          
         CLC   KEY+4(2),QBMKT      TEST RIGHT MARKET                            
         BE    NV80                YES                                          
         BH    NV78                                                             
         MVC   KEY+4(5),QBMKTSTA   READ FOR MARKET/STATION                      
         XC    KEY+9(4),KEY+9                                                   
         B     NV72                                                             
         SPACE 1                                                                
* READ FOR NEXT PRODUCT *                                                       
         SPACE 1                                                                
NV78     CLI   QBPRD,0             TEST FOR PRODUCT FILTERING                   
         BE    NV79                                                             
         OC    PGRKEY,PGRKEY       YES - TEST BY PRDGRP                         
         BNZ   NV71                      YES - NEXT PRODUCT IN GROUP            
         B     EXIT                      NO  - DONE                             
*                                                                               
NV79     CLI   KEY+3,X'FF'         TEST HIGH PRD CODE                           
         BE    EXIT                YES - DONE                                   
         IC    RE,KEY+3            NO -  NEXT PRODUCT                           
         LA    RE,1(RE)                                                         
         STC   RE,KEY+3                                                         
         MVC   KEY+4(5),QBMKTSTA                                                
         XC    KEY+9(4),KEY+9                                                   
         B     NV72                                                             
*                                                                               
NV80     OC    QBSTA,QBSTA         TEST STATION OR CABLE FILTER                 
         BNZ   NV90                YES                                          
         CLI   QMGR,C' '           TEST MGR REQUEST                             
         BE    *+12                NO YSFI                                      
         CLI   QSTA,C'0'           TEST NUMERIC STA (MKTGRP)                    
         BNL   NV82                YES                                          
         CLI   QSTA+4,C'/'         TEST CABLE ONLY                              
         BE    NV90                                                             
         CLI   QSTA+4,C'-'         TEST EXCLUDE CABLE                           
         BE    NV90                                                             
         CLI   QCBLNET,C' '        TEST CABLE FILTER                            
         BH    NV90                YES                                          
         B     NV100                                                            
         EJECT                                                                  
* FILTER ON MKTGRP *                                                            
         SPACE 1                                                                
NV82     SR    RE,RE                                                            
         ICM   RE,3,KEY+4          GET MKT NUM                                  
         AR    RE,RE               X 2                                          
         A     RE,SVMKTADR         + TABLE START                                
         UNPK  DUB,0(3,RE)         UNPACK 1 EXTRA BYTE                          
         LH    RE,QMGRLEN                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   QSTA(0),DUB+3    *EXECUTED*                                      
         BE    NV90                                                             
*                                                                               
         MVC   KEY+6(7),XFF        READ FOR NEXT MARKET                         
         B     NV72                                                             
*                                                                               
NV90     BAS   RE,STAFILT                                                       
         BE    NV100                                                            
*                                                                               
         MVC   KEY+9(4),XFF          FORCE NEXT STA                             
         B     NV72                                                             
         EJECT                                                                  
NV100    DS    0H                                                               
         L     RF,ADAGY            TEST CANADIAN                                
         CLI   AGYPROF+7-AGYHDR(RF),C'C'                                        
         BNE   NV102                                                            
         CLI   QMED,C'N'           NETWORK                                      
         BNE   NV102                                                            
*                                                                               
         OC    KEY+4(2),KEY+4      PROCESS ONLY NETWORK                         
         BZ    NV102                                                            
         MVC   KEY+4(9),XFF        FORCE NEXT PROD                              
         B     NV72                                                             
*                                                                               
NV102    CLI   KEY+3,X'FF'         TEST POL                                     
         BNE   NV104               NO                                           
         TM    KEY+10,X'80'        TEST SPILL POINTER                           
         BZ    NV110               NO - PROCESS                                 
         MVC   KEY+10(3),XFF       FORCE NEXT EST                               
         B     NV72                                                             
*                                                                               
NV104    CLI   QBPRD,0             TEST PRODUCT FILTERING                       
         BNE   NV110               YES                                          
         CLI   KEY+10,0            TEST REG OR ACTIVE P/B                       
         BE    NV110               YES - PROCESS                                
         MVC   KEY+10(3),XFF       ELSE FORCE NEXT EST                          
         B     NV72                                                             
*                                                                               
NV110    CLC   KEY+9(1),QBEST      TEST ESTIMATE IS WITHIN RANGE                
         BNL   *+20                                                             
         MVC   KEY+9(1),QBEST      LOW - JUMP TO LOWEST IN RANGE                
         XC    KEY+10(3),KEY+10                                                 
         B     NV72                                                             
         CLC   KEY+9(1),QBESTEND                                                
         BNH   *+14                                                             
         MVC   KEY+9(4),XFF        HIGH - NEXT STATION                          
         B     NV72                                                             
*        CLI   ESTFILT,C'Y'        TEST FOR ESTIMATE FILTERING                  
*        BNE   NV120               NO                                           
         LLC   RE,KEY+3            PRODUCT                                      
         BCTR  RE,0                                                             
         SLL   RE,8                X 256                                        
         LLC   R0,KEY+9            ESTIMATE                                     
         BCTR  R0,0                                                             
         AR    RE,R0                                                            
         A     RE,AESTTAB                                                       
         CLI   0(RE),0             TEST ACTIVE                                  
         BNE   NV120                                                            
         MVC   KEY+10(3),XFF       NO - FORCE NEXT EST                          
         B     NV72                                                             
         SPACE 2                                                                
NV120    CLI   QEIXSTA,C' '        TEST INCLUDE/EXCLUDE EASI STATIONS           
         BNH   NV121                                                            
         GOTO1 MSUNPK,DMCB,(X'80',KEY+4),WORK,WORK+4                            
         CLI   WORK+8,C' '                                                      
         BH    *+8                                                              
         MVI   WORK+8,C'T'                                                      
         L     R1,AESTATAB                                                      
*                                                                               
NV120A   CLI   0(R1),0                                                          
         BE    NV120B                                                           
         CLC   WORK+4(5),0(R1)                                                  
         BE    *+12                                                             
         LA    R1,L'ESTATAB(R1)                                                 
         B     NV120A                                                           
         CLI   QEIXSTA,C'Y'                                                     
         BE    NV121                                                            
         B     NV120C                                                           
*                                                                               
NV120B   CLI   QEIXSTA,C'Y'                                                     
         BNE   NV121                                                            
*                                                                               
NV120C   MVC   KEY+9(4),XFF                                                     
         B     NV72                                                             
*                                                                               
NV121    MVC   KEYSAVE,KEY         SET BUY DATA IN PROCESS                      
*                                                                               
NV122    BAS   RE,NVBUY                                                         
*                                                                               
NV124    GOTO1 SEQ                                                              
*                                                                               
         CLC   KEY(10),KEYSAVE      A-M/C/P/MKT/STA/EST                         
         BE    NV125                                                            
         CLI   QCBLSYS,C'Y'         TEST REPORT CABLE BY SYSTEM                 
         BNE   NV128                                                            
         CLC   KEY(6),KEYSAVE       YES-TEST ALL IS THE SAME EXCEPT             
         BNE   NV128                    FOR CABLE NETWORK                       
         CLC   KEY+9(1),KEYSAVE+9                                               
         BNE   NV128                                                            
         GOTO1 MSUNPK,DMCB,(X'80',KEY+4),WORK,WORK+4                            
         CLI   WORK+4,C'0'                                                      
         BL    NV128                                                            
         GOTO1 (RF),(R1),(X'80',KEYSAVE+4),WORK+12,WORK+16                      
         CLC   WORK(4),WORK+16                                                  
         BNE   NV128                                                            
*                                                                               
NV125    CLI   KEY+3,X'FF'         TEST POL                                     
         BNE   NV126               NO                                           
         TM    KEY+10,X'80'        TEST SPILL POINTER                           
         BZ    NV122               NO - PROCESS                                 
         B     NV124               YES - SKIP                                   
*                                                                               
NV126    CLI   QBPRD,0             TEST PRODUCT FILTER                          
         BNE   NV122                                                            
         CLI   KEY+10,0            NO - TEST REG OR ACTIVE P/B                  
         BE    NV122                                                            
         B     NV124                                                            
*                                                                               
NV128    BAS   RE,NVGEN            GENERATE SORT RECORDS                        
         B     NV74                                                             
         SPACE 2                                                                
* ALL INPUT DATA PROCESSED - PASS CONTROL TO OUTPUT ROUTINES *                  
         SPACE 1                                                                
NV200    DS    0H                                                               
         L     RF,=V(SPNV02B)      GO TO OUTPUT PHASE                           
         GOTO1 (RF),DMCB,(R9),(R8)                                              
*                                                                               
         CLI   PROGPROF+6,C'Y'     PRINT BUY CONTRACT ID?                       
         BNE   EXIT                NO                                           
         L     R1,ATSARBF          A(BUFFER)                                    
         ICM   R0,15,=A(TSARBUFL)  LENGTH OF BUFFER                             
         FREEMAIN RC,A=(1),LV=(0)  FREE STORAGE                                 
         LTR   RF,RF                                                            
         BZ    *+6                 CHECK FOR ERRORS                             
         DC    H'0'                                                             
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
* SUBROUTINE TO PROCESS BUY RECORD AND POST TO TABLE *                          
         SPACE 1                                                                
NVBUY    NTR1                                                                   
         L     R2,ADBUY                                                         
         USING BUYRECD,R2                                                       
*                                                                               
         MVI   DMINBTS,X'08'       SET TO PASS DELETED RECORDS                  
         MVI   DMOUTBTS,X'FD'      DO NOT TEST X'02'                            
         GOTO1 GETBUY                                                           
*                                                                               
         MVC   BUYKSAVE,BUYREC     SAVE BUYREC KEY                              
*                                                                               
         TM    BUYREC+15,X'80'     TEST DELETED                                 
         BO    EXIT                YES - IGNORE                                 
         CLC   BUYKCLT,KEY+1       TEST CLIENT IN REC = CLIENT IN KEY           
         BNE   EXIT                NO - IGNORE                                  
*                                                                               
         CLC   BUYMSTA(2),KEY+4    TEST SPILL                                   
         BNE   EXIT                YES - IGNORE                                 
*                                                                               
         TM    BDCIND2,X'04'       BDCIND IS A CHARACTER                        
         BZ    NVBUY1A                                                          
         TM    BDCIND2,BDC2NEG     ADD NEGATIVES INTO EXCLUDED                  
         BO    EXIT                                                             
         B     NVBUY1B                                                          
NVBUY1A  TM    BDCIND,X'01'        TEST MINUS SPOT                              
         BO    EXIT                YES - IGNORE                                 
*                                                                               
NVBUY1B  CLI   QOPT4,C'Y'          TEST SUPPRESS SPECIAL REP $                  
         BNE   *+14                                                             
         OC    BDREP,BDREP         YES-TEST FOR SPECIAL REP                     
         BNZ   EXIT                    YES-IGNORE                               
*                                                                               
         CLI   QOPT4,C'S'          TEST SPECIAL REP $ ONLY?                     
         BNE   *+14                                                             
         OC    BDREP,BDREP         TEST FOR SPECIAL REP                         
         BZ    EXIT                 NO - IGNORE                                 
*                                                                               
         BAS   RE,FIX              FIX SPECIAL TRAFFIC PRODUCT CODES            
*                                                                               
         MVC   BUYPRD1,BUYKEY+3    SET BUYLINE PRD CODE                         
         MVI   BUYPRD2,0                                                        
         XC    AIDELEM,AIDELEM     NO BUY ID FIELD                              
         CLI   PROGPROF+6,C'Y'     PRINT BUY CONTRACT ID?                       
         BNE   NVBUY1C             NO                                           
*                                                                               
         LA    R6,BDELEM           A(BUY RECORD)                                
         MVI   ELCDLO,IDELCODQ     X'70' BUY ID ELEMENT CODE                    
         MVI   ELCDHI,IDELCODQ     X'70' BUY ID ELEMENT CODE                    
         BAS   RE,NEXTEL           HAVE A BUY ID ELEMENT?                       
         BNE   *+12                NO                                           
         USING IDELEM,R6                                                        
         LA    R3,IDCONNO          BUY ID FIELD                                 
         ST    R3,AIDELEM          SAVE A(BUY ID)                               
         DROP  R6                                                               
*                                                                               
NVBUY1C  CLI   BDTIME,0            TEST NON-POL PIGGYBACK                       
         BE    NVBUY1X                                                          
* FIND PBELEM                                                                   
         LA    R6,BDELEM                                                        
         MVI   ELCDLO,4                                                         
         MVI   ELCDHI,4                                                         
         BAS   RE,NEXTEL                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   BUYPRD2,2(R6)                                                    
         MVI   BDTIME,0            NOW SUPPRESS P/B SPLIT IN GETRATE            
*                                                                               
NVBUY1X  MVI   ELCDLO,6                                                         
         MVI   ELCDHI,13                                                        
         LA    R6,BDELEM                                                        
*                                                                               
NVBUY2   BAS   RE,NEXTEL                                                        
         BNE   EXIT                                                             
*                                                                               
         SPACE 1                                                                
* TEST FOR PRIOR OR SUBSEQUENT *                                                
         SPACE 1                                                                
NVBUY3   CLC   2(2,R6),BQSTARTP    TEST PRIOR TO REQ START                      
         BL    NVBUY2              YES                                          
*                                                                               
NVBUY4   CLC   2(2,R6),BQENDP      TEST AFTER REQ END                           
         BH    NVBUY2              NO                                           
         SPACE 1                                                                
* ELEMENT IS IN REQUEST PERIOD *                                                
         SPACE 1                                                                
         XC    SPTCNTRS,SPTCNTRS   CLEAR COUNTERS                               
         MVC   SPTDATE,2(R6)       SAVE ELEMENT DATE                            
*                                                                               
         CLI   BUYREC+3,X'FF'      TEST POL BUY                                 
         BNE   NVBUY6              NO - PRD SET ALREADY                         
         CLI   1(R6),10            TEST UNALLOCATED                             
         BNH   NVBUY2                                                           
         MVC   BUYPRD1,10(R6)      MOVE PRD 1                                   
         MVI   BUYPRD2,0                                                        
         CLI   1(R6),18            TEST PIGGYBACK                               
         BL    *+10                                                             
         MVC   BUYPRD2,14(R6)      MOVE PRD 2                                   
         CLI   QBPRD,0             TEST FOR PRODUCT FILTER                      
         BE    NVBUY6                                                           
         CLI   QBPRD,X'FF'                                                      
         BE    NVBUY6                                                           
         CLI   1(R6),18            YES - TEST PIGGYBACK                         
         BNL   NVBUY2                    YES - SKIP                             
         CLC   BUYPRD1,QBPRD             TEST PRODUCT                           
         BNE   NVBUY2                    NOT EQUAL - SKIP                       
*                                                                               
NVBUY6   LLC   R0,BUYREC+3         GET BUYLINE PRD CODE                         
*                                                                               
         IC    R3,6(R6)            SAVE RSTATUS                                 
         L     RF,ADAGY                                                         
         CLI   AGYPROF+7-AGYHDR(RF),C'C'   IF CANADIAN                          
         BNE   NVBUY6D                                                          
         CLI   QMED,C'N'           NETWORK                                      
         BNE   NVBUY6D                                                          
         OC    7(3,R6),7(R6)       SUPPRESS 0 COST OVERRIDES                    
         BNZ   *+8                                                              
         NI    6(R6),X'DF'                                                      
         CLI   BUYREC+3,X'FF'      SHOULD BE POOL FOR UNALLOC BUT CHECK         
         BNE   NVBUY6D                                                          
         CLC   BUYMSTA(2),=X'0000'                                              
         BNE   *+8                                                              
         MVI   BUYMSTA+1,X'01'     FUDGE MKT TO GET VALUES FROM GETRATE         
*                                                                               
NVBUY6D  DS    0H                                                               
         GOTO1 GETRATE,DMCB,((R0),SPOTS),BUYREC,(R6)                            
         STC   R3,6(R6)            RESTORE RSTATUS                              
*                                                                               
         ICM   RE,15,SPOTS         TEST NO SPOTS                                
         BZ    EXIT                YES - IGNORE                                 
         L     R0,SPTORD                                                        
         AR    R0,RE                                                            
         ST    R0,SPTORD                                                        
         L     R0,DOLORD                                                        
         A     R0,GROSS                                                         
         ST    R0,DOLORD                                                        
         L     R0,DOLORDN                                                       
         A     R0,NET                                                           
         ST    R0,DOLORDN                                                       
*                                                                               
         OC    4(2,R6),4(R6)       TEST SPOT PAID                               
         BZ    NVBUY6A                                                          
         L     R0,SPTPAID                                                       
         A     R0,SPOTS                                                         
         ST    R0,SPTPAID                                                       
         L     R0,DOLPAID                                                       
         A     R0,GROSS                                                         
         ST    R0,DOLPAID                                                       
         L     R0,DOLPAIDN                                                      
         A     R0,NET                                                           
         ST    R0,DOLPAIDN                                                      
         B     NVBUY6B                                                          
*                                                                               
NVBUY6A  DS    0H                                                               
         L     R0,SPTUNPD                                                       
         A     R0,SPOTS                                                         
         ST    R0,SPTUNPD                                                       
         L     R0,DOLUNPD                                                       
         A     R0,GROSS                                                         
         ST    R0,DOLUNPD                                                       
         L     R0,DOLUNPDN                                                      
         A     R0,NET                                                           
         ST    R0,DOLUNPDN                                                      
*                                                                               
NVBUY6B  TM    6(R6),X'C0'         TEST MINUS OR MINUSED                        
         BNZ   NVBUY6C                                                          
         L     R0,SPTUNMAT                                                      
         A     R0,SPOTS                                                         
         ST    R0,SPTUNMAT         CONSIDER ALL SPOTS UNMATCHED                 
*                                                                               
NVBUY6C  CLC   SPTACTDT,4(R6)                                                   
         BH    *+10                                                             
         MVC   SPTACTDT,4(R6)                                                   
*                                                                               
NVBUY8   LLC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    NVBUY20                                                          
         CLI   0(R6),6                                                          
         BL    NVBUY8                                                           
         CLI   0(R6),13                                                         
         BH    NVBUY10                                                          
         B     NVBUY20                                                          
*                                                                               
NVBUY10  CLI   0(R6),X'10'         TEST AFFID                                   
         BNE   NVBUY8                                                           
* AFFID PRESENT - ADD TO MATCHED, SUB FROM UNMATCHED                            
         L     R0,SPTMATCH                                                      
         AHI   R0,1                                                             
         ST    R0,SPTMATCH                                                      
         L     R0,SPTUNMAT                                                      
         BCTR  R0,0                                                             
         ST    R0,SPTUNMAT                                                      
         B     NVBUY8                                                           
         SPACE 1                                                                
* FIND CORRECT BUCKETS *                                                        
         SPACE 1                                                                
*                                                                               
NVBUY20  LA    R4,NVDATES                                                       
*                                                                               
NVBUY24  CLC   SPTDATE,2(R4)       COMPARE TO MONTH END DATE                    
         BNH   NVBUY26             IF LOW OR EQUAL, HAVE MONTH                  
         LA    R4,6(R4)                                                         
         B     NVBUY24                                                          
*                                                                               
NVBUY26  MVC   BUYYEAR(2),4(R4)    MOVE YEAR/MONTH                              
*                                                                               
         L     R5,ATOTS            POINT TO TOTAL AREA                          
         USING TOTD,R5                                                          
*                                                                               
NVBUY28  CLC   TOTPRD(4),BUYPRD1   MATCH PRD1/PRD2/YEAR/MONTH                   
         BE    NVBUY30             YES                                          
         CLI   TOTPRD,0            TEST END OF BUFFER                           
         BE    NVBUY30                                                          
         LA    R5,L'TOTDATA(R5)    NEXT ENTRY                                   
         B     NVBUY28                                                          
*                                                                               
NVBUY30  MVC   TOTPRD(4),BUYPRD1   SET PRD1/PRD2/YEAR/MONTH IN BUFFER           
*                                                                               
         OC    SPTUNMAT,SPTUNMAT   TEST ANY UNMATCHED SPOTS                     
         BNZ   NVBUY31             YES - OK                                     
         OC    SPTUNPD,SPTUNPD     TEST ANY UNPAID SPOTS                        
         BZ    NVBUY32             NO - SKIP                                    
*                                                                               
NVBUY31  L     R0,TOTSPOTS                                                      
         A     R0,SPTUNPD          *** USE UNPAID SPOTS                         
         ST    R0,TOTSPOTS                                                      
*                                                                               
         LM    R0,R1,TOTGROSS                                                   
         A     R0,DOLUNPD          *** USE UNPAID DOLLARS                       
         A     R1,DOLUNPDN                                                      
         STM   R0,R1,TOTGROSS                                                   
*                                                                               
NVBUY32  OC    SPTPAID,SPTPAID     TEST ANY PAID SPOTS                          
         BZ    *+8                                                              
         OI    TOTCLEAR,1          SET FLAG                                     
*                                                                               
         OC    SPTUNPD,SPTUNPD                                                  
         BZ    *+8                                                              
         OI    TOTUNCLR,1                                                       
*                                                                               
         OC    SPTMATCH,SPTMATCH                                                
         BZ    *+8                                                              
         OI    TOTMATCH,1                                                       
*                                                                               
         OC    SPTUNMAT,SPTUNMAT                                                
         BZ    *+8                                                              
         OI    TOTUNMAT,1                                                       
*                                                                               
         OC    TOTMATCH(4),TOTMATCH  TEST ANY DATA IN ENTRY                     
         BNZ   NVBUY50               YES                                        
         XC    TOTDATA,TOTDATA       ELSE CLEAR ENTRY                           
         B     NVBUY52                                                          
         SPACE 1                                                                
NVBUY50  CLC   TOTACT,SPTACTDT     TEST FOR HIGH ACTIVITY DATE                  
         BH    *+10                                                             
         MVC   TOTACT(2),SPTACTDT  MOVE DATE                                    
*                                                                               
NVBUY52  ICM   R3,15,AIDELEM       HAVE A(BUY ID)?                              
         BZ    NVBUY54             NO                                           
*                                                                               
         LA    R4,L'IDCONNO-1(R3)  LAST BYTE OF BUY ID                          
         CLI   0(R4),X'40'         HIGHER THEN SPACES?                          
         BH    *+10                YES - LAST CHAR OF BUY ID                    
         BCTR  R4,0                MOVE ONE TO THE LEFT                         
         B     *-10                CHECK IF THIS BYTE IS LAST CHAR              
         SR    R4,R3               R4 = BUY ID LENGTH-1                         
*                                                                               
         L     R1,ADREP                                                         
         USING TSARBID,R1                                                       
         MVI   TBIDTYP,TBIDTYPQ    RECORD TYPE X'01'                            
         MVC   TBIDCLT,KEY+1       CLIENT CODE                                  
         MVC   TBIDPRD(4),TOTPRD   PRD/PIGGY/YEAR/MONTH                         
         CLI   LUMPPOL,C'Y'        LUMP ALL PRODUCTS TO POL?                    
         BNE   *+12                NO                                           
         MVI   TBIDPRD,X'FF'       YES - PRD POL                                
         MVI   TBIDPIG,0           AND NO PIGGY                                 
         MVC   TBIDEST,KEY+9       ESTIMATE                                     
         CLC   =C'NO',QEST         ESTIMATE=NO?                                 
         BNE   *+8                 NO                                           
         MVI   TBIDEST,0           YES - BUILD KEY WITH EST=0                   
         MVC   TBIDSTA,KEY+6       STATION                                      
         MVI   BYTE,TSARDH         TSARHIGH ACTION                              
         BAS   RE,TSAR             ALREADY HAVE CLT/PRD/PRD2/YEAR/MON?          
         BE    NVBUY53             YES - UPDATE THE RECORD                      
*                                                                               
         MVC   TBIDKEY,TSARSVKY    RESTORE THE TSAR KEY                         
         LA    R7,TBIDBID          PUT BUY ID HERE                              
         BAS   RE,SETBYID          SET THE BUY ID IN THE RECORD                 
         AHI   R4,TSARKEYL+3       ADD TSAR KEY LEN + REC LEN + EOR 0           
         STCM  R4,3,TBIDLEN        RECORD LENGTH                                
         MVI   BYTE,TSAADD         TSARADD ACTION                               
         BAS   RE,TSAR             ADD THE BUY ID TO THE TABLE                  
         B     NVBUY54                                                          
*                                                                               
NVBUY53  LA    R7,TBIDBID          FIRST BUY ID ELEMENT                         
         XR    RE,RE               CLEAR RE                                     
NVBUY53A ICM   RE,1,0(R7)          END OF RECORD?                               
         BZ    NVBUY53C            YES                                          
         SHI   RE,2                MINUS ELEM LENGTH +1 FOR EX                  
         CR    RE,R4               ARE BUY ID'S THE SAME LENGTH?                
         BNE   NVBUY53B            NO - DON'T COMPARE THIS ELEMENT!             
         EX    RE,*+8              ** EXECUTED **                               
         B     *+10                BRANCH SO IDF DOESN'T COMPLAIN               
         CLC   1(0,R7),0(R3)       BUY ID'S MATCH?                              
         BE    NVBUY54             YES - DON'T ADD DUPLICATE BUY-ID!            
NVBUY53B LA    R7,2(RE,R7)         BUMP TO NEXT ELEMENT                         
         B     NVBUY53A                                                         
*                                                                               
NVBUY53C XR    R0,R0               CLEAR R0                                     
         ICM   R0,3,TBIDLEN        A(RECORD+KEY LENGTH)                         
         LR    R7,R1               R7 = A(TSAR BUY ID RECORD)                   
         AR    R7,R0               R7 = END OF RECORD                           
         BCTR  R7,0                PUT BUY ID HERE                              
         BAS   RE,SETBYID          SET THE BUY ID IN THE RECORD                 
         AR    R0,R4               NEW RECORD LENGTH                            
         STCM  R0,3,TBIDLEN        NEW RECORD LENGTH                            
         MVI   BYTE,TSAWRT         TSARWRITE ACTION                             
         BAS   RE,TSAR             WRITE THE RECORD BACK                        
         DROP  R1                                                               
*                                                                               
NVBUY54  BAS   RE,NEXTEL2                                                       
         BNE   EXIT                                                             
         B     NVBUY3                                                           
         SPACE 2                                                                
NEXTEL   SR    R0,R0                                                            
         ICM   R0,1,1(R6)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R6,R0                                                            
NEXTEL2  CLI   0(R6),0                                                          
         BE    NEXTELX                                                          
         CLC   0(1,R6),ELCDLO                                                   
         BL    NEXTEL                                                           
         CLC   0(1,R6),ELCDHI                                                   
         BH    NEXTEL                                                           
         CR    RE,RE                                                            
         BR    RE                  EXIT WITH CC EQUAL                           
NEXTELX  LTR   RE,RE                                                            
         BR    RE                  EXIT WITH CC NOT EQUAL                       
*                                                                               
SETBYID  DS    0H                                                               
         XC    0(L'IDCONNO+2,R7),0(R7)                                          
         EX    R4,*+8              R4 = BUY ID LENGTH-1                         
         B     *+10                                                             
         MVC   1(0,R7),0(R3)       MOVE NEXT BUY ID INTO RECORD                 
         AHI   R4,2                ELEMENT LENGTH                               
         STC   R4,0(R7)            SAVE ELEMENT LENGTH                          
         BR    RE                                                               
*                                                                               
TSAR     NTR1                                                                   
*                                                                               
         USING TSARBID,R1                                                       
         LA    R2,TSAREA           TSAR AREA                                    
         USING TSARD,R2                                                         
         MVC   TSARSVKY,TBIDTYP    SAVE OFF THE KEY                             
         ST    R1,TSAREC           A(RECORD)                                    
         MVC   TSOFFACT,BYTE       HIGH/ADD/WRITE                               
         L     RF,ADCONLST         GET A(TSAROFF)                               
         L     RF,TSAROFF-SPADCONS(RF)                                          
         GOTO1 (RF),(R2)                                                        
         CLI   TSOFFACT,TSARDH     WANTED READHIGH CALL?                        
         BE    TSAR10              YES                                          
         CLI   TSERRS,0            ANY ERRORS?                                  
         BE    EXIT                NO                                           
         DC    H'0'                YES - DEATH                                  
*                                                                               
TSAR10   TM    TSERRS,TSEEOF       DID WE HIT EOF?                              
         BZ    *+10                NO                                           
         LTR   RE,RE               YES - EXIT WITH CC NEQ                       
         B     EXIT                                                             
         L     R1,TSAREC           A(RECORD)                                    
         CLC   TSARSVKY,TBIDTYP    COMPARE KEYS                                 
         B     EXIT                AND EXIT WITH CC SET                         
         DROP  R1,R2                                                            
*                                                                               
* SUBROUTINE TO GENERATE SORT RECORDS AT END OF ESTIMATE *                      
*                                                                               
NVGEN    NTR1                                                                   
         XC    NVREC,NVREC                                                      
         LA    R3,NVSPECS          POINT TO SPEC LIST                           
*                                                                               
NVGEN2   L     R2,0(R3)            POINT TO DATA SPEC                           
         USING SPECD,R2                                                         
         CLI   SPECDATA,128        TEST ROW DATA                                
         BH    NVGEN4              NO                                           
         CLI   SPECDATA,9          TEST PRIMARY ROW DATA                        
         BH    NVGEN4              NO                                           
         LLC   RF,SPECDATA                                                      
         N     RF,=X'0000000F'                                                  
         BCTR  RF,0                                                             
         SLL   RF,2                X  4                                         
*                                                                               
         MVI   BYTE,4              SET DEFAULT MOVE LENGTH                      
         XC    WORK,WORK           CLEAR MOVE AREA                              
         B     EXTTAB(RF)          CALL EXTRACT ROUTINE                         
*                                                                               
NVGEN4   LA    R3,4(R3)            NEXT SPEC                                    
         CLI   0(R3),X'FF'                                                      
         BNE   NVGEN2                                                           
         B     NVGEN10                                                          
         SPACE 1                                                                
* JUMP TABLE TO DATA EXTRACT ROUTINES *                                         
         SPACE 1                                                                
EXTTAB   B     EXTOFF                                                           
         B     EXTCLT                                                           
         B     EXTMGR                                                           
         B     EXTMKT                                                           
         B     EXTSTA                                                           
         B     NVGEN4  ** PERIOD                                                
         B     EXTPGR                                                           
         B     NVGEN4  ** PRODUCT                                               
         B     EXTEST                                                           
         B     NVGEN4  ** ACTIVITY DATE                                         
         EJECT                                                                  
EXTOFF   L     R6,ADCLT                                                         
         USING CLTHDRD,R6                                                       
**NOP    MVC   WORK(1),COFFICE                                                  
         XC    OFCBLK,OFCBLK                                                    
         MVI   OFCSYS,C'S'                                                      
         MVC   OFCAGY,AGENCY                                                    
         MVC   OFCOFC,COFFICE                                                   
         L     RF,ADCONLST                                                      
         L     RF,VOFFICER-SPADCONS(RF)                                         
         GOTO1 (RF),DMCB,(C'2',OFFICED),(0,ACOMFACS)                            
         MVC   WORK(2),OFCOFC2                                                  
         B     EXTX                                                             
*                                                                               
EXTCLT   L     R6,ADCLT                                                         
         MVC   WORK(2),2(R6)                                                    
         B     EXTX                                                             
*                                                                               
EXTMGR   CLI   QMGR,C' '                                                        
         BE    NVGEN4                                                           
         SR    RF,RF                                                            
         ICM   RF,3,BUYKSAVE+4                                                  
         AR    RF,RF               X 2                                          
         A     RF,SVMKTADR         ADD A(MKTGRP ASSGN TABLE)                    
         OC    0(2,RF),0(RF)                                                    
         BNZ   *+8                                                              
         LA    RF,=X'9999'         POINT TO UNDEFINED MKTGRP                    
         MVC   WORK(2),0(RF)                                                    
         OC    WORK(2),MGR1MASK                                                 
*                                                                               
         CLC   MGR1LEN,MGR2LEN     TEST 2 LEVELS                                
         BE    EXTX                NO                                           
*                                                                               
         MVI   BYTE,8              SET TO MOVE 8 BYTES                          
         MVC   WORK+4(2),0(RF)                                                  
         OC    WORK+4(2),MGR2MASK                                               
*                                                                               
         CLC   MGR2LEN,MGR3LEN     TEST 3 LEVELS                                
         BE    EXTX                NO                                           
*                                                                               
         MVI   BYTE,12             SET TO MOVE 12 BYTES                         
         MVC   WORK+8(2),0(RF)                                                  
         B     EXTX                                                             
*                                                                               
EXTMKT   MVC   WORK(2),BUYKSAVE+4                                               
         B     EXTX                                                             
*                                                                               
EXTSTA   MVC   WORK(3),BUYKSAVE+6  PACKED STATION                               
         CLI   QCBLSYS,C'Y'        TEST CABLE SYSTEM OPTION                     
         BNE   EXTX                                                             
         GOTO1 MSUNPK,DMCB,(X'80',BUYKSAVE+4),WORK+4,WORK+8  YES--              
         CLI   WORK+8,C'0'         TEST CABLE STATION                           
         BL    EXTX                                                             
         MVC   WORK+13(3),SPACES   YES-REMOVE THE CABLE NETWORK                 
         GOTO1 MSPACK,DMCB,WORK+4,WORK+8,DUB                                    
         MVC   WORK(3),DUB+2                                                    
         B     EXTX                                                             
*                                                                               
EXTPGR   CLI   QPGR,C' '                                                        
         BE    NVGEN4                                                           
         LLC   RF,QBPRD                                                         
         AR    RF,RF               X 2                                          
         A     RF,APGRTAB          ADD A(PRDGRP ASSGN TABLE)                    
         MVC   WORK(2),0(RF)                                                    
         OC    WORK(2),PGR1MASK                                                 
*                                                                               
         CLC   PGR1LEN,PGR2LEN     TEST 2 LEVELS                                
         BE    EXTX                NO                                           
*                                                                               
         MVI   BYTE,8              SET TO MOVE 8 BYTES                          
         MVC   WORK+4(2),0(RF)                                                  
         BE    EXTX                                                             
*                                                                               
EXTEST   CLC   =C'NO',QEST         TEST SUPPRESS EST                            
         BE    NVGEN4                                                           
         MVC   WORK(1),BUYKSAVE+9                                               
         B     EXTX                                                             
*                                                                               
EXTX     LLC   RF,0(R3)            GET DATA DSPL                                
         LA    RF,NVREC(RF)        POINT TO COL                                 
         LLC   RE,BYTE             GET LENGTH TO MOVE                           
         BCTR  RE,0                                                             
         EX    RE,EXTMVC                                                        
         B     NVGEN4                                                           
*                                                                               
EXTMVC   MVC   0(0,RF),WORK   *EXECUTED*                                        
         EJECT                                                                  
* NOW GENERATE DATA FROM BUFFER FOR EACH PRD/PER *                              
         SPACE 1                                                                
NVGEN10  L     R5,ATOTS                                                         
         USING TOTD,R5                                                          
*                                                                               
NVGEN12  CLI   TOTPRD,0            ANY DATA                                     
         BE    EXIT                                                             
         OC    TOTSPOTS,TOTSPOTS                                                
         BNZ   *+14                                                             
         OC    TOTGROSS,TOTGROSS                                                
         BZ    NVGEN30                                                          
*                                                                               
         LA    RE,NVREC+NVKEYLEN                                                
         XC    0(NVRECLEN-NVKEYLEN,RE),0(RE)  CLEAR COLUMNS                     
         SPACE 1                                                                
* TRANSLATE PRD CODES TO DISPLACEMENTS IN CLTHDR PRD LIST *                     
         SPACE 1                                                                
         LA    R6,NVREC                                                         
         AH    R6,NVDSPPRD         ADD DSPL TO PRD                              
*                                                                               
         LA    R1,TOTPRD           POINT TO PRD 1                               
         CLI   LUMPPOL,C'Y'        TEST LUMP POL TOGETHER                       
         BNE   *+8                                                              
         LA    R1,=X'FF'                                                        
         BAS   RE,NVGETPRD                                                      
         SLL   RF,16               LEFT ALIGN                                   
         ST    RF,0(R6)            SET DSPL TO PRD CODE IN CLTHDR               
         OC    TCLTCODE,TCLTCODE   TEST SPECIAL TRAFFIC CLIENT                  
         BZ    *+14                                                             
         MVC   2(2,R6),TCLTCODE    YES-SAVE IT AFTER PRODUCT                    
         B     NVGEN14                                                          
*                                                                               
         CLI   LUMPPOL,C'Y'        TEST LUMP POL TOGETHER                       
         BE    NVGEN14                                                          
         LA    R1,TOTPRD2          POINT TO PRD 2                               
         CLI   0(R1),0                                                          
         BE    NVGEN14                                                          
         BAS   RE,NVGETPRD                                                      
         STH   RF,2(R6)                                                         
*                                                                               
NVGEN14  LA    R6,NVREC                                                         
         AH    R6,NVDSPPER         ADD DSPL TO PERIOD                           
         MVC   0(2,R6),TOTPER                                                   
*                                                                               
         LA    RE,NVREC                                                         
         SR    R0,R0                                                            
         ICM   R0,3,NVDSPSPT       ADD DSPL TO SPOTS                            
         BZ    *+12                                                             
         AR    RE,R0                                                            
         MVC   0(4,RE),TOTSPOTS    MOVE SPOTS                                   
*                                                                               
         LA    RE,NVREC                                                         
         SR    R0,R0                                                            
         ICM   R0,3,NVDSPGRS       ADD DSPL TO GROSS                            
         BZ    *+12                                                             
         AR    RE,R0                                                            
         MVC   0(4,RE),TOTGROSS    MOVE GROSS                                   
*                                                                               
         LA    RE,NVREC                                                         
         SR    R0,R0                                                            
         ICM   R0,3,NVDSPNET       ADD DSPL TO GROSS                            
         BZ    *+12                                                             
         AR    RE,R0                                                            
         MVC   0(4,RE),TOTNET      MOVE NET                                     
*                                                                               
         SR    R0,R0                                                            
         CLI   TOTMATCH,0                                                       
         BE    *+8                                                              
         O     R0,=X'00010000'     MATCHED SPOT IND                             
         CLI   TOTUNMAT,0                                                       
         BE    *+8                                                              
         O     R0,=X'00000001'     UNMATCHED SPOT IND                           
* CALCULATE DATA ADDRESS                                                        
         SR    RE,RE                                                            
         ICM   RE,3,NVDSPMAT                                                    
         BZ    *+12                                                             
         LA    RE,NVREC(RE)                                                     
         ST    R0,0(RE)            SET MATCHED STATUS                           
*                                                                               
         SR    R0,R0                                                            
         CLI   TOTCLEAR,0                                                       
         BE    *+8                                                              
         O     R0,=X'00010000'     CLEARED SPOT IND                             
         CLI   TOTUNCLR,0                                                       
         BE    *+8                                                              
         O     R0,=X'00000001'     UNCLEARED SPOT IND                           
* CALCULATE DATA ADDRESS                                                        
         SR    RE,RE                                                            
         ICM   RE,3,NVDSPCLR                                                    
         BZ    *+12                                                             
         LA    RE,NVREC(RE)                                                     
         ST    R0,0(RE)            SET CLEARED STATUS                           
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,3,NVDSPACT                                                    
         BZ    NVGEN16                                                          
         GOTO1 DATCON,DMCB,(2,TOTACT),(3,FULL)                                  
         LA    RE,NVREC                                                         
         AR    RE,R0                                                            
         MVC   0(3,RE),FULL                                                     
*                                                                               
NVGEN16  GOTO1 =V(SORTER),DMCB,=C'PUT',NVREC                                    
*                                                                               
         CLI   QOPT5,C'T'                                                       
         BE    *+12                                                             
         CLI   QOPT5,C'B'                                                       
         BNE   NVGEN30                                                          
*                                                                               
         GOTO1 PRNTBL,DMCB,=C'PUT',NVREC,C'DUMP',NVRECLEN,=C'1D00'              
*                                                                               
NVGEN30  XC    TOTDATA,TOTDATA     CLEAR THE ENTRY                              
         LA    R5,L'TOTDATA(R5)    NEXT ENTRY                                   
         B     NVGEN12                                                          
         SPACE 2                                                                
* SUBR TO TRANSLATE NUMERIC PRD CODE TO DSPL IN CLTHDR CLIST                    
* DSPL IS RETURNED AS HALFWORD IN RF. R1 POINTS TO 1 BYTE PRD CODE              
         SPACE 1                                                                
NVGETPRD L     RF,ADCLT                                                         
         OC    TCLTCODE,TCLTCODE   TEST SPECIAL TRAFFIC CLIENT                  
         BZ    *+8                                                              
         L     RF,ATCLTREC         YES-ADDRESS THAT CLIENT RECORD               
         LA    RF,CLIST-CLTHDRD(RF)                                             
         LR    R0,RF               SAVE CLIST ADDRESS                           
*                                                                               
NVGETPR2 CLC   0(1,R1),3(RF)                                                    
         BE    NVGETPR4                                                         
         LA    RF,4(RF)                                                         
         CLI   0(RF),C' '                                                       
         BH    NVGETPR2                                                         
         DC    H'0'                                                             
*                                                                               
NVGETPR4 SR    RF,R0               GET DISPLACEMENT                             
         LA    RF,1(RF)            ADD 1 SO DSPL NEVER 0                        
         BR    RE                                                               
         EJECT                                                                  
* CLEAR MGRTAB *                                                                
         SPACE 1                                                                
BLDGRP   NTR1                                                                   
         LA    R0,20000/250                                                     
         L     R1,AMGRTAB          GET MGRTAB ADDRESS                           
         ST    R1,SVMKTADR                                                      
*                                                                               
         XC    0(250,R1),0(R1)                                                  
         LA    R1,250(R1)                                                       
         BCT   R0,*-10                                                          
*                                                                               
         CLI   QMGR,C' '           TEST MKTGRP REQUEST                          
         BE    BLDPGR                                                           
         SPACE 1                                                                
* READ MGRDEF RECORD TO TEST EXCEPTION LIST *                                   
         SPACE 1                                                                
         XC    KEY,KEY                                                          
         MVC   KEY(2),=X'0D02'                                                  
         MVC   KEY+2(1),BAGYMD                                                  
         MVC   KEY+8(1),QMGR                                                    
         CLI   QMGR,C' '           ID'S A-F REQUIRE CLIENT                      
         BL    BLDMGR1                                                          
         CLI   QMGR,C'F'           ID'S A-F REQUIRE CLIENT                      
         BH    BLDMGR1                                                          
         MVC   KEY+3(2),BCLT                                                    
*                                                                               
BLDMGR1  GOTO1 HIGH                                                             
         CLC   KEY(9),KEYSAVE                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,ADBUY                                                       
         GOTO1 GET                                                              
*                                                                               
         MVC   KEY(2),=X'0D82'     SET FOR MKTGRP PASSIVES                      
         SPACE 1                                                                
* FOR ID G+ CHECK FOR OFFC AND/OR CLT EXCEPTION IF ONE CLT REQ                  
         SPACE 1                                                                
         CLC   =C'ALL',QCLT                                                     
         BE    BLDMGR2                                                          
*                                                                               
         MVI   HALF,C'*'           BUILD OFFICE OVERRIDE                        
         L     RE,ADCLT                                                         
         MVC   HALF+1(1),COFFICE-CLTHDR(RE)                                     
*                                                                               
BLDMGR1A MVI   ELCDLO,2                                                         
         MVI   ELCDHI,2                                                         
         L     R6,ADBUY                                                         
         LA    R6,24(R6)                                                        
*                                                                               
BLDMGR1B BAS   RE,NEXTEL                                                        
         BNE   BLDMGR2                                                          
         CLC   QCLT,2(R6)          MATCH UNPACKED CLIENT CODE                   
         BE    BLDMGR1C                                                         
         CLC   HALF,2(R6)          MATCH OFFICE CODE                            
         BNE   BLDMGR1B                                                         
         MVC   KEY+3(2),HALF       AND USE IT UNLESS CLT MATCH                  
         B     BLDMGR1B                                                         
*                                                                               
BLDMGR1C MVC   KEY+3(2),BCLT       MOVE PACKED CLT CODE TO KEY                  
*                                                                               
BLDMGR2  MVC   SVMGRKEY(13),KEY    SAVE KEY FOR OUTPUT ROUTINES                 
         GOTO1 HIGH                                                             
         B     BLDMGR6                                                          
*                                                                               
BLDMGR4  GOTO1 SEQ                                                              
*                                                                               
BLDMGR6  CLC   KEY(9),KEYSAVE                                                   
         BNE   BLDMGR10                                                         
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,3,KEY+11         GET MKT NUM                                  
         AR    RE,RE               X 2                                          
         A     RE,SVMKTADR         + TABLE START                                
         MVC   0(2,RE),KEY+9       MOVE MKTGRP NUMBER                           
         B     BLDMGR4                                                          
         SPACE 1                                                                
* SET MASKS FOR MKTGRPS *                                                       
         SPACE 1                                                                
BLDMGR10 XC    MGR2MASK,MGR2MASK                                                
         CLC   MGR3LEN,MGR2LEN     TEST 3 LEVELS OF MKTGRP                      
         BE    BLDMGR12            NO                                           
         LLC   R0,MGR2LEN                                                       
         BAS   RE,SETMASK                                                       
         STH   R0,MGR2MASK                                                      
*                                                                               
BLDMGR12 XC    MGR1MASK,MGR1MASK                                                
         CLC   MGR2LEN,MGR1LEN     TEST 2 LEVELS OF MKTGRP                      
         BE    BLDPGR                                                           
         LLC   R0,MGR1LEN                                                       
         BAS   RE,SETMASK                                                       
         STH   R0,MGR1MASK                                                      
         B     BLDPGR                                                           
         SPACE 1                                                                
* BUILD PRODUCT GROUP TABLE *                                                   
         SPACE 1                                                                
BLDPGR   L     R1,APGRTAB          CLEAR PRODUCT GROUP ASSIGN TABLE             
         MVI   0(R1),X'99'                                                      
         MVC   1(255,R1),0(R1)                                                  
         MVC   256(256,R1),255(R1)                                              
         CLI   QPGR,C' '           TEST FOR PRODUCT GROUPS                      
         BNH   EXIT                NO - EXIT                                    
*                                                                               
         XC    KEY,KEY             BUILD PRODUCT GROUP ASSIGN TABLE             
         MVC   KEY(2),=X'0D81'                                                  
         MVC   KEY+2(3),BAGYMD                                                  
         MVC   KEY+5(1),QPGR                                                    
         GOTO1 HIGH                                                             
         B     BLDPGR4                                                          
*                                                                               
BLDPGR2  GOTO1 SEQ                                                              
*                                                                               
BLDPGR4  CLC   KEY(6),KEYSAVE                                                   
         BNE   BLDPGR10                                                         
         L     RF,ADCLT            FIND PRODUCT CODE FROM CLTHDR                
         LA    RF,CLIST-CLTHDRD(RF)                                             
*                                                                               
BLDPGR6  CLC   0(3,RF),KEY+8                                                    
         BE    BLDPGR8                                                          
         LA    RF,4(RF)                                                         
         CLI   0(RF),C' '                                                       
         BH    BLDPGR6                                                          
         DC    H'0'                                                             
*                                                                               
BLDPGR8  LLC   RE,3(RF)            PRODUCT CODE                                 
         AR    RE,RE               X 2                                          
         A     RE,APGRTAB          + TABLE START                                
         MVC   0(2,RE),KEY+6       MOVE PRDGRP NUMBER                           
         B     BLDPGR2                                                          
*                                                                               
BLDPGR10 XC    PGR1MASK,PGR1MASK   SET MASK FOR PRODUCT GROUPS                  
         CLC   PGR2LEN,PGR1LEN     TEST 2 LEVELS OF PRDGRP                      
         BE    EXIT                NO                                           
         LLC   R0,PGR1LEN                                                       
         BAS   RE,SETMASK                                                       
         STH   R0,PGR1MASK                                                      
         B     EXIT                                                             
         SPACE 2                                                                
SETMASK  LTR   R0,R0                                                            
         BP    *+6                                                              
         DC    H'0'                                                             
         LA    RF,GRPMASKS-2                                                    
         LA    RF,2(RF)                                                         
         BCT   R0,*-4                                                           
         LH    R0,0(RF)                                                         
         BR    RE                                                               
*                                                                               
GRPMASKS DC    X'0FFF'             FOR BREAK LEN = 3                            
         DC    X'00FF'                             2                            
         DC    X'000F'                             1                            
GRPTABSW DC    X'00'                                                            
         EJECT                                                                  
* BUILD ESTIMATE TABLE FOR ESTIMATE FILTERING *                                 
         SPACE 1                                                                
BLDESTAB NTR1                                                                   
         L     R1,AESTTAB          CLEAR ESTIMATE TABLE                         
         LA    R0,256                                                           
*                                                                               
ET05     XC    0(256,R1),0(R1)     CLEAR 256 BYTES FOR EACH BRAND               
         LA    R1,256(R1)                                                       
         BCT   R0,ET05                                                          
*                                                                               
         L     R6,ADCLT                                                         
         XC    KEY,KEY                                                          
         MVC   KEY(4),0(R6)                                                     
         MVI   KEY+4,C'A'          FORCE PAST CLTHDR                            
         LA    R2,3                                                             
         CLI   QBPRD,0             TEST FOR PRODUCT FILTER                      
         BE    *+14                                                             
         LA    R2,6                                                             
         MVC   KEY+4(3),QPRD                                                    
*                                                                               
ET10     GOTO1 HIGH                PRODUCT HEADER                               
         B     ET30                                                             
*                                                                               
ET20     GOTO1 SEQ                                                              
*                                                                               
ET30     EX    R2,KEYCOMP                                                       
         BNE   ETX                                                              
         OC    KEY+8(5),KEY+8      TEST BILL                                    
         BNZ   ET20                YES                                          
         CLI   KEY+7,0             TEST EST                                     
         BE    ET20                NO                                           
         GOTO1 GETEST              GET EST HEADER                               
         L     R6,ADEST                                                         
         USING ESTHDRD,R6                                                       
*                                                                               
*        CLC   EEND(4),QSTART      EST END MNTH BEFORE REQ START MNTH           
*        BL    ET70                                                             
*        CLC   ESTART(4),QEND      EST START MNTH AFTER REQ END MNTH            
*        BH    ET70                                                             
*                                                                               
         CLI   QCONTREQ,C'*'       HAVE ETYPE FILTER? (2ND REQ CARD)            
         BNE   ET35                NO                                           
         CLI   Q2USER,C' '         IS THERE AN ETYPE FILTER?                    
         BNH   ET35                 NO                                          
         CLI   Q2USER,C'*'         FILTER ON ETYPE?                             
         BE    ET36                NO                                           
*                                                                               
         CLC   ETYPE,Q2USER        MATCH ON ETYPE?                              
         BNE   ET20                NO...SKIP                                    
         B     ET36                                                             
*                                                                               
ET35     CLI   ETYPE,C'B'          ETYPE = BARTER?                              
         BE    ET20                YES...SKIP                                   
         CLI   ETYPE,C'S'          ETYPE = STEWARDSHIP?                         
         BE    ET20                YES...SKIP                                   
*                                                                               
ET36     LA    R0,3                                                             
         LA    RE,QESTEND                                                       
         LA    RF,EPROF                                                         
*                                                                               
         CLI   ESTFILT,C'Y'        FILTER OUT ONLY ETYPE=B/S IF THERE           
         BNE   ET61                ARE NO OTHER ESTIMATE FILTERS                
*                                                                               
ET40     CLI   0(RE),C'*'                                                       
         BE    ET60                                                             
         CLI   0(RE),C' '                                                       
         BE    ET60                                                             
         TM    0(RE),X'40'         TEST NEGATIVE FILTER                         
         BZ    ET50                YES                                          
         CLC   0(1,RE),0(RF)       POSITIVE FILTER MUST MATCH                   
         BNE   ET70                                                             
         B     ET60                                                             
*                                                                               
ET50     MVC   BYTE,0(RE)                                                       
         OI    BYTE,C' '           MAKE CHAR UPPER CASE FOR COMPARE             
         CLC   BYTE,0(RF)          NEGATIVE FILTER MUST NOT MATCH               
         BE    ET70                                                             
*                                                                               
ET60     LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,ET40                                                          
*                                                                               
ET61     LLC   RE,EPRDCD+1         GET PRD NUM                                  
         BCTR  RE,0                                                             
         SLL   RE,8                X 256                                        
         LLC   R0,7(R6)            EST NUM                                      
         BCTR  R0,0                                                             
         AR    RE,R0                                                            
         A     RE,AESTTAB                                                       
         MVI   0(RE),C'Y'                                                       
*                                                                               
ET70     MVC   KEY+8(5),XFF        NEXT ESTIMATE                                
         B     ET10                                                             
*                                                                               
ETX      B     EXIT                                                             
         SPACE 1                                                                
KEYCOMP  CLC   KEY(0),KEYSAVE      * EXECUTED                                   
         EJECT                                                                  
* FIX SPECIAL TRAFFIC PRODUCT CODES                                             
*                                                                               
         SPACE 1                                                                
FIX      LR    R0,RE                                                            
         XC    HALF,HALF                                                        
         L     R2,ADBUY                                                         
         USING BUYRECD,R2                                                       
         LA    R1,BDELEM                                                        
         SR    RF,RF                                                            
*                                                                               
FIX2     CLI   0(R1),0                                                          
         BE    FIXX                                                             
         CLI   0(R1),X'61'         TEST MASTER CLIENT DESCRIPTION               
         BE    *+14                                                             
         IC    RF,1(R1)                                                         
         AR    R1,RF                                                            
         B     FIX2                                                             
         CLI   1(R1),6             YES-TEST ELEMENT HAS PRODUCT CODE            
         BL    FIXX                                                             
         USING MCLTELEM,R1                                                      
         SR    RE,RE                                                            
         ICM   RE,1,MCLTPRD        SAVE THE PRODUCT CODE                        
         BZ    FIXX                                                             
         MVC   HALF,MCLTCODE       SAVE TRAFFIC CLIENT CODE                     
         LA    R1,BDELEM                                                        
*                                                                               
FIX4     CLI   0(R1),0             LOOK FOR ALL POOL BUY ELEMENTS               
         BE    FIX8                                                             
         CLI   0(R1),X'0B'                                                      
         BL    FIX6                                                             
         CLI   0(R1),X'0D'                                                      
         BH    FIX6                                                             
         USING REGELEM,R1                                                       
         CLI   RLEN,10             TEST ALLOCATED                               
         BNH   FIX6                                                             
         STC   RE,RPPRD            YES-REPLACE THE PRODUCT                      
*                                                                               
FIX6     IC    RF,1(R1)                                                         
         AR    R1,RF                                                            
         B     FIX4                                                             
*                                                                               
FIX8     CLC   HALF,TCLTCODE       TEST SPECIAL TRAFFIC CLIENT CHANGE           
         BE    FIXX                                                             
         OC    HALF,HALF           YES-READ CLIENT RECORD                       
         BZ    FIXX                                                             
         MVC   SVKEY,KEY                                                        
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING CLTHDRD,R1                                                       
         MVC   CKEYAM,BAGYMD                                                    
         MVC   CKEYCLT,HALF                                                     
         GOTO1 HIGH                                                             
         CLC   KEY(L'CKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,ATCLTREC                                                    
         GOTO1 GET                                                              
         MVC   KEY,SVKEY                                                        
         GOTO1 HIGH                                                             
*                                                                               
FIXX     MVC   TCLTCODE,HALF                                                    
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*===============================================================*               
* FILTER STATION IN KEY FOR CABLE REQUESTS                      *               
*===============================================================*               
         SPACE 1                                                                
STAFILT  NTR1                                                                   
         CLI   COUNTRY,C'C'        TEST CANADA                                  
         BE    STAF30                                                           
         CLI   KEY+6,X'E8'         TEST CABLE STATION                           
         BL    STAF2               NO                                           
         CLI   QCBLNET,C' '        TEST CABLE NETWORK FILTER                    
         BH    STAF4               YES - ALWAYS APPLY IT                        
*                                                                               
STAF2    CLI   QSTA+4,C'/'         TEST CABLE ONLY                              
         BNE   STAF10                                                           
         CLI   KEY+6,X'E8'         TEST CABLE STATION                           
         BL    STANEQ              NO - NEXT STATION                            
         CLI   QCBLNET,C' '        TEST CABLE NETWORK FILTER                    
         BNH   STAF20              NO                                           
*                                                                               
STAF4    XC    STAWORK,STAWORK                                                  
         LA    R1,STAWORK                                                       
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'K'                                                     
         MVC   STAPAGY,QAGY                                                     
         MVC   STAPCTRY,COUNTRY                                                 
         MVC   STAPMED,QMED                                                     
         MVC   STAPACOM,ACOMFACS                                                
         MVC   STAPMKST,KEY+4      MKTSTA                                       
         MVC   STAPQNET,QCBLNET    CABLE NETWORK                                
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
         CLI   STAPERR,QSTP_YES                                                 
         BE    STAF30                                                           
         CLI   STAPERR,QSTP_NO                                                  
         BE    STANEQ                                                           
         DC    H'0'                                                             
         DROP  R1                                                               
*                                                                               
STAF10   CLI   QSTA+4,C'-'         TEST ALL BUT CABLE                           
         BNE   STAF20                                                           
         CLI   KEY+6,X'E8'         TEST CABLE STATION                           
         BNL   STANEQ              YES - NEXT MARKET                            
         B     STAEQ                                                            
*                                                                               
STAF20   CLC   =C'ALL ',QSTA       TEST ALL STATION REQUEST                     
         BE    STAEQ                YES                                         
         CLI   QMGR,C' '           TEST MGROUPS                                 
         BNE   STAEQ               - YES THEN DON'T MATCH ON STATION            
*                                                                               
* NOT DOING ALL STATIONS - SO MATCH ON STATION                                  
*                                                                               
STAF30   CLI   COUNTRY,C'C'                                                     
         BNE   STAF40              TEST CANADA                                  
         CLI   QMED,C'R'           TEST RADIO                                   
         BE    STAF40                                                           
         CLC   KEY+6(2),QBSTA      COMPARE 2 BYTES FOR T/N/C                    
         BE    STAEQ                                                            
         B     STANEQ                                                           
*                                                                               
STAF40   SR    R1,R1                                                            
         ICM   R1,7,KEY+6                                                       
         SR    RF,RF                                                            
         ICM   RF,7,QBSTA                                                       
         CLI   KEY+6,X'E8'         TEST CABLE                                   
         BL    STAF42              NOT CABLE - DO FULL COMPARE                  
         N     R1,=X'00FFFF80'     DROP NETWORK BITS                            
         N     RF,=X'00FFFF80'     DROP NETWORK BITS                            
STAF42   CR    R1,RF               TEST FOR RIGHT STATION                       
         BNE   STANEQ              READ NEXT MARKET                             
*                                                                               
STAEQ    CR    RB,RB                                                            
         B     *+6                                                              
STANEQ   LTR   RB,RB                                                            
         B     EXIT                                                             
         EJECT                                                                  
AMGRTAB  DC    A(MGRTAB)           RELOCATED AND SAVED IN SVMKTADR              
APGRTAB  DC    A(PGRTAB)                                                        
AESTTAB  DC    A(ESTTAB)                                                        
ATOTS    DC    A(TOTS)                                                          
ATCLTREC DC    A(TCLTREC)                                                       
AESTATAB DC    A(ESTATAB)                                                       
AHDSPEC  DS    A                                                                
ELCDLO   DS    C                                                                
ELCDHI   DS    C                                                                
PGR1MASK DS    H                                                                
MGR1MASK DS    H                                                                
MGR2MASK DS    H                                                                
QPGRLEN  DS    H                                                                
QMGRLEN  DS    H                                                                
QBPRD    DS    X                                                                
QBEST    DS    X                                                                
QBESTEND DS    X                                                                
QBMKTSTA DS    0CL5                                                             
QBMKT    DS    XL2                                                              
QBSTA    DS    XL3                                                              
*QBCBLNET DS    CL3                                                             
*                                                                               
QSTANET  DS    CL8                                                              
*                                                                               
ESTFILT  DS    C                                                                
SPECLST  DS    CL10                                                             
BUYKSAVE DS    XL13                                                             
PGRKEY   DS    XL13                                                             
FIRSTCLI DS    C                                                                
LUMPPOL  DS    C                                                                
SVKEY    DS    XL(L'BUYKEY)                                                     
TCLTCODE DC    XL2'0000'                                                        
XFF      DC    16X'FF'                                                          
         DS    0D                                                               
STAWORK  DS    XL32                                                             
*                                                                               
         DS    0F                                                               
SPTCNTRS DS    0CL48                                                            
SPTDATE  DS    H                                                                
SPTACTDT DS    H                                                                
*                                                                               
SPTORD   DS    F                   ORDERED SPOT COUNTER                         
SPTPAID  DS    F                   PAID SPOT COUNTER                            
SPTUNPD  DS    F                   UNPAID SPOT COUNTER                          
SPTMATCH DS    F                   MATCHED SPOT COUNTER                         
SPTUNMAT DS    F                   UNMATCHED SPOT COUNTER                       
*                                                                               
DOLORD   DS    F                   ORDERED DOLLARS - GROSS                      
DOLORDN  DS    F                   ORDERED DOLLARS - NET                        
DOLPAID  DS    F                   PAID DOLLARS - GROSS                         
DOLPAIDN DS    F                   PAID DOLLARS - NET                           
DOLUNPD  DS    F                   UNPAID DOLLARS - GROSS                       
DOLUNPDN DS    F                   UNPAID DOLLARS - NET                         
*                                                                               
         DS    0F                                                               
BUYPRD1  DS    X                                                                
BUYPRD2  DS    X                                                                
BUYYEAR  DS    X                                                                
BUYMONTH DS    X                                                                
*                                                                               
QEIXSTA  EQU   QDPTDET             Y=INCLUDE,N=EXCLUDE EIX STATIONS             
QCBLSYS  EQU   QDPTMENU            LOCAL CABLE SYSTEM OPTION                    
QOPT6    EQU   QOPT5+1             FORCE CLT=ALL REQUEST                        
QOPT7    EQU   QOPT5+2             CLIENT GROUP EXCEPTION CODE                  
*                                                                               
OFCBLK   DS    XL(OFCLENQ)                                                      
TSARSVKY DS    XL(TSARKEYL)        TSAR'S SAVEKEY BEFORE READHIGH CALL          
AIDELEM  DS    F                   A(BUY ID)                                    
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
* ENTRIES BELOW ARE LIST OF SPEC ADDRESSES FOR DATA SPECS *                     
*                                                                               
HROW1    DC    A(HROWOFF,0,0)                                                   
HROW2    DC    A(HROWCLT,0,0)                                                   
HROW3    DC    A(HROWMGR1,HROWMGR2,HROWMGR3)                                    
HROW4    DC    A(HROWMKT,0,0)                                                   
HROW5    DC    A(HROWSTA,0,0)                                                   
HROW6    DC    A(HROWPER,0,0)                                                   
HROW7    DC    A(HROWPGR1,HROWPGR2,0)                                           
HROW8    DC    A(HROWPRD,0,0)                                                   
HROW9    DC    A(HROWEST,0,0)                                                   
*                                                                               
PROW1    DC    A(PROWOFF,0,0)                                                   
PROW2    DC    A(PROWCLT,0,0)                                                   
PROW3    DC    A(PROWMGR1,PROWMGR2,PROWMGR3)                                    
PROW4    DC    A(PROWMKT,0,0)                                                   
PROW5    DC    A(PROWSTA,0,0)                                                   
PROW6    DC    A(PROWPER,0,0)                                                   
PROW7    DC    A(PROWPGR1,PROWPGR2,0)                                           
PROW8    DC    A(PROWPRD,0,0)                                                   
PROW9    DC    A(PROWEST,0,0)                                                   
PROW10   DC    A(PROWACT,0,0)                                                   
         SPACE 1                                                                
* DATA SPECS FOR HEADLINE ROWS *                                                
         SPACE 1                                                                
HROWOFF  DC    AL1(01,HL,06,02,00),CL6'OFFICE'                                  
HROWCLT  DC    AL1(02,HL,06,03,24),CL6'CLIENT'                                  
HROWMGR1 DC    AL1(03,HL,12,05,24),CL12' '                                      
HROWMGR2 DC    AL1(13,HL,12,05,24),CL12' '                                      
HROWMGR3 DC    AL1(23,HL,12,05,24),CL12' '                                      
HROWMKT  DC    AL1(04,HL,06,04,20),CL6'MARKET'                                  
HROWSTA  DC    AL1(05,HL,08,08,00),CL7'STATION'                                 
HROWPER  DC    AL1(06,HL,06,06,00),CL6'PERIOD'                                  
HROWPGR1 DC    AL1(07,HL,12,04,24),CL12' '                                      
HROWPGR2 DC    AL1(17,HL,12,04,24),CL12' '                                      
HROWPRD  DC    AL1(08,HL,07,07,24),CL7'PRODUCT'                                 
HROWEST  DC    AL1(09,HL,08,03,24),CL8'ESTIMATE'                                
         EJECT                                                                  
* DATA SPECS FOR PRINT LINE ROWS *                                              
         SPACE 1                                                                
PROWOFF  DC    AL1(01,PL+CE,06,02,00),CL6'OFFICE'                               
PROWCLT  DC    AL1(02,PL+LE,06,03,00),CL6'CLIENT'                               
PROWMGR1 DC    AL1(03,PL+LE,12,05,00),CL12' '                                   
PROWMGR2 DC    AL1(13,PL+LE,12,05,00),CL12' '                                   
PROWMGR3 DC    AL1(23,PL+LE,12,05,00),CL12' '                                   
PROWMKT  DC    AL1(04,PL+LE,06,04,20),CL6'MARKET'                               
PROWSTA  DC    AL1(05,PL+LE,08,08,00),CL7'STATION'                              
PROWPER  DC    AL1(06,PL+CE,06,06,00),CL6'PERIOD'                               
PROWPGR1 DC    AL1(07,PL+LE,12,04,00),CL12' '                                   
PROWPGR2 DC    AL1(17,PL+LE,12,04,00),CL12' '                                   
PROWPRD  DC    AL1(08,PL+LE,07,07,00),CL7'PRODUCT'                              
PROWEST  DC    AL1(09,PL+LE,03,03,00),CL3'EST'                                  
PROWACT  DC    AL1(10,PL+CE,08,08,00),CL8'ACT DATE'                             
         SPACE 1                                                                
* DATA SPECS FOR COLUMNS *                                                      
         SPACE 1                                                                
COLSPT   DC    AL1(129,PL+RI,05,00,00),CL5'SPOTS'                               
COLGRS   DC    AL1(130,PL+RI,10,00,00),CL10'GROSS COST'                         
COLNET   DC    AL1(131,PL+RI,10,00,00),CL10'  NET COST'                         
COLMAT   DC    AL1(132,PL+CE,05,00,00),CL5'MATCH'                               
COLCLR   DC    AL1(133,PL+CE,05,00,00),CL5'CLEAR'                               
         EJECT                                                                  
ESTTAB   DS    (256*256)X                                                       
MGRTAB   DS    2500D                                                            
PGRTAB   DS    512X                                                             
TOTS     DS    (220*12*L'TOTDATA)X  220 PRDS * 12 MONS * ACCUM LEN              
TCLTREC  DS    CL(CLTHDRL)         SPECIAL TRAFFIC CLIENT RECORD                
         SPACE 1                                                                
ESTATABL EQU   6000                                                             
ESTATAB  DS    (ESTATABL)CL5       ROOM FOR 2000 EASI STATIONS                  
         SPACE 1                                                                
SPNVWORK DS    250D                                                             
*                                                                               
TOTD     DSECT                                                                  
*                                                                               
TOTDATA  DS    0XL24                                                            
*                                                                               
TOTPRD   DS    XL1                 PRD CODE                                     
TOTPRD2  DS    XL1                 PARTNER PRD CODE                             
TOTPER   DS    XL2                 YEAR/MONTH                                   
*                                                                               
TOTMATCH DS    XL1                 MATCHED SPOT IND                             
TOTCLEAR DS    XL1                 CLEARED SPOT IND                             
TOTUNMAT DS    XL1                 UNMATCHED SPOT IND                           
TOTUNCLR DS    XL1                 UNCLEARED SPOT IND                           
*                                                                               
TOTSPOTS DS    F                   TOTAL SPOTS                                  
TOTGROSS DS    F                   GROSS DOLLARS                                
TOTNET   DS    F                   NET DOLLARS                                  
TOTACT   DS    F                   LATEST ACTIVITY DATE                         
         SPACE 2                                                                
       ++INCLUDE SPREPNVWRK                                                     
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPREPMODES                                                     
       ++INCLUDE DDTSARD                                                        
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
*                                                                               
* CONTROL FILE DSECTS                                                           
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE DDOFFICED                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'085SPREPNV02A08/22/13'                                      
         END                                                                    
