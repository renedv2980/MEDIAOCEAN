*          DATA SET FATO3270   AT LEVEL 004 AS OF 08/13/14                      
*CATALP FATO3270                                                                
         TITLE 'TO3270  - 3270 TERMINAL OUTPUT TRANSLATOR'                      
***********************************************************************         
* THIS MODULE CONVERTS DATA FLDS IN THE TWA TO A 3270 OUTPUT          *         
* STRING IN A BUFFER. PARAMS VIA R1 ARE                               *         
* CL1  INPUT  0=TRANSMIT CHANGED FLDS  1=TRANSMIT ALL FLDS            *         
*      OUTPUT 0=WRITE                  1=ERASE/WRITE                  *         
* AL3  A(TWA)                                                         *         
* AL4  A(OUTPUT BUFFER)                                               *         
* AL4  A(UTL ENTRY)                                                   *         
* AL4  A(TRANSLATOR I/O BLOCK)                                        *         
***********************************************************************         
         PRINT NOGEN                                                            
TO3270   CSECT                                                                  
         ENTRY CTRYXLOT                                                         
         NMOD1 WORKL,**TO3270                                                   
         USING TIOBD,RC                                                         
         SAM24                     FORCE 24 BIT MODE                            
*                                                                               
         ST    R1,SAVEAPL                                                       
         L     RE,12(R1)                                                        
         MVC   TIOBD(TIOBL),0(RE)  MOVE TIOB TO MY W/S                          
         SR    RE,RE                                                            
         ICM   RE,7,1(R1)                                                       
         ST    RE,ATWA             SAVE A(TWA)                                  
         LA    RE,64(RE)                                                        
         ST    RE,AFSTFH           SAVE A(FIRST FIELD HEADER)                   
*                                                                               
         GOTO1 =V(PROTOFF)                                                      
*                                                                               
         L     RE,8(R1)            POINT TO UTL ENTRY                           
         USING UTLD,RE                                                          
         MVC   STAT6,TSTAT6        SAVE UTL STATUS BYTE 6                       
         TM    STAT6,TST6IAMS      =IAM SOURCE TERMINALS XMIT ALL               
         BZ    *+8                                                              
         MVI   0(R1),1                                                          
*                                                                               
INIT0    MVI   NONO,0              CLEAR SPECIAL HEADER MESSAGE FLAGS           
         TM    TSTAT5,TST5NONO     TEST UPDATES INHIBITED                       
         BZ    INIT0J                                                           
         OI    NONO,X'01'          THIS TRANS HAD UPDATES INHIBITED             
         NI    TSTAT5,255-TST5NONO                                              
*                                                                               
INIT0A   TM    TTEST,TTESTUEN      NO MESSAGE IF CONNECTED WITH U=N             
         BO    INIT0J                                                           
         CLC   TSYS(2),=X'0A0C'    NO MESSAGE FOR CON/MAD                       
         BE    INIT0J                                                           
         L     RF,ATWA             RF=A(TWA)                                    
         CLC   72(8,RF),=C'<FALINK>'                                            
         BE    INIT0J              NO MESSAGE FOR FALINK SCREEN                 
*                                                                               
INIT0B   L     RF,=V(SSB)          RF=A(SSB)                                    
         USING SSBD,RF                                                          
         TM    SSBSYSFL,X'80'      IS THIS IS TEST FACPAK                       
         BZ    INIT0D              NO                                           
*                                                                               
INIT0C   EQU   *                   RULES FOR TEST FACPAKS                       
*&&US                                                                           
         CLI   SSBSYSID,1          TST GETS MESSAGE                             
         BE    INIT0E                                                           
         CLI   SSBSYSID,6          MEL GETS MESSAGE                             
         BE    INIT0E                                                           
*&&                                                                             
*&&UK                                                                           
         CLI   SSBSYSID,3          TTS GETS MESSAGE                             
         BE    INIT0E                                                           
         CLI   SSBSYSID,5          NEW GETS MESSAGE                             
         BE    INIT0E                                                           
*&&                                                                             
         B     INIT0J              OTHER TEST SYSTEMS GET NO MESSAGE            
INIT0D   EQU   *                   RULES FOR PRODUCTION FACPAKS                 
*&&US                                                                           
         CLI   SSBDAYNO,7          ON A SUNDAY                                  
         BNE   *+12                                                             
         TM    SSBSYSFL,X'20'      REP SYSTEM DOESN'T WANT THIS MESSAGE         
         BO    INIT0J                                                           
*&&                                                                             
INIT0E   GOTO1 =V(GETTXT),DUB,X'FF00000A' GET DW/0010 MESSAGE                   
         DROP  RF                                                               
         OI    NONO,X'02'          SET HAVE OUTPUT DW/0010 MSG                  
*                                                                               
INIT0F   L     R1,SAVEAPL          POSTION CURSOR TO MESSAGE FIELD              
         OI    TIOBINDS,TIOBSETC   FORCE CURSOR TO MESSAGE FIELD                
         LA    RF,64                                                            
         STCM  RF,3,TIOBCURD                                                    
         XC    TIOBCURI,TIOBCURI                                                
         XC    TIOBCURS,TIOBCURS                                                
         L     RE,8(R1)            POINT TO UTL ENTRY                           
*                                                                               
INIT0J   CLC   TSVCREQ,=X'0000'    TEST APPLICATION PROGRAM                     
         BNE   INIT1                                                            
         TM    TSTAT2,TSTATTCT     TEST HAVE JUST CONNECTED                     
         BZ    INIT1                                                            
         NI    TSTAT2,255-TSTATTCT                                              
         B     INIT1               *NOP* REMOVE THIS TO ACTIVATE CODE           
         L     R3,AFSTFH                                                        
         MVC   SVFSTFH(68),0(R3)   SAVE FIRST FIELD HDR/DATA                    
         BRAS  RE,WHODAT                                                        
         OI    NONO,X'04'          SET HAVE PUT WHOAMI DATA IN FIELD            
*                                                                               
INIT1    L     R1,SAVEAPL          POINT TO UTL ENTRY                           
         L     RE,8(R1)                                                         
         L     RF,=V(SSB)                                                       
*                                                                               
         MVI   XLAT,0              CLEAR CHARACTER SET FALGS                    
         TM    SSBSTAT6-SSBD(RF),SSB6ALLC                                       
         BZ    *+8                                                              
         OI    XLAT,X'01'          SET FACPAK USES ALL CHR TRANSLATE            
*                                                                               
         L     RF,SSBTKADR-SSBD(RF)                                             
         MVC   SYS,TCBSWSOV-TCBD(RF)                                            
         CLI   SYS,0               SET SYS FROM TCBSWSOV                        
         BNE   *+10                                                             
         MVC   SYS,TOVSYS          OR FROM TOVSYS IF NOT SET                    
*                                                                               
         MVC   CTRY,TCTRY          SAVE TERMINALS CTRY/LANG/SYSTEM              
         MVC   LANG,TLANG                                                       
         MVI   XTND,0              SET NO EXTENDED ATTRIBUTES                   
         XC    XCOLMASK,XCOLMASK                                                
         CLC   TSVCREQ,=X'0000'    S/R IS SYSTEM 1                              
         BE    INIT1A                                                           
         CLI   TSVCREQ+1,$UNWIND   $UNWIND DOESN'T COUNT                        
         BE    INIT1A                                                           
         MVI   SYS,1                                                            
         TM    TIOBINDS,TIOBCLR    TEST S/R WANTS COLOUR                        
         BZ    INIT4                                                            
INIT1A   TM    TCLRMSK,X'80'       TEST COLOUR MASK ON                          
         BZ    INIT4                                                            
         TM    TTYPE,X'04'         TEST EXTENDED ATTRIBUTE TERMINAL             
         BZ    INIT4                                                            
         MVI   XTND,1                                                           
         TM    TCLRMSK,X'10'       TEST IGNORE OVERIDE FLAG                     
         BZ    *+12                                                             
         NI    TCLRMSK,X'EF'                                                    
         B     INIT4                                                            
         TM    TCLRMSK,X'40'       TEST OVERIDE COLOURS IN MASK                 
         BZ    INIT4                                                            
         ICM   R2,15,TCLRMSK       EXPAND COLOUR MASK INTO BYTE VALUES          
         LA    R4,XCOLMASK+7                                                    
         LA    RF,8                                                             
INIT2    SRDL  R2,4                EXPAND MASK INTO BYTE VALUES                 
         SRL   R3,28                                                            
         STC   R3,0(R4)                                                         
         AHI   R4,-1                                                            
         BCT   RF,INIT2                                                         
*                                                                               
INIT4    TM    XLAT,X'01'          TEST IF USING ALL CHRS VALID TABLE           
         BZ    INIT4A                                                           
*NOP*    CLI   CTRY,2              ONLY FOR UK/US                               
*NOP*    BH    INIT4A                                                           
         MVC   OUTXLAT,=A(OUTALLC)                                              
         B     INIT5                                                            
INIT4A   LLC   RF,CTRY             POINT TO TRANSLATE TABLE FOR CTRY            
         CHI   RF,15                                                            
         BNH   *+6                                                              
         SR    RF,RF                                                            
         SLL   RF,2                                                             
         LA    RF,CTRYXLOT(RF)                                                  
         MVC   OUTXLAT,0(RF)       SAVE A(OUTPUT TRANSLATE TABLE)               
*                                                                               
INIT5    ST    R1,SAVER1           OUTPUT STEREO INFO IN S/R FIELD              
         ST    RE,SAVERE                                                        
         TM    STAT6,TST6STRO+TST6STFU                                          
         BZ    INIT5X                                                           
         L     R4,AFSTFH           LOCATE FIRST FIELD                           
         LLC   RF,0(R4)                                                         
         AR    R4,RF                                                            
         CLI   8(R4),C'*'          CHECK FOR * IN 1ST CHR                       
         BNE   INIT5S              NO MUST BE A SERVICE REQ                     
         TM    STAT6,TST6STRO                                                   
         BZ    INIT5X              S=NY EMULATOR ONLY                           
*                                                                               
INIT5A   MVC   DUB+0(1),TCOSYS     SET SYSTEM                                   
         TM    TIOBINDS,TIOBSYS    TEST TO USE TOVSYS                           
         BZ    *+10                                                             
         MVC   DUB+0(1),TOVSYS                                                  
         MVC   DUB+1(1),TPRG       SET PROGRAM                                  
         MVI   DUB+2,X'FF'         SET SCREEN (DEFAULT)                         
INIT5B   SR    R5,R5               LOCATE LAST SCREEN OVERLAY NUMBER            
         ICM   R5,1,TSCRNE                                                      
         BP    *+12                                                             
         LA    R5,DUB+2                                                         
         B     INIT5C                                                           
         AHI   R5,-1                                                            
         MHI   R5,3                                                             
         LA    R5,TSCRNUM(R5)                                                   
INIT5C   TM    TIOBINDS,TIOBSCRN   TEST SCREEN NUM IN TIOBCNT(1)                
         BZ    *+10                                                             
         MVC   0(1,R5),TIOBCNT                                                  
         MVC   DUB+2(1),0(R5)      SET SCREEN                                   
         SR    R1,R1                                                            
INIT5D   TM    TIOBINDS,TIOBSUBS   TEST SUBSCREEN NUM IN TIOBAID                
         BZ    *+8                                                              
         IC    R1,TIOBAID                                                       
         SLL   R1,4                                                             
         STC   R1,DUB+3            SET SUBSCREEN                                
         MVC   DUB+4(1),TAGCTRY                                                 
         NI    DUB+4,X'0F'                                                      
         OC    DUB+3(1),DUB+4      SET COUNTRY                                  
*                                                                               
         GOTO1 =V(HEXOUT),DMCB,DUB,(0,8(R4)),4,(24,0)                           
*                                                                               
         MVC   16(2,R4),SPACES                                                  
         TM    TIOBINDS,TIOBASP    TEST ALTERNATE SCREEN PROGRAM SET            
         BZ    INIT5E                                                           
         MVI   16(R4),C'1'         FIRST SET OF ALTERNATE SCREENS               
         MVI   17(R4),C'0'         SPARE - KEEP THINGS EVEN FOR NOW             
*                                                                               
INIT5E   MVI   08(R4),C'*'                                                      
         OI    06(R4),X'80'        TRANSMIT STEREO DATA *SPPSSXC                
         B     INIT5X                                                           
*                                                                               
INIT5S   CLI   TSVCREQ+1,0         TEST IF WELL DEFINED S/R                     
         BE    INIT5T                                                           
         GOTO1 =V(HEXOUT),DMCB,TSVCREQ+1,(0,23(R4)),1,(24,0)                    
         B     INIT5V                                                           
INIT5T   OC    08(17,R4),08(R4)    TEST IF EMPTY S/R FIELD                      
         BZ    INIT5X                                                           
         CLI   TSYS,0                                                           
         BNE   INIT5U                                                           
         MVC   23(2,R4),=C'10'     SET CONNECT VALUE                            
         B     INIT5V                                                           
INIT5U   MVC   23(2,R4),=C'00'     SET DONT KNOW VALUE                          
         CLC   08(7,R4),=C'=SEARCH'                                             
         BE    INIT5U1                                                          
         CLC   08(3,R4),=C'=SRC'                                                
         BNE   INIT5V                                                           
INIT5U1  MVC   23(2,R4),=C'2A'     =SEARCH,N *SPP*2A                            
         MVC   DUB+0(1),TCOSYS                                                  
         MVC   DUB+1(1),TPRG                                                    
         GOTO1 =V(HEXOUT),DMCB,DUB,(0,18(R4)),2,(24,0)                          
         MVI   18(R4),C'*'                                                      
INIT5V   OC    08(17,R4),SPACES                                                 
         MVI   8(R4),C'='                                                       
         MVI   22(R4),C'*'                                                      
INIT5W   OI    6(R4),X'80'         TRANSMIT *SR IN LAST THREE BYTES             
*                                                                               
INIT5X   L     R1,SAVER1                                                        
         L     RE,SAVERE                                                        
*                                                                               
HLP      TM    TFLAG,TFLAGHLP      TEST IF HELP PANEL ON SCREEN                 
         BZ    HLPX                                                             
         CLI   TSVCREQ+1,$PFK      IGNORE IF $PFK S/R                           
         BE    HLPX                                                             
         CLI   TSVCREQ+1,$HLP      IGNORE IF $HELP S/R                          
         BE    HLPX                                                             
         MVI   0(R1),1             SET TO XMIT ALL FIELDS                       
         NI    TFLAG,255-TFLAGHLP  AND TURN OFF HELP PANEL FLAG                 
HLPX     EQU   *                                                                
         DROP  RE                                                               
*&&US                                                                           
XMT      CLI   0(R1),1             XMT ALL IF BEFORE=AFTER=YES                  
         BE    XMTX                                                             
         L     R3,AFSTFH                                                        
         SR    R0,R0                                                            
*                                                                               
XMT1     ICM   R0,1,0(R3)          SEARCH FOR END OF TWA                        
         BZ    XMT3                                                             
         CLI   1(R3),NOPFLD        TEST NO-OP FIELD                             
         BE    XMT2                                                             
         TM    1(R3),FATBXHDR      TEST EXTENDED FLDHDR                         
         BZ    XMT1A                                                            
         CHI   R0,17                                                            
         BNL   XMT2                                                             
         DC    H'0'                                                             
XMT1A    CHI   R0,9                                                             
         JL    *+2                 DIE IF INVALID FIELD HEADER                  
*                                                                               
XMT2     AR    R3,R0                                                            
         B     XMT1                                                             
*                                                                               
XMT3     CLI   1(R3),0             TEST BEFORE                                  
         BE    XMTX                                                             
         CLI   2(R3),0             TEST AFTER                                   
         BE    XMTX                                                             
         MVI   0(R1),1             SET TO XMT ALL FIELDS                        
XMTX     EQU   *                                                                
*&&                                                                             
                                                                                
***********************************************************************         
* FIRST TWA FIELD                                                     *         
***********************************************************************         
FSTFLD   ICM   R2,15,4(R1)         OUTPUT BUFFER ADDR                           
         BZ    EXIT                                                             
FSTFLD1  LA    R2,PTBUFF           BUFFER BUILD AREA                            
*                                                                               
FSTFLD2  XC    FUNP,FUNP           (SBA)(1,1)(SF)(PROT)                         
         MVI   FUNPATB,X'40'                                                    
         MVI   FUNPATBX,X'00'                                                   
         MVI   CURSES,0                                                         
         TM    TIOBINDS,TIOBSETC                                                
         BZ    *+8                                                              
         OI    CURSES,X'08'        SET SPECIAL CURSOR REQUIRED                  
         MVC   0(5,R2),SBAFRST                                                  
         MVI   LTATB,X'40'+FATBPROT  LAST TRANS ATB = (PROT)                    
         XC    LTATBX,LTATBX                                                    
         MVC   LTEND,=X'40C1'      LAST TRANS END = (1,2)                       
         MVI   LTFLAG,FOUTTRN      LAST TRANS FLAG= TRANS                       
         MVI   LTPDEL,0            CLEAR DELIMIT PROTECTED FIELD FLAG           
         MVI   DELFLAG,0           CLEAR DELIMIT IN BUFFER FLAG                 
         MVI   FSTFLAG,X'FF'       FIRST FIELD FLAG SET ON                      
         MVI   FSTATBX,0           CLEAR FIRST FIELD XATTB SAVE                 
*                                                                               
         LA    R2,5(R2)            R2=A(NEXT OUTPUT BYTE)                       
         L     R3,AFSTFH           R3=A(NEXT TWA FIELD)                         
         USING FLDHDRD,R3                                                       
                                                                                
***********************************************************************         
* NEXT TWA FIELD                                                      *         
***********************************************************************         
NXTFLD   LLC   R6,FLDLEN           R6=LEN OF TWA FLD                            
         CHI   R6,9                TEST FOR VALID TWA FIELD                     
         BNL   NXTFLD1                                                          
         LTR   R6,R6               ZERO FOR END OF TWA                          
         BZ    ENDFLDS                                                          
         DC    H'0'                                                             
NXTFLD1  LA    RF,0(R3,R6)         SET R9=A(NEXT ACTIVE FIELD)                  
         SR    R9,R9                                                            
         SR    R0,R0                                                            
NXTFLD2  CLI   0(RF),0                                                          
         BE    NXTFLD3                                                          
         TM    1(RF),NOPFLD                                                     
         BNO   *+14                                                             
         IC    R0,0(RF)                                                         
         AR    RF,R0                                                            
         B     NXTFLD2                                                          
         LR    R9,RF                                                            
*                                                                               
NXTFLD3  TM    FLDATB,NOPFLD       BYPASS NOP FIELDS                            
         BO    TRANSX                                                           
         CLI   FLDOLEN,X'FF'                                                    
         BNE   *+8                                                              
         OI    CURSES,X'80'        SET ALL FIELDS ARE NOW NEW                   
         TM    STAT6,TST6STRO                                                   
         BZ    NXTFLD3A                                                         
         TM    FLDATB,FATBPROT+FATBNUM                                          
         BO    TRANSX              BYPASS PROT/NUM FIELDS FOR STEREO            
*                                                                               
NXTFLD3A MVC   FATB,FLDOIND        CALC FLD ATB BYTE AT FATB                    
         OC    FATB,FLDATB                                                      
*&&US*&& NI    FATB,FATBPROT+FATBLOW+FATBMOD+FATBNUM                            
*&&UK*&& NI    FATB,FATBPROT+FATBLOW+FATBMOD IGNORE NUMERIC ATTRIBUTE           
         TR    FATB,TRNTBL                                                      
         MVI   FATBX,0                                                          
         CLI   XTND,0              TEST ANY EXTENDED ATTRIBUTES                 
         BE    NXTFLD4                                                          
         CLI   FSTATBX,0           TEST FIRST FIELD SET XATTB                   
         BE    NXTFLD3B            AND THIS IS NEXT FIELD                       
         TM    FSTATBX,X'80'       TEST IF FIRST WAS REVERSE VIDEO              
         BNO   NXTFLD3B                                                         
         MVC   FATBX,FSTATBX       SET SECOND TO FIRST XATTS                    
         MVI   FSTATBX,0           CLEAR FIRST XATTB TO AVOID NEXT PASS         
         B     NXTFLD3C                                                         
*                                                                               
NXTFLD3B MVI   FSTATBX,0           RESTORE DEFAULT COLOURS                      
         TM    FATB,FATBPROT+FATBHIGH                                           
         BNZ   *+12                                                             
         MVI   FATBX,4             UNPROT/NORMAL - GREEN                        
         B     NXTFLD3C                                                         
         TM    FATB,FATBPROT                                                    
         BNZ   *+12                                                             
         MVI   FATBX,2             UNPROT/HIGH - RED                            
         B     NXTFLD3C                                                         
         TM    FATB,FATBHIGH                                                    
         BNZ   *+12                                                             
         MVI   FATBX,1             PROT/NORMAL - BLUE                           
         B     NXTFLD3C                                                         
         MVI   FATBX,7             PROT/HIGH - WHITE                            
*                                                                               
NXTFLD3C TM    1(R3),FATBXHDR      TEST EXTENDED FLDHDR                         
         BZ    NXTFLD3D                                                         
         LA    RF,0(R3,R6)         POINT TO EXTENDED ATTRIBUTES                 
         AHI   RF,-3                                                            
         CLI   0(RF),0             SAVE EXTENDED ATTRIBUTES                     
         BE    NXTFLD3D            UNLESS NOT SET                               
         MVC   FATBX,0(RF)                                                      
         B     NXTFLD3E                                                         
*                                                                               
NXTFLD3D TM    1(R3),FATBPROT      TEST PROTECTED WITH EXTENDED ATTB            
         BZ    NXTFLD3E                                                         
         CLI   4(R3),X'0C'         TEST INPUT INDICS FOR XTND ATTB              
         BNE   NXTFLD3E                                                         
         MVC   FATBX,5(R3)         SAVE EXTENDED ATTRIBUTES                     
*                                                                               
NXTFLD3E TM    XCOLMASK,X'04'      TEST OVERIDE COLOUR MASK SET                 
         BZ    NXTFLD4                                                          
         LLC   RE,FATBX            OVERIDE THE XATTB COLOUR                     
         SLL   RE,29                                                            
         SRL   RE,29                                                            
         LTR   RE,RE                                                            
         BZ    NXTFLD4                                                          
         LA    RF,XCOLMASK                                                      
         AR    RF,RE                                                            
         NI    FATBX,X'F8'                                                      
         OC    FATBX,0(RF)                                                      
*                                                                               
NXTFLD4  SR    R4,R4               CALC FLD ATB ADR AT FSTART                   
         ICM   R4,3,2(R3)                                                       
         STH   R4,FSTRABS          SAVE ABSOLUTE ADR OF FIRST FLD CHR           
         AHI   R4,-1               ATTB ONE POSN BACK FROM START                
         STC   R4,FSTART+1                                                      
         NI    FSTART+1,X'3F'                                                   
         SRL   R4,6                                                             
         STC   R4,FSTART                                                        
         TR    FSTART,TRNTBL                                                    
*                                                                               
NXTFLD5  TM    FATB,FATBPROT       SAVE ADDR & ATB OF 1ST UNPROT FIELD          
         BO    NXTFLD5A                                                         
         OC    FUNP,FUNP                                                        
         BNZ   NXTFLD5A                                                         
         MVC   FUNP,FLDADR                                                      
         MVC   FUNPATB,FATB                                                     
         MVC   FUNPATBX,FATBX                                                   
*                                                                               
NXTFLD5A CLI   XTND,0              IF EXTENDED ATTRIBUTES PROCESS MODE          
         BE    NXTFLD6                                                          
         CLI   FSTFLAG,0           IF FIRST FIELD                               
         BE    NXTFLD5B            THEN SAVE EXTENDED ATTRIBUTE                 
         MVC   FSTATBX,FATBX                                                    
         MVI   FSTFLAG,0           CLEAR FIRST FIELD FLAG                       
*                                                                               
NXTFLD5B CLI   LTPDEL,0            TEST PROTECT DELIMIT FLAG SET                
         BE    NXTFLD6                                                          
         MVI   LTPDEL,0            CLEAR PROTECTED DELIMIT FLAG                 
         CLC   FSTART,LTEND        TEST NEXT FIELD NOT STRAIGHT AFTER           
         BE    NXTFLD6                                                          
         MVI   0(R2),X'1D'         THEN MARK END WITH PROTECTED FATB            
         MVI   1(R2),X'40'+FATBPROT                                             
         LA    R2,2(R2)                                                         
         MVI   DELFLAG,1           FLAG DELIMITER IN BUFFER                     
*                                                                               
NXTFLD6  TM    LTFLAG,FOUTTRN      SAVE FATB IF PREV FLD TRANS                  
         BZ    NXTFLD7                                                          
         MVC   LTNATB,FATB                                                      
         MVC   LTNATBX(1),FATBX                                                 
*                                                                               
NXTFLD7  TM    CURSES,X'08'        TEST SPECIAL CURSOR SETTING                  
         BZ    NXTFLD8                                                          
         TM    CURSES,X'02'        TEST CURSOR FIELD FOUND                      
         BO    NXTFLD8                                                          
         OC    TIOBCURD,TIOBCURD   TEST IF A TWA DISPLACEMENT                   
         BZ    NXTFLD8                                                          
         S     R3,ATWA                                                          
         CLM   R3,3,TIOBCURD       TEST THIS IS THE CORRECT FIELD               
         BNE   *+8                                                              
         OI    CURSES,X'02'        YES - SET CURSOR FIELD FOUND                 
         A     R3,ATWA                                                          
         B     NXTFLD8                                                          
*                                                                               
NXTFLD8  CLI   0(R1),1                                                          
         BE    TRANS+4             TRANS ALL FIELDS                             
         TM    CURSES,X'80'                                                     
         BZ    *+8                                                              
         OI    FLDOLEN,X'80'                                                    
         TM    FLDOIND,FOUTTRN                                                  
         BO    TRANS               TRANS THIS FIELD                             
         TM    FLDOLEN,X'80'                                                    
         BO    TRANS               TRANS THIS NEW FIELD                         
         TM    LTFLAG,X'02'                                                     
         BO    TRANS               TRANS THIS BECAUSE PREV WAS NEW              
         LTR   R9,R9                                                            
         BZ    *+12                                                             
         TM    7(R9),X'80'                                                      
         BO    TRANS               TRANS THIS BECAUSE NEXT IS NEW               
         TM    FLDOIND,FOUTCUR                                                  
         BO    NXTFLD9             TRANS THIS CURSOR POSITION                   
         TM    LTFLAG,FOUTCUR                                                   
         BO    TRANSX                                                           
         MVI   LTFLAG,0            SET FIELD NOT TRANSMITTED                    
         B     TRANSX                                                           
*                                                                               
NXTFLD9  TM    CURSES,X'08'        TEST SPECIAL CURSOR SETTING                  
         BNZ   NXTFLDX                                                          
         MVI   LTFLAG,FOUTCUR      SET CURSOR POSND FLAG                        
         OI    CURSES,X'01'                                                     
         MVI   0(R2),X'11'         (SBA)(FSTRABS)(IC)                           
         LH    RE,FSTRABS                                                       
         STC   RE,2(R2)                                                         
         NI    2(R2),X'3F'                                                      
         SRL   RE,6                                                             
         STC   RE,1(R2)                                                         
         TR    1(2,R2),TRNTBL                                                   
         MVI   3(R2),X'13'                                                      
         LA    R2,4(R2)                                                         
*                                                                               
NXTFLDX  B     TRANSX                                                           
                                                                                
***********************************************************************         
* TRANSMIT FIELD                                                      *         
***********************************************************************         
TRANS    OI    FLDOIND,FOUTTRN                                                  
         LR    R7,R6               CALC FLD END AT FEND                         
         AHI   R7,-8                                                            
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R7,-8                                                            
         MVC   FEND,FLDADR                                                      
         AH    R7,FEND                                                          
         CHI   R7,1919             LAST CHR ON SCR USED                         
         BNH   *+6                 NO                                           
         SR    R7,R7               YES SET FLD END TO 1ST CHR                   
         STH   R7,FEND                                                          
         NI    FEND+1,X'3F'                                                     
         SRL   R7,6                                                             
         STC   R7,FEND                                                          
         TR    FEND,TRNTBL                                                      
*                                  SET UP FLD DATA AT FDATA                     
TRANS1   LR    R7,R6                                                            
         AHI   R7,-9               R7=MAX FLD LEN - 1                           
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R7,-8                                                            
         LTR   R7,R7                                                            
         JM    *+2                 DIE IF INVALID EXTENDED HEADER               
         EX    R7,MOVEIT                                                        
         STM   R1,R2,SAVER1                                                     
         EX    R7,TESTDDIT         TEST IF DATA DICTIONARY REQUIRED             
         BNZ   TRANS2              YES                                          
TRANS1A  LA    R8,FDATA(R7)        POINT TO LAST CHR OF DATA                    
         LA    R7,1(R7)                                                         
         B     TRANS3                                                           
*                                                                               
TRANS2   MVI   P1+0,C'T'           SET TRANSLATE WHOLE FIELD                    
         MVI   P1+1,C'U'           SET CASE TO UPPER/LOWER                      
         TM    FLDATB,FATBLC                                                    
         BZ    *+8                                                              
         MVI   P1+1,C'L'                                                        
         MVC   P1+2(1),SYS         SET SYSTEM                                   
         MVC   P1+3(1),LANG        SET LANGUAGE                                 
         LA    RF,FDATA            SET ADR AND LEN OF DATA                      
         ST    RF,P2                                                            
         LA    RF,1(R7)                                                         
         STC   RF,P2                                                            
         ICM   RF,15,=V(DICTATE)   GO TO V(DICTATE) FOR TRANSLATION             
         BZ    TRANS2A                                                          
         LA    R1,P1                                                            
         BASR  RE,RF                                                            
TRANS2A  LM    R1,R2,SAVER1        RESTORE R1,R2 DESTROYED BY TRT               
         EX    R7,RESTORIT         MOVE TRANSLATED FIELD BACK TO TWA            
         B     TRANS1A                                                          
*                                                                               
TRANS3   TM    FLDATB,FATBDEL      DELETE TRAILING BLANKS                       
         BO    *+16                NO                                           
         CLI   0(R8),C' '                                                       
         BNE   *+8                                                              
         MVI   0(R8),0                                                          
         CLI   0(R8),0                                                          
         BNE   TRANS3A                                                          
         AHI   R8,-1                                                            
         BCT   R7,TRANS3                                                        
         B     *+12                                                             
TRANS3A  LR    R7,R8                                                            
         LA    R8,FDATA-1                                                       
         SR    R7,R8                                                            
         NI    LTFLAG,X'FF'-FOUTMOD  SET FIELD NOT NEW                          
         TM    FLDOLEN,X'80'                                                    
         BZ    *+8                                                              
         OI    LTFLAG,FOUTMOD      SET NEW FIELD FLAG                           
         STC   R7,FLDOLEN          SET SIGNIFICANT FIELD LEN                    
         LR    R7,R6                                                            
         AHI   R7,-9                                                            
         TM    FLDATB,FATBXHDR     TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         AHI   R7,-8                                                            
         L     RF,OUTXLAT          POINT TO TRANLATE TABLE FOR CTRY             
         EX    R7,CLEANIT          CLEAN OUTPUT CHRS FOR TRANSMISSION           
         EX    R7,RESTORIT         MOVE BACK TO TWA                             
*                                                                               
         MVC   HALF,FLDADR         DATA COMPACTION ROUTINE                      
         LH    RA,HALF             RA=NEXT SCR ABS ADR                          
         LR    R0,R7               R0=COUNT OF TOTAL COMPARES                   
         SR    R7,R7               R7=COUNT OF EQL COMPARES                     
         LA    R8,FDATA            R8=A(NEXT CHR IN FDATA)                      
         LR    R5,R8               R5=A(1ST COMPARE EQL CHR)                    
PACK     CLC   0(1,R8),1(R8)       TWO CONSECUTIVE CHRS EQL                     
         BNE   PACK4               NO                                           
         LA    R7,1(R7)            BUMP COMPARE EQL COUNT                       
         LA    R8,1(R8)                                                         
PACK1    LA    RA,1(RA)                                                         
         LTR   R0,R0                                                            
         BZ    PACKX                                                            
         BCT   R0,PACK                                                          
PACK4    CHI   R7,4                FIVE OR MORE EQL CHRS                        
         BNL   PACK5               YES                                          
         SR    R7,R7               NO DONT COMPACT                              
         LA    R8,1(R8)                                                         
         LR    R5,R8                                                            
         B     PACK1                                                            
PACK5    LTR   R7,R0               SHIFT CHRS LEFT IF NECESSARY                 
         BZ    PACK6                                                            
         EX    R7,*+8                                                           
         B     PACK6                                                            
         MVC   4(0,R5),1(R8)                                                    
PACK6    LA    R7,1(RA)            R7=ABS SCR ADR OF LAST+1                     
         CHI   R7,1919                                                          
         BNH   *+6                                                              
         SR    R7,R7                                                            
         STH   R7,HALF             CALC SCR ADR OF LAST+1                       
         NI    HALF+1,X'3F'                                                     
         SRL   R7,6                                                             
         STC   R7,HALF                                                          
         TR    HALF,TRNTBL                                                      
         MVI   0(R5),X'3C'         (RA)(LAST+1)(1ST CHR)                        
         MVC   1(2,R5),HALF                                                     
         SR    R7,R7                                                            
         LA    R8,4(R5)                                                         
         LR    R5,R8                                                            
         B     PACK1                                                            
PACKX    LR    RA,R8                                                            
         LA    R8,FDATA                                                         
         SR    RA,R8               RA=COMPACTED FLD DATA LEN                    
*                                                                               
TRANS4   CLC   FSTART,LTEND        FLD START AT LAST TRAN END                   
         BE    TRANS7              YES DONT NEED TO SET ADR                     
         LA    RF,1                COUNT CONTROL CHARS IN BUFFER                
         TM    LTATB,FATBPROT      WAS LAST TRANS FLD PROT                      
         BO    TRANS5              YES                                          
         TM    LTFLAG,FOUTCUR      WAS CURSOR POSN THE LAST OUTPUT              
         BZ    *+18                NO                                           
         MVI   0(R2),X'11'                                                      
         MVC   1(2,R2),LTEND       (SBA)(LTEND)                                 
         LA    R2,3(R2)                                                         
         MVI   0(R2),X'1D'                                                      
         TM    LTNATB,FATBPROT     TWO SUCCESSIVE UNPROT FLDS                   
         BO    *+8                 NO                                           
         MVI   LTNATB,X'40'+FATBPROT  YES DELIMIT 1ST WITH PROT                 
         MVC   1(1,R2),LTNATB      (SF)(LTNATB)                                 
         LA    R2,2(R2)                                                         
         LA    RF,2                                                             
*                                                                               
TRANS5   CLI   DELFLAG,0           TEST IF DELIMITER OUTPUT TO BUFFER           
         BE    *+8                                                              
         LA    RF,1(RF)            IF SO BUMP CONTROL CHAR COUNT                
         MVC   HALF,FLDADR         CALC ADDR OF FLD ATB -1                      
         LH    R4,HALF                                                          
         SR    R4,RF                                                            
         STH   R4,HALF                                                          
         NI    HALF+1,X'3F'                                                     
         SRL   R4,6                                                             
         STC   R4,HALF                                                          
         TR    HALF,TRNTBL                                                      
         CLC   HALF,LTEND          ARE WE NOW POSITIONED AT FIELD               
         BE    TRANS7              YES DONT NEED TO SET ADDRESS                 
*                                                                               
         TM    LTFLAG,X'02'+FOUTMOD  DO WE NEED TO CLEAR GAP                    
         BZ    TRANS6                                                           
         MVI   0(R2),X'3C'                                                      
         MVC   1(2,R2),FSTART                                                   
         MVI   3(R2),X'00'         (RA)(FSTART)(NULL)                           
         LA    R2,4(R2)                                                         
         B     TRANS7                                                           
*                                                                               
TRANS6   MVI   0(R2),X'11'         (SBA)(FSTART)                                
         MVC   1(2,R2),FSTART                                                   
         LA    R2,3(R2)                                                         
*                                                                               
TRANS7   CLI   XTND,0              TEST ANY EXTENDED ATTRIBUTES                 
         BE    TRANS7A                                                          
         CLI   FATBX,0                                                          
         BNE   TRANS7B                                                          
TRANS7A  MVI   0(R2),X'1D'         (SF)(FATB)                                   
         MVC   1(1,R2),FATB                                                     
         LA    R2,2(R2)                                                         
         B     TRANS8                                                           
TRANS7B  LLC   RE,FATBX            B'HHSSSCCC'                                  
         SRDL  RE,3                ISOLATE COLOUR                               
         SRL   RF,32-3                                                          
         STC   RF,FATBXX+1                                                      
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         OI    FATBXX+1,X'F0'                                                   
         SRDL  RE,3                ISOLATE SYMBOL SET                           
         SRL   RF,32-3                                                          
         STC   RF,FATBXX+2                                                      
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         OI    FATBXX+2,X'F0'                                                   
         STC   RE,FATBXX+0         ISOLATE HIGHLIGHTING                         
         LTR   RE,RE                                                            
         BZ    TRANS7C                                                          
         OI    FATBXX+0,X'F0'                                                   
         CLI   FATBXX+0,X'F3'      FOR SOME REASON IBM LEFT A HOLE              
         BNE   TRANS7C                                                          
         MVI   FATBXX+0,X'F4'                                                   
*                                                                               
TRANS7C  MVC   0(3,R2),=X'2901C0'  (SFE)(01)(TYPEATT)(FATB)                     
         MVC   3(1,R2),FATB                                                     
         LR    RF,R2               RF=A(START OF ATTRIBUTE STRING)              
         LA    RE,1                RE=NUMBER OF ATTRIBUTE PAIRS                 
         LA    R2,4(R2)                                                         
TRANS7D  CLI   FATBXX+0,X'F1'      TEST EXTENDED HIGHLIGHTING                   
         BL    TRANS7E                                                          
         CLI   FATBXX+0,X'F4'                                                   
         BH    TRANS7E                                                          
         MVI   0(R2),X'41'                                                      
         MVC   1(1,R2),FATBXX+0                                                 
         LA    R2,2(R2)                                                         
         LA    RE,1(RE)                                                         
TRANS7E  CLI   FATBXX+1,X'F1'      TEST EXTENDED COLOUR                         
         BL    TRANS7F                                                          
         CLI   FATBXX+1,X'F7'                                                   
         BH    TRANS7F                                                          
         MVI   0(R2),X'42'                                                      
         MVC   1(1,R2),FATBXX+1                                                 
         LA    R2,2(R2)                                                         
         LA    RE,1(RE)                                                         
TRANS7F  CLI   FATBXX+2,X'F0'      TEST ALTERNATE CHARACTER SET                 
         BL    TRANS7G                                                          
         CLI   FATBXX+2,X'F7'                                                   
         B     TRANS7G             **NOP** BH                                   
         MVI   0(R2),X'43'                                                      
         MVC   1(1,R2),FATBXX+2                                                 
         LA    R2,2(R2)                                                         
         LA    RE,1(RE)                                                         
TRANS7G  STC   RE,1(RF)            SET NUMBER OF PAIRS                          
*                                                                               
TRANS8   TM    CURSES,X'08'        SET SPECIAL CURSOR SETTING                   
         BNZ   TRANS9                                                           
         TM    FLDOIND,FOUTCUR     CURSOR POSN REQ                              
         BZ    TRANS9                                                           
         OI    CURSES,X'01'                                                     
         MVI   0(R2),X'13'         (IC)                                         
         LA    R2,1(R2)                                                         
*                                                                               
TRANS9   AHI   RA,-1                                                            
         TM    FATB,FATBPROT       TEST IF PROTECTED FIELD                      
         BZ    TRANSA              PROCESS UNPROTECTED FIELD NORMALLY           
         LA    RE,FDATA            RE=A(FIELD DATA)                             
         LA    RF,1(RA)            RF=LENGTH OF FIELD DATA                      
*                                  SEARCH FOR EMBEDDED X'5F'                    
TRANS9A  CLI   0(RE),X'3C'         JUMP PAST (RA) CODES                         
         BE    TRANS9E                                                          
         CLI   0(RE),X'5F'         MULTIPLE FIELD CHARACTER                     
         BE    TRANS9C                                                          
         MVC   0(1,R2),0(RE)       MOVE OUT NORMAL CHARACTER                    
         LA    R2,1(R2)                                                         
         LA    RE,1(RE)                                                         
TRANS9B  BCT   RF,TRANS9A                                                       
         B     TRANSB              FIELD MOVE COMPLETE                          
TRANS9C  TM    STAT6,TST6STRO                                                   
         BZ    TRANS9D                                                          
         MVI   0(R2),X'1D'         MULTIPLE PROTECTED FIELD FOR STEREO          
         MVC   1(1,R2),FATB                                                     
         LA    R2,2(R2)                                                         
         LA    RE,1(RE)                                                         
         B     TRANS9B                                                          
TRANS9D  MVI   0(R2),C' '          REPLACE WITH SPACE FOR NORMAL                
         LA    R2,1(R2)                                                         
         LA    RE,1(RE)                                                         
         B     TRANS9B                                                          
TRANS9E  MVC   0(4,R2),0(RE)       JUMP PAST (RA) REPEAT CHAR CODES             
         LA    R2,4(R2)                                                         
         LA    RE,4(RE)                                                         
         AHI   RF,-3                                                            
         B     TRANS9B                                                          
*                                                                               
TRANSA   EQU   *                   MOVE OUT UNPROTECTED FIELD DATA              
         EX    RA,TRANSIT          (FDATA)                                      
         LA    R2,1(R2,RA)                                                      
*                                                                               
TRANSB   EQU   *                   POST PROCESS FIELD                           
         MVC   LTATB,FATB          SET LAST TRANS ATB BYTE                      
         MVC   LTATBX,FATBX                                                     
         MVC   LTEND,FEND          SET LAST TRANS END ADDR                      
*                                                                               
         TM    FATB,FATBPROT       SET FLAG TO DELIMIT END OF                   
         BNO   *+8                 PROTECTED FIELD                              
         MVI   LTPDEL,X'FF'                                                     
*                                                                               
         OI    LTFLAG,FOUTTRN      SET TRANS FLAG                               
         NI    LTFLAG,FOUTTRN+FOUTMOD  TURN OFF CURSOR/PREVNEW FLAGS            
         TM    LTFLAG,FOUTMOD                                                   
         BZ    *+8                                                              
         OI    LTFLAG,X'02'        TURN ON PREVNEW FLAG                         
*                                                                               
TRANSX   C     R3,AFSTFH           IS THIS THE FIRST FIELD                      
         BNE   TRANSXX                                                          
         TM    NONO,X'04'          IS THERE WHOAMI DATA IN FIRST FIELD          
         BZ    TRANSXX                                                          
         MVC   0(68,R3),SVFSTFH    RESTORE ORIGINAL FIRST FIELD DATA            
         NI    NONO,255-X'04'                                                   
         B     FSTFLD2             BACK TO RESHIP THE FIRST FIELD               
*                                                                               
TRANSXX  AR    R3,R6               BUMP TO NEXT TWA FLD                         
         MVI   DELFLAG,0           CLEAR DELIMIT IN BUFFER FLAG                 
         B     NXTFLD                                                           
*                                                                               
NULLIT   XC    FDATA(0),FDATA                                                   
MOVEIT   MVC   FDATA(0),FLDDATA                                                 
TESTDDIT TRT   FDATA(0),TRTDD                                                   
CLEANIT  TR    FDATA(0),0(RF)                                                   
TRANSIT  MVC   0(0,R2),FDATA                                                    
RESTORIT MVC   FLDDATA(0),FDATA                                                 
                                                                                
***********************************************************************         
* NO MORE TWA FIELDS TO PROCESS                                       *         
***********************************************************************         
ENDFLDS  TM    LTATB,FATBPROT      LAST TRANS FLD PROT                          
         BNO   ENDFLDS0            IF SO PROCESS DELIMIT FLAG                   
         CLI   XTND,0              IF EXTENDED ATTRIBUTES                       
         BE    ENDFLDS1                                                         
         CLI   LTPDEL,0            AND PROTECT DELIMIT FLAG SET                 
         BE    ENDFLDS1                                                         
         CLC   LTEND,=X'4040'      AND NOT END OF SCREEN                        
         BE    ENDFLDS1                                                         
         MVI   0(R2),X'1D'         THEN MARK END WITH PROTECTED FATB            
         MVI   1(R2),X'40'+FATBPROT                                             
         LA    R2,2(R2)                                                         
         MVI   LTPDEL,0            CLEAR PROTECTED DELIMIT FLAG                 
         B     ENDFLDS1            YES                                          
*                                                                               
ENDFLDS0 TM    LTFLAG,FOUTTRN      LAST TRANS FLD LAST IN TWA                   
         BZ    *+8                 NO                                           
         MVI   LTNATB,X'40'+FATBPROT  SET LTNATB TO (PROT)                      
         TM    LTNATB,FATBPROT     LTNATB PROT                                  
         BO    *+8                 YES                                          
         MVI   LTNATB,X'40'+FATBPROT  SET LTNATB TO (PROT)                      
         MVC   0(5,R2),SBAFRST                                                  
         MVC   1(2,R2),LTEND                                                    
         MVC   4(1,R2),LTNATB      (SBA)(LTEND)(SF)(LTNATB)                     
         LA    R2,5(R2)                                                         
*                                                                               
ENDFLDS1 CLI   0(R1),1                                                          
         BNE   *+14                                                             
         MVC   HALF,=X'0100'       SET B,A TO CLEAR,NOTHING                     
         B     ENDFLDS2                                                         
         MVC   HALF,1(R3)          SET B,A TO TWA VALUES                        
         TM    LTFLAG,FOUTTRN+FOUTMOD  LAST TRANS FLD LAST IN TWA & NEW         
         BNO   ENDFLDS2            NO                                           
         MVI   HALF+1,1            SET TO CLEAR AFTER                           
*                                                                               
ENDFLDS2 CLC   HALF,=X'0001'       CLEAR AFTER REQUIRED                         
         BNE   SETCURSE            NO                                           
         CLC   LTEND,=X'4040'                                                   
         BE    SETCURSE                                                         
         MVC   0(4,R2),=X'3C404000'                                             
         LA    R2,4(R2)            (RA)(1,1)(NULL)                              
         B     SETCURSE                                                         
                                                                                
***********************************************************************         
* SET CURSOR IF NOT ALREADY POSITIONED                                *         
***********************************************************************         
SETCURSE TM    CURSES,X'01'        WAS CURSOR POSITIONED                        
         BO    SETLEN              YES                                          
         LH    RF,FUNP             RF=ADDR OF FIRST UNPROT FIELD                
         TM    TIOBINDS,TIOBSETC   TEST SPECIAL CURSOR SETTING                  
         BZ    SETCURS4                                                         
         TM    CURSES,X'02'        TEST TWA DISPLACEMENT                        
         BZ    SETCURS2                                                         
         SR    RE,RE                                                            
         ICM   RE,3,TIOBCURD                                                    
         A     RE,ATWA                                                          
         SR    RF,RF                                                            
         ICM   RF,3,2(RE)          RF=SCREEN ADDRESS                            
         LLC   R0,TIOBCURI         R0=INDEX INTO FIELD                          
         AR    RF,R0                                                            
         B     SETCURS4                                                         
*                                                                               
SETCURS2 OC    TIOBCURS,TIOBCURS   TEST SCREEN DISPLACEMENT SET                 
         BZ    SETCURS4                                                         
         SR    RF,RF                                                            
         ICM   RF,3,TIOBCURS                                                    
*                                                                               
SETCURS4 STH   RF,FUNP             TRANSLATE DISP TO ADDRESS                    
         NI    FUNP+1,X'3F'                                                     
         SRL   RF,6                                                             
         STC   RF,FUNP                                                          
         NI    FUNP+0,X'3F'                                                     
         TR    FUNP,TRNTBL                                                      
*                                                                               
SETCURS6 MVC   0(1,R2),SBAFRST     SET CURSOR                                   
         MVC   1(2,R2),FUNP                                                     
         MVI   3(R2),X'13'         (SBA)(ADDR)(IC)                              
         LA    R2,4(R2)                                                         
*                                                                               
SETLEN   MVI   0(R2),X'03'         (ETX)                                        
         LA    R2,1(R2)                                                         
         LA    RF,PTBUFF                                                        
         SR    R2,RF               R2=LENGTH OF DATA                            
         CHI   R2,BUFFLEN                                                       
         BNH   SETLEN2                                                          
         LA    R2,PTBUFF                                                        
         AHI   R2,BUFFLEN-1                                                     
         CLI   0(R2),X'11'         BACK UP TO PREVIOUS SBA                      
         BE    *+8                                                              
         BCT   R2,*-8                                                           
         MVI   0(R2),X'03'         SET ETX OVER PREVIOUS SBA                    
         LA    R2,1(R2)                                                         
         LA    RF,PTBUFF                                                        
         SR    R2,RF               GET NEW LENGTH                               
*                                                                               
SETLEN2  L     R6,4(R1)                                                         
         AHI   R6,-8                                                            
         STCM  R2,3,6(R6)          SET MSG LEN AT BUFF-2(2)                     
         MVC   0(1,R1),HALF        RETURN WRITE ACTION                          
*                                                                               
         L     R0,4(R1)                                                         
         LA    RE,PTBUFF                                                        
         LR    RF,R2                                                            
         LR    R1,R2                                                            
         MVCL  R0,RE               COPY BUFFER BACK                             
*                                                                               
EXIT     GOTO1 =V(PROTON)                                                       
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* CONSTANTS AND LITERALS                                              *         
***********************************************************************         
BUFFLEN  EQU   TBUFFL              CURRENT MAX LENGTH OF TBUFF                  
NOPFLD   EQU   X'FF'                                                            
$HLP     EQU   X'09'                                                            
$PFK     EQU   X'0B'                                                            
$UNWIND  EQU   X'FB'                                                            
*                                                                               
SBAFRST  DC    X'1140401D6013'     (SBA)(1,1)(SF)(PROT)(IC)                     
SPACES   DC    CL17' '                                                          
*                                                                               
         LTORG                                                                  
*                                                                               
TRNTBL   DC    XL16'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  00-0F                    
         DC    XL16'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  10-1F                    
         DC    XL16'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  20-2F                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  30-3F                    
*                                                                               
TRTDD    DC    XL16'00000000000000000000000000000000'  00-0F                    
         DC    XL16'00000000000000000000000000000000'  10-1F                    
         DC    XL16'03030404040401040303040404040000'  20-2F                    
         DC    XL16'00000000000000000000000000000000'  30-3F                    
         DC    XL16'00000000000000000000000000000000'  40-4F                    
         DC    XL16'00000000000000000000000000000000'  50-5F                    
         DC    XL16'00000000000000000000000000000000'  60-6F                    
         DC    XL16'00000000000000000000000000000000'  70-7F                    
         DC    XL16'00000000000000000000000000000000'  80-8F                    
         DC    XL16'00000000000000000000000000000000'  90-9F                    
         DC    XL16'00000000000000000000000000000000'  A0-AF                    
         DC    XL16'00000000000000000000000000000000'  B0-BF                    
         DC    XL16'00000000000000000000000000000000'  C0-CF                    
         DC    XL16'00000000000000000000000000000000'  D0-DF                    
         DC    XL16'00000000000000000000000000000000'  E0-EF                    
         DC    XL16'00000000000000000000000000000000'  F0-FF                    
*DDDDEQUS                                                                       
       ++INCLUDE DDDDEQUS                                                       
         EJECT                                                                  
**********************************************************************          
* LIST OF OUTPUT TRANSLATE TABLES INDEXED BY COUNTRY CODE            *          
**********************************************************************          
CTRYXLOT DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
         DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
         DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
         DC    A(OUTUKUS),A(OUTUKUS),A(OUTUKUS),A(OUTUKUS)                      
                                                                                
**********************************************************************          
* VALID OUTPUT CHARACTERS FOR UK AND USA                             *          
**********************************************************************          
OUTUKUS  DC    XL16'00404040404040404040404040404040'  00-0F                    
         DC    XL16'40404040404040404040404040404040'  10-1F                    
         DC    XL16'40404040404040404040404040404040'  20-2F                    
         DC    XL16'40404040404040404040404040404040'  30-3F                    
         DC    XL16'404040404040404040404A4B4C4D4E4F'  40-4F                    
         DC    XL16'504040404040404040405A5B5C5D5E5F'  50-5F                    
         DC    XL16'606140404040404040406A6B6C6D6E6F'  60-6F                    
         DC    XL16'404040404040404040797A7B7C7D7E7F'  70-7F                    
         DC    XL16'40818283848586878889404040404040'  80-8F                    
         DC    XL16'4091929394959697989940404040409F'  90-9F                    
         DC    XL16'40A1A2A3A4A5A6A7A8A9404040404040'  A0-AF                    
         DC    XL16'40404040404040404040404040404040'  B0-BF                    
         DC    XL16'C0C1C2C3C4C5C6C7C8C9404040404040'  C0-CF                    
         DC    XL16'D0D1D2D3D4D5D6D7D8D9404040404040'  D0-DF                    
         DC    XL16'E040E2E3E4E5E6E7E8E9404040404040'  E0-EF                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F9404040404040'  F0-FF                    
                                                                                
**********************************************************************          
* ALL CHARACTERS ABOVE X'40' ARE DEFINED AS VALID                    *          
* BOX CHARACTERS ARE 8F,AB,AC,AD,BB,BC,BD,BF,CB,CC,EB,EC,FA AND 42   *          
**********************************************************************          
OUTALLC  DC    XL16'00404040404040404040404040404040'  00-0F                    
         DC    XL16'40404040404040404040404040404040'  10-1F                    
         DC    XL16'40404040404040404040404040404040'  20-2F                    
         DC    XL16'40404040404040404040404040404040'  30-3F                    
         DC    XL16'404142434445464748494A4B4C4D4E4F'  40-4F                    
         DC    XL16'505152535455565758595A5B5C5D5E5F'  50-5F                    
         DC    XL16'606162636465666768696A6B6C6D6E6F'  60-6F                    
         DC    XL16'707172737475767778797A7B7C7D7E7F'  70-7F                    
         DC    XL16'808182838485868788898A8B8C8D8E8F'  80-8F                    
         DC    XL16'909192939495969798999A9B9C9D9E9F'  90-9F                    
         DC    XL16'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'  A0-AF                    
         DC    XL16'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'  B0-BF                    
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'  C0-CF                    
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'  D0-DF                    
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'  E0-EF                    
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'  F0-FF                    
         EJECT                                                                  
***********************************************************************         
* SUBROUTINE TO BUILD MINI WHOAMI DATA IN FIRST PROTECTED HEADER FIELD*         
***********************************************************************         
WHODAT   NTR1  BASE=*,LABEL=*                                                   
         MVC   0(8,R3),=X'442000010000803C' STANDARD FIELD HDR                  
         LA    R3,8(R3)                                                         
         USING WHODATD,R3                                                       
         L     R1,SAVEAPL                                                       
         L     R4,8(R1)                                                         
         USING UTLD,R4                                                          
         XC    WDDATA,WDDATA                                                    
*                                                                               
WD1      MVC   WDMID,=CL8'XD/0001 '                                             
*                                                                               
WD2      MVC   WDPID(2),TAGYPER    AGENCY ALPHA ID FOR PERSON ID                
         XC    HALF,HALF                                                        
         OC    TPERSON,TPERSON     USE PERSON NUM OR PASSWORD NUM               
         BZ    *+14                                                             
         MVC   HALF,TPERSON                                                     
         B     WD2A                                                             
         OC    TPASSWD,TPASSWD                                                  
         BZ    WD2A                                                             
         MVC   HALF,TPASSWD                                                     
WD2A     GOTO1 =V(HEXOUT),DMCB,HALF,WDPID+2,2,(24,0)                            
*                                                                               
WD3      MVC   WDPEXP,=CL60' '     PASSWORD EXPIRATION                          
         CLI   TPASSEXP,0          TEST IF PASSWORD EXPIRATION DEFINED          
         BE    WD3X                                                             
         LLC   R0,TPASSEXP                                                      
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WDPEXP(2),DUB                                                    
         TM    TSTAT7,TST7PSWN     TEST IF WITHIN WARNING PERIOD                
         BZ    *+8                                                              
         MVI   WDPEXP+2,C'W'                                                    
WD3X     EQU   *                                                                
*                                                                               
WD4      L     RF,=V(SSB)          EXTRACT MAX SESSION INFO FROM SSB            
         MVC   FULL+0(2),SSBSSMAX-SSBD(RF)                                      
         MVC   FULL+2(2),SSBSSMXP-SSBD(RF)                                      
         TM    TSTATC,TSTCXSES     TEST IF TRM SUPPORTS EXTRA SESSIONS          
         BO    WD4A                                                             
         LHI   R0,4                OLD CMV SUPPORTS 4 SESSIONS                  
         LHI   R1,4                                                             
         CLC   FULL+2(2),FULL+0                                                 
         BNH   *+8                                                              
         LHI   R1,5                PLUS EXTRA HIDDEN SESSION                    
         STH   R0,FULL                                                          
         STH   R1,FULL+2                                                        
WD4A     LH    R0,FULL                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WDPSES(2),DUB                                                    
         MVI   WDPSES+2,C' '                                                    
         CLC   FULL+2(2),FULL+0                                                 
         BNH   *+8                                                              
         MVI   WDPSES+2,C'+'        EXTRA PHYSICAL SESSIONS                     
*                                                                               
WDX      XIT1                                                                   
         LTORG                                                                  
*                                                                               
WHODATD  DSECT                                                                  
WDDATA   DS    0CL60               WHOAMI DATA IN TWA MESSAGE FIELD             
WDMID    DS    CL8                 MESSAGE ID                                   
WDPID    DS    CL6                 PID ID                                       
WDPEXP   DS    CL3                 PASSWORD EXPIRY DAYS AND WARNING             
WDPSES   DS    CL3                 SESSIONS                                     
WDPSOX   DS    CL19                SOX FIELDS                                   
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE - FIRST FIELDS DEFINED BY FATIOB                    *         
***********************************************************************         
*FATIOB                                                                         
       ++INCLUDE FATIOB                                                         
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
DMCB     DS    6F                                                               
SAVEAPL  DS    F                                                                
SAVER1   DS    F                                                                
SAVER2   DS    F                                                                
SAVERE   DS    F                                                                
OUTXLAT  DS    A                                                                
ATWA     DS    A                                                                
AFSTFH   DS    A                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
*                                                                               
HALF     DS    H                                                                
LTEND    DS    H                                                                
FSTART   DS    H                   FIELD START 3270 ADR (ATB CHR)               
FSTRABS  DS    H                   FIELD START ABSOLUTE ADR (1ST CHR)           
FEND     DS    H                                                                
FUNP     DS    H                                                                
FUNPATB  DS    C                                                                
FUNPATBX DS    C                                                                
CURSES   DS    C                                                                
LTATB    DS    C                                                                
LTATBX   DS    X                                                                
LTNATB   DS    C                                                                
LTNATBX  DS    X                                                                
FATB     DS    C                                                                
FATBX    DS    X                                                                
FATBXX   DS    XL3                                                              
FATBXL   DS    X                                                                
FATBXV   DS    CL7                                                              
LTFLAG   DS    C                                                                
LTPDEL   DS    C                   DELIMIT END OF PROTECTED FIELD FLAG          
DELFLAG  DS    C                   DELIMITER OUTPUT TO BUFFER FLAG              
FSTFLAG  DS    X                   FIRST FIELD FLAG                             
FSTATBX  DS    X                   FIRST FIELD XATTB SAVE                       
CTRY     DS    X                                                                
LANG     DS    X                                                                
XLAT     DS    X                                                                
SYS      DS    X                                                                
XTND     DS    X                                                                
NONO     DS    X                                                                
STAT6    DS    X                                                                
SVFSTFH  DS    XL8                                                              
SVFSTF   DS    CL60                                                             
XCOLMASK DS    XL8                                                              
FDATA    DS    CL255                                                            
*                                                                               
PTBUFF   DS    (BUFFLEN*2)X                                                     
PTBUFFL  EQU   *-PTBUFF                                                         
WORKL    EQU   *-TIOBD                                                          
                                                                                
*DDFLDHDR                                                                       
       ++INCLUDE DDFLDHDR                                                       
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004FATO3270  08/13/14'                                      
         END                                                                    
