*          DATA SET SPMEDGETBY AT LEVEL 102 AS OF 03/04/21                      
*CATALP MEDGETBY                                                                
*                                                                               
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-52654  03/01/21 2-DECIMAL RADIO RATINGS FOR CANADA        *         
***********************************************************************         
*=====================================================================          
* NOTE ABOUT 'NORMALIZED' DEMOS  NOV/16 MHER                                    
* IN AN EFFORT TO SIMPLIFY WHAT WAS BECOMING A NIGHTMARE                        
*                                                                               
* 0D29 POINTERS HAVE CATEGORY/SEQNUM                                            
* 0D2A POINTERS HAVE SEQNUM/CATEGORY                                            
* WHEN A PROGRAM TURNS ON THE 'NORMALIZED' FLAG IN RQOPT2, IT MEANS             
* THAT THE 00NN00 FORMAT FOR NONTRADITIONAL DEMOS HAS BEEN CHANGED              
* IN THE BUY RECORD DEMO ELEMENTS                                               
* FOR EACH DEMO THE FORM IS MOD_NN_NN.E.G FOR COMSCORE DEMO NUMBER 500          
* THE RATING WOULD LOOK LIKE R500 AND THE IMPS X500 -- SO IT HAS                
* 1 BYTE MODIFIER, 2 BYTE DEMO NUMBER                                           
*       <<<========================================                             
* NOTE- YOU CANNOT CALL SPGETDEME FOR A RERATE WITH NORMALIZED DEMOS!           
*       <<<========================================                             
*======================================================================         
*                                                                               
*================================================================               
* NOTE THAT THERE IS A PROBLEM CALLING SUBROUTINES IN THIS PROGRAM              
* IT SAVES THE CALLING PROGRAM'S R1 AND REFERENCES THE PARAMETERS               
* PASSED. MANY PROGRAMS POINT THAT R1 TO DMCB.                                  
* IF YOU USE DMCB IN HERE, YOU OVERWRITE THE CALLERS PARAMS, AND                
* TYPICALLY DIE IN A CALL TO DATCON WITH NO BOOK.                               
* MHER 30NOV04                                                                  
* GOT ME AGAIN MHER 20JAN05                                                     
*================================================================               
*                                                                               
         TITLE 'SPMEDGETBUY - EXTRACT BUY DATA'                                 
*========================   CHANGE LOG   =======================*               
*  DATE      LEVEL  REASON                                      *               
*  ----      -----  ------                                      *               
*  NOV24/97   1-3   ALLOW RERATES WITH UPGRADES                 *               
*  07JAN/98         FIX BUGS WITH -S AND BKTYPES ON UPGRADES    *               
*  07MAY/98         SUPPORT FOR BOOK TYPE UPGRADES              *               
*  06OCT/98         REPORT COSTS AT NET OR BF CONSISTENTLY      *               
*  22JUN/99         IF Q2TRADE SET, SET BDPURP=X'FE' FOR GETRATE*               
*  20SEP/99   MHER  IF SNTISTA>SPACES, SET FLAG FOR GETDEMO     *               
*  10OCT/03   MHER  SUPPORT SOFT CANADIAN DEMOS                 *               
*  06JAN/04   MHER  SUPPORT 2 DECIMAL DEMO RATINGS              *               
*  09AUG/04   MHER  SUPPORT LPM START DATE FOR UPGRADES         *               
*                                                                               
*  24JAN05 THERE ARE SOME PROGRAMS (SPMG02) THAT NEED TO SET THE*               
*         2-DECIMAL DEMO OPTION, BUT DID NOT CALL THIS ROUTINE. *               
*         IF THE FIRST BYTE OF THE BUY RECORD PASSED IS X'00'   *               
*         THIS PROGRAM WILL SET THE DECIMAL OPTION AND EXIT     *               
*  21DEC05    MHER   REMOVE STATION/MARKET READS FOR CANADIAN   *               
*                    DEMOS. LOOKUP DATA SHOULD BE IN BUYREC     *               
*  13JUN08    AKAT  FIX 2 DECIMAL DEMOS BUG FOR OVERNIGHT REQST *               
*                   IF WE HAVE A MEDIA R REQUEST FOLLOWED BY A  *               
*                   MEDIA T REQUEST, THE 2 DECIMAL DEMOS FLAG   *               
*                   IS TURNED OFF FOR THE MEDIA R REQUEST AND   *               
*                   NEVER GETS TURNED ON FOR THE MEDIA T REQUEST*               
*                   BECAUSE WE DON'T RE-READ THE PROFILE KEY!   *               
*  04MAR/11   AKAT  ALWAYS REPORT COST2 IF REQUESTED!           *               
*================================================================               
*                                                                               
* PARAMETERS                       REGISTER USAGE                               
*  1 = A(WORKC)                    RA/RC  A(WORKC)                              
*  2 IS A FULLWORD CONTAINING      R8  EXTRACT WORK                             
*      0=NO DEMOS                  R2  MEDBLOCK                                 
*      1=ESTIMATED                 R3  BUY RECORD                               
*      2=ESTIMATED ADJ.            R4  VARIABLE DSECTS                          
*      3=RERATED                   R5-R7 - OPEN                                 
*      4=RERATED ADJ.                                                           
*      5=ESTIMATED ADJUSTED                                                     
*      6=AFFID                                                                  
*      7=AFFID ADJ.                                                             
*================================================================               
         EJECT                                                                  
VMDGETBY CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 DELTABX-DELTAB,**GTBY**                                          
         LA    R9,2048(RB)                                                      
         LA    R9,2048(R9)                                                      
         LA    R8,2048(R9)                                                      
         LA    R8,2048(R8)                                                      
         USING VMDGETBY+4096,R9,R8                                              
*                                                                               
         LR    R3,RC                                                            
         USING DELTABD,R3                                                       
*                                                                               
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)         SET UP RC ADDRESS                            
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         ST    R1,SAVER1                                                        
                                                                                
* TEST FOR CHANGE OF 00 PROFILE KEY AND READ PROFILE IF NEEDED                  
* READ AGENCY LEVEL PROFILE AND NO-USERID PROFILES                              
                                                                                
         LA    R4,PSLIST           USE PSLIST AS WORK AREA                      
         XC    0(64,R4),0(R4)                                                   
         MVC   0(4,R4),=C'S000'                                                 
         MVC   4(2,R4),QAGY                                                     
         CLC   SV00KEY(6),0(R4)    TEST CHANGE OF PROFILE KEY                   
         BNE   SETDEC1             YES - READ PROFILE AGAIN                     
         CLC   QMED,SV00KEY+6      SAME MEDIA AS LAST REQ?                      
         BE    SETDEC2             YES - OPTION IS OK THEN                      
*                                                                               
SETDEC1  MVC   SV00KEY,0(R4)       ELSE SAVE NEW KEY AND GET PROFILE            
         MVC   SV00KEY+6(1),QMED   SAVE OFF THE MEDIA!                          
         GOTO1 GETPROF,MBPARAM,(X'90',(R4)),32(R4),DATAMGR                      
*                                                                               
         NI    RQOPTS,X'FF'-RQOPTS_2DEC                                         
         CLI   32+9(R4),C'Y'      TEST 2-DECIMALS ACTIVE                        
         BNE   SETDEC1A                                                         
**AKAT** BNE   SETDEC2                                                          
         OI    RQOPTS,RQOPTS_2DEC                                               
*                                                                               
SETDEC1A MVC   0(4,R4),=C'S00A'    NOTE! DOES NOT USE SV00KEY!!!                
         NI    0(R4),X'BF'         MAKE S LOWERCASE                             
         MVI   6(R4),0             DO NOT READ MEDIA LEVEL                      
         GOTO1 GETPROF,MBPARAM,(X'90',(R4)),32(R4),DATAMGR                      
                                                                                
* IF YOU ASK FOR 2-DEC IMPS, YOU ALSO GET 2-DEC RATINGS *                       
                                                                                
         CLI   32+6(R4),C'Y'      TEST 2-DEC IMPS ACTIVE                        
         BNE   SETDEC2                                                          
         CLI   QMED,C'R'          BUT NEVER FOR RADIO                           
         BE    SETDEC2                                                          
         OI    RQOPTS,RQOPTS_2DEC+RQOPTS_2DECIMP                                
                                                                                
SETDEC2  TM    RQOPTS,RQOPTS_1DEC   TEST PROGRAM FORCING 1-DEC                  
         BO    SETDEC6                                                          
         CLI   QMED,C'R'                                                        
         BNE   SETDECX                                                          
*                                                                               
         L     RF,ADAGY            A(AGENCY RECORD)                             
         USING AGYHDRD,RF          AGENCY RECORD DSECT                          
         CLI   AGYPROF+7,C'C'      CANADIAN AGENCY?                             
         BE    SETDECX             YES - THEY GET 2 DEC RADIO RATINGS           
         DROP  RF                  DROP AGENCY RECORD USING                     
*                                                                               
SETDEC6  NI    RQOPTS,X'FF'-RQOPTS_2DEC-RQOPTS_2DECIMP  TURN OFF 2 DEC          
*                                                                               
SETDECX  L     R1,=V(SPXCHBLK)                                                  
         ST    R1,VXCHBLK                                                       
         L     R2,MEDBUFF                                                       
         USING MEDBLOCK,R2                                                      
         MVI   GBHOOK,C'Y'         SET FOR SPOT HOOK                            
*                                                                               
         ST    R3,NXTDEL           SAVE AREA FOR DELETED ELEMENTS               
         ST    R3,FRSTDEL                                                       
         L     RE,NXTDEL                                                        
         LHI   RF,DELTABX-DELTAB                                                
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         DROP  R3                                                               
*                                                                               
         L     R3,ADBUY                                                         
         USING BUYREC,R3                                                        
         USING MEDGLD,R4                                                        
*                                                                               
         CLI   0(R3),0             TEST NO BUY RECORD PRESENT                   
         BE    EXITX                                                            
*                                                                               
         CLI   UNWOPTS,0           TEST ANY UNWIND OPTIONS SPECIFIED            
         BZ    *+8                                                              
         BRAS  RE,SETUNWD                                                       
         EJECT                                                                  
* FOR WESTERN CONVERSION TO COS2, MODIFY PW FLAGS                               
         CLI   MEDEXTPW,C'Y'       TEST EXTRACT PW                              
         BNE   CHKMMRX             NO                                           
***      MVI   RQCOST2,C'N'        RQCOST2 MAY BE "Y"-DON'T SET TO "N"          
         L     RE,ADEST                                                         
         USING ESTHDRD,RE                                                       
         OC    ECOST2,ECOST2       TEST COST2 ESTIMATE                          
         BZ    CHKMMRX             NO                                           
         MVI   RQCOST2,C'Y'        FORCE COST2 VALUES                           
CHKMMRX  DS    0H                                                               
         DROP  RE                                                               
*                                                                               
PB1      XC    MBPRTNR,MBPRTNR                                                  
         XC    PNAME,PNAME                                                      
         CLI   BDTIME,0            TEST P/B                                     
         BE    PBX                 NO                                           
         LA    R5,BDELEM                                                        
PB2      LLC   R0,1(R5)                                                         
         AR    R5,R0                                                            
         CLI   0(R5),0                                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   0(R5),4                                                          
         BNE   PB2                                                              
         CLC   2(1,R5),MEDBRAND                                                 
         BNE   *+10                                                             
         MVC   MBPRTNR,=H'2'       SET DSP TO BILL DATE                         
PBX      DS    0H                                                               
         XC    SPILL,SPILL                                                      
         MVI   MEDSPILL,C'N'                                                    
         CLI   SVBUYKEY,X'10'      BUY KEY                                      
         BNH   NOSPILL              NO - DONT CHECK FOR SPILL                   
         LA    RF,SVBUYKEY+4       MARKET ON NORMAL POINTER                     
         TM    SVBUYKEY,X'0C'                                                   
         BNO   *+8                                                              
         LA    RF,SVBUYKEY+6       MARKET ON ID POINTER                         
         CLC   4(2,R3),0(RF)       TEST VS BUYREC MARKET                        
         BE    NOSPILL                                                          
         MVI   MEDSPILL,C'Y'                                                    
         MVC   SPILL,0(RF)                                                      
*                                                                               
NOSPILL  MVC   SVMDLAST,MEDALAST                                                
         MVC   SVMDFRST,MEDAFRST                                                
         MVC   SVBOOK,QBOOK1                                                    
*                                                                               
         L     R1,SAVER1                                                        
         MVI   MBGETDEM,1                                                       
         CLI   7(R1),5             FORCE ADJ TO PURCH. ADJ                      
         BNE   *+8                                                              
         MVI   7(R1),2                                                          
         MVC   MBDEMTYP,7(R1)                                                   
         MVC   NODEM,MEDEXTDM                                                   
         CLI   MBDEMTYP,0                                                       
         BNE   *+8                                                              
         MVI   NODEM,0                                                          
*                                                                               
         LA    R5,MBDELTAB                                                      
         ST    R5,MEDADEMO                                                      
         L     R5,MEDAFRST                                                      
         LA    R6,MEDTOTAL                                                      
*                                                                               
CLRBUFF  ICM   RE,15,4(R5)         CLEAR TABLE SLOTS                            
         BZ    CLRBUFF2                                                         
         L     RF,MEDLCHNK                                                      
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
CLRBUFF2 LA    R5,12(R5)                                                        
         CR    R5,R6                                                            
         BNH   CLRBUFF                                                          
*                                                                               
CLRBUFF3 CLI   MEDEXTAC,C'Y'                                                    
         BNE   CLRBUFF4                                                         
         CLI   MEDSPILL,C'Y'                                                    
         BE    EXIT                                                             
*                                                                               
CLRBUFF4 CLI   MEDEXMMR,C'Y'                                                    
         MVC   SVAFRST,MEDAFRST                                                 
         MVC   SVALAST,MEDALAST                                                 
*                                                                               
         XC    WORK,WORK           SET UP 1W PROFILE KEY                        
         MVC   WORK(4),=C'S0D0'                                                 
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),QCLT                                                   
*                                                                               
         L     RF,ADCLT            CHECK IF CLIENT HAS OFFICE                   
         USING CLTHDR,RF                                                        
         CLI   COFFICE,X'41'                                                    
         BL    *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  RF                                                               
*                                                                               
         LA    RE,SVD0PROF                                                      
         MVC   15(1,RE),Q2USER+2   SAVE WTP POSTING FLAG                        
         CLC   SVD0KEY(12),WORK    BYPASS READ IF SAME KEY AS BEFORE            
         BE    INITD0X                                                          
*                                                                               
         MVC   SVD0KEY(12),WORK                                                 
         GOTO1 GETPROF,MBPARAM,WORK,SVD0PROF,DATAMGR                            
         LA    RE,SVD0PROF         GET ADDRESS OF D0 PROFILE                    
         ST    RE,ASVD0            MAKE PROFILE AVAILABLE TO REPORTS            
         MVC   15(1,RE),Q2USER+2   SAVE WTP POSTING FLAG                        
*                                                                               
         XC    WORK,WORK           SET UP 1W PROFILE KEY                        
         MVC   WORK(4),=C'S01W'                                                 
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),QCLT                                                   
         L     RF,ADCLT            CHECK IF CLIENT HAS OFFICE                   
         USING CLTHDR,RF                                                        
         CLI   COFFICE,X'41'                                                    
         BL    *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  RF                                                               
*                                                                               
         MVC   SV1WKEY(12),WORK                                                 
         GOTO1 GETPROF,MBPARAM,WORK,SV1WPROF,DATAMGR                            
         LA    RE,SV1WPROF         GET ADDRESS OF 1W PROFILE                    
         ST    RE,ASV1W            MAKE PROFILE AVAILABLE TO REPORTS            
         EJECT                                                                  
INITD0X  DS    0H                                                               
         MVI   MYMEDFLG,C'N'       SET MY MEDBUFF NOT IN USE                    
         CLI   MBDEMTYP,3          RERATE                                       
         BL    DTESET               NO - DATES ARE ALWAYS OK                    
         CLC   QBOOK1(3),=C'LAT'                                                
         BE    *+10                                                             
         CLC   QBOOK1(4),=C'LAST'                                               
         BNE   *+10                                                             
         MVC   QBOOK1(4),=C'0000'                                               
*                                                                               
         CLC   QBOOK1(3),=C'ACT'   ACTUAL BOOK                                  
         BNE   DTESET               NO - DATES ARE STILL GOOD                   
                                                                                
*============================================================                   
* LOOK AT DEMO FILE AND PROFILE TO DETERMINE                                    
* IF DATE EXPANSION IS REQUIRED WHEN WE                                         
* ARE DOING ACTUAL BOOK                                                         
*============================================================                   
                                                                                
         L     R5,MEDAFRST         GET START AND END DATES                      
         MVC   MBSTART,0(R5)                                                    
         L     R5,MEDALAST                                                      
         LA    R5,2(R5)                                                         
         MVC   MBEND,0(R5)                                                      
*                                                                               
         CLI   MEDNYBEF,C'Y'       CHECK BEFORE                                 
         BNE   *+14                                                             
         LA    R5,MEDBFORE                                                      
         MVC   MBSTART,0(R5)                                                    
*                                                                               
         CLI   MEDNYAFT,C'Y'       CHECK AFTER                                  
         BNE   *+14                                                             
         LA    R5,MEDAFTER                                                      
         MVC   MBEND,2(R5)                                                      
*                                                                               
* SET THE SPILL MARKET PARAMETER IF REQUIRED                                    
*                                                                               
         XC    MBPARAM+12(8),MBPARAM+12  INITIALIZE                             
         OC    SPILL,SPILL         ANY SPILL                                    
         BZ    ACTSPILX            NO - ONWARD                                  
         LA    RF,BDELEM           FIND THE SPILL DEMO ELEMENT                  
         USING NDELEM,RF                                                        
*                                                                               
ACTSPIL  CLI   0(RF),0             EOR                                          
         BE    ACTSPILX                                                         
         CLI   NDCODE,3            SPILL DEMO ELEMENT                           
         BNE   ACTSPIL1                                                         
         CLC   SPILL,4(RF)         CHECK THE SPILL NUMBER                       
         BNE   ACTSPIL1                                                         
         MVC   MBPARAM+12(4),=C'SPIL'                                           
         XC    MBPARAM+16(4),MBPARAM+16                                         
         MVC   MBPARAM+18(2),6(RF)                                              
         B     ACTSPILX                                                         
*                                                                               
ACTSPIL1 SR    RE,RE               GET NEXT ELEMENT                             
         IC    RE,1(RF)                                                         
         AR    RF,RE                                                            
         B     ACTSPIL                                                          
*                                                                               
ACTSPILX DS    0H                                                               
         DROP  RF                                                               
*============================================================                   
* BUILD A LIST OF DATES AND BOOKS TO BE USED                                    
* PASS ENOUGH INFO FOR SPILL MARKETS                                            
*============================================================                   
                                                                                
         L     RF,ADBLOCK                                                       
         USING DBLOCK,RF                                                        
         MVC   DBSELMK,SPILL                                                    
         MVC   DBSELSTA,STA                                                     
         DROP  RF                                                               
*                                                                               
         LLC   R7,SVD0PROF+2                                                    
         GOTO1 =V(VMDACTBK),MBPARAM,((R7),(RA)),MBSTART,A(ACTAREA)              
*                                                                               
         L     R1,SAVER1                                                        
         L     RF,=A(ACTAREA)                                                   
         ST    RF,4(R1)            RETURN A(ACTAREA) TO CALLING PGM             
         BRAS  RE,SETDATES                                                      
                                                                                
* GET MEDBLOCK KEY INFORMATION                                                  
                                                                                
DTESET   CLI   MBDEMTYP,0                                                       
         BE    NOEQU                                                            
         CLI   MEDSPTLN,0          SPOT LENGTH = 0                              
         BE    NOSLN               YES - USE BDSEC FOR EQUIVALENCING            
         GOTO1 MEDEQUIV,MBPARAM,(RA)                                            
         MVC   MBEQU,MEDEQFAC                                                   
         B     NOEQU                                                            
         CLI   MBDEMTYP,0                                                       
         BE    SETDEMX                                                          
*                                                                               
NOSLN    DS    0H                                                               
         MVC   MEDSPTLN,BDSEC                                                   
         GOTO1 MEDEQUIV,MBPARAM,(RA)                                            
         MVC   MBEQU,MEDEQFAC                                                   
         MVI   MEDSPTLN,0                                                       
*                                                                               
NOEQU    MVC   MEDPURPS,BDPURP                                                  
         MVC   MEDPRGTP,BDPROGT                                                 
         L     R4,ADSTAT                                                        
         USING STARECD,R4                                                       
         MVC   MEDAFFIL,SNETWRK                                                 
         DROP  R4                                                               
*                                                                               
* SET PRIMARY DEMO AND BUILD DEMO LOOKUP PARAMETERS                             
*                                                                               
         L     RF,PRDBUFF          FIND PRODUCT BUFFER SLOT                     
         SR    RE,RE                                                            
         IC    RE,MEDBRAND                                                      
         CLI   MEDBRAND,X'FF'          FIX POL PRODUCT                          
         BNE   *+8                                                              
         LA    RE,220                                                           
         BCTR  RE,0                                                             
         MH    RE,PRDBUFLN                                                      
         AR    RF,RE                                                            
         MVC   MBDEM,DEMDISP(RF)   SET DEMOS FOR THIS PRODUCT                   
         MVI   MEDPRIMY,0                                                       
                                                                                
* NEED TO SET FLAGS FOR COM/NSI DEMOS IN REQ                                    
                                                                                
         LA    R1,MBDEM                                                         
         LA    R0,20                                                            
*                                                                               
         MVI   NSIREQ,C'N'                                                      
         MVI   COMREQ,C'N'                                                      
*                                                                               
SETDEM2  OC    0(3,R1),0(R1)                                                    
         JZ    SETDEM10                                                         
*                                                                               
         LA    RE,NSIREQ                                                        
         CLI   2(R1),0             TEST COMSCORE DEMO                           
         JNE   *+8                 NO                                           
         LA    RE,COMREQ                                                        
         MVI   0(RE),C'Y'                                                       
*                                                                               
         LA    R1,3(R1)                                                         
         JCT   R0,SETDEM2                                                       
         EJECT                                                                  
SETDEM10 CLI   MEDEXTAV,C'Y'       PERIOD AVERAGES REQUIRED                     
         BNE   SETDEM14                                                         
         CLI   MBDEMTYP,2                                                       
         BH    SETDEM12                                                         
         XC    MEDVPEST(56),MEDVPEST                                            
         XC    MEDVAEST(56),MEDVAEST                                            
         B     SETDEM14                                                         
*                                                                               
SETDEM12 XC    MEDVPACH(56),MEDVPACH                                            
         XC    MEDVAACH(56),MEDVAACH                                            
*                                                                               
SETDEM14 DS    0H                                                               
         XC    MBDELTAB(60),MBDELTAB                                            
         XC    MBDELAD,MBDELAD                                                  
                                                                                
*===========================================================                    
* CALL SPDEMEXT TO EXTRACT THE EXISTING DEMO VALUES                             
* DEMEXT WILL USE VPOL50EL FOR OFFLINE 50 ELEMENTS                              
*===========================================================                    
                                                                                
SETDEM22 LA    R6,BDELEM                                                        
         USING NDELEM,R6                                                        
*                                                                               
SETDEM24 LLC   R0,1(R6)            GET NEXT ELEMENT                             
         AR    R6,R0                                                            
*                                                                               
SETDEM26 CLI   0(R6),0             END                                          
         JE    SETDEMX                                                          
*                                                                               
         OC    SPILL,SPILL                                                      
         JNZ   *+12                                                             
         CLI   0(R6),2                                                          
         JE    SETDEM30                                                         
*                                                                               
         CLI   0(R6),3             SPILL ELEMENT                                
         JNE   SETDEM24                                                         
         CLC   SPILL,4(R6)          IS IT FOR THIS MARKET                       
         JNE   SETDEM24                                                         
*                                                                               
SETDEM30 LARL  R1,DEMXTAB                                                       
         XC    0(DEMXTABX-DEMXTAB,R1),0(R1)                                     
         USING DEMEXTD,R1                                                       
*                                                                               
         ST    R6,DXDEMEL          SAVE DEMEL ADDRESS                           
*                                                                               
         LA    R0,MBDEM                                                         
         ST    R0,DXESTDEM                                                      
*                                                                               
         L     R0,VPOL50EL         GET DEMO LIST ADDRESS                        
         AHI   R0,2                POINT BEYOND CODE/LEN                        
         ST    R0,DXNTNMS          SET NONT DEMO NAMES LIST                     
*                                                                               
         L     RE,ADEST                                                         
         USING ESTHDRD,RE                                                       
         LA    R0,EUSRNMS                                                       
         ST    R0,DXUSRNMS                                                      
         DROP  RE                                                               
*                                                                               
         GOTO1 VDEMEXT,(R1)        SPDEMEXT T00AC3                              
*                                                                               
         ICM   RF,15,DXDEMTAB       GET A(DEMO TABLE ON RETURN)                 
         JZ    SETDEMX                                                          
         USING DXDEMTABD,RF                                                     
         LLC   R0,DXDEMCNT                                                      
         LTR   R0,R0                                                            
         JZ    SETDEMX                                                          
                                                                                
*=================================================                              
* MOVE DEMO VALUES FOR 02/03 DEMEL                                              
*=================================================                              
                                                                                
         L     RE,DXDEMEL                                                       
         MVC   ORIGELEM(24),0(RE)  MOVE CD/LEN/PRGM/BOOK/ETC                    
*                                                                               
         SLL   R0,3                X 8                                          
         AHI   R0,24               +24                                          
         STC   R0,ORIGELEM+1       AND SET ELEM LEN                             
         MVC   NEWELEM(24),ORIGELEM  THEN COPY TO NEWELEM                       
*                                                                               
         LA    R6,ORIGELEM+24      POINT TO FIRST DEMO SLOT                     
         LA    R7,NEWELEM+24                                                    
         L     R4,DXDEMTAB                                                      
                                                                                
*=================================================================              
* MOVE DEMO VALUES FROM TABLE TO NEW DEMO ELEMENTS                              
* R4 POINTS TO DEMXTAB                                                          
* R6 POINTS TO FIRST DEMO IN NEW 02/03 ELEM                                     
*=================================================================              
                                                                                
SETDEM32 MVC   0(3,R6),DXDEMCD     TABLE DEMO CODE                              
         L     R0,DXDEMVAL         GET VALUE FROM TABLE                         
         SR    R1,R1                                                            
         ICM   R1,8,DXDEMFLG                                                    
         N     R1,=X'C0000000'     DROP ALL BUT OVRD/2 DEC                      
         OR    R0,R1                                                            
         STCM  R0,15,4(R6)         SET VALUE IN ELEMENT                         
         MVI   3(R6),100           SET SVI                                      
*                                                                               
         MVC   0(8,R7),0(R6)       COPY ORIGELEM TO NEWELEM                     
         NI    4(R7),X'7F'         DROP OVERIDE FLAG                            
*                                                                               
         LA    R6,8(R6)                                                         
         LA    R7,8(R7)                                                         
*                                                                               
SETDEM36 LA    RF,DXDEMTABL(RF)                                                 
         OC    0(3,RF),0(RF)       TEST MORE DEMOS IN TABLE                     
         JNZ   SETDEM32                                                         
*                                                                               
         LA    R6,ORIGELEM                                                      
         ST    R6,MBDELTAB                                                      
         LA    R6,NEWELEM                                                       
         ST    R6,MBDELTAB+4                                                    
         MVC   SAVELEM,NEWELEM                                                  
         EJECT                                                                  
*==============================================================                 
* FOUND A DEMO ELEMENT                                                          
* MBDELTAB CONTAINS - A(OLD ELEMENT PROPERLY SEQUENCED)                         
*                     A(NEW DEMO ELEMENT)                                       
*==============================================================                 
                                                                                
         USING MEDDATA,R4                                                       
SETDEMX  L     R5,MEDAFRST                                                      
         L     R3,ADBUY                                                         
         XC    PSLIST,PSLIST                                                    
         CLI   MEDBRAND,X'FF'                                                   
         BNE   REG0                                                             
         CLI   QPGR,C' '                                                        
         BE    REG0                                                             
         GOTO1 MEDPSL,MBPARAM,(RA),PSLIST                                       
         CLI   PSLIST,0            ANY ACTIVE PRODUCTS                          
         BNE   REG0                                                             
         MVC   MEDAFRST,SVMDFRST   NO - RESTORE THE DATE POINTERS               
         MVC   MEDALAST,SVMDLAST                                                
         B     EXIT                AND GET OUT                                  
*                                                                               
REG0     DS    0H                                                               
         L     R5,MEDAFRST                                                      
         MVC   MBSTART,0(R5)                                                    
         L     R5,MEDALAST                                                      
         MVC   MBEND,2(R5)                                                      
*                                                                               
         L     R5,MEDAFRST                                                      
         LA    R6,BDELEM                                                        
         USING REGELEM,R6                                                       
*                                                                               
REG2     SR    R7,R7                                                            
         IC    R7,RLEN                                                          
         AR    R6,R7                                                            
REG4     CLI   RCODE,0                                                          
         BE    REGX                                                             
         CLI   RCODE,6                                                          
         BL    REG2                                                             
         CLI   RCODE,13                                                         
         BH    REG2                                                             
*                                                                               
         MVC   MBHALF,RDATE                                                     
         CLI   MBDEMTYP,6                                                       
         BNE   REG4A                                                            
         LLC   RF,1(R6)                                                         
         AR    RF,R6                                                            
         CLI   0(RF),X'10'                                                      
         BNE   REG4A                                                            
         MVC   MBHALF,2(R6)                                                     
*                                                                               
REG4A    OC    BDMGDATE,BDMGDATE   TEST BUY IS A MAKEGOOD                       
         BZ    REG6                                                             
         CLI   RQMGOPT,C'Y'        TEST PROG SUPPORTS MG ON MSSD DATE           
         BNE   REG6                                                             
         L     RF,ADCLT                                                         
         USING CLTHDR,RF                                                        
         CLI   CEXTRA+7,C'Y'       TEST OPTION FOR MG AS MISSED                 
         BNE   REG6                                                             
         CLC   MBCLTKEY,0(RF)      TEST SAME A-M/CLT                            
         BE    REG5                                                             
         MVC   MBCLTKEY,0(RF)      SAVE A-M/CLT                                 
         XC    MBCLTDAT,MBCLTDAT                                                
         SPACE 1                                                                
* MUST READ A0 PROFILE FOR FEATURE START DATE *                                 
         SPACE 1                                                                
         XC    WORK1(16),WORK1     USES WORK2 TOO                               
         MVC   WORK1(4),=C'S0A0'                                                
         MVC   WORK1+4(2),AGENCY                                                
         MVC   WORK1+6(1),QMED                                                  
         MVC   WORK1+7(3),CLT                                                   
         MVI   WORK1+10,C'*'                                                    
         MVC   WORK1+11(1),COFFICE                                              
         DROP  RF                                                               
         GOTO1 GETPROF,MBPARAM,WORK1,DEMWORK,DATAMGR                            
         CLI   8(R1),0                                                          
         BNE   REG6                                                             
         GOTO1 DATCON,(R1),(3,DEMWORK+7),(2,MBCLTDAT)                           
*                                                                               
REG5     CLC   BDMGDATE,MBCLTDAT   TEST MSSD DATE PRIOR TO START                
         BL    REG6                YES - IGNORE                                 
*                                                                               
         MVC   MBHALF,BDMGDATE     SET MISSED DATE FOR THIS SPOT                
         B     REG6                                                             
*                                                                               
MBCLTKEY DS    XL4                                                              
MBCLTDAT DS    H                                                                
*                                                                               
REG6     CLC   MBHALF,MBSTART      BEFORE PERIOD                                
         BNL   REG7                                                             
         CLI   MEDNYBEF,C'Y'       YES - DO I WANT IT                           
         BNE   REG2                NO - NEXT ELEMENT                            
         B     REG8                                                             
*                                                                               
REG7     CLC   MBHALF,MBEND        AFTER PERIOD                                 
         BNH   REG8                                                             
         CLI   MEDNYAFT,C'Y'       YES - DO I WANT IT                           
         BNE   REG2                NO - NEXT ELEMENT                            
*                                                                               
REG8     MVI   SPOTYORN,C'Y'                                                    
*                                                                               
         CLI   PSLIST,0                                                         
         BE    REG9A                                                            
         CLI   RLEN,14             ALLOCATED?                                   
         BNL   REG8A               YES, ACCEPT                                  
         CLI   PGRUNA,C'Y'         NO, TEST TO ACCEPT ANYWAY                    
         BNE   REG2                NO, REJECT                                   
         B     REG9A               ELSE ACCEPT                                  
REG8A    DS    0H                                                               
         LA    RE,PSLIST                                                        
REG9     CLI   0(RE),0                                                          
         BE    REG2                                                             
         MVC   MBHALF,RPPRD                                                     
         CLI   Q2TRADE,C'Y'        TEST COMBINE TRADE/CASH PRD                  
         BNE   *+8                                                              
         NI    MBHALF,X'7F'        DROP HOB OF PRD                              
         CLC   MBHALF(1),0(RE)                                                  
         BE    *+12                                                             
         LA    RE,2(RE)                                                         
         B     REG9                                                             
REG9A    DS    0H                                                               
         MVC   GBBRND,MEDBRAND                                                  
         CLI   GBBRND,220          RETURN POL/UNALLOCATED                       
         BE    REG9X                                                            
         CLI   GBBRND,219          UNALLOCATED REQUESTED                        
         BNE   *+8                                                              
REG9X    MVI   GBBRND,X'FF'                                                     
         XC    ELEMPAID,ELEMPAID                                                
*                                                                               
REG10    DS    0H                                                               
         MVI   MBBYTE,0                                                         
         XC    MBPARAM+12(4),MBPARAM+12                                         
         L     R1,VXCHBLK                                                       
         USING ZXCHARD,R1                                                       
         XC    ZXC58,ZXC58                                                      
         L     RE,ADAGY                                                         
         CLI   AGYPROF+7-AGYHDR(RE),C'C' TEST CANADIAN AGY                      
         BNE   REG11                                                            
         MVI   MBBYTE,C'Z'             SET FLAG THAT AREA PRESENT               
         ST    R1,MBPARAM+12           SET ADDRESS OF EXCHANGE AREA             
         MVC   MBPARAM+12(1),RQCRRNCY  SET REQUESTED CURRENCY                   
*                                                                               
REG11    IC    R0,BDPURP           SAVE PURPOSE CODE                            
         OC    BUYKMKT,BUYKMKT     TEST MARKET 0 BUY                            
         JNZ   REG12                                                            
         TM    RCOPTS,RCOPT_INCLMK0$ TEST TO INCLUDE MKT0 $                     
         JZ    REG12                                                            
         MVI   BDPURP,X'FD'                                                     
                                                                                
REG12    CLI   NOPRDNTP,C'Y'       TEST SUPPRESS NTP BY PRODUCT                 
         BNE   *+8                                                              
         MVI   BDPURP,X'FF'        SET FLAG FOR GETRATE                         
*                                                                               
         CLI   Q2TRADE,C'Y'        TEST COMBINE CASH/TRADE                      
         BNE   *+8                                                              
         MVI   BDPURP,X'FE'                                                     
*                                                                               
         CLI   RQCOST2,C'Y'                                                     
         BNE   *+10                                                             
         MVC   MBSPOTS(4),=C'COS2'                                              
*                                                                               
         GOTO1 GETRATE,MBPARAM,(GBBRND,MBSPOTS),(MEDSPTLN,(R3)),       X        
               (MBBYTE,(R6))                                                    
         STC   R0,BDPURP           RESTORE PURPOSE CODE                         
*                                                                               
         TM    RQOPT2,RQOPT2_NETBUYS  TEST REPORT BUYS AT NET                   
         JZ    *+18                                                             
         TM    BDCIND,X'10'        TEST RATE TYPE = NET                         
         JZ    *+10                                                             
         MVC   MBGROSS,MBNET       SET GROSS = NET                              
*                                                                               
         TM    RCOPTS,RCOPT_NOCOST TEST SUPPRESS ALL COSTS                      
         BZ    *+10                                                             
         XC    MBDOLS,MBDOLS                                                    
*                                                                               
         OC    MBSPOTS(8),MBSPOTS  TEST FOR SPOTS OR DOLLARS                    
         BZ    REG2                NONE - NEXT PLEASE                           
*                                                                               
         CLC   MEDSPTLN,BDSEC      TEST SPOT IS A PIGGYBACK                     
         BE    REG12A              NO                                           
         TM    RQOPTS,RQOPTS_NOPIG TEST SUPPRESS PIGGYBACK SPLIT DOLS           
         BZ    REG12A                                                           
* CALL GETRATE AGAIN                                                            
         IC    R0,BDPURP           SAVE PURPOSE CODE                            
*                                                                               
         CLI   NOPRDNTP,C'Y'       TEST SUPPRESS NTP BY PRODUCT                 
         BNE   *+8                                                              
         MVI   BDPURP,X'FF'        SET FLAG FOR GETRATE                         
*                                                                               
         CLI   Q2TRADE,C'Y'        TEST COMBINE CASH/TRADE                      
         BNE   *+8                                                              
         MVI   BDPURP,X'FE'                                                     
*                                                                               
         MVI   0(R1),X'FF'         NO FILTER ON PRODUCT                         
         MVI   4(R1),0             OR SPOT LENGTH                               
*                                                                               
         CLI   RQCOST2,C'Y'                                                     
         BNE   *+10                                                             
         MVC   MBSPOTS(4),=C'COS2'                                              
*                                                                               
         BASR  RE,RF                                                            
         STC   R0,BDPURP           RESTORE PURPOSE CODE                         
*                                                                               
         TM    RQOPT2,RQOPT2_NETBUYS  TEST REPORT BUYS AT NET                   
         JZ    *+18                                                             
         TM    BDCIND,X'10'        TEST RATE TYPE = NET                         
         JZ    *+10                                                             
         MVC   MBGROSS,MBNET       SET GROSS = NET                              
*                                                                               
REG12A   CLI   MBBYTE,C'Z'         TEST CANADA                                  
         BNE   REG12C                                                           
         CLI   RQGSTOPT,C'Y'       TEST INCLUDE GST/PST                         
         BE    *+12                                                             
         CLI   RQGSTOPT,C'P'       TEST PST ONLY                                
         BNE   REG12C                                                           
         L     R1,VXCHBLK                                                       
         SR    RE,RE               ADD TOTAL PST TO GROSS                       
         LA    R0,10                                                            
         LA    RF,ZXPSTPROV                                                     
*                                                                               
REG12B   A     RE,8(RF)                                                         
         LA    RF,ZXPSTLEN(RF)                                                  
         BCT   R0,REG12B                                                        
*                                                                               
         LTR   RE,RE               TEST ANY PST                                 
         BZ    REG12C                                                           
         ST    RE,ZXTAX            YES-REPLACE TAX AND ADD TO GROSS             
         L     RF,ZXGROSS              AND NET                                  
         AR    RF,RE                                                            
         ST    RF,ZXGROSS                                                       
         L     RF,MBGROSS                                                       
         AR    RF,RE                                                            
         ST    RF,MBGROSS                                                       
         L     RF,ZXNET                                                         
         AR    RF,RE                                                            
         ST    RF,ZXNET                                                         
         L     RF,MBNET                                                         
         AR    RF,RE                                                            
         ST    RF,MBNET                                                         
*                                                                               
REG12C   CLI   MEDEXTPW,C'Y'       EXTRACT CLIENT PW DOLLARS                    
         BNE   REG12D                                                           
         CLI   RQCOST2,C'Y'        TEST COST2 REQUEST                           
         BE    REG12D              YES - NO PW                                  
         BRAS  RE,GETPW                                                         
*                                                                               
         CLI   RQGETBF,C'Y'                                                     
         BNE   REG12D                                                           
         CLI   MEDEXTAC,C'G'       TEST EXTRACT NET INTO GOAL                   
         BNE   REG12D                                                           
* NEED TO CALL BILLFORM FOR NET PW CLIENT                                       
         LA    R1,MBGROSS                                                       
         BAS   RE,BILLFORM                                                      
         MVC   MBNET,MBGROSS       BILLFORM RESULT IN SFMBGROSS !!!             
         B     REG12F                                                           
*                                                                               
REG12D   CLI   RQGETBF,C'X'        TEST REPORT NET DOLLARS                      
         BNE   REG12E                                                           
         MVC   MBGROSS,MBNET       MOVE NET TO GROSS                            
         CLI   MBBYTE,C'Z'                                                      
         BNE   REG12E                                                           
         L     R1,VXCHBLK                                                       
         MVC   0(4,R1),4(R1)       MOVE NET TO GROSS                            
*                                                                               
REG12E   CLI   RQGETBF,C'Y'                                                     
         BNE   REG12F                                                           
*                                                                               
         LA    R1,MBGROSS                                                       
         BAS   RE,BILLFORM                                                      
         CLI   MBBYTE,C'Z'                                                      
         BNE   REG12F                                                           
         L     R1,VXCHBLK                                                       
         BAS   RE,BILLFORM                                                      
*                                                                               
REG12F   CLI   MBBYTE,C'Z'         TEST EXCHANGE                                
         BNE   REG12G                                                           
         L     R1,VXCHBLK                                                       
         MVC   MBXC58,ZXC58        SAVE RATE FOR EQUIV                          
*                                                                               
REG12G   L     R1,VXCHBLK                                                       
         OC    ZXC58,ZXC58         TEST C58 PRESENT                             
         BZ    REG12H              NO                                           
         BAS   RE,GETC58           GET C58 + MSF                                
         CLI   RQC58OPT,C'N'       TEST INCLUDE C58 IN GROSS                    
         BE    REG12H              NO - EXCLUDE                                 
         L     R1,VXCHBLK                                                       
         L     RE,MBGROSS                                                       
         A     RE,ZXC58                                                         
         A     RE,XMSF                                                          
         ST    RE,MBGROSS                                                       
*                                                                               
REG12H   CLI   RQGSTOPT,C'Y'       TEST INCLUDE GST/PST                         
         BE    *+12                                                             
         CLI   RQGSTOPT,C'G'       TEST INCLUDE GST ONLY                        
         BNE   REG13                                                            
         L     R1,VXCHBLK                                                       
         LM    RE,RF,MBGROSS                                                    
         A     RE,ZXGSTAMT                                                      
         A     RF,ZXGSTAMT                                                      
         STM   RE,RF,MBGROSS                                                    
         DROP  R1                                                               
*                                                                               
REG13    DS    0H                                                               
         CLI   MEDBRAND,220        ACCEPT UNALLOCATED                           
         BE    REG14                YES                                         
         CLI   MEDBRAND,219        CHECK FOR UNALOCATED REQUEST                 
         BNE   REG14                                                            
         CLI   RLEN,10                                                          
         BH    REG2                BYPASS ALLOCATED SPOTS                       
REG14    DS    0H                                                               
         CLI   GBHOOK,C'Y'         SPOT HOOK PASS                               
         BNE   REG16                NO - PROCESS ELEMENT                        
         ST    R6,SPOTADDR                                                      
         L     RF,SPOTHOOK                                                      
         LTR   RF,RF                                                            
         BZ    *+10                                                             
         LA    R1,MBPARAM                                                       
         BASR  RE,RF                                                            
         CLI   SPOTYORN,C'Y'                                                    
         BE    REG16                                                            
*                                                                               
REG15    OI    RCODE,X'80'         BYPASS SPOT ON DEMO LOOKUPS                  
         L     R7,NXTDEL                                                        
         ST    R6,0(R7)                                                         
         LA    R7,4(R7)                                                         
         ST    R7,NXTDEL                                                        
         SR    R7,R7               GET AFFIDAVIT                                
         IC    R7,RLEN                                                          
         AR    R6,R7                                                            
         CLI   RCODE,X'10'         AFFID ELEMENT                                
         BE    REG15                YES - SET BYPASS                            
         B     REG4                 NO - PROCESS IT                             
*                                                                               
REG16    DS    0H                                                               
         CLI   QFILTER,C'F'        TEST FILM TYPE FILTERS                       
         BNE   REG17                                                            
*                                                                               
REG16A   LLC   RE,1(R6)                                                         
         AR    RE,R6                                                            
         CLI   0(RE),X'10'         TEST AFFID                                   
         BNE   REG16B              NO                                           
         LLC   R0,1(RE)                                                         
         AR    RE,R0               NEXT ELEMENT                                 
REG16B   CLI   0(RE),X'12'         TEST FILM                                    
         BNE   REG16D              NO                                           
                                                                                
* FILM IS PRESENT - MATCH SEQUENCE NUMBER *                                     
                                                                                
         L     RF,CMLPTR           GET LIST ADDRESS                             
         LA    RF,4(RF)            POINT TO FIRST SEQ NUM                       
REG16C   OC    0(2,RF),0(RF)       TEST E-O-L                                   
         BZ    REG15               YES - NO MATCH - SKIP SPOT                   
         CLC   0(2,RF),3(RE)       MATCH SEQ NUM                                
         BE    REG17               YES - PROCESS                                
         LA    RF,2(RF)            NEXT SEQ NUM                                 
         B     REG16C                                                           
                                                                                
* NO FILM PRESENT - TEST PROCESSING UNKNOWN TYPE (ZZZZ) *                       
                                                                                
REG16D   L     RF,CMLPTR                                                        
         CLC   0(4,RF),=C'ZZZZ'    TEST UNKNOWN TYPE                            
         BNE   REG15               NO - IGNORE SPOT (ELSE PROCESS)              
*                                                                               
REG17    MVC   MBHALF,RDATE                                                     
         CLI   0(R6),X'0B'         TEST POL ELEM                                
         BL    REG17X              NO                                           
         CLI   MBDEMTYP,6          TEST AFFID RERATE                            
         BL    REG17X                                                           
* USE AFFID DATE IF AFFID PRESENT                                               
         SR    R1,R1                                                            
         IC    R1,1(R6)                                                         
         AR    R1,R6                                                            
         CLI   0(R1),X'10'                                                      
         BNE   REG17X                                                           
         MVC   MBHALF,2(R1)                                                     
                                                                                
REG17X   OC    BDMGDATE,BDMGDATE   TEST BUY IS A MAKEGOOD                       
         BZ    REG18                                                            
         CLI   RQMGOPT,C'Y'        TEST PROG SUPPORTS MG ON MSSD DATE           
         BNE   REG18                                                            
         L     RF,ADCLT                                                         
         USING CLTHDR,RF                                                        
         CLI   CEXTRA+7,C'Y'       TEST OPTION FOR MG AS MISSED                 
         BNE   REG18                                                            
         CLC   BDMGDATE,MBCLTDAT   TEST MSSD DATE PRIOR TO START                
         BL    REG18               YES - IGNORE                                 
         MVC   MBHALF,BDMGDATE     SET MISSED DATE FOR THIS SPOT                
         DROP  RF                                                               
REG18    CLC   MBHALF,MBSTART                                                   
         BNL   REG19                                                            
         LA    R5,MEDBFORE         BUCKET IN BEFORE SLOT                        
         CLC   MBHALF,0(R5)        CHECK BEFORE LIMIT                           
         BL    REG80                                                            
         B     REG28                                                            
*                                                                               
REG19    CLC   MBHALF,MBEND                                                     
         BNH   REG20                                                            
         LA    R5,MEDAFTER         BUCKET IN AFTER SLOT                         
         CLC   MBHALF,2(R5)        CHECK AFTER LIMIT                            
         BH    REG80                                                            
         B     REG28                                                            
*                                                                               
REG20    CLC   MBHALF,0(R5)        DATE LESS THAN PREV                          
         BNL   REG22                                                            
         L     R5,MEDAFRST                                                      
         B     REG20                                                            
*                                                                               
REG22    CLC   MBHALF,2(R5)        DATE IN THIS PERIOD                          
         BNH   REG26                                                            
*                                                                               
REG24    LA    R5,12(R5)                                                        
         CLI   0(R5),0             OPEN SLOT                                    
         BE    REG24               YES - GET NEXT                               
         B     REG20                                                            
*                                                                               
REG26    CLC   MBHALF,0(R5)        FILTER ON START                              
         BL    REG2                                                             
*                                                                               
REG28    L     R4,4(R5)            GET BUFFER SLOT                              
         CLI   MEDSPILL,C'Y'                                                    
         BE    REG30                                                            
*                                                                               
REG29    L     RE,MEDBYD           DOLLARS                                      
         A     RE,MBGROSS                                                       
         ST    RE,MEDBYD                                                        
*                                                                               
         CLI   MEDEXTAC,C'G'       EXTRACT NET IN GOAL SLOTS                    
         BNE   REG30                                                            
         L     RE,MEDGLD                                                        
         A     RE,MBNET                                                         
         ST    RE,MEDGLD                                                        
*                                                                               
REG30    L     RE,MEDBYSPT                                                      
         A     RE,MBSPOTS          SPOTS                                        
         ST    RE,MEDBYSPT                                                      
*                                                                               
         MVC   SVTEND,RDATE        SAVE LAST ELEMENT DATE                       
*                                                                               
         CLI   MEDSPILL,C'Y'                                                    
         BE    REG80                                                            
         CLI   MEDEXTCS,C'R'       EXTRACT DIRECT RESPONSE                      
         BNE   REG36                NO - TRY CHILD SPOT                         
         LR    R7,R6                                                            
*                                                                               
REG34    LLC   R0,1(R7)            FIND RESPONSE ELEMENT                        
         AR    R7,R0                                                            
         CLI   0(R7),X'10'                                                      
         BL    REG36                                                            
         CLI   0(R7),X'17'                                                      
         BNE   REG34                                                            
         USING RSVPELEM,R7                                                      
         SR    RF,RF                                                            
         ICM   RF,3,RSVPNUM        ACCUMULATE RESPONSES                         
         DROP  R7                                                               
         L     R7,MEDCSPAY                                                      
         AR    R7,RF                                                            
         ST    R7,MEDCSPAY                                                      
*                                                                               
REG36    CLI   MEDEXTCS,C'Y'                                                    
         BNE   REG50                                                            
         LA    R7,MEDCSNTP         INITIALIZE FOR NTP SPOTS                     
*                                                                               
         TM    BDCIND2,X'04'       BDCIND IS A CHARACTER                        
         BZ    REG36A                                                           
         CLI   BDCIND,BDCNTP                                                    
         BE    REG36C                                                           
         B     REG36B                                                           
*                                                                               
REG36A   TM    BDCIND,X'FE'                                                     
         BZ    *+8                                                              
REG36B   LA    R7,MEDCSPAY         RESET FOR PAY SPOTS                          
*                                                                               
REG36C   L     RE,0(R7)            ACCUMULATE DOLLARS                           
         A     RE,MBGROSS                                                       
         ST    RE,0(R7)                                                         
*                                                                               
         L     RE,20(R7)           ACCUMULATE SPOTS                             
         A     RE,MBSPOTS                                                       
         ST    RE,20(R7)                                                        
*                                                                               
         L     RE,MEDCSTIM         TOTAL TIME DOLLARS                           
         A     RE,MBGROSS                                                       
         ST    RE,MEDCSTIM                                                      
*                                                                               
         OC    TALMOD,TALMOD       MODULE IN CORE                               
         BNZ   REG38                                                            
         L     R0,=A(TMODAREA)                                                  
         GOTO1 LOADER,MBPARAM,=CL8'SPM8CS',(R0)                                 
         MVC   TALMOD,MBPARAM+4                                                 
*                                                                               
REG38    SR    R0,R0                                                            
         IC    R0,GBBRND                                                        
         CLI   NOPRDNTP,C'Y'       TEST SUPRESS TAL FACTOR BY PRD               
         BNE   *+8                 NO - USE REAL BRAND CODE                     
         LA    R0,X'FF'            ELSE SET BRAND TO X'FF'                      
         GOTO1 TALMOD,MBPARAM,QEND,((R0),TALFAC)                                
         L     RE,MBGROSS                                                       
         SRDA  RE,32                                                            
         M     RE,TALFAC                                                        
         SLDA  RE,1                                                             
         D     RE,TALBASE                                                       
         LTR   RF,RF                                                            
         BM    *+8                                                              
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
         ST    RF,MBPARAM                                                       
         S     RF,GROSS                                                         
         A     RF,MEDCSTAL         ACCUMULATE TALENT DOLLARS                    
         ST    RF,MEDCSTAL                                                      
         L     RF,MBPARAM          TOTAL TIME AND TALENT                        
         A     RF,MEDCSTPT                                                      
         ST    RF,MEDCSTPT                                                      
         B     REG50                                                            
*                                                                               
REG50    CLI   MEDEXTAC,C'Y'                                                    
         BNE   REG60                                                            
         CLI   MBBYTE,C'Z'                                                      
         BNE   *+8                                                              
         BAS   RE,EXCHANGE                                                      
*                                                                               
         LA    R7,MEDBYPAY         ACCUMULATE PAID AMOUNTS                      
         OC    RPAY,RPAY                                                        
         BNZ   *+8                                                              
         LA    R7,MEDBYUNP                                                      
         L     RE,MBGROSS                                                       
         OC    ELEMGRS,ELEMGRS     TIME ONLY DOLLARS                            
         BZ    *+8                                                              
         L     RE,ELEMGRS           YES - USE THEM                              
         A     RE,0(R7)                                                         
         ST    RE,0(R7)                                                         
         CLC   MEDLCHNK,=F'188'                                                 
         BL    REG52                                                            
         LA    R7,MEDBYNPY         NET PAID                                     
         OC    RPAY,RPAY                                                        
         BNZ   *+8                                                              
         LA    R7,MEDBYNUP         NET UNPAID                                   
         OC    ELEMNET,ELEMNET                                                  
         BZ    *+8                                                              
         L     RE,ELEMNET                                                       
         L     RE,MBNET                                                         
         A     RE,0(R7)                                                         
         ST    RE,0(R7)                                                         
*                                                                               
REG52    L     RE,MEDBYNET         ACCUMULATE NET                               
         A     RE,MBNET                                                         
         ST    RE,MEDBYNET                                                      
*                                                                               
         L     RE,MEDBYGRS         ACCUMULATE GROSS                             
         A     RE,MBGROSS                                                       
         ST    RE,MEDBYGRS                                                      
*                                                                               
         L     RE,MEDBYPSP                                                      
         A     RE,MBSPOTS                                                       
         OC    RPAY,RPAY                                                        
         BZ    *+8                                                              
         ST    RE,MEDBYPSP                                                      
         CLC   RPAY,TODAYP                                                      
         BNE   REG54                                                            
         LA    R7,MEDBYPTG         ADD UP PAID TODAY                            
         L     RE,MBGROSS                                                       
         A     RE,0(R7)                                                         
         ST    RE,0(R7)                                                         
         LA    R7,MEDBYPTN                                                      
         L     RE,MBNET                                                         
         A     RE,0(R7)                                                         
         ST    RE,0(R7)                                                         
*                                                                               
REG54    DS    0H                                                               
*                                                                               
REG60    CLI   MEDEXTAX,C'Y'       TEST EXTRACT TAX                             
         BE    *+14                                                             
         CLC   MEDLCHNK,=F'192'    TEST ROOM FOR TAX                            
         BL    REG70               NO                                           
*                                                                               
         L     R0,MBTAX                                                         
         A     R0,MEDMSTAX                                                      
         ST    R0,MEDMSTAX                                                      
*                                                                               
REG68    CLI   MEDEXTAC,C'Y'       EXTRACT ACCOUNTING DATA                      
         BNE   REG70                                                            
         L     RE,MEDBYTAX                                                      
         A     RE,MBTAX                                                         
         ST    RE,MEDBYTAX         SAVE TAX                                     
*                                                                               
         CLC   MEDLCHNK,=F'200'    TEST ROOM FOR PAID/UNPD TAX                  
         BL    REG70                                                            
         LA    RE,MEDBYTXP         ASSUME PAID                                  
         OC    RPAY,RPAY                                                        
         BNZ   *+8                 YES                                          
         LA    RE,MEDBYTXU         NO - UNPAID                                  
         L     R0,0(RE)                                                         
         A     R0,MBTAX            ACCUMULATE PAID/UNPAID TAX                   
         ST    R0,0(RE)            SAVE PAID/UNPAID TAX                         
*                                                                               
REG70    DS    0H                                                               
*                                                                               
REG80    LA    RE,MEDBFORE         RESET SLOT IF BEFORE OR AFTER                
         CR    R5,RE                                                            
         BNE   *+8                                                              
         L     R5,MEDAFRST                                                      
         LA    RE,MEDAFTER                                                      
         CR    R5,RE                                                            
         BNE   *+8                                                              
         L     R5,MEDAFRST                                                      
         B     REG2                                                             
*                                                                               
REGX     MVC   BDSAVE2,BDSTART     SET TRUE LAST DATE FOR AUTO ADJ.             
         CLC   BDSTART(2),BDEND    DON'T MESS WITH SOLO DAYS                    
         BE    GETDEM                                                           
         GOTO1 DATCON,MBPARAM,(X'02',SVTEND),(X'03',DEDATE)                     
         CLC   BDEND,DEDATE                                                     
         BNL   *+10                                                             
         MVC   BDEND,DEDATE                                                     
         EJECT                                                                  
*==============================================================                 
* HAVE SPOTS AND DOLLARS NOW GET DEMOS                                          
* EACH ENTRY IN THE MEDBUFF WILL BE LOOKED UP SEPARATELY                        
* AND THEN SUMMED INTO THE ORIGINAL ACCUMULATORS                                
*==============================================================                 
                                                                                
GETDEM   CLI   MBDEMTYP,0                                                       
         BE    DOTOTAL                                                          
         CLI   MEDEXTDM,0                                                       
         BE    DOTOTAL                                                          
         MVI   MBADJSW,0                                                        
*===========================================================                    
* EFFECTIVE 2/1/17, SVI ADJUSTMENTS ARE ALWAYS SUPPRESSED                       
* THEY ARE DEAD AND DO NOT WORK WITH COMSCORE  MHER                             
*===========================================================                    
*&&DO                                                                           
         CLI   MBDEMTYP,2          CHECK FOR ADJUSTMENTS REQUESTED              
         BNE   *+8                                                              
         MVI   MBADJSW,1                                                        
         CLI   MBDEMTYP,4                                                       
         BNE   *+8                                                              
         MVI   MBADJSW,1                                                        
         CLI   MBDEMTYP,5                                                       
         BNE   *+8                                                              
         MVI   MBADJSW,1                                                        
         CLI   MBDEMTYP,7                                                       
         BNE   *+8                                                              
         MVI   MBADJSW,1                                                        
*&&                                                                             
*                                                                               
         L     R5,MEDAFRST                                                      
         MVI   BFRAFTSW,C' '                                                    
*                                                                               
GETDEM2  SR    R0,R0                                                            
         IC    R0,MEDEXTDM                                                      
         LA    R3,TOTDEM                                                        
*                                                                               
         ICM   R4,15,4(R5)                                                      
         BZ    GETDEM30                                                         
*                                                                               
         LA    R1,MEDBY1                                                        
*                                                                               
         OC    MEDBYSPT,MEDBYSPT                                                
         BZ    GETDEM30                                                         
*                                                                               
         BRAS  RE,GODLU            LOOK UP THE DEMOS                            
*                                                                               
GETDEM4  CLC   BMKT,=H'161'                                                     
         B     *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   R7,15,MBDELAD       IF NO DEMO ELEM ADDRESS, SKIP                
         BZ    GETDEM30            SOMEONE CREATED A BAD SPILL PTR              
         BRAS  RE,FIXDEMEL         ADJUST PREC IN DEMO ELEM                     
         USING NDELEM,R7                                                        
         LA    R7,NDEMNO                                                        
         DROP  R7                                                               
*                                                                               
GETDEM6  OC    0(3,R7),0(R7)       TEST NO MORE DEMOS                           
         JZ    GETDEM30                                                         
*                                                                               
         MVI   CLEARDEM,C'N'                                                    
         CLI   MEDSPILL,C'Y'       TEST FOR SPILL                               
         BNE   GETDEM12            NO                                           
*                                                                               
         CLI   QMED,C'R'           TEST RADIO                                   
         JE    GETDEM12            RADIO HAS SPILL IMPS                         
                                                                                
* SPILL BUT NOT RADIO - CLEAR ALL NON-RATING VALUES                             
                                                                                
         TM    RQOPT2,RQOPT2_NONTDEM   TEST NONT DEMOS NORMALIZED               
         JZ    GETDEM7                 NO                                       
         CLI   0(R7),C'R'              TEST FOR NONT RATING                     
         JE    GETDEM12                                                         
         CLI   0(R7),C'E'                                                       
         JE    GETDEM12                                                         
         J     GETDEM10                                                         
*                                                                               
GETDEM7  CLI   2(R7),0             TEST NONT DEMO                               
         JNE   GETDEM8             NO                                           
         LLC   RF,1(R7)            YES, GET THE NON-TRAD INDEX                  
         BCTR  RF,0                                                             
         MHI   RF,9                9 CHARS/DEMOM IN 50EL                        
         L     RE,VPOL50EL                                                      
         LA    RE,2(RE)                                                         
         AR    RE,RF                                                            
         CLI   0(RE),C'R'                                                       
         JE    GETDEM12                                                         
         CLI   0(RE),C'E'                                                       
         JE    GETDEM12                                                         
         J     GETDEM10                                                         
*                                                                               
GETDEM8  CLI   1(R7),C'R'          CHECK FOR SPILL RATING                       
         BE    GETDEM12            ACCEPT VALUE IF THERE                        
         CLI   1(R7),C'E'          CHECK FOR SPILL EXTENDED RTG                 
         BE    GETDEM12            ACCEPT VALUE IF THERE                        
         CLI   1(R7),X'21'         CHECK USER DEMO                              
         JE    GETDEM12                                                         
         CLI   1(R7),63            TEST WEIGHTED DEMO                           
         JE    GETDEM12                                                         
*                                                                               
GETDEM10 XC    4(4,R7),4(R7)       CLEAR VALUE                                  
         MVI   CLEARDEM,C'Y'       SET TO CLEAR TOTDEM LATER                    
*                                                                               
GETDEM12 ICM   RE,15,4(R7)         GET VALUE FROM DEMO ELEMENT                  
*                                                                               
         C     RE,=X'FFFFFFFF'     TEST COMSCORE DEMO ERROR                     
         JNE   GETDEM13                                                         
*                                                                               
         LR    RE,R7               POINT TO DEMO ELEM                           
         S     RE,MBDELAD          GET DSPLACEMENT TO THIS DEMO                 
         LA    RF,ORIGELEM         POINT TO EST ELEM                            
         AR    RF,RE               POINT TO THIS DEMO                           
         L     RE,4(RF)            AND GET VALUE                                
*                                                                               
GETDEM13 N     RE,=X'3FFFFFFF'     DROP FLAGS                                   
         ST    RE,0(R1)            SET DEMO VALUE                               
*                                                                               
         C     RE,=F'500000'       ANOTHER PROTECTIVE PIECE OF CODE             
         BL    *+10                 ZAP DEMOS ABOVE THIS LIMIT                  
         MVC   0(4,R1),=F'1'                                                    
*                                                                               
         LLC   RF,3(R7)            GET SVI VALUE                                
*                                                                               
         CLI   MBADJSW,0           ADJUSTMENTS REQUIRED                         
         BNE   *+8                                                              
         LHI   RF,100              NO - SET TO 100                              
*                                                                               
         CHI   RF,33               TEST FOR AN ABSURD VALUE                     
         BH    *+8                                                              
         LHI   RF,100                                                           
*                                                                               
         AR    RF,RF               SVI VALUE X 2                                
         MR    RE,RE               DEMO VALUE                                   
         D     RE,=F'100'                                                       
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,0(R1)            SAVE ADJUSTED VALUE                          
*                                                                               
         CLI   MBDEMTYP,6          TEST AFFID RERATE                            
         BL    GETDEM16            NO                                           
*                                                                               
         CLC   0(3,R3),0(R7)       TOTDEM MATCH DEMO ELEM DEM                   
         BE    GETDEM20                                                         
*                                                                               
         OC    TOTDEM,TOTDEM       MAKE SURE TOTDEM WAS BUILT                   
         BNZ   GETDEM17                                                         
*                                                                               
GETDEM16 M     RE,MEDBYSPT         SIMULATE TOTDEM TOTAL                        
         ST    RF,0(R1)                                                         
         B     GETDEM22            AND DO NEXT DEMO                             
*                                                                               
GETDEM17 LA    R3,TOTDEM           FIND DEMO IN TOTDEM                          
*                                                                               
GETDEM18 CLC   0(3,R3),0(R7)                                                    
         BE    GETDEM20                                                         
         AHI   R3,12                                                            
         CLI   1(R3),0             TEST E-O-L                                   
         BNE   *+6                                                              
         DC    H'0'                                                             
         B     GETDEM18                                                         
*                                                                               
GETDEM20 CLI   CLEARDEM,C'Y'       CLEAR DEMO VALUE IN TOTDEM ?                 
         BNE   *+10                                                             
         XC    4(8,R3),4(R3)                                                    
*                                                                               
         L     RF,4(R3)            MOVE DEMO TO OUTPUT SLOT                     
         ST    RF,0(R1)            SET IN OUTPUT SLOT                           
*                                                                               
         CLI   MBADJSW,0           ADJUSTMENT REQUIRED                          
         BE    GETDEM22                                                         
*                                                                               
         AR    RF,RF               X 2                                          
         M     RE,8(R3)                                                         
         D     RE,=F'100'                                                       
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,0(R1)                                                         
*                                                                               
GETDEM22 DS    0H                                                               
         BRAS  RE,ADJPREC                                                       
*                                                                               
         AHI   R3,12                                                            
         AHI   R1,8                BUMP TO NEXT DEMO                            
         AHI   R7,8                                                             
         BCT   R0,GETDEM6                                                       
*                                                                               
GETDEM30 CLI   BFRAFTSW,C' '       TEST DOING BEFORE/AFTER                      
         BH    GETDEM36            YES                                          
*                                                                               
GETDEM32 LA    R5,12(R5)           NEXT MEDBUFF ENTRY                           
         C     R5,MEDALAST                                                      
         BH    GETDEM34                                                         
         CLI   0(R5),0                                                          
         BE    GETDEM32            SKIP EMPTY SLOT                              
         B     GETDEM2                                                          
*                                                                               
GETDEM34 MVI   BFRAFTSW,C'B'       SET WE ARE DOING BEFORE                      
         LA    R5,MEDBFORE                                                      
         CLI   MEDNYBEF,C'Y'                                                    
         BE    GETDEM2                                                          
*                                                                               
GETDEM36 CLI   BFRAFTSW,C'A'       TEST WE JUST DID AFTER                       
         BE    GETDEM40            YES - DONE                                   
         MVI   BFRAFTSW,C'A'       SET WE ARE DOING AFTER                       
         LA    R5,MEDAFTER                                                      
         CLI   MEDNYAFT,C'Y'                                                    
         BE    GETDEM2                                                          
         EJECT                                                                  
*=============================================================                  
* ADD MYMEDBUF ACCUMS TO ORIGINAL MEDBUFF ACCUMS                                
*=============================================================                  
                                                                                
GETDEM40 L     R5,MEDAFRST                                                      
         L     RF,SVAFRST                                                       
         CLI   MYMEDFLG,C'Y'       TEST MYMEDBUF IN USE                         
         BNE   GETDEM60                                                         
*                                                                               
GETDEM42 CLC   0(2,RF),0(R5)       ORIG START TO MY START DATE                  
         BH    GETDEM44                                                         
         CLC   2(2,RF),0(R5)       ORIG END TO MY START                         
         BNL   GETDEM46                                                         
*                                                                               
GETDEM44 AHI   RF,12                                                            
         C     RF,SVALAST                                                       
         BNH   GETDEM42                                                         
         DC    H'0'                BUGSTOPPER                                   
*                                                                               
GETDEM46 L     R4,4(R5)            POINT TO MY ACCUMS                           
         CLI   MEDSPILL,C'Y'       IS THIS SPILL                                
         BNE   GETDEM47                                                         
         BAS   RE,CHKSPL           SEE IF ANY NON-ZERO VALUES                   
         OC    MEDBYSPT,MEDBYSPT   TEST ANY SPOTS LEFT                          
         BZ    GETDEM50                                                         
*                                                                               
GETDEM47 L     RE,4(RF)            POINT TO ORIGINAL ACCUMS                     
         L     R0,MEDLCHNK         CHUNK LENGTH                                 
         SRL   R0,2                SETS BCT COUNT                               
*                                                                               
GETDEM48 L     R1,0(R4)                                                         
         A     R1,0(RE)                                                         
         ST    R1,0(RE)                                                         
*                                                                               
         AHI   R4,4                                                             
         AHI   RE,4                                                             
         BCT   R0,GETDEM48                                                      
*                                                                               
GETDEM50 AHI   R5,12               NEXT SLOT IN MYMEDBUF                        
         OC    0(4,R5),0(R5)                                                    
         BNZ   GETDEM42                                                         
         B     GETDEMX                                                          
         EJECT                                                                  
*============================================================                   
* ONE MORE CHECK FOR SPILL SPOTS IN ACCUMS WITH NO DEMOS                        
*============================================================                   
                                                                                
GETDEM60 CLI   MEDSPILL,C'Y'       IS THIS SPILL                                
         BNE   GETDEMX                                                          
*                                                                               
         L     R5,MEDAFRST                                                      
*                                                                               
GETDEM62 ICM   R4,15,4(R5)                                                      
         BZ    GETDEM64                                                         
*                                                                               
         BRAS  RE,CHKSPL                                                        
*                                                                               
GETDEM64 AHI   R5,12                                                            
         C     R5,MEDALAST                                                      
         BNH   GETDEM62                                                         
*                                                                               
GETDEMX  DS    0H                                                               
         B     AVE                                                              
         EJECT                                                                  
*==============================================================                 
* CHECK FOR NON-ZERO SPILL VALUES ACCORDING TO PROFILE OPTIONS                  
* IF SPILL VALUES ARE ZERO, CLEAR SPOTS                                         
*==============================================================                 
                                                                                
CHKSPL   NTR1                                                                   
         CLI   MBDEMTYP,3          BUYERS DEMOS                                 
         BNL   *+12                                                             
         CLI   SVD0PROF,C'N'       CHECK FOR RERATE BASED                       
         BE    CHKSPLX                                                          
         CLC   SVD0PROF(2),=C'NN'  JUST ACCEPT IF BOTH ON                       
         BE    CHKSPLX                                                          
*                                                                               
         SR    RE,RE                                                            
         ICM   RE,1,MEDEXTDM       GET NUMBER OF DEMOS                          
         BZ    CHKSPLX                                                          
         MHI   RE,8                                                             
         BCTR  RE,0                SET FOR EX                                   
         EX    RE,*+8                                                           
         B     *+10                                                             
         OC    MEDBY1(0),MEDBY1    TEST ANY NON-ZERO DEMOS                      
*                                                                               
         BNZ   *+10                                                             
         XC    MEDBYSPT,MEDBYSPT   ELSE CLEAR SPOTS                             
*                                                                               
CHKSPLX  XIT1                                                                   
         EJECT                                                                  
*===========================================================                    
* ALL DATA HAS BEEN EXTRACTED FROM RECORD                                       
* IF NOT AFFID RERATE, MULTIPLY SPOTS X DEMO VALUES                             
* AND CALCULATE DETAIL TOTALS AND PERIOD AVERAGES                               
*===========================================================                    
                                                                                
AVE      MVI   BFRAFTSW,C' '                                                    
         MVC   MEDAFRST,SVAFRST    RESTORE ORIGINAL MEDBUFF ADDRS               
         MVC   MEDALAST,SVALAST                                                 
         L     R5,MEDAFRST                                                      
         XC    MBSPOTS,MBSPOTS                                                  
*                                                                               
AVE2     ICM   R4,15,4(R5)                                                      
         BZ    AVE12                                                            
         OC    MEDBYD(12),MEDBYD                                                
         BZ    AVE12                                                            
*                                                                               
         LA    R6,MEDVPEST                                                      
         LA    R7,MEDVAEST                                                      
         CLI   MBDEMTYP,3                                                       
         BL    *+12                                                             
         LA    R6,MEDVPACH                                                      
         LA    R7,MEDVAACH                                                      
*                                                                               
         SR    R0,R0                                                            
         IC    R0,MEDEXTDM                                                      
         MVC   MBHALF,MEDBYSPT+2                                                
         MVC   MBSHLD,MBHALF                                                    
*                                                                               
**NOP**  CLI   MBDEMTYP,6          TEST AFFID RERATE                            
**NOP**  BL    *+10                                                             
         MVC   MBHALF,=H'1'                                                     
*                                                                               
         L     RE,MEDBY1                                                        
         MH    RE,MBHALF                                                        
         ST    RE,MEDBYP                                                        
*                                                                               
         L     RE,MBSPOTS                                                       
         A     RE,MEDBYSPT                                                      
         ST    RE,MBSPOTS                                                       
*                                                                               
AVE3     L     RE,MEDBY1           GET TOTAL DEMOS FOR PERIOD                   
         MH    RE,MBHALF                                                        
         ST    RE,MEDBY1                                                        
*                                                                               
         CLI   MEDEXTAV,C'Y'                                                    
         BNE   AVE4                                                             
*                                                                               
         A     RE,0(R6)            ADD TO PERIOD AVERAGES                       
         ST    RE,0(R6)                                                         
*                                                                               
         L     RE,MEDBY1EQ         GET TOTAL ADJUSTMENTS FOR PERIOD             
         MH    RE,MBSHLD                                                        
         A     RE,0(R7)            ADD TO PERIOD AVERAGES                       
         ST    RE,0(R7)                                                         
*                                                                               
AVE4     XC    MEDBY1EQ,MEDBY1EQ                                                
*                                                                               
AVE10    DS   0H                                                                
AVE10X   LA    R4,8(R4)            DO NEXT DEMO                                 
         LA    R6,4(R6)                                                         
         LA    R7,4(R7)                                                         
         BCT   R0,AVE3                                                          
*                                                                               
AVE12    CLI   BFRAFTSW,C' '       TEST DOING BEFORE/AFTER                      
         BH    AVE18               YES                                          
*                                                                               
AVE14    LA    R5,12(R5)           NEXT SLOT                                    
         C     R5,MEDALAST                                                      
         BH    AVE16                                                            
         CLI   0(R5),0                                                          
         BE    AVE14                                                            
         B     AVE2                                                             
*                                                                               
AVE16    MVI   BFRAFTSW,C'B'       SET DOING BEFORE                             
         LA    R5,MEDBFORE                                                      
         CLI   MEDNYBEF,C'Y'                                                    
         BE    AVE2                                                             
*                                                                               
AVE18    CLI   BFRAFTSW,C'A'       TEST JUST DID AFTER                          
         BE    CPAVE                                                            
         MVI   BFRAFTSW,C'A'       SET DOING AFTER                              
         LA    R5,MEDAFTER                                                      
         CLI   MEDNYAFT,C'Y'                                                    
         BE    AVE2                                                             
         EJECT                                                                  
*===========================================================                    
* CALCULATE PERIOD AVERAGE DEMOS AND ADJUSTMENTS                                
*===========================================================                    
                                                                                
CPAVE    DS    0H                                                               
         CLI   MEDEXTAV,C'Y'                                                    
         BNE   DOTOTAL                                                          
         OC    MBSPOTS,MBSPOTS                                                  
         BZ    DOTOTAL                                                          
         LA    R6,MEDVPEST                                                      
         CLI   MBDEMTYP,3          SET DEMO ADDRESS                             
         BL    *+8                                                              
         LA    R6,MEDVPACH                                                      
         LA    R7,112(R6)          SET ADJUSTMENT ADDRESS                       
         LLC   R0,MEDEXTDM                                                      
*                                                                               
CPAVE1   ICM   RF,15,0(R6)         AVERAGE DEMOS                                
         BZ    CPAVE2                                                           
         M     RE,=F'2'                                                         
         D     RE,MBSPOTS                                                       
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         ST    RF,0(R6)                                                         
*                                                                               
CPAVE2   ICM   RF,15,0(R7)          AVERAGE ADJ                                 
         BZ    CPAVE3                                                           
         M     RE,=F'2'                                                         
         D     RE,MBSPOTS                                                       
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         ST    RF,0(R7)                                                         
*                                                                               
CPAVE3   LA    R6,4(R6)                                                         
         LA    R7,4(R7)                                                         
         BCT   R0,CPAVE1                                                        
         EJECT                                                                  
*===============================================================                
* TOTAL ROUTINE FOR VARIOUS PERIODS                                             
*===============================================================                
                                                                                
DOTOTAL  CLI   MEDNOLNK,C'Y'       BYPASS SUMMATION LINKS                       
         BNE   DOTOTOK              NO - ADD THEM UP                            
         CLC   QPROG,=C'B1'        FOR NET AND SPOT BILLING                     
         BE    EXIT                EXIT HERE                                    
         CLC   QPROG,=C'D1'                                                     
         BE    EXIT                                                             
         CLC   QPROG,=C'DU'                                                     
         BE    EXIT                                                             
         CLC   QPROG,=C'BU'                                                     
         BE    EXIT                                                             
         MVI   GBHOOK,C'N'         RESET SPOT HOOK                              
                                                                                
* REPROCESS RECORD BASED ON NEW LIST                                            
                                                                                
         L     R7,MEDAFRST                                                      
         LA    RE,MEDMON01                                                      
         CR    R7,RE               HAVE MONTHS BEEN PROCESSED                   
         BNL   DOTOTQT              YES - TRY QUARTER                           
         OC    MEDNUMMO,MEDNUMMO                                                
         BZ    DOTOTQT                                                          
         LA    RE,MEDMON01         SET START                                    
         ST    RE,MEDAFRST                                                      
DOTOTM1  ST    RE,MEDALAST         SET END                                      
         CLI   12(RE),0                                                         
         BE    *+12                                                             
         LA    RE,12(RE)           NEXT SLOT                                    
         B     DOTOTM1                                                          
         MVC   SVAFRST,MEDAFRST                                                 
         MVC   SVALAST,MEDALAST                                                 
         B     SETDEMX             REPROCESS RECORD                             
                                                                                
*  SET FOR QUARTERLY DATES                                                      
                                                                                
DOTOTQT  L     R7,MEDAFRST                                                      
         LA    RE,MEDQRT01                                                      
         CR    R7,RE                                                            
         BNL   DOTOTT1                                                          
         OC    MEDNUMQT,MEDNUMQT                                                
         BZ    DOTOTT1                                                          
         ST    RE,MEDAFRST                                                      
*                                                                               
DOTOTQ1  ST    RE,MEDALAST                                                      
         CLI   12(RE),0                                                         
         BE    *+12                                                             
         LA    RE,12(RE)                                                        
         B     DOTOTQ1                                                          
         MVC   SVAFRST,MEDAFRST                                                 
         MVC   SVALAST,MEDALAST                                                 
         B     SETDEMX                                                          
                                                                                
* SET FOR PERIOD DATES                                                          
                                                                                
DOTOTT1  L     R7,MEDAFRST                                                      
         LA    RE,MEDPERD                                                       
         OC    0(4,RE),0(RE)                                                    
         BZ    MBEQUIV                                                          
         CR    R7,RE                                                            
         BNL   MBEQUIV                                                          
         ST    RE,MEDAFRST                                                      
         ST    RE,MEDALAST                                                      
         MVC   SVAFRST,MEDAFRST                                                 
         MVC   SVALAST,MEDALAST                                                 
         B     SETDEMX                                                          
         EJECT                                                                  
* TOTAL BASED ON LINKS IN DATE TABLE                                            
DOTOTOK  DS    0H                                                               
         L     RE,MEDLCHNK                                                      
         LA    RE,MEDDATA(RE)          CALCULATE NUMBER OF ENTRYS               
         LA    RF,MEDBYD                                                        
         SR    RE,RF                                                            
         SRL   RE,2                                                             
         STC   RE,NOFLDS                                                        
*                                                                               
         L     R5,MEDAFRST                                                      
         LA    R7,MEDTOTAL                                                      
         CR    R5,R7                                                            
         BNL   MBEQUIV                                                          
*                                                                               
DOTOTAL1 L     R6,8(R5)                                                         
         LTR   R6,R6                                                            
         BZ    DOTNXT               NO -TRY NEXT                                
         LR    R4,R6                                                            
         LA    R6,MEDBYD           SET TO FIELDS                                
         L     R4,4(R5)            SET FROM FIELDS                              
         LTR   R4,R4                                                            
         BZ    DOTNXT                                                           
         CLI   MEDEXTAC,C'Y'       DOING ACCOUNTING DATA                        
         BE    *+14                 YES - MAY BE PAYMENTS                       
         OC    MEDBYD(12),MEDBYD   ANY DATA IN SLOT                             
         BZ    DOTNXT               NO - DO NEXT SLOT                           
*                                                                               
         SR    R0,R0                                                            
         IC    R0,NOFLDS                                                        
*                                                                               
DOTOTAL2 L     RE,MEDBYD           ADD FIELD                                    
         A     RE,0(R6)                                                         
         ST    RE,0(R6)                                                         
         LA    R6,4(R6)                                                         
         LA    R4,4(R4)                                                         
         BCT   R0,DOTOTAL2                                                      
*                                                                               
         CLI   MEDEXTAC,C'G'       EXTRACT NET IN GOAL DOLLARS                  
         BNE   DOTOTAL4                                                         
         L     R6,8(R5)            SUM NET IN GOAL SLOTS                        
         LR    R4,R6                                                            
         LA    R6,MEDGLD                                                        
         L     R4,4(R5)                                                         
         L     RE,MEDGLD                                                        
         A     RE,0(R6)                                                         
         ST    RE,0(R6)                                                         
*                                                                               
DOTOTAL4 CLI   MEDEXTCS,C'R'                                                    
         BE    *+12                                                             
         CLI   MEDEXTCS,C'Y'                                                    
         BNE   DOTOTAL6                                                         
         L     R6,8(R5)            DO CHILD SPOT TOTALS                         
         LR    R4,R6                                                            
         LA    R6,MEDCSPAY                                                      
         L     R4,4(R5)                                                         
         LA    R0,7                                                             
DOTOTAL5 L     RE,MEDCSPAY                                                      
         A     RE,0(R6)                                                         
         ST    RE,0(R6)                                                         
         LA    R6,4(R6)                                                         
         LA    R4,4(R4)                                                         
         BCT   R0,DOTOTAL5                                                      
         SPACE 2                                                                
DOTOTAL6 L     R6,8(R5)            DO MS TAX TOTALS                             
         LR    R4,R6                                                            
         LA    R6,MEDMSTAX                                                      
         L     R4,4(R5)                                                         
         L     RE,MEDMSTAX                                                      
         A     RE,0(R6)                                                         
         ST    RE,0(R6)                                                         
*                                                                               
DOTNXT   CLC   4(8,R5),16(R5)                                                   
         BNE   *+8                                                              
         LA    R5,12(R5)                                                        
*                                                                               
         LA    R5,12(R5)                                                        
         CR    R5,R7                                                            
         BH    MBEQUIV                                                          
         CLI   2(R5),0                                                          
         BE    DOTNXT                                                           
         B     DOTOTAL1                                                         
         EJECT                                                                  
*============================================================                   
* EQUIVALENCE DOLLARS AND DEMOS FOR ACTIVE ENTRIES                              
*============================================================                   
                                                                                
MBEQUIV  BAS   RE,VMBEQU                                                        
*                                                                               
EXIT     L     R3,ADBUY                                                         
         MVC   BDSTART(6),BDSAVE2                                               
*                                                                               
EXITX    DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
         EJECT                                                                  
*========================================================                       
* ROUTINE TO APPLY BILL FORMULA                                                 
* R1 POINTS TO GROSS DOLLARS. 8(R1) IS ASSUMED TO BE TAX                        
* DOLLARS WHICH ARE SUBTRACTED OUT BEFORE ADJUSTMENT                            
* THEN ADDED BACK                                                               
*========================================================                       
         SPACE 1                                                                
BILLFORM ST    RE,SVREGE                                                        
         XC    SVBFORM,SVBFORM                                                  
         ST    R1,MBPARAM                                                       
         MVC   MBPARAM(1),GBBRND                                                
         CLI   MBPARAM,X'FF'       RIGHT - THIS MEANS YOU CAN'T HAVE            
         BE    BILLFORX            BILL FORMULAE FOR POL REQ                    
*                                                                               
BILLFOR2 DS    0H                                                               
         GOTO1 GETBF,MBPARAM,,SVBFORM                                           
*                                                                               
BILLFORX L     RE,SVREGE                                                        
         BR    RE                                                               
         EJECT                                                                  
VMBEQU   NTR1                                                                   
         L     R5,MEDAFRST                                                      
*                                                                               
         CLI   MBDEMTYP,0                                                       
         BE    MBEXIT                                                           
*                                                                               
MBEQUIV1 ICM   R4,15,4(R5)                                                      
         BZ    MBENXT                                                           
         L     RF,MEDBYD           EQUIVALENCE DOLLARS                          
         CLI   MEDEXTAX,C'Y'                                                    
         BNE   MBENOTX                                                          
         S     RF,MEDMSTAX         REMOVE TAX DOLLARS BEFORE                    
         ST    RF,MEDBYD            EQUIVALENCING                               
*                                                                               
MBENOTX  DS    0H                                                               
*                                                                               
MBEQUV1B OC    MEDBYSPT,MEDBYSPT                                                
         BZ    MBENXT                                                           
*                                                                               
         OC    MBEQU,MBEQU                                                      
         BNZ   *+10                                                             
         MVC   MBEQU,=H'1000'                                                   
*                                                                               
         CLC   MBEQU,=H'1000'                                                   
         BE    MBEQUV1C                                                         
*                                                                               
         LHI   R6,1                SET FOR NO ADJUST                            
         SR    RE,RE                                                            
         C     RF,=F'5000000'                                                   
         BL    *+12                                                             
         D     RE,=F'10'                                                        
         LHI   R6,10               SET TO ADJUST BY 10                          
         M     RE,=F'1000'                                                      
         SLDA  RE,1                                                             
*                                                                               
         LH    R0,MBEQU            GET ADJ FACTOR                               
         ST    R0,MBFULL           AND SAVE IT                                  
         DR    RE,R0                                                            
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         MR    RE,R6                                                            
*                                                                               
MBEQUV1C ST    RF,MEDBYDEQ                                                      
         LA    R6,MEDBY1           SET DEMO EQUIVALENCE ADDRESS                 
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,MEDEXTDM       GET NUMBER OF DEMOS TO PROCESS               
         BZ    MBENXT                                                           
*                                                                               
MBEQUIV2 L     RF,0(R6)                                                         
         CLC   MBEQU,=H'1000'                                                   
         BE    MBEQUIV3                                                         
         M     RE,MBFULL                                                        
         SLDA  RE,1                                                             
         D     RE,=F'1000'                                                      
         AHI   RF,1                                                             
         SRA   RF,1                                                             
*                                                                               
MBEQUIV3 ST    RF,4(R6)                                                         
*                                                                               
MBEQUIV4 LA    R6,8(R6)                                                         
         BCT   R0,MBEQUIV2                                                      
*                                                                               
         MVC   MEDBYPEQ,MEDBY1EQ                                                
*                                                                               
MBENXT   LA    R5,12(R5)           GET NEXT SUMMATION SLOT                      
*                                                                               
         LA    R7,MEDTOTAL                                                      
*                                                                               
         CR    R5,R7                                                            
         BH    MBEXIT                                                           
         CLI   2(R5),0                                                          
         BE    MBENXT                                                           
         B     MBEQUIV1                                                         
*                                                                               
MBEXIT   CLI   QFILTER,C'F'        ANY FILM TYPE FILTER                         
         BE    *+14                                                             
         OC    SPOTHOOK,SPOTHOOK   ANY SPOT HOOK                                
         BZ    MBEXIT2                                                          
*                                                                               
         L     R6,FRSTDEL                                                       
*                                                                               
MBEXIT1  OC    0(4,R6),0(R6)                                                    
         BZ    MBEXIT2                                                          
         L     R7,0(R6)                                                         
         TM    0(R7),X'80'                                                      
         BO    *+6                                                              
         DC    H'0'                                                             
         NI    0(R7),X'7F'                                                      
         LA    R6,4(R6)                                                         
         B     MBEXIT1                                                          
*                                                                               
MBEXIT2  MVC   MEDALAST,SVMDLAST                                                
         MVC   MEDAFRST,SVMDFRST                                                
         MVC   QBOOK1,SVBOOK                                                    
         XIT1                                                                   
         EJECT                                                                  
GODLU    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVI   LKUPERR,C'N'                                                     
         LA    R6,MBDELTAB                                                      
         ST    R6,MBCURDEL                                                      
         L     R7,0(R6)                                                         
         ST    R7,MBDELAD                                                       
*                                                                               
         XC    WORK,WORK           SET UP 1W PROFILE KEY                        
         MVC   WORK(4),=C'S0D0'                                                 
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),QCLT                                                   
         L     RF,ADCLT            CHECK IF CLIENT HAS OFFICE                   
         USING CLTHDR,RF                                                        
         CLI   COFFICE,X'41'                                                    
         BL    *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  RF                                                               
*                                                                               
         CLC   SVD0KEY(12),WORK    BYPASS READ IF SAME KEY AS BEFORE            
         BE    GD1WPROF                                                         
         MVC   SVD0KEY(12),WORK                                                 
         GOTO1 GETPROF,MBPARAM,WORK,SVD0PROF,DATAMGR                            
*                                                                               
GD1WPROF CLI   MBDEMTYP,3          RERATE                                       
         BL    GODLUX               NO - EXIT                                   
*                                                                               
         XC    WORK,WORK           SET UP 1W PROFILE KEY                        
         MVC   WORK(4),=C'S01W'                                                 
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),QCLT                                                   
*                                                                               
         L     RF,ADCLT            CHECK IF CLIENT HAS OFFICE                   
         USING CLTHDR,RF                                                        
         CLI   COFFICE,X'41'                                                    
         BL    *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  RF                                                               
*                                                                               
         CLC   SV1WKEY(12),WORK    BYPASS READ IF SAME KEY AS BEFORE            
         BE    GDNOPROF                                                         
         MVC   SV1WKEY(12),WORK                                                 
         GOTO1 GETPROF,MBPARAM,WORK,SV1WPROF,DATAMGR                            
*                                                                               
D        USING DEMFACSD,DEMFLIST                                                
*                                                                               
GDNOPROF XC    DEMFLIST,DEMFLIST                                                
*                                                                               
         LA    RE,SVD0PROF         GET ADDRESS OF D0 PROFILE                    
         ST    RE,ASVD0            MAKE PROFILE AVAILABLE TO REPORTS            
         ST    RE,D.DEMFSPD0                                                    
*                                                                               
         LA    RE,SV1WPROF         SET PROFILE IN PARAMETER BLOCK               
         ST    RE,ASV1W            MAKE PROFILE AVAILABLE TO SPREPORT           
         ST    RE,D.DEMFSP1W                                                    
*                                                                               
         MVC   NEWELEM,SAVELEM     SETUP FOR DEMO LOOKUPS                       
         L     R7,0(R6)                                                         
         XC    DELSAVE,DELSAVE                                                  
         SR    RE,RE               DEMO ELEMENT LENGTH                          
         IC    RE,1(R7)                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   DELSAVE(0),NEWELEM  * EXECUTED *                                 
*                                                                               
         LA    R7,DELSAVE                                                       
         USING NDELEM,R7                                                        
*                                                                               
         OC    MBDEM+60(MAXDEMO),MBDEM+60                                       
         BZ    *+10                                                             
         MVC   NDPROG(MAXDEMO),MBDEM+60                                         
         MVI   MBGETDEM,0                                                       
*                                                                               
         CLI   QRERATE,C'U'        TEST UPGRADE                                 
         BE    GODUP                                                            
*                                                                               
         CLI   QHUT1,C'B'          SET FOR NO ADJUSTMENTS                       
         BE    *+10                                                             
         CLC   QHUT1,=C'NO'                                                     
         BE    *+8                                                              
         MVI   MBGETDEM,1                                                       
*                                                                               
         MVI   NDCODE,X'02'        SET FOR RERATE ON PURCHASED                  
         CLI   MBDEMTYP,6                                                       
         BL    *+8                                                              
         MVI   NDCODE,X'12'        SET FOR RERATE ON AFFIDAVIT                  
         CLI   MEDSPILL,C'Y'                                                    
         BNE   *+8                                                              
         OI    NDCODE,X'01'        MAKE SPILL REQUEST 03-13                     
*                                                                               
         MVI   LKUPOPT,C' '        CLEAR OVERNITE/WEEKLY OPTION                 
         CLC   QBOOK1(3),=C'ACT'                                                
         BE    GODLUDT                                                          
*                                                                               
         CLI   Q2LPMWK,C'N'      OVERRIDE PROFILE FOR LPMWK                     
         BE    *+8                                                              
         CLI   Q2LPMWK,C'Y'      OVERRIDE PROFILE FOR LPMWK                     
         BNE   *+10                                                             
         MVC   SVD0PROF+10(1),Q2LPMWK                                           
                                                                                
         CLI   Q2OVNITE,C' '      OVERRIDE PROFILE FOR OVERNIGHTS               
         BNH   *+10                                                             
         MVC   SVD0PROF+11(1),Q2OVNITE                                          
*                                                                               
         MVC   WORK2(4),QBOOK1      SET REQUESTED BOOK                          
         MVC   WORK2+4(2),=C'01'                                                
         GOTO1 DATCON,MBPARAM,WORK2,(3,DUB)                                     
         MVC   NDBOOK,DUB                                                       
         B     GODLUDTX                                                         
*                                                                               
GODLUDT  MVI   MBGETDEM,1          FIX DATES FOR ACTUAL BOOKS                   
*                                                                               
         L     RE,ADBUY                                                         
         USING BUYREC,RE                                                        
         MVC   BDSAVE(6),BDSTART                                                
                                                                                
*FOR WEEKLY AND OVERNIGHT POSTING- DONT DO ANY OF THE FUNNY                     
*BOOK ADJUSTEMENT.  JUST GET THE BOOK OUT OF MEDAFRST                           
                                                                                
         CLI   SVD0PROF+10,C'Y'    PROFILE FOR WEEKLY                           
         BE    GODLUDT6                                                         
         CLI   SVD0PROF+11,C'M'    OVERNIGHTS -NEW OVN OPTION                   
         BE    GODLUDT6                                                         
         CLI   SVD0PROF+11,C'Y'    OVERNIGHTS                                   
         BE    GODLUDT6                                                         
*                                                                               
         SR    R0,R0                                                            
         SR    R4,R4                                                            
         SR    R1,R1                                                            
         ICM   R1,1,BDDAY                                                       
         BNZ   *+8                                                              
         IC    R1,=X'7C'           FORCE M-F IF IT REARS ITS HEAD HERE          
         SLL   R1,25                                                            
*                                                                               
GODLUDT2 SLDL  R0,1                FIND DAY OF WEEK                             
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         BCT   R4,GODLUDT2                                                      
         LPR   R4,R4                                                            
         GOTO1 DATCON,MBPARAM,(X'02',0(R5)),(X'00',WORK1)                       
         GOTO1 ADDAY,MBPARAM,WORK1,WORK2,(R4)                                   
         CLI   MBDEMTYP,5          TAKE CARE OF DUP DATES                       
         BH    GODLUDT4            OCCURS WHEN DOING ACT BOOKS                  
         GOTO1 DATCON,MBPARAM,(X'00',WORK2),(X'02',DSDATE)                      
         CLC   DSDATE(2),2(R5)                                                  
         BNH   GODLUDT4                                                         
         LA    R5,12(R5)                                                        
*                                                                               
GODLUDT4 GOTO1 DATCON,MBPARAM,(X'00',WORK2),(X'03',DSDATE)                      
         GOTO1 DATCON,MBPARAM,(X'02',2(R5)),(X'03',DEDATE)                      
*                                                                               
         L     RE,ADBUY                                                         
         CLC   BDSTART(3),BDEND    DON'T MESS WITH SOLO DAYS                    
         BE    GODLUDT6                                                         
         CLC   BDSAVE(3),DSDATE                                                 
         BH    *+10                                                             
         MVC   BDSTART,DSDATE                                                   
         CLC   BDEND(3),DEDATE                                                  
         BL    *+10                                                             
         MVC   BDEND,DEDATE                                                     
*                                                                               
GODLUDT6 MVC   NDBOOK,8(R5)        SET MONTHLY BOOK FROM SETDATE                
         OC    10(2,R5),10(R5)     WEEKLY/OVERNITE BOOK SET ?                   
         BZ    GODLUDTX                                                         
                                                                                
         CLI   SVD0PROF+11,C'M'    PROFILE FOR OVERNITE-NEW OVN OPTION          
         BE    *+8                 NO                                           
         CLI   SVD0PROF+11,C'Y'    PROFILE FOR OVERNITE                         
         BNE   GODLUDT8            NO                                           
*                                                                               
*BPOO 3/4/2016 NEEDED TO ADD THIS SO OVERNIGHTS ALSO ALWAYS PASS IN             
* THE MONTHLY BOOK TO SPGETDEME.  THIS IS DUE TO THE SET METERED                
* SITUATION WHERE WE ARE ALSO SETTING THE LPM START DATE                        
* AND NEED SPGETDEME TO GET THE MONTHGLY BOOK PASSED IN                         
*                                                                               
         B     GODLUDT8                                                         
*                                                                               
         TM    4(R5),X'80'         TEST OVERNITE BOOK IS THERE                  
         BZ    GODLUDT8            NO                                           
         MVC   NDBOOK,10(R5)                                                    
         MVI   LKUPOPT,C'O'        THEN REQUEST IT                              
         B     GODLUDTX            HAVE TO TEST BOTH OPTION - WE ALLOW          
*                                  WEEKLY AND OVN OPTION TOGETHERE              
GODLUDT8 CLI   SVD0PROF+10,C'Y'    PROFILE FOR WEEKLY                           
         BNE   GODLUDTX            NO                                           
* OPUP: EVEN THOUGH THE WEEKLY BOOK IS PROVIDED WITH OPTIONS WTP AND            
* LPMWKLY=Y, GRAB THE MONTHLY BOOK. WE NEED TO KNOW THE MONTHLY BOOK            
* BECAUSE ONCE THE WEEKLY DATA GOES AWAY WE'LL ENFORCE MONTHLY LOOKUPS          
* IN SPGETDEME.  THE WEEKLY BOOK BELOW IS REALLY NOT NEEDED, BECAUSE            
* SPGETDEME CALCULATES THE WEEK FROM THE DATE.                                  
****     MVC   NDBOOK,10(R5)                                                    
         MVI   LKUPOPT,C'W'                                                     
*                                                                               
GODLUDTX CLI   MEDSPILL,C'Y'                                                    
         BE    GODLU07                                                          
         XC    NDPROG,NDPROG       SET WEIGHTS                                  
         MVC   NDPROG(20),MBDEMWT                                               
*                                                                               
         CLI   SVD0PROF+10,C'Y'    PROFILE FOR LPM WEEKLY                       
         BE    GODLU06                                                          
         CLI   SVD0PROF+11,C'M'    PROFILE FOR OVERNITE - NEW OVN OPT           
         BE    GODLU06                                                          
         CLI   SVD0PROF+11,C'Y'    PROFILE FOR OVERNITE                         
         BE    GODLU06                                                          
         B     GODLU07                                                          
*                                                                               
GODLU06  MVC   NDACTL(4),0(R5)     SET DATES TO BE PROCESSED                    
*                                                                               
GODLU07  CLI   MBDEMTYP,6          AFFIDS REQUESTED                             
         BL    GODLU10              NO - BUILD PARAMETERS                       
         MVI   MBGETDEM,1                                                       
         MVC   NDPROG+14(1),MEDBRAND   SET PRODUCT                              
         MVC   NDPROG+15(1),MEDSPTLN   SET LENGTH                               
         MVC   NDACTL(4),0(R5)         SET DATES                                
*                                                                               
GODLU10  L     RF,ADCLT            SET UP FOR PARAMETER LIST                    
         USING CLTHDR,RF                                                        
         MVC   MBHALF(1),CPROF+4-1 SET RATING SERVICE                           
         NI    MBHALF,X'0F'        DROP HOB'S                                   
         CLI   CEXTRA,C'U'         TEST US DEMOS                                
         BE    GODLU12                                                          
         DROP  RF                                                               
         L     RF,ADAGY                                                         
         LA    RF,AGYPROF+7-AGYHDR(RF)                                          
         CLI   0(RF),C'C'          TEST CANADIAN AGY                            
         BNE   GODLU12                                                          
         OI    MBHALF,X'80'        SET CANADIAN FLAG FOR LOOK-UPS               
*                                                                               
G        USING GETDEMD,MBDEMD                                                   
*                                                                               
GODLU12  XC    MBDEMD,MBDEMD       CLEAR NEW PARM3                              
         L     RE,=V(MASTC)                                                     
         USING MASTD,RE                                                         
         MVC   G.GTDMUID,MCORIGID                                               
         DROP  RE                                                               
*                                                                               
         TM    RQOPTS,RQOPTS_2DEC  TEST 2-DECIMAL RATINGS ON                    
         BZ    *+8                                                              
         OI    G.GTDMFLAG,X'40'    THEN RATINGS TO 2 DECIMALS                   
*                                                                               
         TM    RQOPTS,RQOPTS_2DECIMP  TEST 2-DEC IMPRESSIONS ON                 
         BZ    *+8                                                              
         OI    G.GTDMFLAG,X'01'    THEN IMPRESSIONS TO 2 DECIMALS               
*                                                                               
         CLI   LKUPOPT,C'O'        TEST OVERNITE LOOKUP                         
         BNE   *+8                                                              
         OI    G.GTDMFLAG,X'20'    SET FLAG BYTE                                
*                                                                               
         CLI   LKUPOPT,C'W'        TEST WEEKLY LOOKUP                           
         BNE   *+8                                                              
         OI    G.GTDMFLAG,X'10'    SET FLAG BYTE                                
*                                                                               
         TM    RQOPTS,RQOPTS_POST  TEST SPOT POSTING                            
         BZ    *+8                                                              
         OI    G.GTDMFLAG,X'04'    SET FLAG BYTE                                
*                                                                               
         L     RF,ADEST                                                         
         USING ESTHDRD,RF                                                       
         MVC   G.GTDMEOW,EOWSDAY     ESTIMATE'S EOWSDAY                         
         DROP  RF                                                               
*                                                                               
         L     RF,ADMARKET                                                      
         USING MKTRECD,RF                                                       
         MVC   G.GTDMKALF,MKTALST                                               
         DROP  RF                                                               
*                                                                               
         L     RF,ADSTAT                                                        
         USING STARECD,RF                                                       
*                                                                               
GODLU13  CLC   STAKLEN,=Y(STACRLNQ)  TEST REC IS LONG ENOUGH                    
         JNH   GODLU13A              TO HAVE FLAG                               
         CLI   SPARPLUS,C'Y'                                                    
         JNE   GODLU13A                                                         
         OI    G.GTDMFLAG,X'02'    SET PAR+SAT FLAG                             
*                                                                               
GODLU13A CLI   STYPE,C'C'                                                       
         BNE   GODLU14                                                          
         CLI   SNTISTA,C' '        TEST NTI STATION PRESENT                     
         BNH   GODLU14                                                          
         MVC   G.GTDMNTI,SNTISTA   SET NTI STATION FOR LOOKUP                   
         DROP  RF                                                               
*                                                                               
GODLU14  MVC   G.GTDMHUT,NDBOOK+1                                               
         LLC   RF,NDBOOK+1                                                      
         SLL   RF,4                                                             
         STC   RF,LKUCTRL                                                       
         OC    G.GTDMHUT,LKUCTRL                                                
*                                                                               
         MVC   G.GTDMCBL,QCBLDEM   PASS CABLE OVERRIDE TO SPGETDEM              
*                                                                               
         CLI   QHUT1,C'B'          SETUP FOR NBOOK(RADIO ONLY)                  
         BNE   *+14                                                             
         MVC   G.GTDMHUT,QHUT1+1                                                
         B     GODLU20                                                          
*                                                                               
         CLI   MBDEMTYP,3                                                       
         BE    GODLU20                                                          
         CLI   MBDEMTYP,6                                                       
         BE    GODLU20                                                          
*                                                                               
         CLC   QHUT1,=C'NO'                                                     
         BE    GODLU20                                                          
*                                                                               
         MVI   G.GTDMHUT,0         SET FOR AUTO ADJUST                          
         CLC   QHUT1,=C'  '                                                     
         BE    GODLU20                                                          
*                                                                               
         PACK  MBDUB,QHUT1         SET ADJUSTMENT MONTH                         
         CVB   RF,MBDUB                                                         
         SLL   RF,4                                                             
         STC   RF,G.GTDMHUT                                                     
         DROP  G                                                                
*                                                                               
GODLU20  LA    R4,TOTDEM                                                        
         XC    TOTDEM,TOTDEM                                                    
         ST    R4,MBPARAM+20                                                    
*                                                                               
         MVI   LKUCTRL,0                                                        
         CLI   MEDSPILL,C'Y'                                                    
         BNE   *+8                                                              
         MVI   LKUCTRL,2                                                        
*                                                                               
         MVC   D.DEMFCOMF,ACOMFACS                                              
         MVC   D.DEMFDBLK,ADBLOCK                                               
                                                                                
* SET FLAG TO TELL SPGETDEME THAT 50EL IS PRESENT                               
                                                                                
         CLI   COMSCORE,C'1'         TEST A COMSCORE RUN                        
         JL    GODLU24                                                          
         CLI   MBDEMTYP,3            TEST RERATE                                
         JL    GODLU24                                                          
         ICM   RE,15,VPOL50EL                                                   
         JZ    GODLU24                                                          
         OC    2(160,RE),2(RE)       TEST ANY DEMOS IN IN IT                    
         JZ    GODLU24                                                          
         ST    RE,D.DEMF50EL     ===>  NOTE POINTING TO ELCODE/LEN              
         OI    D.DEMFDBLK,DEMF_50ELQ                                            
         MVC   D.DEMFVCOM,VCOMINTER                                             
         MVC   D.DEMFCOMSD,COMSCRSD    COMSCORE SURVEY DATES                    
         MVC   D.DEMFBYSD(6),BDSAVE2   PASS 3-BYTE BUY DATES                    
*                                                                               
         LA    RE,MEDPERD              POINT TO PERIOD DATES                    
         MVC   D.DEMFQSTR(4),0(RE)     PASS 2-BYTE REQUEST DATES                
*                                                                               
GODLU24  CLI   COMREQ,C'Y'                                                      
         JNE   *+8                                                              
         OI    D.DEMFDBLK,DEMF_COMREQ                                           
*                                                                               
         CLI   NSIREQ,C'Y'                                                      
         JNE   *+8                                                              
         OI    D.DEMFDBLK,DEMF_NSIREQ                                           
*                                                                               
         CLC   =C'ACT',QBOOK1      TEST ACTUAL BOOK REQUEST                     
         JNE   *+8                                                              
         OI    D.DEMFSP1W,DEMF_ACTQ                                             
         DROP  D                                                                
*                                                                               
         CLI   MBDEMTYP,3          TEST RERATE                                  
         BL    GODLU30             NO                                           
*                                                                               
         L     RE,=A(SVPPLVAL)                                                  
         MVI   0(RE),0             CLEAR PEOPLEMETER VALUE                      
         CLC   =C'YN',QAGY                                                      
         BNE   *+8                                                              
         BRAS  RE,SVPPLMTR         SAVE PEOPLEMETER ELEM                        
*                                                                               
         CLC   =C'SC',QAGY                                                      
         BNE   *+8                                                              
         BRAS  RE,SVPPLMTR         SAVE PEOPLEMETER ELEM                        
                                                                                
GODLU30  DS    0H                                                               
         OI    MBHALF,X'40'        SET FLAG THAT P3+1 IS ADDR                   
*                                                                               
         BRAS  RE,FIXDEMEL         GET PREC ADJ IN CASE OF ERROR                
*                                                                               
         GOTO1 GETDEMO,MBPARAM,ADBUY,(LKUCTRL,(R7)),(MBHALF,MBDEMD),   X        
               DEMWORK,DEMFLIST                                                 
*                                                                               
         L     RF,ADCONLST         GET ADCONLST ADDR                            
         USING SPADCONS,RF                                                      
         MVC   VDBLOCK,DEMFLIST+DEMFDBLK-DEMFACSD  RETURN A(DBLOCK)             
         DROP  RF                                                               
*                                                                               
         CLI   MBPARAM,X'45'       TEST NO STATION BOOK ERROR                   
         BNE   GODLU32             ALLOW A PATCH                                
         B     GODLU32                                                          
*                                                                               
GODLU32  L     RE,=A(SVPPLVAL)                                                  
         CLI   0(RE),0             TEST PEOPLEMETER VALUE SAVED                 
         BE    *+8                                                              
         BRAS  RE,RSPPLMTR         RESTORE PEOPLEMETER ELEM                     
*                                                                               
         CLI   0(R1),X'00'         CHECK FOR ERRORS                             
         BE    *+8                                                              
         MVI   LKUPERR,C'Y'        SET ERROR FLAG                               
*                                                                               
         CLC   =C'*TRACE',QUESTOR                                               
         BNE   GODLU34                                                          
         OC    TOTDEM+4(4),TOTDEM+4  ANY VALUE FOR DEMO 1                       
         BZ    *+8                                                              
         BRAS  RE,TRACE                                                         
*                                                                               
GODLU34  CLI   LKUPERR,C'Y'        TEST ERROR                                   
         BNE   GODLU44                                                          
         CLI   MEDSPILL,C'Y'       IS THIS A SPILL LOOKUP                       
         BNE   GODLU44                                                          
         CLI   SVD0PROF+1,C'N'                                                  
         BE    GODLU44                                                          
         DROP  R7                                                               
*                                                                               
         LA    RE,DELSAVE          YES KILL THE ORIGINAL DEMO VALUES            
         USING NDELEM,RE                                                        
*                                                                               
GODLU40  TM    RQOPT2,RQOPT2_NONTDEM  NONT DEMOS NORMALIZED?                    
         BZ    GODLU42                NO                                        
         CLI   NDEMNO,0               TEST NO MORE DEMOS                        
         BE    GODLU44                                                          
         B     GODLU42X                                                         
*                                                                               
GODLU42  CLI   NDEMNO+1,0                                                       
         BE    GODLU44                                                          
*                                                                               
GODLU42X XC    NDEMRAW,NDEMRAW                                                  
         LA    RE,8(RE)                                                         
         B     GODLU40                                                          
*                                                                               
GODLU44  CLI   DELSAVE+4,0                                                      
         BE    *+10                                                             
         MVC   PNAME,DELSAVE+4                                                  
         MVC   DELSAVE+4(16),PNAME                                              
*                                                                               
         MVC   NEWELEM,DELSAVE                                                  
*                                                                               
         CLC   QBOOK1(3),=C'ACT'                                                
         BNE   GODLU50                                                          
*                                                                               
         L     RE,ADBUY                                                         
         USING BUYREC,RE                                                        
         MVC   BDSTART(6),BDSAVE                                                
         DROP  RE                                                               
*                                                                               
GODLU50  LA    R7,DELSAVE                                                       
         ST    R7,MBDELAD                                                       
*                                                                               
GODLUX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*=================================================================*             
* RERATE USING SPOT UPGRADES                                      *             
*=================================================================*             
         SPACE 1                                                                
GODUP    DS    0H                                                               
         L     R3,ADBUY                                                         
         USING BUYREC,R3                                                        
*                                                                               
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
*                                                                               
         USING SPDEMUPD,DEMUPBLK                                                
*                                                                               
         OC    SPUPAFAC,SPUPAFAC                                                
         BNZ   GODUP04                                                          
* THIS CODE ONE TIME ONLY                                                       
         XC    WORK,WORK           SET UP 1W PROFILE KEY                        
         MVC   WORK(4),=C'S01W'                                                 
         MVC   WORK+4(3),SVAGY                                                  
         MVC   WORK+7(3),QCLT                                                   
         L     RF,ADCLT            CHECK IF CLIENT HAS OFFICE                   
         USING CLTHDR,RF                                                        
         CLI   COFFICE,X'41'                                                    
         BL    *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),COFFICE                                               
         DROP  RF                                                               
         GOTO1 GETPROF,MBPARAM,WORK,SV1WPROF,DATAMGR                            
*                                                                               
GODUP04  LA    RE,DEMWORK                                                       
         ST    RE,SPUPAREC                                                      
         MVC   SPUPAFAC,ACOMFACS                                                
         MVC   SPUPAGY,QAGY                                                     
*                                                                               
         TM    RQOPTS,RQOPTS_2DEC  TEST 2-DECIMAL FEATURE ON                    
         BZ    *+8                                                              
         OI    SPUPOPTS,X'01'      REQUEST THEM THEN !                          
*                                                                               
         L     RF,ADMARKET         NOTE ----------                              
         USING MKTRECD,RF          CALLER MUST HAVE FSPEC  GET,MARKET           
         MVC   SPUPMALF,MKTALST    IN 01 PHASE                                  
         MVC   SPUPLPM,MKTLPMDT                                                 
         DROP  RF                                                               
*                                                                               
         MVI   SPUPTYPE,3          SET TYPE = PUT UPGRADE                       
         MVI   SPUPSTYP,C'P'       SET SUBTYPE = PUT                            
         MVI   SPUPFIL,C'T'        SET FILE FOR TP                              
         CLI   Q2INDEX,C' '        ANY INDEX SUPPLIED                           
         BE    GODUP10               NO ITS A PUT UPGRADE                       
         MVC   SPUPFLD3+1(1),Q2INDEX YES ITS HPT WITH INDEX                     
         MVI   SPUPTYPE,SPUPTHPT   SET TYPE = HPT UPGRADE                       
         MVI   SPUPSTYP,0          RESET PUT INDICATOR\                         
*                                                                               
GODUP10  CLI   BDPROGT-1,0         TEST -S BUY                                  
         BE    GODUP30                                                          
*                                                                               
         LA    R0,MAXDEMO                                                       
         LA    R1,MBDEM                                                         
GODUP12  OC    0(3,R1),0(R1)                                                    
         BZ    GODUP14                                                          
         LA    R1,3(R1)                                                         
         CLI   0(R1),X'FF'         TEST SET BEFORE                              
         BE    GODUP14                                                          
         BCT   R0,GODUP12                                                       
*                                                                               
GODUP14  MVI   0(R1),X'FF'         SET EOL IN DEMO LIST FOR SPDEMUP             
*                                                                               
         CLC   0(9,R3),SVDEMKEY    TEST SAME CLT/MKT/STA                        
         BE    GODUP18                                                          
*                                                                               
         MVC   SVDEMKEY,0(R3)                                                   
         MVC   SPUPMED,MED                                                      
         MVC   SPUPCLI,CLT         EBCDIC CLIENT                                
         MVC   SPUPMKT,BUYMSTA     MARKET NUMBER                                
         GOTO1 MSUNPK,MBPARAM,(X'80',BUYMSTA),MBFULL,MBDUB                      
         MVC   SPUPSTA,MBDUB                                                    
         CLI   SPUPSTA+4,C' '                                                   
         BH    *+8                                                              
         MVI   SPUPSTA+4,C'T'                                                   
         XC    SPUPSYSE,SPUPSYSE                                                
         XC    SPUPSYSC,SPUPSYSC                                                
*                                                                               
         CLI   BUYMSTA+2,X'E8'     TEST CABLE                                   
         BL    GODUP16                                                          
         MVC   SPUPSYSE,MBDUB      MOVE SYSCODE TO SYSE                         
         MVC   SPUPSTA(3),MBDUB+5  MOVE NETWORK TO STA                          
         MVI   SPUPSTA+3,C' '                                                   
*                                                                               
GODUP16  MVI   SPUPSRC,C'N'        SET RATING SERVICE                           
         L     RE,ADCLT                                                         
         CLI   CPROF+3-CLTHDR(RE),C'0'                                          
         BE    *+8                                                              
         MVI   SPUPSRC,C'A'                                                     
*                                                                               
         MVC   MBDUB(4),QBOOK1     PUT BOOK                                     
         MVC   MBDUB+4(2),=C'01'                                                
         GOTO1 DATCON,MBPARAM,MBDUB,(3,DUB)                                     
         MVC   SPUPFLD1(2),DUB                                                  
*                                                                               
         MVC   MBDUB(4),Q2BOOK2    SHARE BOOK                                   
         GOTO1 (RF),(R1)                                                        
         MVC   SPUPFBK(2),DUB                                                   
*                                                                               
         CLI   SV1WPROF+5,C'I'     TEST EXTENDED PRECISION                      
         BNE   *+8                                                              
         OI    SPUPOPTS,X'08'                                                   
*                                                                               
         CLI   SV1WPROF+7,C'N'     NSI/USTV NORMALIZED IMPS                     
         BE    *+8                                                              
         OI    SPUPOPTS,SPOPNORM                                                
*                                                                               
GODUP18  MVC   SPUPDAY,BDDAY       SET DAY                                      
         MVC   SPUPTIM,BDPROG      SET TIME(S)                                  
         MVI   SPUPBTYP,0          RESET BOOKTYPE                               
         CLI   Q2BTYPE,C'A'        ALLOW BOOK TYPE UPGRADES                     
         BL    *+10                (BTYPE IN STATION OVERRIDES THIS)            
         MVC   SPUPBTYP,Q2BTYPE                                                 
*                                                                               
         LA    R1,BDELEM                                                        
         SR    R0,R0                                                            
*                                                                               
GODUP20  IC    R0,1(R1)                                                         
         AR    R1,R0                                                            
         CLI   0(R1),0                                                          
         BE    GODUP22                                                          
         CLI   0(R1),X'24'         TEST BOOKTYPE OVERRIDE ELEM                  
         BNE   GODUP20                                                          
         MVC   SPUPBTYP,2(R1)      SET OVERRIDE BOOKTYPE                        
*                                                                               
         CLI   SPUPBTYP,C'O'                                                    
         BNE   *+8                                                              
         MVI   SPUPBTYP,0          OLYMPICS JUST DOESN'T WORK                   
*                                                                               
GODUP22  CLI   QDEMOVRD,C'Y'       TEST DEMO MENU REQUESTED                     
         JNE   GODUP24             NO                                           
*                                                                               
GODUP24  XC    TOTDEM,TOTDEM                                                    
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         L     RF,VSPDEMUP                                                      
         DROP  RE                                                               
         GOTO1 (RF),MBPARAM,DEMUPBLK,MBDEM,TOTDEM                               
         CLI   SPUPSRC,0           IF SOURCE ZERO ON RETURN                     
         BNE   *+8                                                              
         MVI   MBPARAM,X'E2'       MOVE TO UPGRADE ERROR FIELD                  
                                                                                
* MOVE DEMOS TO DUMMY ELEMENT                                                   
                                                                                
GODUP30  LA    R0,20                                                            
         LA    R1,NDPROG-NDELEM+DELSAVE SET THE PROGRAM NAME                    
         MVC   0(L'NDPROG,R1),SPUPPRG                                           
         LA    R1,NDEMNO-NDELEM+DELSAVE                                         
         LA    R4,MBDEM                                                         
         LA    R5,TOTDEM                                                        
*                                                                               
GODUP32  MVC   0(3,R1),0(R4)       MOVE DEMO CODE                               
         MVI   3(R1),100           SET SVI                                      
*                                                                               
         CLI   MBPARAM,0           TEST ERROR IN UPGRADES                       
         BNE   *+12                YES - RETURN BUYER DEMOS                     
         CLI   BDPROGT-1,0         TEST -S BUY                                  
         BNE   *+8                                                              
         BAS   RE,GODUPD           GO POINT R5 TO VALUE IN DEMEL                
*                                                                               
         MVC   4(4,R1),0(R5)       MOVE DEMO VALUE                              
         LA    R1,8(R1)                                                         
         LA    R4,3(R4)                                                         
         CLI   0(R4),X'FF'       TEST ANY MORE DEMOS                            
         BE    GODUPX                                                           
         LA    R5,4(R5)                                                         
         BCT   R0,GODUP32                                                       
         B     GODUPX                                                           
*                                                                               
GODUPX   MVC   NEWELEM,DELSAVE     MOVE DEMO ELEMENT FOR GOOD LUCK              
         B     GODLU50                                                          
         EJECT                                                                  
*=============================================================*                 
* FIND DEMO VALUE IN OLD DEMO ELEMENT FOR -S BUYS             *                 
* AND RETURN VALUE AT 0(R5)                                   *                 
*=============================================================*                 
         SPACE 1                                                                
GODUPD   NTR1                                                                   
         XC    0(4,R5),0(R5)                                                    
         LA    R1,BDELEM                                                        
         SR    R0,R0                                                            
*                                                                               
GODUPD2  IC    R0,1(R1)            FIND DEMO ELEMENT                            
         AR    R1,R0                                                            
         CLI   0(R1),0                                                          
         BE    GODUPDX                                                          
         CLI   0(R1),2                                                          
         BNE   GODUPD2                                                          
*                                                                               
         IC    R0,1(R1)                                                         
         AHI   R0,-24                                                           
         SRL   R0,3                                                             
         LTR   R0,R0                                                            
         BNP   GODUPDX                                                          
         LA    R1,24(R1)                                                        
*                                                                               
GODUPD10 CLC   1(2,R4),1(R1)                                                    
         BE    GODUPD12                                                         
         LA    R1,8(R1)                                                         
         BCT   R0,GODUPD10                                                      
         B     GODUPDX                                                          
*                                                                               
GODUPD12 MVC   0(4,R5),4(R1)       MOVE DEMO VALUE                              
         NI    0(R5),X'7F'         TURN OFF OVERRIDE BIT                        
*                                                                               
GODUPDX  XIT1                                                                   
         DROP  R3                                                               
         LTORG                                                                  
                                                                                
*===================================================================            
* MISC. CONSTANTS FOR SPGETDEM (THESE MUST BE SAVED BETWEEN CALLS)              
*===================================================================            
                                                                                
         DS    0D                                                               
DEMFLIST DS    XL48                                                             
SVD0PROF DS    CL16                                                             
SV00PROF DS    CL16                                                             
SV1WPROF DS    CL16                                                             
SVD0KEY  DC    XL12'00'                                                         
SV00KEY  DC    XL12'00'                                                         
SV1WKEY  DS    CL12                                                             
SVDFEKEY DS    XL8                 LAST ESTHDR KEY                              
SVRQTOT  DC    PL3'0'                                                           
SVDEMKEY DS    XL9                 A-M/CLT/PRD/MKT/STA                          
         DS    0D                                                               
         DC    CL8'DEMUPBLK'                                                    
DEMUPBLK DC    XL96'00'                                                         
         EJECT                                                                  
*======================================================*                        
* ROUTINE ADDS DOLLARS FOR CURRENT SPOT                *                        
* TO ACCUMULATORS IN OTHER CURRENCY                    *                        
*======================================================*                        
         SPACE 1                                                                
EXCHANGE LR    R0,RE                                                            
         CLC   MEDLCHNK,=A(200+MEDXCHX-MEDXCHG) TEST ROOM FOR EXCHANGE          
         BL    EXCHX                                                            
         L     R1,VXCHBLK                                                       
         USING ZXCHARD,R1                                                       
*                                                                               
         L     RE,MEDX58G          C58 RESERVE GROSS                            
         A     RE,ZXC58                                                         
         ST    RE,MEDX58G                                                       
*                                                                               
         L     RE,MEDXMSG          MEDIA SERVICE FEE GROSS                      
         A     RE,XMSF                                                          
         ST    RE,MEDXMSG                                                       
*                                                                               
         L     RE,MEDXCHG          EXCHANGE GROSS DOLLARS                       
         A     RE,ZXGROSS                                                       
         ST    RE,MEDXCHG                                                       
*                                                                               
         LA    R7,MEDXCHGP         EXCHANGE GROSS PAID/UNPAID                   
         OC    RPAY,RPAY                                                        
         BNZ   *+8                                                              
         LA    R7,MEDXCHGU                                                      
         L     RE,ZXGROSS                                                       
         A     RE,0(R7)                                                         
         ST    RE,0(R7)                                                         
*                                                                               
         L     RE,MEDXCHN          EXCHANGE NET DOLLARS                         
         A     RE,ZXNET                                                         
         ST    RE,MEDXCHN                                                       
*                                                                               
         OC    RPAY,RPAY           EXCHANGE NET PAID                            
         BZ    *+16                                                             
         L     RE,ZXNET                                                         
         A     RE,MEDXCHNP                                                      
         ST    RE,MEDXCHNP                                                      
*                                                                               
         L     RE,MEDXCHT          EXCHANGE TAX DOLLARS                         
         A     RE,ZXTAX                                                         
         ST    RE,MEDXCHT                                                       
*                                                                               
         LA    R7,MEDXCHTP         EXCHANGE TAX PAID/UNPAID                     
         OC    RPAY,RPAY                                                        
         BNZ   *+8                                                              
         LA    R7,MEDXCHTU                                                      
         L     RE,ZXTAX                                                         
         A     RE,0(R7)                                                         
         ST    RE,0(R7)                                                         
*                                                                               
EXCHX    LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
*======================================================*                        
* CALCULATE C58 RESERVE AND MEDIA SERVICE FEE          *                        
* XC58 CONTAINS C58 RATE FROM GETRATE IF REPORTING     *                        
* IN CANADIAN DOLLARS                                  *                        
*======================================================*                        
         SPACE 1                                                                
GETC58   LR    R0,RE               SAVE RETURN REG                              
         L     R1,VXCHBLK                                                       
         USING ZXCHARD,R1                                                       
         OC    ZXC58,ZXC58         TEST ANY C58                                 
         BZ    GETC58X             NO - EXIT                                    
         MVC   FULL,ZXC58          KEEP IT AROUND                               
         SR    RE,RE                                                            
         L     RF,ZXGROSS           TAKE EXCHANGE GROSS                         
         SLDA  RE,1                                                             
         M     RE,FULL                                                          
         L     R1,=F'10000'                                                     
         S     R1,FULL                                                          
         DR    RE,R1                                                            
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         L     R1,VXCHBLK          RESET R1 - XCHAREA                           
         ST    RF,XC58             SET C58 DOLLARS THIS SPOT                    
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,SVSTAMSF       MEDIA SERVICE FEE                            
         BZ    GETC58X                                                          
         MR    RE,R1                                                            
         SLDA  RE,1                                                             
         D     RE,=F'10000'                                                     
         LTR   RF,RF                                                            
         BM    *+8                                                              
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         ST    RF,XMSF                                                          
*                                                                               
GETC58X  LR    RE,R0                                                            
         BR    RE                                                               
         LTORG                                                                  
         EJECT                                                                  
LNACTBK  EQU   9                                                                
MEDGBWRK DS    0D                                                               
SAVER1   DS    F                                                                
SVMEDATA DS    F                                                                
MBCURDEL DS    F                                                                
MBFULL   DS    F                                                                
MBDUB    DS    D                                                                
MBDEMD   DS    XL32                                                             
FRSTDEL  DS    F                                                                
NXTDEL   DS    F                                                                
MBPARAM  DS    6F                                                               
MBHALF   DS    H                                                                
MBSHLD   DS    H                                                                
MBEQU    DS    H                   EQUIVALENCE FACTOR HOLD                      
MBPRTNR  DS    H                                                                
RATING   DS    C                                                                
NOFLDS   DS    C                                                                
LKUPOPT  DS    CL1                 FOR WEEKLY/OVERNITE                          
LKUPERR  DS    CL1                                                              
MBDEMTYP DS    CL1                 DEMO TYPE                                    
MYMEDFLG DS    CL1                 FLAG FOR MY MEDBUFF IN USE                   
BFRAFTSW DS    CL1                                                              
MBNODEL  DS    CL1                 NUMBER OF DEMO ELEMENTS                      
CLEARDEM DS    C                                                                
GBHOOK   DS    C                                                                
MBBYTE   DS    C                                                                
DLNKSW   DS    C                   LINK DATE SWITCH                             
LKUCTRL  DS    C                                                                
SPILL    DS    CL2                                                              
*                                                                               
NSIREQ   DS    C                                                                
COMREQ   DS    C                                                                
         DS    0D                                                               
MBDEM    DS    0CL80               CURRENT DEMOS (20 X 3)+(20 X 1)              
MBDEMST  DS    CL60                                                             
MBDEMWT  DS    CL20                                                             
MAXDEMO  EQU   20                  MAX DEMOS/WEIGHTS                            
MBDELAD  DS    F                   A(FIRST DEMO ELEMENT)                        
LINNUM   DC    F'0'                                                             
MBSTART  DS    H                   START DATE                                   
MBEND    DS    H                   END DATE                                     
MBPDTES  DS    CL212               PREVIOUS DATES                               
MBDELTAB DS    15F                 DEMO ELEMENT TABLE                           
*                                  ELEMENTS WHICH ARE POINTED TO BY             
*                                  THIS TABLE HAVE BEEN SEQUENCED FOR           
*                                  THE CURRENT BRAND                            
*                                   A(ORIGINAL DEMO ELEMENT)                    
*                                   A(RERATED DEMO ELEMENT)                     
MBDADDR  DS    6F                  A(START OF NEW PERIOD TYPE)                  
MBSPOTS  DS    F                   GETRATE SPOTS                                
MBDOLS   DS    0XL16                                                            
MBGROSS  DS    F                           GROSS DOLLARS                        
MBNET    DS    F                           NET DOLLARS                          
MBTAX    DS    F                           TAX DOLLARS                          
MBXC58   DS    F                   C58 RATE FROM GETRATE                        
ELEMPAID DS    0CL8                                                             
ELEMGRS  DS    F                                                                
ELEMNET  DS    F                                                                
DEMSB    DS    CL18                STATION BOOK PARAMETER SAVE                  
GBBRND   DS    C                                                                
BOOKEQU  DC    AL1(2,2,2,5,5,5,5,7,7,11,11,11)                                  
         DS    C                                                                
MBADJSW  DS    CL1                 ADJUSTMENT REQUIRED SWITCH                   
MBGETDEM DS    C                                                                
NODEM    DS    C                   NUMBER OF DEMOS                              
PNAME    DS    CL16                                                             
         DS    0D                                                               
DELSAVE  DS    CL240               SAVE AREA FOR RERATED DEMO ELEMENTS          
TALMOD   DC    F'0'                                                             
TALFAC   DS    F                                                                
TALBASE  DS    F                                                                
DEMDISP  EQU   28                                                               
ORIGELEM DS    CL240               RESEQUENCED ORIGINAL ELEMENT                 
NEWELEM  DS    CL240               RERATED ELEMENT                              
SAVELEM  DS    CL240                                                            
         DS    0F                                                               
TOTDEM   DS    CL252               240=20 X 12 + 12X'00'                        
SVBFLAST DS    F                                                                
SVMDLAST DS    F                                                                
SVMDFRST DS    F                                                                
SVREGE   DS    F                                                                
SVBOOK   DS    CL6                                                              
BDSAVE2  DS    CL6                                                              
SVTEND   DS    CL2                                                              
BDSAVE   DS    CL6                                                              
DSDATE   DS    CL3                                                              
WORK1    DS    CL8                                                              
WORK2    DS    CL8                                                              
DEDATE   DS    CL3                                                              
PSLIST   DS    CL150                                                            
PTLEN    EQU   56                                                               
MBLEN    EQU   180                                                              
MAXENT   EQU   28                                                               
SVAFRST  DS    F                                                                
SVALAST  DS    F                                                                
DEMWORK  DS    250D                                                             
ACTAREA  DS    80D                                                              
         EJECT                                                                  
*=============================================================                  
* SAVE THE ORIGINAL MEDBUFF REQUEST POINTERS                                    
* AND BUILD NEW BUFFERS IN LOCAL STORAGE                                        
* BUFFER REQUIREMENTS ARE                                                       
* 12 BYTES + MAX MEDLCHNK OF 256 = 268 BYTES/DATE                               
* X 75 DATES MAX = 20100 BYTES                                                  
*=============================================================                  
                                                                                
SETDATES NTR1  BASE=*,LABEL=*                                                   
         MVI   MYMEDFLG,C'Y'       SET MY MEDBUFF IN USE                        
         MVC   SVAFRST,MEDAFRST    SAVE MEDBUFF START/END POINTERS              
         MVC   SVALAST,MEDALAST                                                 
* CLEAR SAVE AREA                                                               
         L     R0,=A(MYMEDBUF)                                                  
         L     R1,=A(MYMEDBFX)                                                  
         SR    R1,R0                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R5,MEDAFRST                                                      
         L     R6,MEDALAST                                                      
         L     R7,=A(ACTAREA)                                                   
         L     RE,=A(MYMEDBUF)                                                  
         LA    RF,75*12(RE)       LEAVE ROOM FOR 75 DATES                       
*                                                                               
SETDAT2  MVC   0(2,RE),0(R5)       SET START DATE                               
         ST    RF,4(RE)            SET ACCUM ADDRESS                            
         MVC   4(1,RE),8(7)        MOVE FLAGS                                   
         MVC   8(4,RE),4(R7)       MOVE MONTHLY AND WEEKLY BOOKS                
         OC    8(2,RE),8(RE)       TEST NO BOOK                                 
         BNZ   *+10                                                             
         MVC   8(2,RE),=X'5002'    THEN SET A BOOK THAT WON'T BE FOUND          
*                                                                               
         CLC   0(2,RE),0(R7)       COMPARE TO ACTBOOK DATE                      
         BNL   *+10                                                             
         MVC   0(2,RE),0(R7)                                                    
*                                                                               
         MVC   2(2,RE),2(R5)       END DATE                                     
*                                                                               
         CLC   2(2,R7),MBEND       FIX ACTAREA END DATE IF AFTER PERD           
         BNH   *+10                                                             
         MVC   2(2,R7),MBEND                                                    
*                                                                               
         CLC   2(2,RE),2(R7)                                                    
         BNH   *+10                                                             
         MVC   2(2,RE),2(R7)                                                    
*                                                                               
         CLC   2(2,RE),2(R5)                                                    
         BNE   SETDAT6                                                          
*                                                                               
SETDAT4  AHI   R5,12                                                            
         CLI   0(R5),0             CHECK FOR EMPTY SLOT                         
         BNE   SETDAT6                                                          
         CR    R5,R6               OR END OF TABLE                              
         BH    SETDAT10                                                         
         B     SETDAT4                                                          
*                                                                               
SETDAT6  CLC   2(2,RE),2(R7)       TEST REACHED END DATE YET                    
         BNE   *+8                                                              
         LA    R7,LNACTBK(R7)                                                   
*                                                                               
         AHI   RE,12               12 BYTES FOR DATES/ACCUMS                    
         A     RF,MEDLCHNK         NEXT CHUNK                                   
         L     R0,=A(MYMEDBFX)                                                  
         CR    RF,R0                                                            
         BL    *+6                                                              
         DC    H'0'                PREVENT OVERRUN                              
*                                                                               
         CLI   0(R7),X'FF'                                                      
         BE    SETDAT8                                                          
         CLI   0(R7),0                                                          
         BNE   SETDAT2                                                          
*                                                                               
SETDAT8  AHI   RE,-12                                                           
*                                                                               
SETDAT10 ST    RE,MEDALAST                                                      
         L     RE,=A(MYMEDBUF)                                                  
         ST    RE,MEDAFRST                                                      
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*===============================================================*               
* SUBROUTINE TO LOOK UP PW PERCENTAGE AND RETURN CLIENT DOLLARS *               
* ON ENTRY R6 CONTAINS REGEL ADDRESS                            *               
*===============================================================*               
         SPACE 1                                                                
GETPW    NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     RE,ADCONLST         EXTRACT PWREC ADDRESS FROM                   
         USING SPADCONS,RE         EXTENDED ADCONS                              
         L     R4,ADPWREC                                                       
         DROP  RE                                                               
*                                                                               
         L     R3,ADBUY                                                         
         USING BUYREC,R3                                                        
*                                                                               
         XC    PWKEY,PWKEY                                                      
         LA    R5,PWKEY                                                         
         USING PWRECD,R5                                                        
*                                                                               
         MVC   PWKTYP,=X'0D7A'                                                  
         MVC   PWKAGMD(3),BUYKAM   AGMD/CLT                                     
         NI    PWKAGMD,X'FF'-X'08' DROP 'ORIG' BIT                              
         MVC   PWKPRD,10(R6)       MOVE PRODUCT FROM REGEL                      
         MVC   PWKEST,BUYKEST      ESTIMATE                                     
         MVC   PWKMKT,BUYMSTA      MARKET                                       
         CLC   0(13,R4),PWKEY      TEST SAME AS PREVIOUS                        
         BE    GETPW10             YES - SKIP READ                              
         DROP  R6                                                               
*                                                                               
         XC    0(256,R4),0(R4)     CLEAR PW RECORD AREA                         
         MVC   0(13,R4),PWKEY      SAVE CURRENT KEY                             
         GOTO1 DATAMGR,PWDMCB,DMRDHI,SPTDIR,(R4),PWKEY                          
         CLC   0(13,R4),PWKEY      NO PW REC - USE EST PCT                      
         BNE   GETPW09                                                          
*                                                                               
         GOTO1 DATAMGR,PWDMCB,GETREC,SPTFILE,PWKEY+14,(R4),DMWORK               
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
GETPW09  GOTO1 DATAMGR,PWDMCB,DMRDHI,SPTDIR,SVBUYKEY,KEY                        
*                                                                               
GETPW10  L     RE,ADEST                                                         
         USING ESTHDRD,RE                                                       
*                                                                               
         ICM   R7,14,EPWPCT        GET DEFAULT PERCENTAGE                       
         SRA   R7,8                                                             
         C     R7,=X'FF800000'     TEST PW=0                                    
         BNE   *+6                                                              
         SR    R7,R7                                                            
         DROP  RE                                                               
*                                                                               
         LR    R5,R4                                                            
         LA    R5,24(R5)                                                        
*                                                                               
GETPW12  CLI   0(R5),0             TEST EOR                                     
         BE    GETPW20                                                          
*                                                                               
         CLI   0(R5),5                                                          
         BNE   GETPW14                                                          
*                                                                               
         USING PWWKEL,R5                                                        
*                                                                               
         CLC   2(2,R6),PWWKDATE    BUY ELEM DATE TO PW ELEM DATE                
         BL    GETPW20             IF LOW, DONE                                 
         ICM   R7,15,PWWKPCT       PICK UP THE PERCENTAGE                       
*                                                                               
GETPW14  SR    R0,R0                                                            
         ICM   R0,1,1(R5)                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R5,R0                                                            
         B     GETPW12                                                          
         DROP  R5                                                               
*                                                                               
GETPW20  L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         OC    VPWCALC,VPWCALC                                                  
         BNZ   GETPW24                                                          
* NEED TO LOAD PWCALC NOW                                                       
         GOTO1 LOADER,PWDMCB,=CL8'T00A79',0                                     
         ICM   RF,15,4(R1)         GET LOAD ADDRESS                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         L     RE,ADCONLST                                                      
         ST    RF,VPWCALC          SAVE ADDRESS IN COMMON STORAGE               
         DROP  RE                                                               
*                                                                               
GETPW24  DS    0H                                                               
         XC    PWBLOCK,PWBLOCK                                                  
         LA    R2,PWBLOCK                                                       
         USING PWBLKD,R2                                                        
         MVI   PWACT,PWGETBUY                                                   
         L     RE,=A(MBGROSS)                                                   
         MVC   PWACTBUY,0(RE)                                                   
         ST    R7,PWPCT                                                         
         L     RE,=A(MBTAX)                                                     
         MVC   PWTAX,0(RE)                                                      
*                                                                               
         L     RE,ADCONLST                                                      
         USING SPADCONS,RE                                                      
         L     RF,VPWCALC                                                       
         DROP  RE                                                               
*                                                                               
         GOTO1 (RF),PWDMCB,(R2)                                                 
         CLI   PWERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(MBGROSS)                                                   
         MVC   0(4,RE),PWVAL       CHANGE THE GROSS DOLLARS                     
*                                                                               
         L     RE,=A(MBTAX)                                                     
         MVC   0(4,RE),PWCLTTAX    CHANGE THE TAX DOLLARS                       
* COMPUTE NET DOLLARS                                                           
         L     R1,PWVAL                                                         
         S     R1,PWCLTTAX                                                      
         AR    R1,R1               X 2                                          
         M     R0,=F'85'                                                        
         D     R0,=F'100'                                                       
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AHI   R1,1                                                             
         SRA   R1,1                                                             
         A     R1,PWCLTTAX                                                      
         L     RE,=A(MBNET)                                                     
         ST    R1,0(RE)                                                         
*                                                                               
GETPWX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
PWDMCB   DS    6F                                                               
         DS    0D                                                               
         DC    CL8'*PWBLOCK'                                                    
PWBLOCK  DS    XL48                                                             
         DS    0D                                                               
         DC    CL8'**PWKEY*'                                                    
PWKEY    DS    XL18                                                             
         EJECT                                                                  
*==================================================================             
* OBLITERATE PEOPLEMETER BOOKTYPE FOR RERATES                                   
*==================================================================             
         SPACE 1                                                                
SVPPLMTR NTR1  BASE=*,LABEL=*                                                   
         LA    R6,BDELEM                                                        
         SR    R0,R0                                                            
SVPPL2   IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    SVPPLX                                                           
         CLI   0(R6),X'24'                                                      
         BNE   SVPPL2                                                           
         CLI   2(R6),C'P'                                                       
         BNE   SVPPLX                                                           
         MVC   SVPPLVAL,2(R6)       AND SET VALUE TO RESTORE                    
         MVI   2(R6),0              CLEAR THE BOOKTYPE                          
SVPPLX   XIT1                                                                   
         SPACE 1                                                                
*==================================================================             
* RESTORE PEOPLEMETER BOOKTYPE                                                  
* ALWAYS SEARCH FOR ELEMENT IN CASE IT HAS MOVED !                              
*==================================================================             
         SPACE 1                                                                
RSPPLMTR NTR1  BASE=*,LABEL=*                                                   
         LA    R6,BDELEM                                                        
         SR    R0,R0                                                            
RSPPL2   IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    RSPPLX                                                           
         CLI   0(R6),X'24'                                                      
         BNE   RSPPL2                                                           
         MVC   2(1,R6),SVPPLVAL     RESTORE THE VALUE                           
RSPPLX   XIT1                                                                   
*                                                                               
SVPPLVAL DS    C                                                                
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
* MODIFY DECIMAL PRECISION FOR RATINGS TO REPORTING PRECISION                   
* R7 POINTS TO 8 BYTE DEMO CODE/VALUE IN DEMO ELEMENT                           
* R1 POINTS TO EXTRACTED DEMO VALUE * SPOTS                                     
*=======================================================================        
                                                                                
ADJPREC  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         BRAS  RE,ISRATING                                                      
         BNE   ADJPRC12                                                         
*                                                                               
         LA    R0,1                SET FLAG DEMO IS A RATING                    
         TM    4(R7),X'40'         RTG HAVE 2 DEC                               
         BO    ADJPRC20            YES                                          
* 1-DEC VALUE                                                                   
         TM    RQOPTS,RQOPTS_2DEC  REPORT WANT 2-DEC                            
         BZ    ADJPRECX            NO - JWST EXIT                               
         B     ADJPRC14            YES - ADJUST                                 
*                                                                               
ADJPRC12 TM    4(R7),X'40'         IMP HAVE 2-DEC                               
         BO    ADJPRC22            YES                                          
*                                                                               
         TM    RQOPTS,RQOPTS_2DECIMP  REPORT WANT 2-DEC IMPS                    
         BZ    ADJPRECX               NO - DONE                                 
*                                                                               
ADJPRC14 ICM   RE,15,0(R1)         ADJUST 1 DECIMAL PRECISION TO 2              
         MHI   RE,10                                                            
         STCM  RE,15,0(R1)                                                      
         B     ADJPRECX                                                         
                                                                                
*=========================================================                      
* DEMO HAS 2 DEC                                                                
*=========================================================                      
                                                                                
ADJPRC20 TM    RQOPTS,RQOPTS_2DEC  USER WANT 2-DEC RTG                          
         BO    ADJPRECX            YES - DONE                                   
         B     ADJPRC24                                                         
*                                                                               
ADJPRC22 TM    RQOPTS,RQOPTS_2DECIMP  USER WANT 2-DEC IMPS                      
         BO    ADJPRECX                                                         
*                                                                               
ADJPRC24 ICM   RF,15,0(R1)         ADJUST 2 DECIMAL PRECISION TO 1              
         M     RE,=F'2'                                                         
         D     RE,=F'10'                                                        
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         STCM  RF,15,0(R1)                                                      
*                                                                               
ADJPRECX XIT1                                                                   
                                                                                
*==============================================================                 
* SUBROUTINE RETURNS WITH CC EQ IF DEMO AT 0(R7) IS A RATING                    
* ON ENTRY R7 POINTS TO 3-BYTE DEMO CODE                                        
*==============================================================                 
                                                                                
ISRATING DS    0H                                                               
         TM    RQOPT2,RQOPT2_NONTDEM  NONT DEMOS NORMALIZED?                    
         JZ    ISRTG2                 NO                                        
         CLI   0(R7),0                TEST NON-TRAD DEMO                        
         JE    ISRTG2                 NO                                        
         CLI   0(R7),C'R'                                                       
         JE    ISRTGYES                                                         
         CLI   0(R7),C'E'                                                       
         JE    ISRTGYES                                                         
         J     ISRTGNO                                                          
*                                                                               
ISRTG2   CLI   2(R7),0             NON-TRAD DEMO CATEGORY?                      
         JE    ISRTG4              YES                                          
*                                                                               
         CLI   1(R7),C'R'          SEE IF NORMAL DEMO IS A RATING               
         JE    ISRTGYES                                                         
         CLI   1(R7),C'E'                                                       
         JE    ISRTGYES                                                         
         J     ISRTGNO                                                          
*                                                                               
ISRTG4   LLC   RF,1(R7)            YES, GET THE NON-TRAD INDEX                  
         BCTR  RF,0                                                             
         MHI   RF,9                9 CHARS/DEMO IN 50EL                         
         A     RF,VPOL50EL                                                      
         LA    RF,2(RF)                                                         
         CLI   0(RF),C'R'          SEE IF NON-TRAD CTGY IS RATING               
         JE    ISRTGYES              OR EXTENDED RATING                         
         CLI   0(RF),C'E'                                                       
         JNE   ISRTGNO                                                          
*                                                                               
ISRTGYES MVI   RATING,C'Y'                                                      
         CR    RE,RE                                                            
         BR    RE                                                               
*                                                                               
ISRTGNO  MVI   RATING,C'N'                                                      
         LTR   RE,RE                                                            
         BR    RE                                                               
         EJECT                                                                  
*================================================================               
* FIX PRECISION OF VALUES IN DEMO ELEMENT                                       
* R7 POINTS TO DEMO ELEM                                                        
*================================================================               
                                                                                
FIXDEMEL NTR1  BASE=*,LABEL=*                                                   
         USING NDELEM,R7                                                        
*                                                                               
         LLC   R0,1(R7)                                                         
         AHI   R0,-24                                                           
         JZ    FIXDEMX                                                          
         SRL   R0,3                SET FOR NUMBER OF DEMOS                      
         LA    R7,NDEMNO                                                        
         DROP  R7                                                               
*                                                                               
FIXDEM2  BRAS  RE,ISRATING                                                      
         JNE   FIXDEM4                                                          
*                                                                               
         TM    4(R7),X'40'         RTG HAVE 2 DEC                               
         BO    FIXDEM10            YES                                          
* 1-DEC VALUE                                                                   
         TM    RQOPTS,RQOPTS_2DEC  REPORT WANT 2-DEC                            
         BZ    FIXDEM20            NO                                           
         B     FIXDEM6             YES - ADJUST                                 
*                                                                               
FIXDEM4  TM    4(R7),X'40'         IMP HAVE 2-DEC                               
         BO    FIXDEM12            YES                                          
*                                                                               
         TM    RQOPTS,RQOPTS_2DECIMP  REPORT WANT 2-DEC IMPS                    
         BZ    FIXDEM20               NO - DONE                                 
*                                                                               
FIXDEM6  ICM   RE,15,4(R7)         ADJUST 1 DECIMAL PRECISION TO 2              
         N     RE,=X'3FFFFFFF'     DROP FLAGS                                   
         MHI   RE,10                                                            
         O     RE,=X'40000000'     SET 2-DEC FLAG                               
         TM    4(R7),X'80'         TEST OVERRIDE                                
         JZ    *+8                                                              
         O     RE,=X'80000000'                                                  
         STCM  RE,15,4(R7)                                                      
         B     FIXDEM20                                                         
                                                                                
*=========================================================                      
* DEMO HAS 2 DEC                                                                
*=========================================================                      
                                                                                
FIXDEM10 TM    RQOPTS,RQOPTS_2DEC  USER WANT 2-DEC RTG                          
         BO    FIXDEM20            YES - DONE                                   
         B     FIXDEM14                                                         
*                                                                               
FIXDEM12 TM    RQOPTS,RQOPTS_2DECIMP  USER WANT 2-DEC IMPS                      
         BO    FIXDEM20                                                         
*                                                                               
FIXDEM14 ICM   RF,15,4(R7)         ADJUST 2 DECIMAL PRECISION TO 1              
         N     RF,=X'3FFFFFFF'     DROP FLAGS                                   
         M     RE,=F'2'                                                         
         D     RE,=F'10'                                                        
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         TM    4(R7),X'80'         TEST OVERRIDE                                
         JZ    *+8                                                              
         O     RF,=X'80000000'                                                  
         STCM  RF,15,4(R7)                                                      
*                                                                               
FIXDEM20 LA    R7,8(R7)            NEXT DEMO                                    
         JCT   R0,FIXDEM2                                                       
*                                                                               
FIXDEMX  XIT1                                                                   
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*======================================================================         
* FOR UNWIND OPTION MODIFY BUY RECORD TO REMOVE ALL UNWANTED CHANGES            
* NOTE THAT SPREPDM WILL CHECK TO PREVENT WRITING THIS RECORD BACK !            
* UNWIND EACH RECORD ONLY ONCE !                                                
*=======================================================================        
                                                                                
SETUNWD  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,ADBUY                                                         
         USING BUYREC,R3                                                        
*                                                                               
         CLC   LASTUNWD,0(R3)                                                   
         BE    SETUNX                                                           
         MVC   LASTUNWD,0(R3)                                                   
*                                                                               
         LA    R6,BDELEM                                                        
         USING REGELEM,R6                                                       
*                                                                               
SETUN2   SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
*                                                                               
SETUN4   CLI   RCODE,0                                                          
         BE    SETUNX                                                           
         CLI   RCODE,6                                                          
         BL    SETUN2                                                           
         CLI   RCODE,7                                                          
         BNH   SETUN10                                                          
         CLI   RCODE,13                                                         
         BH    SETUN2                                                           
                                                                                
* THIS IS A POL SPOT                                                            
                                                                                
         CLC   RDATE,UNWSTRP       TEST SPOT BEFORE UNWIND PERIOD               
         BL    SETUN2              YES - KEEP IT                                
         CLC   RDATE,UNWENDP       TEST SPOT AFTER UNWIND PERIOD                
         BH    SETUN2              YES - KEEP IT                                
*                                                                               
         TM    UNWOPTS,X'80'       TEST UNWIND MAKEGOODS                        
         BZ    SETUN10             NO                                           
         OC    BDMGDATE,BDMGDATE   TEST BUY IS A MAKEGOOD                       
         BNZ   SETUN30             YES - DELETE SPOT                            
*                                                                               
         TM    RSTATUS,X'42'       TEST MISSED/MADEGOOD                         
         BNO   SETUN10             NO                                           
         NI    RSTATUS,X'FF'-X'42' RESET STATUS BITS                            
*                                                                               
         SR    R0,R0                                                            
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   RCODE,X'0C'         FIND THE -OTO THAT SHOULD FOLLOW             
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    RSTATUS,X'80'       TEST MINUS SPOT                              
         BO    *+6                                                              
         DC    H'0'                                                             
         B     SETUN30                                                          
*                                                                               
SETUN10  TM    UNWOPTS,X'40'       TEST UNWIND -OTO'S                           
         BZ    SETUN20                                                          
         CLI   BUYKEY+3,X'FF'      TEST POL BUY                                 
         BNE   SETUN12                                                          
         TM    RSTATUS,X'40'       TEST SPOT MISSED                             
         BZ    SETUN12             NO                                           
         TM    RSTATUS,X'02'       TEST MADEGOOD                                
         BO    SETUN20             YES - THEN NOT A -OTO                        
         NI    RSTATUS,X'FF'-X'40' UNSET MISSED CODE                            
*                                                                               
         SR    R0,R0               DELETE -OTO THAT SHOULD FOLLOW               
         IC    R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   RCODE,X'0C'                                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         TM    RSTATUS,X'80'       TEST MINUS                                   
         BO    *+6                                                              
         DC    H'0'                                                             
         B     SETUN30                                                          
*                                                                               
SETUN12  CLI   RCODE,7                                                          
         BNE   SETUN20                                                          
*                                                                               
         TM    RSTATUS,X'80'       TEST MINUS                                   
         BO    SETUN30                                                          
*                                                                               
SETUN20  TM    UNWOPTS,X'20'       TEST REMOVE +OTOS                            
         BZ    SETUN2              NO - CONTINUE                                
         CLI   RCODE,X'0C'         TEST OTO                                     
         BE    SETUN22                                                          
         CLI   RCODE,7                                                          
         BNE   SETUN2                                                           
*                                                                               
SETUN22  TM    RSTATUS,X'80'       TEST MINUS OTO                               
         BO    SETUN2              YES - IGNORE                                 
         B     SETUN30                                                          
*                                                                               
SETUN30  GOTO1 RECUP,MBPARAM,(R3),(R6) DELETE SPOT AND ITS ASSOCIATES           
         CLI   RCODE,0                                                          
         BE    SETUNX                                                           
         CLI   RCODE,X'10'                                                      
         BL    SETUN4                                                           
         CLI   RCODE,X'1F'                                                      
         BH    SETUN4                                                           
         B     SETUN30                                                          
*                                                                               
SETUNX XIT1                                                                     
*                                                                               
LASTUNWD DS    XL13                KEY OF LAST RECORD UNWOUND                   
                                                                                
         EJECT                                                                  
TRACE    NTR1  BASE=*,LABEL=*                                                   
         MVI   LINE,0              SUPPRESS ALL PAGING                          
         MVI   FORCEHED,C'N'                                                    
         MVI   FORCEMID,C'N'                                                    
*                                                                               
         L     R4,ADBUY                                                         
         GOTO1 HEXOUT,TRDMCB,(R4),P,13,=C'TOG'                                  
*                                                                               
         GOTO1 (RF),(R1),(R5),P+30,4        DATES IN HEX                        
*                                                                               
         L     RE,=A(LKUPERR)                                                   
         MVC   P+27(1),0(RE)                                                    
*                                                                               
         L     R4,=A(TOTDEM)                                                    
         GOTO1 (RF),(R1),(R4),P2+30,24                                          
         GOTO1 REPORT                                                           
         XIT1                                                                   
TRDMCB   DS    6F                                                               
         LTORG                                                                  
         EJECT                                                                  
DEMXTAB  DS    0D                                                               
         DS    XL64                                                             
DEMXTABX EQU   *                                                                
*                                                                               
         DS    0D                                                               
TMODAREA DS    2500C                                                            
         DS    0D                                                               
         DC    CL8'MYMEDBUF'                                                    
MYMEDBUF DS    2600D                                                            
MYMEDBFX EQU   *                                                                
                                                                                
* AREA BELOW ACQUIRED VIA NMOD                                                  
*                                                                               
DELTABD  DSECT                                                                  
DELTAB   DS    0D                                                               
         DS    2400C               ROOM FOR 600 SPOTS/AFFIDS                    
DELTABX  EQU   *                                                                
                                                                                
         PRINT OFF                                                              
ACTAREAD DSECT                                                                  
ACTSTDT  DS    XL2                 PERIOD START DATE                            
ACTENDDT DS    XL2                 PERIOD END DATE                              
ACTYYMM  DS    XL2                 MONTHLY RERATE BOOK                          
ACTYYWW  DS    XL2                 WEEKLY RERATE BOOK                           
ACTFLAG  DS    XL1                                                              
ACTFLAG_OV  EQU  X'80'             OVERNIGHT BOOK AVAILABLE                     
* SPMEDBLOCK                                                                    
       ++INCLUDE SPMEDBLOCK                                                     
* DEDBLOCK                                                                      
       ++INCLUDE DEDBLOCK                                                       
* SPGETDEMD                                                                     
       ++INCLUDE SPGETDEMD                                                      
         EJECT                                                                  
*SPDEMUPD                                                                       
       ++INCLUDE SPDEMUPD                                                       
*SPDEMEXTD                                                                      
       ++INCLUDE SPDEMEXTD                                                      
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
* SPGENSTA                                                                      
STARECD  DSECT                                                                  
       ++INCLUDE SPGENSTA                                                       
* SPGENMKT                                                                      
MKTRECD  DSECT                                                                  
       ++INCLUDE SPGENMKT                                                       
         EJECT                                                                  
* SPGENCLT                                                                      
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
* SPGENBUY                                                                      
       ++INCLUDE SPGENBUY                                                       
         EJECT                                                                  
* SPGENEST                                                                      
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
       ++INCLUDE SPGENWIPW                                                      
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
         PRINT ON                                                               
ZXCHARD DSECT                                                                   
*PREFIX=Z                                                                       
       ++INCLUDE SPXCHAREA                                                      
*PREFIX=                                                                        
         EJECT                                                                  
       ++INCLUDE SPPWBLOCK                                                      
         EJECT                                                                  
       ++INCLUDE DDMASTD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'102SPMEDGETBY03/04/21'                                      
         END                                                                    
