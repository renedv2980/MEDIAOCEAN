*          DATA SET CXSAGC     AT LEVEL 013 AS OF 01/20/04                      
*CATALP CXSAGC                                                                  
         TITLE 'CXSAGC - CONTROL SECURITY EXTRACT - AGENCY RECORD'              
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
* AL4 N/A                                                            *          
* AL4 N/A                                                            *          
* AL4 A(EXTRACT CONTROL BLOCK)                                       *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSAGC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSAGC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSAGD,R2           R2=A(EXTRACT RECORD)                         
         USING CT5REC,R3           R3=A(CONFILE RECORD)                         
         L     R7,ADXBLOCK         R7=A(EXTRACT CONTROL BLOCK)                  
         USING DXBLOCKD,R7                                                      
         L     R6,DXSTPTR          RF=A(EXTRACT SYSTEM TABLE)                   
         USING SXDTABD,R6                                                       
         MVC   VERSION,SXDTVER     VERSION NUMBER                               
*                                                                               
CXSLEN   XC    CXSAGLEN,CXSAGLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSAGLEN(2),=AL2(CXSAGDL)                                        
         MVC   CXSAGTYP,CXSAGDQ                                                 
         MVI   CXSAGDX,MXTRTQ                                                   
         MVI   CXSAGDX+1,MXTRTQ                                                 
*                                  GET KEY INFO                                 
CXSAID   MVI   CXSAGAID-1,MXTRTQ   UNIT                                         
         MVC   CXSAGAID,CT5KALPH                                                
*                                                                               
         MVI   CXSAGSEC-1,MXTRTQ                                                
         MVC   CXSAGSEC,CT5KALPH                                                
*                                                                               
         LA    R4,CT5DATA          R4=A(FIRST ELEMENT)                          
*                                                                               
CXSELM   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    CXSPPS              AT EOR-GO TO PPS LOGIC                       
         CLI   0(R4),CTSEAELQ      GET SECURITY AGENCY ELEMENT                  
         BE    CXSSEC                                                           
CXSNXT   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         BNH   EXIT                EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     CXSELM                                                           
*                                                                               
         USING CTSEAD,R4                                                        
CXSSEC   EQU   *                   EXTRACT SECURITY AGENCY ID                   
         MVC   CXSAGSEC,CTSEAAID                                                
         DROP  R4                                                               
*                                                                               
CXSPPS   CLI   VERSION,3           TEST VERSION >= 3                            
         BL    CXSNXTX                                                          
*                                                                               
         MVI   CXSAGIPP-1,MXTRTQ   PLUG IN DELIMITERS FOR PPS FIELDS            
         MVI   CXSAGPWE-1,MXTRTQ                                                
         MVI   CXSAGPWW-1,MXTRTQ                                                
         MVI   CXSAGPWR-1,MXTRTQ                                                
         MVI   CXSAGPWM-1,MXTRTQ                                                
*                                                                               
         LA    R4,CT5DATA          R4=A(FIRST ELEMENT)                          
         USING CTAADD,R4                                                        
*                                                                               
CXPPS10  CLI   CTAADEL,0           TEST FOR EOR                                 
         BE    CXSNXTX                                                          
         CLI   CTAADEL,CTAADELQ    TEST FOR AGENCY ACCESS DETAILS ELEM          
         BE    CXPPS20                                                          
*                                                                               
         ZIC   RF,CTAADLEN                                                      
         CHI   RF,1                TEST FOR BAD ELEM LENGTH                     
         BNH   EXIT                                                             
         AR    R4,RF               BUMP TO NEXT ELEMENT                         
         B     CXPPS10                                                          
*                                                                               
CXPPS20  MVI   CXSAGIPP,C'N'                                                    
         TM    CTAADFLG,CTAADPRQ   TEST FOR PERSON ID REQUIRED                  
         BNO   *+8                 NO                                           
         MVI   CXSAGIPP,C'Y'                                                    
*                                                                               
         ZIC   RF,CTAADPTO         N'DAYS TO EXPIRATION                         
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSAGPWE,DUB+6(2)                                                
*                                                                               
         ZIC   RF,CTAADPTW         N'DAYS TO WARN IN ADVANCE                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSAGPWW,DUB+6(2)                                                
*                                                                               
         ZIC   RF,CTAADPRU         N'TIMES BEFORE REUSE ALLOWED                 
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSAGPWR,DUB+6(2)                                                
*                                                                               
         ZIC   RF,CTAADPML         MINIMUM LENGTH OF PASSWORD                   
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSAGPWM,DUB+6(2)                                                
         DROP  R4                                                               
*                                                                               
CXSNXTX  B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
*                                                                               
EXIT     XMOD1 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL20               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
ADXBLOCK DS    A                   A(EXTRACT CONTROL BLOCK)                     
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
VERSION  DS    XL1                 VERSION NUMBER                               
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSAGD                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         EJECT                                                                  
       ++INCLUDE DXDSECTS                                                       
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013CXSAGC    01/20/04'                                      
         END                                                                    
