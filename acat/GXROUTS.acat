*          DATA SET GXROUTS    AT LEVEL 006 AS OF 12/10/19                      
*CATALP GXROUTS                                                                 
         TITLE 'GXROUTS - CONTROL EXTRACT ROUTINES'                             
         PRINT NOGEN                                                            
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
GXROUTS  CSECT                                                                  
*                                                                               
         ENTRY GXIDIC                                                           
         ENTRY GXDSTC                                                           
         ENTRY GXDSTRC                                                          
         ENTRY GXPARC                                                           
         ENTRY GXVENC                                                           
         ENTRY GXVEFC                                                           
         ENTRY GXFEAC                                                           
         ENTRY GXBAGC                                                           
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
EXIT     XIT1                                                                   
*                                                                               
**********************************************************************          
* THESE USINGS HAVE EFFECT ACROSS ENTIRE BOOK                                   
*                                                                               
         USING LITERAL,RA                                                       
         USING WORKD,RC                                                         
**********************************************************************          
*                                                                               
GXIDIC   NMOD1 WORKL,**IDIC**,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING GXIDID,R2           R2=A(EXTRACT RECORD)                         
         USING CTIREC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
         MVC   GXIDITYP,GXIDIQ                                                  
*                                  GET KEY INFO                                 
         MVC   GXIDIUID,CTIKID                                                  
*                                                                               
         LA    R4,CTIDATA          R4=A(FIRST ELEMENT)                          
*                                                                               
GXIDI10  CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GXIDI100                                                         
*                                                                               
         CLI   0(R4),CTDSTELQ      X'30' DESTINATION DETAIL ELEMENT             
         BE    GXIDI30                                                          
         CLI   0(R4),CTUSAELQ      X'33' AGENCY EXTRA INFO ELEMENT              
         BE    GXIDI40                                                          
         CLI   0(R4),CTAGYELQ      X'06' AGENCY ALPHA ID ELEMENT                
         BE    GXIDI50                                                          
         CLI   0(R4),CTSHPELQ      X'4C' SHIPPING ELEMENT                       
         BE    GXIDI60                                                          
         CLI   0(R4),CTORGELQ      X'36' ORIGIN ELEMENT                         
         BE    GXIDI70                                                          
         CLI   0(R4),CTOCOELQ      X'42' OUTPUT TYPE ELEMENT                    
         BE    GXIDI80                                                          
*                                                                               
GXIDI20  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   EQXIT               EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GXIDI10                                                          
*                                                                               
         USING CTDSTD,R4           X'30' DESTINATION DETAIL ELEMENT             
GXIDI30  DS    0H                  DESTINATION NAME                             
         MVC   GXIDIDNM,CTDSTNAM                                                
         MVC   GXIDIDA1,CTDSTADD                                                
         MVC   GXIDILO1,CTDSTLG1                                                
         MVC   GXIDILO2,CTDSTLG2                                                
         MVC   GXIDIPOW,CTDSTPOW                                                
*                                                                               
         CLI   CTDSTLEN,166                                                     
         BL    GXIDI20                                                          
         MVC   GXIDIDA2,CTDSTAD2                                                
         MVC   GXIDIATT,CTDSTAD3                                                
         B     GXIDI20                                                          
         DROP  R4                                                               
*                                                                               
         USING CTUSAD,R4           X'33' EXTRA INFO ELEMENT                     
GXIDI40  DS    0H                  DARE PARTNER CODE                            
         MVC   GXIDIDPC,CTUSADPI                                                
*                                                                               
         OC    CTUSADMQ,CTUSADMQ                                                
         BZ    GXIDI45                                                          
*                                                                               
         EDIT  (B2,CTUSADMQ),(5,GXIDIMQI),FILL=0                                
*                                                                               
GXIDI45  DS    0H                                                               
         OC    CTUSADRC,CTUSADRC                                                
         BZ    *+10                                                             
         MVC   GXIDIRCD,CTUSADRC                                                
*                                                                               
         MVI   GXIDIXML,C'N'                                                    
         TM    CTUSADFL,CTUSADFX                                                
         BZ    *+8                                                              
         MVI   GXIDIXML,C'Y'                                                    
*                                                                               
         CLI   CTUSALEN,CTUSALN2                                                
         BL    GXIDI20                                                          
         OC    CTUSAVEN,CTUSAVEN                                                
         BZ    GXIDI20                                                          
         MVC   GXIDIVEN,CTUSAVEN                                                
*                                                                               
         B     GXIDI20                                                          
         DROP  R4                                                               
*                                                                               
         USING CTAGYD,R4           X'06' AGENCY ALPHA ID ELEMENT                
GXIDI50  DS    0H                                                               
         MVC   GXIDIAGY,CTAGYID                                                 
         B     GXIDI20                                                          
         DROP  R4                                                               
*                                                                               
         USING CTSHPD,R4           X'4C' SHIPPING ELEMENT                       
GXIDI60  DS    0H                                                               
         LLC   RF,CTSHPLEN                                                      
         AHI   RF,-(CTSHPINS-CTSHPEL)                                           
         CHI   RF,60                                                            
         BNH   *+8                                                              
         LHI   RF,60                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GXIDISH1(0),CTSHPINS                                             
*                                                                               
         LLC   RF,CTSHPLEN                                                      
         AHI   RF,-(CTSHPINS-CTSHPEL)                                           
         CHI   RF,60                                                            
         BNH   GXIDI20                                                          
         AHI   RF,-60                                                           
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GXIDISH2(0),66(R4)                                               
*                                                                               
         B     GXIDI20                                                          
         DROP  R4                                                               
*                                                                               
         USING CTORGD,R4           X'36' ORIGIN DETAIL ELEMENT                  
GXIDI70  DS    0H                                                               
         MVC   GXIDIOR1,CTORGNAM                                                
         MVC   GXIDIOR2,CTORGADD                                                
         B     GXIDI20                                                          
         DROP  R4                                                               
*                                                                               
         USING CTOCOD,R4           X'42' OUTPUT TYPE ELEMENT                    
GXIDI80  DS    0H                                                               
         MVC   GXIDIOUT,CTOCODE                                                 
         B     GXIDI20                                                          
         DROP  R4                                                               
*                                                                               
GXIDI100 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
GXIDIX   J     EQXIT                                                            
*                                                                               
*                                                                               
* DSTA RECORDS, NON-RADIO                                                       
GXDSTC   DS    0H                                                               
         LARL  RA,LITERAL                                                       
         L     R2,GXPDXBLK-GXPARMD(R1) A(DXBLOCKD)                              
         L     R2,DXAXREC-DXBLOCKD(R2) A(EXTRACT OUTPUT BUFFER)                 
         USING GXDSTD,R2           R2=A(EXTRACT RECORD)                         
         MVC   GXDSTTYP,GXDSTQ                                                  
         J     GXDST                                                            
*                                                                               
*                                                                               
* DSTA RECORDS, RADIO ONLY                                                      
GXDSTRC  DS    0H                                                               
         LARL  RA,LITERAL                                                       
         L     R2,GXPDXBLK-GXPARMD(R1) A(DXBLOCKD)                              
         L     R2,DXAXREC-DXBLOCKD(R2) A(EXTRACT OUTPUT BUFFER)                 
         USING GXDSTD,R2           R2=A(EXTRACT RECORD)                         
         MVC   GXDSTTYP,GXDSRQ                                                  
         J     GXDST                                                            
*                                                                               
*                                                                               
*                                                                               
GXDST    NMOD1 WORKL,**DST**,CLEAR=Y                                            
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING GXDSTD,R2           R2=A(EXTRACT RECORD)                         
         USING STAKEYD,R3          R3=A(GENFIL RECORD)                          
*                                                                               
* MEDIA                                                                         
         MVC   GXDSTMED,STAKMEDA                                                
* STATION                                                                       
         MVC   GXDSTSTA,STAKSTIN                                                
* EFFECTIVE DATE                                                                
         ZICM  RF,STAKEFDA,3       INVERT KEY DATE                              
         LNR   RF,RF                                                            
         STCM  RF,7,WORK                                                        
         GOTO1 ADATCON,DMCB,(3,WORK),(17,GXDSTEFF)                              
*                                                                               
         MVC   GXDSTSTT,=C'**DARE**'                                            
*                                                                               
         LA    R4,STAFSTEL         R4=A(FIRST ELEMENT)                          
*                                                                               
GX0001   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GX0100                                                           
*                                                                               
         CLI   0(R4),STAREPCQ      X'10' PRIMARY REP INFO ELEMENT               
         BE    GX0010                                                           
         CLI   0(R4),STARP2CQ      X'11' SECONDARY REP INFO ELEMENT             
         BE    GX0020                                                           
         CLI   0(R4),STASTACQ      X'20' NEW STATION ELEMENT                    
         BE    GX0030                                                           
         CLI   0(R4),STAHOMCQ      X'30' HOME MARKET ELEMENT                    
         BE    GX0040                                                           
         CLI   0(R4),STAH2CQ       X'31' HIDDEN RECEIVING ID                    
         BE    GX0050                                                           
         CLI   0(R4),STABLELQ      X'40' BILL TO ELEMENT                        
         BE    GX0060                                                           
         CLI   0(R4),X'F1'         ACTVEL, ACTIVITY ELEMENT                     
         BE    GX0070                                                           
*                                                                               
GX0005   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   EQXIT               EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GX0001                                                           
*                                                                               
         USING STAREPD,R4          X'10' PRIMARY REP INFO ELEMENT               
GX0010   DS    0H                                                               
         OC    STAREPCR,STAREPCR                                                
         BZ    GX0012                                                           
         MVC   GXDSTREP,STAREPCR   CURRENT REP                                  
*                                                                               
GX0012   DS    0H                                                               
         OC    STAREPED,STAREPED   EFFECTIVE DATE                               
         BZ    GX0014                                                           
         GOTO1 ADATCON,DMCB,(6,STAREPED),(17,GXDSTRDA)                          
*                                                                               
GX0014   DS    0H                                                               
         OC    STAREPPR,STAREPPR                                                
         BZ    GX0016                                                           
         MVC   GXDSTPRE,STAREPPR   PREVIOUS REP                                 
*                                                                               
GX0016   DS    0H                                                               
         MVI   GXDSTEIN,C'N'                                                    
         TM    STAREPFG,STAREPFG_EI ELECTRONIC INVOICE ENABLED?                 
         BZ    GX0005                                                           
         MVI   GXDSTEIN,C'Y'                                                    
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
         USING STAREPD,R4          X'11' SECONDARY REP INFO ELEMENT             
GX0020   DS    0H                                                               
         OC    STAREPCR,STAREPCR                                                
         BZ    GX0005                                                           
         MVC   GXDSTSCR(L'STAREPCR),STAREPCR                                    
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
*                                                                               
         USING STASTAD,R4          X'20' NEW STATION ELEMENT                    
GX0030   DS    0H                                                               
         OC    STASTA,STASTA       NEW CALL LETTERS                             
         BZ    GX0005                                                           
         MVC   GXDSTNCL,STASTA                                                  
*                                                                               
         OC    STASTADT,STASTADT   EFFECTIVE DATE                               
         BZ    GX0005                                                           
         GOTO1 ADATCON,DMCB,(6,STASTADT),(17,GXDSTSDA)                          
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
         USING STAHOMD,R4          X'30' HOME MARKET ELEMENT                    
GX0040   DS    0H                                                               
         OC    STAHOMCT,STAHOMCT                                                
         BZ    GX0042                                                           
         MVC   GXDSTHMC,STAHOMCT                                                
*                                                                               
GX0042   DS    0H                                                               
         OC    STAHOMIC,STAHOMIC                                                
         BZ    GX0005                                                           
         MVC   GXDSTRID,STAHOMIC                                                
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
         USING STAH2D,R4                                                        
GX0050   DS    0H                                                               
         OC    STAH2IC,STAH2IC                                                  
         BZ    GX0005                                                           
         MVC   GXDSTSLC,STAH2IC                                                 
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
         USING STABLELD,R4         X'40', BILL TO ELEMENT                       
GX0060   DS    0H                                                               
         OC    STABLWHO,STABLWHO                                                
         BZ    GX0005                                                           
         MVC   GXDSTBTO,STABLWHO                                                
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
         USING ACTVD,R4            X'F1', ACTIVITY ELEMENT                      
GX0070   DS    0H                                                               
         OC    ACTVCHDT,ACTVCHDT                                                
         BZ    GX0005                                                           
         GOTO1 ADATCON,DMCB,(3,ACTVCHDT),(17,GXDSTCHA)                          
         B     GX0005                                                           
         DROP  R4                                                               
*                                                                               
GX0100   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
GXDSTX   J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
GXPARC   NMOD1 WORKL,**PARC**,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING GXPARD,R2           R2=A(EXTRACT RECORD)                         
         USING CTEPRECD,R3         R3=A(GENFIL RECORD)                          
*                                                                               
         MVC   GXPARTYP,GXPARQ                                                  
* PARTNER CODE                                                                  
         MVC   GXPARCOD,CTEPKREP                                                
*                                                                               
         LA    R4,CTEPDAT          R4=A(FIRST ELEMENT)                          
*                                                                               
GXP001   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GXP0100                                                          
*                                                                               
         CLI   0(R4),CTEPRELQ      X'02' REP PARTNER PRIMARY INFO ELEM          
         BE    GXP010                                                           
         CLI   0(R4),X'F1'         ACTVEL, ACTIVITY ELEMENT                     
         BE    GXP040                                                           
*                                                                               
GXP005   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   EQXIT               EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GXP001                                                           
*                                                                               
         USING CTEPRD,R4           X'02' REP PARTNER PRIMARY INFO ELEM          
GXP010   DS    0H                                                               
         OC    CTEPRMED,CTEPRMED                                                
         BZ    GXP012                                                           
         MVC   GXPARMED,CTEPRMED   MEDIA                                        
*                                                                               
GXP012   DS    0H                                                               
         OC    CTEPVNAM,CTEPVNAM   VENDOR NAME                                  
         BZ    GXP014                                                           
         MVC   GXPARVEN,CTEPVNAM                                                
*                                                                               
GXP014   DS    0H                                                               
         OC    CTEPPNAM,CTEPPNAM                                                
         BZ    GXP016                                                           
         MVC   GXPARPNM,CTEPPNAM   PARTNER NAME                                 
*                                                                               
GXP016   DS    0H                                                               
         XR    RF,RF                                                            
         ICM   RF,1,CTEPLPRF         USERID PREFIX LENGTH                       
         BZ    GXP018              NEXT ELEMENT                                 
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     GXP018                                                           
         MVC   GXPARPRF(0),CTEPPREF   USERID PREFIX                             
*                                                                               
GXP018   DS    0H                                                               
         OC    CTEPVERS,CTEPVERS                                                
         BZ    GXP020                                                           
*                                                                               
         EDIT  (B1,CTEPVERS),(3,GXPARVER),ALIGN=LEFT,ZERO=NOBLANK               
*                                                                               
         LA    RF,GXPARVER+L'GXPARVER-1                                         
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         LA    RF,1(RF)                                                         
         MVI   0(RF),C'.'                                                       
         EDIT  (B1,CTEPVERS+1),(3,1(RF)),ALIGN=LEFT,ZERO=NOBLANK                
*                                                                               
GXP020   DS    0H                                                               
         MVI   GXPARNOF,C'N'                                                    
         TM    CTEPFLG1,CTE1NOOF                                                
         BZ    *+8                                                              
         MVI   GXPARNOF,C'Y'                                                    
*                                                                               
         MVI   GXPARTST,C'N'                                                    
         TM    CTEPFLG1,CTE1TSTR                                                
         BZ    *+8                                                              
         MVI   GXPARTST,C'Y'                                                    
*                                                                               
         MVI   GXPARNDR,C'N'                                                    
         TM    CTEPFLG1,CTE1NDAR                                                
         BZ    *+8                                                              
         MVI   GXPARNDR,C'Y'                                                    
*                                                                               
         MVI   GXPARMOR,C'N'                                                    
         TM    CTEPFLG1,CTE1MORP                                                
         BZ    *+8                                                              
         MVI   GXPARMOR,C'Y'                                                    
*                                                                               
         MVI   GXPARXML,C'N'                                                    
         TM    CTEPFLG1,CTE1XMLR                                                
         BZ    *+8                                                              
         MVI   GXPARXML,C'Y'                                                    
*                                                                               
         MVI   GXPARNDS,C'N'                                                    
         TM    CTEPFLG1,CTE1NDDS                                                
         BZ    *+8                                                              
         MVI   GXPARNDS,C'Y'                                                    
*                                                                               
         DROP  R4                                                               
         B     GXP005                                                           
*                                                                               
*                                                                               
         USING ACTVD,R4            X'F1', ACTIVITY ELEMENT                      
GXP040   DS    0H                                                               
         OC    ACTVCHDT,ACTVCHDT                                                
         BZ    GXP005                                                           
         GOTO1 ADATCON,DMCB,(3,ACTVCHDT),(17,GXPARCHA)                          
         B     GXP005                                                           
         DROP  R4                                                               
*                                                                               
GXP0100  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
GXPARX   J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
GXVENC   NMOD1 WORKL,**VENC**,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING GXVEND,R2           R2=A(EXTRACT RECORD)                         
         USING CTEVRECD,R3         R3=A(GENFIL RECORD)                          
*                                                                               
         MVC   GXVENTYP,GXVENQ                                                  
*                                                                               
         MVC   GXVENCOD,CTEVKVND   VENDOR CODE                                  
*                                                                               
         CLI   CTEVKARS,CTEVKRPQ   REP?                                         
         BNE   GXV010                                                           
*                                                                               
         MVC   GXVENPAR,CTEVKUSC                                                
         OC    CTEVKOFF,CTEVKOFF                                                
         BZ    GXV010                                                           
         MVC   GXVENOFF,CTEVKOFF                                                
*                                                                               
GXV010   DS    0H                                                               
         CLI   CTEVKARS,CTEVKAGQ   AGENCY?                                      
         BNE   GXV020                                                           
         MVC   GXVENAGY,CTEVKRAG                                                
         OC    CTEVKROF,CTEVKROF                                                
         BZ    GXV020                                                           
         MVC   GXVENOFF,CTEVKROF                                                
*                                                                               
GXV020   DS    0H                                                               
         CLI   CTEVKARS,CTEVKLSQ   LOCAL STATION?                               
         BNE   *+10                                                             
         MVC   GXVENSTA,CTEVKUID                                                
*                                                                               
* IF EXTRATING VENDOR FEATURES, STOP AFTER POPULATING KEY FIELDS                
*                                                                               
         CLI   VENFEA,C'Y'                                                      
         JE    EQXIT                                                            
*                                                                               
         LA    R4,CTEVDAT          R4=A(FIRST ELEMENT)                          
*                                                                               
GXV030   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GXV060                                                           
*                                                                               
         CLI   0(R4),CTVDNELQ      X'03' VENDOR NAME LEEMENT                    
         BE    GXV050                                                           
*                                                                               
GXV040   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   EQXIT               EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GXV030                                                           
*                                                                               
         USING CTVDND,R4           X'03' VENDOR NAME ELEMENT                    
GXV050   DS    0H                                                               
         LLC   RF,CTVDNLEN                                                      
         AHI   RF,-3                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GXVENNAM(0),CTVDN                                                
*                                                                               
         CLC   SVVEN,GXVENCOD                                                   
         BE    GXV090                                                           
         MVC   SVVEN,GXVENCOD                                                   
         MVC   SVVENNAM,GXVENNAM                                                
         B     GXV090                                                           
*                                                                               
         DROP  R4                                                               
*                                                                               
GXV060   DS    0H                                                               
         CLC   GXVENNAM,SPACES                                                  
         BH    GXV090              VENDOR NAME POPULATED                        
* VENDOR NAME EMPTY HERE                                                        
         CLC   SVVEN,GXVENCOD      SAME VENDOR CODE?                            
         BNE   GXV090                                                           
         MVC   GXVENNAM,SVVENNAM                                                
*                                                                               
GXV090   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
GXVENX   J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
GXVEFC   NMOD1 WORKL,**VEFC**,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         MVI   VENFEA,C'Y'         INDICATE EXTRACTING FEATURES                 
         L     R1,APARM                                                         
         BRAS  RE,GXVENC           EXTRACT VENDOR RECORD                        
         MVI   VENFEA,C'N'         INDICATE EXTRACTING FEATURES                 
*                                                                               
         USING GXVEFD,R2           R2=A(EXTRACT RECORD)                         
         USING CTEVRECD,R3         R3=A(GENFIL RECORD)                          
*                                                                               
         MVC   GXVEFTYP,GXVEFQ                                                  
*                                                                               
         LA    R4,CTEVDAT          R4=A(FIRST ELEMENT)                          
*                                                                               
         L     RF,APARM                                                         
         USING GXPARMD,RF                                                       
         LLC   R5,GXPELCNT                                                      
         OI    GXPFLAG1,GXPF1_MULTIQ                                            
         AHI   R5,1                GUARANTEE AT LEAST 1 LOOP EXECUTION          
         DROP  RF                                                               
*                                                                               
GXVF030  CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GXVF080                                                          
*                                                                               
         CLI   0(R4),CTEVRELQ      X'02' VENDOR FEATURE LEEMENT                 
         BE    GXVF050                                                          
*                                                                               
GXVF040  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   GXVF080             EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GXVF030                                                          
*                                                                               
         USING CTEVRD,R4           X'02' VENDOR FEATURE ELEMENT                 
GXVF050  DS    0H                                                               
         BCT   R5,GXVF040          SKIP FIRST N=ELCOUNT ELEMENTS                
*                                                                               
         EDIT  CTEVFEAT,(3,GXVEFFEA)                                            
         DROP  R4                                                               
*                                                                               
         L     RF,APARM                                                         
         LLC   R5,GXPELCNT-GXPARMD(RF) A(DXBLOCKD)                              
         AHI   R5,1                                                             
         STC   R5,GXPELCNT-GXPARMD(RF) A(DXBLOCKD)                              
         B     GXVF090                                                          
*                                                                               
GXVF080  DS    0H                  PROCESSED ALL ELEMENTS HERE                  
         L     RF,APARM                                                         
         USING GXPARMD,RF                                                       
         MVI   GXPELCNT,X'00'                                                   
         NI    GXPFLAG1,X'FF'-GXPF1_MULTIQ                                      
         DROP  RF                                                               
         J     NEQXIT                                                           
*                                                                               
GXVF090  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
GXFEAC   NMOD1 WORKL,**FEAC**,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         MVI   DESCCNT,X'00'                                                    
         MVI   MSGCNT,X'00'                                                     
*                                                                               
         USING GXFEAD,R2           R2=A(EXTRACT RECORD)                         
         USING CTEFRECD,R3         R3=A(GENFIL RECORD)                          
*                                                                               
         MVC   GXFEATYP,GXFEAQ                                                  
*                                                                               
         EDIT  CTEFKNUM,(3,GXFEANUM)                                            
*                                                                               
         LA    R4,CTEFDAT          R4=A(FIRST ELEMENT)                          
*                                                                               
*                                                                               
GXF030   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GXF090                                                           
*                                                                               
         CLI   0(R4),CTFNMELQ      X'02' FEATURE NAME ELEMENT                   
         BE    GXF050                                                           
         CLI   0(R4),CTFDSELQ      X'03' FEATURE DESC ELEMENT                   
         BE    GXF060                                                           
         CLI   0(R4),CTMSGELQ      X'04' MESSAGE ELEMENT                        
         BE    GXF070                                                           
*                                                                               
GXF040   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   GXF090              EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GXF030                                                           
*                                                                               
         USING CTFNMD,R4           X'02' FEATURE NAME ELEMENT                   
GXF050   DS    0H                                                               
         LLC   RF,CTFNMLEN                                                      
         AHI   RF,-3                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GXFEANAM(0),CTFNM                                                
         J     GXF040                                                           
*                                                                               
         USING CTFDSD,R4           X'03' FEATURE DESC ELEMENT                   
GXF060   DS    0H                                                               
         LLC   RF,DESCCNT                                                       
         MHI   RF,L'GXFEADS1+1                                                  
         LA    RF,GXFEADS1(RF)                                                  
*                                                                               
         LLC   RE,CTFDSLEN                                                      
         AHI   RE,-3                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),CTFDS                                                    
*                                                                               
         LLC   RF,DESCCNT                                                       
         AHI   RF,1                                                             
         STC   RF,DESCCNT                                                       
         J     GXF040                                                           
*                                                                               
         USING CTMSGD,R4           X'04' MESSAGE ELEMENT                        
GXF070   DS    0H                                                               
         LLC   RF,MSGCNT                                                        
         MHI   RF,L'GXFEATX1+L'GXFEACT1+2                                       
         LA    RF,GXFEATX1(RF)                                                  
*                                                                               
         MVC   0(L'GXFEATX1,RF),CTMSGTXT                                        
         MVC   L'GXFEATX1+1(L'GXFEACT1,RF),CTMSGCTX                             
*                                                                               
         LLC   RF,MSGCNT                                                        
         AHI   RF,1                                                             
         STC   RF,MSGCNT                                                        
         J     GXF040                                                           
*                                                                               
GXF090   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
GXBAGC   NMOD1 WORKL,**BAGC**,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING GXBAGD,R2           R2=A(EXTRACT RECORD)                         
         USING BAGRECD,R3         R3=A(GENFIL RECORD)                           
*                                                                               
         MVC   GXBAGTYP,GXBAGQ                                                  
         MVC   GXBAGAGY,BAGKAGY                                                 
         MVC   GXBAGCOD,BAGKBAGY                                                
*                                                                               
         LA    R4,BAGFIRST(R3)     R4=A(FIRST ELEMENT)                          
*                                                                               
GXB030   CLI   0(R4),0             GET ELEMENT FROM RECORD                      
         BE    GXB090                                                           
*                                                                               
         CLI   0(R4),BANAMELQ      X'0A' OFFICE NAME ELEMENT                    
         BE    GXB050                                                           
*                                                                               
GXB040   SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R4)                                                         
         CH    RF,=H'1'                                                         
         JNH   GXB090              EXIT IF FUNNY ELEMENT LENGTH                 
         AR    R4,RF                                                            
         B     GXB030                                                           
*                                                                               
         USING BANAMED,R4          X'0A' OFFICE NAME ELEMENT                    
GXB050   DS    0H                                                               
         LLC   RF,BANAMLN                                                       
         CHI   RF,3                                                             
         BL    GXB040                                                           
         AHI   RF,-3               -LEN,-ELCODE, BCTR FOR EXECUTED MVC          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   GXBAGNAM(0),BANAME                                               
         J     GXB040                                                           
*                                                                               
*                                                                               
GXB090   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
COMSETUP DS    0H                                                               
         LARL  RA,LITERAL                                                       
*                                                                               
         ST    R1,APARM                                                         
         L     R1,GXPDXBLK-GXPARMD(R1) A(DXBLOCKD)                              
*                                                                               
         L     R2,DXAXREC-DXBLOCKD(R1) A(EXTRACT OUTPUT BUFFER)                 
         L     R3,DXARECB-DXBLOCKD(R1) A(RECORD TO EXTRACT)                     
*                                                                               
         L     RF,DXCOMFAC-DXBLOCKD(R1) A(DXTRACTS'S COMFACS)                   
         MVC   ADATCON,CDATCON-COMFACSD(RF) SAVE A(DATCON)                      
*                                                                               
         CLI   DXMODE-DXBLOCKD(R1),DXUPDTQ                                      
         JNE   *+8                                                              
         LA    R3,4+L'RECVHDR(R3)                                               
*                                                                               
         BR    RE                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* R2 EXPECTED TO ADDRESS EXTRACT AREA                                           
* 5-CHAR RECORD TYPE EXPECTED TO BE FILLED IN                                   
***********************************************************************         
DELIMS   NTR1  BASE=*,LABEL=*                                                   
         USING DXHDRD,R2           R2=A(EXTRACT RECORD)                         
*                                                                               
         L     R1,=V(PIDTAB)       PIDTAB FROM SPXCNVX                          
*                                                                               
DELIMS10 CLI   0(R1),X'FF'         END OF PIDTAB?                               
         BNE   *+6                                                              
         DC    H'0'                TYPE NOT FOUND IN PIDTAB                     
*                                                                               
         ICM   RF,15,0(R1)         A(REC TYPE)                                  
         CLC   DXHDRTYP,0(RF)      SAME TYPE AS OUR RECORD IN 0(R2)?            
         BE    *+12                                                             
         LA    R1,8(R1)            PIDTAB ENTRIES ARE 8 BYTES LONG              
         B     DELIMS10                                                         
*                                                                               
         LHI   R4,9                FIELD TABLE LINE LENGTH                      
         ICM   R1,15,4(R1)         A(FIELD TABLE FOR GIVEN RECORD TYPE)         
* POINT R5 TO THE LAST ENTRY IN THE TABLE                                       
         LR    R5,R1                                                            
         AH    R5,0(R1) TABLE SIZE (TABLE DBL-WORD ALIGNED, AH IS OK)           
         SR    R5,R4               A(LAST ENTRY IN THE TABLE) FOR BXLE          
*                                                                               
         LA    R1,2(R1)            ADVANCE PAST TABLE SIZE                      
*                                                                               
         LA    R3,DXHDRCTI+L'DXHDRCTI  A(1ST DELIMITER) IN XTRACT BUFFR         
*                                                                               
DELIMS20 MVI   0(R3),MXTRTQ        INSERT DELIMITER                             
         XR    RF,RF                                                            
         ICM   RF,3,0(R1)          LENGTH OF FIELD                              
*                                                                               
* TRT THE FIELD HERE, WHILE WE'RE STILL POINTING AT IT                          
*                                                                               
         LR    RE,RF               FIELD LENGTH                                 
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         TR    1(0,R3),MXTRT                                                    
*                                                                               
         LA    R3,1(RF,R3)         ADVANCE PAST THE DELIMITER AND FIELD         
         BXLE  R1,R4,DELIMS20                                                   
*                                                                               
         MVI   0(R3),MXTRTQ        TWO SEMICOLONS AT EOR                        
         MVI   1(R3),MXTRTQ                                                     
*                                                                               
         J     EQXIT                                                            
         DROP  R2                                                               
*                                                                               
*                                                                               
*                                                                               
LITERAL  DS    0D                                                               
APARM    DC    A(0)                A(PARAMETER LIST)                            
VENFEA   DC    C'N'                                                             
SPACES   DC    CL128' '                                                         
SVVEN    DC    CL(L'CTEVKVND)' '   SAVED VENDOR CODE                            
SVVENNAM DC    CL(L'CTVDN)' '      SAVED VENDOR NAME                            
       ++INCLUDE GXRECID                                                        
         LTORG                                                                  
       ++INCLUDE MXTRT                                                          
*                                                                               
*                                                                               
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
ADATCON  DS    A                   A(DATCON)                                    
DMCB     DS    6F                                                               
WORK     DS    XL128                                                            
DESCCNT  DS    X                                                                
MSGCNT   DS    X                                                                
*                                                                               
WORKL    EQU   *-WORKD                                                          
*                                                                               
*                                                                               
*                                                                               
       ++INCLUDE GXPARMD                                                        
       ++INCLUDE GXRECD                                                         
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE CTGENSTAD                                                      
       ++INCLUDE DXDSECTS                                                       
       ++INCLUDE DMRCVRHDR                                                      
       ++INCLUDE GEGENEDI                                                       
       ++INCLUDE DXHDRD                                                         
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE DDACTIVD                                                       
       ++INCLUDE GEGENBAG                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006GXROUTS   12/10/19'                                      
         END                                                                    
