*          DATA SET REBLAME    AT LEVEL 115 AS OF 07/09/03                      
*CATALP REBLAMEA                                                                
        TITLE 'REBLAME' - REPPAK RECORD AUDIT ELEM OUTPUT ROUTINE'              
*                                                                               
* INPUT  PARAMETER 1:      A(RECORD)                                            
*                                                                               
*        PARAMETER 2:   0  MAX O/P LINES OR 0 FOR ALL                           
*                     1-3  A(O/P AREA)                                          
*                          DEFINED: 1 BYTE HEADER = # OF 42-BYTE LINES          
*                                   FOLLOWED BY 42-BYTE O/P LINES               
*                                                                               
*        PARAMETER 3:      A(COMFACS)                                           
*                                                                               
*                                                                               
REBLAME  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 (WORKDX-WORKD),*BLAME*,CLEAR=YES,RR=R5                           
         USING WORKD,RC                                                         
*                                                                               
* INITIALIZATION                                                                
*                                                                               
         ST    R5,RELO                                                          
         ST    RD,SAVERD                                                        
         ST    R1,SVPARM                                                        
*                                                                               
         MVC   ELCODE,0(R1)                                                     
         CLI   ELCODE,0                                                         
         BNE   *+8                                                              
         MVI   ELCODE,X'FC'        DEFAULT TO FC ELEM CODE                      
         ZICM  RF,1(R1),3                                                       
         ST    RF,ARECORD                                                       
*                                                                               
         MVC   MAXLINES,4(R1)                                                   
         CLI   MAXLINES,8                                                       
         BNH   *+6                                                              
         DC    H'0'                MAX 8 ENTRIES                                
         CLI   MAXLINES,0                                                       
         BNE   *+8                                                              
         MVI   MAXLINES,8                                                       
         ZICM  RF,5(R1),3                                                       
         ST    RF,AOPAREA                                                       
*                                                                               
         L     RF,8(R1)            ACOMFACS                                     
         USING COMFACSD,RF                                                      
         MVC   DATCON,CDATCON                                                   
         MVC   HEXOUT,CHEXOUT                                                   
         DROP  RF                                                               
*                                                                               
         L     R2,AOPAREA          O/P AREA                                     
         MVI   0(R2),0             INITIALIZE                                   
*                                                                               
         L     R6,ARECORD                                                       
         BAS   RE,GETEL                                                         
         BNE   EXITOK                                                           
         ZIC   R5,1(R6)                                                         
         AR    R5,R6                                                            
         LA    R6,RAUDOVLQ(R6)     1ST MIN ELEM                                 
         USING RAUDMINI,R6                                                      
         LA    R4,1(R2)            1ST O/P LINE                                 
         USING OPLINE,R4                                                        
*                                                                               
BLAME050 DS    0H                                                               
         CR    R6,R5                                                            
         BNL   EXITOK                                                           
*                                                                               
         ZIC   R1,0(R2)            INCREMENT & TEST LINE COUNTER                
         LA    R1,1(R1)                                                         
         STC   R1,0(R2)                                                         
         CLC   0(R2),MAXLINES                                                   
         BH    EXITOK                                                           
*                                                                               
         GOTO1 DATCON,DMCB,(2,RAUDDATE),(11,OPDATE)                             
         GOTO1 HEXOUT,DMCB,RAUDTIME,OPTIME,1                                    
         GOTO1 HEXOUT,DMCB,RAUDTIME+1,OPTIME+3,1                                
         MVI   OPTIME+2,C':'                                                    
*                                                                               
         MVC   OPLUID,RAUDLUID                                                  
*                                                                               
         EDIT  RAUDCTID,OPCTID                                                  
*                                                                               
         MVC   OPNOTE,RAUDNOTE                                                  
*                                                                               
         LA    R4,42(R4)                                                        
         LA    R6,RAUDMLNQ(R6)     NEXT MINI ELEM                               
         B     BLAME050                                                         
**********************************************************************          
* COMMON ROUTINES                                                               
**********************************************************************          
EXITH    CLI   *,0                 SET CC HIGH                                  
         B     EXIT                                                             
EXITL    CLI   *,X'FF'                                                          
         B     EXIT                                                             
EXITNO   LTR   RB,RB               SET CC NOT EQUAL                             
         B     EXIT                                                             
OK       EQU   *                                                                
EXITOK   CR    RB,RB               SET CC EQUAL                                 
         B     EXIT                                                             
EXIT     DS    0H                  JUST EXIT                                    
         XIT1                                                                   
*                                                                               
         GETEL R6,34,ELCODE                                                     
*                                                                               
SPACES   DC    80C' '                                                           
         LTORG                                                                  
***>>>   FOR TESTING PURPOSES, KEEP THE SIZE OF THIS MODULE THE SAME            
***>>>   ORG   REBLAME+(((*-REBLAME)/2048)+1)*2048                              
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
RELO     DS    F                                                                
SVPARM   DS    F                                                                
SAVERD   DS    F                                                                
DATCON   DS    A                                                                
HEXOUT   DS    A                                                                
ARECORD  DS    A                                                                
AOPAREA  DS    A                                                                
ELCODE   DS    X                                                                
MAXLINES DS    X                                                                
BYTE     DS    X                                                                
WORK     DS    CL64                                                             
*                                                                               
WORKDX   EQU   *                                                                
OPLINE   DSECT                                                                  
OPDATE   DS    CL8                                                              
         DS    CL1                                                              
OPTIME   DS    CL5                                                              
         DS    CL2                                                              
OPLUID   DS    CL8                                                              
         DS    CL2                                                              
OPCTID   DS    CL5                                                              
         DS    CL2                                                              
OPNOTE   DS    CL10                                                             
         DSECT                                                                  
       ++INCLUDE REGENAUD                                                       
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'115REBLAME   07/09/03'                                      
         END                                                                    
