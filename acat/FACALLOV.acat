*          DATA SET FACALLOV   AT LEVEL 003 AS OF 12/18/19                      
*&&      SET   DB=N                                                             
*CATALP FACALLOV                                                                
         TITLE 'FACILITIES CALLOV - LOAD OVERLAY PHASE'                         
         PRINT NOGEN                                                            
CALLOV   CSECT                                                                  
         ENTRY XAHOLE                                                           
         NMOD1 WORKL,*CALLOV*,RA,CLEAR=YES                                      
         USING WORKD,RC                                                         
         USING PROGSPCD,PSREC      CURRENT PHASE RECORD                         
SV       USING PROGSPCD,SPSREC                                                  
         USING LIBUFFD,LIBUFF      DATASPACE     ARREDIT BLOCK                  
C        USING LIBUFFD,LIBUFFC     CORE RESIDENT ARREDIT BLOCK                  
*                                                                               
         ST    R1,SAVER1                                                        
         MVC   IPARMS,0(R1)        INCOMING PARAMETERS                          
         USING CALLOVD,IPARMS                                                   
         SAM24                     SET 24-BIT MODE                              
*                                                                               
         L     R9,VSYSFAC                                                       
         USING SYSFACD,R9          R9=A(SYSTEM FACILITY LIST)                   
         L     R3,VSYSFAC0                                                      
         USING COMFACSD,R3                                                      
         GOTO1 CPROTOFF            DISABLE STORAGE PROTECTION                   
         DROP  R3                                                               
*                                                                               
         BRAS  RE,INIT             DO INITIALISATION                            
*                                                                               
         LA    R1,CMDTAB           KNOWN ROUTINES                               
         LHI   RF,L'CMDTAB                                                      
MAIN02   CLI   0(R1),255           EOT                                          
         BE    MAIN04                                                           
         CLC   CMCMD,0(R1)         TEST MATCH                                   
         BE    MAIN04                                                           
         BXH   R1,RF,MAIN02                                                     
*                                                                               
MAIN04   ICM   RF,15,4(R1)         GO TO REQUESTED ROUTINE                      
         BNZR  RF                                                               
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* READ REQUEST                                                        *         
***********************************************************************         
READ     BRAS  RE,BLDKEY           BUILD 0SPPOO/LANG/LVL KEY                    
         CLI   USECIL,YES                                                       
         BNE   READ02                                                           
         CLI   CMCMD,CMCMDSR       SPECIAL READ?                                
         BNE   *+12                                                             
         CLI   CMTYP,CMTDIR        WANT DIRECTORY RECORD?                       
         BE    READ04              YES                                          
         B     READ10              READ FROM CIL                                
*                                                                               
READ02   CLI   CMCMD,CMCMDN        NORMAL CALL?                                 
         BNE   READ04              NO                                           
         OC    CMAPHS,CMAPHS       LOAD ADDRESS SPECIFIED ALREADY?              
         BNZ   READ04              YES                                          
         BRAS  RE,BLDMAP           BUILD MAP TABLE (IF REQUIRED)                
         BRAS  RE,GETDSPC          GET PHASE ENTRY FROM DATASPACE               
         BNE   XMOD                NOT FOUND                                    
*                                                                               
         TM    PSFLAG1,CTPHSCRQ    PHASE IS CORE RESIDENT?                      
         BZ    READ03              NO                                           
         BRAS  RE,GCORERES         GET PHASE ENTRY FOR CORE RESIDENT            
         BH    READ                TRY AGAIN                                    
         BL    XMOD                                                             
         MVI   CMCMD,0             SET OK RETURN                                
         B     XMOD                AND EXIT                                     
*                                                                               
READ03   BRAS  RE,GETPATH          FIND LONGEST PATH FOR NODE                   
         BRAS  RE,DOPGMLD          LOAD AT PATH ADDRESS                         
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* CALL IS TO LOAD TO DIRECTED ADDRESS                                 *         
***********************************************************************         
READ04   BRAS  RE,GETDSPC          GET PHASE ENTRY FROM DATASPACE               
         BNE   XMOD                NOT FOUND                                    
         CLI   CMCMD,CMCMDSR       SPECIAL READ?                                
         BNE   READ06                                                           
         CLI   CMTYP,CMTDIR        WANT DIRECTORY INFO ONLY?                    
         BNE   READ06              NO                                           
         XR    RF,RF                                                            
         ICM   RF,7,CMAPHS                                                      
         MVC   0(PROGSPCL,RF),PROGSPCD                                          
         B     READ08                                                           
*                                                                               
READ06   TM    PSFLAG1,CTPHSCRQ    REQUESTED PHASE IS CORE RESIDENT?            
         BNZ   *+12                SFC                                          
         BRAS  RE,LTOADD           LOAD TO SPECIFIED ADDRESS                    
         B     READ08                                                           
*                                                                               
         BRAS  RE,GCORERES         GET PHASE ENTRY FOR CORE RESIDENT            
         BH    READ                                                             
         BL    XMOD                                                             
*                                                                               
READ08   BRAS  RE,SCRMAP           SET SCREEN MAPPING INFO                      
         ICM   RF,15,PSLEN         RETURN DIRECTORY INFO                        
         STH   RF,CMLPHS                                                        
         TM    PSFLAG1,CTPHSCRQ    CORE RESIDENT FLAG                           
         BZ    *+8                                                              
         OI    CMIPHS,PHRES                                                     
         MVI   BYTE,0              TEST LEVEL                                   
         CLI   PSLVL,C'A'                                                       
         BNE   *+8                                                              
         MVI   BYTE,X'10'                                                       
         CLI   PSLVL,C'B'                                                       
         BNE   *+8                                                              
         MVI   BYTE,X'20'                                                       
         CLI   PSLVL,C'C'                                                       
         BNE   *+8                                                              
         MVI   BYTE,X'30'                                                       
         OC    CMIPHS,BYTE                                                      
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* CALL IS TO LOAD FROM CIL                                            *         
***********************************************************************         
READ10   CLI   CMCMD,CMCMDN        NORMAL CALL?                                 
         BE    READ18              YES                                          
         MVC   MODID,SPACES                                                     
         GOTO1 AHEXOUT,DMCB,PSNAME,MODID,L'PSNAME,0                             
         MVC   MODID+0(1),LANGDEF  LANGUAGE LETTER OF PHASE NAME                
         CLI   PSLVL,C' '          TEST LEVEL?                                  
         BNH   *+10                NO                                           
         MVC   MODID+6(1),PSLVL    SET LEVEL                                    
*                                                                               
         XR    RF,RF               SET ADDRESS                                  
         ICM   RF,7,CMAPHS                                                      
         ST    RF,OVLYADDR                                                      
         MVC   OVLYLEN,EFFS        SET DUMMY LENGTH                             
*                                                                               
         BRAS  RE,SSET             SUSPEND TIMER                                
         BRAS  RE,GETFRCIL         READ PHASE FROM CIL                          
         BNE   READ16                                                           
*                                                                               
         ICM   R2,15,ATCB          IF LOADING INTO TCBPGMA MAKE SURE IT         
         BZ    READ12              DAMN WELL FITS                               
         USING TCBD,R2                                                          
*                                                                               
         XR    R0,R0               GET LOAD TO ADDRESS                          
         ICM   R0,7,CMAPHS                                                      
         C     R0,TCBPGMA                                                       
         BL    READ12              NOT IN PROGRAMS AREA                         
         C     R0,TCBPGMX                                                       
         BH    READ12              NOT IN PROGRAMS AREA                         
         LR    RF,R0                                                            
         A     RF,OVLYLEN                                                       
         BCTR  RF,0                                                             
         C     RF,TCBPGMX                                                       
         BNH   READ12              IT WILL FIT IN PROGRAMS AREA                 
         MVC   OVLYLEN,EFFS                                                     
         B     READ14              SET PHASE WONT FIT IN PGM AREA               
         DROP  R2                                                               
*                                                                               
READ12   L     RE,OVLYADDR         GET MOVE FROM ADDRESS                        
         L     RF,OVLYLEN                                                       
         XR    R0,R0               GET MOVE TO ADDRESS                          
         ICM   R0,7,CMAPHS                                                      
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE PHASE TO REQUIRED LOCATION              
*                                                                               
READ14   LA    R0,MODID            REMOVE PHASE                                 
         DELETE EPLOC=(0)                                                       
         BRAS  RE,RSET             RESTART TIMER                                
         MVI   CMCMD,0                                                          
         CLC   OVLYLEN,EFFS                                                     
         JE    *+2                 DIE IF PHASE WONT FIT                        
*                                                                               
         BRAS  RE,SCRMAP           SET SCREEN MAPPING INFO                      
         B     XMOD                                                             
*                                                                               
READ16   BRAS  RE,RSET                                                          
         MVI   CMCMD,X'FF'         NO MATCHING LEVEL FOUND                      
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* NORMAL LOAD FROM CIL                                                *         
***********************************************************************         
READ18   L     R3,BASEMAPA                                                      
         USING MPAREAD,R3                                                       
         XR    R0,R0                                                            
READ20   CLI   MPNODE,0            LOOK FOR THIS NODE OR EMPTY SLOT             
         BE    READ22                                                           
         ICM   RF,15,MPLEN         GET LENGTH TO THIS ONE                       
         AR    R0,RF                                                            
         AHI   R3,MPAREAL                                                       
         B     READ20                                                           
*                                                                               
READ22   ICM   R2,15,ATCB                                                       
         JZ    *+2                                                              
         USING TCBD,R2                                                          
         A     R0,TCBPGMA                                                       
         STCM  R0,7,CMAPHS         SET LOAD ADDRESS                             
         ST    R0,OVLYADDR                                                      
         MVC   OVLYLEN,EFFS        SET DUMMY LENGTH                             
*                                                                               
         MVC   MODID,SPACES                                                     
         GOTO1 AHEXOUT,DMCB,PSNAME,MODID,L'PSNAME,0                             
         MVC   MODID+0(1),LANGDEF  LANGUAGE LETTER OF PHASE NAME                
         CLI   PSLVL,C' '          TEST LEVEL?                                  
         BNH   *+10                NO                                           
         MVC   MODID+6(1),PSLVL    SET LEVEL                                    
*                                                                               
         BRAS  RE,SSET                                                          
         BRAS  RE,GETFRCIL         READ PHASE FROM CIL                          
         BNE   READ24              NOT THERE                                    
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS                                                      
         A     R0,OVLYLEN                                                       
         C     R0,TCBPGMX                                                       
         JH    *+2                 PHASE IS PAST END OF TCB                     
*                                                                               
         MVI   MPNODE,255          SET THIS NODE USED                           
         MVC   MPLEN,OVLYLEN                                                    
         DROP  R3                                                               
*                                                                               
         L     RE,OVLYADDR         GET MOVE FROM ADDRESS                        
         L     RF,OVLYLEN                                                       
         XR    R0,R0               GET MOVE TO ADDRESS                          
         ICM   R0,7,CMAPHS                                                      
         LR    R1,RF                                                            
         MVCL  R0,RE               MOVE PHASE TO REQUIRED LOCATION              
*                                                                               
         LA    R0,MODID            REMOVE PHASE                                 
         DELETE EPLOC=(0)                                                       
         BRAS  RE,RSET             RESTART TIMER                                
         MVI   CMCMD,0                                                          
         CLC   OVLYLEN,EFFS                                                     
         JE    *+2                 DIE IF PHASE WONT FIT                        
*                                                                               
         BRAS  RE,SCRMAP           SET SCREEN MAPPING INFO                      
         B     XMOD                                                             
*                                                                               
READ24   BRAS  RE,RSET                                                          
         MVI   CMCMD,X'FF'         NO MATCHING LEVEL FOUND                      
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* WRITE REQUEST                                                       *         
***********************************************************************         
WRITE    CLI   CMTYP,CMTDIR        WRITE DIRECTORY RECORD?                      
         BNE   WRT02               NO                                           
         ICM   R2,15,CLADS         GET REQUESTED KEY                            
DS       USING PROGSPCD,R2                                                      
         MVC   PSREC(PSKEYL),DS.PROGSPCD                                        
*                                                                               
         MVI   CLCMD,0             SET OK                                       
         MVC   TPSKEY,PSREC                                                     
         MVI   LIACTN,LIAHIGH      TRY TO READ THE RECORD                       
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         MVI   LIACTN,LIAWRT       WRITE RECORD                                 
         CLC   TPSKEY,PROGSPCD     FOUND IT?                                    
         BE    *+8                 YES                                          
         MVI   LIACTN,LIAADD       ADD RECORD                                   
*                                                                               
         MVC   PSREC(PROGSPCL),DS.PROGSPCD                                      
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLI   LIRTN,LIROK                                                      
         BE    XMOD                                                             
         MVI   CLCMD,255                                                        
         B     XMOD                                                             
         DROP  DS                                                               
         EJECT                                                                  
***********************************************************************         
* WRITE FROM LOADLIB TO DATASPACE                                     *         
***********************************************************************         
WRT02    XR    R2,R2                                                            
         ICM   R2,7,CLALL          R2 = A(LOADLIB PHASE)                        
LL       USING PROGSPCD,R2         LOADLIB PHASE RECORD                         
*                                                                               
         CLI   USECIL,YES          WRITE FROM LOADLIB TO DSPACE?                
         BNE   WRT08               NO                                           
         TM    LL.PSFLAG1,CTPHSDMQ DUMMY PHASE?                                 
         BO    WRT08               YES - DO SPECIAL STUFF                       
*                                                                               
         BRAS  RE,SSET             SUSPEND TIMER                                
*                                                                               
         MVC   MODID,SPACES                                                     
         GOTO1 AHEXOUT,DMCB,LL.PSNAME,MODID,L'PSNAME,0                          
         MVC   MODID+0(1),LL.PSLANG                                             
*                                                                               
         CLI   LL.PSLVL,C' '       TEST LEVEL?                                  
         BNH   *+10                NO                                           
         MVC   MODID+6(1),LL.PSLVL                                              
*                                                                               
         LA    R0,MODID            POINT TO MODULE NAME                         
         LOAD  EPLOC=(0),ERRET=WRITERR                                          
*                                                                               
         ST    R0,OVLYADDR                                                      
         LA    RF,0(R1)            CALCULATE PHASE LEN                          
         SLL   RF,3                                                             
         ST    RF,OVLYLEN          AND SET IT                                   
*                                                                               
         TM    LL.PSFLAG1,CTPHSCRQ                                              
         BZ    WRT06               IGNORE UNLESS CORE RESIDENT                  
         XR    R2,R2                                                            
         BRAS  RE,LAMR2                                                         
         USING FAPGMSD,R2                                                       
         ICM   RE,15,PGMSLST       LEAST AMOUNT OF CORE IN ANY SYSTEM           
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
         LTR   RE,RE               IF NOT SET ASSUME IT WILL FIT                
         BZ    WRT06                                                            
         L     RF,OVLYLEN                                                       
         CR    RF,RE               SEE IF IT FITS IN SMALLEST                   
         BNH   WRT06                                                            
*                                                                               
WRT04    SR    RF,RE                                                            
         STH   RF,CMLPHS           SET LENGTH IT WON'T FIT BY                   
         MVI   CMIPHS,255          FLAG BAD FIT                                 
*                                                                               
         LA    R0,MODID            DELETE FORCE LOADED PHASE                    
         DELETE EPLOC=(0)                                                       
         B     WRITERR             SET ERROR                                    
*                                                                               
WRT06    L     R2,CLADS            NOW SET DATASPACE KEY                        
DS       USING PROGSPCD,R2                                                      
         MVC   PSREC(PSKEYL),DS.PROGSPCD                                        
*                                                                               
         MVI   CLCMD,0             SET OK                                       
         BRAS  RE,PUTDRCT          PUT PHASE TO DATASPACE                       
*                                                                               
         LA    R0,MODID            DELETE FORCE LOADED PHASE                    
         DELETE EPLOC=(0)                                                       
         BRAS  RE,RSET             RESTART TIMER                                
         B     XMOD                                                             
*                                                                               
WRITERR  BRAS  RE,RSET             RESTART TIMER                                
         MVI   CLCMD,X'FF'         SET ERROR                                    
         B     XMOD                                                             
         DROP  LL                                                               
         EJECT                                                                  
***********************************************************************         
* WRITE FROM CORE TO DATASPACE                                        *         
***********************************************************************         
WRT08    XR    RF,RF               GET A(PHASE) IN CORE                         
         ICM   RF,7,CMAPHS                                                      
         ST    RF,OVLYADDR                                                      
         LLH   RF,CMLPHS           GET NEW LENGTH (IF SET)                      
         ST    RF,OVLYLEN                                                       
*                                                                               
         ICM   RF,7,CMNPHS         PHASE RECORD DETAILS                         
         MVC   PSREC,0(RF)                                                      
         MVI   CLCMD,0             SET OK                                       
         BRAS  RE,PUTDRCT                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO WRITE OR ADD A PHASE + RECORD TO DATASPACE               *         
***********************************************************************         
PUTDRCT  NTR1                                                                   
         MVC   TPSKEY,PSREC                                                     
         MVI   LIACTN,LIAHIGH      TRY TO READ THE RECORD                       
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         MVI   LIACTN,LIAWRT       SET TO WRITE RECORD                          
         CLC   TPSKEY,PROGSPCD     FOUND RECORD?                                
         BE    PTD02               YES                                          
*                                                                               
         MVI   LIACTN,LIAADD       SET TO ADD RECORD                            
         XC    PSREC,PSREC                                                      
         MVC   PSREC(PSKEYL),TPSKEY                                             
*                                                                               
PTD02    ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,LOCKME           LOCK TABLE                                   
         BRAS  RE,PUTITIN          MOVE PROGRAM TO DSPACE                       
         BNE   PTDERR                                                           
*                                                                               
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLI   LIRTN,LIROK                                                      
         BNE   PTDERR                                                           
         ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,FREEME                                                        
         B     EXITOK                                                           
*                                                                               
PTDERR   ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,FREEME                                                        
         MVI   CLCMD,X'FF'         PUTDRCT RETURNED ERROR                       
         B     EXITL                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOAD PROGRAM INTO CURRENT SLOT (IF IT FITS) OR NEW ONE   *         
* NTRY:  FAPROGSPCD FILLED IN                                         *         
*        OVLYADDR = A(OVERLAY IN CORE)                                *         
*        OVLYLEN  = L'OVERLAY IN CORE (IF ZERO - USE OLD LENGTH)      *         
*        ASSUMES TABLE (AND HENCE PGMS AREA) IS LOCKED                *         
***********************************************************************         
PUTITIN  NTR1                                                                   
         XR    R0,R0                                                            
         ICM   R0,3,PSCOUNT        INCREMENT NUMBER OF LOADS                    
         AHI   R0,1                                                             
         STCM  R0,3,PSCOUNT                                                     
*                                  UPDATE TIME OF LOAD                          
         GOTO1 ADATTIM,DMCB,(X'01',PSTIME),0                                    
         CLI   4(R1),0                                                          
         BNE   EXITL               DATTIM NOT HAPPY                             
         TM    PSFLAG1,CTPHSDMQ    DUMMY PHASE                                  
         BO    EXITOK              YES, JUST EXIT - THIS CLEARS SLOT            
*                                                                               
         SAM31                                                                  
         ICM   RF,15,PSLENH        LENGTH OF SLOT FOR THIS PROGRAM              
         BZ    PIN02               NO SLOT - NEED TO GET ONE                    
*                                                                               
         OC    OVLYLEN,OVLYLEN     USE OLD LENGTH IF ZERO                       
         BNZ   *+10                                                             
         MVC   OVLYLEN,PSLEN       USE OLD LENGTH IF ZERO                       
         L     RE,OVLYLEN          LENGTH OF PHASE TO LOAD                      
         CR    RE,RF                                                            
         BH    PIN02               NEED A NEW SLOT                              
*                                                                               
         LAM   AR0,ARF,ARZERO      PUT PHASE INTO OLD SLOT                      
         LAM   AR2,AR2,ALET                                                     
         ICM   R2,15,PSADR         A(PHASE IN DATASPACE)                        
         ICM   R3,15,PSLENH        LENGTH OF SLOT FOR PHASE                     
*                                                                               
         ICM   RE,15,OVLYADDR      A(PHASE IN CORE)                             
         ICM   RF,15,OVLYLEN       LENGTH OF PHASE TO LOAD                      
         SAC   512                                                              
         MVCL  R2,RE               COPY IN PHASE                                
         BRAS  RE,ARSOFF                                                        
         MVC   PSLEN,OVLYLEN                                                    
         B     EXITOK              WILL TAKE US OUT OF XA                       
*                                                                               
PIN02    LAM   AR0,ARF,ARZERO      PHASE WON'T FIT - GET NEW SLOT               
         LAM   AR2,AR2,ALET                                                     
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,PGMSPGM-FAPGMSD(R2)                                        
         USING PGAREAD,R2                                                       
*                                                                               
         L     RF,OVLYLEN          LENGTH OF PHASE TO LOAD                      
         STCM  RF,15,PSLEN                                                      
         AHI   RF,63                                                            
         SRL   RF,6                                                             
         SLL   RF,6                ROUND UP TO NEXT 64 BYTES                    
         STCM  RF,15,PSLENH                                                     
*                                                                               
PIN04    ICM   RE,15,PGANOW                                                     
         STCM  RE,15,PSADR                                                      
         LA    R0,0(RE,RF)         MAKE SURE THE FUCKER FITS                    
         CLM   R0,15,PGALAST                                                    
         BNH   PIN06                                                            
         BRAS  RE,ARSOFF           MUST REBUILD PROGRAMS DATASPACE              
         L     RE,=A(UPDFULL)                                                   
         MVC   MSG(L'UPDFULL),0(RE)                                             
         BRAS  RE,DOMSG                                                         
         BRAS  RE,ARSOFF                                                        
         B     EXITL                                                            
*                                                                               
PIN06    CS    RE,R0,PGANOW        SWAP IN THE NEW LENGTH                       
         BNE   PIN04                                                            
         DROP  R2                                                               
*                                                                               
         ICM   R2,15,PSADR         A(PHASE IN DATASPACE)                        
         ICM   R3,15,PSLENH        L'SLOT FOR PHASE IN DATASPACE                
         L     RE,OVLYADDR         A(PHASE IN CORE)                             
         L     RF,OVLYLEN          L'PHASE IN CORE                              
         SAC   512                                                              
         MVCL  R2,RE                                                            
         BRAS  RE,ARSOFF                                                        
         B     EXITOK              WILL TAKE US OUT OF XA                       
         EJECT                                                                  
***********************************************************************         
* GET PHASE IN MODID FROM LOADLIB                                     *         
***********************************************************************         
GETFRCIL NTR1                                                                   
         MVC   DUB,MODID           SAVE ORIGINAL MODULE NAME                    
GCF02    LA    R0,MODID            POINT TO MODULE NAME                         
         LOAD  EPLOC=(0),ERRET=GCF04                                            
*                                                                               
         ST    R0,OVLYADDR         PHASE IN LIBRARY R0=ADR,R1=LEN               
         LA    RF,0(R1)            CALCULATE PHASE LEN                          
         SLL   RF,3                                                             
         ST    RF,OVLYLEN                                                       
         B     EXITOK                                                           
*                                                                               
GCF04    CLC   LANGDEF,MODID       FOREIGN VERSION NOT FOUND                    
         BE    GCF08                                                            
         CLI   MODID+6,C' '        FOREIGN TEST VERSION?                        
         BE    GCF06               YES                                          
         CLI   CMTYP,CMTADD        FORCE READ FOR ONLY THIS LEVEL?              
         BE    EXITL               YES - NOT FOUND                              
         MVI   MODID+6,C' '        TRY FOR LIVE FOREIGN VERSION                 
         B     GCF02                                                            
*                                                                               
GCF06    MVC   MODID+0(1),LANGDEF  TRY TEST ENGLISH VERSION                     
         MVC   MODID+6(1),DUB+6                                                 
         B     GCF02                                                            
*                                                                               
GCF08    CLI   MODID+6,C' '        ENGLISH VERSION NOT FOUND                    
         BE    EXITL                                                            
         CLI   CMTYP,CMTADD        FORCE READ FOR ONLY THIS LEVEL?              
         BE    EXITL               YES - NOT FOUND                              
         MVI   MODID+6,C' '        TRY FOR LIVE ENGLISH VERSION                 
         B     GCF02                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD INITIAL 0SPPOO/LANG/LVL KEY FOR LOOKUP                        *         
***********************************************************************         
BLDKEY   NTR1                                                                   
         L     R2,ATCB                                                          
         USING TCBD,R2                                                          
         XR    R0,R0               DEFAULT IS SYSTEM HOST LANGUAGE              
         ICM   R3,15,AUTL                                                       
         USING UTLD,R3                                                          
         BZ    *+8                                                              
         IC    R0,TLANG            EXTRACT LANGUAGE CODE FROM UTL               
         CHI   R0,8                                                             
         BNH   *+6                                                              
         XR    R0,R0               SET TO ENGLISH IF UNKNOWN                    
*                                                                               
         L     RF,ALANGTAB         GET LANGUAGE CHARACTERS                      
         MH    R0,0(RF)                                                         
         AHI   RF,6                                                             
         USING LANGTABD,RF                                                      
         MVC   LANGDEF,LANGOVL1    DEFAULT LANGUAGE CHARACTER                   
         AR    RF,R0                                                            
         MVC   LANGCHR,LANGOVL1    USER LANGUAGE CHARACTER                      
         MVC   PSLANG,LANGCHR                                                   
         DROP  RF                                                               
*                                                                               
         MVC   PSOVR,CMOVR         NORMAL CALL PASSES X'OO' ONLY                
         CLI   CMCMD,CMCMDN        NORMAL CALL?                                 
         BE    BKY04               YES                                          
*                                                                               
         MVC   PSNAME,CMNPHS       ASSUME MAINT CALL                            
         NI    PSSYS,X'0F'                                                      
         OC    PSNAME(2),PSNAME    CALLER SET X'0SPP' CORRECTLY                 
         BNZ   BKY06               YES - DON'T USE TCB VALUES                   
*                                                                               
BKY04    MVC   PSSYS,TCBOVSYS                                                   
         MVC   PSPRG,TCBPRG                                                     
*                                                                               
         LTR   R3,R3               UTL AROUND?                                  
         BZ    BKY14               NO                                           
         CLI   TCBOVSYS,1          SERVICE REQUEST?                             
         BE    BKY06               YES                                          
         MVC   PSSYS,TCOSYS        SET OVERRIDE SYSTEM                          
*                                                                               
BKY06    LTR   R3,R3               UTL AROUND?                                  
         BZ    BKY14               NO                                           
         OC    TSVCREQ,TSVCREQ     SERVICE REQUEST?                             
         BZ    BKY10               NO                                           
         CLC   PSNAME(2),=X'000A'  CORE MODULE                                  
         BE    BKY10               YES                                          
*                                                                               
         TM    TTEST1,TTESTSRY     LOADLIB MODE FOR S/R?                        
         BZ    BKY08                                                            
         MVI   USECIL,YES                                                       
         TM    TTEST1,TTESTSRA                                                  
         BZ    BKY12                                                            
         MVI   PSLVL,C'A'          SET LEVEL A TEST MODE                        
         B     BKY12                                                            
*                                                                               
BKY08    TM    TTEST1,TTESTURT     USE REAL TTEST?                              
         BZ    BKY12               NO                                           
*                                                                               
BKY10    MVC   BYTE,TTEST          EXTRACT TEST STATUS FROM UTL                 
         NI    BYTE,TTESTLVL       SET TEST LEVEL                               
         XR    R1,R1                                                            
         IC    R1,BYTE                                                          
         LA    R1,LVLTRN(R1)                                                    
         MVC   PSLVL,0(R1)         SET TEST LEVEL REQUIRED                      
*                                                                               
BKY12    TM    TTEST,TTESTTAC                                                   
         BZ    BKY14                                                            
         MVC   TERMID,TNUM         SET TEST TERM NUM                            
         MVC   TESTID,TACCS        SET TEST USER ID                             
*                                                                               
BKY14    ICM   RE,15,TCBMAP        CHECK IF PHASE MAP ALREADY EXISTS            
         LA    R1,MPHDRL(RE)                                                    
         ST    R1,BASEMAPA                                                      
         CLI   PSSYS,0             NO PHASE MAP FOR SYS ZERO                    
         BNE   BKY16                                                            
         TM    TTEST1,TTESTURT     USE REAL TTEST?                              
         BNO   BLDKEYX                                                          
*                                                                               
         USING MPHDRD,RE                                                        
BKY16    CLC   MHSP,PSNAME         SAME LEVEL/PROGRAM/LANGUAGE                  
         BNE   BKY18                                                            
         CLC   PSLVL,MHLVL                                                      
         BNE   BKY18                                                            
         CLC   PSLANG,MHLANG                                                    
         BNE   BKY18                                                            
         B     BLDKEYX                                                          
*                                                                               
BKY18    XC    MPHDRD(MPHDRL),MPHDRD                                            
         L     R0,BASEMAPA         CLEAR PHASE MAP FOR TASK                     
         LHI   R1,MAPLEN                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         DROP  RE                                                               
*                                                                               
BLDKEYX  TM    TTEST1,TTESTURT     USE REAL TTEST?                              
         BO    *+8                                                              
         BRAS  RE,CHKVRSN          ADJUST FOR VERSION CONTROL                   
         MVC   SPSREC,PSREC        SAVE REQUESTED RECORD INFO                   
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* CHECK VERSION TABLE FOR PHASE ENTRY                                 *         
***********************************************************************         
         PUSH  USING                                                            
         USING DMSPACED,WORK                                                    
CHKVRSN  NTR1                                                                   
         ICM   RF,15,=AL4(DTVRSN)                                               
         BRAS  RE,ASKME                                                         
         ICM   R2,15,DSPTFRST                                                   
         N     R2,=XL4'0FFFFFFF'                                                
         LH    RE,DSPTWIDE                                                      
         L     RF,DSPTEND                                                       
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,ALETT                                                    
         SAC   512                                                              
         USING VRSNTABD,R2                                                      
*                                                                               
         MVC   FULL(3),PSSYS       COPY PHASE TO FULL                           
         OI    FULL,X'80'          +X'80' BIT                                   
*                                                                               
CKV02    OC    VRSNPGM,VRSNPGM     EOT?                                         
         BZ    CKVX                YES                                          
         CLC   VRSNPHS,FULL        FIND IT IN VERSION TABLE                     
         BE    *+12                                                             
CKV04    BXLE  R2,RE,CKV02                                                      
         B     CKVX                                                             
*                                                                               
CKV08    ICM   R3,15,VSSB                                                       
         USING SSBD,R3                                                          
         OC    VRSNADV,VRSNADV     CHECK ADV NUM                                
         BZ    *+14                                                             
         CLC   SSBSYSID,VRSNADV                                                 
         BE    CKV10                                                            
*                                                                               
         ICM   R3,15,AUTL                                                       
         BZ    CKV04                                                            
         USING UTLD,R3                                                          
         OC    VRSNSEN,VRSNSEN     CHECK SE NUM                                 
         BZ    *+14                                                             
         CLC   TSYS,VRSNSEN                                                     
         BE    CKV10                                                            
*                                                                               
         OC    VRSNAGY,VRSNAGY     CHECK TAGY                                   
         BZ    *+14                                                             
         CLC   TAGY,VRSNAGY                                                     
         BE    CKV10                                                            
         B     CKV04                                                            
*                                                                               
CKV10    XR    RF,RF              SET TEST LEVEL                                
         IC    RF,VRSNABC         SET TEST LEVEL                                
         N     RF,=AL4(TTESTLVL)                                                
         LA    RF,LVLTRN(RF)                                                    
         MVC   PSLVL,0(RF)         SET TEST LEVEL REQUIRED                      
*                                                                               
CKVX     LAM   AR2,AR2,ARZERO                                                   
         SAC   0                                                                
         B     EXITOK                                                           
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* GET BEST MATCH FOR PHASE ENTRY FROM DATASPACE INTO PSREC            *         
* MATCHES ARE PERFORMED IN THIS ORDER:                                *         
* 1. TEST + LANGUAGE                                                  *         
* 2. LIVE + LANGUAGE                                                  *         
* 3. TEST + ENGLISH                                                   *         
* 4. LIVE + ENGLISH                                                   *         
***********************************************************************         
GETDSPC  NTR1                                                                   
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
*                                                                               
         CLC   PSLANG,LANGDEF      ANY LANGUAGE?                                
         BE    GDR04               NO                                           
         CLI   PSLVL,0             ANY TEST LEVEL?                              
         BE    GDR02               NO                                           
         BRAS  R2,GDGETIT          LANGUAGE + TEST LEVEL                        
*                                                                               
GDR02    MVI   PSLVL,0             LANGUAGE + LIVE LEVEL                        
         BRAS  R2,GDGETIT                                                       
*                                                                               
GDR04    CLI   PSLVL,0             ANY TEST LEVEL?                              
         BE    GDR06               NO                                           
         MVC   PSLANG,LANGDEF      ENGLISH + TEST LEVEL                         
         BRAS  R2,GDGETIT                                                       
*                                                                               
GDR06    MVC   PSLANG,LANGDEF      ENGLISH + LIVE LEVEL                         
         MVI   PSLVL,0                                                          
         BRAS  R2,GDGETIT                                                       
         XC    PSREC,PSREC                                                      
         MVI   CMCMD,X'FF'         NO MATCHING PHASE FOUND                      
         B     EXITL                                                            
*                                                                               
GDGETIT  MVC   TPSKEY,PSREC        CALL ARREDIT FOR THIS PHASE                  
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   TPSKEY,PSREC        MATCH?                                       
         BNE   GDGNO               NO                                           
         CLI   PSSTAT,255          IS THIS DELETED?                             
         BNE   EXITOK              NO - RETURN IT                               
*                                                                               
GDGNO    MVC   PSREC,SPSREC        RESTORE CALLER'S KEY                         
         BR    R2                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD MAP TABLE ENTRIES FOR EACH PHASE                   *         
* NTRY:  ASSUMES PSREC AND SPSREC FILLED IN CORRECTLY                 *         
***********************************************************************         
BLDMAP   NTR1                                                                   
         MVC   XPSREC,PSREC        SAVE REAL PSREC VALUE                        
         L     RF,ATCB                                                          
         L     RF,TCBMAP-TCBD(RF)                                               
         USING MPHDRD,RF                                                        
         MVC   MHID,=CL4'MAPD'                                                  
         MVC   MHSP,PSNAME         SET LEVEL/PROGRAM/LANGUAGE                   
         MVC   MHLVL,PSLVL                                                      
         MVC   MHLANG,PSLANG                                                    
         DROP  RF                                                               
*                                                                               
         MVC   SVCMD,CMCMD         SAVE OFF RETURN CODE                         
         SLR   R0,R0               R0 = OVERLAY                                 
*                                                                               
BMP02    XC    PSREC,PSREC         READ FOR ONLY 0SPPOO                         
         MVC   PSNAME,SV.PSNAME                                                 
         STC   R0,PSOVR            SET OO FROM R0                               
*                                                                               
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,PROGSPCD                                                      
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   PSNAME(2),SV.PSNAME CHECK 0SPP IS STILL THE SAME                 
         BNE   BMP12               NO - FINISHED                                
*                                                                               
         MVC   PSLANG,SV.PSLANG    RESET TO OUR LANGUAGE/LEVEL                  
         MVC   PSLVL,SV.PSLVL                                                   
         MVC   SPSREC,PSREC        SPOOF GETDSPC                                
*                                                                               
         IC    R0,PSOVR            SAVE TIME LATER - SAVE THIS IN R0            
         BRAS  RE,GETDSPC          GET BEST VALUE                               
         BNE   BMP10               NADA                                         
*                                                                               
BMP04    CLI   PSNODES,0                                                        
         BE    BMP10               IGNORE ZERO NODE PHASES                      
         TM    PSFLAG1,(CTPHSSCQ+CTPHSCSQ)                                      
         BNZ   BMP10               IGNORE SCREENS AND CORERES                   
*                                                                               
         L     R3,BASEMAPA                                                      
         USING MPAREAD,R3                                                       
BMP06    CLI   MPNODE,0            LOOK FOR THIS NODE OR EMPTY SLOT             
         BE    BMP08                                                            
         CLC   PSNODES,MPNODE                                                   
         BE    BMP08                                                            
         AHI   R3,MPAREAL                                                       
         B     BMP06                                                            
*                                                                               
BMP08    MVC   MPNODE,PSNODES      SET LONGEST MATCH FOR THIS NODE              
         TM    PSFLAG1,CTPHSCRQ                                                 
         BZ    *+10                                                             
         XC    PSLEN,PSLEN                                                      
         CLC   MPLEN,PSLEN                                                      
         BH    BMP10                                                            
         MVC   MPLEN,PSLEN                                                      
         MVC   MPLANG,PSLANG                                                    
         MVC   MPOVR,PSOVR                                                      
         MVC   MPLVL,PSLVL                                                      
         DROP  R3                                                               
*                                                                               
BMP10    MVC   PSREC,XPSREC        RESTORE ORIGINAL PSREC                       
         AHI   R0,1                                                             
         CHI   R0,255              STILL WITHIN RANGE?                          
         BNH   BMP02               YES                                          
*                                                                               
BMP12    L     R3,BASEMAPA         SORT NODES INTO ASCENDING ORDER              
         USING MPAREAD,R3                                                       
*                                                                               
BMP14    CLI   MPNODE,0            END OF LIST?                                 
         BE    BMP22               YES                                          
         LA    R4,MPAREAL(R3)                                                   
NXT      USING MPAREAD,R4                                                       
*                                                                               
BMP16    CLI   NXT.MPNODE,0        END OF LIST?                                 
         BE    BMP20               YES                                          
         CLC   MPNODE,NXT.MPNODE   UNSORTED NODE IS LOWER THAN SORTED           
         BL    BMP18               NO                                           
         XC    MPNODE(MPAREAL),NXT.MPNODE                                       
         XC    NXT.MPNODE(MPAREAL),MPNODE                                       
         XC    MPNODE(MPAREAL),NXT.MPNODE                                       
*                                                                               
BMP18    AHI   R4,MPAREAL          NEXT IN UNSORTED LIST                        
         B     BMP16                                                            
*                                                                               
BMP20    AHI   R3,MPAREAL                                                       
         B     BMP14                                                            
         DROP  R3,NXT                                                           
*                                                                               
BMP22    MVC   PSREC,XPSREC        RESTORE ORIGINAL PSREC                       
         MVC   SPSREC,XPSREC       RESTORE ORIGINAL SPSREC                      
         MVC   CMCMD,SVCMD                                                      
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* GO FIND LONGEST PATH TO THIS OVERLAY USING MAP TABLE                *         
* EXIT:  TOTLEN  = LENGTH OF LONGEST PATH                             *         
***********************************************************************         
GETPATH  NTR1                                                                   
         XC    TOTLEN,TOTLEN       CLEAR PATH LENGTH                            
         CLI   PSOVR,0             BASE PHASE HAS NO LINKAGE                    
         BE    EXITOK                                                           
         XC    WORK,WORK           FIRST BUILD NODE LIST INTO WORK              
         LA    R2,WORK             BUILD IN REVERSE SEQUENCE ORDER              
         L     RF,ATCB                                                          
*                                                                               
         L     R4,BASEMAPA                                                      
         USING MPAREAD,R4                                                       
GP01     CLI   0(R4),0                                                          
         JE    *+2                                                              
         CLC   PSNODES,MPNODE                                                   
         BE    GP02                                                             
         AHI   R4,MPAREAL                                                       
         B     GP01                                                             
         DROP  R4                                                               
*                                                                               
GP02     LR    R1,R4               R1 = LAST FOUND ENTRY                        
         USING MPAREAD,R1                                                       
         PACK  STNODE,MPNODE       GET START NODE                               
         NI    STNODE,X'0F'                                                     
         CLI   STNODE,0            STNODE IS ZERO FOR ROOT PHASE                
         BE    GP08                                                             
*                                                                               
         L     R1,BASEMAPA                                                      
GP04     MVC   NDNODE,MPNODE       GET END NODE                                 
         NI    NDNODE,X'0F'                                                     
         CLC   STNODE,NDNODE       START NODE = END NODE?                       
         BE    GP06                YES - SAVE NODE                              
         AHI   R1,MPAREAL                                                       
         CLI   0(R1),0                                                          
         BNE   GP04                                                             
         DC    H'0'                DIE IF CANT MATCH NODES                      
*                                                                               
GP06     MVC   0(1,R2),STNODE      SAVE THIS NODE                               
         AHI   R2,1                NEXT NODE SAVE AREA                          
         LR    R4,R1               SAVE NEW START NODE                          
         B     GP02                                                             
*                                                                               
GP08     LA    R2,WORK             GET LONGEST PHASE FOR EACH NODE PAIR         
*                                                                               
GP10     OC    0(2,R2),0(R2)       REACHED END OF NODES?                        
         BZ    EXITOK              YES                                          
         PACK  BYTE,1(1,R2)        CREATE 'START-END' NODE IN BYTE              
         OC    BYTE,0(R2)                                                       
         XR    R0,R0               R0 = LENGTH OF LONGEST MATCH                 
         L     R1,BASEMAPA                                                      
*                                                                               
GP12     CLC   MPNODE,BYTE         MATCH NODES?                                 
         BNE   GP14                NO                                           
         ICM   R0,15,MPLEN                                                      
*                                                                               
GP14     AHI   R1,MPAREAL                                                       
         CLI   0(R1),0             REACHED HIGH ADDRESS?                        
         BNE   GP12                NO - NEXT NODE                               
         A     R0,TOTLEN                                                        
         ST    R0,TOTLEN                                                        
         AHI   R2,1                POINT TO NEXT START NODE                     
         B     GP10                                                             
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* DO LOAD TO NODE DETERMINED ADDRESS                                  *         
* NTRY:  TOTLEN  = DISPLACEMENT TO PROGRAM AREA LOAD POINT            *         
***********************************************************************         
DOPGMLD  NTR1                                                                   
         L     R3,ATCB                                                          
         USING TCBD,R3                                                          
         L     RF,TCBPGMA                                                       
         A     RF,TOTLEN                                                        
         STCM  RF,7,CMAPHS         SET OVERLAY LOAD ADDRESS                     
*                                                                               
         ICM   R0,15,PSLEN         MAKE SURE OVERLAY WILL FIT                   
         BCTR  R0,0                                                             
         AR    RF,R0                                                            
         C     RF,TCBPGMX                                                       
         JH    *+2                 DIE IF PHASE WONT FIT                        
*                                                                               
         BRAS  RE,LTOADD           NOW GO DO LOAD                               
*                                                                               
         ICM   R1,15,AUTL          TEST FOR DEBUG TERMINAL                      
         BZ    EXITOK                                                           
         TM    TSTAT6-UTLD(R1),TST6DBUG                                         
         BZ    EXITOK                                                           
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,7,CMAPHS                                                      
         C     RE,TCBPGMA          TEST PHASE IS WITHIN TASK                    
         BL    DPL02                                                            
         C     RE,TCBPGMX                                                       
         BH    DPL02                                                            
         CLC   0(2,RE),=X'90EC'                                                 
         BNE   DPL02                                                            
         XC    0(2,RE),0(RE)                                                    
*                                                                               
DPL02    B     EXITOK                                                           
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* GET ADDRESS OF A  ORE RESIDENT PHASE                                *         
***********************************************************************         
GCORERES NTR1                                                                   
         MVC   DPSREC,PSREC        SAVE DATASPACE PHASE RECORD FOUND            
DS       USING PROGSPCD,DPSREC                                                  
*                                                                               
         MVI   C.LIACTN,LIAHIGH    SEE IF RECORD ALREADY IN CORE                
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,C.LIBUFFD                                          
         CLC   PSREC(PSKEYL),DPSREC                                             
         BE    GCO02               YES IT IS                                    
*                                                                               
         MVC   PSREC(PROGSPCL),DPSREC                                           
         MVC   PSADR,=AL4(PSADRL)                                               
         MVC   PSTIME,EFFS                                                      
         XC    PSLENH,PSLENH                                                    
         XC    PSLEN,PSLEN                                                      
         XC    PSCHAIN,PSCHAIN                                                  
         MVI   C.LIACTN,LIAADD     ADD NEW RECORD TO LIST IN CORE               
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLI   LIRTN,LIROK                                                      
         JNE   *+2                 CANNOT ADD TO CORE                           
*                                                                               
GCO02    CLC   PSTIME,DS.PSTIME    CORE LOADED AT SAME TIME OR SOONER           
         BE    GCO04               YES                                          
         BRAS  RE,RLDACORE         RELOAD CORERES PHASE                         
         BE    GCO04                                                            
         BH    EXITH                                                            
         MVI   CLCMD,X'FF'         ERROR                                        
         B     EXITL                                                            
*                                                                               
GCO04    TM    PSFLAG1,CTPHSCSQ    CORERES SCREEN?                              
         BNO   GCO08               NO                                           
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS         MUST PASS ADDRESS FOR CORERES SCREEN         
         JZ    *+2                                                              
*                                                                               
         ICM   RE,15,PSCHAIN       PHASE IS IN THE HOLE?                        
         BZ    *+12                NO                                           
         LA    RE,HOPHASE-HOLED(RE)                                             
         B     GCO06                                                            
         ICM   RE,15,PSADR         MOVE BACK CORERES SCREEN                     
*                                                                               
GCO06    ICM   R1,15,PSLEN                                                      
         ICM   RF,15,PSLEN                                                      
         MVCL  R0,RE                                                            
         B     GCO20                                                            
*        B     EXITOK                                                           
*                                                                               
GCO08    ICM   RF,15,PSCHAIN       PHASE IS IN THE HOLE?                        
         BZ    GCO10               NO                                           
         LA    RF,HOPHASE-HOLED(RF)                                             
         STCM  RF,7,CMAPHS                                                      
         B     GCO20                                                            
*        B     EXITOK                                                           
*                                                                               
GCO10    ICM   RF,15,PSADR                                                      
         STCM  RF,7,CMAPHS         RETURN ADDRESS OF CORERES PHASE              
         B     GCO20                                                            
*        B     EXITOK                                                           
*                                                                               
GCO20    ICM   RF,15,VPHSCNT                                                    
         BZ    EXITOK                                                           
         XC    DUB,DUB                                                          
         MVC   DUB(L'PSNAME),PSNAME                                             
         GOTO1 (RF),DMCB,(C'A',DUB),0                                           
         B     EXITOK                                                           
         DROP  DS                                                               
                                                                                
***********************************************************************         
* ROUTINE TO RELOAD CORERES PHASES                                    *         
* NTRY:  PSREC  = CORERES   PROGSPCD ENTRY                            *         
*        DPSREC = DATASPACE PROGSPCD ENTRY                            *         
***********************************************************************         
DS       USING PROGSPCD,DPSREC                                                  
RLDACORE NTR1                                                                   
         BRAS  RE,SSET             SUSPEND TIMER                                
         L     R2,AHOLE            NEED A FRESH SLOT FOR THIS PHASE             
         USING HOLED,R2                                                         
         L     RF,AHOLE                                                         
         A     RF,LHOLE                                                         
         BCTR  RF,0                RF = A(END OF HOLE)                          
*                                                                               
RLC02    OC    HOKEY,HOKEY         END OF STUFF IN THE HOLE?                    
         BZ    RLC04               YES                                          
         ICM   RE,15,HPSLENH       NEXT SLOT                                    
         AHI   RE,HOLELQ                                                        
         BXLE  R2,RE,RLC02                                                      
         B     RLC08               NO FREE CORE TO LOAD PHASE                   
*                                                                               
RLC04    AHI   RF,1                                                             
         SR    RF,R2               RF = LENGTH OF HOLE REMAINING                
*                                                                               
         ICM   RE,15,DS.PSLENH     WILL THIS PHASE FIT IN HOLE?                 
         AHI   RE,HOLELQ                                                        
         CR    RF,RE                                                            
         BL    RLC08               NO - HOLE IS FULL                            
*                                                                               
         ICM   RF,15,PSCHAIN       ALREADY LOADED BEFORE?                       
         BZ    RLC06               NO                                           
*                                                                               
OLD      USING HOLED,RF                                                         
         MVC   OLD.HOKEY,EFFS      FLAG OLD HOLE ENTRY AS FREE                  
         DROP  OLD                                                              
*                                                                               
RLC06    STCM  R2,15,PSCHAIN       FILL OUT NEW HOLE DETAILS                    
         XR    R0,R0                                                            
         ICM   R0,3,PSCOUNT        INCREMENT NUMBER OF LOADS                    
         AHI   R0,1                                                             
         STCM  R0,3,PSCOUNT                                                     
*                                                                               
         MVC   PSTIME,DS.PSTIME    SET NEW LOAD TIME                            
         MVC   HPSTIME,DS.PSTIME                                                
*                                                                               
         MVC   HOKEY,DS.PSNAME     SET KEY AND LENGTHS IN HOLE                  
         MVC   HPSLENH,DS.PSLENH                                                
         MVC   HPSLEN,DS.PSLEN                                                  
*                                                                               
         SAM31                                                                  
         LAM   AR0,ARF,ARZERO                                                   
         LAM   ARE,ARE,ALET        MOVE PHASE INTO HOLE IN CORE                 
         ICM   RE,15,DS.PSADR      RE = DATASPACE                               
         ICM   RF,15,DS.PSLEN                                                   
         TM    PSFLAG1,CTPHSDMQ    DUMMY PHASE                                  
         BZ    *+6                 NO                                           
         XR    RF,RF               CLEAR PHASE TO ZEROS                         
*                                                                               
         LA    R0,HOPHASE          R0 = CORE                                    
         ICM   R1,15,HPSLENH                                                    
         SAC   512                                                              
         MVCL  R0,RE                                                            
         SAC   0                                                                
         LAM   ARE,ARE,ARZERO                                                   
         SAM24                                                                  
*                                                                               
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
         GOTO1 VCALLOV,DMCB,0,(C'D',0),0                                        
         BRAS  RE,RSET             RESTART TIMER                                
         B     EXITOK                                                           
*                                                                               
RLC08    CLC   PSTIME,EFFS         IS THIS AN ADD THAT DOESN'T FIT?             
         BNE   RLC10               NO                                           
*                                                                               
         MVC   PSADR,=AL4(PSADRF)  SET FAILED                                   
         MVC   CLALL,=AL4(PSADRF)  SET FAILED                                   
*                                                                               
         MVC   MODID,SPACES        SET PHASE NAME FOR WARNING                   
         GOTO1 AHEXOUT,DMCB,PSNAME,MODID,L'PSNAME,0                             
         MVC   MODID+0(1),LANGDEF                                               
         CLI   PSLVL,C' '                                                       
         BNH   *+10                                                             
         MVC   MODID+6(1),PSLVL                                                 
         MVC   UPDNLD+6(8),MODID                                                
*                                                                               
         MVC   MSG(L'UPDWARN),UPDWARN                                           
         BRAS  RE,DOMSG                                                         
         MVC   MSG(L'UPDNLD),UPDNLD                                             
         BRAS  RE,DOMSG                                                         
*                                                                               
         MVI   PSSTAT,255          FLAG DELETED                                 
         MVI   C.LIACTN,LIAWRT     WRITE IT BACK                                
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         BRAS  RE,RSET             RESTART TIMER                                
*                                                                               
         ICM   RF,15,AUTL                                                       
         TM    TSTATC-UTLD(RF),TSTCLOAD  $LOAD                                  
         BNE   EXITH                                                            
         MVI   CMCMD,X'FF'                                                      
         B     EXITL                                                            
*                                                                               
RLC10    CLC   PSLENH,DS.PSLEN     WILL IT FIT IN ORIGINAL SLOT?                
         BL    RLC12               NO                                           
*                                                                               
         SAM31                     WRITE OVER ORIGINAL                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   ARE,ARE,ALET        MOVE PHASE INTO HOLE IN CORE                 
         ICM   RE,15,DS.PSADR      RE = DATASPACE                               
         ICM   RF,15,DS.PSLEN                                                   
         TM    PSFLAG1,CTPHSDMQ    DUMMY PHASE                                  
         BZ    *+6                 NO                                           
         XR    RF,RF               CLEAR PHASE TO ZEROS                         
*                                                                               
         ICM   R0,15,PSADR         R0 = CORE                                    
         ICM   R1,15,PSLENH                                                     
         SAC   512                                                              
         MVCL  R0,RE                                                            
         SAC   0                                                                
         LAM   ARE,ARE,ARZERO                                                   
         SAM24                                                                  
*                                                                               
         MVC   PSTIME,DS.PSTIME    RESET NEW LOAD TIME                          
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
         GOTO1 VCALLOV,DMCB,0,(C'D',0),0                                        
*                                                                               
         MVC   MODID,SPACES        SET PHASE NAME FOR WARNING                   
         GOTO1 AHEXOUT,DMCB,PSNAME,MODID,L'PSNAME,0                             
         MVC   MODID+0(1),LANGDEF                                               
         CLI   PSLVL,C' '                                                       
         BNH   *+10                                                             
         MVC   MODID+6(1),PSLVL                                                 
         MVC   UPDOVER+6(8),MODID                                               
*                                                                               
         MVC   MSG(L'UPDWARN),UPDWARN                                           
         BRAS  RE,DOMSG                                                         
         MVC   MSG(L'UPDOVER),UPDOVER                                           
         BRAS  RE,DOMSG                                                         
         BRAS  RE,RSET             RESTART TIMER                                
         B     EXITOK                                                           
*                                                                               
RLC12    MVC   PSTIME,DS.PSTIME    IGNORE LOAD - JUST SET TIME                  
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
         MVC   MSG(L'UPDWARN),UPDWARN                                           
         BRAS  RE,DOMSG                                                         
         MVC   MSG(L'UPDLOAD),UPDLOAD                                           
         BRAS  RE,DOMSG                                                         
         BRAS  RE,RSET             RESTART TIMER                                
         B     EXITL                                                            
         DROP  DS,R2                                                            
         EJECT                                                                  
***********************************************************************         
* REBUILD PROGRAMS IN CORE                                            *         
***********************************************************************         
BLDPGMS  BRAS  RE,SSET             SUSPEND TIMER                                
         L     R0,AHOLE                                                         
         A     R0,LHOLE            A(END OF HOLE)                               
         GOTO1 VPROGBLD,DMCB,APGMS,(R0),VPHLIST,LXPH,0                          
         L     R5,VSSB                                                          
         USING SSBD,R5                                                          
         ICM   RF,15,4(R1)                                                      
         STCM  RF,15,SSBCRADR      RESET HOLE ADDRESS                           
         STCM  RF,15,AHOLE         RESET HOLE ADDRESS                           
         SR    R0,RF                                                            
         STCM  R0,15,SSBCRLEN                                                   
         STCM  R0,15,LHOLE                                                      
*                                                                               
         L     R0,AHOLE            NOW CLEAR HOLE                               
         L     R1,LHOLE                                                         
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         BRAS  RE,RSET             RESTART TIMER AND EXIT                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* FIX HOLE IF YOU ARE THE ONLY TASK RUNNING                           *         
***********************************************************************         
FIXHOLE  BRAS  RE,BLDCRNDX         REBUILD CRNDX IF NECESSARY                   
*                                                                               
         MVI   FIXUP,NO            SEE IF HOLE NEEDS FIXING                     
         L     RF,AHOLE                                                         
         USING HOLED,RF                                                         
         OC    HOKEY,HOKEY         HOLE EMPTY?                                  
         BNZ   *+12                                                             
         MVI   CMCMD,0                                                          
         B     XMOD                                                             
         DROP  RF                                                               
*                                                                               
         BRAS  RE,SSET             SUSPEND TIMER                                
*                                                                               
         L     R2,VTCB             ARE WE THE ONLY TASK?                        
         LH    RE,0(R2)                                                         
         L     RF,2(R2)                                                         
         AHI   R2,6                                                             
         USING TCBD,R2                                                          
FHOL02   CLI   TCBSEN,0            FREE TASK?                                   
         BE    *+12                YES                                          
         CLM   R2,15,ATCB          ACTIVE TASK IS ME?                           
         BNE   *+12                NO - EXIT                                    
         BXLE  R2,RE,FHOL02                                                     
         B     FHOL04                                                           
*                                                                               
         BRAS  RE,RSET             FLAG BUSY AND RESTART TIMER                  
         MVI   CMCMD,255                                                        
         B     XMOD                                                             
         DROP  R2                                                               
*                                                                               
FHOL04   BRAS  RE,GOHOME           PUT BACK ANYTHING THAT WILL FIT              
         SAM31                                                                  
         L     R0,XAHOLE           COPY HOLE INTO XAHOLE                        
         L     RE,AHOLE                                                         
         L     R1,LHOLE                                                         
         L     RF,LHOLE                                                         
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,AHOLE            CLEAR HOLE                                   
         L     R1,LHOLE                                                         
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R2,XAHOLE           NOW COPY BACK ALL STUFF STILL THERE          
XA       USING HOLED,R2            XA HOLE COPY                                 
         L     R3,AHOLE                                                         
LC       USING HOLED,R3            REGULAR HOLE COPY                            
         L     R5,LHOLE                                                         
         AR    R5,R2                                                            
         BCTR  R5,0                R5 = A(END OF XA HOLE-1)                     
*                                                                               
FHOL06   OC    XA.HOKEY,XA.HOKEY   END OF ALL IN HOLE?                          
         BZ    FHOL10              YES                                          
*                                                                               
         CLC   XA.HOKEY,EFFS       IGNORE THIS ONE?                             
         BNE   *+12                NO                                           
         MVI   FIXUP,YES                                                        
         B     FHOL08                                                           
*                                                                               
         MVC   LC.HOLED(HOLELQ),XA.HOLED                                        
         LA    R0,LC.HOPHASE                                                    
         ICM   R1,15,LC.HPSLENH                                                 
         LA    RE,XA.HOPHASE                                                    
         ICM   RF,15,XA.HPSLENH                                                 
         MVCL  R0,RE               COPY BACK THE PHASE INFO                     
         SAM24                                                                  
*                                                                               
         XC    PSREC,PSREC         GET CURRENT PHASE RECORD                     
         MVC   PROGSPCD(PSKEYL),LC.HOKEY                                        
         MVC   TPSKEY,PSREC                                                     
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   TPSKEY,PROGSPCD     YOU HAD BETTER GET A MATCH                   
         JNE   *+2                                                              
         STCM  R3,15,PSCHAIN       SET NEW CHAIN ADDRESS                        
*                                                                               
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
         SAM31                                                                  
         ICM   RF,15,LC.HPSLENH    NEXT SLOT IN LO CORE HOLE                    
         AHI   RF,HOLELQ                                                        
         AR    R3,RF                                                            
*                                                                               
FHOL08   ICM   R4,15,XA.HPSLENH    NEXT SLOT IN XA HOLE                         
         AHI   R4,HOLELQ                                                        
         BXLE  R2,R4,FHOL06                                                     
*                                                                               
FHOL10   SAM24                                                                  
         CLI   FIXUP,YES           ANY ADDRESSES CHANGED AT ALL?                
         BNE   FHOL12              NO                                           
         ST    R3,FULL             FIX CORE REMAINING IN DATASPACE              
         BRAS  RE,SETCORE                                                       
*                                                                               
         GOTO1 VCALLOV,DMCB,0,(C'D',0),0                                        
*                                                                               
FHOL12   BRAS  RE,RSET             RESTART TIMER                                
         MVI   CMCMD,0                                                          
         B     XMOD                                                             
         DROP  XA,LC                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO TRY TO PUT A PROGRAM IN THE HOLE BACK IN ORIGINAL SLOT   *         
***********************************************************************         
GOHOME   NTR1                                                                   
         L     R2,AHOLE            SEE IF HOLE NEEDS FIXING                     
         USING HOLED,R2                                                         
         L     R5,AHOLE                                                         
         A     R5,LHOLE                                                         
         BCTR  R5,0                RF = A(END OF HOLE)                          
*                                                                               
GHM02    OC    HOKEY,HOKEY         END OF STUFF IN THE HOLE?                    
         BZ    EXITOK              YES                                          
         CLC   HOKEY,EFFS                                                       
         BE    GHM04                                                            
*                                                                               
         XC    PSREC,PSREC         GET PHASE ENTRY FOR HOLE PHASE               
         MVC   PROGSPCD(PSKEYL),HOKEY                                           
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,PROGSPCD                                                      
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   HOKEY,PROGSPCD                                                   
         JNE   *+2                                                              
*                                                                               
         CLC   PSADR,=AL4(PSADRL)  IS THIS A 'NEW' CORE PHASE?                  
         BE    GHM04               YES - NO SLOT TO GO TO                       
         CLC   PSLENH,HPSLEN       WILL IT FIT IN ORIGINAL SLOT?                
         BL    GHM04               NO                                           
*                                                                               
         MVI   FIXUP,YES           ENTRY CHANGED - FIX CRNDX                    
*                                                                               
         ICM   R0,15,PSADR         MOVE PHASE BACK TO ORIGNAL SLOT              
         ICM   R1,15,PSLENH                                                     
         LA    RE,HOPHASE                                                       
         ICM   RF,15,HPSLEN                                                     
         MVCL  R0,RE                                                            
*                                                                               
         MVC   PSLEN,HPSLEN        SET NEW LENGTH                               
         XC    PSCHAIN,PSCHAIN     CLEAR CHAIN INFO                             
*                                                                               
         MVI   C.LIACTN,LIAWRT     AND WRITE IT BACK                            
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         MVC   HOKEY,EFFS          SET SLOT AS FREED                            
*                                                                               
GHM04    ICM   R4,15,HPSLENH       NEXT SLOT                                    
         AHI   R4,HOLELQ                                                        
         BXLE  R2,R4,GHM02                                                      
         XC    PSREC,PSREC                                                      
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET CORE LENGTH FOR THIS FACPAK IN DATASPACE             *         
* NTRY:  FULL = A(FIRST FREE BYTE IN HOLE)                            *         
***********************************************************************         
SETCORE  NTR1                                                                   
         ICM   R5,15,VSSB                                                       
         USING SSBD,R5                                                          
         LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,ALET                                                     
         XR    R2,R2                                                            
         SAC   512                                                              
         USING FAPGMSD,R2                                                       
         ICM   R2,15,PGMSCORE                                                   
         XR    R0,R0                                                            
         IC    R0,SSBSYSID                                                      
         BCTR  R0,0                                                             
         MHI   R0,((SBFACMAX*4)+4)                                              
         AR    R2,R0               INDEX INTO CORE BLOCK                        
         DROP  R2                                                               
*                                                                               
         XR    R0,R0                                                            
         IC    R0,SSBSYSIX                                                      
         SRL   R0,4                GET AOR NUMBER                               
         MHI   R0,4                                                             
         AHI   R0,4                                                             
         AR    R2,R0               GO INTO BLOCK                                
*                                                                               
         L     RF,FULL                                                          
         L     RE,AHOLE                                                         
         A     RE,LHOLE                                                         
         SR    RE,RF                                                            
         STCM  RE,15,0(R2)                                                      
*                                                                               
         XR    R2,R2                                                            
         USING FAPGMSD,R2                                                       
         ICM   R2,15,PGMSCORE                                                   
         CPYA  AR3,AR2                                                          
         LHI   R0,16               R0 = # OF FACPAKS                            
         XR    RF,RF               RF = SMALLEST CORE LENGTH                    
*                                                                               
SC02     OC    0(4,R2),0(R2)       SLOT HAS A VALUE?                            
         BZ    SC08                NO                                           
*                                                                               
         LA    R3,4(R2)            LOOK FOR SMALLEST CORE VALUE                 
         LHI   R1,SBFACMAX                                                      
SC04     LTR   RF,RF               RF HAS A VALUE?                              
         BNZ   *+8                 YES                                          
         ICM   RF,15,0(R3)                                                      
*                                                                               
         OC    0(4,R3),0(R3)       THIS ENTRY HAS A VALUE?                      
         BZ    SC06                NO                                           
         CLM   RF,15,0(R3)                                                      
         BL    *+8                                                              
         ICM   RF,15,0(R3)         GET SMALLEST ENTRY                           
*                                                                               
SC06     AHI   R3,4                NEXT VALUE IN SLOT                           
         BCT   R1,SC04                                                          
*                                                                               
SC08     AHI   R2,(SBFACMAX*4)+4   NEXT SLOT                                    
         BCT   R0,SC02                                                          
*                                                                               
         XR    R2,R2                                                            
         ICM   R2,15,PGMSCORE                                                   
         OC    PGMSLST,PGMSLST                                                  
         BNZ   *+8                                                              
         STCM  RF,15,PGMSLST                                                    
*                                                                               
         CLM   RF,15,PGMSLST       SEE IF LESS CORE NOW                         
         BH    *+8                                                              
         STCM  RF,15,PGMSLST                                                    
*                                                                               
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         B     EXITOK                                                           
         DROP  R2,R5                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD SCREEN MAP FOR PHASES LOADED INTO TWA                         *         
***********************************************************************         
SCRMAP   NTR1                                                                   
         ICM   R3,15,AUTL          POINT TO UTL ENTRY                           
         BZ    EXITOK                                                           
         USING UTLD,R3                                                          
         OC    TSVCREQ,TSVCREQ                                                  
         BNZ   EXITOK              EXIT IF SERVICE REQUEST                      
*                                                                               
         XR    R2,R2                                                            
         ICM   R2,7,CMAPHS                                                      
         S     R2,ATWA                                                          
         BNP   EXITOK              NOT IN TWA                                   
         CHI   R2,4096                                                          
         BH    EXITOK              TOO FAR INTO TWA                             
*                                                                               
         CLI   PSOVR,FF            BASE SCREEN?                                 
         BNE   SMAP02                                                           
         MVI   TSCRNE,1            INITIALISE IF BASE SCREEN                    
         MVI   TSCRNUM,X'FF'                                                    
         STH   R2,TSCRNE+2                                                      
         XC    TSCRNUM+3(6),TSCRNUM+3                                           
         B     EXITOK                                                           
*                                                                               
SMAP02   LA    R4,TSCRNUM          R4=A(ENTRY)                                  
         LHI   R0,3                R0=MAX NUM OF ENTRYS                         
         XR    R1,R1               R1=NUM OF ENTRYS                             
*                                                                               
SMAP04   CLM   R2,3,1(R4)          SKIP ENTRYS WITH LOWER DISPS                 
         BNH   SMAP06                                                           
         CLI   0(R4),0                                                          
         BE    SMAP06                                                           
         AHI   R4,3                                                             
         AHI   R1,1                                                             
         BCT   R0,SMAP04                                                        
         B     EXITOK                                                           
*                                                                               
SMAP06   MVC   0(1,R4),PSOVR       SET OLAY NUMBER                              
         STCM  R2,3,1(R4)          SET DISPLACEMENT                             
         AHI   R4,3                                                             
         AHI   R1,1                                                             
         STC   R1,TSCRNE           SET NUMBER OF ENTRYS                         
         AHI   R0,-1                                                            
         BNP   EXITOK                                                           
*                                                                               
SMAP08   XC    0(3,R4),0(R4)       CLEAR REMAINING ENTRYS                       
         AHI   R4,3                                                             
         BCT   R0,SMAP08                                                        
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* LOAD PHASE TO A GIVEN ADDRESS FROM DATASPACE                        *         
* NTRY:  CMAPHS = LOAD TO ADDRESS                                     *         
*        PSREC  = A(PROGSPCD ENTRY)                                   *         
***********************************************************************         
LTOADD   NTR1                                                                   
         L     R1,ATCB                                                          
         USING TCBD,R1                                                          
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS                                                      
         C     R0,TCBPGMA          CHECK WITHIN PRGMS AREA                      
         BL    LTOA02                                                           
         C     R0,TCBPGMX                                                       
         BH    LTOA02                                                           
*                                                                               
         ICM   RF,15,PSLEN         MAKE SURE OVERLAY WILL FIT                   
         BCTR  RF,0                                                             
         AR    R0,RF                                                            
         C     R0,TCBPGMX                                                       
         JH    *+2                 DIE IF PHASE WONT FIT                        
         DROP  R1                                                               
*                                                                               
LTOA02   SAM31                                                                  
         LAM   AR0,ARF,ARZERO                                                   
         LAM   ARE,ARE,ALET        MOVE IN PROGRAM TO CORE                      
         ICM   RE,15,PSADR                                                      
         ICM   RF,15,PSLEN                                                      
         XR    R0,R0                                                            
         ICM   R0,7,CMAPHS                                                      
         LR    R1,RF                                                            
         SAC   512                                                              
         MVCL  R0,RE                                                            
         SAC   0                                                                
         LAM   ARE,ARE,ARZERO                                                   
*                                                                               
         ICM   RF,15,VPHSCNT                                                    
         BZ    EXITOK                                                           
         XC    DUB,DUB                                                          
         MVC   DUB(L'PSNAME),PSNAME                                             
         GOTO1 (RF),DMCB,(C'A',DUB),0                                           
*                                                                               
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* INITIALISE AND GET PARAMETER LISTS                                  *         
***********************************************************************         
         PUSH  USING                                                            
         USING DMSPACED,WORK                                                    
INIT     NTR1                                                                   
         L     R3,VSYSFAC0                                                      
         USING COMFACSD,R3                                                      
         MVC   APROTON,CPROTON     STORAGE PROTECT MACROS                       
         MVC   APROTOFF,CPROTOFF                                                
         MVC   ADATTIM,CDATTIM                                                  
         MVC   AHEXOUT,CHEXOUT                                                  
*                                                                               
         L     R3,VSSB                                                          
         USING SSBD,R3                                                          
         MVC   ALANGTAB,SSBALANG   SET A(LANGUAGE TABLE)                        
         MVC   ALET,SSBPGMTA                                                    
         MVC   ALETT,SSBTBLET                                                   
         MVC   PHLMAX,SSBPHMAX     SET MAX PHASE LENGTH                         
         MVC   AHOLE,SSBCRADR                                                   
         MVC   LHOLE,SSBCRLEN                                                   
         MVC   APGMS,SSBAPGMS                                                   
         MVC   LXPH,SSBLXPH                                                     
*                                                                               
         MVC   MSGH,SPACES         SET UP MSG HEADER                            
         MVC   MSG,SPACES                                                       
         MVC   MSGH(L'FAC),FAC                                                  
         MVC   MSGH+4(L'SSBSYSNA),SSBSYSNA                                      
*                                                                               
         ICM   R3,15,SSBTKADR      GET A(TASK)                                  
         BNZ   INIT02                                                           
         CLI   CMCMD,CMCBCORE      WANT TO REBUILD PROGRAMS IN CORE             
         BE    INIT04                                                           
         CLI   CMCMD,CMCSYSF       WANT TO REBUILD CRNDX?                       
         BE    INIT04                                                           
         DC    H'0'                                                             
*                                                                               
INIT02   ST    R3,ATCB             R3=A(TCB ENTRY)                              
         USING TCBD,R3                                                          
         MVC   ATWA,TCBTWA                                                      
         MVC   AUTL,TCBUTL                                                      
*                                                                               
         ICM   R3,15,AUTL                                                       
         BZ    INIT04                                                           
         ST    R3,AUTL             R3=A(UTL ENTRY)                              
         USING UTLD,R3                                                          
*                                                                               
         TM    TTEST,TTESTCIL      LOADING FROM CIL?                            
         BZ    *+8                 NO                                           
         MVI   USECIL,YES                                                       
         DROP  R3                                                               
*                                                                               
INIT04   ICM   RF,15,=AL4(DPDPGMS) GET DSPACE PARAMETER LIST                    
         BRAS  RE,ASKME                                                         
*                                                                               
         SAM31                                                                  
         ICM   R2,15,DSPTFRST                                                   
         N     R2,=XL4'0FFFFFFF'                                                
         BRAS  RE,LAMR2            GET INTO DATASPACE                           
         MVC   LIBUFF,0(R2)        COPY DSPACE PARAMETER LIST                   
         BRAS  RE,ARSOFF                                                        
*                                                                               
         MVC   LIALET,ALET         SET CORRECT ALET                             
         OI    LIFLAG1,LIF1ARS                                                  
         NI    LIFLAG1,255-LIF1INS SET LINKED LIST REQUIRED FOR ANY ADD         
*                                                                               
         ICM   RF,15,VPHLIST       GET LOCAL PARAMETER LIST                     
         MVC   LIBUFFC,0(RF)                                                    
         NI    C.LIFLAG1,255-(LIF1INS+LIF1ARS)                                  
         B     EXITOK              DON'T WORRY - EXIT TURNS OFF XA              
         POP   USING                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO FIX UP CRNDX IN DATASPACE                                *         
***********************************************************************         
FIXCRNDX ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,LOCKME           LOCK DATASPACE                               
         BRAS  RE,SSET             ENSURE NO TIMER POPS                         
         MVI   FLAG,NO                                                          
*                                                                               
DS       USING PROGSPCD,WORK                                                    
         XC    WORK,WORK           GET CRNDX DIR RECORD FROM DATASPACE          
         MVC   DS.PSNAME,=XL3'000AFF'                                           
         MVI   DS.PSLANG,C'T'                                                   
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   DS.PSNAME,=XL3'000AFF'                                           
         JNE   *+2                                                              
*                                                                               
DCX      USING CRNDXD,R2                                                        
FXC02    ICM   R2,15,DS.PSADR      GET A(CRNDX) IN DATASPACE                    
         BRAS  RE,LAMR2                                                         
         SAM31                                                                  
*                                                                               
FXC06    OC    0(2,R2),0(R2)       TEST END OF INDEX                            
         BZ    FXC12                                                            
         MVC   FULL+0(2),=X'000A'                                               
         MVC   FULL+2(1),DCX.CRNDPHS                                            
         MVI   BYTE,0              CLEAR INDICATORS                             
         BRAS  RE,ARSOFF                                                        
*                                                                               
         LA    R3,CRTEST           R3 WILL BE TEST LEVEL: 0-3 = 0,A,B,C         
FXC08    XC    WORK,WORK           GET CRNDX RECORD FROM DATASPACE              
         MVC   DS.PSNAME,FULL                                                   
         MVI   DS.PSLANG,C'T'                                                   
         MVC   DS.PSLVL,0(R3)                                                   
         MVI   LIACTN,LIAHIGH                                                   
*                                                                               
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
         CLC   DS.PSNAME,FULL                                                   
         BNE   FXC10                                                            
         CLC   DS.PSLVL,0(R3)                                                   
         BNE   FXC10                                                            
         OC    BYTE,1(R3)                                                       
*                                                                               
FXC10    AHI   R3,2                NEXT TEST LEVEL                              
         CLI   0(R3),X'FF'                                                      
         BNE   FXC08                                                            
*                                                                               
         BRAS  RE,LAMR2            MAYBE PARANOID, BUT BETTER OFF SAFE          
         CLC   DCX.CRNDINDS,BYTE   ANY CHANGES?                                 
         BE    *+8                 NO                                           
         MVI   FLAG,YES            YES                                          
         MVC   DCX.CRNDINDS,BYTE   SET NEW A,B,C FLAGS IN DATASPACE             
*                                                                               
         LA    R2,DCX.CRNDNTRY     NOW GO PAST ADCONS TO NEXT ENTRY             
FXC11A   CLI   0(R2),X'FF'         TRACE DOWN ADCON LIST TILL X'FF'             
         BE    FXC11B                                                           
         AHI   R2,4                                                             
         B     FXC11A                                                           
         DROP  DCX                                                              
*                                                                               
FXC11B   AHI   R2,1                GO PAST THE X'FF'                            
         B     FXC06               NOW DO THE NEXT ENTRY                        
*                                                                               
FXC12    BRAS  RE,ARSOFF           BACK TO NORMAL                               
         CLI   FLAG,YES            DID WE UPDATE INDEX?                         
         BNE   FXC14               NO                                           
*                                                                               
         GOTO1 ADATTIM,DMCB,(X'01',FULL),0                                      
         XC    WORK,WORK                                                        
         MVC   DS.PSNAME,=XL3'000AFF'                                           
         MVI   DS.PSLANG,C'T'                                                   
         MVI   LIACTN,LIAHIGH      GET CRNDX BACK                               
         LA    RF,WORK             UPDATE DATASPACE VERSION                     
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         MVC   DS.PSTIME,FULL                                                   
         MVI   LIACTN,LIAWRT       WRITE CRNDX WITH UPDATED TIME                
         LA    RF,WORK                                                          
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                  UPDATE TIME OF LOAD                          
FXC14    BRAS  RE,ARSOFF                                                        
         GOTO1 ADATTIM,DMCB,(X'01',FULL),0                                      
         CLI   4(R1),0                                                          
         BNE   FXC16               DATTIM NOT HAPPY                             
*                                                                               
         BRAS  RE,LAMR2                                                         
         XR    R2,R2                                                            
         USING FAPGMSD,R2                                                       
         MVC   PGMSTIME,FULL       SET NEW UPDATE TIME                          
         BRAS  RE,ARSOFF                                                        
*                                                                               
*&&DB*&& L     RE,=A(UPDMSGD)                                                   
*&&DB*&& MVC   MSG,0(RE)                                                        
*&&DB*&& BRAS  RE,DOMSG                                                         
         DROP  R2                                                               
*                                                                               
FXC16    ICM   RF,15,=AL4(DPDPGMS)                                              
         BRAS  RE,FREEME           FREE DATASPACE                               
         BRAS  RE,RSET             RESET TIMER POPS                             
         B     EXITOK                                                           
         DROP  DS                                                               
         EJECT                                                                  
***********************************************************************         
* SEE IF THIS CRNDX NEEDS REBUILDING                                  *         
*                                                                     *         
* EACH AOR/FACPAK WILL INSPECT LAST UPDATED DATE AND REBUILD IF IT IS *         
* NOT UP TO DATE                                                      *         
*                                                                     *         
* COMPARE TOR/AOR EXCHANGE TIME TO PROGRAM DATASPACE TIME             *         
***********************************************************************         
BLDCRNDX NTR1                                                                   
         LAM   AR0,ARF,ARZERO      SEE IF CRNDX NEEDS REBUILDING                
         L     R3,VSSB                                                          
         USING SSBD,R3                                                          
         LAM   AR2,AR2,SSBALET                                                  
         ICM   R2,15,SSBATOR                                                    
         AHI   R2,TORFACLQ         INDEX TO FACPAK EXCHANGE GRID                
         USING SBEXCHD,R2                                                       
         SAC   512                                                              
         LH    RE,0(,R2)           BXLE R2,R4,R5                                
         L     RF,2(,R2)                                                        
         LA    R2,6(,R2)                                                        
*                                                                               
BCN02    CLC   SBSTOKEN,SSBSTOKN   OUR SLOT?                                    
         BE    BCN04               YES                                          
         BXLE  R2,RE,BCN02                                                      
         DC    H'0'                WHERE IS OUR FACPAK IN LIST                  
         DROP  R3                                                               
*                                                                               
BCN04    LAM   AR3,AR3,ALET                                                     
         XR    R3,R3                                                            
         USING FAPGMSD,R3                                                       
         OC    PGMSTIME,PGMSTIME   CHECK PGMSTIME TO SEE IF SET                 
         BZ    BCN08               NO                                           
*                                                                               
BCN06    CLC   PGMSTIME,SBPGTIME                                                
         BNH   BCN08                                                            
         MVC   SBPGTIME,PGMSTIME   SET REBUILD TIME                             
         BRAS  RE,ARSOFF                                                        
*                                                                               
*&&DB*&& L     RE,=A(UPDMSGC)                                                   
*&&DB*&& MVC   MSG,0(RE)           REBUILD CRNDX AND SET MESSAGE                
*&&DB*&& BRAS  RE,DOMSG                                                         
         GOTO1 VCALLOV,DMCB,0,(C'D',0),0                                        
*                                                                               
BCN08    BRAS  RE,ARSOFF           NOTHING TO DO                                
         B     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO REBUILD ADDRESSES USING CRNDX                            *         
* THERE ARE TWO LOOPS                                                 *         
* LOOP 1 CHECKS TO SEE IF A NEWER RECORD EXISTS IN THE DATASPACE      *         
*        THIS IS NEEDED FOR AORS AND OTHER FACPAKS                    *         
* LOOP 2 THEN SETS THE CORRECT ADDRESS FOR THIS PHASE IN THE CRNDX    *         
***********************************************************************         
RBLDCRND BRAS  RE,SSET                                                          
*                                                                               
CR       USING PROGSPCD,XPSREC                                                  
         XC    XPSREC,XPSREC       CRNDX INFO IN XPSREC                         
         MVC   CR.PSNAME,=XL3'000AFF'                                           
         MVI   CR.PSLANG,C'T'                                                   
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,XPSREC                                                        
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   CR.PSNAME,=XL3'000AFF'                                           
         JNE   *+2                                                              
*                                                                               
         ICM   R4,15,CR.PSCHAIN    PHASE IS IN THE HOLE?                        
         BZ    *+12                NO                                           
         AHI   R4,HOPHASE-HOLED    GET A(CRNDX) IN HOLE                         
         B     *+8                                                              
         ICM   R4,15,CR.PSADR      GET A(CRNDX) IN CORE                         
         ST    R4,ACRNDX           SAVE A(CRNDX) FOR LOOP 2                     
         DROP  CR                                                               
         EJECT                                                                  
***********************************************************************         
* FIRST LOOP - CHECK NEWER VERSION                                    *         
***********************************************************************         
         USING CRNDXD,R4                                                        
RBC02    OC    0(2,R4),0(R4)       END OF CRNDX?                                
         BZ    RBC08               YES                                          
*                                                                               
DS       USING PROGSPCD,DPSREC                                                  
         XC    DPSREC,DPSREC       FIRST CHECK DSPACE FOR NEWER VERSION         
         MVI   DS.PSLANG,C'T'                                                   
         MVC   DS.PSNAME(2),=X'000A'                                            
         MVC   DS.PSOVR,CRNDPHS                                                 
         MVC   FULL(3),DS.PSNAME                                                
         MVI   LIACTN,LIAHIGH                                                   
         LA    RF,DPSREC                                                        
         ST    RF,LIAREC                                                        
         GOTO1 VARREDIT,DMCB,LIBUFF                                             
*                                                                               
         CLC   DS.PSNAME,FULL      ENSURE MATCH IN DATASPACE                    
         BNE   RBC06               IF PHASE DOESN'T EXIST, JUST IGNORE          
         CLI   DS.PSLVL,0                                                       
         BNE   RBC06               IF IT'S NOT LIVE, JUST IGNORE                
*                                                                               
CR       USING PROGSPCD,PSREC                                                   
         XC    PSREC,PSREC         NOW GET SAME PHASE IN CORE                   
         MVI   CR.PSLANG,C'T'                                                   
         MVC   CR.PSNAME(2),=X'000A'                                            
         MVC   CR.PSOVR,CRNDPHS                                                 
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,PSREC                                                         
         ST    RF,C.LIAREC                                                      
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
*                                                                               
         CLC   CR.PSNAME,DS.PSNAME MATCH NAME                                   
         BNE   RBC04                                                            
         CLC   CR.PSLVL,DS.PSLVL   MATCH LEVEL                                  
         BNE   RBC04                                                            
         CLC   DS.PSTIME,CR.PSTIME CHECK TIME LAST LOADED                       
         BNH   RBC06               STILL GOOD                                   
*                                                                               
RBC04    DS    0H                                                               
*&&DB*&& MVC   MSG,UPDMSGL                                                      
*&&DB*&& MVC   MSG+11(L'CRNDNAME),CRNDNAME                                      
*&&DB*&& BRAS  RE,DOMSG                                                         
         MVI   TIMESET,C'Y'        SET TIMERS ALREADY SUSPENDED                 
         BRAS  RE,RLDACORE         LOAD PHASE INTO HOLE                         
         MVI   TIMESET,C'N'        RESET TIMER SET FLAG                         
         DROP  DS                                                               
*                                                                               
RBC06    LA    RF,CRNDNTRY         GO TO NEXT CRNDX ENTRY                       
         CLI   0(RF),X'FF'         TEST END OF LIST                             
         BE    *+12                                                             
         AHI   RF,4                BUMP TO NEXT LIST ENTRY                      
         B     *-12                                                             
         LA    R4,1(RF)            NEXT CRNDX ENTRY                             
         B     RBC02                                                            
         EJECT                                                                  
***********************************************************************         
*  SECOND LOOP - SET ADDRESSES                                        *         
***********************************************************************         
RBC08    L     R4,ACRNDX           GET CRNDX DIRECTORY RECORD FROM CORE         
*                                                                               
RBC10    OC    0(2,R4),0(R4)       END OF INDEX?                                
         BZ    RBC16               YES                                          
*                                                                               
         LA    R5,CRNDNTRY         R5 = A(ADDRESS LIST)                         
         XC    SAVE,SAVE                                                        
         XC    PSREC,PSREC                                                      
         MVI   CR.PSLANG,C'T'                                                   
         MVC   CR.PSNAME(2),=X'000A'                                            
         MVC   CR.PSOVR,CRNDPHS                                                 
         MVC   FULL(3),CR.PSNAME                                                
         MVI   C.LIACTN,LIAHIGH                                                 
         LA    RF,PSREC                                                         
         ST    RF,C.LIAREC                                                      
*                                                                               
         GOTO1 VARREDIT,DMCB,LIBUFFC                                            
         CLC   CR.PSNAME,FULL      MATCH NAME AND LEVEL                         
         BNE   RBC12                                                            
         CLI   CR.PSLVL,0                                                       
         BNE   RBC12                                                            
*                                                                               
         ICM   RF,15,CR.PSCHAIN    PHASE IS IN THE HOLE?                        
         BZ    *+12                NO                                           
         AHI   RF,HOPHASE-HOLED                                                 
         B     *+8                                                              
         ICM   RF,15,CR.PSADR      GET A(PHASE IN CORE)                         
         ST    RF,SAVE             AND SAVE IT                                  
         DROP  CR                                                               
*                                                                               
RBC12    CLI   0(R5),255           TEST END OF LIST                             
         BE    RBC14                                                            
         SR    RE,RE                                                            
         ICM   RE,3,0(R5)                                                       
         L     RE,SYSFACD(RE)      RE=A(SYSTEM FACILITY LIST)                   
         SR    RF,RF                                                            
         ICM   RF,3,2(R5)          RF=DISP OF ADCON INTO LIST                   
         LA    RE,0(RE,RF)                                                      
         MVC   0(4,RE),SAVE        SET A(PHASE) IN LIST                         
         AHI   R5,4                BUMP TO NEXT LIST ENTRY                      
         B     RBC12                                                            
*                                                                               
RBC14    LA    R4,1(R5)            NEXT CRNDX ENTRY                             
         B     RBC10                                                            
*                                                                               
RBC16    BRAS  RE,RSET             RESET SYSTEM CLOCK                           
         B     EXITOK                                                           
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND USEFUL ROUTINES                                     *         
***********************************************************************         
ARSOFF   SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         BR    RE                                                               
*                                                                               
LAMR2    LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,ALET                                                     
         SAC   512                                                              
         BR    RE                                                               
*                                                                               
SSET     CLI   TIMESET,C'Y'                                                     
         BER   RE                                                               
         LR    R0,RE                                                            
         GOTO1 VTICTOC,DUB,C'SSET' STOP TIMER                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
RSET     CLI   TIMESET,C'Y'                                                     
         BER   RE                                                               
         LR    R0,RE                                                            
         GOTO1 VTICTOC,DUB,C'RSET' RESTART TIMER                                
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
LOCKME   LR    R0,RE               LOCK TABLE                                   
         GOTO1 VLOCKSPC,DUB,(X'80',(RF)),WORK                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
FREEME   LR    R0,RE               FREE TABLE                                   
         GOTO1 VLOCKSPC,DUB,(X'10',(RF)),WORK                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
ASKME    LR    R0,RE               ENQUIRE ON TABLE                             
         GOTO1 VLOCKSPC,DUB,(X'20',(RF)),WORK                                   
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
EXITL    CLI   *,FF                                                             
         B     EXIT                                                             
*                                                                               
EXITH    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
XMOD     GOTO1 APROTON             RESTORE STORAGE PROTECTION                   
         L     R1,SAVER1           RETURN UPDATED PARAMETERS                    
         MVC   0(L'IPARMS,R1),IPARMS                                            
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
DOMSG    NTR1                                                                   
         GOTO1 VDMOD000,DMCB,VWCTYPE,MSGH,MSGL,C'LVL1'                          
         MVC   MSG,SPACES                                                       
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
CMDTAB   DS    0XL8                                                             
         DC    AL1(CMCBCORE,0,0,0) ! - REBUILD PROGRAMS IN CORE                 
         DC    AL4(BLDPGMS)                                                     
         DC    AL1(CMCHOLE,0,0,0)  H - FIX HOLE                                 
         DC    AL4(FIXHOLE)                                                     
         DC    AL1(CMCCND,0,0,0)   C - FIX CRNDX IN DATASPACE                   
         DC    AL4(FIXCRNDX)                                                    
         DC    AL1(CMCSYSF,0,0,0)  D - FIX CORE ADDRESSES USING CRNDX           
         DC    AL4(RBLDCRND)                                                    
         DC    AL1(CMCWRT,0,0,0)   W - WRITE                                    
         DC    AL4(WRITE)                                                       
         DC    AL1(CMCMDSR,0,0,0)  S - SPECIAL READ                             
         DC    AL4(READ)                                                        
         DC    AL1(CMCRD,0,0,0)    R - READ                                     
         DC    AL4(READ)                                                        
         DC    AL1(255,0,0,0)      DEFAULT IS TO CALL THE READ ROUTINE          
         DC    AL4(READ)                                                        
*                                                                               
         DC    CL16'XAHOLE**XAHOLE**'                                           
XAHOLE   DC    A(0)                                                             
*                                                                               
FF       EQU   X'FF'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
MAPLEN   EQU   1024                LENGTH OF MAP ENTRY                          
*                                                                               
EFFS     DC    16X'FF'                                                          
SPACES   DC    80C' '                                                           
*                                                                               
*&&US                                                                           
FAC      DC    CL8'*FACPAK*'                                                    
*&&                                                                             
*&&UK                                                                           
FAC      DC    CL8'+FACPAK+'                                                    
*&&                                                                             
LVLTRN   DS    0CL4                LEVEL TRANSLATION TABLE                      
         DC    AL1(0),CL3'ABC'                                                  
*                                                                               
CRTEST   DC    X'00',X'00',C'A',X'01',C'B',X'02',C'C',X'04',X'FF'               
*                                                                               
VSYSFAC  DC    V(SYSFAC)                                                        
VPROGBLD DC    V(PROGBLD)                                                       
VPHSCNT  DC    V(PHSCNT)                                                        
ARZERO   DC    16F'0'                                                           
         EJECT                                                                  
***********************************************************************         
* MESSAGES                                                            *         
***********************************************************************         
UPDFULL  DC    CL(L'MSG)'**ERROR*** Must reload PGMS Dspace for facpak'         
UPDMSGD  DC    CL(L'MSG)'CRNDX updated in dataspace'                            
UPDMSGC  DC    CL(L'MSG)'CRNDX updated in core in this facpak'                  
UPDMSGL  DC    CL(L'MSG)'Attempting XXXXXXX reload for CRNDX update'            
*                                                                               
UPDWARN  DC    CL(L'MSG)'**WARNING** No free HOLE in this facpak'               
UPDOVER  DC    CL(L'MSG)'Phase XXXXXXXX reloaded over top of original'          
UPDNLD   DC    CL(L'MSG)'Phase XXXXXXXX was not loaded and is ignored'          
UPDLOAD  DC    CL(L'MSG)'Requested phase was not loaded into hole'              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
*                                                                               
SAVER1   DS    A                   A(CALLERS R1)                                
IPARMS   DS    0XL12               MY COPY OF INCOMING PARAMETERS               
IP1      DS    A                                                                
IP2      DS    A                                                                
IP3      DS    A                                                                
*                                                                               
DMCB     DS    6F                                                               
*                                                                               
APROTON  DS    A                   A(PROTON ROUTINE)                            
APROTOFF DS    A                   A(PROTOFF ROUTINE)                           
ADATTIM  DS    A                   A(DATTIM ROUTINE)                            
AHEXOUT  DS    A                   A(HEXOUT ROUTINE)                            
ACRNDX   DS    A                   A(CRNDX)                                     
*                                                                               
ATCB     DS    A                   A(TCB)                                       
AUTL     DS    A                   A(UTL)                                       
ATWA     DS    A                   A(TWA)                                       
ALANGTAB DS    A                   A(LANGTAB - FROM SSB)                        
ALET     DS    A                   SSBPGMTA                                     
ALETT    DS    A                   SSBTBLET                                     
*                                                                               
AHOLE    DS    F                   A(HOLE IN CORE)                              
LHOLE    DS    F                                                                
APGMS    DS    A                                                                
LXPH     DS    A                                                                
SAVE     DS    F                                                                
SVCRTIME DS    F                                                                
SVDSTIME DS    F                                                                
*                                                                               
MODID    DS    CL8                                                              
*                                                                               
LIBUFF   DS    XL(LIBUFFL)         DATASPACE ARREDIT PARAMETERS                 
LIBUFFC  DS    XL(LIBUFFL)         CORE      ARREDIT PARAMETERS                 
*                                                                               
PSREC    DS    XL(PROGSPCL)        CURRENT PSREC                                
SPSREC   DS    XL(PROGSPCL)        SAVED PSREC (FROM BLDKEY)                    
XPSREC   DS    XL(PROGSPCL)        SAVED PSREC (FOR BLDMAP)                     
DPSREC   DS    XL(PROGSPCL)        SAVED PSREC (FROM DSPACE - CORERES)          
TPSKEY   DS    XL(PSKEYL)                                                       
TIMESET  DS    X                                                                
*                                                                               
BASEMAPA DS    A                                                                
OVLYADDR DS    A                                                                
OVLYLEN  DS    A                                                                
SVADDR   DS    A                                                                
TOTLEN   DS    F                                                                
TERMID   DS    XL2                                                              
TESTID   DS    CL4                                                              
*                                                                               
USECIL   DS    X                                                                
WANTDEL  DS    X                                                                
FIXUP    DS    X                                                                
*                                                                               
WORK     DS    CL64                                                             
CWORK    DS    CL64                                                             
*                                                                               
MSGH     DS    CL9                                                              
MSG      DS    CL70                                                             
MSGL     EQU   *-MSGH                                                           
*                                                                               
FLAG     DS    X                                                                
STNODE   DS    X                                                                
NDNODE   DS    X                                                                
SVCMD    DS    X                                                                
*                                                                               
PHLMAX   DS    H                   MAXIMUM LENGTH OF A PHASE                    
PHLSWRT  DS    C                                                                
LANGCHR  DS    X                   USER LANGUAGE CHARACTER                      
LANGDEF  DS    X                   DEFAULT LANGUAGE CHARACTER                   
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* MAP AREA DSECTS                                                     *         
***********************************************************************         
MPHDRD   DSECT                                                                  
MHID     DS    CL4                                                              
MHLANG   DS    XL1                                                              
MHSP     DS    XL2                                                              
MHLVL    DS    XL1                                                              
         DS    XL8                                                              
MPHDRL   EQU   *-MPHDRD                                                         
*                                                                               
MPAREAD  DSECT                                                                  
MPNODE   DS    X                                                                
MPLVL    DS    X                                                                
MPOVR    DS    X                                                                
MPLANG   DS    X                                                                
MPLEN    DS    AL4                                                              
MPAREAL  EQU   *-MPAREAD                                                        
         EJECT                                                                  
***********************************************************************         
* INPUT PARAMETERS                                                    *         
***********************************************************************         
CALLOVD  DSECT                                                                  
CMOVR    DS    X                   OVERLAY                                      
CMAPHS   DS    XL3                 A(PHASE)                                     
*                                                                               
CMCMD    DS    X                   COMMAND                                      
CMCMDN   EQU   0                   NORMAL CALL                                  
CMCRD    EQU   C'R'                MAINT READ                                   
CMCWRT   EQU   C'W'                MAINT WRITE                                  
CMCHOLE  EQU   C'H'                FIX HOLE                                     
CMCMDSR  EQU   C'S'                SPECIAL READ                                 
CMCCND   EQU   C'C'                FIX CRNDX IN DSPACE                          
CMCSYSF  EQU   C'D'                FIX ADDRESSES USING CRNDX IN CORE            
CMCBCORE EQU   C'!'                REBUILD PROGRAMS IN CORE                     
*                                                                               
CMNPHS   DS    AL3                 PHASE NAME X'0SPPOO'                         
*                                                                               
CMTYP    DS    X                   WRITE TYPE                                   
CMTDIR   EQU   C'D'                DIRECTORY                                    
CMTADD   EQU   C'A'                ADD                                          
CMIPHS   DS    XL1                 PHASE IND                                    
CMLPHS   DS    H                   PHASE LENGTH (ZERO USE OLD LENGTH)           
*                                                                               
         ORG   CALLOVD                                                          
CLADS    DS    A                   A(PSKEY FOR DATASPACE)                       
CLCMD    DS    X                   C'L'                                         
CLALL    DS    AL3                 A(PSKEY FOR LOADLIB)                         
CLMOD    DS    X                                                                
         DS    AL3                 N/D                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*FASYSFAC                                                                       
       ++INCLUDE FASYSFAC                                                       
*DDCOMFACS                                                                      
       ++INCLUDE DDCOMFACS                                                      
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
*FALANG                                                                         
       ++INCLUDE FALANG                                                         
*FAPHLIST                                                                       
       ++INCLUDE FAPHLIST                                                       
*FAHOLED                                                                        
       ++INCLUDE FAHOLED                                                        
*FATSTTAB                                                                       
       ++INCLUDE FATSTTAB                                                       
*FAVRSNTAB                                                                      
       ++INCLUDE FAVRSNTAB                                                      
*FATABSDEQU                                                                     
       ++INCLUDE FATABSDEQU                                                     
*FAPGMSDEQU                                                                     
       ++INCLUDE FAPGMSDEQU                                                     
*DMSPACED                                                                       
       ++INCLUDE DMSPACED                                                       
*DDARREDITD                                                                     
       ++INCLUDE DDARREDITD                                                     
*FAPROGSPCD                                                                     
       ++INCLUDE FAPROGSPCD                                                     
*FAPIGFACD                                                                      
       ++INCLUDE FAPIGFACD                                                      
*FAPGMSD                                                                        
       ++INCLUDE FAPGMSD                                                        
*FACRNDXD                                                                       
       ++INCLUDE FACRNDXD                                                       
*CTGENFILE                                                                      
       ++INCLUDE CTGENFILE                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FACALLOV  12/18/19'                                      
         END                                                                    
