*          DATA SET PPGETCGR   AT LEVEL 010 AS OF 06/08/01                      
*CATALP PPGETCG                                                                 
         TITLE 'PPGETCG - BUILDS AN OUTPUT BLOCK OF ADDITIONAL CHARGES'         
*                                                                               
*                                                                               
*    ****  NOTE THAT THIS MODULE MAY READ THE PRINT FILE AND    ****            
*    ****  DESTROY SEQUENTIAL READING, HOWEVER IF A COUNT OF    ****            
*    ****  ZERO IS RETURNED IN PARAM1, THE PRINT FILE WAS NOT   ****            
*    ****  READ AND NO RESTORATION OF SEQUENCE WOULD BE NEEDED  ****            
*    ****  IN THE CALLING PROGRAM.                              ****            
*                                                                               
*    THIS PROGRAM WILL BUILD AN OUTPUT PRINT BLOCK UP TO 13 LINES               
*    IN LENGTH CONTAINING ADDITIONAL CHARGE DATA FROM THE PACELEM OF            
*    PBUYREC AND THE ASSOCIATED DATA FROM THE ADDITIONAL CHARGE                 
*    RECORD (PSPCGREC)                                                          
*                                                                               
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
* SMYE 05.16.01    DO NOT GROSS UP IF NOT SUBJECT TO AGENCY COMMISSION          
*                                                                               
* YKAP 04.20.01    C'O' IN THIRD PARAMETER                                      
*                                                                               
* YKAP 03.26.01    CALCULATE GROSS AND NET IF C'T' PASSED IN FIRST              
*                  PARAMETER,ALSO P3 HAS TO HAVE ADDRESS OF GETINS              
*                                                                               
*        PARAMETER LIST                                                         
*                                                                               
*        INPUT                                                                  
*                                                                               
*   PARAM1                                                                      
*   BYTE 0              X'00' --  REGULAR CALL                                  
*                       C'T'  --  CALCULATE TOTALS                              
*   BYTE 1-3            A(PBUYREC)                                              
*                                                                               
*   PARAM2              A(DATAMGR)                                              
*                                                                               
*   PARAM3              A(GETINS)                                               
*   BYTE 0              C'O' OR X'00'                                           
*                                                                               
*   BYTE 1-3            A(GETINS)                                               
*                                                                               
*   PARAM4              A(PPRDKPRD)                                             
*        OUTPUT                                                                 
*                                                                               
*   PARAM1   BYTE  0    X'FF' IF ERROR                                          
*                       X'NN' NN=NO. OF LINES IN OUTPUT BLOCK (00-0C)           
*                                                                               
*            BYTES 1-3  A(OUTPUT BLOCK)                                         
*            ***** SEE PPGETCGD DSECT FOR DETAILED CONTENT DESCRIPTION          
*                                                                               
*                                                                               
*                                                                               
PPGETCG  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,PPGETCG,CLEAR=YES                                    
         USING WORKD,RC                                                         
         OC    1(3,R1),1(R1)       PBUYREC ADDRESS GIVEN ?                      
         BZ    PGCERR              NO - ERROR                                   
         OC    4(4,R1),4(R1)       DATAMGR ADDRESS GIVEN ?                      
         BZ    PGCERR              NO - ERROR                                   
         CLI   0(R1),C'T'          T OPTION GIVEN ?                             
         BNE   SAVER1OK            NO - SKIP NEXT FEW LINES                     
         OC    9(3,R1),9(R1)       GETINS ADDRESS GIVEN ?                       
         BZ    PGCERR              NO - ERROR                                   
         OC    12(4,R1),12(R1)     ADDRESS OF PRODUCT GIVEN ?                   
         BZ    PGCERR              NO - ERROR                                   
         MVI   TFLAG,X'01'         SET FLAG FOR T OPTION                        
*                                                                               
SAVER1OK DS    0H                                                               
         ST    R1,SAVER1           R1=A(PARAMETER LIST)                         
         L     R9,0(R1)            R9=A(BUYREC)                                 
*                                                                               
         MVC   DATAMGR,4(R1)       V(DATAMGR)                                   
*                                                                               
         CLI   TFLAG,X'01'         GETINS ADDRESS EXIST ONLY                    
         BNE   CLEAROK             IF FLAG IS ON                                
         MVC   VGETINS,8(R1)       V(GETINS)                                    
         MVC   PRODUCT,12(R1)      V(PPRDKPRD)                                  
*                                                                               
CLEAROK  DS    0H                                                               
         LA    R7,ADCAREA          R7=A(OUTPUT BLOCK AREA)                      
         LA    RF,ADCDLNTH                                                      
         XCEF  (R7),(RF)           CLEAR                                        
         MVI   LNCNT,0                                                          
*                                                                               
         USING ADDCHGD,R7                                                       
         USING PBUYRECD,R9                                                      
*                                                                               
*                                                                               
*                                  FIRST SEE IF BUY HAS ADD'L CHARGES           
*                                                                               
         LA    R2,PBUYREC+33                                                    
*                                                                               
PGCTST10 CLI   0(R2),0             EOR ?                                        
         BE    PGCXIT              YES - DONE - NO ADD'L CHARGES FOUND          
         CLI   0(R2),X'44'         ADD'L. CHG. ELEMENT ?                        
         BE    PGCTSTOK            YES - CONTINUE                               
         ZIC   R0,1(R2)            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         B     PGCTST10                                                         
*                                                                               
PGCTSTOK DS    0H                  GET THE ADD'L. CHARGE REC                    
         LA    R4,KEY                                                           
         USING PSPLRECD,R4                                                      
         XC    KEY,KEY                                                          
         MVC   PSPLKEY(3),PBUYKEY  AGENCY/MEDIA                                 
         MVI   PSPLKRCD,PSPLKIDQ   RECORD CODE (X'60')                          
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,(0,DMRDHI),PRTDIR,KEY,KEY                           
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   PSPLKEY(4),KEYSAVE  ANY ADD'L. CHG. RECORDS ?                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                  GET THE ADD'L. CHG. RECORD                   
*                                                                               
         GOTO1 DATAMGR,DMCB,(0,GETREC),PRTFIL,KEY+27,IO,DMWORK                  
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         DROP  R4                                                               
*                                                                               
         LA    R4,IO               POINT R4 TO RECORD IN IO                     
*                                                                               
*                           ** FIND THE ADDITIONAL CHARGE ELEMENT(S) **         
*                                                                               
         LA    R2,PBUYREC+33                                                    
*                                                                               
PGCBUY10 CLI   0(R2),0             EOR ?                                        
         BE    OPTION              NEW CHANGE CALCULATE TOTALS                  
*NOP*    BE    PGCXIT              YES - DONE                                   
         CLI   0(R2),X'44'         ADD'L. CHG. ELEMENT ?                        
         BNE   PGCBUY20            NO - TEST NEXT                               
*                                                                               
         BAS   RE,PGCMAIN          GO PROCESS OUTPUT LINE                       
*                                                                               
         LA    R7,ADCLNLEN(R7)     POINT TO NEXT LINE IN BLOCK                  
*                                                                               
PGCBUY20 ZIC   R0,1(R2)            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         B     PGCBUY10                                                         
         SPACE 3                                                                
***********************************************************************         
*       R2 POINTING TO ADDITIONAL CHARGE ELEMENT IN BUYREC                      
*       R7 POINTING TO NEXT LINE OF OUTPUT BLOCK                                
*       IO CONTAINS THE ADDITIONAL CHARGE RECORD (PSPCGREC)                     
***********************************************************************         
PGCMAIN  NTR1                      OUTPUT A BLOCK LINE                          
         USING PACELEMD,R2         BUYREC ADD'L. CHG. ELEMENT                   
         MVC   ADCCOD1,PACCODE                                                  
         MVC   ADCDES1,=C'** CODE NOT FOUND **'                                 
         LA    R4,IO+33            POINT TO ADD. CHG. REC ELEMENT               
         USING PSPLELEM,R4                                                      
PGCMLUP  DS    0H                                                               
         CLI   PSPLELEM,0          EOR ?                                        
         BE    PGCMOK10            YES - LEAVE "CODE NOT FOUND" IN DESC         
         CLC   PSPLCODE,PACCODE                                                 
         BE    PGCMOK                                                           
         ZIC   R0,1(R4)            BUMP TO NEXT ELEMENT                         
         AR    R4,R0                                                            
         B     PGCMLUP                                                          
PGCMOK   DS    0H                                                               
         MVC   ADCDES1,PSPLDESC    REPLACE "CODE NOT FOUND" DESCRIPTION         
PGCMOK10 DS    0H                                                               
*                            "NET DOWN" GROSS, IF PACAMT ENTERED AT NET         
         CLI   PACGN,0                                                          
         BNE   PGCMOK20                                                         
         OC    PACAMT,PACAMT                                                    
         BNZ   PGCMOK20            NO AMT PRESENT                               
         ZAP   DUB2,=P'0'                                                       
         B     PGCMOK60            OUTPUT ZERO AMT                              
*                                                                               
PGCMOK20 DS    0H                                                               
         CLI   PACGN,C'G'          GROSS AMOUNT?                                
         BE    PGCMOK30                                                         
         CLI   PACGN,C'N'          NET AMOUNT?                                  
         BE    PGCMOK40                                                         
*                                                                               
         DC    H'0'                NO OTHER TYPE OF AMT FOR NOW                 
*                                                                               
PGCMOK30 ZAP   DUB2,PACAMT         AMT IN GROSS, NO CALCULATIONS NEEDED         
         B     PGCMOK60                                                         
*                                                                               
PGCMOK40 DS    0H                                                               
         CLI   PACAC,C'N'          SUBJECT TO COMMISSION?                       
         BE    PGCMOK30            NO, NET AND GROSS ARE SAME                   
*                                                                               
         ZAP   MYAGC,PBDACP        AGY COMM FROM PBDELEM                        
         CP    MYAGC,=P'-1'        MEANS 100% AGY COMM                          
         BNE   *+10                                                             
         ZAP   MYAGC,=P'100000'    100 PCT (100.000)                            
         ZAP   FULL,MYAGC                                                       
         OC    PACACOM,PACACOM     ADDTNL CHRGS AGY COMM IS ZERO?               
         BZ    *+10                                                             
         ZAP   FULL,PACACOM        AGY COMM FROM PACELEM (ADD'L. CHG.)          
*                                                                               
         ZAP   WORK(12),PACAMT                                                  
         ZAP   DUB2,=P'100000'                                                  
         SP    DUB2,FULL           NET PCT                                      
         MP    WORK(12),DUB2+4(4)                                               
         DP    WORK(12),=P'100000'   THREE DECIMALS AND PCT                     
         ZAP   DUB2,WORK(8)                                                     
         OI    WORK+11,X'0F'       MAKE POSITIVE                                
         CP    WORK+8(4),=P'50000'                                              
         BL    PGCMOK60                                                         
         CP    DUB2,=P'0'          NEGATIVE AMT?                                
         BL    PGCMOK50            YES                                          
         AP    DUB2,=P'1'          ROUND UP                                     
         B     PGCMOK60                                                         
PGCMOK50 SP    DUB2,=P'1'          ROUND UP (NEGATIVE)                          
*                                                                               
PGCMOK60 EDIT  (P8,DUB2),ADCCHG1,2,COMMAS=YES,FLOAT=-                           
*                 IF ENTERED AT NET, FLOAT AN "N" IN FRONT OF CHARGE $          
         CLI   PACGN,C'N'          NET AMOUNT ?                                 
         BNE   PGCMOK90            NO - LEAVE CHARGE $ ALONE                    
         CP    DUB2,=P'0'          ZERO AMOUNT ?                                
         BE    PGCMOK90            YES - LEAVE CHARGE $ ALONE                   
         LA    RF,L'ADCCHG1        NO OF CHARACTERS IN CHARGE $ FIELD           
         LA    RE,ADCCHG1+(L'ADCCHG1-1)   POINT TO "END" OF FIELD               
PGCMOK64 CLI   0(RE),C' '          BLANK ?                                      
         BNH   PGCMOK69            YES - PLACE THE "N" THERE                    
         AHI   RE,-1               BUMP TO "LEFT"                               
         BCT   RF,PGCMOK64                                                      
         DC    H'0'                FIELD LONGER THAN SPACE ALLOTED              
PGCMOK69 MVI   0(RE),C'N'                                                       
*                                                                               
PGCMOK90 ZIC   RF,LNCNT                                                         
         LA    RF,1(RF)            ADD TO BLOCK LINE COUNT                      
         STC   RF,LNCNT                                                         
PGCMXIT  XIT1                                                                   
*                                                                               
*                                                                               
PGCERR   DS    0H                                                               
         L     RF,SAVER1           A(PARAMETER LIST)                            
         MVI   0(RF),X'FF'         ERROR IN CALL                                
         B     PGCXITX                                                          
*                                                                               
OPTION   DS    0H                                                               
         CLI   TFLAG,X'01'         CHECK IF T OPTION IS ON                      
         BNE   PGCXIT                                                           
         ZIC   R7,LNCNT                                                         
         MHI   R7,ADCLNLEN                                                      
         LA    R7,ADCAREA(R7)                                                   
*                                                                               
         MVC   SPC#ADD(ADCLNLEN),RECAP                                          
         LA    R7,ADCLNLEN(R7)                                                  
*                                                                               
* C'O' OPTION                                                                   
*                                                                               
         CLI   VGETINS,C'O'      CHECK IF 'O' WAS PASSED                        
         BNE   OOPTION1                                                         
         MVI   VGETINS,X'00'     IF IT WAS CLEAR FIRST BYTE OF VGETINS          
         GOTO1 VGETINS,DMCB,PBUYREC,(C'O',PVALUES),PRODUCT,(C'X',0)             
         B     OOPTION2                                                         
*                                                                               
OOPTION1 DS    0H                                                               
         GOTO1 VGETINS,DMCB,PBUYREC,PVALUES,PRODUCT,(C'X',0)                    
*                                                                               
OOPTION2 DS    0H                                                               
*                                                                               
         MVC   SPC#ADD,=CL7'SPACE'                                              
         EDIT  GROSS,CHGGROSS,2,COMMAS=YES,FLOAT=-                              
         L     R5,GROSS                                                         
         S     R5,AGYCOM                                                        
         EDIT  (R5),CHGNET,2,COMMAS=YES,FLOAT=-                                 
         LA    RE,CHGNET+(L'CHGNET-1)   POINT TO "END" OF FIELD                 
TOPTION1 CLI   0(RE),C' '          BLANK ?                                      
         BNH   TOPTION2            YES - PLACE THE "N" THERE                    
         AHI   RE,-1               BUMP TO "LEFT"                               
         BCT   RF,TOPTION1                                                      
         DC    H'0'                FIELD LONGER THAN SPACE ALLOTED              
TOPTION2 MVI   0(RE),C'N'                                                       
         LA    R7,ADCLNLEN(R7)                                                  
*                                                                               
         GOTO1 VGETINS,DMCB,PBUYREC,PVALUES,PRODUCT,(C'A',0),,=C'ALL'           
*                                                                               
         MVC   SPC#ADD,=CL7'ADDN''L'                                            
         EDIT  GROSS,CHGGROSS,2,COMMAS=YES,FLOAT=-                              
         L     R5,GROSS                                                         
         S     R5,AGYCOM                                                        
         EDIT  (R5),CHGNET,2,COMMAS=YES,FLOAT=-                                 
         LA    RE,CHGNET+(L'CHGNET-1)   POINT TO "END" OF FIELD                 
TOPTION3 CLI   0(RE),C' '          BLANK ?                                      
         BNH   TOPTION4            YES - PLACE THE "N" THERE                    
         AHI   RE,-1               BUMP TO "LEFT"                               
         BCT   RF,TOPTION3                                                      
         DC    H'0'                FIELD LONGER THAN SPACE ALLOTED              
TOPTION4 MVI   0(RE),C'N'                                                       
*                                                                               
         ZIC   RF,LNCNT                                                         
         LA    RF,3(RF)            ADD 3 TO BLOCK LINE COUNT                    
         STC   RF,LNCNT                                                         
*                                                                               
PGCXIT   DS    0H                                                               
         LA    R7,ADCAREA                                                       
         ST    R7,FULL                                                          
         L     RF,SAVER1           A(PARAMETER LIST)                            
         MVC   0(4,RF),FULL        ADDRESS OF ADDRESS DATA                      
         MVC   0(1,RF),LNCNT       NO OF LINES IN BLOCK                         
PGCXITX  XIT1                                                                   
         LTORG                                                                  
         SPACE 1                                                                
DMRDHI   DC    CL8'DMRDHI'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
GETREC   DC    CL8'GETREC'                                                      
PRTDIR   DC    CL8'PRTDIR'                                                      
PRTFIL   DC    CL8'PRTFILE'                                                     
RECAP    DC    CL(ADCLNLEN)'** CHARGE RECAP **'                                 
         EJECT                                                                  
WORKD    DSECT                     ** WORKING STORAGE **                        
WORK     DS    CL64                                                             
SPACES   DS    CL20                                                             
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
VGETINS  DS    V                                                                
PRODUCT  DS    V                                                                
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
DUB2     DS    D                                                                
MYAGC    DS    PL4                                                              
ELCODE   DS    CL1                                                              
LNCNT    DS    XL1                 NO. OF LINES IN BLOCK (WPRK)                 
TFLAG    DS    XL1                 FLAG FOR T OPTION '01' IS ON                 
FULL     DS    F                                                                
SAVER1   DS    F                                                                
       ++INCLUDE PVALUES                                                        
*                                                                               
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
ADCAREA  DS    CL(ADCDLNTH)        CREATE OUTPUT BLOCK HERE                     
IO       DS    4000C               FOR PSPCGREC (ADDITIONAL CHARGES)            
WORKX    EQU   *                                                                
         EJECT                                                                  
******   ADDCHGD DSECT                                                          
       ++INCLUDE PPGETCGD                                                       
         EJECT                                                                  
PBUYRECD DSECT                                                                  
       ++INCLUDE PBUYREC                                                        
       ++INCLUDE PBDELEM                                                        
PACELEMD DSECT                                                                  
       ++INCLUDE PACELEM                                                        
         EJECT                                                                  
PSPLRECD DSECT                                                                  
       ++INCLUDE PSPCGREC                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010PPGETCGR  06/08/01'                                      
         END                                                                    
