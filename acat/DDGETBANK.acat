*          DATA SET DDGETBANK  AT LEVEL 009 AS OF 03/22/10                      
*CATALP GETBANK                                                                 
*                                                                               
**********************************************************************          
*                                                                    *          
* PARAMETERS ARE AS FOLLOWS                                          *          
*                                                                    *          
* P1  BYTE  0     X'01' - BANK INFO                                  *          
*     BYTE 1-3    A(BLOCK)                                           *          
*                                                                    *          
* P2  BYTE 0      X'00'                                              *          
*     BYTE 1-3    A(COMFACS)                                         *          
*                                                                    *          
* P3  BYTE 0      X'00'                                              *          
*     BYTE 1-3    A(SYSTEM SPECIFIC OVERLAY)                         *          
*                                                                    *          
**********************************************************************          
         TITLE 'GETBANK - GENERAL BANK INFORMATION'                             
GETBANK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,*GETBANK,RR=RE                                       
         USING WORKD,RC                                                         
*                                                                               
         MVC   PARMS(PARMSLNQ),0(R1)                                            
         ST    RE,RELO                                                          
*                                                                               
         USING BANKD,R2                                                         
         SR    R2,R2                                                            
         ICM   R2,7,PBLOCK                                                      
         USING COMFACSD,R1                                                      
         ICM   R1,15,PCOMFACS                                                   
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VMASTC,CMASTC                                                    
         MVC   VGETFACT,CGETFACT                                                
         DROP  R1                                                               
*                                                                               
         BRAS  RE,INIT                                                          
         BRAS  RE,BANKREAD                                                      
*                                                                               
         USING MASTD,RE                                                         
         ICM   RE,15,VMASTC        MASTC RESOLVED?                              
         BZ    MAIN10              . NO, EXIT                                   
*                                                                               
         USING UTLD,R1                                                          
         L     R1,MCUTL            R1=A(UTL)                                    
         MVC   TSYS,SVTSYS         RESTORE PHYSICAL SYSTEM NUMBER               
         DROP  R1,RE                                                            
*                                                                               
MAIN10   OC    PSYSOVR,PSYSOVR     ANY SYSTEM SPECIFIC OVERLAY?                 
         BNZ   MAIN30                                                           
         LA    RE,SYSTAB                                                        
MAIN20   CLI   0(RE),EOF           DO WE HAVE SYSTEM SPECIFIC OVERLAY?          
         BE    EXIT                                                             
         CLC   SVTOVSYS,0(RE)                                                   
         BE    *+12                                                             
         LA    RE,L'SYSTAB(RE)                                                  
         B     MAIN20                                                           
*                                                                               
         SR    RF,RF                                                            
         IC    RF,1(RE)                                                         
         LA    RE,RELOLST                                                       
         AR    RE,RF                                                            
         MVC   PSYSOVR,0(RE)                                                    
MAIN30   GOTO1 PSYSOVR,PARMS                                                    
*                                                                               
EXIT     XIT1                                                                   
*                                                                               
SYSTAB   DS    0AL2                                                             
         DC    AL1(06),AL1(VACCOVR-RELOLST)        ACCOUNTING OVERLAY           
         DC    AL1(EOF)                                                         
         EJECT                                                                  
***********************************************************************         
* INIT                                                                          
***********************************************************************         
         SPACE 1                                                                
INIT     NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R1,RELOLST          RELOCATE ADCONS                              
         LA    R0,RELOLSTN                                                      
         LA    RE,ADCONS                                                        
INIT05   L     RF,0(RE)                                                         
         A     RF,RELO                                                          
         ST    RF,0(R1)                                                         
         AHI   R1,L'RELOLST                                                     
         AHI   RE,L'ADCONS                                                      
         BCT   R0,INIT05                                                        
*                                                                               
         A     RE,RELO                                                          
         ICM   RE,15,VMASTC         MASTC RESOLVED?                             
         BZ    INIT10               . NO, EXIT                                  
         USING MASTD,RE                                                         
         L     R1,MCUTL             R1=A(UTL)                                   
         USING UTLD,R1                                                          
         MVC   SVTSYS,TSYS          SAVE SE # OF CALLING PROGRAM                
         MVC   SVTOVSYS,MCOVSYS     SAVE SYSTEM OF CALLING PROGRAM              
         MVI   TSYS,X'0A'           CONTROL                                     
         B     INIT20                                                           
         DROP  R1,RE                                                            
*                                                                               
INIT10   GOTOR VGETFACT,DMCB,0                                                  
         L     RE,0(R1)                                                         
         USING FACTSD,RE           R1=A(SYSTEM DEFINITION BLOCK)                
         MVC   SVTOVSYS,FAOVSYS     SAVE SYSTEM OF CALLING PROGRAM              
         DROP  RE                                                               
*                                                                               
INIT20   LA    RE,BRTNAREA         RE=A(OUTPUT RETURN AREA)                     
         LHI   RF,BRTNLNQ          RF=(LENGTH OF OUTPUT RETURN AREA)            
         SR    R1,R1                                                            
         MVCL  RE,R0               CLEAR RETURN AREA                            
*                                                                               
         MVI   SPACES,X'40'        INIT THE FIELD WITH A SPACE                  
         LA    RE,SPACES                                                        
         LHI   RF,L'SPACES                                                      
         SR    R1,R1                                                            
         ICM   R1,8,SPACES         FILL SPACES WITH SPACES                      
         MVCL  RE,R0                                                            
*                                                                               
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ BANK INFO                                                                
***********************************************************************         
         SPACE 1                                                                
         USING BANKRECD,IO                                                      
BANKREAD NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         USING BANKTBD,R5                                                       
         LA    R5,BANKTAB                                                       
BR010    CLI   0(R5),EOF           END OF BANK TABLE READING?                   
         BE    BRX                                                              
         LA    RE,BANCDE           POINT TO START OF STORAGE                    
         SR    R1,R1                                                            
         IC    R1,BNKFLD           ADD DISPLACEMENT TO CUR FIELD                
         AR    RE,R1                                                            
         SR    R1,R1                                                            
         IC    R1,BNKFLN                                                        
         AHI   R1,-1                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),SPACES                                                   
         BNH   BRX                                                              
*                                                                               
         MVC   SVDISP,BNKNDSP      SAVE OFF DISPL TO NAME FIELD                 
         XC    BANKEY,BANKEY                                                    
         MVI   BANKTYP,BANKTYPQ    C'B' BANK RECORD                             
         MVI   BANKSUB,BANKSGQ     C'G' GENERAL RECORD                          
         SR    R1,R1                                                            
         IC    R1,BNKKYLN                                                       
         AHI   R1,-1                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   BANKCDE(0),BANCDE   BANK/HUB/BRANCH CODE                         
         MVC   BANKFLAG,BNKSTAT    MOVE STATUS BYTE- RECORD NOT FOUND           
         DROP  R5                                                               
*                                                                               
         MVC   KEY,IO                                                           
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'GENDIR',KEY,IO                       
         BNE   BRX                  EXIT IF NOT FOUND                           
*                                                                               
         GOTO1 VDATAMGR,DMCB,=C'GETREC',=C'GENFIL',BANKDA,IO,DMWORK             
         CLI   8(R1),0                                                          
         BNE   BRX                  EXIT IF NOT FOUND                           
*                                                                               
         XC    BANKFLAG,BANKFLAG    CLEAR STATUS TO BANK RECORD FOUND           
         LA    R4,IO+BANFIRST                                                   
BR020    CLI   0(R4),0                                                          
         BE    BR090                                                            
         CLI   0(R4),NAMELQ         NAME ELEMENT                                
         BE    BR040                                                            
         CLI   0(R4),IADELQ         INTERNATIONAL ADDRESS ELEMENT               
         BE    BR050                                                            
         CLI   0(R4),BCOELCQ        CLIENT SERVICE CONTACT ELEMENT              
         BE    BR060                                                            
         CLI   0(R4),BCOELTQ        TECHNICAL CONTACT ELEMENT                   
         BE    BR060                                                            
         CLI   0(R4),BAGELQ         BANK GENERAL DETAIL ELEMENT                 
         BE    BR070                                                            
         CLI   0(R4),BATELQ         BANK TRANSMISSION INFO ELEMENT              
         BE    BR080                                                            
BR030    SR    R0,R0                                                            
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BR020                                                            
*                                                                               
         USING NAMELD,R4                                                        
BR040    LA    RE,BNAMES                                                        
         SR    R1,R1                                                            
         IC    R1,SVDISP                                                        
         AR    RE,R1                                                            
         IC    R1,NAMLN                                                         
         CHI   R1,L'BNAMES                                                      
         BNH   *+8                                                              
         LHI   R1,L'BNAMES                                                      
         SHI   R1,NAMLN1Q+1                                                     
         EX    R1,*+8                                                           
         B     BR030                                                            
         MVC   0(0,RE),NAMEREC     BANK/HUB/BRANCH NAME                         
*                                                                               
         USING IADELD,R4                                                        
BR050    MVC   BANADDR1,IADLINE1   ADDRESS LINE 1                               
         MVC   BANADDR2,IADLINE2   ADDRESS LINE 2                               
         MVC   BANCSZC,IADCITY     CITY/STATE/ZIP/COUNTRY                       
         B     BR030                                                            
         DROP  R4                                                               
*                                                                               
         USING BCOELD,R4                                                        
BR060    CLC   BCONA,SPACES        ANYTHING IN NAME ?                           
         BNH   BR030                                                            
         LA    RE,BANCCONT         ASSUME IT'S CLIENT SERVICE CONTACT           
         CLI   BCOEL,BCOELCQ                                                    
         BE    *+8                                                              
         LA    RE,BANTCONT         POINT TO TECHNICAL CONTACT                   
         MVI   BANCIND-BANCCONT(RE),C'C'               CTFILE IND               
         MVC   BANCCNME-BANCCONT(L'BANCCNME,RE),BCONA  CONTACT NAME             
         MVC   BANCPNO-BANCCONT(L'BANCPNO,RE),BCOPH    PHONE NUMBER             
         SR    R1,R1                                                            
         IC    R1,BCOLN                                                         
         SHI   R1,BCOLNQ+1                                                      
         BM    BR030                                                            
         EX    R1,*+8                                                           
         B     BR030                                                            
         MVC   BANCEML-BANCCONT(0,RE),BCOEM     CONTACT EMAIL ADDR              
         DROP  R4                                                               
*                                                                               
         USING BAGELD,R4                                                        
BR070    MVC   BANRNO,BAGRNO       ROUTING NUMBER                               
         CLC   BAGFORM,SPACES      IS FORMAT CODE SET AT THIS LEVEL             
         BNH   BR030               NO                                           
         MVC   BANDFFKY,BAGFORM    FORMAT CODE - ANY LEVEL                      
         B     BR030                                                            
         DROP  R4                                                               
*                                                                               
         USING BATELD,R4                                                        
BR080    CLC   BATTKEY,SPACES      SET AT THIS LEVEL ?                          
         BNH   *+10                NO THEN DON'T OVERWRITE                      
         MVC   BANDFTKY,BATTKEY    TRANSMISSION KEY                             
*                                                                               
         CLC   BATUSER,SPACES                                                   
         BNH   *+10                                                             
         MVC   BANDFUSR,BATUSER    ADVANTIS USER ID                             
*                                                                               
         CLC   BATACCN,SPACES                                                   
         BNH   *+10                                                             
         MVC   BANDFACN,BATACCN    ADVANTIS ACCOUNT NUMBER                      
*                                                                               
         CLC   BATCLAS,SPACES                                                   
         BNH   *+10                                                             
         MVC   BANDFCLS,BATCLAS    CLASS NAME                                   
*                                                                               
         CLC   BATCHRG,SPACES                                                   
         BNH   *+10                                                             
         MVC   BANDFCHR,BATCHRG    CHARGE                                       
*                                                                               
         OC    BATTTYP,BATTTYP                                                  
         BZ    *+10                                                             
         MVC   BANDFTYP,BATTTYP    TRANSMISSION TYPE                            
*                                                                               
         CLC   BATCMPR,SPACES                                                   
         BNH   BR030                                                            
         CLI   BATCMPR,C'Y'        CHECK IF COMPRESSION IS USED                 
         BNE   BR030                                                            
         OI    BANDFSTA,BANDFCMR                                                
         B     BR030                                                            
         DROP  R4                                                               
*                                                                               
BR090    AHI   R5,BANKLNQ                                                       
         B     BR010                                                            
*                                                                               
BRX      J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* TABLES                                                              *         
***********************************************************************         
         SPACE 1                                                                
BANKTAB  DS    0C                                                               
         DC    AL1(BANCDE-BANCDE),AL1(L'BANCDE),AL1(L'BANCDE),AL1(BNKNM*        
               E-BNAMES),AL1(BCDENFND)                                          
         DC    AL1(BANHUB-BANCDE),AL1(L'BANHUB),AL1(L'BANCDE+L'BANHUB),*        
               AL1(HUBNME-BNAMES),AL1(BHUBNFND)                                 
         DC    AL1(BANBRA-BANCDE),AL1(L'BANBRA),AL1(L'BANCDE+L'BANHUB+L*        
               'BANBRA),AL1(BRNNME-BNAMES),AL1(BBRANFND)                        
         DC    AL1(EOF)                                                         
         EJECT                                                                  
***********************************************************************         
* CONSTANTS                                                           *         
***********************************************************************         
         SPACE 1                                                                
ADCONS   DS    0A                                                               
         DC    V(ACGTBNK)                                                       
         EJECT                                                                  
***********************************************************************         
* LITERALS                                                            *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* STORAGE DSECT                                                       *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
RELOLST  DS    0A                                                               
VACCOVR  DS    A                                                                
RELOLSTN EQU   (*-RELOLST)/L'RELOLST                                            
*                                                                               
RELO     DS    A                                                                
*                                                                               
PARMS    DS    0A                                                               
PREC     DS    XL1                                                              
PBANK    EQU   X'01'                                                            
PFRM     EQU   X'02'                                                            
PBLOCK   DS    AL3                 USER BLOCK                                   
PCOMFACS DS    AL4                                                              
PSYSOVR  DS    AL4                 SYSTEM SPECIFIC OVERLAY                      
PARMSLNQ EQU   *-PARMS                                                          
*                                                                               
VDATAMGR DS    A                                                                
VMASTC   DS    A                                                                
VGETFACT DS    A                                                                
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
ACURREC  DS    A                   CURRENT ADDRESS OF RECORD ENTRIES            
ACURENT  DS    A                   CURRENT ADDRESS OF FIELD ENTRIES             
*                                                                               
HALF     DS    H                                                                
BYTE     DS    X                                                                
SVDISP   DS    X                   SAVED AREA FOR DISPLACMENT TO BNAME          
SVADDR   DS    AL4                 SAVED AREA FOR ADDRESS                       
SVADDR2  DS    AL4                 SAVED AREA FOR ADDRESS                       
SVADDR3  DS    AL4                 SAVED AREA FOR ADDRESS                       
*                                                                               
SVTSYS   DS    XL(L'TSYS)                                                       
SVTOVSYS DS    XL(L'TOVSYS)                                                     
*                                                                               
KEY      DS    CL42                                                             
KEYSAVE  DS    CL42                                                             
DMWORK   DS    XL42                                                             
*                                                                               
SPACES   DS    CL180                                                            
*                                                                               
         DS    0D                                                               
IO       DS    2000C                                                            
WORKX    EQU   *                                                                
*                                                                               
EOF      EQU   X'FF'                                                            
         EJECT                                                                  
***********************************************************************         
* BANK TABLE DSECT                                                    *         
***********************************************************************         
         SPACE 1                                                                
BANKTBD  DSECT                                                                  
BNKFLD   DS    AL1                 DISPLACEMENT TO CURRENT FIELD                
BNKFLN   DS    XL1                 FIELD LENGTH                                 
BNKKYLN  DS    XL1                 KEY FIELD LENGTH COMBINED                    
BNKNDSP  DS    XL1                 KEY FIELD LENGTH COMBINED                    
BNKSTAT  DS    XL1                 STATUS                                       
BANKLNQ  EQU   *-BANKTBD                                                        
         EJECT                                                                  
***********************************************************************         
* FIELD ADDRESS TABLE                                                 *         
***********************************************************************         
         SPACE 1                                                                
FLDATBD  DSECT                                                                  
FLDAREC# DS    XL1                 FIELD NUMBER                                 
FLDALOC  DS    AL4                 ADDRESS OF FIELD ENTRY TABLE                 
FLDALNQ  EQU   *-FLDATBD                                                        
         EJECT                                                                  
***********************************************************************         
* GETBANK DSECT                                                       *         
***********************************************************************         
         SPACE 1                                                                
       ++INCLUDE DDGETBANKD                                                     
         EJECT                                                                  
         PRINT OFF                                                              
***********************************************************************         
* INCLUDES                                                            *         
***********************************************************************         
         SPACE 1                                                                
*                                                                               
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE FASYSLSTD                                                      
       ++INCLUDE GEGENFILE                                                      
       ++INCLUDE GEGENBNK                                                       
       ++INCLUDE ACKEYWRD                                                       
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE FAUTL                                                          
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE ACGENFILE                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDGETBANK 03/22/10'                                      
         END                                                                    
