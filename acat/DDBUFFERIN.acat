*          DATA SET DDBUFFERIN AT LEVEL 002 AS OF 10/15/04                      
*CATALP BUFFERIN                                                                
BUFFERIN TITLE '- FLEXIBLE ACCUMULATOR MAINTENANCE'                             
BUFFERIN CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**BUFF**,RA,CLEAR=YES                                      
         USING WORKD,RC            RC=A(WORKING STORAGE)                        
         LR    R2,R1                                                            
         USING BUFFPARM,R2         R2=A(PARAMETER LIST)                         
         SR    R3,R3                                                            
         ICM   R3,7,BUFFABLK       R3=A(CONTROL BLOCK)                          
         USING BUFFD,R3                                                         
FILE     USING DTFPHD,BUFFFILE                                                  
                                                                                
         STCM  RF,8,MODE           SAVE CALLING PROGRAM MODE                    
         ST    RD,SAVERD                                                        
                                                                                
         L     RF,BUFFACOM         EXTRACT V(DATAMGR) FROM COMFACS              
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RF)                                   
                                                                                
         LH    R7,BUFFLKEY                                                      
         SLL   R7,1                                                             
         AHI   R7,4                R7=L'INDEX ENTRY                             
         LH    R8,BUFFLKEY                                                      
         BCTR  R8,0                R8=L'KEY-1 (FOR EXECUTE)                     
         SR    R9,R9                                                            
         ICM   R9,7,BUFFAREC       R9=A(NEW RECORD)                             
                                                                                
         LA    RF,*+10             TURN ON XA ADDRESSING MODE                   
         O     RF,XAON                                                          
         BSM   0,RF                                                             
                                                                                
         MVI   BUFFERRS,0          RESET ERROR RETURN                           
         CLI   BUFFACTN,BUFFACLO                                                
         BNH   *+6                                                              
         DC    H'0'                INVALID ACTION CODE                          
         CLI   BUFFACTN,BUFFAINI   TEST ACTION IS INITIALIZE                    
         BE    BUFF02                                                           
                                                                                
         TM    BUFFINDS,BUFFIINI   NO - TEST BUFFER INITIALIZED                 
         BNZ   *+14                                                             
         CLI   BUFFACTN,BUFFACLO   IGNORE IF ACTION IS CLOSE                    
         BE    EXITCC                                                           
         DC    H'0'                ELSE - INVALID ACTION SEQUENCE               
                                                                                
         SR    RE,RE                                                            
         ICM   RE,7,BUFFUCNT       BUMP USAGE COUNTER                           
         AHI   RE,1                                                             
         STCM  RE,7,BUFFUCNT                                                    
                                                                                
         L     RE,BUFFANDX                                                      
         ST    RE,ANDXNDX          SET A(BUFFER INDEX)                          
         SR    RF,RF                                                            
         IC    RF,BUFFNBUF                                                      
         MHI   RF,NDXLEN                                                        
         AR    RE,RF                                                            
         ST    RE,ATRKNDX          SET A(TRACK INDEX)                           
                                                                                
BUFF02   SR    RF,RF               GO TO ACTION ROUTINE                         
         IC    RF,BUFFACTN                                                      
         SLL   RF,2                                                             
         B     *+4(RF)                                                          
                                                                                
         B     BUFINI              0 - OPEN FILE/ACQUIRE BUFFER                 
         B     BUFPUT              1 - PUT RECORD                               
         B     BUFRDH              2 - READ HIGH FOR KEY                        
         B     BUFRDH              3 - READ FOR KEY                             
         B     BUFSEQ              4 - GET NEXT SEQUENTIAL RECORD               
         DC    AL4(0)              5 - N/D                                      
         DC    AL4(0)              6 - N/D                                      
         DC    AL4(0)              7 - N/D                                      
         DC    AL4(0)              8 - N/D                                      
         B     BUFCLO              9 - CLOSE FILE/FREE BUFFERS                  
                                                                                
EXITCC   C     RD,SAVERD           TEST EXITING TO CALLER                       
         BNE   EXITCC02                                                         
                                                                                
         LA    RE,EXITCC02         YES - RESET MODE FOR CALLER                  
         ICM   RE,8,MODE                                                        
         BSM   0,RE                                                             
                                                                                
EXITCC02 CLI   BUFFERRS,0          SET CONDITION CODE FOR CALLER                
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALIZATION - OPEN FILE, ALLOCATE TRACK BUFFER AND INDEX AND     *         
* CLEAR BUFFERS ETC.                                                  *         
***********************************************************************         
                                                                                
BUFINI   CLI   BUFFLKEY,0          ENSURE KEY LENGTH NOT GR 255                 
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         TM    BUFFINDS,BUFFIINI   TEST ALREADY INITIALIZED                     
         BZ    BUFINI04                                                         
         CLC   BUFFLKEY(BUFFUCNT-BUFFLKEY),FILE.DNDX                            
         BE    BUFINI02            SAME VALUES AS LAST TIME                     
         NI    BUFFINDS,FF-(BUFFIINI)                                           
         SR    R1,R1               NO - NEED TO REFRESH BUFFERS                 
         IC    R1,BUFFNBUF                                                      
         M     R0,BUFFLBUF                                                      
         LR    R0,R1                                                            
         A     R0,BUFFLNDX         R0=L'ACQUIRED STORAGE                        
         L     R1,BUFFANDX         FREE PREVIOUS BUFFERS                        
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    BUFINI06                                                         
         DC    H'0'                                                             
                                                                                
BUFINI02 OC    BUFFNREC,BUFFNREC   TEST ANY RECORDS ADDED                       
         BZ    EXITCC              NO - ALL READY TO GO THEN                    
         B     BUFINI08            ELSE JUST CLEAR COUNTS AND INDEX             
                                                                                
BUFINI04 XC    PARM(PARML),PARM    OPEN FILE & EXTRACT VALUES                   
         GOTOR VDATAMGR,PARM,DMDADDS,DAOPEN,,,BUFFFILE                          
         GOTOR (RF),(R1),,DATRNS,,,,=X'FFFF0101',DUB                            
         MVC   BUFFNTRK,DUB        EXTRACT N'TRACKS ALLOCATED TO FILE           
         GOTOR (RF),(R1),,DARPT,,X'0000FFFF'                                    
         MVC   BUFFLTRK,P4+2       SAVE TRACK CAPACITY                          
                                                                                
BUFINI06 MVC   FILE.DNDX(BUFFUCNT-BUFFLKEY),BUFFLKEY                            
         SR    R0,R0                                                            
         IC    R0,BUFFNCOL         (NOTE:- BUFFNCOL=ZERO FOR TYPE=DATA)         
         SLL   R0,2                N'COLUMNS*4 (FOR BINARY)                     
         TM    BUFFINDS,BUFFIBIN   TEST BINARY ACCUMS                           
         BNZ   *+8                                                              
         SLL   R0,1                N'COLUMNS*8 (FOR PACKED)                     
         AH    R0,BUFFLKEY         +L'KEY                                       
         AH    R0,BUFFLCOM         +L'COMMENT = TOTAL RECORD LENGTH             
         STH   R0,BUFFLREC         SAVE TOTAL RECORD LENGTH                     
                                                                                
         SR    RE,RE                                                            
         ICM   RE,3,BUFFLTRK                                                    
         CR    R0,RE                                                            
         BNH   *+6                                                              
         DC    H'0'                RECORD LONGER THAN A TRACK                   
         SRDL  RE,32                                                            
         DR    RE,R0                                                            
         STH   RF,BUFFRPRT         SAVE N'RECORDS PER TRACK                     
         MR    RE,R0               RF=L'TRACK BUFFER                            
         ST    RF,BUFFLBUF         SET L'TRACK BUFFER                           
                                                                                
         LH    RE,BUFFNTRK                                                      
         SRDL  RE,32               RF=N'TRACKS                                  
         MR    RE,R7               RF=(N'TRACKS)*L'TRACK INDEX                  
         SR    RE,RE                                                            
         IC    RE,BUFFNBUF                                                      
         CHI   RE,MINNBUF                                                       
         BNL   *+8                                                              
         LHI   RE,MINNBUF                                                       
         STC   RE,BUFFNBUF                                                      
         MHI   RE,NDXLEN           RE=N'BUFFERS*L'BUFFER INDEX                  
         AR    RF,RE                                                            
         ST    RF,BUFFLNDX         SET TOTAL L'INDEX                            
                                                                                
         TM    BUFFINDS,BUFFIINI   TEST NEED TO ACQUIRE STORAGE                 
         BNZ   BUFINI08                                                         
         SR    R1,R1               YES - GET ABOVE THE LINE AREA                
         IC    R1,BUFFNBUF                                                      
         M     R0,BUFFLBUF         R1=N'BUFFERS*BUFFER LENGTH                   
         LR    R0,R1                                                            
         AR    R0,RF               R0=L'INDEX+L'BUFFERS                         
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF               TEST FOR GETMAIN ERROR                       
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
         ST    R1,BUFFANDX         SET A(TRACK BUFFER)                          
         A     R1,BUFFLNDX                                                      
         ST    R1,BUFFABUF         SET A(BUFFERS)                               
                                                                                
BUFINI08 XC    BUFFALST,BUFFALST   CLEAR A(LAST RECORD)                         
         XC    BUFFNREC,BUFFNREC   CLEAR N'RECORDS IN FILE                      
         XC    BUFFUTRK,BUFFUTRK   CLEAR N'TRACKS USED                          
         XC    BUFFCTRK,BUFFCTRK   CLEAR LAST TRACK NUMBER                      
         XC    BUFFUCNT,BUFFUCNT   CLEAR USAGE COUNTER                          
         XC    BUFFNIOS,BUFFNIOS   CLEAR I/O COUNT                              
         NI    BUFFINDS,BUFFIBIN+BUFFIPAK+BUFFIDTA+BUFFIRCM                     
         OI    BUFFINDS,BUFFIINI   SET INITIALIZED                              
                                                                                
         LA    RE,*+10             ENSURE XA MODE FOR MVCL BELOW                
         O     RE,XAON                                                          
         BSM   0,RE                                                             
                                                                                
         L     R1,BUFFLNDX         R1=L'INDEX                                   
         L     R0,BUFFANDX         R0=A(INDEX)                                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEAR THE BUFFER INDEX                       
                                                                                
BUFINIX  B     EXITCC                                                           
         EJECT                                                                  
***********************************************************************         
* ADD A NEW RECORD OR UPDATE AN EXISTING ONE                          *         
***********************************************************************         
                                                                                
BUFPUT   OC    BUFFNREC,BUFFNREC   TEST ANY RECORDS ADDED TO FILE YET           
         BNZ   BUFPUT02                                                         
         GOTOR GETTRK,0            INITIALIZE FIRST BUFFER                      
         B     BUFPUT18                                                         
                                                                                
BUFPUT02 GOTOR BUFRDHN             LOCATE RECORD                                
         BE    BUFPUT26            RECORD FOUND - UPDATE IT                     
         TM    BUFFERRS,BUFFEEOF                                                
         MVI   BUFFERRS,0          RESET ERROR BYTE                             
         BZ    BUFPUT04                                                         
                                                                                
BUFPUT04 L     R6,ARECORD                                                       
         L     RF,ATRKNTRY         INSERT A NEW RECORD INTO BUFFER              
         SR    RE,RE                                                            
         ICM   RE,3,TRKREC-TRKD(RF)                                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CH    RE,BUFFRPRT         TEST THIS BUFFER IS FULL                     
         BNE   BUFPUT14                                                         
                                                                                
         LR    R0,RE               BUFFER IS FULL                               
         MH    R0,BUFFLREC                                                      
         L     RF,ABUFFER                                                       
         CR    R6,RF               TEST ADDING TO BOTTOM OF BUFFER              
         BE    BUFPUT06                                                         
         AR    R0,RF                                                            
         CR    R0,R6               OR ADDING TO TOP OF BUFFER                   
         BE    BUFPUT08                                                         
         B     BUFPUT10                                                         
                                                                                
BUFPUT06 L     RF,ATRKNTRY         SEE IF RECORD FITS IN PREVIOUS TRACK         
         SR    RF,R7                                                            
         C     RF,ATRKNDX                                                       
         BL    BUFPUT08                                                         
         CLC   TRKREC-TRKD(,RF),BUFFRPRT                                        
         BE    BUFPUT08                                                         
         ST    RF,STRKNTRY                                                      
         MVC   DATRK,TRKTRK-TRKD(RF)                                            
         GOTOR GETTRK,DATRK        IT DOES FIT - GET TRACK INTO STORAGE         
         L     RF,STRKNTRY                                                      
         ST    RF,ATRKNTRY                                                      
         SR    RE,RE                                                            
         ICM   RE,3,TRKREC-TRKD(RF)                                             
         MH    RE,BUFFLREC                                                      
         A     RE,ABUFFER                                                       
         ST    RE,ARECORD          SET INSERTION POINT AT END OF BUFFER         
         B     BUFPUT18                                                         
                                                                                
BUFPUT08 GOTOR GETTRK,0            GET A NEW TRACK & BUFFER                     
         B     BUFPUT18                                                         
                                                                                
BUFPUT10 MVC   HTRK,BUFFCTRK       SPLIT THE BLOCK (HOLD THIS ONE)              
         MVC   SBUFFER,ABUFFER     SAVE A(CURRENT BUFFER)                       
         MVC   STRKNTRY,ATRKNTRY   SAVE A(CURRENT TRACK INDEX ENTRY)            
         MVC   SNDXNTRY,ANDXNTRY   SAVE A(CURRENT BUFFER INDEX ENTRY)           
         GOTOR GETTRK,0            GET A NEW TRACK & BUFFER                     
         LH    RF,BUFFRPRT         SPLIT CURRENT BLOCK INTO TWO                 
         SRL   RF,1                                                             
         ST    RF,DUB+4            SAVE N'RECORDS MOVED                         
         MH    RF,BUFFLREC         RF=(N'RECORDS/2)*RECORD LENGTH               
         ST    RF,DUB              SAVE L'RECORDS TO BE MOVED                   
                                                                                
         L     RE,ABUFFER                                                       
         L     R0,SBUFFER                                                       
         A     R0,BUFFLBUF                                                      
         S     R0,DUB                                                           
         LR    R1,RF                                                            
         MVCL  RE,R0               MOVE TOP HALF TO 2ND BUFFER                  
                                                                                
         L     R0,SBUFFER                                                       
         A     R0,BUFFLBUF                                                      
         S     R0,DUB                                                           
         L     R1,DUB                                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEAR THE MOVED AREA IN 1ST BUFFER           
                                                                                
         L     RF,SNDXNTRY         POINT TO SAVED BUFFER INDEX ENTRY            
         OI    NDXINDS-NDXD(RF),NDXIBWP                                         
         L     RF,STRKNTRY                                                      
         SR    RE,RE                                                            
         ICM   RE,3,TRKREC-TRKD(RF)                                             
         S     RE,DUB+4            RE=N'RECORDS REMAINING IN BLOCK              
         STCM  RE,3,TRKREC-TRKD(RF)                                             
         BCTR  RE,0                                                             
         MH    RE,BUFFLREC                                                      
         A     RE,SBUFFER                                                       
         LA    RF,TRKLEN+1(RF,R8)                                               
         EX    R8,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(RE)       SET NEW HIGH KEY IN BLOCK                    
                                                                                
         L     R0,DUB+4                                                         
         L     RF,ATRKNTRY         BUILD ENTRY FOR NEW TRACK                    
         STCM  R0,3,TRKREC-TRKD(RF)                                             
         BCTR  R0,0                                                             
         MH    R0,BUFFLREC                                                      
         L     RE,ABUFFER                                                       
         EX    R8,*+8                                                           
         B     *+10                                                             
         MVC   TRKKEYS-TRKD(0,RF),0(RE)                                         
         LA    RF,TRKKEYS+1-TRKD(RF,R8)                                         
         AR    RE,R0               POINT TO LAST RECORD IN BUFFER               
         EX    R8,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(RE)                                                    
         GOTOR UPDTRK              MOVE INDEX TO CORRECT SLOT                   
         B     BUFPUT02            NOW GO BACK AND INSERT RECORD                
                                                                                
BUFPUT14 SR    RE,RE               CREATE SPACE FOR RECORD IN BLOCK             
         ICM   RE,3,TRKREC-TRKD(RF)                                             
         MH    RE,BUFFLREC                                                      
         L     RF,ABUFFER                                                       
         AR    RF,RE                                                            
         ST    RF,DUB              DUB=A(NEXT RECORD IN BUFFER)                 
                                                                                
BUFPUT16 CLC   ARECORD,DUB         TEST INSERTION POINT REACHED                 
         BE    BUFPUT18                                                         
         L     RE,DUB                                                           
         LR    R0,RE                                                            
         LH    R1,BUFFLREC                                                      
         SR    R0,R1                                                            
         ST    R0,DUB                                                           
         LR    RF,R1                                                            
         MVCL  RE,R0               MOVE PREVIOUS RECORD TO THIS                 
         B     BUFPUT16                                                         
                                                                                
BUFPUT18 L     RE,ARECORD          MOVE RECORD INTO BUFFER                      
         LR    R0,R9                                                            
         LH    R1,BUFFLREC                                                      
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
                                                                                
         L     RF,ATRKNTRY         UPDATE LOW/HIGH KEY IN INDEX                 
         AHI   RF,TRKLEN           RF=A(LOW KEY)                                
         EX    R8,*+8                                                           
         BZ    BUFPUT20                                                         
         OC    0(0,RF),0(RF)                                                    
         EX    R8,*+8                                                           
         BL    BUFPUT22                                                         
         CLC   0(0,RF),0(R9)                                                    
BUFPUT20 EX    R8,*+8              UPDATE LOW KEY IN BLOCK                      
         B     BUFPUT22                                                         
         MVC   0(0,RF),0(R9)                                                    
                                                                                
BUFPUT22 AH    RF,BUFFLKEY                                                      
         EX    R8,*+8                                                           
         BH    BUFPUT24                                                         
         CLC   0(0,RF),0(R9)                                                    
         EX    R8,*+8              UPDATE HIGH KEY IN BLOCK                     
         B     BUFPUT24                                                         
         MVC   0(0,RF),0(R9)                                                    
                                                                                
BUFPUT24 L     RF,ATRKNTRY         UPDATE TRACK INDEX                           
         SR    R1,R1                                                            
         ICM   R1,3,TRKREC-TRKD(RF)                                             
         LA    RE,1(R1)                                                         
         STCM  RE,3,TRKREC-TRKD(RF)                                             
                                                                                
         L     RE,BUFFNREC         BUMP TOTAL N'RECORDS ADDED                   
         AHI   RE,1                                                             
         ST    RE,BUFFNREC                                                      
                                                                                
         L     RF,ANDXNTRY         SET BLOCK WRITE PENDING                      
         OI    NDXINDS-NDXD(RF),NDXIBWP                                         
                                                                                
         LTR   R1,R1               TEST FIRST RECORD JUST ADDED                 
         BNZ   *+8                                                              
         GOTOR UPDTRK              YES - UPDATE TRACK INDEX                     
         B     BUFPUTX                                                          
                                                                                
BUFPUT26 L     RF,ANDXNTRY         SET BUFFER WRITE PENDING                     
         OI    NDXINDS-NDXD(RF),NDXIBWP                                         
         L     R6,ARECORD                                                       
         AH    R6,BUFFLKEY         POINT TO COMMENT IN INPUT RECORD             
         AH    R9,BUFFLKEY         AND OUTPUT RECORD TOO                        
                                                                                
         SR    R1,R1                                                            
         ICM   R1,3,BUFFLCOM       R1=L'COMMENT                                 
         BZ    BUFPUT32            NO COMMENTS IN RECORD                        
         TM    BUFFINDS,BUFFIDTA   TEST TYPE=DATA SPECIFIED                     
         BNZ   BUFPUT30            YES - ALWAYS HAVE COMMENTS REPLACED          
         TM    BUFFINDS,BUFFIRCM   TEST REPCOM=Y SPECIFIED                      
         BNZ   BUFPUT30            YES - ALWAYS REPLACE COMMENT                 
         LR    RE,R9               RE=A(NEW COMMENT)                            
         LR    R0,R1               R0=L'COMMENT                                 
                                                                                
BUFPUT28 LR    RF,R0               ONLY UPDATE COMMENT IF NOT NULLS             
         CHI   RF,L'BZEROES                                                     
         BNH   *+8                                                              
         LHI   RF,L'BZEROES                                                     
         BCTR  RF,0                DECREMENT FOR EXECUTE INSTRUCTION            
         EX    RF,*+8                                                           
         BNE   BUFPUT30                                                         
         CLC   0(0,RE),BZEROES     TEST CHUNK IS BINARY ZEROES                  
         AHI   RF,1                                                             
         AR    RE,RF               POINT TO NEXT CHUNK                          
         SR    R0,RF               DECREMENT REMAINING LENGTH                   
         BNZ   BUFPUT28            DO NEXT IF REMAINING LENGTH                  
         B     BUFPUT32            NO COMMENT IN NEW RECORD                     
                                                                                
BUFPUT30 LR    R0,R6               MOVE COMMENT (OR DATA) TO RECORD             
         LR    RE,R9                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
BUFPUT32 TM    BUFFINDS,BUFFIDTA   IF TYPE=DATA WE ARE DONE                     
         BNZ   BUFPUTX                                                          
         AH    R9,BUFFLCOM         POINT TO 1ST ACCUM IN INPUT RECORD           
         AH    R6,BUFFLCOM         AND OUTPUT RECORD TOO                        
         SR    R0,R0                                                            
         ICM   R0,1,BUFFNCOL       R0=N'ACCUMULATOR COLUMNS                     
         BNZ   *+6                                                              
         DC    H'0'                COLUMNS SHOULD BE NON-ZERO                   
                                                                                
         TM    BUFFINDS,BUFFIBIN   TEST BINARY ACCUMULATORS                     
         BNZ   BUFPUT34                                                         
         TM    BUFFINDS,BUFFIPAK   TEST PACKED ACCUMULATORS                     
         BNZ   BUFPUT36                                                         
         DC    H'0'                TYPE=???????                                 
                                                                                
BUFPUT34 ICM   RE,15,0(R9)         BINARY ACCUMULATORS                          
         ICM   RF,15,0(R6)                                                      
         AR    RE,RF                                                            
         STCM  RE,15,0(R6)                                                      
         AHI   R9,4                                                             
         AHI   R6,4                                                             
         BCT   R0,BUFPUT34                                                      
         B     BUFPUTX                                                          
                                                                                
BUFPUT36 AP    0(8,R6),0(8,R9)     PACKED ACCUMULATORS                          
         AHI   R9,8                                                             
         AHI   R6,8                                                             
         BCT   R0,BUFPUT36                                                      
                                                                                
BUFPUTX  B     EXITCC                                                           
         EJECT                                                                  
***********************************************************************         
* READ HIGH FOR KEY/GET RECORD WITH SPECIFIC KEY                      *         
***********************************************************************         
                                                                                
BUFRDHN  NTR1  ,                   ENTRY POINT FOR NESTED CALLS                 
                                                                                
BUFRDH   XC    BUFFALST,BUFFALST   CLEAR A(LAST RECORD PASSED)                  
         OC    BUFFNREC,BUFFNREC   TEST ANY RECORDS ADDED TO BUFFER             
         BNZ   *+12                YES - DO KEY SEARCH                          
         OI    BUFFERRS,BUFFERNF+BUFFEEOF                                       
         B     BUFRDHX                                                          
                                                                                
         L     R6,ATRKNDX          R6=A(START OF TRACK INDEX)                   
         LH    RF,BUFFUTRK                                                      
         CH    RF,MAXLIN           TEST N'ENTRIES AGAINST LINEAR MAX            
         BH    BUFRDH04                                                         
                                                                                
BUFRDH02 LA    RE,TRKLEN+1(R8,R6)  DO LINEAR SEACH OF TRACK INDEX               
         EX    R8,*+8              TEST RECORD IN THIS TRACK                    
         BNH   BUFRDH10                                                         
         CLC   0(0,R9),0(RE)                                                    
         AR    R6,R7                                                            
         BCT   RF,BUFRDH02                                                      
         B     BUFRDH12                                                         
                                                                                
BUFRDH04 MR    RE,R7               DO BINARY SEARCH OF TRACK INDEX              
         LA    R1,0(R6,RF)                                                      
         BCTR  R1,0                R1=A(END OF TRACK INDEX-1)                   
         LR    R0,R7                                                            
         SR    R6,R7                                                            
         SR    R1,R6                                                            
         AR    R0,R0                                                            
         CR    R0,R1                                                            
         BNH   *-4                                                              
                                                                                
         AR    R1,R6                                                            
         BASR  RE,0                                                             
         SRL   R0,1                                                             
         CR    R0,R7                                                            
         BL    BUFRDH08                                                         
         BXH   R6,R0,BUFRDH06                                                   
                                                                                
         LA    RF,TRKLEN+1(R8,R6)                                               
         EX    R8,*+10                                                          
         BE    BUFRDH10                                                         
         BHR   RE                                                               
         CLC   0(0,R9),0(RF)                                                    
                                                                                
BUFRDH06 SR    R6,R0                                                            
         BR    RE                                                               
                                                                                
BUFRDH08 AR    R6,R7                                                            
                                                                                
BUFRDH10 ST    R6,ATRKNTRY                                                      
         LR    R1,R6               CALCULATE TRACK INDEX NUMBER                 
         S     R1,ATRKNDX                                                       
         SR    R0,R0                                                            
         DR    R0,R7                                                            
         AHI   R1,1                                                             
         CH    R1,BUFFUTRK         TEST > MAXIMUM N'TRACKS USED                 
         BNH   BUFRDH14            YES - SET EOF                                
                                                                                
BUFRDH12 CLI   BUFFACTN,BUFFAPUT   TEST PUTTING A RECORD                        
         BNE   BUFRDH30                                                         
         SR    R6,R7               YES - BACK UP TO PREVIOUS ENTRY              
         BCTR  R1,0                                                             
         ST    R6,ATRKNTRY         AND SET THIS AS TRACK ENTRY                  
                                                                                
BUFRDH14 STH   R1,BUFFINDN         SET INDEX NUMBER                             
         MVC   DATRK,TRKTRK-TRKD(R6)                                            
                                                                                
         GOTOR GETTRK,DATRK        GET THIS TRACK INTO A BUFFER                 
                                                                                
         L     R6,ABUFFER          R6=A(TRACK BUFFER)                           
         L     RF,ATRKNTRY                                                      
         SR    RE,RE                                                            
         ICM   RE,3,TRKREC-TRKD(RF)                                             
         CH    RE,MAXLIN           TEST N'RECORDS AGAINST LINEAR MAX            
         BH    BUFRDH20            HIGH - DO BINARY SEARCH                      
                                                                                
BUFRDH16 EX    R8,*+8              DO LINEAR SEARCH OF TRACK BUFFER             
         BNH   BUFRDH18                                                         
         CLC   0(0,R9),0(R6)                                                    
         AH    R6,BUFFLREC         POINT TO NEXT RECORD IN BUFFER               
         BCT   RE,BUFRDH16         DO FOR NUMBER OF RECORDS IN BLOCK            
         CLI   BUFFACTN,BUFFAPUT   TEST PUTTING A RECORD                        
         BNE   *+12                                                             
         OI    BUFFERRS,BUFFERNF   YES - CARRY ON                               
         B     BUFRDH26                                                         
         DC    H'0'                NO - SHOULD HAVE FOUND A HIGHER KEY          
                                                                                
BUFRDH18 BE    BUFRDH26            IF KEY MATCH DON'T SET NOT FOUND             
         CLI   BUFFACTN,BUFFARDH   TEST READ HIGH                               
         BE    BUFRDH26                                                         
         OI    BUFFERRS,BUFFERNF   YES - SET RECORD KEY NOT FOUND               
         B     BUFRDH26                                                         
                                                                                
BUFRDH20 MH    RE,BUFFLREC         DO BINARY SEARCH OF TRACK BUFFER             
         LA    R1,0(R6,RE)                                                      
         BCTR  R1,0                R1=A(END OF ACTIVE BUFFER-1)                 
         LH    R0,BUFFLREC         R0=L'RECORD                                  
         SR    R6,R0                                                            
         SR    R1,R6                                                            
         AR    R0,R0                                                            
         CR    R0,R1                                                            
         BNH   *-4                                                              
                                                                                
         AR    R1,R6                                                            
         BASR  RE,0                                                             
         SRL   R0,1                                                             
         CH    R0,BUFFLREC                                                      
         BL    BUFRDH24                                                         
         BXH   R6,R0,BUFRDH22                                                   
                                                                                
         EX    R8,*+10                                                          
         BE    BUFRDH26                                                         
         BHR   RE                                                               
         CLC   0(0,R9),0(R6)                                                    
                                                                                
BUFRDH22 SR    R6,R0                                                            
         BR    RE                                                               
                                                                                
BUFRDH24 AH    R6,BUFFLREC                                                      
         CLI   BUFFACTN,BUFFARDH   TEST GET FOR SPECIFIC KEY                    
         BE    BUFRDH26                                                         
         OI    BUFFERRS,BUFFERNF   YES - SET RECORD KEY NOT FOUND               
                                                                                
BUFRDH26 ST    R6,ARECORD          SAVE A(RECORD OR INSERTION POINT)            
         CLI   BUFFACTN,BUFFAPUT   TEST CALLED BY PUT ROUTINE                   
         BE    BUFRDH28                                                         
         LR    R0,R9               R0=A(OUTPUT RECORD)                          
         LH    R1,BUFFLREC         R1=L'RECORD                                  
         LR    RE,R6                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT AREA                   
                                                                                
         CLC   BUFFCTRK,BUFFUTRK   TEST LAST TRACK IN CORE BUFFER               
         BNE   BUFRDH28                                                         
         L     RF,ATRKNTRY                                                      
         SR    RE,RE                                                            
         ICM   RE,3,TRKREC-TRKD(RF)                                             
         MH    RE,BUFFLREC                                                      
         A     RE,ABUFFER                                                       
         CR    R6,RE               TEST JUST MOVED THIS RECORD                  
         BL    BUFRDH28                                                         
         OI    BUFFERRS,BUFFEEOF   YES - SET END OF FILE                        
         B     BUFRDHX                                                          
                                                                                
BUFRDH28 L     R1,ARECORD          CALCULATE RECORD NUMBER                      
         S     R1,ABUFFER                                                       
         SR    R0,R0                                                            
         LH    RF,BUFFLREC         RF=RECORD LENGTH                             
         DR    R0,RF                                                            
         AHI   R1,1                MAKE NUMBER BASE 1                           
         STH   R1,BUFFRECN         SET RECORD NUMBER                            
         B     BUFRDHX                                                          
                                                                                
BUFRDH30 OI    BUFFERRS,BUFFEEOF   SET END OF FILE                              
         CLI   BUFFACTN,BUFFARDH                                                
         BE    BUFRDHX                                                          
         OI    BUFFERRS,BUFFERNF   SET RECORD NOT FOUND (IF GET)                
                                                                                
BUFRDHX  B     EXITCC                                                           
         EJECT                                                                  
***********************************************************************         
* GET NEXT SEQUENTIAL RECORD IN THE FILE                              *         
***********************************************************************         
                                                                                
BUFSEQ   OC    BUFFALST,BUFFALST   TEST A(LAST RECORD) IS SET                   
         BNZ   *+12                                                             
         MVI   BUFFERRS,BUFFEEOF   NO - RETURN EOF                              
         B     BUFSEQX                                                          
                                                                                
         LH    RF,BUFFINDN                                                      
         LA    R1,1(RF)                                                         
         BCTR  RF,0                                                             
         MR    RE,R7                                                            
         A     RF,ATRKNDX          POINT TO CURRENT INDEX ENTRY                 
         SR    RE,RE                                                            
         ICM   RE,3,BUFFRECN                                                    
         AHI   RE,1                                                             
         CLM   RE,3,TRKREC-TRKD(RF)                                             
         BNH   BUFSEQ02                                                         
         CLC   BUFFINDN,BUFFUTRK                                                
         BNE   *+12                                                             
         OI    BUFFERRS,BUFFEEOF   YES - SET END OF FILE & EXIT                 
         B     BUFSEQX                                                          
         STCM  R1,3,BUFFINDN                                                    
         AR    RF,R7               POINT TO NEXT TRACK INDEX ENTRY              
         LHI   RE,1                SET FIRST RECORD                             
                                                                                
BUFSEQ02 MVC   DATRK,TRKTRK-TRKD(RF)                                            
         LR    R0,RE                                                            
         GOTOR GETTRK,DATRK                                                     
         LR    RE,R0                                                            
         STCM  RE,3,BUFFRECN       SET NEXT RECORD NUMBER                       
         SHI   RE,1                                                             
         MH    RE,BUFFLREC                                                      
         A     RE,ABUFFER                                                       
                                                                                
BUFSEQ04 LR    R0,R9               MOVE RECORD TO CALLER'S OUTPUT AREA          
         LH    R1,BUFFLREC                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
                                                                                
BUFSEQX  B     EXITCC                                                           
         EJECT                                                                  
***********************************************************************         
* CLOSE FILES/FREE ACQUIRED STORAGE                                   *         
***********************************************************************         
                                                                                
BUFCLO   SR    R1,R1                                                            
         IC    R1,BUFFNBUF                                                      
         M     R0,BUFFLBUF                                                      
         LR    R0,R1                                                            
         A     R0,BUFFLNDX         R0=L'ACQUIRED STORAGE                        
         L     R1,BUFFANDX         YES - FREE BUFFERS                           
         FREEMAIN RC,A=(1),LV=(0)                                               
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         GOTOR VDATAMGR,PARM,DMDADDS,DACLOSE,,,BUFFFILE                         
         NI    BUFFINDS,BUFFIBIN+BUFFIPAK+BUFFIDTA+BUFFIRCM                     
         XC    BUFFNTRK(BUFFFILE-BUFFNTRK),BUFFNTRK                             
                                                                                
BUFCLOX  B     EXITCC                                                           
         EJECT                                                                  
***********************************************************************         
* ALLOCATE A BUFFER FOR A TRACK AND READ IT IN OR INITIALIZE IT       *         
***********************************************************************         
                                                                                
GETTRK   NTR1  ,                                                                
         XC    BUFFCTRK,BUFFCTRK                                                
         LTR   R1,R1                                                            
         BZ    *+10                                                             
         MVC   BUFFCTRK,0(R1)                                                   
                                                                                
         L     R1,ANDXNDX                                                       
         USING NDXD,R1                                                          
         SR    R0,R0                                                            
         IC    R0,BUFFNBUF                                                      
         SR    RF,RF                                                            
GETTRK02 OC    NDXTRK,NDXTRK       TEST FREE ENTRY                              
         BZ    GETTRK06                                                         
         CLC   NDXTRK,BUFFCTRK     IS THIS THE REQUESTED TRACK                  
         BE    GETTRK06                                                         
         CLC   NDXTRK,HTRK         TEST WANT THIS TRACK HELD                    
         BE    GETTRK04                                                         
         LTR   RF,RF               TEST FIRST TIME                              
         BZ    *+14                YES - POINT TO IT                            
         CLC   NDXUCNT,NDXUCNT-NDXD(RF)                                         
         BNL   GETTRK04            ELSE SAVE ADDRESS OF ENTRY WITH              
         LR    RF,R1               EARLIEST USAGE                               
                                                                                
GETTRK04 AHI   R1,NDXLEN           BUMP TO NEXT INDEX ENTRY                     
         BCT   R0,GETTRK02         DO FOR N'INDEX ENTRIES                       
         LR    R1,RF               POINT TO LAST RECENTLY USED BUFFER           
                                                                                
GETTRK06 ST    R1,ANDXNTRY         SAVE A(BUFFER INDEX ENTRY)                   
         MVC   NDXUCNT,BUFFUCNT    SET INDEX USAGE COUNT                        
                                                                                
         LR    RF,R1                                                            
         S     RF,ANDXNDX                                                       
         LA    R0,NDXLEN                                                        
         SR    RE,RE                                                            
         DR    RE,R0                                                            
         M     RE,BUFFLBUF                                                      
         A     RF,BUFFABUF                                                      
         ST    RF,ABUFFER          SET A(TRACK BUFFER)                          
                                                                                
         OC    NDXTRK,NDXTRK       TEST FREE ENTRY                              
         BZ    GETTRK10                                                         
         CLC   NDXTRK,BUFFCTRK     IS THIS THE REQUESTED TRACK                  
         BE    GETTRKX                                                          
         TM    NDXINDS,NDXIBWP     TEST BUFFER WRITE PENDING FOR BLOCK          
         BZ    GETTRK10                                                         
                                                                                
         MVC   DATRK,NDXTRK        SET TRACK ADDRESS                            
         MVI   DABLK,1                                                          
         MVI   DAREC,0                                                          
         LA    RF,WTID                                                          
         NI    NDXINDS,FF-(NDXIBWP)                                             
         TM    NDXINDS,NDXITWP     TEST TRACK INITIALIZATION DONE               
         BNZ   GETTRK08                                                         
         OI    NDXINDS,NDXITWP                                                  
         XC    FILE.DNEXT,FILE.DNEXT                                            
         MVC   FILE.DNEXT(L'DATRK),DATRK                                        
         LA    RF,WTCKD                                                         
                                                                                
GETTRK08 L     R0,BUFFLBUF         R0=L'BUFFER                                  
         GOTOR VDATAMGR,PARM,(X'80',DMDADDS),(RF),ABUFFER,(R0),        *        
               BUFFFILE,DA,0                                                    
         OC    DADDSERR,DADDSERR   TEST FOR DADDS ERRORS                        
         BZ    *+6                                                              
         DC    H'0'                                                             
         GOTOR UPDIOC              UPDATE THE I/O COUNT                         
         GOTOR UPDTRK              ENSURE INDEX IS UPDATED                      
         L     R1,ANDXNTRY         POINT BACK TO INDEX ENTRY                    
                                                                                
GETTRK10 OC    BUFFCTRK,BUFFCTRK   TEST ALLOCATING NEW TRACK                    
         BZ    GETTRK12                                                         
         MVC   NDXTRK,BUFFCTRK     SET TRACK NUMBER IN INDEX                    
         MVI   NDXINDS,NDXITWP     SET BUFFER INITIALIZED                       
         MVC   DATRK,BUFFCTRK                                                   
         MVI   DABLK,1                                                          
         MVI   DAREC,0                                                          
         GOTOR VDATAMGR,PARM,(X'80',DMDADDS),RDID,ABUFFER,0,BUFFFILE,  *        
               DA,0                                                             
         OC    DADDSERR,DADDSERR   TEST FOR DADDS ERRORS                        
         BZ    *+6                                                              
         DC    H'0'                                                             
         GOTOR UPDIOC              UPDATE THE I/O COUNT                         
         B     GETTRKX                                                          
                                                                                
GETTRK12 MVI   NDXINDS,NDXIBWP     SET BUFFER WRITE PENDING                     
         MVC   NDXUCNT,BUFFUCNT    SET USAGE COUNT                              
         SR    RF,RF               INITIALIZE A NEW TRACK                       
         ICM   RF,3,BUFFUTRK                                                    
         AHI   RF,1                                                             
         CH    RF,BUFFNTRK                                                      
         BNH   *+6                                                              
         DC    H'0'                MAKE DISK FILE BIGGER                        
         STH   RF,BUFFUTRK                                                      
         STH   RF,BUFFCTRK                                                      
         STCM  RF,3,NDXTRK                                                      
         BCTR  RF,0                                                             
         MR    RE,R7                                                            
         A     RF,ATRKNDX                                                       
         ST    RF,ATRKNTRY                                                      
                                                                                
         LR    R0,RF                                                            
         LR    R1,R7                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEAR THE INDEX ENTRY                        
         L     RF,ATRKNTRY                                                      
         MVC   TRKTRK-TRKD(,RF),BUFFCTRK                                        
         MVC   ARECORD,ABUFFER     SET START OF BUFFER AS INSERTION             
                                                                                
         L     R0,ABUFFER          CLEAR THE BUFFER                             
         L     R1,BUFFLBUF                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
                                                                                
GETTRKX  XC    HTRK,HTRK           ALWAYS CLEAR HOLD TRACK ON EXIT              
         B     EXIT                                                             
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* SORT TRACK INDEX INTO ASCENDING SEQUENCE                            *         
***********************************************************************         
                                                                                
UPDTRK   NTR1  ,                                                                
         LH    RF,BUFFUTRK         RF=N'INDEX ENTRIES                           
         SHI   RF,1                                                             
         BZ    UPDTRKX                                                          
         LR    R0,RF                                                            
         MR    RE,R7                                                            
         A     RF,ATRKNDX          RF=A(LAST INDEX ENTRY)                       
                                                                                
UPDTRK02 LR    RE,RF                                                            
         SR    RE,R7               RE=A(PREVIOUS INDEX ENTRY)                   
         EX    R8,*+8              TEST PREVIOUS KEY AGAINST THIS               
         BH    UPDTRKX                                                          
         CLC   TRKLEN(0,RF),TRKLEN(RE)                                          
         XC    0(TRKLEN,RE),0(RF)  SWAP RECORDS/TRACK#                          
         XC    0(TRKLEN,RF),0(RE)                                               
         XC    0(TRKLEN,RE),0(RF)                                               
         AHI   RE,TRKLEN                                                        
         AHI   RF,TRKLEN                                                        
         EX    R8,XCFE             SWAP LOW KEYS IN TRACK                       
         EX    R8,XCEF                                                          
         EX    R8,XCFE                                                          
         AH    RE,BUFFLKEY                                                      
         AH    RF,BUFFLKEY                                                      
         EX    R8,XCFE             SWAP HIGH KEYS IN TRACK                      
         EX    R8,XCEF                                                          
         EX    R8,XCFE                                                          
         AH    RE,BUFFLKEY                                                      
         LR    RF,RE               BACK UP TO PREVIOUS ENTRY                    
         SR    RF,R7                                                            
         BCT   R0,UPDTRK02         DO FOR NUMBER OF INDEX ENTRIES               
                                                                                
UPDTRKX  B     EXIT                                                             
                                                                                
***********************************************************************         
* UPDATE THE I/O COUNTER                                              *         
***********************************************************************         
                                                                                
UPDIOC   CLC   BUFFNIOS,MAXIOS     TEST REACHED MAXIMUM VALUE                   
         BER   RE                                                               
         SR    RF,RF                                                            
         ICM   RF,3,BUFFNIOS       NO - BUMP AND STORE COUNT                    
         AHI   RF,1                                                             
         STCM  RF,3,BUFFNIOS                                                    
         BR    RE                                                               
         EJECT                                                                  
XCFE     XC    0(0,RF),0(RE)                                                    
XCEF     XC    0(0,RE),0(RF)                                                    
                                                                                
         DS    0F                                                               
XAON     DC    X'80000000'                                                      
MAXLIN   DC    H'10'               MAXIMUM N'ENTRIES FOR LINEAR SEARCH          
MAXIOS   DC    X'FFFF'             MAXIMUM I/O COUNT                            
DMDADDS  DC    C'DADDS '                                                        
                                                                                
         LTORG                                                                  
                                                                                
FF       EQU   X'FF'                                                            
MINNBUF  EQU   2                   MINIMUM N'BUFFERS TO ALLOCATE                
         EJECT                                                                  
WORKD    DSECT                     ** DSECT TO COVER WORKING STORAGE **         
DUB      DS    D                                                                
SAVERD   DS    A                   SAVED RD VALUE ON ENTRY                      
VDATAMGR DS    V                   V(DATAMGR)                                   
                                                                                
ANDXNDX  DS    A                   A(BUFFER INDEX)                              
ANDXNTRY DS    A                   A(BUFFER INDEX ENTRY)                        
SNDXNTRY DS    A                   SAVE A(BUFFER INDEX ENTRY)                   
ATRKNDX  DS    A                   A(TRACK INDEX)                               
ATRKNTRY DS    A                   A(TRACK INDEX ENTRY)                         
STRKNTRY DS    A                   SAVE A(TRACK INDEX ENTRY)                    
ABUFFER  DS    A                   A(CURRENT BUFFER)                            
SBUFFER  DS    A                   SAVE A(CURRENT BUFFER)                       
ARECORD  DS    A                   A(CURRENT RECORD/INSERTION POINT)            
                                                                                
STRK     DS    XL2                 SAVE TRACK NUMBER                            
HTRK     DS    XL2                 HOLD TRACK NUMBER                            
                                                                                
DA       DS    0XL(L'DNEXT)        DISK ADDRESS                                 
DATRK    DS    XL2                 TRACK NUMBER                                 
DABLK    DS    XL1                 BLOCK NUMBER                                 
DAREC    DS    XL1                 RECORD NUMBER                                
                                                                                
PARM     DS    0F                                                               
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
DADDSERR DS    0XL2                DADDS ERROR RETURN CODE                      
P4       DS    A                                                                
P5       DS    A                                                                
P6       DS    A                                                                
P7       DS    A                                                                
PARML    EQU   *-PARM                                                           
                                                                                
MODE     DS    X                   HOB OF RF ON ENTRY TO MODULE                 
                                                                                
BZEROES  DS    XL256                                                            
                                                                                
WORKL    EQU   *-WORKD                                                          
                                                                                
       ++INCLUDE DDBUFFD                                                        
                                                                                
NDXD     DSECT                     ** BUFFER INDEX **                           
NDXTRK   DS    XL2                 TRACK NUMBER (ZERO=FREE ENTRY)               
NDXINDS  DS    XL1                 INDICATORS                                   
NDXITWP  EQU   X'80'               TRACK INITIALIZATION DONE                    
NDXIBWP  EQU   X'40'               BUFFER WRITE PENDING                         
NDXUCNT  DS    XL3                 USAGE COUNT                                  
NDXLEN   EQU   *-NDXD                                                           
                                                                                
TRKD     DSECT                     ** TRACK INDEX **                            
TRKTRK   DS    XL2                 TRACK NUMBER                                 
TRKREC   DS    XL2                 N'RECORDS IN TRACK                           
TRKLEN   EQU   *-TRKD              LENGTH OF FIXED PORTION OF ENTRY             
TRKKEYS  DS    0X                  LOW/HIGH KEYS IN BLOCK                       
                                                                                
*DMDTFPH                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
                                                                                
*DMGREQUS                                                                       
       ++INCLUDE DMGREQUS                                                       
                                                                                
*DDCOMFACSD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDBUFFERIN10/15/04'                                      
         END                                                                    
