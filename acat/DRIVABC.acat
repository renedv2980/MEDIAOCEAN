*          DATA SET DRIVABC    AT LEVEL 006 AS OF 05/01/02                      
*CATALP DRIVABC                                                                 
         TITLE 'DRIVABC - SPECIAL DRIVER CONTROLLER FOR ABC'                    
DRIVABC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,**DRVR**                                                      
         USING ABCD,RC                                                          
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
         L     R7,GLABCBPL         A(BUILDER PARAMETER LIST)                    
         USING BUPARMD,R7                                                       
         GOTO1 =V(DRIVINIT),DMCB,(RA)                                           
         MVC   AACTREC,GLAIO                                                    
         L     R1,GLAIO                                                         
         MVI   0(R1),1                                                          
         EJECT                                                                  
*              ROUTINE TO CONTROL HANDLING OF OUTLINE TABLE                     
         SPACE 3                                                                
         L     R6,BUPAOUTT         R6=A(OUTLINE TABLE)                          
         B     TABEND              ALWAYS SKIP PLAN HEADER                      
         USING OUTTABD,R6                                                       
         SPACE 1                                                                
TAB2     ST    R6,GLABCATO                                                      
         MVI   GLABCNCC,1          INITIALIZE NEXT TEXT COLUMN                  
         CLI   OUTLVLN,1           AUTO SPACE BEFORE LEVEL 1                    
         BNE   TAB3                                                             
         TM    OUTIND1,X'40'       INDICATOR NOT TO PRINT                       
         BO    TAB3                                                             
         MVI   SPACING,1                                                        
         BAS   RE,SPLAT                                                         
         SPACE 1                                                                
TAB3     TM    OUTIND2,OUTITOT     IS OUTLINE A PARENT                          
         BNO   TAB4                                                             
         MVI   GLABCMOD,1          THEN PRINT THE HEADER ONLY                   
         BAS   RE,PRINTOUT                                                      
         B     TABEND                                                           
         SPACE 1                                                                
TAB4     BAS   RE,CHECKSUP         THIS IS A DETAIL                             
         BNE   TAB5                                                             
         MVI   GLABCMOD,2                                                       
         BAS   RE,PRINTOUT                                                      
         SPACE 1                                                                
TAB5     LA    R5,OUTTABL(R6)                                                   
         DROP  R6                                                               
         SPACE 1                                                                
         USING OUTTABD,R5                                                       
TAB6     MVC   NEXTLEV,OUTLVLN     FIND NEXT REAL ENTRY'S LEVEL                 
         OC    OUTSEQN,OUTSEQN                                                  
         BNZ   TAB8                                                             
         MVI   NEXTLEV,0           NEXT ENTRY IS PAST THE END                   
         B     TAB10                                                            
         SPACE 1                                                                
TAB8     TM    OUTIND1,X'40'       DON'T WANT DELETED ENTRY                     
         BNO   TAB10                                                            
         LA    R5,OUTTABL(R5)                                                   
         B     TAB6                                                             
         DROP  R5                                                               
         SPACE 1                                                                
         USING OUTTABD,R6                                                       
TAB10    CLC   OUTLVLN,NEXTLEV     IS NEXT AT A 'HIGHER' LEVEL WHICH            
         BNH   TABEND              HAS A LOWER LEVEL NUMBER                     
         MVC   SAVATO,GLABCATO     SAVE PRESENT OUTLINE TABLE ADDRESS           
         MVI   GLABCMOD,3          AND SET UP FOR SOME TOTALLING                
         SPACE 1                                                                
TAB12    MVC   DUB(2),OUTSEQP      PICK UP SEQUENCE NUMBER OF PARENT            
         LH    R6,DUB                                                           
         BCTR  R6,0                                                             
         LA    R1,OUTTABL                                                       
         STH   R1,DUB                                                           
         MH    R6,DUB              X WIDTH OF TABLE                             
         A     R6,BUPAOUTT         ADDRESS PARENT'S ENTRY                       
         ST    R6,GLABCATO                                                      
         BAS   RE,CHECKSUP                                                      
         BNE   TAB14                                                            
         BAS   RE,PRINTOUT         AND PRINT TOTALS AT THAT LEVEL               
         SPACE 1                                                                
TAB14    CLC   OUTLVLN,NEXTLEV     HAVE WE GOT BACK TO LEVEL                    
         BH    TAB12                                                            
         L     R6,SAVATO           YES SO RESTORE PREVIOUS TABLE ENTRY          
         SPACE 1                                                                
TABEND   LA    R6,OUTTABL(R6)                                                   
         OC    OUTSEQN,OUTSEQN     END OF TABLE                                 
         BNZ   TAB2                                                             
         MVI   SPACING,X'FF'       PASS END REPORT TO DRIVOUT                   
         BAS   RE,SPLAT                                                         
*                                                                               
         TM    GLABCTXT,X'40'      TEST FOR FOOTNOTES                           
         BZ    TABEND2             NO                                           
         MVI   GLHOOK,GLRESOLV     SET ROUTINE ADDRESS FOR                      
         MVC   GLLABEL,=CL8'ENDTEXT' END OF REPORT TEXT                         
         GOTO1 GLASYSDR,DMCB,(RA)                                               
*                                                                               
         MVI   GLHOOK,GLROUT                                                    
         MVC   GLAIFLD,GLAP1       SET DUMMY ADDRESS FOR                        
         MVC   GLAOFLD,GLAP1       INPUT AND OUTPUT FIELDS                      
         GOTO1 GLASYSDR,DMCB,(RA)                                               
*                                                                               
TABEND2  TM    GLABCTXT,X'30'      TEST FOR FOOTNOTE OPTION                     
         BZ    TABENDX             NO-ALL DONE                                  
         MVI   GLHOOK,GLRESOLV                                                  
         MVC   GLLABEL,=CL8'FOOTNUM'                                            
         TM    GLABCTXT,X'20'      TEST FOR REFERENCE NUMBERS                   
         BO    *+10                YES                                          
         MVC   GLLABEL,=CL8'FOOTLET'  NO-MUST BE LETTERS                        
         TM    GLABCTXT,X'04'      TEST PRINTING W/O BOXES                      
         BZ    *+8                                                              
         MVI   GLLABEL+7,C'A'      MAKE ROUTINE FOOTNUMA/FOOTLETA               
         GOTO1 GLASYSDR,DMCB,(RA)                                               
*                                                                               
         MVI   GLHOOK,GLROUT                                                    
         MVC   GLAIFLD,GLAP1                                                    
         MVC   GLAOFLD,GLAP1                                                    
         GOTO1 GLASYSDR,DMCB,(RA)                                               
*                                                                               
TABENDX  B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO CONTROL PRINTING OF OUTLINE                           
*                                                                               
*              AT ENTRY, GLABCMOD = 1 FOR PARENT OUTLINE HEADER                 
*                                   2 FOR DETAIL LINE                           
*                                   3 SUB-TOTAL                                 
         SPACE 3                                                                
PRINTOUT NTR1                                                                   
         L     R6,GLABCATO         R6=A(OUTLINE TABLE ENTRY)                    
         USING OUTTABD,R6                                                       
         TM    OUTIND1,X'40'       DON'T HANDLE DELETED ENTRIES                 
         BO    XIT                                                              
         CLI   GLABCMOD,3          ARE WE DOING TOTALS?                         
         BNE   PO1A                                                             
         TM    OUTIND1,X'10'       OPTION TO SUPPRESS TOTALS                    
         BO    XIT                                                              
         MVI   SPACING,1                                                        
         CLI   OUTLVLN,0           AUTO SPACE BEFORE PLAN TOTALS                
         BE    PO1B                                                             
         SPACE 1                                                                
PO1A     CLI   OUTBEF,0            OPTION TO SPACE BEFORE                       
         BE    PO2                                                              
         MVC   SPACING,OUTBEF                                                   
         SPACE 1                                                                
PO1B     BAS   RE,SPLAT                                                         
         SPACE 1                                                                
PO2      GOTO1 =V(DRIVIN),DMCB,(RA)      FORMAT INTERNAL RECORD                 
         SPACE 1                                                                
         MVC   SAVOPT,GLABCOPT     SAVE ABC OPTIONS                             
         CLI   GLABCMOD,1          TEST FOR PARENT OUTLINE                      
         BNE   *+8                                                              
         NI    GLABCOPT,X'FF'-GLABCSUP TURN OFF SUPPRESS ZERO LINE              
         GOTO1 =V(DRIVOUT),DMCB,(RA)     AND PRINT IT OUT                       
         MVC   GLABCOPT,SAVOPT     RESTORE OPTION BYTE                          
         SPACE 1                                                                
         CLI   OUTAFT,0            OPTION TO SPACE AFTER                        
         BE    PO4                                                              
         MVC   SPACING,OUTAFT                                                   
         BAS   RE,SPLAT                                                         
         B     XIT                                                              
         SPACE 1                                                                
PO4      TM    OUTIND1,X'20'       OPTION TO SKIP AFTER                         
         BNO   XIT                                                              
         L     R1,GLAFHEAD         TURN ON FORCEHED                             
         MVI   0(R1),C'Y'                                                       
         B     XIT                                                              
         SPACE 1                                                                
SPLAT    NTR1                                                                   
         TM    GLDOWNLD,X'80'      DONT BOTHER IF WE'RE                         
         BO    XIT                 DOWNLOADING                                  
         MVC   SPLATSW,SPACING     USE DRIVOUT FOR SPACING                      
         CLI   SPLATSW,0                                                        
         BNE   *+8                                                              
         MVI   SPLATSW,1                                                        
         GOTO1 =V(DRIVOUT),DMCB,(RA)                                            
         B     XIT                                                              
         EJECT                                                                  
*              CHECK FOR SUPPRESSION OF UNNECESSARY LINES                       
         SPACE 3                                                                
CHECKSUP NTR1                                                                   
         L     R6,GLABCATO                                                      
         USING OUTTABD,R6                                                       
         ZIC   R2,OUTLVLN          PICK UP THIS LEVEL NUMBER                    
         SLL   R2,1                                                             
         LA    R2,LEVCOUNT(R2)                                                  
         LH    R1,0(R2)            ADD 1 TO COUNT AT THIS LEVEL                 
         LA    R1,1(R1)                                                         
         STH   R1,0(R2)                                                         
         LA    R2,2(R2)            NOW HAVE A LOOK AT NEXT LEVEL                
         LH    R1,0(R2)                                                         
         XC    0(2,R2),0(R2)       (CLEAR)                                      
         CH    R1,=H'1'                                                         
         BE    NO                  REJECT IF EXACTLY 1                          
         B     YES                                                              
         SPACE 1                                                                
LEVCOUNT DC    32X'00'                                                          
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
NO       LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              LTORG AND MODULE STORAGE                                         
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 1                                                                
ABCD     DSECT                                                                  
SAVATO   DS    A                                                                
NEXTLEV  DS    XL1                                                              
SPACING  DS    XL1                                                              
SAVOPT   DS    XL1                                                              
         EJECT                                                                  
       ++INCLUDE BUILDERD                                                       
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DRIVABC   05/01/02'                                      
         END                                                                    
