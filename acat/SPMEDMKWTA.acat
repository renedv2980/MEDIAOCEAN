*          DATA SET SPMEDMKWTA AT LEVEL 028 AS OF 05/01/02                      
*CATALP MEDMKTWT                                                                
         TITLE 'ADJUSTS EXTRACTED RATINGS WITH MARKET WEIGHTS'                  
         PRINT NOGEN                                                            
VMDMKTWT CSECT                                                                  
         NMOD1 10,**MKTWT*                                                      
         USING WTD,RC                                                           
         L     RA,0(R1)            A(WORKC)                                     
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD,RA,R9                                                    
         L     R8,MEDBUFF                                                       
         USING MEDBLOCK,R8                                                      
         MVC   MWEXTCS,MEDEXTCS                                                 
         OC    MEDADDWT,MEDADDWT   ADDWEIGHT PRESENT                            
         BZ    MWGW                                                             
         GOTO1 MEDADDWT,DMCB,(RA)   YES - GO TO IT                              
         MVC   4(4,R1),SPWEIGHT                                                 
MWGW     DS    0H                                                               
         CLI   MEDEXTDM,0                                                       
         BE    XIT                                                              
         L     R7,4(R1)            WEIGHTING FACTOR                             
         CLI   SPOTPROF+1,C'N'     SUPPRESS WEIGHTING                           
         BE    XIT                  YES                                         
         MVC   NODEM,MEDEXTDM                                                   
         EJECT                                                                  
*              ESTABLISH LIST OF R'S FOR BRAND                                  
         SPACE 3                                                                
         CH    R7,=H'1000'                                                      
         BE    XIT                                                              
         CH    R7,=H'2'                                                         
         BL    XIT                                                              
         SR    R6,R6               LOOK UP BRAND INFO                           
         IC    R6,MEDBRAND                                                      
         CLI   MEDBRAND,X'FF'                                                   
         BNE   *+8                                                              
         LA    R6,220                                                           
         BCTR  R6,0                                                             
         MH    R6,PRDBUFLN                                                      
         A     R6,PRDBUFF                                                       
         LA    R6,28(R6)           BUMP TO DEMO NUMBER LIST                     
         XC    LIST,LIST                                                        
         LA    R2,LIST                                                          
         ZIC   R4,MEDEXTDM                                                      
         SPACE 2                                                                
MW2      OC    0(3,R6),0(R6)                                                    
         BZ    MW10                                                             
         CLI   1(R6),C'E'          INCLUDE EXTENDED DEMOS AS RTGS               
         BE    *+8                                                              
         CLI   1(R6),C'R'                                                       
         BNE   *+8                                                              
         MVI   0(R2),C'R'                                                       
         CLI   1(R6),C'A'          CHECK FOR WGTD OR USER DEMO                  
         BNL   *+8                                                              
         BAS   RE,GETDNAM          GET DEMO NAME FOR WGTD OR USER               
         LA    R2,1(R2)                                                         
         LA    R6,3(R6)                                                         
         BCT   R4,MW2                                                           
         EJECT                                                                  
*              NOW GO DOWN EXTRACT TABLE TO SEE IF ANY                          
*              VALUES NEED TO BE ADJUSTED BY FACTOR                             
         SPACE 3                                                                
MW10     LA    R2,MEDDATES         CHECK EACH BLOCK FOR WEIGHTING               
         LA    R3,75+14+6+4                                                     
         LA    R4,MEDDATA-MEDBLOCK                                              
         SPACE 2                                                                
MW10A    OC    0(4,R2),0(R2)                                                    
         BZ    MW10B                                                            
         L     R8,4(R2)                                                         
         LTR   R8,R8                                                            
         BZ    MW10B                                                            
         SR    R8,R4               ADJUST BASE REGISTER FOR BLOCK               
         BAS   RE,MW11                                                          
         SPACE 2                                                                
MW10B    LA    R2,12(R2)                                                        
         BCT   R3,MW10A                                                         
         B     XIT                                                              
         SPACE 2                                                                
MW11     NTR1                                                                   
         OC    MEDGL1(12),MEDGL1   ANY GOALS                                    
         BZ    MW11LK               NO - CHECK LOCKINS                          
         LA    R2,LIST             GOALS                                        
         LA    R3,MEDGL1                                                        
         BAS   RE,ADJ                                                           
         LA    R2,LIST+1                                                        
         LA    R3,MEDGL2                                                        
         BAS   RE,ADJ                                                           
MW11LK   CLI   MWEXTCS,C'Y'        BYPASS WEIGHTING FOR CHILD SPOT              
         BE    MW11A                                                            
         OC    MEDLKSPT,MEDLKSPT   ANY LOCKINS                                  
         BZ    MW11A               NO - CHECK BUYS                              
         LA    R2,LIST             LOCK-INS                                     
         LA    R3,MEDLK1                                                        
         BAS   RE,ADJ                                                           
         LA    R2,LIST+1                                                        
         LA    R3,MEDLK2                                                        
         BAS   RE,ADJ                                                           
MW11A    OC    MEDBYD(12),MEDBYD   ANY SPOTS OR DOLLARS                         
         BZ    XIT                                                              
         LA    R2,LIST                                                          
         LA    R3,MEDBYP                                                        
         ZIC   R4,NODEM                                                         
         BAS   RE,ADJ                                                           
         LA    R3,8(R3)                                                         
         SPACE 2                                                                
MW12     BAS   RE,ADJ                                                           
         LA    R2,1(R2)                                                         
         LA    R3,8(R3)                                                         
         BCT   R4,MW12                                                          
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO ADJUST                                                
         SPACE 3                                                                
ADJ      CLI   SPOTPROF+1,C'D'     WEIGHT ALL DEMOS                             
         BE    *+10                 YES                                         
         CLI   0(R2),C'R'          IS IT A RATING                               
         BCR   7,RE                                                             
         OC    0(8,R3),0(R3)       ANY VALUES                                   
         BCR   8,RE                                                             
         L     R1,0(R3)            YES - WEIGHT DEMO                            
         MR    R0,R7                                                            
         SLDA  R0,1                                                             
         AH    R1,=H'1'                                                         
         SRA   R1,R1                                                            
         ST    R1,0(R3)                                                         
         L     R1,4(R3)            AND WEIGHT EQUIVALENCE                       
         MR    R0,R7                                                            
         SLDA  R0,1                                                             
         AH    R1,=H'1'                                                         
         SRA   R1,R1                                                            
         ST    R1,4(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
*FIND A USER OR WEIGHTED DEMO NAME IN EST HDR AND SET DEMO TYPE CODE            
GETDNAM  NTR1                                                                   
         L     R3,ADEST                                                         
         USING ESTHDR,R3                                                        
         ZIC   RE,2(R6)            GET DEMO NUMBER                              
         CLI   1(R6),63            TEST WGHTD DEMO                              
         BNE   *+8                                                              
         LA    RE,5                                                             
         BCTR  RE,0                                                             
         MH    RE,=H'7'                                                         
         LA    RE,EUSRNMS(RE)      SET TO NAME                                  
         CLI   0(RE),C'E'          WEIGHT EXTENDED OR REGULAR RATINGS           
         BE    *+8                                                              
         CLI   0(RE),C'R'                                                       
         BNE   *+8                                                              
         MVI   0(R2),C'R'                                                       
         XIT1                                                                   
         EJECT                                                                  
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
WTD      DSECT                                                                  
LIST     DS    CL20                                                             
NODEM    DS    C                   NUMBER OF DEMOS                              
MWEXTCS  DS    C                   EXTRACT CHILD SPOT                           
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE SPGENEST                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028SPMEDMKWTA05/01/02'                                      
         END                                                                    
