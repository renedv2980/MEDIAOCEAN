*          DATA SET SPMEDPRMY  AT LEVEL 001 AS OF 01/06/83                      
*CATALP SPMEDPRY                                                                
*                                                                               
*                                                                               
* PARAMETER 1  BYTE 0              I=INITIALIZE                                 
*                                  P=PRIMARY DEMO NUMBER RETURNED               
*                                  D=DEMO CODE (3 BYTE) RETURNED                
*                                                                               
*              BYTES 1-3           A(SPWORKC)                                   
*                                                                               
* PARAMETER 2  BYTE 0              BRAND CODE OR                                
*                                  RELATIVE DEMO NUMBER                         
*                                                                               
*              BYTES 1-3           A(AREA FOR 3 BYTE DEMO CODE)                 
*                                                                               
* AFTER MODULE                                                                  
* PARAMETER 2  BYTE 0              RETURNS PRIMARY DEMO - FUNCTION 'P'          
*                                                                               
         TITLE 'MODULE TO CONVERT BETWEEN PRIMARY DEMO CODE + NUMBER'           
VMEDPRMY CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 PRIWX-PRIWD,**PRMY,CLEAR=YES                                     
         USING PRIWD,RC                                                         
         MVC   PARAMS(8),0(R1)                                                  
         ST    R1,PLIST                                                         
         L     RA,0(R1)            A(SPWORKC)                                   
         USING SPWORKD,RA                                                       
         LA    R9,2048(RA)                                                      
         LA    R9,2048(R9)                                                      
         USING SPWORKD+4096,R9                                                  
         SPACE                                                                  
         CLI   FUNCTION,C'P'                                                    
         BE    PRIMARY                                                          
         CLI   FUNCTION,C'D'                                                    
         BE    DEMCOD                                                           
         CLI   FUNCTION,C'I'                                                    
         BE    INIT                                                             
         DC    H'0'                INVALID FUNCTION                             
         SPACE 2                                                                
INIT     DS    0H                  INITIALIZATION ROUTINE                       
         XC    PRITAB,PRITAB       ZERO OUT TABLE OF DEMO CODES/                
         XC    PRICOUNT,PRICOUNT   PRIMARY NUMBERS                              
         L     R2,PRDBUFF                                                       
         USING PTBUFFD,R2                                                       
         LA    R3,PRODUCTS         COUNTER OF MAX ENTRIES IN PRDBUFF            
         MVC   HALF,PRDBUFLN       LENGTH OF ENTRY IN PRODUCT BUFFER            
         LH    R4,HALF                                                          
         SPACE                                                                  
INIT2    DS    0H                  INSERT UNIQUE PRIMARY DEMO IN TABLE          
         LA    R1,PRIDEMS          COUNTER-MAX ENTRIES IN TABLE                 
         LA    RE,PRITAB                                                        
         OC    PTDEMO(3),PTDEMO    SKIP OVER ZERO PRIMARY DEMO CODE             
         BZ    INIT6                                                            
         SPACE                                                                  
INIT3    OC    0(L'PRIENT,RE),0(RE)     INSERT CODE IN TABLE WHEN               
         BZ    INIT4                    FIRST ZERO ENTRY IS REACHED             
         CLC   PTDEMO(3),0(RE)                                                  
         BE    INIT6               DEMO CODE IS IN TABLE                        
         LA    RE,L'PRIENT(RE)                                                  
         BCT   R1,INIT3                                                         
         DC    H'0'                TABLE IS FILLED-BLOW TO STOP OVRFLOW         
         SPACE                                                                  
INIT4    DS    0H                                                               
         MVC   0(3,RE),PTDEMO      ADD DEMO CODE TO TABLE                       
         L     RE,PRICOUNT         INCREMENT ENTRY COUNT                        
         LA    RE,1(RE)                                                         
         ST    RE,PRICOUNT                                                      
         B     INIT6                                                            
         SPACE                                                                  
INIT6    DS    0H                  CHECK NEXT ENTRY IN PRODUCT BUFFER           
         AR    R2,R4               POINT TO NEXT ENTRY                          
         BCT   R3,INIT2                                                         
         SPACE                                                                  
INIT8    DS    0H                  SORT THEN NUMBER ENTRIES IN                  
         L     R3,PRICOUNT         PRIMARY DEMO TABLE                           
         LTR   R3,R3                                                            
         BNZ   *+6                                                              
         DC    H'0'                BLOW UP IF TABLE HAS NO ENTRIES              
         GOTO1 XSORT,MYLIST,PRITAB,(R3),L'PRIENT,3,0                            
         SPACE                                                                  
         LA    R1,1                START COUNT AT 1                             
         LA    RE,PRITAB                                                        
         STC   R1,3(RE)            RELATIVE NUMBER                              
         LA    R1,1(R1)                                                         
         LA    RE,L'PRIENT(RE)     NEXT ENTRY                                   
         BCT   R3,*-12                                                          
         B     EXIT                                                             
         EJECT                                                                  
* FOR A BRAND CODE, FIND ITS PRIMARY DEMO NUMBER BY FIRST INDEXING              
* IN PRODUCT BUFFER FOR 3-BYTE CODE, THEN USING TABLE TO GET PRIMARY            
* NUMBER.                                                                       
*                                                                               
* ASSUME R1 POINTS TO USER PARAMETER LIST                                       
*                                                                               
PRIMARY  DS    0H                                                               
         L     R2,PRDBUFF                                                       
         ZIC   R4,4(R1)            BRAND NUMBER                                 
         BCTR  R4,0                                                             
         MVC   HALF,PRDBUFLN       X ENTRY LENGTH                               
         MH    R4,HALF                                                          
         AR    R2,R4               POINT TO ENTRY                               
         MVI   4(R1),0             SET RESULT TO ZERO                           
         OC    PTDEMO(3),PTDEMO    EXIT FOR ZERO CODE FIELD                     
         BZ    EXIT                                                             
         SPACE                                                                  
         LA    R3,PRITAB                                                        
         ICM   R4,15,PRICOUNT                                                   
         BNZ   *+6                                                              
         DC    H'0'                DUMP-NO INITIALIZATION DONE                  
         SPACE                                                                  
         CLC   PTDEMO(3),0(R3)                                                  
         BE    PRIMARY2            FOUND DEMO CODE IN TABLE                     
         LA    R3,L'PRIENT(R3)                                                  
         BCT   R4,*-14                                                          
         B     EXIT                EXIT IF NOT IN TABLE                         
         SPACE                                                                  
PRIMARY2 DS    0H                                                               
         MVC   4(1,R1),3(R3)       PRIMARY DEMO NUMBER                          
         B     EXIT                                                             
         SPACE 2                                                                
* RETURN 3-BYTE DEMO CODE FOR A PRIMARY DEMO NUMBER                             
*                                                                               
DEMCOD   DS    0H                                                               
         MVC   BYTE,4(R1)          PRIMARY DEMO NUMBER                          
         LA    R3,PRITAB                                                        
         ICM   R4,15,PRICOUNT                                                   
         BNZ   *+6                                                              
         DC    H'0'                DUMP FOR NO INITIALIZATION                   
         SPACE                                                                  
         CLC   BYTE,3(R3)                                                       
         BE    DEMCOD2             FOUND TABLE ENTRY                            
         LA    R3,L'PRIENT(R3)                                                  
         BCT   R4,*-14                                                          
         DC    H'0'                BLOW UP-PRIMARY CODE NOT IN TABLE            
         SPACE                                                                  
DEMCOD2  DS    0H                                                               
         L     RE,4(R1)            A(OUTPUT AREA)                               
         MVC   0(3,RE),0(R3)       CODE TO OUTPUT                               
         B     EXIT                                                             
         SPACE 2                                                                
EXIT     XMOD1 1                                                                
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
PRICOUNT DC    F'0'                                                             
PRITAB   DS    0CL100              25 X 4 TABLE                                 
PRIENT   DS    25CL4               BYTES 0-2=3 BYTE DEMO CODE                   
*                                  BYTE  3  =PRIMARY (RELATIVE) DEMO            
PRIDEMS  EQU   L'PRITAB/L'PRIENT   MAXIMUM ENTRIES IN PRITAB                    
PRODUCTS EQU   220                 LIMIT OF PRODUCT BUFFER ENTRIES              
         SPACE 2                                                                
* DSECT TO COVER WORKING STORAGE                                                
*                                                                               
PRIWD    DSECT                                                                  
PLIST    DS    V                   A(USER PARAMETER LIST)                       
PARAMS   DS    6F                  USER PARAMS                                  
         ORG   PARAMS                                                           
FUNCTION DS    C                                                                
         ORG                                                                    
MYLIST   DS    6F                  MODULE PARAMETER LIST                        
PRIWX    EQU   *                                                                
         SPACE 2                                                                
       ++INCLUDE SPREPPTBUF                                                     
         SPACE 2                                                                
* SPONSOR WORKING STORAGE DSECT                                                 
         PRINT OFF                                                              
       ++INCLUDE SPREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001SPMEDPRMY 01/06/83'                                      
         END                                                                    
