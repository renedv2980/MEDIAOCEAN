*          DATA SET UNLDBALT   AT LEVEL 106 AS OF 05/01/02                      
*CATALP UNLDBALC                                                                
         TITLE 'LDBALCK - UNIT - FILE BALANCE CHECK/PRINT'                      
* INPUTS PARAMETER 1 - BYTE 1- X'01' CALCULATE                                  
*                              X'FF' PRINT                                      
*                      BYTES 2-4 - A(RECORD)                                    
*        PARAMETER 2 - A(FILE NAME)                                             
* GLOBALS: R6 - POINTS TO RECORD                                                
*          RA - POINTS TO PRINT                                                 
*          R9 - POINTS TO PRINT LINE                                            
*********************************************************************           
         PRINT NOGEN                                                            
LDBALCK  CSECT                                                                  
         ENTRY UTL                                                              
         NMOD1 0,UNLDBALT                                                       
         LA    R8,4095(RB)                                                      
         LA    R8,1(R8)                                                         
         USING LDBALCK,RB,R8       NOTE 2ND BASE REG = R8                       
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
                                                                                
*                                                                               
         CLI   0(R1),X'FF'         IF EOF                                       
         BNE   CALC                                                             
         BAS   RE,EOFPRINT                                                      
         B     EXIT                                                             
*                                                                               
CALC     L     R6,0(R1)            R6 POINTS TO RECORD                          
         ST    R6,AREC                                                          
         USING NURECD,R6                                                        
         CLI   0(R6),X'04'         CHECK IF UNIT REC                            
         BNE   CALCERR                                                          
         TM    NURSTAT,X'80'       CHECK IF DELETED                             
         BNZ   CALCERR                                                          
         TM    NUPACKST,X'01'      CHECK IF NO-SHOW                             
         BO    CALCERR                                                          
         CLI   NUKSUB,X'C1'        IS IT TRAFFIC REC                            
         BNL   CALCERR                                                          
*                                                                               
CALC02   CLI   SVAM,0              TEST FIRST TIME                              
         BE    CALC03                                                           
         CP    LINE,=PL2'59'                                                    
         BL    CALC05                                                           
         MVC   LINE,=PL2'75'                                                    
         BAS   RE,PRTHEAD                                                       
         B     CALC05                                                           
*                            FIRST TIME                                         
CALC03   DS    0H                                                               
         L     R3,4(R1)            GET A(FILENAME)                              
         MVC   FILNAM,0(R3)                                                     
         MVC   SVAM,NUKAM          SET AGY,CLT                                  
         MVC   SVAGY,NUALPHA       SET ALPHA AGY                                
         MVC   SVCLT,NUKCLT        SET NEW CLIENT                               
         MVC   SVEST,NUKEST        SET NEW EST                                  
         BAS   RE,PRTHEAD                                                       
         GOTO1 =V(DATCON),DMCB,(5,0),(2,TODAY)                                  
*        L     R1,LDDDTFDA                                                      
*        CLI   28(R1),C'2'         TEST UNTFIL2                                 
*        BNE   *+8                 (HARDCODED FOR UNTFIL1)                      
*        MVI   UTL+4,X'25'                                                      
*        GOTO1 ADMGR,DMCB,=C'DMOPEN',=C'SPOT',FLIST,AIO                         
*        BAS   RE,GETCLI           GET CLIHDR,SAVE CLIST                        
*        LTR   R1,R1                                                            
*        BZ    CALC03A                                                          
*        MVC   P(6),=C'CLIENT'                                                  
*        B     PUTITOUT                                                         
CALC03A  DS    0H                                                               
*        BAS   RE,CHKEST                                                        
*        LTR   R1,R1                                                            
*        BZ    CALC05                                                           
*        MVC   P(8),=C'ESTIMATE'                                                
*        B     PUTITOUT                                                         
         B     CALC05                                                           
*                                                                               
FLIST    DC    CL8'NSPTFILE'                                                    
         DC    CL8' SPTDIR '                                                    
         DC    CL8'X       '                                                    
         SPACE                                                                  
UTL      DC    4X'00'                                                           
         DC    X'23'               HARCODE FOR NET1                             
         EJECT                                                                  
*                                                                               
CALC05   CLC   NUKAM,SVAM          CHECK SAME AGENCY                            
         BE    CALC10                                                           
         BAS   RE,CLTTOT                                                        
         BAS   RE,AGYTOT                                                        
         MVC   SVAM,NUKAM          SET NEW AGY                                  
         MVC   SVAGY,NUALPHA       SET NEW AGY ALPHA                            
         MVC   SVCLT,NUKCLT        SET NEW CLIENT                               
*        BAS   RE,GETCLI           GET CLIHDR,CLIST                             
*                                                                               
CALC10   CLC   NUKCLT,SVCLT        CHECK SAME CLIENT                            
         BE    CALC15                                                           
         BAS   RE,CLTTOT                                                        
         MVC   SVCLT,NUKCLT        SET NEW CLIENT                               
*        BAS   RE,GETCLI           TEST CLIENT                                  
*        LTR   R1,R1                                                            
*        BZ    *+14                                                             
*        MVC   P(6),=C'CLIENT'                                                  
*        B     PUTITOUT                                                         
*                                                                               
CALC15   DS    0H                                                               
         MVC   SVEST,NUKEST        SET NEW EST                                  
*        BAS   RE,CHKEST           TEST ESTIMATE                                
*        LTR   R1,R1                                                            
*        BZ    *+14                                                             
*        MVC   P(8),=C'ESTIMATE'                                                
*        B     PUTITOUT                                                         
*                                                                               
CALC15A1 MVI   ELCODE,X'01'        MAIN ELEMENT                                 
         USING NUMAINEL,R6                                                      
         L     R6,AREC                                                          
         BAS   RE,GETEL                                                         
         BNE   CALC16                                                           
         TM    NUUNITST,X'42'      TEST PREEMPT,MISSED                          
         BNZ   CALC16                                                           
         TM    NUUNITST,X'80'      TEST IF MINUSSED                             
         BZ    CALC15B                                                          
         MVC   FULL,NUACTUAL       IF MINUSSED/MAKE SURE ITS MINUS              
         L     R1,FULL                                                          
         LNR   R1,R1                                                            
         ST    R1,FULL                                                          
         MVC   NUACTUAL,FULL                                                    
         MVC   FULL,NUINTEG                                                     
         L     R1,FULL                                                          
         LNR   R1,R1                                                            
         ST    R1,FULL                                                          
         MVC   NUINTEG,FULL                                                     
*                                                                               
CALC15B  MVC   FULL,NUACTUAL                                                    
         L     R1,FULL                                                          
         MVC   FULL,NUINTEG                                                     
         A     R1,FULL                                                          
         LR    R3,R1                                                            
         BAS   RE,SPECIALS         ARE THERE SPECIAL RATES                      
         A     R3,FULL             ADD TO ORDERED TOTAL                         
         CVD   R3,DUB                                                           
         AP    CLTORDER,DUB                                                     
*                                                                               
CALC16   MVI   ELCODE,X'10'        BILLING ELEMENT                              
         USING NUBILD,R6                                                        
         L     R6,AREC                                                          
         BAS   RE,GETEL                                                         
         BNE   CALC17                                                           
*                                                                               
CALC16A  TM    NUBILST,X'20'       TEST UNBILLED                                
         BO    CALC16B                                                          
         L     R1,NUBILGRS                                                      
         CVD   R1,DUB                                                           
         AP    CLTBGRS,DUB                                                      
         L     R1,NUBILNET                                                      
         CVD   R1,DUB                                                           
         AP    CLTBNET,DUB                                                      
         CLC   NUBILDAT,TODAY      TODAY'S BILLING?                             
         BNE   CALC16B                                                          
         L     R1,NUBILGRS                                                      
         CVD   R1,DUB                                                           
         AP    TODBGRS,DUB         TODAY BILL GROSS                             
         L     R1,NUBILNET                                                      
         CVD   R1,DUB                                                           
         AP    TODBNET,DUB         TODAY BILL NET                               
CALC16B  BAS   RE,NEXTEL                                                        
         BE    CALC16A                                                          
*                                                                               
CALC17   MVI   ELCODE,X'12'        PAYING ELEMENT                               
         USING NUPAYD,R6                                                        
         L     R6,AREC                                                          
         BAS   RE,GETEL                                                         
         BNE   EXIT                                                             
CALC17A  L     R1,NUPAYGRS                                                      
         CVD   R1,DUB                                                           
         AP    CLTPGRS,DUB                                                      
         L     R1,NUPAYNET                                                      
         CVD   R1,DUB                                                           
         AP    CLTPNET,DUB                                                      
         BAS   RE,NEXTEL                                                        
         BE    CALC17A                                                          
         B     EXIT                                                             
*                                                                               
CALCERR  DS    0H                                                               
*                                                                               
EXIT     XMOD1                                                                  
*                                                                               
XIT      XIT1                                                                   
         SPACE                                                                  
*                                                                               
SPECIALS NTR1                                                                   
*  GETS SPECIAL RATES AND RETUNRS THEM IN FULL                                  
         XC    FULL,FULL                                                        
         SR    R2,R2                                                            
         L     R6,AREC                                                          
         MVI   ELCODE,X'03'        GET SPECIAL RATES ELEMENT                    
         BAS   RE,GETEL                                                         
         USING NUSPRD,R6                                                        
         BNE   SPX                                                              
SP5      ICM   R3,15,NUSPRAMT                                                   
         AR    R2,R3                                                            
         BAS   RE,NEXTEL                                                        
         BE    SP5                                                              
         L     R4,AREC                                                          
         ST    R2,FULL                                                          
SPX      XIT1                                                                   
***************************************************************8                
         EJECT                                                                  
**********************************************                                  
******** TESTING FOR CLIENT HEADER/SAVE CLIST                                   
**********************************************                                  
GETCLI   NTR1                                                                   
         XC    KEY,KEY                                                          
         MVC   KEY+1(1),SVAM                                                    
         MVC   KEY+2(2),SVCLT      READ CLIENT                                  
         BAS   RE,READIT                                                        
         LTR   R1,R1                                                            
         BNZ   GETCLIX                                                          
         BAS   RE,GETIT                                                         
         LTR   R1,R1                                                            
         BNZ   GETCLIX                                                          
         LA    R2,AIO                                                           
         USING CLTHDR,R2                                                        
         MOVE  (SVCLIST,880),CLIST                                              
         SR    R1,R1               BECAUSE MOVE USES R1                         
         DROP  R2                                                               
GETCLIX  XIT1  REGS=(R1)                                                        
         SPACE 2                                                                
******************************************                                      
* IS ESTIMATE VALID                                                             
*****************************************                                       
CHKEST   NTR1                                                                   
         L     R6,AREC                                                          
         USING NURECD,R6                                                        
         MVI   ELCODE,X'01'                                                     
         BAS   RE,GETEL                                                         
         BE    CHKE2                                                            
         LA    R1,1                                                             
         B     CHKX                                                             
         USING NUMAINEL,R6                                                      
CHKE2    CLI   NUPRD,0             ZERO=UNALLOCATED                             
         BNE   CHKE3                                                            
         SR    R1,R1               SET IT TO OK                                 
         B     CHKX                                                             
CHKE3    XC    KEY,KEY                                                          
         MVC   KEY+1(1),SVAM                                                    
         MVC   KEY+2(2),SVCLT                                                   
         MVC   BPRD,NUPRD                                                       
         L     R6,AREC             RESET R6                                     
         BAS   RE,GETPRD                                                        
         LTR   R1,R1                                                            
         BNZ   CHKX                                                             
         MVC   KEY+4(3),QPRD                                                    
         MVC   KEY+7(1),SVEST                                                   
         BAS   RE,READIT                                                        
         LTR   R1,R1                                                            
         BZ    CHKX                                                             
         LA    R1,1                                                             
CHKX     XIT1  REGS=(R1)                                                        
               SPACE                                                            
******************************************                                      
*  GET PROD CODE                                                                
*****************************************8                                      
GETPRD   NTR1                                                                   
         LA    R2,AIO                                                           
         USING CLTHDR,R2                                                        
         LA    R2,SVCLIST                                                       
GP5      CLI   3(R2),0             IF E-O-F CLIST                               
         BNE   GP7                                                              
         MVC   P(10),=C'PROD UNDEF'                                             
         GOTO1 VPRINT                                                           
         B     GPX                 SET TO UNDEFINED                             
GP7      CLC   3(1,R2),BPRD                                                     
         BE    GP10                                                             
         LA    R2,4(R2)            INCREMENT CLIST                              
         B     GP5                 RETURN TO LOOP                               
GP10     MVC   QPRD,0(R2)      SET 3 CHAR PRINTABLE PRD CODE                    
         SR    R1,R1                                                            
         B     *+8                                                              
GPX      LA    R1,1                                                             
         XIT1  REGS=(R1)                                                        
*                                                                               
PUTITOUT DS    0H                                                               
         GOTO1 VPRINT                                                           
         L     R6,AREC                                                          
         GOTO1 AHEXOUT,DMCB,(R6),P,100                                          
         GOTO1 VPRINT                                                           
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
EOFPRINT NTR1                                                                   
         BAS   RE,CLTTOT                                                        
         BAS   RE,AGYTOT                                                        
         LA    R9,P                                                             
         USING PLINED,R9                                                        
         GOTO1 VPRINT              SKIP LINE                                    
         MVC   PSTART(17),=C'ALL AGENCY TOTALS'                                 
         LA    R2,ALLBGRS          R2 POINTS TO $ SAVE AREA                     
         BAS   RE,EDLINE                                                        
         GOTO1 VPRINT                                                           
                                                                                
         LA    R2,ALTBGRS                                                       
         LA    R4,PBGRS                                                         
         BAS   RE,EDITIT                                                        
         LA    R2,ALTBNET                                                       
         LA    R4,PBNET                                                         
         BAS   RE,EDITIT                                                        
         CLC   ALTBGRS,=PL8'0'                                                  
         BNE   ALTPRT                                                           
         CLC   ALTBNET,=PL8'0'                                                  
         BE    ALTPRT2                                                          
ALTPRT   MVC   PORDER(7),=C'(TODAY)'                                            
         GOTO1 VPRINT                                                           
                                                                                
                                                                                
ALTPRT2  MVC   LINE,=PL2'75'       FORCE PGBREAK FOR NEXT MODULE                
         B     EXIT                                                             
*                                                                               
CLTTOT   NTR1                                                                   
         LA    R9,P                                                             
         USING PLINED,R9                                                        
         MVC   PAM,SVAGY                                                        
         GOTO1 =V(CLUNPK),DMCB,SVCLT,PCLT                                       
         LA    R2,CLTBGRS                                                       
         BAS   RE,EDLINE                                                        
         GOTO1 VPRINT                                                           
                                                                                
***************************************************************                 
* SKIP PRINTING TODAY BILLED CLIENT TOTALS                                      
*                                                                               
         B     TODPRT2             PATCH OUT IF CLIENT TOTS WANTED              
                                                                                
         LA    R2,TODBGRS          PUT TODAY'S BILLED ON NEXT LINE              
         LA    R4,PBGRS                                                         
         BAS   RE,EDITIT                                                        
         LA    R2,TODBNET                                                       
         LA    R4,PBNET                                                         
         BAS   RE,EDITIT                                                        
                                                                                
         CLC   TODBGRS,=PL8'0'                                                  
         BNE   TODPRT                                                           
         CLC   TODBNET,=PL8'0'                                                  
         BE    TODPRT2                                                          
                                                                                
TODPRT   MVC   PORDER(7),=C'(TODAY)'                                            
         GOTO1 VPRINT                                                           
*******************************************************************             
                                                                                
TODPRT2  AP    AGYBGRS,CLTBGRS               ADD TO AGY TOTALS                  
         AP    AGYBNET,CLTBNET                                                  
         AP    AGTBGRS,TODBGRS               TODAY BILLED GROSS                 
         AP    AGTBNET,TODBNET               TODAY BILLED NET                   
         AP    AGYPGRS,CLTPGRS                                                  
         AP    AGYPNET,CLTPNET                                                  
         AP    AGYORDER,CLTORDER                                                
*        GOTO1 =V(PRNTBL),DMCB,=C'ALLTOT',32(R2),C'DUMP',20,=C'1D'              
         ZAP   CLTBGRS,=P'0'                 CLEAR CLIENT TOTALS                
         ZAP   CLTBNET,=P'0'                                                    
         ZAP   CLTPGRS,=P'0'                                                    
         ZAP   CLTPNET,=P'0'                                                    
         ZAP   CLTORDER,=P'0'                                                   
         ZAP   TODBGRS,=P'0'                 CLEAR CLIENT TOTALS                
         ZAP   TODBNET,=P'0'                                                    
         B     EXIT                                                             
         DROP  R9                                                               
         EJECT                                                                  
*                                                                               
AGYTOT   NTR1                                                                   
         LA    R9,P                                                             
         USING PLINED,R9                                                        
         MVC   PSTART(9),=C'AGY TOTAL'                                          
         MVC   PAM,HALF                                                         
         LA    R2,AGYBGRS                                                       
         BAS   RE,EDLINE                                                        
         GOTO1 VPRINT                                                           
                                                                                
         LA    R2,AGTBGRS          PUT TODAY'S BILLED ON NEXT LINE              
         LA    R4,PBGRS                                                         
         BAS   RE,EDITIT                                                        
         LA    R2,AGTBNET                                                       
         LA    R4,PBNET                                                         
         BAS   RE,EDITIT                                                        
         CLC   AGTBGRS,=PL8'0'                                                  
         BNE   AGTPRT                                                           
         CLC   AGTBNET,=PL8'0'                                                  
         BE    AGTPRT2                                                          
                                                                                
AGTPRT   MVC   PORDER(7),=C'(TODAY)'                                            
         GOTO1 VPRINT                                                           
                                                                                
AGTPRT2  XC    P,P                                                              
         GOTO1 VPRINT              SKIP LINE AFTER AGY TOTS                     
         AP    ALLBGRS,AGYBGRS               ADD TO ACROSS AGY TOTALS           
         AP    ALLBNET,AGYBNET                                                  
         AP    ALTBGRS,AGTBGRS               TODAY BILLED GROSS                 
         AP    ALTBNET,AGTBNET               TODAY BILLED NET                   
         AP    ALLPGRS,AGYPGRS                                                  
         AP    ALLPNET,AGYPNET                                                  
         AP    ALLORDER,AGYORDER                                                
         ZAP   AGYBGRS,=P'0'                 CLEAR AGENCY TOTALS                
         ZAP   AGYBNET,=P'0'                                                    
         ZAP   AGYPGRS,=P'0'                                                    
         ZAP   AGYPNET,=P'0'                                                    
         ZAP   AGYORDER,=P'0'                                                   
         ZAP   AGTBGRS,=P'0'                 CLEAR AGENCY TODAY TOTAL           
         ZAP   AGTBNET,=P'0'                                                    
         B     EXIT                                                             
*                                                                               
         USING PLINED,R9                                                        
EDLINE   NTR1                 COMMON ROUTINE FOR EDIT                           
         LA    R4,PBGRS       R2 PASSED = $ AREA/R9 = PRINT LINE AREA           
         BAS   RE,EDITIT                                                        
         LA    R2,8(R2)                                                         
         LA    R4,PBNET                                                         
         BAS   RE,EDITIT                                                        
         LA    R2,8(R2)                                                         
         LA    R4,PPGRS                                                         
         BAS   RE,EDITIT                                                        
         LA    R2,8(R2)                                                         
         LA    R4,PPNET                                                         
         BAS   RE,EDITIT                                                        
         LA    R2,8(R2)                                                         
         LA    R4,PORDER                                                        
         BAS   RE,EDITIT                                                        
         XIT1                                                                   
         SPACE                                                                  
EDITIT   NTR1                                                                   
         UNPK  WORK(15),0(8,R2)                                                 
         MVC   BYTE,WORK+14        SAVE SIGN                                    
         OI    WORK+14,X'F0'                                                    
         MVC   15(2,R4),WORK+13    MOVE IN DECIMALS                             
         MVI   14(R4),C'.'         AND DECIMAL POINT                            
         MVC   1(13,R4),WORK                                                    
         LA    R1,12               DROP LEADING ZEROS                           
         LR    RE,R4               SAVE START OF PRINT AREA                     
         LA    R4,1(R4)            POINT TO START OF NUMBER                     
EDI10    CLI   0(R4),X'F0'                                                      
         BNE   EDI12                                                            
         MVI   0(R4),X'40'                                                      
         LA    R4,1(R4)                                                         
         BCT   R1,EDI10                                                         
EDI12    TM    BYTE,X'D0'          IS IT NEGATIVE                               
         BNO   EDIX                                                             
         CLI   1(RE),X'40'         YES/SET NEG SIGN BEFORE 1ST                  
         BH    *+12                    SIGNIFICANT DIGIT                        
         LA    RE,1(RE)                                                         
         B     *-12                                                             
         MVI   0(RE),C'-'                                                       
EDIX     XIT1                                                                   
         EJECT                                                                  
***************************************                                         
* PRINT HEADLINE FOR COLUMNS                                                    
*                                                                               
**************************************                                          
         SPACE                                                                  
PRTHEAD  NTR1                                                                   
         LA    R9,P                                                             
         USING PLINED,R9                                                        
         MVC   PAM(3),=C'AGY'                                                   
         MVC   PCLT,=C'CLT'                                                     
         MVC   PBGRS+5(12),=C'BILLED      '                                     
         MVC   PBNET+5(10),=C'BILLED NET'                                       
         MVC   PPGRS+5(10),=C'CLEARED   '                                       
         MVC   PPNET+5(8),=C'PAID NET'                                          
         MVC   PORDER+5(7),=C'ORDERED'                                          
         GOTO1 VPRINT                                                           
         B     EXIT                                                             
         DROP  R9                                                               
         EJECT                                                                  
*                                                                               
GETEL    LA    R6,27(R6)                                                        
GT5      CLC   ELCODE,0(R6)                                                     
         BE    GOTEL                                                            
NEXTEL   ZIC   R1,1(R6)                                                         
         LTR   R1,R1                                                            
         BZ    NOEL                                                             
         AR    R6,R1                                                            
         B     GT5                                                              
NOEL     LTR   RE,RE                                                            
GOTEL    BR    RE                                                               
         SPACE 2                                                                
*                                                                               
*  DATAMGR INTERFACE                                                            
READIT   NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 ADMGR,DMCB,=CL8'DMRDHI',=C'SPTDIR  ',KEY,KEY,0                   
         CLC   KEY(13),KEYSAVE                                                  
         BE    READOK                                                           
         LA    R1,1                                                             
         B     *+6                                                              
READOK   SR    R1,R1                                                            
         XIT1  REGS=(R1)                                                        
*                                                                               
GETIT    NTR1                                                                   
         LA    R2,KEY+14                                                        
         LA    R3,AIO                                                           
         MVC   FILE,=C'SPTFILE '                                                
         GOTO1 ADMGR,DMCB,(X'00',=C'GETREC'),FILE,(R2),(R3),DMWORK              
         CLI   8(R1),0                                                          
         BE    GETOK                                                            
         LA    R1,1                                                             
         B     *+6                                                              
GETOK    SR    R1,R1                                                            
         XIT1  REGS=(R1)                                                        
         EJECT                                                                  
*                                                                               
FULL     DS    F                                                                
AREC     DS    F                                                                
*                                                                               
CLTBGRS  DC    PL8'0'                                                           
CLTBNET  DC    PL8'0'                                                           
CLTPGRS  DC    PL8'0'                                                           
CLTPNET  DC    PL8'0'                                                           
CLTORDER DC    PL8'0'                                                           
TODBGRS  DC    PL8'0'              TODAY BILLED                                 
TODBNET  DC    PL8'0'                                                           
*                                                                               
AGYBGRS  DC    PL8'0'                                                           
AGYBNET  DC    PL8'0'                                                           
AGYPGRS  DC    PL8'0'                                                           
AGYPNET  DC    PL8'0'                                                           
AGYORDER DC    PL8'0'                                                           
AGTBGRS  DC    PL8'0'              TODAY BILLED                                 
AGTBNET  DC    PL8'0'                                                           
*                                                                               
ALLBGRS  DC    PL8'0'                                                           
ALLBNET  DC    PL8'0'                                                           
ALLPGRS  DC    PL8'0'                                                           
ALLPNET  DC    PL8'0'                                                           
ALLORDER DC    PL8'0'                                                           
ALTBGRS  DC    PL8'0'              TODAY BILLED                                 
ALTBNET  DC    PL8'0'                                                           
*                                                                               
WORK     DS    CL40                                                             
FILE     DS    CL8                                                              
BPRD     DS    CL1                 BINARY PRD                                   
QPRD     DS    CL3                 PRINTABLE PRD                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
DMWORK   DS    12D                                                              
HALF     DS    H                                                                
ELCODE   DS    CL1                                                              
SVAM     DS    CL1                                                              
SVAGY    DS    CL2                                                              
SVCLT    DS    CL2                                                              
SVEST    DS    CL1                                                              
KEY      DS    CL30                                                             
KEYSAVE  DS    CL30                                                             
FILNAM   DS    CL7                                                              
BYTE     DS    CL1                                                              
TODAY    DS    CL2                 TODAY'S DATE                                 
*                                                                               
VPRINT   DC    V(PRINTER)                                                       
AHEXOUT  DC    V(HEXOUT)                                                        
ADMGR    DC    V(DATAMGR)                                                       
VLDDEFN  DC    V(LDDEFN)           FILE DEFINITION                              
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
AIO      DS    CL1200                                                           
SVCLIST  DS    CL880                                                            
*                                                                               
         EJECT                                                                  
*                                                                               
PLINED   DSECT                                                                  
PSTART   DS    CL1                                                              
         DS    CL9                                                              
PAM      DS    CL2                                                              
         DS    CL2                                                              
PCLT     DS    CL3                                                              
         DS    CL2                                                              
PBGRS    DS    CL17                                                             
         DS    CL2                                                              
PBNET    DS    CL17                                                             
         DS    CL2                                                              
PPGRS    DS    CL17                                                             
         DS    CL2                                                              
PPNET    DS    CL17                                                             
         DS    CL2                                                              
PORDER   DS    CL17                                                             
         SPACE 2                                                                
*                                                                               
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE DMLDDEFN                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'106UNLDBALT  05/01/02'                                      
         END                                                                    
