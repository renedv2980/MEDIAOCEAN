*          DATA SET DDSIXUNPK  AT LEVEL 018 AS OF 10/29/92                      
*CATALP SIXUNPK                                                                 
         TITLE 'ROUTINE TO DECOMPRESS 6-BIT TEXT TO EBCDIC'                     
**********************************************************************          
* PARAMETERS:                                                        *          
* -----------                                                        *          
*      1       A(COMPRESSED DATA)                                    *          
*                                                                    *          
*      2       A(OUTPUT AREA)                                        *          
*                                                                    *          
*      3       L'INPUT    (S.B. MULTIPLE OF 3)                       *          
*                                                                    *          
**********************************************************************          
SIXUNPK  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,*SIXUNPK                                                       
         LM    R2,R4,0(R1)                                                      
*                                                                               
         SR    R7,R7                 CLEAR COUNTER                              
         SR    R6,R6                 CLEAR INNER LOOP COUNTER                   
         LR    R5,R3                 R5=OUTPUT FIELD                            
*                                                                               
OUTLP    SR    RE,RE                 RE=HOLDS 3 BYTES OF INPUT                  
         ICM   RE,7,0(R2)            GET 3 LOW BYTES->4 CHARS                   
*                                                                               
LP       SRDL  RE,6                  SHIFT LOWER 6 BITS INTO RF                 
         SRL   RF,26                 RT JUSTIFY BITS                            
         LA    R1,X'C0'              TURN ON 2 HOBS (TOSSED BY SIXPACK)         
         OR    RF,RF                 IS THIS A SPACE CHARACTER?                 
         BNZ   *+8                    NO                                        
         LA    R1,X'40'              NO HOB FOR SPACE                           
         OR    RF,R1                                                            
*                                                                               
         STC   RF,3(R5)              STORE EBCIDIC CHAR IN OUTPUT               
         LA    R6,1(R6)              BUMP LOOP COUNTER                          
         BCTR  R5,0                  BACK UP 1 LOCATION                         
         CH    R6,=H'4'              HAVE WE OUTPUT 4 CHARS?                    
         BL    LP                    NOT YET                                    
         LA    R7,3(R7)              BUMP OUTER LOOP COUNTER                    
         SR    R6,R6                 RESET INNER LOOP COUNTER                   
         LA    R5,8(R5)              BUMP OUTPUT POINTER                        
         CR    R4,R7                 HAVE WE PRINTED ALL CHARS?                 
         BNH   XIT                   YES                                        
         LA    R2,3(R2)              BUMP INTPUT POINTER                        
         B     OUTLP                 LOOP BACK TO TOP                           
XIT      XIT1                                                                   
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018DDSIXUNPK 10/29/92'                                      
         END                                                                    
