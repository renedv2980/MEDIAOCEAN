*          DATA SET PPBVAL     AT LEVEL 008 AS OF 04/01/13                      
*CATALP PPBVAL                                                                  
         TITLE 'PPBVAL - PRINT BILL VALUES'                                     
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   NOTE- SEE PPBVALD FOR PARAMETER LIST AND DESCRIPTION OF                     
*         PROGRAM FUNCTION                                                      
*                                                                               
***********************************************************************         
         SPACE 2                                                                
*                                                                               
*   CHANGE LOG                                                                  
*                                                                               
*    BPLA   04/13   HST FOR PEI                                                 
*                                                                               
*    BPLA   06/10   HST NOW FOR BRITISH COLUMBIA AND ONTARIO                    
*                                                                               
*    SMYE   12/00   RENAME PNBVAL TO PPBVAL AND PNBILREC TO PBILLREC            
*                   AND PNBVALD TO PPBVALD                                      
*                                                                               
*    BPLA    7/00   COPY OF PPBVAL FOR NEW PBILLREC (PNBILREC)                  
*                   USES A COPY OF PPBVALD (PNBVALD)                            
*                                                                               
*    WAS PPBVALA - RENAMED 2/24/98 (NEW PPVALD - SEPARATE HST FIELD)            
*                                                                               
PPBVAL   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,PPBVAL                                                    
*                                                                               
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         L     R8,0(R1)            A(INPUT)                                     
         L     RA,4(R1)            A(OUTPUT BLOCK)                              
         USING PPBVALD,RA                                                       
         XC    PPBVALD(PPBVALDL),PPBVALD                                        
         ZAP   PPBVEBG,=P'0'                                                    
         ZAP   PPBVEBB,=P'0'                                                    
         ZAP   PPBVEBN,=P'0'                                                    
         ZAP   PPBVEBC,=P'0'                                                    
*                                                                               
         ZAP   PPBVBG,=P'0'                                                     
         ZAP   PPBVBB,=P'0'                                                     
         ZAP   PPBVBN,=P'0'                                                     
         ZAP   PPBVBC,=P'0'                                                     
*                                                                               
         ZAP   PPBVBACT,=P'0'                                                   
*                                                                               
         CLI   0(R1),C'B'          IS INPUT A BILL HEADER                       
         BE    PRBILL                                                           
         CLI   0(R1),C'E'          OR A BILLING ELEMENT                         
         BE    PRELEM                                                           
         DC    H'0'                                                             
         SPACE 1                                                                
***********************************************************************         
*   PRBILL - PROCESS BILL HEADER                                                
***********************************************************************         
         SPACE 1                                                                
PRBILL   DS    0H                                                               
         USING PBILLREC,R8                                                      
*                                                                               
         ZAP   PPBVBACT,PBILLRCV   ACTUAL (RECEIVABLE)                          
*                                                                               
         LA    R2,PBILLREC+33                                                   
         MVI   ELCODE,X'0A'        NOW GET GST FROM PBILVEL                     
         BAS   RE,NEXTEL                                                        
         BNE   PRB2                                                             
         USING PBILVEL,R2                                                       
*                                                                               
         MVC   PPBVGST,PBILLVAT                                                 
         DROP  R2                                                               
*                                                                               
PRB2     DS    0H                                                               
         LA    R2,PBILLREC+33                                                   
         MVI   ELCODE,X'84'        NOW GET PST FROM PBLPSTEL                    
PRB2C    BAS   RE,NEXTEL                                                        
         BNE   PRB5                                                             
         USING PBLPSTEL,R2                                                      
*                                                                               
         L     R0,PPBVPST          TOTAL ALL PSTS                               
         A     R0,PBLPVAMT                                                      
         ST    R0,PPBVPST                                                       
         CLI   PBLPVPRV,1          HST PROVINCE ?                               
         BE    PRB2F               YES                                          
         CLI   PBLPVPRV,5          HST PROVINCE ?                               
         BE    PRB2F               YES                                          
         CLI   PBLPVPRV,7          HST PROVINCE ?                               
         BE    PRB2F               YES                                          
         CLI   PBLPVPRV,8          HST PROVINCE ?                               
         BE    PRB2F               YES                                          
         CLI   PBLPVPRV,9          HST PROVINCE ?                               
         BE    PRB2F               YES                                          
         CLI   PBLPVPRV,10         HST PROVINCE ?                               
         BNE   PRB2C               NO                                           
*                                                                               
PRB2F    L     R0,PPBVHST          TOTAL ALL HSTS                               
         A     R0,PBLPVAMT                                                      
         ST    R0,PPBVHST                                                       
         B     PRB2C                                                            
         DROP  R2                                                               
*                                                                               
PRB5     DS    0H                                                               
         TM    PBILCMSW,BSTTAORQ   FOR TRUE AOR                                 
         BNZ   PRB80               DONE                                         
*                                                                               
*        SET "TRUE" VALUES                                                      
*                                                                               
         ZAP   PPBVBG,PBILLGRS     GROSS                                        
         ZAP   PPBVBB,PBILLBIL     GROSS-CD                                     
         ZAP   PPBVBN,PBILLNET     NET/NET                                      
         ZAP   PPBVBC,PBILLGRS     GROSS                                        
         SP    PPBVBC,PBILLBIL     MINUS (GROSS-CD)  = CD                       
*                                                                               
         LA    R2,PBILLREC+33                                                   
         MVI   ELCODE,X'09'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   PRB10                                                            
         USING PBILOTH,R2                                                       
         CLI   PBILOLEN,X'20'             LENGHT MUST BE GREATER THAN           
         BL    PRB10                      OR EQUAL TO 32 FOR TAX TO BE          
         MVC   PPBVTAX,PBILLTAX           PRESENT                               
         DROP  R2                                                               
*                                                                               
PRB10    TM    PBILCMSW,BSTSCOMQ+BSTSNETQ                                       
         BZ    PRB30                                                            
*                                                                               
         TM    PBILCMSW,BSTSCOMQ                                                
         BZ    PRB20                                                            
*                         *** UFC COMMISSION BILL**                             
         ZAP   PPBVEBG,PPBVBG      GROSS - NET = EFFECTIVE GRS                  
         SP    PPBVEBG,PPBVBN                                                   
         SP    PPBVEBG,PPBVBC      SINCE PPBVBN IS NET-CD                       
         ZAP   PPBVEBB,PPBVEBG    EFF GROSS-CD = EFF GROSS                      
*                                                                               
         B     PRB80               EFF NET, CD, AND TAX = 0                     
*                                                                               
PRB20    DS    0H         *** UFC NET BILLS**                                   
         ZAP   PPBVEBG,PPBVBN      EFF GROSS = TRUE NET                         
         AP    PPBVEBG,PPBVBC      RE-ADD CD                                    
         ZAP   PPBVEBB,PPBVBN      EFF GROSS-CD = TRUE NET-CD                   
         ZAP   PPBVEBN,PPBVBN      EFF NET-CD IS TRUE NET-CD                    
         MVC   PPBVETAX,PPBVTAX    EFF TAX = TRUE TAX                           
         MVC   PPBVEBC,PPBVBC      EFF CD = TRUE CD                             
         B     PRB80                                                            
*                                                                               
PRB30    DS    0H                  NORMAL BILLS                                 
         MVC   PPBVEBG(24),PPBVBG    EFF = TRUE FOR EVERYTHING                  
         MVC   PPBVETAX,PPBVTAX            TAX                                  
*                                                                               
PRB80    DS    0H                                                               
PRBILLX  DS    0H                                                               
         B     EXIT                                                             
         SPACE 1                                                                
***********************************************************************         
*   PRELEM - PROCESS PBUYREC BILLING ELEM                                       
***********************************************************************         
         SPACE 1                                                                
PRELEM   DS    0H                                                               
         USING PBILELEM,R8                                                      
*                                                                               
         MVC   PPBVEEG(12),PBGROSS                                              
*                                                                               
         TM    PBBILST,X'01'       UFC COMMISSION BILLING                       
         BZ    PRE05                                                            
         MVC   PPBVEEG,PBAGYCOM   SET GROSS TO AC                               
         XC    PPBVEEC,PPBVEEC     AND CLEAR CD                                 
         B     PRE25                                                            
*                                                                               
PRE05    TM    PBBILST,X'02'      UFC NET BILLING                               
         BZ    PRE25                                                            
         L     R0,PPBVEEG         SET GROSS TO NET                              
         S     R0,PPBVEEA                                                       
         ST    R0,PPBVEEG                                                       
         XC    PPBVEEA,PPBVEEA    AND CLEAR AC                                  
*                                                                               
PRE25    DS    0H                                                               
         L     R0,PPBVEEG                                                       
         S     R0,PPBVEEA                                                       
         ST    R0,PPBVEEN         SET NET                                       
*                                                                               
*                                                                               
PRE80    DS    0H                                                               
         L     R1,SAVR1            SEE IF HAVE BILL FORMULA                     
         SR    R2,R2                                                            
         ICM   R2,7,9(R1)                                                       
         BZ    PRE90               NO, DON'T CALCULATE ACTUAL                   
****************************                                                    
*********       ACTUAL NOT CODED YET                                            
****************************                                                    
*                                                                               
*        TM    STAT,STABSSNQ       IF NET BILL                                  
*        BZ    PRE82                                                            
*        MVC   PPBVACT,PPBVNET     ACTUAL = NET                                 
*        B     PRE90                                                            
*                                                                               
*RE82    DS    0H                                                               
*        MVC   PPBVACT,PPBVEGRS    ACTUAL = EFFECTIVE GROSS                     
*        OC    0(5,R2),0(R2)       IF NO FORMULA                                
*        BZ    PRE90                                                            
*                                                                               
*        ICM   R1,15,1(R2)         ADJUSTMENT %                                 
*        BZ    PRE84               NONE, JUST USE BILL BASIS                    
*        L     RF,PPBVGRS          ADJ BASIS IS GROSS                           
*        TM    0(R2),X'01'                                                      
*        BZ    *+8                                                              
*        L     RF,PPBVNET          OR NET                                       
*        S     RF,PPBVTAX          NO TAX IN ADJ CALC                           
*        MR    R0,RF                                                            
*        L     RF,=F'1000000'                                                   
*        DRNDR (R0),(RF)                                                        
*                                                                               
*RE84    DS    0H                                                               
*        L     RF,PPBVGRS          BILL BASIS IS GROSS                          
*        TM    0(R2),X'10'                                                      
*        BZ    *+8                                                              
*        L     RF,PPBVNET          OR NET                                       
*        AR    R1,RF                                                            
*                                                                               
*        TM    STAT,STABSSCQ       IF SEP COMMISSION BILL                       
*        BZ    *+8                 ACTUAL IS FORMULA LESS NET                   
*        S     R1,PPBVNET          (TRUE AGENCY COMMISSION)                     
*                                                                               
*        ST    R1,PPBVACT                                                       
*        B     PRE90                                                            
*                                                                               
PRE90    DS    0H                  REGUALR BILL                                 
*                                                                               
PRELEMX  DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
*                                                                               
NEXTEL   DS    0H                                                               
         CLI   0(R2),0       END OF RECORD                                      
         BE    NEXTELX                                                          
         ZIC   R0,1(R2)                                                         
         LTR   R0,R0                                                            
         BNP   NEXTELX                                                          
         AR    R2,R0                                                            
         CLC   ELCODE,0(R2)                                                     
         BCR   8,RE            RETURN WITH CC=                                  
         B     NEXTEL                                                           
*                                                                               
NEXTELX  LTR   R2,R2                                                            
         BR    RE              RETURN WITH CC NOT =                             
*                                                                               
EXIT     XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
BSTTAORQ EQU   X'20'           AOR BILL                                         
BSTSCOMQ EQU   X'01'           UFC COMM BILL                                    
BSTSNETQ EQU   X'08'           UFC NET BILL                                     
*                                                                               
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
ELCODE   DS    CL1                                                              
SAVR1    DS    F                                                                
STAT     DS    X                                                                
*                                                                               
WORKDL   EQU   *-WORKD                                                          
*                                                                               
         EJECT                                                                  
       ++INCLUDE PBILLREC                                                       
       ++INCLUDE PBILELEM                                                       
*                                                                               
       ++INCLUDE PBLPSTEL                                                       
*                                                                               
PPBVALDD DSECT                                                                  
*                                                                               
       ++INCLUDE PPBVALD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008PPBVAL    04/01/13'                                      
         END                                                                    
