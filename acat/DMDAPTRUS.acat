*          DATA SET DMDAPTRUS  AT LEVEL 006 AS OF 07/11/11                      
*CATALP DMDAPTRS                                                                
         TITLE 'DMDAPTRS - ADD DIRECTORY POINTERS'                              
*====================================================================           
* 26OCT05 MHER CANADIAN DESKTOP ADDS CAN HAVE PRODUCT ALLOCATIONS               
*              SO USE PRODUCT LIST ADDRESS IF PRESENT                           
* 07SEP07 MHER 2-BYTE BUYLINE NUMBERS/NOP ADVTSR/ID POINTERS                    
*====================================================================           
                                                                                
* R2 POINTS TO DMCB                                                             
                                                                                
DMDAPTRS CSECT                                                                  
         NMOD1 WRKX-WRKD,DMDAPTRS,CLEAR=YES                                     
         USING WRKD,RC                                                          
         LA    RE,*+6                                                           
         BSM   0,RE                FORCE 24-BIT MODE                            
         EJECT                                                                  
* SEARCH FILE TABLE AND EXTRACT FILE INFO                                       
*                                                                               
         L     R3,4(R2)            POINT TO FILE NAME                           
         LA    R7,FILTAB                                                        
DMDAP1   CLC   0(3,R3),0(R7)       MATCH TO FIRST THREE CHRS OF DIR             
         BE    DMDAP2                                                           
         LA    R7,L'FILTAB(R7)                                                  
         CLI   0(R7),0                                                          
         BNE   DMDAP1                                                           
         DC    H'0'                                                             
DMDAP2   MVC   FILDIR(L'FILTAB),0(R7)                                           
         SPACE 2                                                                
* TRANSFER CONTROL TO ROUTINE FOR FILE                                          
*                                                                               
         CLI   8(R2),0             EXIT IF ERRORS IF DMDALINK                   
         BNE   DMDAPTRX                                                         
         L     R3,0(R2)                                                         
         L     RF,FILADD           SET ADDREC FUNCTION                          
         CLI   0(R3),C'A'                                                       
         BE    *+8                                                              
         L     RF,FILPUT           SET PUTREC FUNCTION                          
         LTR   RF,RF                                                            
         BZ    DMDAPTRX            EXIT IF FUNCTION NOT DEFINED                 
*                                                                               
         L     R3,8(R2)            R3=A(DISK ADDRESS)                           
         L     R7,12(R2)           R7=A(RECORD)                                 
         BR    RF                                                               
         SPACE 2                                                                
DMDAPTRX XMOD1 1                                                                
         ORG   *-2                                                              
         BSM   0,RE                                                             
         EJECT                                                                  
* GENERAL ADD ROUTINE                                                           
*                                                                               
GENADD   LA    R4,DIRK             POINT TO DIRECTORY AREA                      
         LH    RE,FILKLN           GET KEY LEN                                  
         BCTR  RE,0                                                             
         EX    RE,GAMOVE           MOVE KEY                                     
         LA    R4,1(R4,RE)                                                      
         LA    R7,3(R7,RE)                                                      
         LH    RE,FILCLN           GET CONTROL LEN                              
         BCTR  RE,0                                                             
         EX    RE,GAMOVE           MOVE CONTROL                                 
         LA    R4,1(R4,RE)                                                      
         MVC   0(4,R4),0(R3)       MOVE DISK ADDRESS                            
*                                                                               
         LA    RF,DMADD            SET DMADD COMMAND                            
         ST    RF,DIDMCB                                                        
         TM    0(R2),X'24'         TEST MAINTENANCE ADD ON ADDREC               
         BNO   *+8                 NO                                           
         OI    DIDMCB,X'24'        YES MAINTENANCE ADD ON DIRECTORY             
         GOTO1 =V(DATAMGR),DIDMCB,,FILDIR,DIRK,DIRK                             
*                                                                               
         MVC   8(1,R2),DIDMCB+8    SET ERROR CODE IN ORIGINAL DMCB              
         B     DMDAPTRX                                                         
*                                                                               
GAMOVE   MVC   0(0,R4),0(R7)       EXECUTED MOVE FROM REC TO DIR                
         EJECT                                                                  
* SPTDIR ADD ROUTINE                                                            
*                                                                               
         USING RECD,R7                                                          
*                                                                               
SPTADD   XC    KEY,KEY                                                          
         MVC   KEY(13),0(R7)                                                    
         MVC   KEY+13(1),15(R7)                                                 
*                                                                               
         CLI   0(R7),X'10'         TEST BUY                                     
         BH    SPTBUY                                                           
         CLI   0(R7),X'02'         TEST GOAL                                    
         BE    SPTGL                                                            
* ADD PTR EQUAL TO REC                                                          
         BRAS  R9,TESTADD                                                       
         BNE   DMDAPTRX                                                         
         BRAS  R9,ADDKEY                                                        
         B     DMDAPTRX                                                         
         SPACE 2                                                                
SPTGL    BRAS  R9,TESTADD                                                       
         BNE   DMDAPTRX                                                         
*                                                                               
         BRAS  R9,ADDKEY                                                        
*                                                                               
* CHECK FOR ADVTSR                                                              
*                                                                               
         CLI   GADVAGY,0                                                        
         BE    DMDAPTRX                                                         
*                                                                               
         ZIC   RE,KEY+1            A/M                                          
         SRDL  RE,4                ISOLATE AG IN RE/MED IN RF                   
         STC   RE,KEY+11                                                        
*                                                                               
         IC    RE,GADVAGY                                                       
         SLDL  RE,4                                                             
         STC   RE,KEY+1                                                         
*                                                                               
         BRAS  R9,ADDKEY                                                        
         B     DMDAPTRX                                                         
         EJECT                                                                  
* BUY RECORDS                                                                   
*                                                                               
SPTBUY   XC    KEY,KEY                                                          
         MVC   KEY(10),0(R7)                                                    
         MVC   KEY+11(1),10(R7)    LINE NUM                                     
*                                                                               
         TM    BUYRCNTL,BUYRLN2    TEST 2-BYTE LINE NUMBERS                     
         BZ    SPTB1                                                            
         MVC   KEY+11(2),10(R7)                                                 
         OI    KEY+13,X'10'                                                     
         B     SPTB2                                                            
*                                                                               
SPTB1    MVI   ELCODE,X'61'        CHECK FOR MASTER CLIENT CODE ELEM            
         LA    R4,BDELEM                                                        
         BRAS  R9,NEXTEL                                                        
         BNE   *+10                                                             
         MVC   BUYMCLT(3),2(R4)    SAVE CLIENT CODE/SEQNUM                      
         CLI   1(R4),6             TEST ELEMENT HAS PRD CODE                    
         BL    *+10                                                             
         MVC   BUYMPRD,5(R4)       YES - SAVE IT                                
*                                                                               
SPTB2    L     RE,0(R2)            POINT TO COMMAND                             
         CLI   0(RE),C'A'          TEST ADD                                     
         BNE   SPTB4                                                            
         TM    BDSTAT3,BDST3_DSKADD  TEST ADD BY DESKTOP                        
         BZ    SPTB5                 NO                                         
*                                                                               
SPTB4    CLI   3(R7),X'FF'         TEST POL                                     
         BNE   DMDAPTRX                                                         
         ICM   R4,15,20(R2)        ADDED PRD LIST                               
         BZ    DMDAPTRX                                                         
         B     SPTB10                                                           
*                                                                               
* SEARCH FOR PBELEM IF NON-POOL                                                 
*                                                                               
SPTB5    CLI   3(R7),X'FF'         TEST POL                                     
         BE    SPTB10              YES                                          
*                                                                               
         CLI   BDTIME,0            TEST P/B                                     
         BNE   SPTB6               YES                                          
*                                                                               
* NON-POL - NON-P/B                                                             
*                                                                               
         MVI   KEY+12,1            SET REGULAR                                  
         BRAS  R9,ADDKEY                                                        
         B     DMDAPTRX                                                         
         EJECT                                                                  
* PIGGYBACK                                                                     
*                                                                               
SPTB6    MVI   ELCODE,4            FIND PBELEM                                  
         LA    R4,BDELEM                                                        
         BRAS  R9,NEXTEL                                                        
         BE    SPTB7                                                            
         DC    H'0'                                                             
*                                                                               
SPTB7    MVI   KEY+12,2                                                         
         BRAS  R9,ADDKEY                                                        
*                                                                               
* NOW ADD PIGGYBACK PTR                                                         
*                                                                               
         ZIC   RE,1(R4)                                                         
         SRDA  RE,32                                                            
         D     RE,=F'7'                                                         
         LR    R5,RF               SET FOR BCT                                  
         LA    R4,2(R4)                                                         
*                                                                               
SPTB8    MVC   KEY(10),0(R7)                                                    
         MVC   KEY+3(1),0(R4)      PRD                                          
         MVC   KEY+9(1),1(R4)      EST                                          
         MVC   KEY+10(1),3(R7)     ACTIVE PRD                                   
         MVC   KEY+11(1),9(R7)     ACTIVE EST                                   
         MVC   KEY+12(1),10(R7)    LINE NUM                                     
*                                                                               
         BRAS  R9,ADDKEY                                                        
*                                                                               
         LA    R4,7(R4)                                                         
         BCT   R5,SPTB8                                                         
*                                                                               
         B     DMDAPTRX                                                         
         EJECT                                                                  
* POOL BUY                                                                      
*                                                                               
SPTB10   DS    0H                  SEARCH FOR ID ELEMENT                        
         XC    BUYID,BUYID                                                      
         CLI   BDXFRAGY,0          TEST TRANSFERRED BUY                         
         BNE   SPTB18              YES                                          
         TM    BDCANAD,X'80'       NOT FOR CANADA                               
         BO    SPTB18                                                           
         MVI   ELCODE,X'70'                                                     
         LA    R4,BDELEM                                                        
         BRAS  R9,NEXTEL                                                        
         BNE   SPTB18                                                           
*                                  CHECK RIGHT KIND OF ID ELEM                  
         CLI   3(R4),C'A'          MGR SCHEME CODE MUST BE 'A'                  
         BL    SPTB18                                                           
         CLI   3(R4),C'K'          THRU 'K'                                     
         BH    SPTB18                                                           
*                                                                               
         LA    R5,4(R4)            MUST THEN HAVE 4 NUMERICS                    
         LA    R0,4                NOTE- 0000 WILL NOT PRODUCE ID PTR           
*                                                                               
SPTB12   DS    0H                                                               
         CLI   0(R5),C'0'                                                       
         BL    SPTB18                                                           
         CLI   0(R5),C'9'                                                       
         BH    SPTB18                                                           
         LA    R5,1(R5)                                                         
         BCT   R0,SPTB12                                                        
*                                  HAVE VALID ID                                
         PACK  DUB(3),4(5,R4)      PWOS                                         
         MVC   BUYID,DUB                                                        
*                                                                               
SPTB18   DS    0H                                                               
         L     RE,0(R2)            POINT TO COMMAND                             
         CLI   0(RE),C'A'          TEST ADD                                     
         BE    SPTB20                                                           
*                                                                               
         L     R4,20(R2)           ADDED PRD LIST                               
         LTR   R4,R4                                                            
         BZ    DMDAPTRX                                                         
         LA    R5,128                                                           
         B     SPTB32                                                           
*                                                                               
SPTB20   DS    0H                                                               
         TM    BUYRCNTL,BUYRLN2    TEST 2-BYTE LINE NUMBERS                     
         BO    *+8                                                              
         MVI   KEY+12,1                                                         
*                                                                               
         CLI   BDXFRAGY,0          TEST TRANSFERRED BUY                         
         BE    *+10                                                             
         MVC   KEY+12(1),BDXFRAGY                                               
         BRAS  R9,ADDKEY                                                        
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         BRAS  RE,ADDSPLL                                                       
         MVC   KEY,KEYSAVE                                                      
*                                                                               
         TM    BDCANAD,X'80'       TEST CANADA                                  
         BZ    SPTB24                                                           
         IC    R0,KEY              GET AGY/MD                                   
         N     R0,=X'0000000F'     DROP AGY                                     
         CHI   R0,1                TEST SPOT TV                                 
         BE    SPTB22                                                           
         CHI   R0,3                TEST NETWORK TV                              
         BNE   SPTB24                                                           
*                                                                               
SPTB22   BRAS  RE,ADDCMBD                                                       
         BRAS  RE,ADDSPLL                                                       
*                                                                               
SPTB24   MVC   KEY,KEYSAVE                                                      
                                                                                
*===========================================================                    
* ON NEW BUY ONLY POSSIBLE ALLOCATIONS ARE MASPRDS                              
*===========================================================                    
                                                                                
SPTB30   LA    R4,BDMASPRD                                                      
         LA    R5,2                                                             
*                                                                               
         TM    BDSTAT3,BDST3_DSKADD  TEST ADD BY DESKTOP                        
         BZ    SPTB32                                                           
         ICM   R4,15,20(R2)        MUST PASS PRDLIST ADDRESS                    
         BZ    DMDAPTRX                                                         
         LA    R5,128                                                           
*                                                                               
SPTB32   CLI   0(R4),0                                                          
         BE    DMDAPTRX                                                         
         CLI   0(R4),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
         MVC   KEY(10),0(R7)                                                    
         MVC   KEY+3(1),0(R4)                                                   
         MVI   KEY+10,X'FF'                                                     
         MVC   KEY+12(1),10(R7)    LINE NUM                                     
         MVI   KEY+11,0                                                         
*                                                                               
         CLI   BDXFRAGY,0          TEST TRANSFERRED BUY                         
         BE    *+10                                                             
         MVC   KEY+11(1),BDXFRAGY                                               
*                                                                               
         TM    BUYRCNTL,BUYRLN2    TEST 2-BYTE LINE NUMBER                      
         BZ    *+10                                                             
         MVC   KEY+11(2),10(R7)                                                 
*                                                                               
         BRAS  R9,ADDKEY                                                        
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         BRAS  RE,ADDSPLL                                                       
         MVC   KEY,KEYSAVE                                                      
*                                                                               
         TM    BDCANAD,X'80'       TEST CANADA                                  
         BZ    SPTB36                                                           
         IC    R0,KEY              GET AGY/MD                                   
         N     R0,=X'0000000F'     DROP AGY                                     
         CHI   R0,1                TEST SPOT TV                                 
         BE    SPTB34                                                           
         CHI   R0,3                TEST NETWORK TV                              
         BNE   SPTB36                                                           
*                                                                               
SPTB34   BRAS  RE,ADDCMBD                                                       
         BRAS  RE,ADDSPLL                                                       
*                                                                               
SPTB36   MVC   KEY,KEYSAVE                                                      
*                                                                               
SPTB38   LA    R4,1(R4)                                                         
         BCT   R5,SPTB32                                                        
*                                                                               
SPTBX    B     DMDAPTRX                                                         
         EJECT                                                                  
ADDKEY   DS    0H                                                               
         MVC   KEY+14(4),0(R3)     MOVE DISK ADDRESS TO KEY                     
         GOTO1 =V(DATAMGR),DIDMCB,DMADD,FILDIR,KEY,KEY                          
*                                                                               
         TM    8(R1),X'DF'         TEST ALL ERRORS BUT DUP KEY                  
         BZR   R9                                                               
         DC    H'0'                                                             
         SPACE 2                                                                
NEXTEL   DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
         CLI   0(R4),0                                                          
         BE    NEXTELX                                                          
         CLC   0(1,R4),ELCODE                                                   
         BER   R9                                                               
         B     NEXTEL                                                           
NEXTELX  LTR   R9,R9                                                            
         BR    R9                                                               
         SPACE 2                                                                
TESTADD  L     RE,0(R2)            A(COMMAND)                                   
*                                                                               
         CLC   =C'ADD',0(RE)       TEST 'ADDREC'                                
         BE    *+10                                                             
         CLC   =C'DMA',0(RE)       TEST 'DMADD'                                 
         BR    R9                  EXIT WITH CC SET                             
         EJECT                                                                  
ADDCMBD  NTR1                                                                   
*                                                                               
         MVC   KEY+10(1),KEY       MOVCE AGY/MED                                
         NI    KEY+10,X'0F'        LEAVE MEDIA ONLY                             
*                                                                               
         CLI   KEY+10,1            TEST SPOT TV                                 
         BE    ADDCMBD2                                                         
         CLI   KEY+10,3            TEST NTWK TV                                 
         BNE   ADDCMBDX                                                         
*                                                                               
ADDCMBD2 NI    KEY,X'F0'           DROP MEDIA                                   
         OI    KEY,X'08'           SET MEDIA TO CMBD                            
         BRAS  R9,ADDKEY                                                        
*                                                                               
ADDCMBDX XIT1                                                                   
         ORG   *-2                                                              
         BSM   0,RE                                                             
         SPACE 2                                                                
ADDSPLL  NTR1                                                                   
*                                                                               
         MVI   ELCODE,3                                                         
         LA    R4,BDELEM                                                        
*                                                                               
ADDSPLL2 BRAS  R9,NEXTEL                                                        
         BNE   ADDSPLLX                                                         
*                                                                               
         MVC   KEY+4(2),4(R4)      SET SPILL MKT                                
         CLI   KEY+10,X'FF'        NOTE THIS WILL LEAVE MEDIA IN                
         BNE   *+8                   KEY+10 FOR CMBD PTRS                       
         MVI   KEY+10,0                                                         
         OI    KEY+10,X'80'        SET SPILL PTR IND                            
*                                                                               
         BRAS  R9,ADDKEY                                                        
***NOP   BRAS  R9,IDPTR            ADD ID POINTER IF REQUIRED                   
         B     ADDSPLL2                                                         
*                                                                               
ADDSPLLX XIT1                                                                   
         ORG   *-2                                                              
         BSM   0,RE                                                             
         SPACE 3                                                                
IDPTR    DS    0H                  ADD ID POINTER IF REQUIRED                   
         OC    BUYID,BUYID         TEST ANY ID                                  
         BZR   R9                                                               
         TM    BUYRCNTL,BUYRLN2    TEST 2-BYTE LINE NUMBER                      
         BOR   R9                  YES - NO ID POINTERS                         
*                                                                               
         MVC   KEYSAV2,KEY                                                      
         XC    KEY,KEY                                                          
         MVC   KEY(4),KEYSAV2      AM/CLT/PRD                                   
         NI    KEY,X'F0'           CLEAR MEDIA                                  
         OI    KEY,X'0C'           SET ID POINTER CODE INSTEAD                  
         MVC   KEY+4(2),BUYID      ID NUMBER                                    
         MVC   KEY+6(6),KEYSAV2+4  MKT/STA/EST                                  
         MVC   KEY+12(1),BUYKBUY+0  LINE NO. TAKEN FROM RECORD                  
*                                                                               
         LR    R0,R9               SAVE R9                                      
         BRAS  R9,ADDKEY                                                        
         LR    R9,R0                                                            
         MVC   KEY,KEYSAV2         RESTORE KEY                                  
         BR    R9                                                               
         EJECT                                                                  
DMADD    DC    CL8'DMADD'                                                       
         SPACE 2                                                                
         DS    0F                                                               
FILTAB   DS    0CL20                                                            
SPTDIR   DC    C'SPTDIR  ',H'13',H'01',A(SPTADD),A(SPTADD)                      
         DC    C'XSPDIR  ',H'32',H'04',A(GENADD),A(0)                           
         DC    C'PRTDIR  ',H'25',H'02',A(GENADD),A(0)                           
         DC    C'PUBDIR  ',H'25',H'02',A(GENADD),A(0)                           
         DC    C'REPDIR  ',H'27',H'01',A(GENADD),A(0)                           
         DC    C'ROIDIR  ',H'31',H'01',A(GENADD),A(0)                           
         DC    C'UNTDIR  ',H'20',H'01',A(GENADD),A(0)                           
         DC    C'PERDIR  ',H'36',H'02',A(GENADD),A(0)                           
         DC    C'MPLDIR  ',H'32',H'04',A(GENADD),A(0)                           
         DC    C'MPQDIR  ',H'32',H'04',A(GENADD),A(0)                           
         DC    C'MPRDIR  ',H'19',H'09',A(GENADD),A(0)                           
         DC    C'BUDDIR  ',H'32',H'04',A(GENADD),A(0)                           
         DC    C'GENDIR  ',H'32',H'04',A(GENADD),A(0)                           
         DC    C'MBADIR  ',H'32',H'08',A(GENADD),A(0)                           
         DC    C'MBUDIR  ',H'32',H'08',A(GENADD),A(0)                           
         DC    C'ACCDIR  ',H'42',H'08',A(GENADD),A(0)                           
         DC    C'TALDIR  ',H'32',H'02',A(GENADD),A(0)                           
         DC    C'CHKDIR  ',H'32',H'02',A(GENADD),A(0)                           
         DC    C'TRFDIR  ',H'13',H'01',A(SPTADD),A(SPTADD)                      
FILTABX  DC    X'00'                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DIRK     DS    CL40                MAX DIR REC = 40 BYTES                       
         ORG   DIRK                                                             
KEY      DS    CL18                                                             
KEYSAVE  DS    CL18                                                             
KEYSAV2  DS    CL18                                                             
ELCODE   DS    CL1                                                              
         ORG                                                                    
DIDMCB   DS    6F                                                               
*                                  FILE TABLE ENTRY                             
FILDIR   DS    CL8                 DIRECTORY NAME                               
FILKLN   DS    H                   KEY LENGTH                                   
FILCLN   DS    H                   CONTROL LENGTH                               
FILADD   DS    A                   ADDREC ROUTINE                               
FILPUT   DS    A                   PUTREC ROUTINE                               
BUYID    DS    XL2                                                              
BUYMCLT  DS    XL2                                                              
BUYMSEQ  DS    XL1                                                              
BUYMPRD  DS    XL1                                                              
*                                                                               
WRKX     EQU   *                                                                
         EJECT                                                                  
RECD     DSECT                                                                  
*                                                                               
       ++INCLUDE SPGENGOAL                                                      
         EJECT                                                                  
         ORG   RECD                                                             
       ++INCLUDE SPGENBUY                                                       
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DMDAPTRUS 07/11/11'                                      
         END                                                                    
