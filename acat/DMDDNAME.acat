*          DATA SET DMDDNAME   AT LEVEL 008 AS OF 09/21/17                      
*CATALP DMDDNAME                                                                
                                                                                
***********************************************************************         
*R1 POINTS TO DMCB SO THIS CAN BE CALLED VIA DMDMGR                   *         
*DMCBW1  A(C'DDNAME' DATAMGR COMMAND)  -X'24'=RCV FILE,X'22'=REQ FILE *         
*DMCBW2  A(FILE DDNAME OR SENAME/SENUM)-X'80'=R/O OK  ,X'40'=DSN ALOC *         
*DMCBW3  A(FILE INFO TABLE)                                           *         
***********************************************************************         
         TITLE 'DMDDNAME - CL8 DDNAME OR SYS=XXXXX TO SE NUMBER'                
         PRINT NOGEN                                                            
DMDDNAME CSECT                                                                  
         NMOD1 WRKX-WRKD,DMDDNAME,CLEAR=YES                                     
         USING WRKD,RC                                                          
         LR    R2,R1               R2=A(PARAM LIST)                             
         MVI   DDNAFACI,X'FF'      SET FACPAK ID                                
         MVC   DDNADSN,SPACES      SET FILE NOT FOUND IN DYDNDD TABLE           
         MVC   DDNASYN3,SPACES                                                  
         XR    RE,RE                                                            
         ICM   RE,7,5(R2)          DMCBW2=A(DDNAME) OR A(SYS=XXXXX)             
         LA    RF,DDNADATA                                                      
         ST    RF,8(R2)            SET DMCBW3=A(FILE INFO AREA)                 
*                                                                               
DMDDN1   CLC   0(4,RE),=C'SYS='                                                 
         BNE   DMDDN2                                                           
         MVC   DDNASENA,4(RE)      SET SE NAME PASSED (5-CHR EG ACC##)          
         B     CHKSYN                                                           
*                                                                               
DMDDN2   CLC   0(2,RE),=C'S='                                                   
         BNE   DMDDN3                                                           
         MVC   DDNASENA(1),2(RE)   SET SE NAME PASSED (3-CHR EG A##)            
         MVC   DDNASENA+1(2),SPACES                                             
         MVC   DDNASENA+3(2),3(RE)                                              
         B     CHKSYN                                                           
*                                                                               
DMDDN3   CLC   0(3,RE),=C'SE='     SE=0X TO PASS BINARY SE NUMBER X             
         BNE   DMDDN4                                                           
         MVC   DDNASENO,4(RE)      SET SE NUM PASSED                            
         MVC   DDNASENA,SPACES                                                  
         B     CHKSE#                                                           
*                                                                               
DMDDN4   MVC   DDNADDN,0(RE)       SET DDNAME PASSED  (8-CHR)                   
         EJECT                                                                  
***********************************************************************         
*SEARCH DDS DYNAMIC FILE ALLOCATION TABLE                             *         
***********************************************************************         
CHKDYN   ICM   RF,15,=V(DMDYNDD)                                                
         BZ    CHKDYNX                                                          
         GOTO1 (RF),MYWORK,0,DDNADDN,0                                          
         BNE   CHKDYNX                                                          
         ICM   RE,15,0(R1)                                                      
         JZ    *+2                                                              
         NILH  GRE,X'00FF'                                                      
         MVC   DDNADSN,0(RE)       MOVE IN DSN IT WOULD ALLOCATE                
*                                                                               
         L     RE,8(R1)            RE=A(TABLE ENTRY)                            
         USING DYNDD,RE                                                         
         MVC   DDNADDND,DYNDDLEN   COMPARE LENGTH MATCH IN DDNAME               
         LA    R0,L'DYNDSN                                                      
         LA    RF,DYNDSN                                                        
         DROP  RE                                                               
*                                                                               
         LR    RE,RF               SEARCH DSN FOR LAST CHR                      
CHKDYN10 CLI   0(RE),C'='                                                       
         BE    CHKDYN12                                                         
         CLI   0(RE),C' '                                                       
         BE    CHKDYN12                                                         
         LA    RE,1(RE)                                                         
         BRCT  R0,CHKDYN10                                                      
*                                                                               
CHKDYN12 SR    RE,RF                                                            
         STC   RE,DDNADSND         SET DISPLACEMENT TO DSN CHRS                 
*                                                                               
CHKDYNX  CLI   DDNADSN,C' '                                                     
         BE    RETN                EXIT IF NOT IN TABLE                         
         EJECT                                                                  
***********************************************************************         
*GET ACTUAL DSN ALLOCATED USING DYNALLOC                              *         
***********************************************************************         
GETDSN   TM    4(R2),X'40'         TEST IF CALLER WANTS ALLOCATED DSN           
         BZ    GETDSNX                                                          
         LA    R1,DYNBLK2                                                       
         ST    R1,DYNBLK1                                                       
         OI    DYNBLK1,X'80'                                                    
         LA    R1,DYNBLK4                                                       
         ST    R1,DYNBLK3                                                       
         LA    R1,DYNBLK5                                                       
         ST    R1,DYNBLK3+4                                                     
         OI    DYNBLK3+4,X'80'                                                  
         MVC   DYNBLK2,=X'1407000000000000000000000000000018000000'             
         LA    R1,DYNBLK3                                                       
         ST    R1,DYNBLK2+8                                                     
         MVC   DYNBLK4(6),=X'000100010008'                                      
         MVC   DYNBLK4+6(8),DDNADDN                                             
         MVC   DYNBLK5(6),=X'000500010020'                                      
         MVC   DYNBLK5+6(32),SPACES                                             
         LA    R1,DYNBLK1                                                       
         DYNALLOC                                                               
*                                                                               
GETDSN1  LA    RF,DYNBLK5+6        RF=A(ALLOCATED DSN)                          
         CLI   0(RF),C' '                                                       
         BE    GETDSNX                                                          
         MVC   DDNADSN,0(RF)       SET ACTUAL DSN                               
         OI    DDNADSNF,X'40'      SET ACTUAL DSN ALLOCATED FLAG                
*                                                                               
GETDSNX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
*SEARCH SYSFLES FOR MATCH ON DD NAME AT DTF+22                        *         
***********************************************************************         
FNDFIL   ICM   R3,15,=V(SYSFLES)   R3=A(SYSFLES ENTRY FOR FILE)                 
         BZ    FNDFILX                                                          
         USING SYSFLSTD,R3                                                      
FNDFIL1  CLI   0(R3),X'FF'         TEST END OF SYSFLES LIST                     
         BE    FNDFILX                                                          
         MVC   DDNASENO,SYSFSYS#   SAVE SENUM                                   
         MVC   DDNASEF1,SYSFSTAT   SAVE SEFLAGS                                 
         ST    R3,DDNAASFL                                                      
         SR    R0,R0                                                            
         IC    R0,SYSF#FLS+1       GET NUMBER OF FILES                          
         LA    R3,SYSFLIST         POINT TO FIRST                               
         SR    R4,R4                                                            
*                                                                               
FNDFIL2  TM    SYSFIND2,SFALIAS    IGNORE ALIAS FILES                           
         BO    FNDFIL3                                                          
         ICM   R4,7,SYSFADTF       R4=A(DTF)                                    
         USING DTFPHD,R4                                                        
         TM    DTFOPEN,DTF_RO      TEST READ ONLY FILE                          
         BZ    *+12                                                             
         TM    4(R2),X'80'         TEST CALLER WANTS READ ONLY FILES            
         BZ    FNDFIL3                                                          
         TM    DTFTYPE,DTFTEMU     IGNORE EMULATED FILES                        
         BO    FNDFIL3                                                          
FNDFIL2A TM    DTFTYPE,DTFTIS      TEST IF INDEX SEQ FILE                       
         BO    FNDFIL2B                                                         
         TM    DTFFLAG,DTFRODTF    IGNORE ADD=NO DA FILES                       
         BZ    FNDFIL2C                                                         
         TM    4(R2),X'80'         TEST CALLER WANTS READ ONLY FILES            
         BO    FNDFIL2C                                                         
         B     FNDFIL3                                                          
         USING ISDTF,R4                                                         
FNDFIL2B TM    ISCMPRSW,X'40'      TEST IF ADD=NO IS FILE                       
         BZ    FNDFIL2C                                                         
         TM    4(R2),X'80'         TEST CALLER WANTS READ ONLY FILES            
         BO    FNDFIL2C                                                         
*&&US*&& CLI   DDNASENO,X'0C'      US DEMOS CAN HAVE ADD=NO FILES               
*&&US*&& BE    FNDFIL2C                                                         
         B     FNDFIL3                                                          
FNDFIL2C CLC   DDNADDN,22(R4)      MATCH ON DDNAME IN DTF                       
         BE    FNDFIL4                                                          
*                                                                               
FNDFIL3  LA    R3,SYSFLNQ(R3)      LOOP FOR EACH FILE                           
         BCT   R0,FNDFIL2                                                       
         B     FNDFIL1             TRY NEXT SYSTEM                              
*                                                                               
FNDFIL4  MVC   DDNAFINO,SYSFILE#   SET FILE NUMBER AND FILE FLAGS               
         MVC   DDNAFIF1(2),SYSFIND1                                             
         ST    R4,DDNAADTF         SET A(DTF)                                   
FNDFILX  EQU   *                                                                
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
*FIND SYSTEM NAME FROM SE NUMBER                                      *         
***********************************************************************         
FNDSYS   ICM   R3,15,=V(SYSFLES)   R3=A(SYSFLES ENTRY FOR FILE)                 
         BZ    FNDSYSX                                                          
         AHI   R3,-16              POINT TO LIST OF TABLES IN DMFILES           
         L     R0,12(R3)                                                        
         S     R0,08(R3)           R0=LEN OF SYSTEM LETTERS TABLE               
         L     RE,8(R3)                                                         
         SR    R1,R1                                                            
         IC    R1,DDNASENO                                                      
         AR    RE,R1                                                            
         CHI   R0,256                                                           
         BH    FNDSYS2                                                          
FNDSYS1  MVC   DDNASEN3+1(1),0(RE) SET SINGLE SYSTEM LETTER                     
         MVI   DDNASEN3+2,C' '                                                  
         B     FNDSYS3                                                          
*                                                                               
FNDSYS2  AR    RE,R1                                                            
         MVC   DDNASEN3+1(2),0(RE) SET TWO CHR SYSTEM LETTERS                   
FNDSYS3  L     RE,4(R3)                                                         
         AR    RE,R1                                                            
         MVC   DDNASYNO,0(RE)      SET SYSTEM NUMBER                            
         IC    R1,DDNASYNO                                                      
         LA    RE,SIDTAB1(R1)                                                   
         MVC   DDNASEN3(1),0(RE)   SET SINGLE SYSTEM CHR                        
         IC    R1,DDNASYNO                                                      
         SLL   R1,2                                                             
         LA    RE,SIDTAB(R1)                                                    
         MVC   DDNASENA(3),0(RE)                                                
         MVC   DDNASENA+3(2),DDNASEN3+1                                         
FNDSYSX  B     RETN                                                             
         EJECT                                                                  
***********************************************************************         
*INPUT IS SE=0X WHERE X IS ONE CHR BINARY SE NUMBER                   *         
*CONVERT TO SHORT SENAME FORMAT AND PROCESS AS S=XXX                  *         
***********************************************************************         
CHKSE#   ICM   R3,15,=V(SYSFLES)   R3=A(SYSFLES ENTRY FOR FILE)                 
         BZ    RETN                                                             
         AHI   R3,-16              R3=A(LIST OF TABLES IN DMFILES)              
         SR    R1,R1               R1=SENUM                                     
         ICM   R1,1,DDNASENO                                                    
CHKSE#1  L     RE,4(R3)            RE=A(TABLE OF SYSTEM NUMBERS)                
         AR    RE,R1                                                            
         SR    RF,RF                                                            
         ICM   RF,1,0(RE)                                                       
         BZ    RETN                                                             
         STC   RF,DDNASYNO                                                      
CHKSE#2  L     RE,0(R3)            RE=A(TABLE OF ONE CHR SYSTEM NAMES)          
         AR    RE,RF                                                            
         MVC   DDNASENA(1),0(RE)                                                
CHKSE#3  L     RE,8(R3)            RE=A(TABLE OF SYSTEM LETTERS)                
         AR    RE,R1                                                            
         L     R0,12(R3)                                                        
         S     R0,08(R3)           R0=LEN OF SYSTEM LETTERS TABLE               
CHKSE#4  CHI   R0,256              TEST ONE OR TWO CHR ENTRIES                  
         BH    CHKSE#5                                                          
         MVC   DDNASENA+3(1),0(RE)                                              
         B     CHKSE#X                                                          
CHKSE#5  AR    RE,R1                                                            
         MVC   DDNASENA+3(2),0(RE)                                              
CHKSE#X  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
*INPUT IS SYS=XXXXX WHERE XXXXX IS A FIVE CHR SYSTEM NAME EG. C'ACC1 '*         
*INPUT IS S=XXX WHERE XXX IS A THREE CHR SYSTEM NAME EG. C'A1 '       *         
*FIND SE NUMBER FROM FOUR CHR SYS NAME STORED IN SENAME               *         
***********************************************************************         
CHKSYN   EQU   *                   CHECK FOR SYSTEM NAME SYNONYMS               
*&&UK*&& B     CHKSEN                                                           
*&&US                                                                           
         CLI   DDNASENA,C'N'       TEST US NETWORK SYSTEM PASSED                
         BNE   CHKSEN                                                           
         LA    RE,SPTNET                                                        
CHKSYN1  CLI   0(RE),0             TEST END OF SPT TO NET TABLE                 
         BE    RETN                                                             
         CLC   DDNASENA+3(2),3(RE) COMPARE INPUT WITH NET SYSTEM                
         BE    CHKSYN2                                                          
         LA    RE,L'SPTNET(RE)                                                  
         B     CHKSYN1                                                          
CHKSYN2  MVI   DDNASYN3,C'N'       RETURN NAME OF SYNONYM                       
         MVC   DDNASYN3+1(2),3(RE)                                              
         MVI   DDNASENA,C'S'       SET ACTUAL SYSTEM NAME                       
         MVC   DDNASENA+1(2),SPACES                                             
         MVC   DDNASENA+3(2),1(RE)                                              
*&&                                                                             
CHKSEN   LA    RE,SIDTAB           SEARCH SYSTEM NAME TABLE                     
         SR    R1,R1                                                            
         CLI   DDNASENA+1,C' '                                                  
         BNE   CHKSEN3                                                          
         LA    RE,SIDTAB1                                                       
CHKSEN1  CLC   DDNASENA(1),0(RE)   MATCH ON ONE CHR SYS LETTER                  
         BE    CHKSEN2                                                          
         LA    RE,1(RE)                                                         
         LA    R1,1(R1)                                                         
         CHI   R1,16                                                            
         BL    CHKSEN1                                                          
         B     RETN                                                             
CHKSEN2  LR    RE,R1               CONVERT TO THREE CHR NAME                    
         SLL   RE,2                                                             
         LA    RE,SIDTAB(RE)                                                    
         MVC   DDNASENA(3),0(RE)                                                
         B     CHKSEN4                                                          
CHKSEN3  CLC   DDNASENA(3),0(RE)   MATCH ON THREE CHRS OF SYS NAME              
         BE    CHKSEN4                                                          
         LA    RE,4(RE)                                                         
         LA    R1,1(R1)                                                         
         CHI   R1,16                                                            
         BL    CHKSEN3                                                          
         B     RETN                                                             
CHKSEN4  STC   R1,DDNASYNO         POSITION IN TABLE GIVES SYS NUM              
         IC    R1,DDNASYNO                                                      
         LA    RE,SIDTAB1(R1)                                                   
         MVC   DDNASEN3(1),0(RE)   SET SINGLE SYSTEM CHR                        
         MVC   DDNASEN3+1(2),DDNASENA+3                                         
*                                                                               
CHKSEN5  ICM   R3,15,=V(SYSFLES)   R3=A(SYSFLES ENTRY FOR FILE)                 
         BZ    RETN                                                             
         AHI   R3,-16              POINT TO LIST OF TABLES IN DMFILES           
         L     R0,12(R3)                                                        
         S     R0,08(R3)           R0=LEN OF SYSTEM LETTERS TABLE               
         L     RE,4(R3)            RE=A(TABLE OF SYS NUMS)                      
         SR    R1,R1                                                            
CHKSEN6  CLC   0(1,RE),DDNASYNO    MATCH ON SYS NUM                             
         BNE   CHKSEN9                                                          
         L     RF,8(R3)            RF=A(TABLE OF SYSTEM LETTERS)                
         AR    RF,R1                                                            
CHKSEN7  CHI   R0,256              TEST ONE OR TWO CHR ENTRIES                  
         BH    CHKSEN8                                                          
         CLI   0(RF),C' '          NO LETTER MEANS SINGLE SYSTEM                
         BE    CHKSEN10                                                         
         CLC   DDNASENA+3(1),0(RF) MATCH ON 4TH CHR OF SYSTEM NAME              
         BE    CHKSEN30                                                         
         B     CHKSEN9                                                          
CHKSEN8  AR    RF,R1                                                            
         CLC   0(2,RF),SPACES      NO LETTERS MEANS SINGLE SYSTEM               
         BE    CHKSEN10                                                         
         CLC   DDNASENA+3(2),0(RF) MATCH ON 4TH/5TH CHRS OF SYSTEM NAME         
         BE    CHKSEN30                                                         
CHKSEN9  LA    RE,1(RE)            BUMP TO NEXT ENTRY IN SYSNUM TABLE           
         LA    R1,1(R1)                                                         
         CHI   R1,256                                                           
         BL    CHKSEN6                                                          
         B     RETN                                                             
*                                                                               
CHKSEN10 MVC   DDNASENA+3(2),SPACES IGNORE 4TH/5TH CHRS IF SINGLE SYS           
         MVC   DDNASEN3+1(2),SPACES                                             
*                                                                               
CHKSEN30 STC   R1,DDNASENO         POSITION IN TABLE GIVES SE NUM               
         EJECT                                                                  
***********************************************************************         
*FIND FIRST FILE FOR SYSTEM NAME                                      *         
*FIND RECOVERY FILE IF DMCBW1(1)=X'24'                                *         
*FIND REQUEST FILE IF DMCBW1(1)=X'22'                                 *         
***********************************************************************         
FNDFIF   ICM   R3,15,=V(SYSFLES)   R3=A(SYSFLES ENTRY FOR FILE)                 
         BZ    RETN                                                             
         USING SYSFLSTD,R3                                                      
FNDFIF1  CLI   0(R3),X'FF'         TEST END OF SYSFLES LIST                     
         BE    RETN                                                             
         CLC   DDNASENO,SYSFSYS#   FIND ENTRY FOR SE NUM                        
         BE    FNDFIF2                                                          
         SR    R0,R0                                                            
         IC    R0,SYSF#FLS+1       GET NUMBER OF FILES                          
         SLL   R0,3                                                             
         LA    R3,SYSFLIST         BUMP TO NEXT SE ENTRY                        
         AR    R3,R0                                                            
         B     FNDFIF1                                                          
*                                                                               
FNDFIF2  ST    R3,DDNAASFL         SAVE A(SYSFLES ENTRY)                        
         MVC   DDNASEF1,SYSFSTAT   SAVE SYSTEM FLAGS                            
         SR    R0,R0                                                            
         IC    R0,SYSF#FLS+1       R0=NUM OF FILES                              
         LA    R3,SYSFLIST                                                      
*                                                                               
FNDFIF3  L     R4,SYSFADTF-1       R4=A(DTF)                                    
         LA    R4,0(R4)                                                         
         TM    20(R4),X'80'        IGNORE EMULATED FILES                        
         BO    FNDFIF4                                                          
*                                                                               
FNDFIF3A TM    0(R2),X'24'         WANT RECOVERY FILE                           
         BNO   FNDFIF3B                                                         
         TM    SYSFIND1,SFRCV                                                   
         BO    FNDFIF5                                                          
         B     FNDFIF4                                                          
*                                                                               
FNDFIF3B TM    0(R2),X'22'         WANT REQUEST FILE                            
         BNO   FNDFIF3C                                                         
         TM    SYSFIND1,SFREQ                                                   
         BO    FNDFIF5                                                          
         B     FNDFIF4                                                          
*                                                                               
FNDFIF3C B     FNDFIF5             WANT FIRST FILE                              
*                                                                               
FNDFIF4  LA    R3,SYSFLNQ(R3)      LOOP FOR EACH FILE                           
         BCT   R0,FNDFIF3                                                       
         B     RETN                ERROR IF FILE NOT FOUND                      
*                                                                               
FNDFIF5  MVC   DDNAFINO,SYSFILE#   SET FILE NUMBER AND FILE FLAGS               
         MVC   DDNAFIF1(2),SYSFIND1                                             
         ST    R4,DDNAADTF         SET A(DTF)                                   
         MVC   DDNADDN,22(R4)      SET DDNAME                                   
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*BUILD RETURN INFO IN PARAM LIST                                      *         
***********************************************************************         
RETN     CLI   DDNAFINO,0          TEST FILE OR SYSTEM FOUND                    
         BNE   RETN1                                                            
         OI    8(R2),X'10'         SET NOT FOUND                                
         XC    DDNAFILE,DDNAFILE   CLEAR RETURN INFO AREAS                      
         XC    DDNADSNI,DDNADSNI                                                
         MVI   DDNADSN,C' '                                                     
         MVC   DDNADSN+1(L'DDNADSN-1),DDNADSN                                   
         B     RETNX                                                            
*                                                                               
RETN1    EQU   *                   SET SYNONYM SYSTEM IF DEFINED                
*&&UK*&& B     RETNX                                                            
*&&US                                                                           
         CLI   DDNASYN3,C' '                                                    
         BH    RETN1X                                                           
         CLC   DDNASENA(3),=C'SPT'                                              
         BNE   RETN1X                                                           
         LA    RE,SPTNET                                                        
RETN1A   CLI   0(RE),0             TEST END OF SPT TO NET TABLE                 
         BE    RETN1X                                                           
         CLC   DDNASENA+3(2),1(RE) TEST IF SPT SYSTEM IS A NET SYSTEM           
         BE    RETN1B                                                           
         LA    RE,L'SPTNET(RE)                                                  
         B     RETN1A                                                           
RETN1B   MVI   DDNASYN3,C'N'       RETURN NAME OF SYNONYM                       
         MVC   DDNASYN3+1(2),3(RE)                                              
RETN1X   EQU   *                                                                
*&&                                                                             
RETNX    XMOD1 1                                                                
                                                                                
SPACES   DC    CL32' '                                                          
         LTORG                                                                  
                                                                                
*&&US                                                                           
SIDTAB   DS    0CL4                                                             
         DC    C'    SER SPT NET PRT MPL ACC TAL '                              
         DC    C'REP MBA CON GAM DEM STR PER     '                              
SIDTAB1  DC    C'  SNPLATRBCGDFE '                                              
                                                                                
SPTNET   DS    0CL5                                                             
       ++INCLUDE DMSPTNET                                                       
*&&                                                                             
*&&UK                                                                           
SIDTAB   DC    C'    SER         MED MPL ACC FEE '                              
         DC    C'    MBA CON GAM         PER     '                              
SIDTAB1  DC    C'    MLAF BCG  E '                                              
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
*THIS WORKING STORAGE AREA IS RETURNED TO CALLER                      *         
*FIELDS DEFINED BY DMDDNAMED PAN BOOK                                 *         
***********************************************************************         
WRKD     DSECT                                                                  
*DMDDNAMED                                                                      
       ++INCLUDE DMDDNAMED                                                      
MYWORK   DS    3F                                                               
DYNBLK1  DS    F                   DYNALLOC BLOCK TO GET DSN                    
DYNBLK2  DS    XL20                                                             
DYNBLK3  DS    XL8                                                              
DYNBLK4  DS    XL14                                                             
DYNBLK5  DS    XL38                                                             
WRKX     DS    0X                                                               
                                                                                
*FASSBOFF                                                                       
SSBOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
                                                                                
*DMDYNDD                                                                        
       ++INCLUDE DMDYNDDD                                                       
                                                                                
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
                                                                                
*DMDTFIS                                                                        
       ++INCLUDE DMDTFIS                                                        
                                                                                
*DMSYSFD                                                                        
       ++INCLUDE DMSYSFD                                                        
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DMDDNAME  09/21/17'                                      
         END                                                                    
