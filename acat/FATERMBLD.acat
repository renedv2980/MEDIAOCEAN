*          DATA SET FATERMBLD  AT LEVEL 003 AS OF 02/06/14                      
*CATALP TERMBLD                                                                 
         TITLE 'TERMBLD - BUILD A TERMINAL UTL ENTRY'                           
***********************************************************************         
* INPUT PARAMETER LIST                                                *         
*                                                                     *         
* PARM1  XL1   N/D                                                    *         
*        AL3   ZERO OR ADR OF CL8 SYMBOLIC ID (IF WANT NEW ENTRY)     *         
* PARM2  AL4   ZERO OR ADR OF EMBRIO UTL ENTRY (IF ALREADY EXISTS)    *         
* PARM3  XL1   N/D                                                    *         
*        AL3   ZERO OR ADR OF TERMINAL RECORD                         *         
*                                                                     *         
* RETURNED PARAMETER LIST                                             *         
*                                                                     *         
* PARM1  XL1   80=NO SPACE IN UTL,40=NO SPACE IN PRQ                  *         
*              20=TERM REC NOT FOUND,10=TERM NOT AUTH FOR THIS FACPAK *         
*        AL3   ZERO OR ADR OF CL8 SYMBOLIC ID (IF WANT NEW ENTRY)     *         
* PARM2  AL4   ZERO OR ADR OF EMBRIO UTL ENTRY (IF ALREADY EXISTS)    *         
* PARM3  XL1   N/D                                                    *         
*        AL3   ZERO OR ADR OF TERMINAL RECORD                         *         
***********************************************************************         
         PRINT NOGEN                                                            
TERMBLD  CSECT                                                                  
         NMOD1 WORKX-WORKD,**TBLD**                                             
         USING WORKD,RC            RC=A(WORK)                                   
         SAM31                     THIS IS NOW IN XA MODE                       
*                                                                               
         LR    R2,R1               R2=A(PARAM LIST)                             
         MVC   IFLAGS,0(R2)                                                     
         MVI   0(R2),0             CLEAR HOBS OF PARMS                          
         MVI   8(R2),0                                                          
         LM    R3,R5,0(R2)         R3=A(SYMID),R4=A(UTL),R5=A(TERMREC)          
         L     R8,=V(SYSFAC)       R8=A(SYSTEMS FACILITIES LIST)                
         USING SYSFACD,R8                                                       
         MVI   FLAG,0              INITIALISE APPLICATION ID FLAG               
         XC    TRMCFN,TRMCFN                                                    
         XC    TRMDEF,TRMDEF                                                    
*                                                                               
TBLD1    LTR   R3,R3               TEST IF UTL ENTRY ALREADY EXISTS             
         BZ    TBLD1A                                                           
         LTR   R4,R4                                                            
         JNZ   *+2                                                              
         MVI   NEW,X'01'           SET NEW ENTRY REQUIRED                       
         MVC   SYMID,0(R3)         GET NEW SYMBOLIC ID TO BE ADDED              
         B     TBLD1B                                                           
TBLD1A   LTR   R4,R4                                                            
         JZ    *+2                 PARAM LIST CONFLICT                          
         USING UTLD,R4                                                          
         MVI   NEW,X'00'           SET UTL ENTRY ALREADY EXISTS                 
         MVC   SYMID,TSYM                                                       
TBLD1B   LTR   R5,R5               TEST IF CALLER PASSED TERMINAL REC           
         BZ    TBLD2                                                            
         USING CTTREC,R5                                                        
         CLC   CTTKTID,SYMID                                                    
         BE    TBLD3                                                            
         DC    H'0'                PARAM LIST CONFLICT                          
*                                                                               
TBLD2    LA    R5,TERMREC          SET CTFILE KEY AND READ TERMINAL REC         
         XC    CTTKEY,CTTKEY                                                    
         MVI   CTTKTYP,C'T'                                                     
         MVC   CTTKTID,SYMID                                                    
         MVC   KEY,CTTKEY                                                       
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'CTFILE',KEY,TERMREC                  
         CLI   8(R1),0                                                          
         BE    TBLD2X                                                           
*                                                                               
         XC    CTTKEY,CTTKEY       OR TRY FOR GENERIC                           
         MVI   CTTKTYP,C'T'                                                     
         MVC   CTTKTID,SYMID                                                    
         MVC   CTTKTID+4(4),=C'%%%T'                                            
         MVC   KEY,CTTKEY                                                       
         GOTO1 VDATAMGR,DMCB,=C'DMREAD',=C'CTFILE',KEY,TERMREC                  
         CLI   8(R1),0                                                          
         BE    TBLD2X                                                           
*                                                                               
         OI    0(R2),X'20'         SET TERMINAL REC NOT FOUND                   
         B     TBLDX                                                            
*                                                                               
TBLD2X   ST    R5,8(R2)            RETURN A(TERMINAL REC)                       
         MVC   CTTKTID,SYMID       RESTORE IN CASE OF GENERIC                   
*                                                                               
TBLD3    MVC   SVSTAT,CTTSTAT      SEARCH TERMINAL RECORD                       
         SR    R0,R0                                                            
         LA    R6,CTTDATA                                                       
TBLD4    CLI   0(R6),X'00'                                                      
         BE    TBLD9                                                            
*                                                                               
TBLD4A   CLI   0(R6),X'03'         TERMINAL CONTROL FILE NUMBER                 
         BNE   TBLD4B                                                           
         CLI   1(R6),X'04'                                                      
         BNE   TBLD4B                                                           
         MVC   TRMCFN,2(R6)                                                     
         B     TBLD5                                                            
*                                                                               
TBLD4B   CLI   0(R6),X'24'         APPLICATION ID ELEMENT                       
         BE    TBLD6                                                            
*                                                                               
TBLD4C   CLI   0(R6),X'25'         TERMINAL DEFINITION ELEMENT                  
         BNE   TBLD4D                                                           
         MVC   TRMDEF,0(R6)                                                     
         B     TBLD5                                                            
*                                                                               
TBLD4D   CLI   0(R6),X'26'         TERMINAL LUID ELEMENT                        
         BE    TBLD8                                                            
*                                                                               
TBLD5    IC    R0,1(R6)            BUMP TO NEXT ELEMENT                         
         AR    R6,R0                                                            
         CHI   R0,2                                                             
         BNL   TBLD4                                                            
         MVI   0(R2),X'20'         SET INVALID RECORD STRUCTURE                 
         B     TBLDX                                                            
*                                                                               
TBLD6    OI    FLAG,X'80'          VTAM APPLICATION ID ELEMENT                  
         USING CTAPLD,R6                                                        
         TM    FLAG,X'01'          TEST IF APPLICATION ID ALREADY OK            
         BO    TBLD5                                                            
         MVC   DUB(8),=CL8' '      EXTRACT VTAM APPLID INTO DUB                 
         LLC   R1,CTAPLLEN                                                      
         AHI   R1,-4                                                            
         BNP   TBLD5                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   DUB(0),CTAPLID                                                   
         CLC   DUB,=CL8'ALL'       TEST IF ALL VTAM IDS OK                      
         BE    TBLD6X              NO                                           
         L     RE,VSSB                                                          
         CLC   DUB,SSBVTID-SSBD(RE)                                             
         BE    TBLD6X              EXACT MATCH ON VTAM APPLID                   
*&&UK                                                                           
TBLD6A   CLI   SYMID+7,C'S'        LADVA IS OK FOR ANY UK TERMINAL              
         BE    TBLD5                                                            
         CLC   SSBVTID-SSBD(8,RE),=C'LADVA   '                                  
         BE    TBLD6X                                                           
         CLC   SSBVTID-SSBD(8,RE),=C'LADVB   '                                  
         BE    TBLD6X                                                           
         CLC   SSBVTID-SSBD(8,RE),=C'LADVC   '                                  
         BE    TBLD6X                                                           
         B     TBLD5                                                            
*&&                                                                             
*&&US                                                                           
TBLD6C   CLI   SYMID+7,C'S'        TEST SHUTTLE                                 
         BE    TBLD5                                                            
         CLC   DUB(3),=C'ADV'      TEST ADV ELEMENT                             
         BNE   TBLD6D                                                           
         CLC   SSBVTID-SSBD(3,RE),=C'ADV'                                       
         BE    TBLD6F              FUZZY MATCH ON APPLID ADV                    
         B     TBLD5                                                            
*                                                                               
TBLD6D   CLC   DUB(3),=C'REP'      TEST REP ELEMENT                             
         BNE   TBLD5                                                            
         CLC   SSBVTID-SSBD(3,RE),=C'REP'                                       
         BE    TBLD6F              FUZZY MATCH ON APPLID REP                    
         B     TBLD5                                                            
*&&                                                                             
TBLD6F   OI    FLAG,X'02'          SET FUZZY MATCH ON VTAM APPLID               
         B     TBLD5                                                            
TBLD6X   OI    FLAG,X'01'          SET MATCH ON VTAM APPLID FOUND               
         B     TBLD5                                                            
*                                                                               
TBLD8    TM    2(R6),X'80'         LUID ELEMENT FOUND                           
         BO    TBLD5               IGNORE IF BTAM EL (IN A VTAM REC)            
         MVC   SYMID,3(R6)         EXTRACT VTAM LUID                            
         B     TBLD5                                                            
*                                                                               
TBLD9    LA    R6,TRMDEF           TERMINAL DEFINITION ELEMENT                  
         USING CTTRMD,R6                                                        
         TM    CTTRMDEV,X'80'      TEST PRINTER                                 
         BZ    TBLD9A                                                           
         TM    CTTRMAT1,X'02'      TEST AUTO START                              
         BZ    TBLD9A                                                           
         TM    FLAG,X'01'          MUST GET EXACT MATCH FOR AUTO-START          
         BO    TBLD9B                                                           
         OI    0(R2),X'10'         SET INVALID                                  
         B     TBLDX                                                            
TBLD9A   TM    FLAG,X'02'          ALLOW FUZZY MATCH TERMINALS                  
         BZ    *+8                                                              
         OI    FLAG,X'01'                                                       
TBLD9B   OC    TRMDEF,TRMDEF       END OF RECORD                                
         BNZ   TBLD9C                                                           
         MVC   TRMDEF,TRMEL        USE DEFAULT DEFN IF NOT PRESENT              
         TM    SVSTAT,X'04'                                                     
         BZ    *+10                                                             
         MVC   TRMDEF,PRTEL                                                     
TBLD9C   TM    FLAG,X'80'          TEST IF ANY APPLID ID ELEMENTS               
         BZ    TBLDA                                                            
         TM    FLAG,X'01'          ERROR RETURNS IF END OF RECORD               
         BO    TBLDA                                                            
         OI    0(R2),X'10'         SET NO APPLICATION ID MATCH                  
         B     TBLDX                                                            
*                                                                               
TBLDA    TM    NEW,X'01'           DO WE NEED TO GET A NEW UTL ENTRY            
         BZ    TBLDB               NO                                           
         L     R1,VUTL             YES FIRST CHECK IF ALREADY EXISTS            
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
TBLDA1   CLC   TSYM-UTLD(8,R1),SYMID                                            
         BE    TBLDA2                                                           
         BXLE  R1,RE,TBLDA1                                                     
         B     TBLDA3                                                           
TBLDA2   OI    NEW,X'80'           SET NEW ENTRY ALREADY EXISTS                 
         LR    R4,R1                                                            
         B     TBLDA5                                                           
TBLDA3   GOTO1 VLCM,DMCB,VTGETUTL                                               
         LTR   R4,R1               R1=A(NEW UTL ENTRY)                          
         BNZ   TBLDA4                                                           
         L     RE,VSSB                                                          
         OI    SSBVTFL1-SSBD(RE),SSBVTUTL                                       
         OI    0(R2),X'80'         SET NO ROOM IN UTL                           
         B     TBLDX                                                            
TBLDA4   MVC   TSYM,SYMID                                                       
TBLDA5   ST    R4,4(R2)            RETURN UTL ENTRY ADDRESS                     
*                                                                               
TBLDB    LA    R6,TRMDEF           TERMINAL DEFINITION ELEMENT                  
         USING CTTRMD,R6                                                        
         MVC   TCFN,TRMCFN                                                      
         MVC   TOFFCODE,CTTRMOFC                                                
         CLI   CTTRMOFC,C'*'                                                    
         BNE   TBLDB1                                                           
         OI    TSTAT1,TSTATRSV                                                  
         TM    CTTRMFL1,X'02'                                                   
         BZ    TBLDB1                                                           
         OI    TSTAT1,TSTATMST                                                  
TBLDB1   CLI   CTTRMDEV,X'04'                                                   
         BNE   *+8                                                              
         OI    TSTAT1,TSTATWEB                                                  
*&&UK                                                                           
         CLI   TSYM+7,C'W'         TEMP UNTIL SOMEONE SETS ALL THE WEB          
         BNE   *+8                 TERMINALS TO DEVICE TYPE WEB                 
         OI    TSTAT1,TSTATWEB                                                  
*&&                                                                             
         TM    CTTRMTYP,X'01'                                                   
         BZ    *+8                                                              
         OI    TTYPE,TTYPE327                                                   
         CLI   CTTRMDEV,X'82'                                                   
         BNE   *+8                                                              
         OI    TTYPE,TTYPERMC                                                   
         TM    CTTRMAT2,X'04'      AT2=F IBM ADVANTIS TERMINAL/PRINTER          
         BZ    *+8                                                              
         OI    TTYPE2,TTYPEIAT                                                  
         MVC   TCTRY,CTTRMCTY                                                   
         TM    CTTRMDEV,X'80'      TEST IF DEVICE IS A PRINTER                  
         BO    TBLDC                                                            
         XC    TXPRNT,TXPRNT                                                    
         XC    TPRNT,TPRNT         .....                                        
         TM    CTTRMAT1,X'20'      AT1=C FOR COLOUR TERMINAL                    
         BZ    *+8                                                              
         OI    TTYPE,TTYPECOL                                                   
*&&US                                                                           
         TM    CTTRMAT1,X'08'      AT1=E ELECTRONIC CONTRACT TERMINAL           
         BZ    *+8                                                              
         OI    TTYPE2,TTYPEECT                                                  
*&&                                                                             
*&&UK                                                                           
         TM    CTTRMAT1,X'04'      AT1=F AUTOSWAP (F=FIND THE ADV)              
         BZ    *+8                                                              
         OI    TSTAT8,TST8ASWP                                                  
*&&                                                                             
         MVI   TSESSION,0          INITILISE SESSION INFO                       
         MVI   TNAHNAH,0                                                        
         MVI   TSSXBITS,0                                                       
         MVI   TSSBITS,0                                                        
         MVI   TSSSWAP,X'FF'                                                    
         MVI   TSSRSRV,X'FF'                                                    
         B     TBLDX                                                            
*                                                                               
TBLDC    CLI   CTTRMAT1,0          SET DEFAULTS FOR UNDEFINED FIELDS            
         BNE   *+10                                                             
         MVC   CTTRMAT1,PRTAT1                                                  
         CLI   CTTRMLTY,0                                                       
         BNE   *+10                                                             
         MVC   CTTRMLTY,PRTLTY                                                  
         OC    CTTRMLSP,CTTRMLSP                                                
         BNZ   *+10                                                             
         MVC   CTTRMLSP,PRTLSP                                                  
         OC    CTTRMPSP,CTTRMPSP                                                
         BNZ   *+10                                                             
         MVC   CTTRMPSP,PRTPSP                                                  
         CLI   CTTRMPST,0                                                       
         BNE   *+10                                                             
         MVC   CTTRMPST,PRTPST                                                  
         OC    CTTRMPBS,CTTRMPBS                                                
         BNZ   *+10                                                             
         MVC   CTTRMPBS,PRTPBS                                                  
         CLI   CTTRMPRQ,0                                                       
         BNE   *+10                                                             
         MVC   CTTRMPRQ,PRTPRQ                                                  
*                                                                               
TBLDD    MVC   TPRBUFFL,CTTRMPBS   PRINTER BUFFER LENGTH                        
         MVC   TPRATT3,CTTRMAT3    NEW THIRD ATTRIBUTE                          
         MVI   TPRSPD,8            NEW STYLE SPEED DEFAULT                      
         MVI   TPRSPDTY,X'20'      NEW STYLE SPEED TYPE DEFAULT                 
         MVC   TPRATTR,CTTRMAT1    PRINTER ATTRIBUTES SET 1                     
         MVC   TPRATT2,CTTRMAT2    PRINTER ATTRIBUTES SET 2                     
         MVC   TPRESC,CTTRMESC     PRINTER ESCAPE SEQUENCE NUMBER               
         MVC   TPRAGY,CTTRMAGY     PRINTER AGENCY ALPHA                         
         MVC   TPRLSP,CTTRMLSP     PRINTER LINE SPEED (CPS)                     
         MVC   TPRNEMAX,CTTRMPRQ   PRINTER MAX NUMBER OF QUEUE ENTRIES          
         CLI   TPRNEMAX,NEMAX                                                   
         BH    *+8                                                              
         MVI   TPRNEMAX,NEMAX                                                   
         ICM   R7,15,TXPRNT        R7=A(PRINTER QUEUE ENTRY)                    
         BNZ   TBLDF                                                            
         USING PRQD,R7                                                          
*                                                                               
TBLDE    LLC   R0,CTTRMPRQ         R0=REQUIRED NUM OF PRQ ENTRIES               
         GOTO1 VLCM,DMCB,VTGETPRQ,(R0)                                          
         L     R0,DMCB+4           R0=ASSIGNED NUM OF PRQ ENTRIES               
         LTR   R7,R1                                                            
         BNZ   TBLDE1                                                           
         L     RE,VSSB                                                          
         OI    SSBVTFL1-SSBD(RE),SSBVTPRQ                                       
         OI    0(R2),X'40'         SET NO ROOM IN PRTQ                          
         B     TBLDX                                                            
TBLDE1   ST    R7,TXPRNT           SET A(PRQ) IN UTL                            
         STCM  R7,7,TPRNT          .....                                        
         ST    R4,PRQAUTL          SET A(UTL) IN PRQ                            
         STC   R0,TPRNEMAX         SET ASSIGNED NUM OF PRQ ENTRIES              
*                                                                               
TBLDF    MVC   PRQBUFFL,TPRBUFFL   PRINTER BUFFER LENGTH                        
         MVC   PRQATTR,TPRATTR     PRINTER ATTRIBUTES SET 1                     
         MVC   PRQATT2,TPRATT2     PRINTER ATTRIBUTES SET 2                     
         MVC   PRQESCN,TPRESC      PRINTER ESCAPE SEQUENCE NUMBER               
         MVC   PRQNEMAX,TPRNEMAX   PRINTER MAX NUMBER OF QUEUE ENTRIES          
                                                                                
***********************************************************************         
* PRINTER SPEED INFO - ONE BYTE PRINTER SPEED AND ONE BYTE SPEED TYPE *         
* NEW STYLE USES TOP 2 BITS OF SPEED TYPE FOR TYPE AND HAS X'20' BIT  *         
* ON TO SHOW NEW STYLE (X'20'NOT ON IN CHS C'P',C'L'.C'B',OR C'C')    *         
* SPEED CHANGES FROM 2 BYTE TO ONE BYTE FIELD WITH DIFFERENT UNITS    *         
***********************************************************************         
TBLDG    MVI   PRQSPDTY,X'20'      NEW STYLE (WAS CHR B/C/L/P)                  
         MVI   PRQSPD,8            NEW ONE BYTE SPEED (WAS 2 BYTES)             
         MVC   PRQATT3,CTTRMAT3    NEW THIRD ATTRIBUTE                          
*                                                                               
TBLDH    CLI   CTTRMPST,C'P'       PAGES PER MINUTE                             
         BNE   TBLDH1                                                           
         OI    PRQSPDTY,X'00'      B'00......' FOR PAGES/MIN                    
         MVC   PRQSPD,CTTRMPSP+1   UNIT IS PAGES/MIN                            
         B     TBLDJ                                                            
*                                                                               
TBLDH1   CLI   CTTRMPST,C'L'       LINES PER MINUTE                             
         BNE   TBLDH2                                                           
         OI    PRQSPDTY,X'40'      B'01......' FOR LINES/6SECS                  
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,3,CTTRMPSP                                                    
         D     RE,=F'10'           UNIT IS LINES/6SECS                          
         STC   RF,PRQSPD                                                        
         B     TBLDJ                                                            
*                                                                               
TBLDH2   CLI   CTTRMPST,C'B'       CHARS PER SECOND (BISYNCH)                   
         BNE   TBLDH3                                                           
         OI    PRQSPDTY,X'80'      B'10......' FOR CHARS/HALFSEC                
         ICM   RF,3,CTTRMPSP                                                    
         SRL   RF,1                CONVERT TO CHARS/HALFSEC                     
         STC   RF,PRQSPD                                                        
         B     TBLDJ                                                            
*                                                                               
TBLDH3   CLI   CTTRMPST,C'C'       CHARS PER SECOND                             
         BNE   TBLDJ                                                            
         OI    PRQSPDTY,X'C0'      B'11......' FOR CHARS/HALFSEC                
         ICM   RF,3,CTTRMPSP                                                    
         SRL   RF,1                CONVERT TO CHARS/HALFSEC                     
         STC   RF,PRQSPD                                                        
*                                                                               
TBLDJ    MVC   TPRSPD,PRQSPD       NEW STYLE SPEED TO UTL                       
         MVC   TPRSPDTY,PRQSPDTY   NEW STYLE SPEED TYPE TO UTL                  
*                                                                               
TBLDX    CLI   0(R2),0             EXIT WITH CC EQL IF OK                       
         XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTATNTS                                             *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
NEMAX    EQU   99                  DEFAULT MAXIMUM NUM OF ENTRIES               
*                                                                               
TRMEL    DC    X'2520'             DEFAULT TERMINAL ELEMENT                     
         DC    X'0101'             TRM/3270                                     
         DC    X'00'               CTRY                                         
         DC    C'  ',C' '          AGYALPHA/OFFICE                              
         DC    XL24'00'                                                         
*&&UK                                                                           
PRTEL    DC    X'2520'             DEFAULT PRINTER ELEMENT                      
         DC    X'8101'             PRT/3270                                     
         DC    X'00'               CTRY                                         
         DC    C'  ',C' '          AGYALPHA/OFFICE                              
PRTFL1   DC    X'00'               FLAG BYTE 1 01=DDS1,02=DDS2                  
PRTFL2   DC    X'00'               FLAG BYTE 2                                  
PRTAT1   DC    X'A1'               ATTRIBUTE BYTE 1=HAC                         
PRTAT2   DC    X'00'               ATTRIBUTE BYTE 2                             
PRTLTY   DC    C'B'                LINE TYPE B=BSC,L=LCL,S=SDLC,T=TWX           
PRTLSP   DC    AL2(600)            LINE SPEED CPS                               
PRTPSP   DC    AL2(120)            PRINTER SPEED CPS OR LPM                     
PRTPST   DC    C'B'                PRINTER SPEED TYPE B/C/L                     
PRTPRQ   DC    AL1(NEMAX)          PRINTER NUMBER OF PRQ ENTRIES                
PRTPBS   DC    AL2(1920)           PRINTER BUFFER SIZE                          
         DC    XL11'00'            SPARE                                        
*&&                                                                             
*&&US                                                                           
PRTEL    DC    X'2520'             DEFAULT PRINTER ELEMENT                      
         DC    X'8101'             PRT/3270                                     
         DC    X'00'               CTRY                                         
         DC    C'  ',C' '          AGYALPHA/OFFICE                              
PRTFL1   DC    X'00'               FLAG BYTE 1 01=DDS1,02=DDS2                  
PRTFL2   DC    X'00'               FLAG BYTE 2                                  
PRTAT1   DC    X'A1'               ATTRIBUTE BYTE 1=HAC                         
PRTAT2   DC    X'00'               ATTRIBUTE BYTE 2                             
PRTLTY   DC    C'B'                LINE TYPE B=BSC,L=LCL,S=SDLC,T=TWX           
PRTLSP   DC    AL2(600)            LINE SPEED CPS                               
PRTPSP   DC    AL2(232)            PRINTER SPEED CPS/LPM/PPM                    
PRTPST   DC    C'L'                PRINTER SPEED TYPE B/C/L/P                   
PRTPRQ   DC    AL1(NEMAX)          PRINTER NUMBER OF PRQ ENTRIES                
PRTPBS   DC    AL2(1700)           PRINTER BUFFER SIZE                          
         DC    XL11'00'            SPARE                                        
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                                                               
IFLAGS   DS    X                                                                
FLAG     DS    X                                                                
NEW      DS    X                                                                
SVSTAT   DS    X                                                                
SYMID    DS    CL8                                                              
TRMCFN   DS    XL2                                                              
TRMDEF   DS    CL32                                                             
KEY      DS    CL25                                                             
TERMREC  DS    2000C                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER FACPAK DSECTS                                                 *         
***********************************************************************         
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* FAPRQ                                                                         
       ++INCLUDE FAPRQ                                                          
* FACHKPT                                                                       
       ++INCLUDE FACHKPT                                                        
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003FATERMBLD 02/06/14'                                      
         END                                                                    
