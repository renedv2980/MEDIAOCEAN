*          DATA SET ACPOPTCONV AT LEVEL 015 AS OF 05/01/02                      
*CATALP POPTCONV                                                                
         TITLE 'POPTCONV PRODUCTION OPTION CONVERSION'                          
POPTCONV CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 GODX-GOD,POTPCONV,RA,RR=R2                                       
         USING GOD,RC                                                           
         MVC   FROM,0(R1)          PASS A C P J                                 
         MVC   AINPUT,0(R1)        PARAMETER 1 =A(INPUT RECORD)                 
         MVC   AOUTPUT,4(R1)       PARAMETER 2 =A(OUTPUT RECORD)                
         MVC   USERHOOK,8(R1)      PARAMETER 3 =A(CALLERS HOOK)                 
         MVC   USERRD,4(RD)                                                     
         MVC   ACOMFACS,12(R1)     PARAMETER 4=A(COMFACS)                       
         L     R7,ACOMFACS                                                      
         USING COMFACSD,R7                                                      
         GOTO1 CDATCON,DMCB,(5,0),(1,TODAY)                                     
         MVI   ANYOPT,C'N'                                                      
         L     R2,AOUTPUT                                                       
         XCEF  IO,1000                                                          
         MVC   42(2,R2),=H'70'     SET UP FOR RULE RECORDS                      
         MVC   IO(42),0(R2)                                                     
         MVC   LENGTH,=H'50'                                                    
         BAS   RE,OLDTOBUF                                                      
         MOVE  ((R2),1000),IO                                                   
         CLI   ANYOPT,C'Y'                                                      
         BNE   XIT                                                              
         BAS   RE,HOOK                                                          
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE CONVERTS OLD RECORDS TO BUFFER ELEMENTS                  
         SPACE 3                                                                
*                                  R6=A(RECORD)                                 
         SPACE 1                                                                
OLDTOBUF NTR1                                                                   
         L     R6,AINPUT                                                        
         MVI   ELCODE,0                                                         
         LA    R4,BUFFEL                                                        
         USING OPTELD,R4                                                        
         BAS   RE,GETEL                                                         
         B     OTB4                                                             
         SPACE 1                                                                
OTB2     BAS   RE,NEXTEL                                                        
         SPACE 1                                                                
OTB4     BNE   XIT                                                              
         CLI   0(R6),X'10'                                                      
         BE    OTBCOMP                                                          
         CLI   0(R6),X'24'         PICK OUT USEFUL ELEMENTS                     
         BE    OTBPROF                                                          
         CLI   0(R6),X'3C'                                                      
         BE    OTBEXTRA                                                         
         CLI   0(R6),X'42'                                                      
         BE    OTBRULE                                                          
         B     OTB2                                                             
         SPACE 1                                                                
OTBCOMP  MVI   FROM+1,C'C'         OLD COMPANY ELEMENT                          
         MVI   OPTFROM,C'C'                                                     
         BAS   RE,COMP                                                          
         B     OTB2                                                             
         SPACE 1                                                                
OTBPROF  MVI   FROM+1,C'P'         OLD PROFILE                                  
         MVI   OPTFROM,C'P'                                                     
         BAS   RE,PROF                                                          
         B     OTB2                                                             
         SPACE 1                                                                
OTBEXTRA MVI   FROM+1,C'X'         OLD EXTRA PROFILE                            
         MVI   OPTFROM,C'X'                                                     
         BAS   RE,EXTRA                                                         
         B     OTB2                                                             
         SPACE 1                                                                
OTBRULE  MVI   FROM+1,C'R'         OLD RULE                                     
         MVI   OPTFROM,C'R'                                                     
         BAS   RE,RULE                                                          
         B     OTB2                                                             
         EJECT                                                                  
*              ROUTINE TO HANDLE OLD COMPANY ELEMENT                            
         SPACE 1                                                                
COMP     NTR1                                                                   
         USING ACCOMPD,R6                                                       
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         MVI   OPTWG,0                                                          
         XC    OPTWORK,OPTWORK                                                  
         SPACE 1                                                                
COMP2    MVI   OPTFIELD,116                                                     
         MVI   OPTLEN,9                                                         
         MVI   OPTDATA,C'Y'        DEFAULT IS AUTOMATIC EST T/A                 
         TM    ACMPSTAT,X'01'      TEST IF T/A SUPPRESSED                       
         BZ    *+8                                                              
         MVI   OPTDATA,C'N'        YES                                          
         CLI   OPTDATA,C'Y'                                                     
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
COMP4    MVI   OPTFIELD,127        SUPPRESS STICKY LABELS                       
         MVI   OPTLEN,9                                                         
         MVI   OPTDATA,C'N'        DEFAULT IS PRODUCE THE LABELS                
         TM    ACMPSTAT,X'02'      TEST TO SUPPRESS STICKY LABELS               
         BNO   *+8                                                              
         MVI   OPTDATA,C'Y'                                                     
         CLI   OPTDATA,C'N'                                                     
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO HANDLE OLD PROFILE                                    
         SPACE 1                                                                
PROF     NTR1                                                                   
         USING ACPROFD,R6                                                       
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         MVI   OPTWG,0                                                          
         XC    OPTWORK,OPTWORK                                                  
         CLI   ACPRBILL,0          BILLING TYPE                                 
         BE    PROF4                                                            
         MVI   OPTFIELD,103                                                     
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA(1),ACPRBILL                                              
         OC    ACPRBLAM(4),ACPRBLAM                                             
         BZ    PROF2                                                            
         MVC   OPTDATA+1(4),ACPRBLAM                                            
         MVI   OPTLEN,13                                                        
         SPACE 1                                                                
PROF2    BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
PROF4    CLI   ACPRUNBL,X'41'      UNBILLABLE WORK CODES                        
         BL    XIT                                                              
         MVC   OPTDATA(12),ACPRUNBL                                             
         MVI   OPTFIELD,102                                                     
         MVI   OPTLEN,20                                                        
         CLI   ACPRUNBL+10,X'40'                                                
         BH    PROF6                                                            
         MVI   OPTLEN,18                                                        
         CLI   ACPRUNBL+8,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,16                                                        
         CLI   ACPRUNBL+6,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,14                                                        
         CLI   ACPRUNBL+4,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,12                                                        
         CLI   ACPRUNBL+2,X'40'                                                 
         BH    PROF6                                                            
         MVI   OPTLEN,10                                                        
         SPACE 1                                                                
PROF6    BAS   RE,OPTOUT                                                        
         B     XIT                                                              
         DROP  R6                                                               
         DROP  R4                                                               
         EJECT                                                                  
*              ROUTINE TO HANDLE OLD EXTRA PROFILE                              
         SPACE 1                                                                
EXTRA    NTR1                                                                   
         USING ACXPROFD,R6                                                      
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         MVI   OPTWG,0                                                          
         XC    OPTWORK,OPTWORK                                                  
         SPACE 1                                                                
         MVI   OPTFIELD,104        NUMBER OF DAYS DUE                           
         MVI   OPTLEN,9                                                         
         ZAP   DUB,ACXPDUE                                                      
         CVB   R1,DUB                                                           
         STC   R1,OPTDATA                                                       
         CLI   OPTDATA,10          (DEFAULT IS 10)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,105        PERCENT OF EST                               
         MVI   OPTLEN,12                                                        
         ZAP   OPTDATA(4),ACXPOVER                                              
         CP    ACXPOVER,=P'10000'  (DEFAULT IS 100.00%)                         
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,108        MINIMUM BILL AMOUNT                          
         MVI   OPTLEN,12                                                        
         ZAP   OPTDATA(4),ACXPLOW                                               
         CP    ACXPLOW,=P'5000'    (DEFAULT VALUE IS $50.00)                    
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,109        CLIENT SUMMARY ON BILLS?                     
         CLI   FROM,C'P'                                                        
         BE    EXTRA2              (NOT SET AT PRODUCT LEVEL)                   
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA,ACXPSUM                                                  
         CLI   OPTDATA,C'Y'        (DEFAULT IS YES)                             
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
EXTRA2   MVI   OPTFIELD,110        PRODUCT SUMMARY ON BILLS?                    
         CLI   FROM,C'P'                                                        
         BNE   EXTRA4              (ONLY SET AT PRODUCT LEVEL)                  
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA,ACXPSUM                                                  
         CLI   OPTDATA,C'N'        (DEFAULT IS NO)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
EXTRA4   MVI   OPTFIELD,114        PAYMENTS ARE NET                             
         MVI   OPTLEN,9                                                         
         MVI   OPTDATA,C'Y'                                                     
         CLI   ACXPNET,C'N'        (DEFAULT IS NO)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,115        SUPPRESS AGENCY COMMISSION                   
         MVI   OPTLEN,9            ON NET PAYMENTS                              
         MVI   OPTDATA,C'Y'                                                     
         CLI   ACXPNET,C'S'        (DEFAULT IS NO)                              
         BNE   *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,111        DETAIL LINES ON PROGRESSIVE BILLS            
         MVI   OPTDATA,C'N'                                                     
         CLI   ACXPDET,C'Y'        (DEFAULT IS YES)                             
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,112        SHOW WC ON EST BILLING                       
         MVI   OPTDATA,C'Y'                                                     
         CLI   ACXPEDET,C'N'       (DEFAULT IS NO)                              
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,113        PASS CASH DISCOUNT TO CLIENT                 
         MVI   OPTDATA,C'N'                                                     
         CLI   ACXPCD,X'41'                                                     
         BL    EXTRA6                                                           
         CLI   ACXPCD,C'Y'         (DEFAULT IS YES)                             
         BE    EXTRA6                                                           
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
EXTRA6   MVI   OPTFIELD,107        ESTIMATE NEEDED FOR BILLS?                   
         MVC   OPTDATA,ACXPEST                                                  
         CLI   OPTDATA,X'41'                                                    
         BL    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,116        AUTOMATIC TURN-AROUNDS?                      
         MVI   OPTDATA,C'N'                                                     
         TM    ACXPST1,X'80'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,118        ESTIMATE BILLS COMMISSIONABLE                
         MVI   OPTDATA,C'N'                                                     
         TM    ACXPST1,X'40'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,120        NEW JOBS ARE UNAPPROVED                      
         MVI   OPTDATA,C'Y'                                                     
         TM    ACXPST1,X'02'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,125        PRINT WORK CODE ON AC21                      
         MVI   OPTDATA,C'Y'                                                     
         TM    ACXPST1,X'01'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,122        AUTO UNHOLD ON BILLING                       
         MVI   OPTDATA,C'Y'                                                     
         TM    ACXPST2,X'80'                                                    
         BNO   *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         MVI   OPTFIELD,124        FILTER 2 WHEN EST TO TOT                     
         MVC   OPTDATA,ACXPREP                                                  
         CLI   OPTDATA,0                                                        
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         SPACE 1                                                                
         MVI   OPTFIELD,123        NEW BILL TYPE FOR EST                        
         MVC   OPTDATA,ACXPBILL                                                 
         CLI   OPTDATA,0                                                        
         BE    *+8                                                              
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
         XIT                                                                    
         SPACE 1                                                                
         DROP  R6                                                               
         DROP  R4                                                               
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO HANDLE OLD RULE ELEMENT                               
         SPACE 1                                                                
RULE     NTR1                                                                   
         USING ACRULED,R6                                                       
         USING OPTELD,R4                                                        
         MVI   OPTMG,0                                                          
         MVI   OPTMED,0                                                         
         CLI   FROM,C'J'           TEST FOR JOB                                 
         BE    *+10                YES-CANNOT HAVE MEDIA OPTION                 
         MVC   OPTMED,ACRLMED                                                   
         MVI   OPTWG,0                                                          
         MVC   OPTWORK,ACRLWORK                                                 
         CLI   ACRLCOMM,X'FF'      IGNORE IF THIS IS OVERRIDE                   
         BE    RULE6                                                            
         ZAP   DUB,ACRLCOMM                                                     
         CVB   R1,DUB                                                           
         CLI   ACRLLEN,X'0B'                                                    
         BL    RULE2                                                            
         TM    ACRLSTAT,X'80'      IS IT ALREADY 4 DEC?                         
         BO    RULE4                                                            
         SPACE 1                                                                
RULE2    MH    R1,=H'100'          ELSE CONVERT TO 4 DEC                        
         SPACE 1                                                                
RULE4    MVI   OPTFIELD,101        AGENCY COMMISSION                            
         MVI   OPTLEN,12                                                        
         CVD   R1,DUB                                                           
         MVC   OPTDATA(4),DUB+4                                                 
         BAS   RE,OPTOUT                                                        
         SPACE 1                                                                
RULE6    CLI   ACRLTAX,X'FF'       TAX CODE                                     
         BE    XIT                                                              
         MVI   OPTFIELD,126        AGENCY COMMISSION                            
         MVI   OPTLEN,9                                                         
         MVC   OPTDATA,ACRLTAX                                                  
         BAS   RE,OPTOUT                                                        
         B     XIT                                                              
         SPACE 1                                                                
         DROP  R6                                                               
         DROP  R4                                                               
         EJECT                                                                  
*              ROUTINE TO HANDLE OPTION ELEMENTS                                
         SPACE 3                                                                
OPTOUT   NTR1                                                                   
         USING OPTELD,R4                                                        
         L     R6,AOUTPUT                                                       
         AH    R6,DATADISP                                                      
         USING ACOPD,R6                                                         
         MVI   ACOPEL,ACOPELQ                                                   
         ZIC   R1,OPTLEN                                                        
         LA    R1,8(R1)                                                         
         STC   R1,ACOPLEN                                                       
         MVC   ACOPNUM,OPTFIELD                                                 
         MVC   ACOPPERS,=CL8'DDS'                                               
         MVC   ACOPLAST,TODAY                                                   
         XC    ACOPLAST+3(2),ACOPLAST+3                                         
         XC    ACOPDATA(24),ACOPDATA                                            
         ZIC   R1,OPTLEN                                                        
         SH    R1,=H'9'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   ACOPDATA(0),OPTDATA                                              
         OC    OPTMG(5),OPTMG      IS THIS A RULE OVERRIDE                      
         BZ    OPTOUT2                                                          
         L     R3,AOUTPUT          THEN PUT IT OUT NOW                          
         USING ACOPKEY,R3                                                       
         MVC   ACOPMG,OPTMG                                                     
         MVC   ACOPMED,OPTMED      COMPLETE KEY                                 
         MVC   ACOPWG,OPTWG                                                     
         MVC   ACOPWORK,OPTWORK                                                 
         CLI   ACOPMED,0                                                        
         BE    *+8                                                              
         MVI   ACOPMG,X'FF'                                                     
         OC    ACOPWORK,ACOPWORK                                                
         BZ    *+8                                                              
         MVI   ACOPWG,X'FF'                                                     
         BAS   RE,HOOK                                                          
         B     XIT                                                              
         SPACE 1                                                                
OPTOUT2  LH    R1,LENGTH                                                        
         BCTR  R1,0                                                             
         LA    R3,IO(R1)                                                        
         ZIC   R1,ACOPLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),ACOPEL                                                   
         ZIC   R1,ACOPLEN                                                       
         AH    R1,LENGTH                                                        
         STH   R1,LENGTH                                                        
         MVI   ANYOPT,C'Y'                                                      
         B     XIT                                                              
         EJECT                                                                  
*              ODDMENTS                                                         
         SPACE 3                                                                
HOOK     NTR1                                                                   
         L     RF,USERHOOK                                                      
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
DATADISP DC    H'49'                                                            
         SPACE 1                                                                
         SPACE 1                                                                
NO       LA    R1,1                                                             
         B     *+6                                                              
         SPACE 1                                                                
YES      SR    R1,R1                                                            
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
GETELIO  LA    R6,IO                                                            
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
*              LTORG FOR POPTCONV                                               
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*              WORKING STORAGE FOR GETOPT                                       
         SPACE 3                                                                
GOD      DSECT                                                                  
WORK     DS    CL64                                                             
DUB      DS    D                                                                
DMCB     DS    CL24                                                             
KEYSAVE  DS    CL42                I/O AREAS                                    
KEY      DS    CL42                                                             
LENGTH   DS    H                                                                
STATUS   DS    CL1                                                              
         DS    CL4                                                              
ELEMENTS DS    0C                                                               
         ORG   KEY                                                              
IO       DS    1000C                                                            
ELCODE   DS    CL1                                                              
         SPACE 1                                                                
BUFFLEN  DS    XL1                                                              
BUFFEL   DS    CL256                                                            
SPACES   DS    CL132                                                            
AINPUT   DS    A                                                                
AOUTPUT  DS    A                                                                
USERHOOK DS    A                                                                
ACOMFACS DS    A                                                                
USERRD   DS    F                                                                
ANYOPT   DS    C                                                                
TODAY    DS    PL3                                                              
FROM     DS    CL2                                                              
GODX     EQU   *                                                                
         SPACE 1                                                                
         EJECT                                                                  
*              DSECT TO COVER OPTION ELEMENT IN BUFFER                          
         SPACE 1                                                                
OPTELD   DSECT                                                                  
OPTFIELD DS    XL1                 FIELD NUMBER                                 
OPTLEN   DS    XL1                 ELEMENT LENGTH (L'DATA+8)                    
OPTMG    DS    CL1                 MEDIA GROUP OR X'00'                         
OPTMED   DS    CL1                 MEDIA CODE OR X'00'                          
OPTWG    DS    CL1                 WORK CODE GROUP OR X'00'                     
OPTWORK  DS    CL2                 WORK CODE OF X'0000'                         
OPTFROM  DS    CL1                 CAME FROM P X R OR O                         
OPTDATA  DS    0C                  OPTION DATA                                  
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE ACGENBOTH                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015ACPOPTCONV05/01/02'                                      
         END                                                                    
