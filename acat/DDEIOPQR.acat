*          DATA SET DDEIOPQR   AT LEVEL 003 AS OF 01/24/13                      
*CATALP EIOPQR                                                                  
         TITLE 'EIOPQR - ESS IO PRINT QUEUE REPORT PROCESSOR'                   
***********************************************************************         
* EIOPQR - ESS IO PQ REPORT SUB SYSTEM INTERFACE                      *         
*                                                                     *         
* PARAMS - P1 = A(ESSATC - ESSIO ATTACHED TASK CONTROL BLOCK)         *         
***********************************************************************         
                                                                                
         PRINT NOGEN                                                            
EIOPQR   CSECT                                                                  
         NMOD1 WORKX-WORKD,*EIOPQR*,CLEAR=YES,RA,R9,R8,RR=RE                    
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RE,RELO                                                          
         ST    RD,SAVERD                                                        
         MVC   PARM,0(R1)          SAVE INPUT PARAMETER LIST                    
         L     RF,0(R1)                                                         
         USING ESSATCD,RF          EXTRACT VALUES FROM ESSATCD                  
         ST    RF,AESSATCB         SAVE ADDRESS ATCB                            
         L     R2,EATDATA          R2=A(GENERAL MESSAGE DATA SAVE AREA)         
         USING ESSDATAD,R2                                                      
         LA    R3,ESSDBUFF         R3=A(PQ REPORT MESSAGE DATA)                 
         USING EPQRDAT,R3                                                       
         L     R4,EATWORK          R4=A(PQ REPORT DATA SAVE AREA)               
         USING EIOPQRD,R4                                                       
         MVC   ESSNAME,EATNAME     SAVE ESS ID NAME                             
         MVC   ESSNAME(3),=CL3'ESS'                                             
         MVC   ESSIDNUM,EATENUM    AND NUMBER                                   
         ICM   RE,15,EATPQHDR      SAVE PQ IO BUFFERS                           
         STCM  RE,15,AEPQHDR                                                    
         ICM   RE,15,EATEDHDR                                                   
         STCM  RE,15,AEEDHDR                                                    
         ICM   RE,15,EATPQBUF                                                   
         STCM  RE,15,AEPQBUFF                                                   
         L     R5,EATFACS          R5=A(COMMON FACILITIES)                      
         USING ESSFACSD,R5                                                      
         ICM   RE,15,EATESSB                                                    
         USING ESSBD,RE                                                         
*                                                                               
         MVI   FTPFLAG,C'N'                                                     
         TM    ESSBSTA1,ESSBSFTP   FTP=YES FOR THIS HOSTESS?                    
         BZ    SETFTPX             NO                                           
         TM    EATHFLG1,GESSFNFQ   FTP ENABLED FOR ESS ID?                      
         BZ    SETFTPX             YES                                          
         MVI   FTPFLAG,C'Y'                                                     
SETFTPX  EQU   *                                                                
*                                                                               
         MVI   COMFLAG,C'N'                                                     
         TM    EATHFLG1,GESSFCOQ                                                
         BNZ   *+12                                                             
         TM    ESSBSTA1,ESSBSCOM                                                
         BZ    *+8                                                              
         MVI   COMFLAG,C'Y'        SAVE COMPRESSION FLAG                        
*                                                                               
         MVC   P,SPACES                                                         
         MVC   WTOMSG,SPACES                                                    
         MVC   WTOMSG(8),ESSBJNAM                                               
         MVI   WTOMSG+8,C'.'                                                    
         MVC   WTOMSG+9(8),EATNAME                                              
         MVI   WTOMSG+17,C'.'                                                   
         ICM   R1,15,EATAESS                                                    
         BZ    *+10                                                             
         MVC   WTOMSG+18(8),ESEELU-ESSESSD(R1)                                  
         DROP  RE,RF                                                            
*YYUN, 1/24/2013, NOT REALLY NEEDED                                             
*        MVC   P(8),=C'FTPFLAG='                                                
*        MVC   P+9(1),FTPFLAG                                                   
*        MVC   P+19(6),=C'EIOPQR'                                               
*        GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         LA    RE,COMFACS          MAP COMMON FACILITIES INTO COMFACS           
         USING COMFACSD,RE                                                      
         L     R1,ADATAMGR                                                      
         ST    R1,CDATAMGR                                                      
         L     R1,ADATCON                                                       
         ST    R1,CDATCON                                                       
         L     R1,AGETDAY                                                       
         ST    R1,CGETDAY                                                       
         L     R1,AGETRET                                                       
         ST    R1,CGETRET                                                       
         DROP  RE                                                               
*                                                                               
         LH    RF,=Y(SOONREC-WORKD)                                             
         LA    RF,WORKD(RF)                                                     
         ST    RF,ASOONREC         ADDRESS SOON RECORD                          
         LH    RF,=Y(IDREC-WORKD)                                               
         LA    RF,WORKD(RF)                                                     
         ST    RF,AIDREC           ADDRESS CONTROL FILE RECORD                  
         LH    RF,=Y(CARDS-WORKD)                                               
         LA    RF,WORKD(RF)                                                     
         ST    RF,ADRCARDS         ADDRESS REQUEST CARDS BUFFER                 
*                                                                               
         L     R7,AMAJORNM         MAJOR RESCOURCE NAME                         
         ENQ   ((7),EQESSIO,E,8)   ENQUEUE ESSIO PROCESS                        
*                                                                               
         CLI   ESSDMODE,ESSDMRCQ   TEST MESSAGE MODE CODE                       
         BE    PROCRCV             PROCESS RECEIVE MODE (ESSLURCV)              
         CLI   ESSDMODE,ESSDMSNQ                                                
         BE    PROCSND             PROCESS SEND MODE (ESSLUSND)                 
         BAS   RE,DCH0                                                          
*                                                                               
EPQROK   EQU   *                   RETURN OK                                    
         MVI   ESSDRETC,ESSDROKQ                                                
         B     EXIT                                                             
*                                                                               
EPQRNF   EQU   *                   RETURN NO PQ REPORTS FOR TRANSFER            
         MVI   ESSDRETC,ESSDRNFQ                                                
         B     EXIT                                                             
*                                                                               
EREX0001 EQU   *                   INVALID MESSAGE RECEIVED FROM ESS            
         MVC   ESSDERRC,=AL4(1)                                                 
         B     EPQRWARN                                                         
EREX0002 EQU   *                   INVALID REQUEST USER ID                      
         MVC   P(40),=CL40'EIOPQR INVALID USER ID'                              
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(2)                                                 
         B     EPQRWARN                                                         
EREX0003 EQU   *                   INVALID REQUEST SYSTEM                       
         MVC   P(40),=CL40'EIOPQR INVALID SYSTEM'                               
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(3)                                                 
         B     EPQRWARN                                                         
EREX0004 EQU   *                   INVALID REQUEST PROGRAM                      
         MVC   P(40),=CL40'EIOPQR INVALID PROGRAM'                              
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   ESSDERRC,=AL4(4)                                                 
         B     EPQRWARN                                                         
EREX0005 EQU   *                   ERROR IN PROCESSING REQUEST FILE             
         MVC   ESSDERRC,=AL4(5)    (SEE DDESSRQSD)                              
         B     EPQRERR                                                          
EREX0006 EQU   *                   GENERAL DCH0 LOGIC ERROR                     
         MVC   ESSDERRC,=AL4(6)                                                 
         B     EPQRERR                                                          
EREX0008 EQU   *                   INVALID DATA IN REQUEST TABLE                
         MVC   ESSDERRC,=AL4(8)                                                 
         B     EPQRWARN                                                         
EREX0009 EQU   *                   INVALID DATA IN REQUEST TABLE                
         MVC   ESSDERRC,=AL4(9)                                                 
         B     EPQRWARN                                                         
EREX0010 EQU   *                   REPORT COMPRESSION EROR                      
         MVC   ESSDERRC,=AL4(10)   (SEE DDCOMPRES)                              
         B     EPQRERR                                                          
EREX0011 EQU   *                   INVALID DATA IN REQUEST TABLE                
         MVC   ESSDERRC,=AL4(11)                                                
         B     EPQRERR                                                          
EREX0012 EQU   *                   INVALID DATA IN REQUEST TABLE                
         MVC   ESSDERRC,=AL4(12)                                                
         B     EPQRERR                                                          
EREX0013 EQU   *                   PQ REPORT ERROR RETURN MESSAGE               
         MVC   ESSDERRC,=AL4(13)                                                
         B     EPQRWARN                                                         
EREX0014 EQU   *                   SOON REQUEST 'SYSINFO' ERROR                 
         MVC   ESSDERRC,=AL4(14)                                                
         B     EPQRWARN                                                         
EREX0015 EQU   *                   SOON REQUEST 'READID' ERROR                  
         MVC   ESSDERRC,=AL4(15)                                                
         B     EPQRWARN                                                         
EREX0016 EQU   *                   REPORT REQUEST 'READID' ERROR                
         MVC   ESSDERRC,=AL4(16)                                                
         B     EPQRWARN                                                         
EREX0017 EQU   *                   REPORT RECEIVED 'READID' ERROR               
         MVC   ESSDERRC,=AL4(17)                                                
         B     EPQRWARN                                                         
EREX0018 EQU   *                   SEND NOTIFY 'READID' ERROR                   
         MVC   ESSDERRC,=AL4(18)                                                
         B     EPQRERR                                                          
EREX0019 EQU   *                   SOON REQUEST 'READSOON' ERROR                
         MVC   ESSDERRC,=AL4(19)                                                
         B     EPQRWARN                                                         
EREX0020 EQU   *                   SOON REQUEST 'BLDPQ' ERROR                   
         MVC   ESSDERRC,=AL4(20)                                                
         B     EPQRERR                                                          
EREX0021 EQU   *                   BULK DONWLOAD FIRST PQ LINE ERROR            
         MVC   ESSDERRC,=AL4(21)                                                
         B     EPQRERR                                                          
EREX0022 EQU   *                   BULK DOWNLOAD GET PQ LINE ERROR              
         MVC   ESSDERRC,=AL4(22)                                                
         B     EPQRERR                                                          
EREX0023 EQU   *                   INVALID DOWNLOAD LINE COUNT                  
         MVC   ESSDERRC,=AL4(23)                                                
         B     EPQRWARN                                                         
EREX0024 EQU   *                   INVALID MESSAGE RECEIVED FROM ESS            
         MVC   ESSDERRC,=AL4(24)   AFTER LAST MESSAGE IN SEND SEQNCE.           
         B     EPQRWARN                                                         
EREX0025 EQU   *                   BULK DOWNLOAD GETPQNDX ERROR                 
         MVC   ESSDERRC,=AL4(25)                                                
         B     EPQRERR                                                          
*                                                                               
EPQRWARN EQU   *                   RETURN WARNING ERROR CODE                    
         MVC   P(40),=CL40'<<<WARNING>>> EIOPQR RETURN CODE: '                  
         GOTO1 AHEXOUT,DMCB,ESSDERRC,P+34,4,=C'TOG'                             
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         MVI   ESSDRETC,ESSDROKQ                                                
         B     EXIT                                                             
*                                                                               
EPQRERR  EQU   *                   RETURN SERIOUS ERROR CODE                    
         MVC   P(40),=CL40'<<<ERROR>>> EIOPQR ERROR CODE: '                     
         GOTO1 AHEXOUT,DMCB,ESSDERRC,P+34,4,=C'TOG'                             
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   WTOMSG+26(2),WTOALARM                                            
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         MVC   WTOMSG+26(2),SPACES                                              
         MVI   ESSDRETC,ESSDRERQ                                                
         B     EXIT                                                             
*                                                                               
EXIT     EQU   *                                                                
         L     R7,AMAJORNM         MAJOR RESCOURCE NAME                         
         DEQ   ((7),EQESSIO,8)     DEQUEUE ESSIO PROCESS                        
*                                                                               
         L     RD,SAVERD                                                        
         L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         CLI   ESSDRETC,ESSDRERQ   SET CC .NE. IF RETURN CODE ERROR             
         BE    EXITERR                                                          
         B     EXITOK                                                           
EXITOK   SR    RC,RC                                                            
EXITERR  LTR   RC,RC                                                            
         XMOD1                                                                  
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB-SYSTEM REQUEST MESSAGE RECEIVED FROM ESS      *         
***********************************************************************         
                                                                                
PROCRCV  EQU   *                                                                
*                                  INITIALISE PQR MESSAGE WORK AREA             
         LR    R0,R4                                                            
         ICM   R1,15,=AL4(EIOPQRDL)                                             
         LA    RE,*                                                             
         ICM   RF,15,=XL4'40000000'                                             
         MVCL  R0,RE                                                            
         MVI   EIOPREQC,0                                                       
         MVI   EIOPCCNT,0                                                       
         MVI   EIOPCLFT,0                                                       
*                                                                               
         BAS   RE,VALPQRM          VALIDATE PQ REPORT SUB-SYSTEM MSGE.          
         BNE   EREX0001                                                         
*                                                                               
         TM    ESSDPCF,ESSHLSTQ    TEST LAST MESSAGE IN SEQUENCE                
         BZ    EPQROK              IF NOT THEN EXIT                             
*                                                                               
         CLI   ESSDMTYP,ESSHINFQ   TEST IF INFO MESSAGE TYPE                    
         BE    PRCVINF                                                          
         CLI   ESSDMTYP,ESSHREQQ   TEST IF REQUEST MESSAGE TYPE                 
         BE    PRCVREQ                                                          
         CLI   ESSDMTYP,ESSHERRQ   TEST IF ERROR MESSAGE TYPE                   
         BE    PROCERR                                                          
         BAS   RE,DCH0                                                          
*                                                                               
PRCVINF  EQU   *                   PROCESS INFO MESSAGE TYPE                    
         CLC   EIOPMID,=AL4(EPQRRCVQ)                                           
         BE    PRPQRCV             PROCESS PQ REPORT RECEIVE ACKNOW.            
         BAS   RE,DCH0                                                          
*                                                                               
PRCVREQ  EQU   *                   PROCESS REQUEST MESSAGE TYPE                 
         CLC   EIOPMID,=AL4(EPQRREQQ)                                           
         BE    PRPQREQ             PROCESS PQ REPORT DOWNLOAD REQUEST           
         CLC   EIOPMID,=AL4(EPQRINQQ)                                           
         BE    PIPQREQ             PROCESS PQ REPORT INQUIRY REQUEST            
         CLC   EIOPMID,=AL4(EPQRINSQ)                                           
         BE    PIPSREQ             PROCESS SINGLE PQ REPORT INQUIRY             
         CLC   EIOPMID,=AL4(EPQRSONQ)                                           
         BE    PSOONREQ            PROCESS SOON REQUEST                         
         BAS   RE,DCH0                                                          
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB SYSTEM DOWNLOAD REQUEST                       *         
***********************************************************************         
                                                                                
PRPQREQ  EQU   *                                                                
*                                                                               
PRRQ010  MVC   USERID,SPACES                                                    
*                                                                               
         OC    ESSDUID,ESSDUID                                                  
         BZ    PRRQ020                                                          
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         MVC   EIOPUID,USERID                                                   
         CLC   USERID,SPACES                                                    
         BE    EREX0002                                                         
*                                                                               
PRRQ020  GOTO1 =A(READID)                                                       
         BNE   EREX0016                                                         
         MVC   EIOPUIDN,USERIDNO                                                
         BAS   RE,GETDATM                                                       
*                                                                               
         LA    RE,REQAREA          WRITE DATA TO REQUEST QUEUE                  
         LA    RF,1000                                                          
         XCEF                                                                   
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         MVC   ESSRDATE,DATENC                                                  
         MVC   ESSRTIME,TIMENB                                                  
         MVI   ESSRSEQN,0                                                       
         LA    R0,ESSRFEL-ESSRKEY+1                                             
         STCM  R0,3,ESSRRLEN                                                    
*                                  GET REQUEST FOR PQ REPORT ID                 
         OC    EIOPBREP,EIOPBREP                                                
         BNZ   PRRQ030                                                          
         GOTO1 =A(INITPQR)                                                      
         BNE   PRERROR                                                          
         XC    RQSWORK,RQSWORK                                                  
         LA    R7,RQSWORK                                                       
         USING ERPQRD,R7                                                        
         MVI   ERPQREL,ERPQRELQ                                                 
         MVI   ERPQRELL,ERPQRLNQ                                                
         MVC   ERPQRSID,EIOPUIDN                                                
         MVC   ERPQRSUB,EIOPSID                                                 
         MVC   ERPQRRNM,EIOPNUMB                                                
         MVC   ERPQRPRI,ESSDPRI                                                 
         MVC   ERPQRDTC,EIOPDTMC                                                
         MVI   ERPQRSTA,ERPQRWTQ                                                
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELAQ),(R6),RQSWORK                         
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         B     PRRQ100                                                          
*                                                                               
PRRQ030  LA    R7,EIOPBLCK                                                      
         USING EIOPBENT,R7                                                      
*                                                                               
PRRQ032  OC    EIOPBENT,EIOPBENT                                                
         BZ    PRRQ100                                                          
         MVC   EIOPBCON,=CL6'000000'                                            
         MVC   EIOPCON,EIOPBCON                                                 
         MVC   EIOPSID,EIOPBSID                                                 
         MVC   EIOPNUM,EIOPBNUM                                                 
*                                                                               
         GOTO1 ANUMVAL,DMCB,EIOPNUM,(X'00',5)                                   
         CLI   0(R1),0                                                          
         BNE   PRERROR                                                          
         L     R1,4(R1)                                                         
         STCM  R1,3,EIOPNUMB                                                    
*                                                                               
         MVI   BYTE,ERPQRWTQ                                                    
         GOTO1 =A(INITPQR)                                                      
         BE    PRRQ040                                                          
         MVC   EIOPBCON,=CL6'009999'                                            
         MVC   EIOPCON,EIOPBCON                                                 
         MVI   BYTE,ERPQRERQ                                                    
*                                                                               
PRRQ040  XC    RQSWORK,RQSWORK                                                  
         LA    RF,RQSWORK                                                       
         USING ERPQRD,RF                                                        
         MVI   ERPQREL,ERPQRELQ                                                 
         MVI   ERPQRELL,ERPQRLNQ                                                
         MVC   ERPQRSID,EIOPUIDN                                                
         MVC   ERPQRSUB,EIOPSID                                                 
         MVC   ERPQRRNM,EIOPNUMB                                                
         MVC   ERPQRPRI,ESSDPRI                                                 
         MVC   ERPQRDTC,EIOPDTMC                                                
         MVC   ERPQRSTA,BYTE                                                    
         DROP  RF                                                               
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELAQ),(R6),RQSWORK                         
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         LA    R7,EIOPBLEN(R7)                                                  
         B     PRRQ032                                                          
*                                  SAVE DATA IN REQUEST FILE                    
PRRQ100  GOTO1 AESSRQS,DMCB,=AL4(ERQAADDQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         DROP  R6                                                               
*                                  SAVE DATA IN REQUEST FILE                    
         BAS   RE,SAVEREQ                                                       
*                                  BUILD REQUEST MESSAGE                        
         MVI   ESSDMTYP,ESSHINFQ                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         MVC   EIOPCON,=CL6'000000'                                             
         MVC   EIOPSPAG,=CL6'000001'                                            
         MVC   EIOPEPAG,EIOPNPAG                                                
*                                                                               
         BAS   RE,BLDPQRM          BUILD PQ REPORT SUB-SYSTEM MESSAGE           
*                                                                               
         OC    EIOPBREP,EIOPBREP                                                
         BNZ   PRRQ200                                                          
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'PQ REPORT REQUEST:      '                            
         BAS   RE,FORMOPM                                                       
         B     PRRQ210                                                          
*                                                                               
PRRQ200  MVC   P(40),=CL40'PQ BLOCK REPORT REQUEST'                             
*                                                                               
PRRQ210  MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB SYSTEM PQ INQUIRY REQUEST                     *         
***********************************************************************         
                                                                                
PIPQREQ  EQU   *                                                                
         MVC   USERID,SPACES                                                    
*                                                                               
         OC    ESSDUID,ESSDUID                                                  
         BZ    PIPQ010                                                          
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         MVC   EIOPUID,USERID                                                   
         CLC   USERID,SPACES                                                    
         BE    EREX0002                                                         
*                                                                               
PIPQ010  GOTO1 =A(READID)                                                       
         BNE   EREX0016                                                         
         MVC   EIOPUIDN,USERIDNO                                                
         BAS   RE,GETDATM                                                       
*                                                                               
         LA    RE,REQAREA          WRITE DATA TO REQUEST QUEUE                  
         LA    RF,1000                                                          
         XCEF                                                                   
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         MVC   ESSRDATE,DATENC                                                  
         MVC   ESSRTIME,TIMENB                                                  
         MVI   ESSRSEQN,0                                                       
         LA    R0,ESSRFEL-ESSRKEY+1                                             
         STCM  R0,3,ESSRRLEN                                                    
*                                                                               
         XC    RQSWORK,RQSWORK                                                  
         LA    R7,RQSWORK                                                       
         USING ERPQID,R7                                                        
         MVI   ERPQIEL,ERPQIELQ                                                 
         MVI   ERPQIELL,ERPQILNQ                                                
         MVC   ERPQISID,EIOPUIDN                                                
         MVC   ERPQISUB,EIOPSID                                                 
         MVC   ERPQIRNM,EIOPNUMB                                                
         MVC   ERPQIPRI,ESSDPRI                                                 
         MVC   ERPQIFMT,EIOPFMAT                                                
         MVI   ERPQISTA,ERPQRWTQ                                                
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELAQ),(R6),RQSWORK                         
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
*                                  SAVE DATA IN REQUEST FILE                    
         GOTO1 AESSRQS,DMCB,=AL4(ERQAADDQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         DROP  R6                                                               
*                                                                               
         BAS   RE,SAVEREQ                                                       
*                                                                               
         MVI   ESSDMTYP,ESSHINFQ                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         MVC   EIOPCON,=CL6'000000'                                             
         MVC   EIOPSPAG,=CL6'000001'                                            
         MVC   EIOPEPAG,EIOPNPAG                                                
*                                                                               
         XC    EIOPBREP,EIOPBREP                                                
         BAS   RE,BLDPQRM          BUILD PQ REPORT SUB-SYSTEM MESSAGE           
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(18),=CL18'PQ INQUIRY:'                                         
         MVC   P+20(8),ESSDUID                                                  
         OC    EIOPSID,EIOPSID                                                  
         BZ    *+10                                                             
         MVC   P+30(3),EIOPSID                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB SYSTEM SINGLE REPORT INQUIRY REQUEST          *         
***********************************************************************         
                                                                                
PIPSREQ  EQU   *                                                                
         MVC   USERID,SPACES                                                    
*                                                                               
         OC    ESSDUID,ESSDUID                                                  
         BZ    PIPS010                                                          
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         MVC   EIOPUID,USERID                                                   
         CLC   USERID,SPACES                                                    
         BE    EREX0002                                                         
*                                                                               
PIPS010  GOTO1 =A(READID)                                                       
         BNE   EREX0016                                                         
         MVC   EIOPUIDN,USERIDNO                                                
         BAS   RE,GETDATM                                                       
*                                                                               
         GOTO1 =A(INITPQR)                                                      
         BNE   PRERROR                                                          
         MVI   ESSDMTYP,ESSHINFQ                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         MVC   EIOPCON,=CL6'000000'                                             
         MVC   EIOPSPAG,=CL6'000001'                                            
         MVC   EIOPEPAG,EIOPNPAG                                                
*                                                                               
         XC    EIOPBREP,EIOPBREP                                                
         BAS   RE,BLDPQRM          BUILD PQ REPORT SUB-SYSTEM MESSAGE           
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(18),=CL18'PQ REPORT INQUIRY:'                                  
         MVC   P+20(8),ESSDUID                                                  
         MVC   P+30(3),EIOPSID                                                  
         MVI   P+33,C','                                                        
         MVC   P+34(5),EIOPNUM                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT REQUEST ERROR RETURN                              *         
***********************************************************************         
                                                                                
PRERROR  EQU   *                                                                
*                                  BUILD REQUEST MESSAGE                        
         MVI   ESSDMTYP,ESSHERRQ                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         MVC   EIOPCON,=CL6'009999'                                             
*                                                                               
         BAS   RE,BLDPQRM          BUILD PQ REPORT SUB-SYSTEM MESSAGE           
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(18),=CL18'PQ REPORT REQUEST:'                                  
         MVC   P+20(8),ESSDUID                                                  
         MVC   P+30(3),EIOPSID                                                  
         MVI   P+33,C','                                                        
         MVC   P+34(5),EIOPNUM                                                  
         MVC   P+40(13),=CL13'<<< ERROR >>>'                                    
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     EREX0013                                                         
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB-SYSTEM SOON REQUEST                           *         
***********************************************************************         
                                                                                
PSOONREQ EQU   *                                                                
         LA    R0,SOONCARD         INITIALISE SOFT MONSOON TABLE                
         L     RE,=A(SOONCTAB)     FOR REENTRANT CODE                           
         LA    R1,SOONTLEN                                                      
         LA    RF,SOONTLEN                                                      
         MVCL  R0,RE                                                            
*                                                                               
PSRQ010  MVC   USERID,SPACES                                                    
         OC    ESSDUID,ESSDUID                                                  
         BZ    PSRQ020                                                          
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         MVC   EIOPUID,USERID                                                   
         CLC   USERID,SPACES                                                    
         BE    EREX0002                                                         
*                                                                               
PSRQ020  MVC   SYSTEM,SPACES                                                    
         OC    ESSDSYS,ESSDSYS                                                  
         BZ    PSRQ030                                                          
         MVC   SYSTEM,ESSDSYS                                                   
         CLC   SYSTEM,SPACES                                                    
         BE    EREX0003                                                         
*                                                                               
PSRQ030  MVC   PROGRAM,SPACES                                                   
         OC    ESSDPRG,ESSDPRG                                                  
         BZ    PSRQ040                                                          
         MVC   PROGRAM,ESSDPRG                                                  
         CLC   PROGRAM,SPACES                                                   
         BE    EREX0004                                                         
*                                                                               
PSRQ040  MVC   PRIORITY,SPACES                                                  
         OC    ESSDPRI,ESSDPRI                                                  
         BZ    PSRQ050                                                          
         MVC   PRIORITY,ESSDPRI                                                 
*                                                                               
PSRQ050  MVC   JOBID,SPACES                                                     
         OC    ESSDREF,ESSDREF                                                  
         BZ    PSRQ100                                                          
         MVC   JOBID,ESSDREF                                                    
*                                                                               
PSRQ100  GOTO1 =A(SYSINFO)                                                      
         BNE   EREX0014                                                         
         GOTO1 =A(READID)                                                       
         BNE   EREX0015                                                         
         MVC   EIOPUIDN,USERIDNO                                                
*                                                                               
         TM    EIOPREQC,RCFLD      DID WE HAVE FIELD CARDS?                     
         BO    PSRQ110                                                          
         MVC   P(40),=CL40'REQUEST CARD FORMAT'                                 
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     PSRQ120                                                          
*                                                                               
PSRQ110  MVC   P(40),=CL40'FIELD FORMAT'                                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         L     RF,EIOPCPOS                                                      
         BCTR  RF,0                BACK UP 1 COLUMN                             
         MVI   0(RF),C'*'          EOD CHARACTER                                
*                                                                               
PSRQ120  MVC   REQCARD,EIOPREQC                                                 
         L     RE,ADRCARDS                                                      
         LA    RF,EIOPCRDS                                                      
         CLI   EIOPCCNT,CARDMAX                                                 
         BH    EREX0023                                                         
         CLI   EIOPCCNT,0                                                       
         BNH   EREX0023                                                         
         ZIC   R0,EIOPCCNT                                                      
*                                                                               
PSRQ130  MVC   0(L'CARDS,RE),0(RF)                                              
         LA    RE,L'CARDS(RE)                                                   
         LA    RF,L'CARDS(RF)                                                   
         BCT   R0,PSRQ130                                                       
*                                                                               
         MVC   CARDCT,EIOPCCNT                                                  
*                                                                               
         GOTO1 =A(READSOON)                                                     
         BNE   EREX0019                                                         
         GOTO1 =A(BLDPQ)                                                        
         BNE   EREX0020                                                         
         MVC   EIOPUIDN,USERIDNO                                                
         MVC   EIOPSID,REPSUBID                                                 
         MVC   EIOPNUMB,REPRNO                                                  
         MVC   EIOPCLAS,REPCLASS                                                
*                                                                               
         BAS   RE,GETDATM                                                       
         LA    RE,REQAREA          WRITE DATA TO REQUEST QUEUE                  
         LA    RF,1000                                                          
         XCEF                                                                   
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         MVC   ESSRDATE,DATENC                                                  
         MVC   ESSRTIME,TIMENB                                                  
         MVI   ESSRSEQN,0                                                       
         LA    R0,ESSRFEL-ESSRKEY+1                                             
         STCM  R0,3,ESSRRLEN                                                    
         LA    R7,ESSRFEL                                                       
         USING ERPQRD,R7                                                        
         MVI   ERPQREL,ERPQRELQ                                                 
         MVI   ERPQRELL,ERPQRLNQ                                                
         MVC   ERPQRSID,EIOPUIDN                                                
         MVC   ERPQRSUB,EIOPSID                                                 
         MVC   ERPQRRNM,EIOPNUMB                                                
         MVC   ERPQRPRI,ESSDPRI                                                 
         MVI   ERPQRSTA,ERPQRWTQ                                                
         ICM   R0,3,ESSRRLEN                                                    
         LA    R1,ERPQRLNQ                                                      
         AR    R0,R1                                                            
         STCM  R0,3,ESSRRLEN                                                    
         GOTO1 AESSRQS,DMCB,=AL4(ERQAADDQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         DROP  R6,R7                                                            
*                                  SAVE REQUEST IN RECOVERY FILE                
         BAS   RE,SAVEREQ                                                       
*                                  BUILD RETURN APPC MESSAGE                    
         XC    EIOPBREP,EIOPBREP                                                
         MVI   ESSDMTYP,ESSHINFQ                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         BAS   RE,BLDPQRM                                                       
*                                                                               
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT RECEIVED ACKNOWLEDGMENT FROM ESS                  *         
***********************************************************************         
                                                                                
PRPQRCV  EQU   *                                                                
*                                                                               
PRRC010  MVC   USERID,SPACES                                                    
         OC    ESSDUID,ESSDUID                                                  
         BZ    PRRC020                                                          
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         MVC   EIOPUID,USERID                                                   
         CLC   USERID,SPACES                                                    
         BE    EREX0002                                                         
*                                                                               
PRRC020  GOTO1 =A(READID)                                                       
         BNE   EREX0017                                                         
         MVC   EIOPUIDN,USERIDNO                                                
         BAS   RE,GETDATM                                                       
*                                                                               
         GOTO1 =A(INITPQR)                                                      
         BNE   PRERROR                                                          
*                                                                               
         GOTO1 MARKREP,DMCB,1       MARK REPORT IS SENT                         
*                                                                               
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         XC    ESSRKEY,ESSRKEY                                                  
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQARHIQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         CLC   ESSRKEY(ESSRDATE-ESSRKEY),ESSRKEY                                
         BNE   EREX0008                                                         
*                                                                               
         LA    R7,ESSRFEL                                                       
         XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQRELQ                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELGQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    PRRC040                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
PRRC030  XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQRELQ                                                 
         ICM   R7,15,ALSTRQEL                                                   
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELNQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    PRRC040                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
PRRC040  MVC   ALSTRQEL,12(R1)                                                  
         ICM   R7,15,ALSTRQEL                                                   
         USING ERPQRD,R7                                                        
         CLC   EIOPUIDN,ERPQRSID                                                
         BNE   PRRC030                                                          
         CLC   EIOPSID,ERPQRSUB                                                 
         BNE   PRRC030                                                          
         CLC   EIOPNUMB,ERPQRRNM                                                
         BNE   PRRC030                                                          
         TM    ERPQRSTA,ERPQRSNQ    TEST HAS SENT NOTIFICATION                  
         BZ    EREX0009                                                         
         OI    ERPQRSTA,ERPQRRCQ    SET RECEIVED ACKNOWLEDGEMENT                
*                                   WRITE RECORD BACK TO REQUEST TABLE          
         GOTO1 AESSRQS,DMCB,=AL4(ERQAWRTQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'PQ REPORT RECEIVED:     '                            
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
*                                                                               
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB-SYSTEM SEND REQUEST MESSAGE                   *         
***********************************************************************         
                                                                                
PROCSND  EQU   *                                                                
         CLI   ESSDSEQN,ESSDSFSQ   TEST MESSAGE FIRST IN SEQUENCE               
         BE    PSNDFST             GET FIRST MESSAGE IN SEQUENCE                
         CLI   ESSDSEQN,ESSDSNXQ   TEST MESSAGE NEXT IN SEQUENCE                
         BE    PSNDNXT             GET NEXT MESSAGE IN SEQUENCE                 
         CLI   ESSDSEQN,ESSDSLSQ   TEST AFTER MESSAGE LAST IN SEQUENCE          
         BE    PSNDLST             PROCESS RESULT OF MESSAGE SEND               
         BAS   RE,DCH0                                                          
                                                                                
***********************************************************************         
* PROCESS FIRST MESSAGE IN SEND REQUEST SEQUENCE                      *         
***********************************************************************         
                                                                                
PSNDFST  EQU   *                                                                
         BAS   RE,READREQ          READ ESS REQUEST QUEUE                       
         BNE   PSNFNF              EXIT NO REPORTS FOUND                        
         CLC   EIOPMID,=AL4(EPQRNRQQ)                                           
         BE    PSNFPQR             PROCESS REPORT WAITING TRANSFER              
         CLC   EIOPMID,=AL4(EPQRNIQQ)                                           
         BE    PSNFINQ             PROCESS INQUIRY WAITING TRANSFER             
         DC    H'0'                                                             
*                                                                               
PSNFNF   B     EPQRNF                                                           
PSNFOK   B     EPQROK                                                           
                                                                                
***********************************************************************         
* PROCESS FIRST MESSAGE IN SEND REQUEST SEQUENCE                      *         
* FOR PQ REPORT TRANSFER                                              *         
***********************************************************************         
                                                                                
PSNFPQR  EQU   *                                                                
         MVI   ESSDMTYP,ESSHINFQ                                                
         MVC   ESSDMID,=AL3(ESSHPQRQ)                                           
         OI    ESSDMFF,ESSHLSTQ                                                 
         CLI   FTPFLAG,C'Y'                                                     
         BE    *+8                                                              
         OI    ESSDMFF,ESSHBDLQ                                                 
         CLI   COMFLAG,C'N'                                                     
         BE    *+8                                                              
         OI    ESSDMFF,ESSHCOMQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         MVC   ESSDSYS,SPACES                                                   
         MVC   ESSDPRG,SPACES                                                   
         MVC   ESSDPWD,SPACES                                                   
*                                                                               
         PACK  ESSDRNUM,EIOPNLIN(7)                                             
         PACK  DUB,EIOPNPAG(6)                                                  
         AP    ESSDRNUM,DUB                                                     
         AP    ESSDRNUM,=PL8'2'                                                 
         ZAP   ESSDRCNT,=PL8'0'                                                 
*                                                                               
         MVC   EIOPMID,=AL4(EPQRNRQQ)                                           
         MVC   EIOPCON,=CL6'000000'                                             
         MVC   EIOPSPAG,=CL6'000001'                                            
         MVC   EIOPEPAG,EIOPNPAG                                                
*                                                                               
         BAS   RE,BLDPQRM          BUILD PQ REPORT SUB-SYSTEM MESSAGE           
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(16),=CL16'PQ REPORT FOUND:'                                    
         MVC   P+18(8),ESSDUID                                                  
         MVC   P+28(3),EIOPSID                                                  
         MVI   P+31,C','                                                        
         MVC   P+32(5),EIOPNUM                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PSNFOK                                                           
         EJECT                                                                  
                                                                                
***********************************************************************         
* PROCESS FIRST MESSAGE IN SEND REQUEST SEQUENCE                      *         
* FOR PQ INQUIRY TRANSFER                                             *         
***********************************************************************         
                                                                                
PSNFINQ  EQU   *                                                                
         MVI   ESSDMTYP,ESSHINFQ                                                
         MVC   ESSDMID,=AL3(ESSHPQRQ)                                           
         OI    ESSDMFF,ESSHLSTQ                                                 
         CLI   FTPFLAG,C'Y'                                                     
         BE    *+8                                                              
         OI    ESSDMFF,ESSHBDLQ                                                 
         CLI   COMFLAG,C'N'                                                     
         BE    *+8                                                              
         OI    ESSDMFF,ESSHCOMQ                                                 
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         MVC   ESSDSYS,SPACES                                                   
         MVC   ESSDPRG,SPACES                                                   
         MVC   ESSDPWD,SPACES                                                   
*                                                                               
         ZAP   ESSDRNUM,=PL8'0'                                                 
         ZAP   ESSDRCNT,=PL8'0'                                                 
*                                                                               
         MVC   EIOPMID,=AL4(EPQRNIQQ)                                           
         MVC   EIOPCON,=CL6'000000'                                             
         MVC   EIOPSPAG,=CL6'000001'                                            
         MVC   EIOPEPAG,EIOPNPAG                                                
*                                                                               
         MVC   EIOPFSID,EIOPSID                                                 
         MVC   EIOPFNUM,EIOPNUMB                                                
         MVC   EIOPFFMT,EIOPFMAT                                                
*                                                                               
         BAS   RE,BLDPQRM          BUILD PQ REPORT SUB-SYSTEM MESSAGE           
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(16),=CL16'PQ INQUIRY FOUND:'                                   
         MVC   P+18(8),ESSDUID                                                  
         OC    EIOPSID,EIOPSID                                                  
         BZ    *+10                                                             
         MVC   P+28(3),EIOPSID                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PSNFOK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS NEXT MESSAGE IN SENDER REQUEST SEQUENCE                     *         
* GET NEXT MESSAGE IN SENDER TRANSRED SEQUENCE                        *         
***********************************************************************         
                                                                                
PSNDNXT  EQU   *                                                                
         CLC   EIOPMID,=AL4(EPQRNRQQ)                                           
         BE    PBDNPQR             PROCESS REPORT BULK DOWNLOAD                 
         CLC   EIOPMID,=AL4(EPQRNIQQ)                                           
         BE    PBDNINQ             PROCESS INQUIRY BULK DOWNLOAD                
         BAS   RE,DCH0                                                          
         EJECT                                                                  
***********************************************************************         
* BULK DOWNLOAD PQ REPORT                                             *         
***********************************************************************         
                                                                                
PBDNPQR  EQU   *                                                                
         CLI   ESSDBLKD,C'Y'                                                    
         BE    *+8                                                              
         BAS   RE,DCH0                                                          
         OI    ESSDHTYP,ESSHDATQ                                                
         XC    ESSDDLN,ESSDDLN                                                  
         NI    ESSDMFF,X'FF'-ESSHLSTQ                                           
         CLI   ESSDBLKO,C'Y'       TEST BULK DATA FILE OPENED                   
         BE    PBDR020                                                          
         MVI   ESSDBLKO,C'Y'       SET BULK DATA FILE OPENED                    
*                                  BUILD PQ REPORT TRANSFER HEADER              
PBDR010  EQU   *                                                                
         GOTO1 =A(FIRSTLIN)                                                     
         BNE   EREX0021                                                         
         CLI   EIOPEOF,C'Y'                                                     
         BE    PBDREOD                                                          
         BAS   RE,BHEADER                                                       
         SR    R1,R1                                                            
         ICM   R1,3,=AL2(EPHDLEN)                                               
         STCM  R1,3,ESSDDLN                                                     
         B     PBDR030                                                          
*                                                                               
PBDR020  EQU   *                   GET NEXT RECORD FROM EXTRACT FILE            
         GOTO1 =A(GETPQLIN)                                                     
         BNE   EREX0022                                                         
         CLI   EIOPEOF,C'Y'                                                     
         BE    PBDREOD                                                          
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,3,ESSDDLN                                                     
*                                  MOVE DATA TO MESSAGE BUFFER                  
PBDR030  LA    RE,ESSDBUFF         CLEAR BUFFER                                 
         ICM   RF,15,=AL4(ESSDATAL-ESSDHDRL)                                    
         XCEF                                                                   
         CLI   COMFLAG,C'N'        TEST FOR DATA COMPRESSION                    
         BE    PBDR040                                                          
         LR    RF,R1                                                            
         GOTO1 ACOMPRES,DMCB,(RF),R,ESSDBUFF                                    
         CLI   0(R1),0                                                          
         BNE   EREX0010                                                         
         ICM   R1,15,0(R1)                                                      
         STCM  R1,3,ESSDDLN                                                     
         B     PBDR100                                                          
*                                                                               
PBDR040  LA    RE,ESSDBUFF                                                      
         LA    R0,R                                                             
         LR    RF,R1                                                            
         MVCL  RE,R0               MOVE DATA                                    
         B     PBDR100                                                          
*                                                                               
PBDR100  AP    ESSDRCNT,=PL8'1'    BUMP BULK DONWLOAD RECORD COUNT              
         B     PBDROK                                                           
*                                                                               
PBDREOD  EQU   *                                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         NI    ESSDMFF,X'FF'-ESSHBDLQ                                           
         MVC   ESSDBUFF(25),=C'END OF BULK DATA TRANSFER'                       
         MVC   ESSDBUFF+25(2),=XL2'0D15'                                        
         LA    R1,27                                                            
         STCM  R1,3,ESSDDLN                                                     
         B     PBDROK                                                           
*                                                                               
PBDROK   B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* BULK DOWNLOAD PQ INQUIRY                                            *         
***********************************************************************         
                                                                                
PBDNINQ  EQU   *                                                                
         CLI   ESSDBLKD,C'Y'                                                    
         BE    *+8                                                              
         BAS   RE,DCH0                                                          
         OI    ESSDHTYP,ESSHDATQ                                                
         XC    ESSDDLN,ESSDDLN                                                  
         NI    ESSDMFF,X'FF'-ESSHLSTQ                                           
         CLI   ESSDBLKO,C'Y'       TEST BULK DATA FILE OPENED                   
         BE    PBDI020                                                          
         MVI   ESSDBLKO,C'Y'       SET BULK DATA FILE OPENED                    
*                                  BUILD PQ REPORT TRANSFER HEADER              
PBDI010  EQU   *                                                                
         XC    EIOPNDX,EIOPNDX                                                  
         BAS   RE,BHEADER                                                       
         SR    R1,R1                                                            
         ICM   R1,3,=AL2(EPHDLEN)                                               
         STCM  R1,3,ESSDDLN                                                     
         B     PBDI030                                                          
*                                                                               
PBDI020  EQU   *                   GET NEXT PQ INDEX ENTRY                      
         GOTO1 =A(GETPQNDX)                                                     
         BNE   EREX0025                                                         
         CLI   EIOPEOF,C'Y'                                                     
         BE    PBDIEOD                                                          
*                                                                               
         GOTO1 =A(FILTNDX)                                                      
         BNE   PBDI020                                                          
*                                                                               
         GOTO1 =A(RNDPAGE),DMCB,0  READ REPORT HEADER RECORD                    
         BNE   PBDI020                                                          
*                                                                               
         GOTO1 =A(GETPQHDR)                                                     
*                                                                               
         GOTO1 =A(SETFORM)         SET REPORT FORMAT CODE                       
         BNE   PBDI020                                                          
*                                                                               
         GOTO1 =A(FILTHDR)                                                      
         BNE   PBDI020                                                          
*                                                                               
         BAS   RE,BINQUIRY                                                      
         SR    R1,R1                                                            
         ICM   R1,3,=AL2(EPIDLEN)                                               
         STCM  R1,3,ESSDDLN                                                     
*                                                                               
PBDI030  EQU   *                                                                
         LA    RE,ESSDBUFF         CLEAR BUFFER                                 
         ICM   RF,15,=AL4(ESSDATAL-ESSDHDRL)                                    
         XCEF                                                                   
*                                                                               
         LA    RE,ESSDBUFF                                                      
         LA    R0,R                                                             
         LR    RF,R1                                                            
         MVCL  RE,R0               MOVE DATA                                    
*                                                                               
PBDI100  AP    ESSDRCNT,=PL8'1'    BUMP BULK DOWNLOAD RECORD COUNT              
         CP    ESSDRCNT,=PL8'1000'                                              
         BL    *+6                                                              
         DC    H'0'                                                             
         B     PBDIOK                                                           
*                                                                               
PBDIEOD  EQU   *                                                                
         OI    ESSDMFF,ESSHLSTQ                                                 
         NI    ESSDMFF,X'FF'-ESSHBDLQ                                           
         MVC   ESSDBUFF(25),=C'END OF BULK DATA TRANSFER'                       
         MVC   ESSDBUFF+25(2),=XL2'0D15'                                        
         LA    R1,27                                                            
         STCM  R1,3,ESSDDLN                                                     
         B     PBDIOK                                                           
*                                                                               
PBDIOK   B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS LAST MESSAGE IN SENDER REQUEST SEQUENCE                     *         
***********************************************************************         
                                                                                
PSNDLST  EQU   *                                                                
         OI    ESSDHTYP,ESSHDATQ+ESSHXTNQ                                       
         BAS   RE,VALPQRM                                                       
         BNE   EREX0024                                                         
         CLC   EIOPMID,=AL4(EPQRNRQQ)                                           
         BE    PSLSPQR             PROCESS REPORT BULK DOWNLOAD                 
         CLC   EIOPMID,=AL4(EPQRNIQQ)                                           
         BE    PSLSINQ             PROCESS INQUIRY BULK DOWNLOAD                
         BAS   RE,DCH0                                                          
         EJECT                                                                  
***********************************************************************         
* PROCESS LAST SENDER MESSAGE FOR PQ REPORT TRANSFER                  *         
***********************************************************************         
                                                                                
PSLSPQR  EQU   *                                                                
         CLI   ESSDMTYP,ESSHINFQ   TEST IF INFO MESSAGE TYPE                    
         BE    PSLRINF                                                          
         CLI   ESSDMTYP,ESSHERRQ   TEST IF ERROR MESSAGE TYPE                   
         BE    PSLRERR                                                          
         BAS   RE,DCH0                                                          
*                                                                               
PSLRERR  EQU   *                   PROCESS ERROR MESSAGE TYPE                   
         MVC   ESSDCON,EIOPCON                                                  
         MVC   P(27),=CL27'EIOPQR ERROR MESSAGE CODE: '                         
         MVC   P+28(6),ESSDCON                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PNOTPQR             NOTIFY PQ REPORT SENT                        
*                                                                               
PSLRINF  EQU   *                   PROCESS INFO MESSAGE TYPE                    
*                                                                               
*                                  CHECK BULK DOWNLOAD OK                       
PSLRCBD  EQU   *                                                                
         CLI   FTPFLAG,C'Y'                                                     
         BE    PNOTPQR             PROCESS POST PQ REPORT SENT ??               
*                                  TEST 'FILE ALREADY RECEIVED'                 
         CLC   EIOPCON,=CL6'000999'  MESSAGE RETURNED (NO DOWNLOAD)             
         BE    PNOTPQR             NOTIFY PQ REPORT SENT                        
*                                                                               
         MVC   P(42),=CL42'LINE NUMBER:          DOWNLOADED: '                  
         EDIT  ESSDRNUM,(8,P+13),ZERO=NOBLANK,FILL=0                            
         EDIT  ESSDRCNT,(8,P+34),ZERO=NOBLANK,FILL=0                            
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                  COMPARE FILE RECORD NUMBER WITH              
         CP    ESSDRCNT,ESSDRNUM   DOWNLOADED RECORD COUNT                      
         BE    PNOTPQR             NOTIFY PQ REPORT SENT                        
*                                  COMPARE FILE RECORD NUMBER WITH              
         MVC   P(40),=CL40'ERROR FILE BULK DOWNLOAD RECORD COUNT'               
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PNOTPQR             NOTIFY PQ REPORT SENT                        
         EJECT                                                                  
***********************************************************************         
* PROCESS LAST SENDER MESAGE FOR PQ INQUIRY TRANSFER                  *         
***********************************************************************         
                                                                                
PSLSINQ  EQU   *                                                                
         CLI   ESSDMTYP,ESSHINFQ   TEST IF INFO MESSAGE TYPE                    
         BE    PSLIINF                                                          
         CLI   ESSDMTYP,ESSHERRQ   TEST IF ERROR MESSAGE TYPE                   
         BE    PSLIERR                                                          
         BAS   RE,DCH0                                                          
*                                                                               
PSLIERR  EQU   *                   PROCESS ERROR MESSAGE TYPE                   
         MVC   ESSDCON,EIOPCON                                                  
         MVC   P(27),=CL27'EIOPQR ERROR MESSAGE CODE: '                         
         MVC   P+28(6),ESSDCON                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PNOTINQ             NOTIFY PQ INQUIRY SENT                       
*                                                                               
PSLIINF  EQU   *                   PROCESS INFO MESSAGE TYPE                    
*                                                                               
*                                  CHECK BULK DOWNLOAD OK                       
PSLICBD  EQU   *                                                                
         CLI   FTPFLAG,C'Y'                                                     
         BE    PNOTINQ             PROCESS POST PQ REPORT SENT ??               
*                                  TEST 'FILE ALREADY RECEIVED'                 
         CLC   EIOPCON,=CL6'000999'  MESSAGE RETURNED (NO DOWNLOAD)             
         BE    PNOTINQ             NOTIFY PQ INQUIRY SENT                       
*                                                                               
         MVC   P(42),=CL42'LINE NUMBER:          DOWNLOADED: '                  
         EDIT  ESSDRNUM,(8,P+13),ZERO=NOBLANK,FILL=0                            
         EDIT  ESSDRCNT,(8,P+34),ZERO=NOBLANK,FILL=0                            
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     PNOTINQ             NOTIFY PQ INQURIY SENT                       
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SENDER TRANSFER NOTIFICATION                      *         
***********************************************************************         
                                                                                
PNOTPQR  EQU   *                                                                
         MVC   USERID,SPACES                                                    
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         CLC   USERID,SPACES                                                    
         BNE   *+8                                                              
         BAS   RE,DCH0                                                          
         MVC   EIOPUID,USERID                                                   
         GOTO1 =A(READID)                                                       
         BNE   EREX0018                                                         
         MVC   EIOPUIDN,USERIDNO                                                
*                                                                               
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         XC    ESSRKEY,ESSRKEY     CLEAR KEY AREA                               
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQARHIQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         CLC   ESSRKEY(ESSRDATE-ESSRKEY),ESSRKEY                                
         BNE   EREX0011                                                         
*                                                                               
         LA    R7,ESSRFEL                                                       
         XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQRELQ                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELGQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    PNOR020                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
PNOR010  XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQRELQ                                                 
         ICM   R7,15,ALSTRQEL                                                   
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELNQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    PNOR020                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
PNOR020  MVC   ALSTRQEL,12(R1)                                                  
         ICM   R7,15,ALSTRQEL                                                   
         USING ERPQRD,R7                                                        
         CLC   EIOPUIDN,ERPQRSID                                                
         BNE   PNOR010                                                          
         CLC   EIOPSID,ERPQRSUB                                                 
         BNE   PNOR010                                                          
         CLC   EIOPNUMB,ERPQRRNM                                                
         BNE   PNOR010                                                          
         TM    ERPQRSTA,ERPQRWTQ    TEST WAITING TO BE SENT                     
         BZ    EREX0012                                                         
         NI    ERPQRSTA,X'FF'-ERPQRWTQ                                          
         OI    ERPQRSTA,ERPQRSNQ    SET NOTIFICATION SENT TO ESS                
*                                   WRITE RECORD BACK TO REQUEST TABLE          
         GOTO1 AESSRQS,DMCB,=AL4(ERQAWRTQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'PQ REPORT SENT:         '                            
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
*                                                                               
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ INQUIRY SENDER TRANSFER NOTIFICATION                     *         
***********************************************************************         
                                                                                
PNOTINQ  EQU   *                                                                
         MVC   USERID,SPACES                                                    
         MVC   USERID(L'ESSDUID),ESSDUID                                        
         CLC   USERID,SPACES                                                    
         BNE   *+8                                                              
         BAS   RE,DCH0                                                          
         MVC   EIOPUID,USERID                                                   
         GOTO1 =A(READID)                                                       
         BNE   EREX0018                                                         
         MVC   EIOPUIDN,USERIDNO                                                
*                                                                               
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         XC    ESSRKEY,ESSRKEY     CLEAR KEY AREA                               
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQARHIQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
         CLC   ESSRKEY(ESSRDATE-ESSRKEY),ESSRKEY                                
         BNE   EREX0011                                                         
*                                                                               
         LA    R7,ESSRFEL                                                       
         XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQIELQ                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELGQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    PNOI010                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
PNOI010  MVC   ALSTRQEL,12(R1)                                                  
         ICM   R7,15,ALSTRQEL                                                   
         USING ERPQID,R7                                                        
         TM    ERPQISTA,ERPQIWTQ    TEST WAITING TO BE SENT                     
         BZ    EREX0012                                                         
         NI    ERPQISTA,X'FF'-ERPQIWTQ                                          
         OI    ERPQISTA,ERPQISNQ    SET NOTIFICATION SENT TO ESS                
*                                   WRITE RECORD BACK TO REQUEST TABLE          
         GOTO1 AESSRQS,DMCB,=AL4(ERQAWRTQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'PQ INQUIRY SENT:        '                            
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         B     PDELREQ                                                          
         EJECT                                                                  
***********************************************************************         
* DELETE PQR ENTRY IN REQUEST TABLE                                   *         
***********************************************************************         
                                                                                
PDELREQ  EQU   *                                                                
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         XC    ESSRKEY,ESSRKEY     CLEAR KEY AREA                               
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         MVC   ESSRREF,ESSDREFB                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQADELQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    *+8                                                              
         BAS   RE,RQSERROR                                                      
*                                                                               
         MVC   P,SPACES                                                         
         MVC   P(24),=CL24'DELETED PQR REQUEST:    '                            
         BAS   RE,FORMOPM                                                       
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
*                                                                               
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS PQ REPORT SUB-SYSTEM ERROR MESSAGE                          *         
***********************************************************************         
                                                                                
PROCERR  EQU   *                                                                
         MVC   ESSDCON,EIOPCON                                                  
         MVC   P(27),=CL27'EIOPQR ERROR MESSAGE CODE: '                         
         MVC   P+28(6),ESSDCON                                                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
*                                                                               
         B     EPQROK                                                           
         EJECT                                                                  
***********************************************************************         
* READ ESS REQUEST QUEUE TO FIND PENDING PQ SUB SYSTEM REQUESTS       *         
***********************************************************************         
                                                                                
READREQ  NTR1                                                                   
         LA    R6,REQAREA                                                       
         USING ESSREQD,R6                                                       
         XC    ESSRKEY,ESSRKEY     CLEAR KEY AREA                               
         MVC   ESSRTYP,=AL3(ESSRTPQR)                                           
         MVC   ESSRIDN,ESSIDNUM                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQARHIQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    RREQ020                                                          
         CLI   0(R1),ERQREOFQ                                                   
         BNE   RREQ020             EXIT AT END OF TABLE                         
         B     RREQNO              WITH NO REPORT FOUND                         
*                                  CHECK THIS MESSAGE ID/ESS ID                 
RREQ020  CLC   ESSRTYP,=AL3(ESSHPQRQ)                                           
         BNE   RREQ030                                                          
         CLC   ESSRIDN,ESSIDNUM                                                 
         BNE   RREQ030                                                          
         B     RREQ040                                                          
*                                                                               
RREQ030  EQU   *                   GET NEXT RECORD FROM TSAR                    
         GOTO1 AESSRQS,DMCB,=AL4(ERQARSQQ),(R6)                                 
         CLI   0(R1),ERQROKQ                                                    
         BE    RREQ020                                                          
         CLI   0(R1),ERQREOFQ                                                   
         BE    RREQNO              EXIT AT END OF TABLE                         
         BAS   RE,RQSERROR                                                      
*                                                                               
RREQ040  LA    R7,ESSRFEL                                                       
         XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQRELQ                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELGQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    RREQ050                                                          
         CLI   0(R1),ERQREORQ                                                   
         BE    RREQ100                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
RREQ042  XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQRELQ                                                 
         ICM   R7,15,ALSTRQEL                                                   
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELNQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    RREQ050                                                          
         CLI   0(R1),ERQREORQ                                                   
         BE    RREQ030                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
RREQ050  MVC   ALSTRQEL,12(R1)                                                  
         LA    R7,RQSWORK                                                       
         USING ERPQRD,R7                                                        
         TM    ERPQRSTA,ERPQRWTQ    TEST WAITING TO BE SENT                     
         BZ    RREQ042                                                          
         MVC   EIOPMID,=AL4(EPQRNRQQ)                                           
         MVC   EIOPUIDN,ERPQRSID                                                
         MVC   EIOPSID,ERPQRSUB                                                 
         MVC   EIOPNUMB,ERPQRRNM                                                
         MVC   ESSDPRI,ERPQRPRI                                                 
         GOTO1 =A(INITPQR)                                                      
         BNE   RREQ042                                                          
         CLC   EIOPDTMC,ERPQRDTC                                                
         BNE   RREQ042                                                          
*                                                                               
         MVC   ESSID(3),=CL3'ESS'                                               
         EDIT  ESSIDNUM,(5,ESSID+3),FILL=0                                      
         GOTO1 =A(GETPQUID),DMCB,EIOPUIDN,ESSDUID                               
         EDIT  EIOPNUMB,(5,EIOPNUM),FILL=0,ZERO=NOBLANK                         
         MVC   ESSDREFB,ESSRREF                                                 
         ICM   RF,15,ESSDREFB                                                   
         CVD   RF,DUB                                                           
         UNPK  ESSDREF,DUB                                                      
         B     RREQOK                                                           
*                                  SEARCH FOR PQ INQUIRY ELEMENT                
RREQ100  LA    R7,ESSRFEL                                                       
         XC    RQSWORK,RQSWORK                                                  
         MVI   RQSWORK,ERPQIELQ                                                 
         GOTO1 AESSRQS,DMCB,=AL4(ERQAELGQ),(R6),RQSWORK,(R7)                    
         CLI   0(R1),ERQROKQ                                                    
         BE    RREQ110                                                          
         BAS   RE,RQSERROR                                                      
*                                                                               
RREQ110  MVC   ALSTIQEL,12(R1)                                                  
         LA    R7,RQSWORK                                                       
         USING ERPQID,R7                                                        
         TM    ERPQISTA,ERPQIWTQ    TEST WAITING TO BE SENT                     
         BZ    RREQ030                                                          
         MVC   EIOPMID,=AL4(EPQRNIQQ)                                           
         MVC   EIOPUIDN,ERPQISID                                                
         MVC   EIOPSID,ERPQISUB                                                 
         MVC   EIOPNUMB,ERPQIRNM                                                
         MVC   EIOPFMAT,ERPQIFMT                                                
         GOTO1 =A(INITPQF)                                                      
         BNE   RREQ030                                                          
*                                                                               
         MVC   ESSID(3),=CL3'ESS'                                               
         EDIT  ESSIDNUM,(5,ESSID+3),FILL=0                                      
         LA    RE,EIOPUIDN                                                      
         LA    RE,ESSDUID                                                       
         GOTO1 =A(GETPQUID),DMCB,EIOPUIDN,ESSDUID                               
         EDIT  EIOPNUMB,(5,EIOPNUM),FILL=0,ZERO=NOBLANK                         
         MVC   ESSDREFB,ESSRREF                                                 
         ICM   RF,15,ESSDREFB                                                   
         CVD   RF,DUB                                                           
         UNPK  ESSDREF,DUB                                                      
         B     RREQOK                                                           
*                                                                               
RREQNO   B     NO                                                               
RREQOK   B     YES                                                              
         DROP  R6,R7                                                            
         EJECT                                                                  
***********************************************************************         
* FORMAT FILE TRANSFER OPERATOR MESSAGE                               *         
***********************************************************************         
                                                                                
FORMOPM  NTR1                                                                   
         LA    R6,P+24                                                          
         MVC   0(L'ESSNAME,R6),ESSNAME                                          
         LA    R6,L'ESSNAME(R6)                                                 
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         EDIT  ESSDREFB,(11,0(R6)),ZERO=NOBLANK,FILL=0,MINUS=NO                 
         LA    R6,11(R6)                                                        
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         MVC   0(L'EIOPUID,R6),EIOPUID                                          
         LA    R6,L'EIOPUID(R6)                                                 
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         MVC   0(L'EIOPSID,R6),EIOPSID                                          
         LA    R6,L'EIOPSID(R6)                                                 
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         MVC   0(L'EIOPNUM,R6),EIOPNUM                                          
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* SAVE REQUEST IN RECOVERY FILE                                       *         
***********************************************************************         
                                                                                
SAVEREQ  NTR1                                                                   
*        MVC   VSAMKEY,REQAREA                                                  
*        GOTO1 AVSAMIO,DMCB,(X'00',=C'ADD'),=C'VSAMFIL',VSAMKEY,REQAREA         
*        MVC   VSAMERR,DMCB+8                                                   
*        CLI   VSAMERR,X'21'       OUT OF SEQUENCE KEY                          
*        BNE   *+6                                                              
*        DC    H'0'                                                             
*        TM    VSAMERR,X'20'       DUPLICATE KEY                                
*        BZ    *+6                                                              
*        DC    H'0'                                                             
*        TM    VSAMERR,X'80'       END OF FILE                                  
*        BZ    *+6                                                              
*        DC    H'0'                                                             
*        TM    VSAMERR,X'40'       DISK ERROR                                   
*        BZ    *+6                                                              
*        DC    H'0'                                                             
*        CLI   VSAMERR,0           OTHER ERROR                                  
*        BE    *+6                                                              
*        DC    H'0'                                                             
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* BUILD PQ REPORT EXTRACT HEADER                                      *         
***********************************************************************         
                                                                                
BHEADER  NTR1                                                                   
         XC    R,R                                                              
         LA    R6,R                                                             
         MVC   R,SPACES                                                         
         USING EPQRHDRD,R6                                                      
         MVC   EPHEYE,=CL4'PQRH'                                                
         MVI   EPHDATE-1,C';'                                                   
         BAS   RE,GETDATM                                                       
*                                                                               
         MVC   EPHDATE,DATEN                                                    
         MVI   EPHTIME-1,C';'                                                   
         MVC   EPHTIME,TIMEN                                                    
*                                                                               
         MVI   EPHESSID-1,C';'                                                  
         MVC   EPHESSID,ESSNAME                                                 
         MVI   EPHRQREF-1,C';'                                                  
         MVC   EPHRQREF,ESSDREF                                                 
*                                                                               
         MVI   EPHLEV-1,C';'                                                    
         MVC   EPHLEV,=CL3'001'                                                 
         MVI   EPHVER-1,C';'                                                    
         MVC   EPHVER,=CL3'001'                                                 
*                                                                               
         MVI   EPHUSID-1,C';'                                                   
         MVC   EPHUSID,ESSDUID                                                  
         MVI   EPHSUBID-1,C';'                                                  
         MVC   EPHSUBID,EIOPSID                                                 
         MVI   EPHREFNM-1,C';'                                                  
         MVC   EPHREFNM,EIOPNUM                                                 
*                                                                               
         MVI   EPHSPAGE-1,C';'                                                  
         MVC   EPHSPAGE,EIOPSPAG                                                
         MVI   EPHEPAGE-1,C';'                                                  
         MVC   EPHEPAGE,EIOPEPAG                                                
         MVI   EPHCLASS-1,C';'                                                  
         MVC   EPHCLASS,EIOPCLAS                                                
         MVI   EPHDESC-1,C';'                                                   
         MVC   EPHDESC,EIOPDESC                                                 
         MVI   EPHNLINE-1,C';'                                                  
         MVC   EPHNLINE,EIOPNLIN                                                
         MVI   EPHNPAGE-1,C';'                                                  
         MVC   EPHNPAGE,EIOPNPAG                                                
         MVI   EPHCPL-1,C';'                                                    
         MVC   EPHCPL,EIOPCPL                                                   
         MVI   EPHLPP-1,C';'                                                    
         MVC   EPHLPP,EIOPLPP                                                   
         MVI   EPHDTMC-1,C';'                                                   
         MVC   EPHDTMC,EIOPDTMC                                                 
         MVI   EPHDTME-1,C';'                                                   
         MVC   EPHDTME,EIOPDTME                                                 
         MVI   EPHRPSWD-1,C';'                                                  
         MVC   EPHRPSWD,EIOPRPWD                                                
         MVI   EPHFRMS-1,C';'                                                   
         MVC   EPHFRMS,EIOPFRMS                                                 
         MVI   EPHCHRS-1,C';'                                                   
         MVC   EPHCHRS,EIOPCHRS                                                 
         MVI   EPHCPYS-1,C';'                                                   
         MVC   EPHCPYS,EIOPCPYS                                                 
         MVI   EPHATTR-1,C';'                                                   
         MVC   EPHATTR,EIOPATTR                                                 
         MVI   EPHSTAT-1,C';'                                                   
         MVC   EPHSTAT,EIOPSTAT                                                 
         MVI   EPHFMAT-1,C';'                                                   
         MVC   EPHFMAT,EIOPFMAT                                                 
         MVI   EPHX,C';'                                                        
         MVI   EPHX+1,C';'                                                      
         MVC   EPHTERM,=XL2'0D15'                                               
         XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD PQ INQUIRY RECORD                                             *         
***********************************************************************         
                                                                                
BINQUIRY NTR1                                                                   
         XC    R,R                                                              
         LA    R6,R                                                             
         MVC   R,SPACES                                                         
         USING EPQRINQD,R6                                                      
         MVC   EPISUBID,EIOPSID                                                 
         MVC   EPIREFNM,EIOPNUM                                                 
         MVC   EPICLASS,EIOPCLAS                                                
         MVC   EPIDESC,EIOPDESC                                                 
         MVC   EPINLINE,EIOPNLIN                                                
         MVC   EPINPAGE,EIOPNPAG                                                
         MVC   EPICPL,EIOPCPL                                                   
         MVC   EPILPP,EIOPLPP                                                   
         MVC   EPIDTMC,EIOPDTMC                                                 
         MVC   EPIDTME,EIOPDTME                                                 
         MVC   EPIRPSWD,EIOPRPWD                                                
         MVC   EPIFRMS,EIOPFRMS                                                 
         MVC   EPICHRS,EIOPCHRS                                                 
         MVC   EPICPYS,EIOPCPYS                                                 
         MVC   EPIATTR,EIOPATTR                                                 
         MVC   EPISTAT,EIOPSTAT                                                 
         MVC   EPIFMAT,EIOPFMAT                                                 
         MVC   EPITERM,=XL2'0D15'                                               
         XIT1                                                                   
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* GET DATE AND TIME                                                   *         
***********************************************************************         
                                                                                
GETDATM  NTR1                                                                   
         SR    R0,R0                                                            
         SR    R1,R1                                                            
*                                  GET THE CURRENT TIME IN BINARY               
GDTM010  TIME  BIN                                                              
         LTR   R0,R0                                                            
         BZ    GDTM010             BAD RETURN FROM MACRO                        
         ST    R0,TIMENB                                                        
*                                                                               
         SR    R0,R0                                                            
         SR    R1,R1                                                            
*                                  GET THE CURRENT MVS DATE/TIME                
GDTM020  TIME                                                                   
         LTR   R0,R0                                                            
         BZ    GDTM020             BAD RETURN FROM MACRO                        
         ST    R0,MVSTIME                                                       
         ST    R1,MVSDATE                                                       
*                                                                               
*                                  GET DATE NOW IN VARIOUS FORMATS              
         MVC   CENTURY,=CL2'19'    HARD CODE CENTURY VALUE                      
         GOTO1 ADATCON,DMCB,(5,0),(0,DATEN+2)                                   
         MVC   DATEN(2),CENTURY                                                 
         GOTO1 ADATCON,DMCB,(5,0),(2,DATENC)                                    
         GOTO1 ADATCON,DMCB,(5,0),(3,DATENB)                                    
*                                                                               
         L     R1,MVSTIME                                                       
         STCM  R1,15,TIMEND                                                     
*                                  CONVERT TIME NOW TO EBCDIC                   
         SRL   R1,28                                                            
         STC   R1,TIMEN                                                         
         OI    TIMEN,X'F0'                                                      
         L     R1,MVSTIME                                                       
         SRL   R1,24                                                            
         STC   R1,TIMEN+1                                                       
         OI    TIMEN+1,X'F0'                                                    
         L     R1,MVSTIME                                                       
         SRL   R1,20                                                            
         STC   R1,TIMEN+2                                                       
         OI    TIMEN+2,X'F0'                                                    
         L     R1,MVSTIME                                                       
         SRL   R1,16                                                            
         STC   R1,TIMEN+3                                                       
         OI    TIMEN+3,X'F0'                                                    
         L     R1,MVSTIME                                                       
         SRL   R1,12                                                            
         STC   R1,TIMEN+4                                                       
         OI    TIMEN+4,X'F0'                                                    
         L     R1,MVSTIME                                                       
         SRL   R1,8                                                             
         STC   R1,TIMEN+5                                                       
         OI    TIMEN+5,X'F0'                                                    
         L     R1,MVSTIME                                                       
         SRL   R1,4                                                             
         STC   R1,TIMEN+6                                                       
         OI    TIMEN+6,X'F0'                                                    
         L     R1,MVSTIME                                                       
         STC   R1,TIMEN+7                                                       
         OI    TIMEN+7,X'F0'                                                    
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* BUILD PQ REPORT EXTRACT TRAILER                                     *         
***********************************************************************         
                                                                                
BTRAIL   NTR1                                                                   
         XC    R,R                                                              
         MVC   R(40),=CL40'PQ REPORT TRAILER'                                   
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE DATA                          *         
***********************************************************************         
                                                                                
VALPQRM  NTR1                                                                   
         MVI   ERRFLAG,C'N'                                                     
         XC    EIOPBREP,EIOPBREP                                                
*                                  VALIDATE MESSAGE ID                          
         BAS   RE,VALPMID                                                       
         BNE   VPQMER1                                                          
*                                                                               
         CLC   EIOPMID,=AL4(EPQRREQQ)                                           
         BE    VPQM010                                                          
         CLC   EIOPMID,=AL4(EPQRSONQ)                                           
         BE    VPQM030                                                          
         B     VPQM020                                                          
*                                                                               
VPQM010  EQU   *                                                                
         BAS   RE,VALPBLK          VALIDATE MULTIPLE REPORT BLOCK               
         BNE   VPQMER0                                                          
         OC    EIOPBREP,EIOPBREP                                                
         BZ    VPQM020                                                          
         B     VPQMOK                                                           
*                                  VALIDATE CONDITION CODE                      
VPQM020  BAS   RE,VALPCON                                                       
         BNE   VPQMER3                                                          
*                                                                               
         BAS   RE,VALPSID          VALIDATE REPORT SUB ID                       
         BNE   VPQMER4                                                          
*                                                                               
         BAS   RE,VALPNUM          VALIDATE REPORT SEQUENCE NUMBER              
         BNE   VPQMER5                                                          
         B     VPQMOK                                                           
*                                  VALIDATE SOON REQUEST PARAMETERS             
VPQM030  BAS   RE,VALPSRQ                                                       
         BNE   VPQMER6                                                          
         B     VPQMOK                                                           
*                                                                               
         CLI   ERRFLAG,C'Y'                                                     
         BE    VPQMER7                                                          
         B     VPQMOK                                                           
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER0  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 0'             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER1  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 1'             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER2  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 2'             
         DC    H'0'                                                             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER3  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 3'             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER4  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 4'             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER5  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 5'             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER6  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 6'             
         B     VPQMERR                                                          
*                                  PQ REPORT MESSAGE ERROR                      
VPQMER7  EQU   *                                                                
         MVC   P(40),=CL40'INVALID PQ REPORT SYSTEM MESSAGE DATA 7'             
         B     VPQMERR                                                          
*                                                                               
VPQMERR  EQU   *                                                                
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   WTOMSG+26(2),WTOALARM                                            
         GOTO1 AESSWTO,DMCB,X'FF000001',(L'WTOMSG,WTOMSG)                       
         MVC   WTOMSG+26(2),SPACES                                              
         B     VPQMNO                                                           
*                                                                               
VPQMNO   B     NO                                                               
VPQMOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE ID                            *         
***********************************************************************         
                                                                                
VALPMID  NTR1                                                                   
*                                                                               
         MVC   EIOPMID,EPQRMID                                                  
*                                                                               
         CLC   EIOPMID,=AL4(EPQRINQQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRINSQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRREQQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRRCVQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRNRQQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRNSOQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRNSHQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRNIQQ)                                           
         BE    VPMIOK                                                           
         CLC   EIOPMID,=AL4(EPQRSONQ)                                           
         BE    VPMIOK                                                           
         B     VPMIER                                                           
*                                                                               
VPMIER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE ID'                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPMINO                                                           
*                                                                               
VPMINO   B     NO                                                               
VPMIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQR SUB-SYSTEM CONDITION CODE                              *         
***********************************************************************         
                                                                                
VALPCON  NTR1                                                                   
*                                                                               
         MVC   EIOPCON,EPQRCON                                                  
         B     VPCOOK                                                           
*                                                                               
VPCOER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQR MESSAGE CONDITION CODE'                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPCONO                                                           
*                                                                               
VPCONO   B     NO                                                               
VPCOOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE REPORT SUB ID                 *         
***********************************************************************         
                                                                                
VALPSID  NTR1                                                                   
*                                                                               
         MVC   EIOPSID,EPQRSID                                                  
         B     VPSIOK                                                           
*                                                                               
VPSIER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE SUB ID'                    
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPSINO                                                           
*                                                                               
VPSINO   B     NO                                                               
VPSIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE REPORT SEQUENCE NUMBER        *         
***********************************************************************         
                                                                                
VALPNUM  NTR1                                                                   
*                                                                               
         XC    EIOPNUMB,EIOPNUMB                                                
         MVC   EIOPNUM,SPACES                                                   
*                                                                               
         CLC   EPQRNUM,SPACES                                                   
         BE    VPNU010                                                          
         OC    EPQRNUM,EPQRNUM                                                  
         BZ    VPNU010                                                          
         GOTO1 ANUMVAL,DMCB,EPQRNUM,(X'00',5)                                   
         CLI   0(R1),0                                                          
         BNE   VPNUER                                                           
         L     R1,4(R1)                                                         
         STCM  R1,3,EIOPNUMB                                                    
         MVC   EIOPNUM,EPQRNUM                                                  
         B     VPNUOK                                                           
*                                                                               
VPNU010  EQU   *                                                                
         CLC   EIOPMID,=AL4(EPQRINQQ)                                           
         BE    VPNUOK                                                           
         CLC   EIOPMID,=AL4(EPQRNIQQ)                                           
         BE    VPNUOK                                                           
         B     VPNUER                                                           
*                                                                               
VPNUER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQR MESSAGE SEQUENCE NUMBER'                 
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPNUNO                                                           
*                                                                               
VPNUNO   B     NO                                                               
VPNUOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE MULTIPLE REPORT BLOCK         *         
***********************************************************************         
                                                                                
VALPBLK  NTR1                                                                   
*                                                                               
         LA    R0,EIOPBLCK                                                      
         ICM   R1,15,=AL4(EIOPBLEN*EIOPBMAX)                                    
         LA    RE,*                                                             
         ICM   RF,15,=XL4'00000000'                                             
         MVCL  R0,RE                                                            
*                                                                               
         CLC   EPQRBREP,SPACES                                                  
         BE    VPBLOK                                                           
         OC    EPQRBREP,EPQRBREP                                                
         BZ    VPBLOK                                                           
         CLC   EPQRBREP,=CL5'00000'                                             
         BE    VPBLOK                                                           
         GOTO1 ANUMVAL,DMCB,EPQRBREP,(X'00',5)                                  
         CLI   0(R1),0                                                          
         BNE   VPBLER                                                           
         L     R1,4(R1)                                                         
         STCM  R1,3,EIOPBREP                                                    
         SR    R0,R0                                                            
         LA    RF,EIOPBLEN                                                      
         MR    R0,RF                                                            
         LA    R0,EIOPBLCK                                                      
         LA    RE,EPQRBLCK                                                      
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     VPBLOK                                                           
*                                                                               
VPBLER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQR MESSAGE REPORT BLOCK'                    
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPBLNO                                                           
*                                                                               
VPBLNO   B     NO                                                               
VPBLOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE USER ID VALUE                 *         
***********************************************************************         
                                                                                
VALPUID  NTR1                                                                   
*                                                                               
         MVC   EIOPUID,EPQRVAL                                                  
         OC    EIOPUID,SPACES                                                   
         OI    EIOPREQC,RCUID                                                   
         B     VPUIOK                                                           
*                                                                               
VPUIER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE USER ID'                   
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPUINO                                                           
*                                                                               
VPUINO   B     NO                                                               
VPUIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE SYSTEM VALUE                  *         
***********************************************************************         
                                                                                
VALPSYS  NTR1                                                                   
*                                                                               
         MVC   EIOPSYS,EPQRVAL                                                  
         OI    EIOPREQC,RCSYS                                                   
         B     VPSYOK                                                           
*                                                                               
VPSYER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE SYSTEM'                    
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPSYNO                                                           
*                                                                               
VPSYNO   B     NO                                                               
VPSYOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE PROGRAM VALUE                 *         
***********************************************************************         
                                                                                
VALPPGM  NTR1                                                                   
*                                                                               
         MVC   EIOPPGM,EPQRVAL                                                  
         OI    EIOPREQC,RCPGM                                                   
         B     VPPGOK                                                           
*                                                                               
VPPGER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE PROGRAM'                   
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPPGNO                                                           
*                                                                               
VPPGNO   B     NO                                                               
VPPGOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE PRIORITY VALUE                *         
***********************************************************************         
                                                                                
VALPPRY  NTR1                                                                   
*                                                                               
         MVC   EIOPPRY,EPQRVAL                                                  
         B     VPPGOK                                                           
*                                                                               
VPPRER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE PRIORITY'                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPPRNO                                                           
*                                                                               
VPPRNO   B     NO                                                               
VPPROK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE JOB ID VALUE                  *         
***********************************************************************         
                                                                                
VALPEID  NTR1                                                                   
*                                                                               
         ICM   RF,15,EPQRVAL                                                    
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPJID,DUB                                                      
         OI    EIOPREQC,RCEID                                                   
         B     VPEIOK                                                           
*                                                                               
VPEIER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE JOB ID'                    
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPEINO                                                           
*                                                                               
VPEINO   B     NO                                                               
VPEIOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE SOON REQUEST DATA             *         
***********************************************************************         
                                                                                
VALPSRQ  NTR1                                                                   
*                                                                               
         BAS   RE,VALPRCF                                                       
         BNE   VPSRER                                                           
         CLI   EIOPRCF,EPQRRCFR                                                 
         BE    VPSR010                                                          
         CLI   EIOPRCF,EPQRRCFF                                                 
         BE    VPSR020                                                          
         B     VPSRER                                                           
*                                                                               
VPSR010  BAS   RE,VALPRQT                                                       
         BNE   VPSRER                                                           
         B     VPSROK                                                           
*                                                                               
VPSR020  BAS   RE,VALPFLD                                                       
         BNE   VPSRER                                                           
         B     VPSROK                                                           
*                                                                               
VPSRER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE SOON REQUEST'              
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPSRNO                                                           
*                                                                               
VPSRNO   B     NO                                                               
VPSROK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE REQUEST DATA CARD FORMAT      *         
***********************************************************************         
                                                                                
VALPRCF  NTR1                                                                   
*                                                                               
         CLI   EPQRRCF,EPQRRCFR                                                 
         BE    VPRC010                                                          
         CLI   EPQRRCF,EPQRRCFF                                                 
         BNE   VPRCER                                                           
*                                                                               
VPRC010  MVC   EIOPRCF,EPQRRCF                                                  
         B     VPRCOK                                                           
*                                                                               
VPRCER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT DATA CARD FORMAT'                  
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPPRNO                                                           
*                                                                               
VPRCNO   B     NO                                                               
VPRCOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE REQUEST CARD VALUE            *         
***********************************************************************         
                                                                                
VALPRQT  NTR1                                                                   
*                                                                               
         MVI   EIOPREQC,RCDATA+RCRQT                                            
*                                                                               
         LA    RF,EIOPCRDS         1ST REQ CARD                                 
         ZICM  RE,EIOPCCNT,1       # CARDS USED                                 
         BZ    *+12                NO CARDS USED                                
         LA    RF,L'EIOPCRDS(RF)   GET FIRST BLANK CARD                         
         BCT   RE,*-4                                                           
         SR    R6,R6                                                            
         ICM   R6,3,ESSDDLN                                                     
         LA    R0,EPQRVAL-EPQRDAT                                               
         SR    R6,R0                                                            
         LA    R7,EPQRVAL                                                       
*                                                                               
VPRQ010  CH    R6,=H'80'                                                        
         BNH   *+12                                                             
         LA    R1,80                                                            
         B     *+6                                                              
         LR    R1,R6                                                            
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R7)       MOVE DATA TO CARD                            
         IC    RE,EIOPCCNT         CARDCT++                                     
         LA    RE,1(RE)                                                         
         STC   RE,EIOPCCNT                                                      
         CH    R6,=H'80'                                                        
         BNH   VPRQOK                                                           
         LA    RF,80(RF)                                                        
         LA    R7,80(R7)                                                        
         SH    R6,=H'80'                                                        
         B     VPRQ010                                                          
*                                                                               
VPRQER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE REQUEST CARD'              
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPRQNO                                                           
*                                                                               
VPRQNO   B     NO                                                               
VPRQOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE PQ REPORT SUB-SYSTEM MESSAGE FIELD FORMAT REQUEST VALUE    *         
***********************************************************************         
                                                                                
VALPFLD  NTR1                                                                   
*                                                                               
         MVI   EIOPREQC,RCDATA+RCFLD                                            
*                                                                               
         GOTO1 =A(FIELD),DMCB,ESSDDLN,EPQRVAL                                   
         B     VPFLOK                                                           
*                                                                               
VPFLER   EQU   *                                                                
         MVI   ERRFLAG,C'Y'                                                     
         MVC   P(40),=CL40'INVALID PQ REPORT MESSAGE FIELD REQUEST'             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     VPFLNO                                                           
*                                                                               
VPFLNO   B     NO                                                               
VPFLOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* VALIDATE ALPHA NUMERIC FIELD                                        *         
***********************************************************************         
                                                                                
VALALNUM NTR1                                                                   
         LM    R2,R3,0(R1)                                                      
         LTR   R3,R3                                                            
         BZ    VALNOK                                                           
*                                                                               
VALN010  CLI   0(R2),X'81'                                                      
         BL    VALNNO                                                           
         CLI   0(R2),X'A9'                                                      
         BNH   VALN030                                                          
         CLI   0(R2),C'A'                                                       
         BL    VALNNO                                                           
         CLI   0(R2),C'Z'                                                       
         BNH   VALN030                                                          
         CLI   0(R2),C'0'                                                       
         BL    VALNNO                                                           
         CLI   0(R2),C'9'                                                       
         BH    VALNNO                                                           
VALN030  LA    R2,1(R2)                                                         
         BCT   R3,VALN010                                                       
*                                                                               
VALNOK   B     YES                                                              
VALNNO   B     NO                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD PQ REPORT SUB-SYSTEM RETURN MESSAGE                           *         
***********************************************************************         
                                                                                
BLDPQRM  NTR1                                                                   
         MVC   EPQRMID,EIOPMID                                                  
         MVC   EPQRCON,EIOPCON                                                  
         MVC   EPQRSID,EIOPSID                                                  
         MVC   EPQRNUM,EIOPNUM                                                  
         MVC   EPQRSPAG,EIOPSPAG                                                
         MVC   EPQREPAG,EIOPEPAG                                                
         MVC   EPQRCLAS,EIOPCLAS                                                
         MVC   EPQRDESC,EIOPDESC                                                
         MVC   EPQRNPAG,EIOPNPAG                                                
         MVC   EPQRNLIN,EIOPNLIN                                                
         MVC   EPQRCPL,EIOPCPL                                                  
         MVC   EPQRLPP,EIOPLPP                                                  
         MVC   EPQRDTMC,EIOPDTMC                                                
         MVC   EPQRDTME,EIOPDTME                                                
         MVC   EPQRRPWD,EIOPRPWD                                                
         MVC   EPQRFRMS,EIOPFRMS                                                
         MVC   EPQRCHRS,EIOPCHRS                                                
         MVC   EPQRCPYS,EIOPCPYS                                                
         MVC   EPQRCPYS,EIOPCPYS                                                
         MVC   EPQRATTR,EIOPATTR                                                
         MVC   EPQRSTAT,EIOPSTAT                                                
         MVC   EPQRFMAT,EIOPFMAT                                                
         MVC   EPQRND,SPACES                                                    
         MVC   EPQRDSN,SPACES                                                   
         MVC   EPQRBREP,=CL5'00000'                                             
         LA    R0,EPQRBLCK                                                      
         ICM   R1,15,=AL4(EPQRBLEN*EPQRBMAX)                                    
         LA    RE,*                                                             
         ICM   RF,15,=XL4'40000000'                                             
         MVCL  R0,RE                                                            
         OC    EIOPBREP,EIOPBREP                                                
         BZ    BPQR100                                                          
         EDIT  EIOPBREP,(5,EPQRBREP),ZERO=NOBLANK,FILL=0                        
         ICM   R1,3,EIOPBREP                                                    
         SR    R0,R0                                                            
         LA    RF,EIOPBLEN                                                      
         MR    R0,RF                                                            
         LA    R0,EIOPBLCK                                                      
         LA    RE,EPQRBLCK                                                      
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
         B     VPBLOK                                                           
*                                                                               
BPQR100  EQU   *                                                                
         MVC   EPQRDATX(2),=CL2'DD'                                             
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,=Y(EPQRDATX-EPQRDAT)                                        
         STCM  RF,3,ESSDDLN                                                     
         B     BPQROK                                                           
*                                  EXCHANGE MESSAGE ERROR                       
BPQRER   EQU   *                                                                
         BAS   RE,DCH0                                                          
         B     BPQRNO                                                           
*                                                                               
BPQRNO   B     NO                                                               
BPQROK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* TRANSLATE  CARRIAGE CONTROL CHARACTER IN 1ST BYTE OF R              *         
***********************************************************************         
                                                                                
TRANSCC  NTR1                                                                   
         MVC   BYTE,R                                                           
         LA    RE,R                                                             
         LA    R0,R+1                                                           
         SR    RF,RF                                                            
         ICM   RF,3,ESSDDLN                                                     
         LR    R1,RF                                                            
         MVCL  RE,R0                                                            
         SR    R1,R1                                                            
         ICM   R1,3,ESSDDLN                                                     
         LA    RF,R(R1)                                                         
         CLI   BYTE,X'01'                                                       
         BE    TRCCDS0                                                          
         CLI   BYTE,X'09'                                                       
         BE    TRCCDS1                                                          
         CLI   BYTE,X'0B'                                                       
         BE    TRCCIS1                                                          
         CLI   BYTE,X'11'                                                       
         BE    TRCCDS2                                                          
         CLI   BYTE,X'13'                                                       
         BE    TRCCIS2                                                          
         CLI   BYTE,X'19'                                                       
         BE    TRCCDS3                                                          
         CLI   BYTE,X'1B'                                                       
         BE    TRCCIS3                                                          
         CLI   BYTE,X'89'                                                       
         BE    TRCCDC1                                                          
         CLI   BYTE,X'8B'                                                       
         BE    TRCCIC1                                                          
         BE    TRCCUNK                                                          
*                                                                               
TRCCDS0  MVC   0(1,RF),=X'0D'                                                   
         LA    R1,1(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCDS1  MVC   0(2,RF),=X'0D15'                                                 
         LA    R1,2(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCIS1  MVC   0(2,RF),=X'0D15'                                                 
         LA    R1,2(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCDS2  MVC   0(4,RF),=X'0D150D15'                                             
         LA    R1,4(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCIS2  MVC   0(4,RF),=X'0D150D15'                                             
         LA    R1,4(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCDS3  MVC   0(6,RF),=X'0D150D150D15'                                         
         LA    R1,6(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCIS3  MVC   0(6,RF),=X'0D150D150D15'                                         
         LA    R1,6(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCDC1  MVC   0(3,RF),=X'0D150C'                                               
         LA    R1,3(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCIC1  MVC   0(3,RF),=X'0D150C'                                               
         LA    R1,3(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
TRCCUNK  EQU   *                                                                
         MVC   0(2,RF),=XL2'0D15'                                               
         LA    R1,2(R1)                                                         
         STCM  R1,3,ESSDDLN                                                     
         B     TRCCX                                                            
*                                                                               
TRCCX    XIT1                                                                   
                                                                                
***********************************************************************         
* TRANSLATE BOX CHARACTERS IN PQ REPORT DATA BUFFER R                 *         
***********************************************************************         
                                                                                
TRANSBOX NTR1                                                                   
         SR    RF,RF                                                            
         ICM   RF,3,ESSDDLN                                                     
         LA    RE,R                                                             
*                                                                               
TBOX010  CLI   0(RE),X'EC'                                                      
         BNE   *+12                                                             
         MVI   0(RE),X'15'                                                      
         B     TBOX020                                                          
         CLI   0(RE),X'CC'                                                      
         BNE   *+12                                                             
         MVI   0(RE),X'3E'                                                      
         B     TBOX020                                                          
         CLI   0(RE),X'CB'                                                      
         BNE   *+12                                                             
         MVI   0(RE),X'3B'                                                      
         B     TBOX020                                                          
         CLI   0(RE),X'EB'                                                      
         BNE   *+12                                                             
         MVI   0(RE),X'0A'                                                      
         B     TBOX020                                                          
*                                                                               
TBOX020  LA    RE,1(RE)                                                         
         BCT   RF,TBOX010                                                       
         B     TBOXX                                                            
*                                                                               
TBOXX    XIT1                                                                   
                                                                                
         EJECT                                                                  
***********************************************************************         
* MARK THE REPORT FOR DOWNLOADING                                     *         
* P1 = 1 (MARK IT SENT)  P1=0 (MARK IT SENDING)                       *         
***********************************************************************         
                                                                                
MARKREP  NTR1                                                                   
         LA    R6,=C'PRTQUE'                                                    
         L     R7,AMAJORNM         MAJOR RESCOURCE NAME                         
         ENQ   ((7),(6),E,6)       ENQUEUE THE PRINT QUEUE                      
*                                                                               
*        GOTO1 ADATAMGR,DMCB,(0,=C'INDEX'),EIOPFID,EIOPNDX,R,AEPQBUFF           
*        CLI   DMCB+8,0                                                         
*        BNE   MREPDIE1                                                         
*                                                                               
         GOTO1 ADATAMGR,DMCB,(0,=C'SENT'),EIOPFID,EIOPNDX,R,AEPQBUFF            
         CLI   8(R1),0                                                          
         BNE   MREPDIE2                                                         
*                                                                               
MREPDEQ  LA    R6,=C'PRTQUE'                                                    
         L     R7,AMAJORNM                                                      
         DEQ   ((7),(6),6)         DEQUEUE THE PRINT QUEUE                      
         B     MREPOK                                                           
*                                                                               
MREPDIE1 LA    R6,=C'PRTQUE'                                                    
         L     R7,AMAJORNM                                                      
         DEQ   ((7),(6),6)         DEQUEUE THE PRINT QUEUE                      
         BAS   RE,DCH0                                                          
*                                                                               
MREPDIE2 LA    R6,=C'PRTQUE'                                                    
         L     R7,AMAJORNM                                                      
         DEQ   ((7),(6),6)         DEQUEUE THE PRINT QUEUE                      
         BAS   RE,DCH0                                                          
*                                                                               
MREPNO   B     NO '                                                             
MREPOK   B     YES                                                              
         EJECT                                                                  
***********************************************************************         
* PROCESS ERROR FROM ESSRQS                                           *         
***********************************************************************         
                                                                                
RQSERROR EQU   *                                                                
         SR    RE,RB                                                            
         STCM  RE,15,FULL                                                       
         MVC   BYTE,0(R1)                                                       
         MVC   P(40),=CL40'ESSRQS ERROR CODE: '                                 
         GOTO1 AHEXOUT,DMCB,BYTE,P+19,1,=C'TOG'                                 
         GOTO1 AHEXOUT,DMCB,FULL,P+25,4,=C'TOG'                                 
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         MVC   P(40),=CL40'REPORT PARMS: '                                      
         MVC   P+20(8),ESSDUID                                                  
         MVC   P+30(3),EIOPSID                                                  
         MVI   P+33,C','                                                        
         MVC   P+34(5),EIOPNUM                                                  
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         B     EREX0005                                                         
                                                                                
***********************************************************************         
* PROCESS DC H'0' ERROR                                               *         
***********************************************************************         
                                                                                
DCH0     EQU   *                                                                
         SR    RE,RB                                                            
         STCM  RE,15,FULL                                                       
         MVC   P(40),=CL40'DCH0 ERROR OFFSET: '                                 
         GOTO1 AHEXOUT,DMCB,FULL,P+22,4,=C'TOG'                                 
         MVC   WTOMSGD(L'WTOMSGD),P                                             
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 AESSWTO,DMCB,X'FE000001',(L'WTOMSG,WTOMSG)                       
         B     EREX0006                                                         
*                                                                               
         DROP  R2,R3,R4                                                         
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
         LTORG                                                                  
         DS    0D                                                               
WTOALARM DC    CL2'*A'             WTO ALARM MESSAGE                            
EQESSIO  DC    CL8'EQESSIO '       MINOR RESCOURCE NAME FOR ESSIO ENQ           
         DS    0D                                                               
       ++INCLUDE FASYSLST                                                       
         EJECT                                                                  
         DS    0D                                                               
SOONCTAB DC    AL1(09),C'$$MONSOON',AL1(00)                                     
         DC    AL1(05),C'TIME=',AL1(05)                                         
         DS    CL5                                                              
         DC    AL1(09),C'PRIORITY=',AL1(01)                                     
         DS    CL1                                                              
         DC    AL1(06),C'CLASS=',AL1(06)                                        
         DS    CL6                                                              
         DC    AL1(07),C'SYSTEM=',AL1(01)                                       
         DS    C                                                                
         DC    AL1(08),C'PROGRAM=',AL1(02)                                      
         DS    CL2                                                              
         DC    AL1(05),C'EXEC=',AL1(08)                                         
         DS    CL8                                                              
         DC    AL1(12),C'DESCRIPTION=',AL1(24)                                  
         DS    CL24                                                             
         DC    AL1(12),C'$$MONSOONEND',AL1(00)                                  
         DC    X'FF'                                                            
SOONTLEN EQU   *-SOONCTAB                                                       
*                                                                               
BUFFER   DC    CL8'BUFFER'                                                      
GFILE    DC    CL8'GFILE'                                                       
INDEX    DC    CL8'INDEX'                                                       
RANDOM   DC    CL8'RANDOM'                                                      
READL    DC    CL8'READ'                                                        
DMREAD   DC    CL8'DMREAD'                                                      
DMWRT    DC    CL8'DMWRT'                                                       
PRTQUE   DC    CL8'PRTQUE'                                                      
SPACES   DC    132C' '                                                          
         DS    0D                                                               
         EJECT                                                                  
***********************************************************************         
* GET USER ID APLHA FROM BINARY VALUE                                 *         
***********************************************************************         
                                                                                
GETPQUID NTR1  BASE=*                                                           
         LM    R6,R7,0(R1)                                                      
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING CTIREC,R1                                                        
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID+8(2),0(R6)                                                
         DROP  R1                                                               
*                                                                               
         GOTO1 ADATAMGR,DMCB,(0,=C'DMREAD'),=C'CTFILE',KEY,AIDREC,0             
         CLI   DMCB+8,0                                                         
         BE    *+8                                                              
         BAS   RE,DCH0             USERID RECORD IS GONE                        
         MVI   DATADISP+1,28                                                    
*                                                                               
         L     R6,AIDREC                                                        
         MVI   ELCODE,X'02'                                                     
         GOTO1 =A(GETEL)                                                        
         BE    *+8                                                              
         BAS   RE,DCH0                                                          
         MVC   0(8,R7),2(R6)                                                    
*                                                                               
GPQUX    XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE INITIALIZES THE PQ REPORT BUFFER FOR READING           *         
***********************************************************************         
                                                                                
         DS    0D                                                               
INITPQR  NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         XC    EIOPNDX,EIOPNDX     GET PRTQ FILE FOR THIS USER ID               
         MVC   EIOPNDX+UKSRCID-UKINDEX(2),EIOPUIDN                              
         GOTO1 ADATAMGR,DMCB,(X'00',GFILE),PRTQUE,EIOPNDX,R,AEPQBUFF            
         CLI   8(R1),0                                                          
         BNE   IPQRER1             DISK ERROR                                   
         MVC   EIOPFID,EIOPNDX+UKUSRINF-UKINDEX                                 
*                                  INITIALIZE BUFFER                            
         GOTO1 ADATAMGR,DMCB,(X'00',BUFFER),EIOPFID,EIOPNDX,R,         +        
               AEPQBUFF,0                                                       
         CLI   8(R1),0                                                          
         BNE   IPQRER2             DISK ERROR                                   
*                                                                               
         L     R7,AEPQBUFF         R7 = A(PQ BUFFER)                            
         USING PQRECD,R7                                                        
*                                                                               
         MVC   EIOPSKSV,8(R7)      SAVE DISPLACEMENT TO SAVE DATA               
*                                                                               
         ICM   RF,15,EIOPSKSV      RF = A(SAVE DATA AT END OF BUFFER)           
         AR    RF,R7                                                            
         USING SKBUFFD,RF                                                       
         MVC   EIOPLAB,SKLABEL     SAVE PQ BUFFER LABEL FOR THIS TASK           
         DROP  RF                                                               
*                                                                               
         LA    R6,EIOPNDX          R6 = A(INDEX)                                
         USING UKRECD,R6                                                        
*                                                                               
         XC    UKINDEX,UKINDEX     ELSE CLEAR INDEX                             
         MVC   UKSRCID,EIOPUIDN                                                 
         MVC   UKSUBID,EIOPSID                                                  
         MVC   UKREPNO,EIOPNUMB                                                 
*                                  READ REPORT INDEX                            
IPQR040  GOTO1 ADATAMGR,DMCB,(X'08',INDEX),EIOPFID,EIOPNDX,R,AEPQBUFF,0         
         TM    8(R1),X'80'                                                      
         BO    IPQRER4             END OF FILE                                  
         CLI   8(R1),0                                                          
         BNE   IPQRER3             DISK ERROR                                   
*                                                                               
         L     RF,AEPQBUFF         DON'T ALLOW AGENCIES TO SEE REPORTS          
         CLC   UKSRCID,10(RF)      THAT ARE NOT THEIRS                          
* ??     BNE   IPQR040                                                          
*                                                                               
         L     RF,AEPQBUFF         POINT RF TO END OF BUFFER                    
         ICM   RE,15,EIOPSKSV                                                   
         AR    RF,RE                                                            
         USING SKBUFFD,RF                                                       
         MVC   EIOPSVPB,SKBCTRL    SAVE WHOLE END OF BUFFER                     
         DROP  RF                                                               
*                                                                               
         GOTO1 =A(RNDPAGE),DMCB,0  READ REPORT HEADER RECORD                    
         BNE   IPQRNO                                                           
*                                                                               
         GOTO1 =A(GETPQHDR)        GET REPORT HEADER VALUES                     
*                                                                               
         GOTO1 =A(SETFORM)         SET REPORT FORMAT CODE                       
         BNE   IPQRER8                                                          
*                                                                               
         TM    PQATTB,PQATJOBI                                                  
         BO    IPQRER5                                                          
         TM    PQATTB,PQATERR                                                   
         BO    IPQRER6                                                          
*                                                                               
         B     IPQROK              EXIT OK                                      
*                                  DISK ERROR DURING PQ ID CALL                 
IPQRER1  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ ID CALL'                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  DISK ERROR DURING PQ BUFFER CALL             
IPQRER2  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ BUFFER CALL'                    
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  DISK ERROR DURING PQ INDEX CALL              
IPQRER3  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ INDEX CALL'                     
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  EOF ON PQ INDEX CALL                         
IPQRER4  EQU   *                                                                
         MVC   P(40),=CL40'EOF ON PQ INDEX CALL'                                
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  ATTRIBUTE ERROR 1                            
IPQRER5  EQU   *                                                                
         MVC   P(40),=CL40'PQ REPORT ATTRIBUTE ERROR 1'                         
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  ATTRIBUTE ERROR 2                            
IPQRER6  EQU   *                                                                
         MVC   P(40),=CL40'PQ REPORT ATTRIBUTE ERROR 2'                         
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  STATUS ERROR                                 
IPQRER7  EQU   *                                                                
         MVC   P(40),=CL40'PQ REPORT STATUS ERROR'                              
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                  SET FORMAT ERROR                             
IPQRER8  EQU   *                                                                
         MVC   P(40),=CL40'PQ REPORT SET FORMAT ERROR'                          
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQRNO                                                           
*                                                                               
IPQROK   SR    RC,RC                                                            
IPQRNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4,R6,R7                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE INITIALIZES THE PQ FILES FOR READING                   *         
***********************************************************************         
                                                                                
INITPQF  NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         XC    EIOPNDX,EIOPNDX     GET PRTQ FILE FOR THIS USER ID               
         MVC   EIOPNDX+UKSRCID-UKINDEX(2),EIOPUIDN                              
         GOTO1 ADATAMGR,DMCB,(X'00',GFILE),PRTQUE,EIOPNDX,R,AEPQBUFF            
         CLI   8(R1),0                                                          
         BNE   IPQFER1             DISK ERROR                                   
         MVC   EIOPFID,EIOPNDX+UKUSRINF-UKINDEX                                 
*                                  INITIALIZE BUFFER                            
         GOTO1 ADATAMGR,DMCB,(X'00',BUFFER),EIOPFID,EIOPNDX,R,         +        
               AEPQBUFF,0                                                       
         CLI   8(R1),0                                                          
         BNE   IPQFER2             DISK ERROR                                   
*                                                                               
         L     R7,AEPQBUFF         R7 = A(PQ BUFFER)                            
         USING PQRECD,R7                                                        
*                                                                               
         MVC   EIOPSKSV,8(R7)      SAVE DISPLACEMENT TO SAVE DATA               
*                                                                               
         ICM   RF,15,EIOPSKSV      RF = A(SAVE DATA AT END OF BUFFER)           
         AR    RF,R7                                                            
         USING SKBUFFD,RF                                                       
         MVC   EIOPLAB,SKLABEL     SAVE PQ BUFFER LABEL FOR THIS TASK           
         DROP  RF                                                               
*                                                                               
         LA    R6,EIOPNDX          R6 = A(INDEX)                                
         USING UKRECD,R6                                                        
*                                                                               
         XC    UKINDEX,UKINDEX     CLEAR INDEX                                  
*                                                                               
         B     IPQFOK              EXIT OK                                      
*                                  DISK ERROR DURING PQ ID CALL                 
IPQFER1  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ ID CALL'                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQFNO                                                           
*                                  DISK ERROR DURING PQ BUFFER CALL             
IPQFER2  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ BUFFER CALL'                    
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     IPQFNO                                                           
*                                                                               
IPQFOK   SR    RC,RC                                                            
IPQFNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4,R6,R7                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE CALLS VDATAMGR WITH A PQ 'INDEX' CALL.                 *         
***********************************************************************         
                                                                                
GETPQNDX NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         MVI   EIOPEOF,C'N'        END OF FILE INDICATOR = 'N'                  
*                                                                               
         LA    R6,EIOPNDX          R6 = A(INDEX)                                
         USING UKRECD,R6                                                        
*                                  READ REPORT INDEX                            
GNDX010  GOTO1 ADATAMGR,DMCB,(X'08',INDEX),EIOPFID,EIOPNDX,R,AEPQBUFF,0         
         TM    8(R1),X'80'                                                      
         BO    GNDXEOF             END OF FILE                                  
         CLI   8(R1),0                                                          
         BNE   GNDXIND             DISK ERROR                                   
         CLC   EIOPUIDN,UKSRCID                                                 
         BNE   GNDX010                                                          
         MVC   EIOPSID,UKSUBID                                                  
         MVC   EIOPNUMB,UKREPNO                                                 
         EDIT  EIOPNUMB,(5,EIOPNUM),FILL=0,ZERO=NOBLANK                         
*                                                                               
         L     RF,AEPQBUFF          POINT R7 TO END OF BUFFER                   
         ICM   RE,15,EIOPSKSV                                                   
         AR    RF,RE                                                            
         USING SKBUFFD,RF                                                       
         MVC   EIOPSVPB,SKBCTRL    SAVE WHOLE END OF BUFFER                     
         DROP  RF                                                               
*                                  DISK ERROR DURING PQ ID CALL                 
         B     GNDXOK                                                           
*                                                                               
GNDXEOF  MVI   EIOPEOF,C'Y'        END OF FILE INDICATOR = 'Y'                  
         B     GNDXOK                                                           
*                                  DISK ERROR DURING PQ INDEX CALL              
GNDXIND  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ NDX CALL'                       
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     GNDXNO                                                           
*                                  SET FORMAT ERROR                             
GNDXEFM  EQU   *                                                                
         MVC   P(40),=CL40'PQ NDX CALL SET FORMAT ERROR'                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     GNDXNO                                                           
*                                                                               
GNDXOK   SR    RC,RC                                                            
GNDXNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4,R6                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* FILTER PQ INDEX FOR INQUIRY                                         *         
***********************************************************************         
                                                                                
FILTNDX  NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         OC    EIOPFSID,EIOPFSID                                                
         BZ    FNDX010                                                          
         CLC   EIOPFSID,SPACES                                                  
         BE    FNDX010                                                          
         CLC   EIOPFSID,EIOPSID                                                 
         BNE   FNDXNO                                                           
*                                                                               
FNDX010  EQU   *                                                                
         B     FNDXOK                                                           
*                                                                               
FNDXOK   SR    RC,RC                                                            
FNDXNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* FILTER PQ REPORT HEADER FOR INQUIRY                                 *         
***********************************************************************         
                                                                                
FILTHDR  NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         OC    EIOPFFMT,EIOPFFMT                                                
         BZ    FHDR010                                                          
         CLC   EIOPFFMT,SPACES                                                  
         BZ    FHDR010                                                          
         CLC   EIOPFFMT,EIOPFMAT                                                
         BNE   FHDRNO                                                           
*                                                                               
FHDR010  EQU   *                                                                
         B     FHDROK                                                           
*                                                                               
FHDROK   SR    RC,RC                                                            
FHDRNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* GET PQ REPORT HEADER VALUES                                         *         
***********************************************************************         
                                                                                
GETPQHDR NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         L     R6,AEPQBUFF                                                      
         USING PQRECD,R6                                                        
         GOTO1 AHEXOUT,DMCB,PQATTB,EIOPATTR,1    ATTRIBUTE BYTE                 
         GOTO1 AHEXOUT,DMCB,PQSTAT,EIOPSTAT,1    STATUS BYTE                    
         GOTO1 ADATCON,DMCB,(2,PQAGELD),(0,WORK) CREATION DATE                  
                                                                                
GPQH010  TM    PQTYPE,PQTYNEW      TEST IF NEW/OLD STYLE PQ                     
         BZ    GOQH010                                                          
         MVC   EIOPDTMC(4),WORK+2  SET DATE/TIME CREATED (MMDDHHMM)             
         SR    RF,RF                                                            
         IC    RF,PQTIMEL                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTMC+4(2),DUB+6(2)                                           
         IC    RF,PQTIMEL+1                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTMC+6(2),DUB+6(2)                                           
*                                                                               
         LA    R7,BLOCK            CALL GETRET TO CALCULATE DATE/TIME           
         USING GETRETD,R7                                                       
         GOTO1 ADATCON,DMCB,(0,WORK),(3,GRDIDY)                                 
         MVC   GRDITH(2),PQTIMEL                                                
         MVC   GRDHRS,PQRETNL      SET LIVE RETAIN                              
         TM    PQSTAT,PQSTLIVE     TEST LIVE                                    
         BNZ   *+10                                                             
         MVC   GRDHRS,PQRETND      SET DEAD RETAIN                              
         GOTO1 AGETRET,(R7)                                                     
*                                  SET DATE/TIME EXPIRES (MMDDHHMM)             
         GOTO1 ADATCON,DMCB,(3,GRDODY),(0,WORK)                                 
         MVC   EIOPDTME(4),WORK+2                                               
         SR    RF,RF                                                            
         IC    RF,GRDOTH                                                        
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTME+4(2),DUB+6(2)                                           
         IC    RF,GRDOTH+1                                                      
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTME+6(2),DUB+6(2)                                           
*                                                                               
         GOTO1 AHEXOUT,DMCB,PQTYPE,EIOPTYPE,1                                   
         EDIT  (B1,PQPRCNT),(2,EIOPCUNT),FILL=0                                 
         MVC   EIOPSLUI,PQPRSYM                                                 
         GOTO1 ADATCON,DMCB,(2,PQDATED),(0,WORK)                                
*                                  SET DATE/TIME SENT (MMDDHHMM)                
         MVC   EIOPSENT(4),WORK+2                                               
         SR    RF,RF                                                            
         IC    RF,PQTIMED                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPSENT+4(2),DUB+6(2)                                           
         IC    RF,PQTIMED+1                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPSENT+6(2),DUB+6(2)                                           
*                                                                               
         MVC   EIOPMAKE,PQMAKER                                                 
         GOTO1 AHEXOUT,DMCB,PQAGES,EIOPSIZE,1                                   
*                                                                               
         MVC   EIOPCLAS,PQCLASS                                                 
*                                                                               
         MVC   EIOPDESC,PQDESC                                                  
         OC    EIOPDESC,SPACES                                                  
*                                  SET LINES AND PAGES                          
         SR    RF,RF                                                            
         ICM   RF,7,PQLINES                                                     
         XC    NUMLINES,NUMLINES                                                
         STCM  RF,7,NUMLINES                                                    
         EDIT  (3,PQLINES),(7,EIOPNLIN),FILL=0                                  
         SR    RF,RF                                                            
         ICM   RF,3,PQPAGES                                                     
         XC    NUMPAGES,NUMPAGES                                                
         STCM  RF,3,NUMPAGES                                                    
         EDIT  (2,PQPAGES),(6,EIOPNPAG),FILL=0                                  
*                                                                               
GPQH040  SR    R0,R0               SET CPL AND LPP                              
         ICM   R0,3,PQAVCPL                                                     
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPCPL,DUB                                                      
         SR    R0,R0                                                            
         IC    R0,PQLPP                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPLPP,DUB                                                      
*                                                                               
GPQH060  MVC   EIOPRPWD,SPACES     SET PASSWORD                                 
         TM    PQATTB,PQATPW                                                    
         BZ    GPQH070                                                          
         TM    PQSECF1,PQSIPIN                                                  
         BZ    GPQH070                                                          
         MVC   EIOPRPWD(4),PQPSWD                                               
         OC    EIOPRPWD,SPACES                                                  
*                                                                               
GPQH070  MVC   EIOPFRMS,PQFORMS    SET FORMS DATA                               
         OC    EIOPFRMS,SPACES                                                  
         MVC   EIOPCHRS,PQCHARS                                                 
         OC    EIOPCHRS,SPACES                                                  
         MVC   DUB(1),PQCOPIES     SET NUMBER OF COPIES                         
         CLI   DUB,C' '                                                         
         BNE   *+8                                                              
         MVI   DUB,0                                                            
         CLI   DUB,C'0'                                                         
         BL    *+8                                                              
         NI    DUB,X'0F'           ALLOW C'N' VALUE                             
         CLI   DUB,0                                                            
         BNE   *+8                                                              
         MVI   DUB,1               DEFAULT IS 1                                 
         SR    R0,R0                                                            
         IC    R0,DUB                                                           
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPCPYS,DUB                                                     
         B     GPQHX                                                            
                                                                                
GOQH010  MVC   EIOPDTMC(4),WORK+2  SET DATE/TIME CREATED (MMDDHHMM)             
         SR    RF,RF                                                            
         IC    RF,OQTIMEL                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTMC+4(2),DUB+6(2)                                           
         IC    RF,OQTIMEL+1                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTMC+6(2),DUB+6(2)                                           
*                                                                               
         LA    R7,BLOCK            CALL GETRET TO CALCULATE DATE/TIME           
         USING GETRETD,R7                                                       
         GOTO1 ADATCON,DMCB,(0,WORK),(3,GRDIDY)                                 
         MVC   GRDITH(2),OQTIMEL                                                
         MVC   GRDHRS,OQRETNL      SET LIVE RETAIN                              
         TM    PQSTAT,PQSTLIVE     TEST LIVE                                    
         BNZ   *+10                                                             
         MVC   GRDHRS,OQRETND      SET DEAD RETAIN                              
         GOTO1 AGETRET,(R7)                                                     
*                                  SET DATE/TIME EXPIRES (MMDDHHMM)             
         GOTO1 ADATCON,DMCB,(3,GRDODY),(0,WORK)                                 
         MVC   EIOPDTME(4),WORK+2                                               
         SR    RF,RF                                                            
         IC    RF,GRDOTH                                                        
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTME+4(2),DUB+6(2)                                           
         IC    RF,GRDOTH+1                                                      
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPDTME+6(2),DUB+6(2)                                           
*                                                                               
         GOTO1 AHEXOUT,DMCB,PQTYPE,EIOPTYPE,1                                   
         EDIT  (B1,OQPRCNT),(2,EIOPCUNT),FILL=0                                 
         MVC   EIOPSLUI,OQPRSYM                                                 
         GOTO1 ADATCON,DMCB,(2,OQDATED),(0,WORK)                                
*                                  SET DATE/TIME SENT (MMDDHHMM)                
         MVC   EIOPSENT(4),WORK+2                                               
         SR    RF,RF                                                            
         IC    RF,OQTIMED                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPSENT+4(2),DUB+6(2)                                           
         IC    RF,OQTIMED+1                                                     
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPSENT+6(2),DUB+6(2)                                           
*                                                                               
         MVC   EIOPMAKE,OQMAKER                                                 
         GOTO1 AHEXOUT,DMCB,PQAGES,EIOPSIZE,1                                   
*                                                                               
         MVC   EIOPCLAS,PQCLASS                                                 
*                                                                               
         MVC   EIOPDESC,OQDESC                                                  
         OC    EIOPDESC,SPACES                                                  
*                                  SET LINES AND PAGES                          
         SR    RF,RF                                                            
         ICM   RF,7,OQLINES                                                     
         XC    NUMLINES,NUMLINES                                                
         STCM  RF,7,NUMLINES                                                    
         EDIT  (3,OQLINES),(7,EIOPNLIN),FILL=0                                  
         SR    RF,RF                                                            
         ICM   RF,3,OQPAGES                                                     
         XC    NUMPAGES,NUMPAGES                                                
         STCM  RF,3,NUMPAGES                                                    
         EDIT  (2,OQPAGES),(6,EIOPNPAG),FILL=0                                  
*                                                                               
GOQH040  SR    R0,R0               SET CPL AND LPP                              
         ICM   R0,3,OQAVCPL                                                     
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPCPL,DUB                                                      
         SR    R0,R0                                                            
         IC    R0,OQLPP                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPLPP,DUB                                                      
*                                                                               
GOQH060  MVC   EIOPRPWD,SPACES     SET PASSWORD                                 
         TM    PQATTB,PQATPW                                                    
         BZ    GOQH070                                                          
         TM    OQSECF1,PQSIPIN                                                  
         BZ    GOQH070                                                          
         MVC   EIOPRPWD(4),OQPSWD                                               
         OC    EIOPRPWD,SPACES                                                  
*                                                                               
GOQH070  MVC   EIOPFRMS,OQFORMS    SET FORMS DATA                               
         OC    EIOPFRMS,SPACES                                                  
         MVC   EIOPCHRS,OQCHARS                                                 
         OC    EIOPCHRS,SPACES                                                  
         MVC   DUB(1),OQCOPIES     SET NUMBER OF COPIES                         
         CLI   DUB,C' '                                                         
         BNE   *+8                                                              
         MVI   DUB,0                                                            
         CLI   DUB,C'0'                                                         
         BL    *+8                                                              
         NI    DUB,X'0F'           ALLOW C'N' VALUE                             
         CLI   DUB,0                                                            
         BNE   *+8                                                              
         MVI   DUB,1               DEFAULT IS 1                                 
         SR    R0,R0                                                            
         IC    R0,DUB                                                           
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EIOPCPYS,DUB                                                     
*                                                                               
GPQHX    XIT1                                                                   
         DROP  R4,R6                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE DETERMINES WHETHER THE REPORT IS IN DATA OR TEXT FORMAT.         
* IT DOES SO BY READING THROUGH THE SECOND PAGE (IF IT HAS ONE) AND             
* LOOKING FOR A LINE THAT ENDS WITH ' ;'.  IF IT FINDS SUCH A LINE,             
* THEN THE REPORT IS IN DATA FORMAT.  OTHERWISE IT IS IN TEXT FORMAT.           
* THE ROUTINE SETS THE REPORT FORMAT TO 'D' FOR DATA FORMAT AND 'T'             
* FOR TEXT FORMAT. THE ROUTINE RETURNS 'NO' IF ONE OF THE ROUTINES IT           
* CALLS HAS A PROBLEM.  OTHERWISE IT RETURNS 'YES'.                             
***********************************************************************         
                                                                                
SETFORM  NTR1  BASE=*                                                           
         USING ESSDATAD,R2                                                      
         USING EIOPQRD,R4                                                       
         MVI   EIOPFMAT,C'T'                                                    
*                                                                               
         CLC   NUMPAGES,=F'2'      IF REPORT DOESN'T HAVE EXACTLY TWO           
         BNE   SFYES                   PAGES THEN NOT DATA FORMAT               
*                                                                               
         GOTO1 =A(RNDPAGE),DMCB,2 READ SECOND PAGE OF REPORT                    
         BNE   SFNO                                                             
*                                                                               
SF10     GOTO1 =A(GETPQLIN)                                                     
         BNE   SFNO                                                             
*                                                                               
         CLI   EIOPEOF,C'Y'       IF END OF FILE THEN NOT DATA FORMAT           
         BE    SFYES                                                            
*                                  ELSE IF PQ LINE ENDS WITH SPACE AND          
         ICM   RF,15,ESSDDLN                                                    
         SH    RF,=H'2'               A SEMICOLON THEN REPORT IS IN             
         LA    RF,R+1(RF)             DATA FORMAT                               
         CLC   0(2,RF),=X'405E'                                                 
         BE    SF20                                                             
*                                                                               
         B     SF10                ELSE TRY NEXT LINE                           
*                                                                               
SF20     MVI   EIOPFMAT,C'D'       REPORT IS IN DATA FORMAT                     
         B     SFYES               RETURN 'YES'                                 
*                                                                               
SFYES    SR    RC,RC                                                            
SFNO     LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R2,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* GET NEXT LINE OF PQ REPORT                                          *         
***********************************************************************         
                                                                                
GETPQLIN NTR1  BASE=*                                                           
         USING ESSDATAD,R2                                                      
         USING EIOPQRD,R4                                                       
         MVI   EIOPEOF,C'N'        END OF FILE = 'N'                            
*                                                                               
         CLI   EIOPEJCT,C'Y'       IF LAST PRINT LINE HAD A DATA BEFORE         
         BNE   GPQL010                 EJECT CC CHAR                            
*                                  THEN BUMP PAGE AND RESET LINE NUMBER         
         SR    RF,RF                                                            
         ICM   RF,3,EIOPPAGE                                                    
         LA    RF,1(RF)                                                         
         STCM  RF,3,EIOPPAGE                                                    
         MVC   EIOPLNUM,=XL2'0000'                                              
         MVI   EIOPEJCT,C'N'       RESET EJECT FLAG                             
*                                                                               
*                                  READ NEXT LINE FROM PRINT QUEUE              
GPQL010  GOTO1 ADATAMGR,DMCB,(X'01',READL),EIOPFID,EIOPNDX,R,AEPQBUFF,0         
         TM    8(R1),X'80'                                                      
         BO    GPQLEOF             END OF REPORT ENCOUNTERED                    
         CLI   8(R1),0                                                          
         BNE   GPQLER1             DISK ERROR                                   
*                                                                               
         L     RF,AEPQBUFF         POINT RF TO END OF BUFFER                    
         ICM   RE,15,EIOPSKSV                                                   
         AR    RF,RE                                                            
         USING SKBUFFD,RF                                                       
         MVC   EIOPSVPB,SKBCTRL    SAVE WHOLE END OF BUFFER                     
         DROP  RF                                                               
*                                                                               
         SR    RF,RF               GET LINE LENGTH FROM RETURN VALUE            
         ICM   RF,3,22(R1)         AND SUBTRACT ONE FOR CARRIAGE                
         BCTR  RF,0                CONTROL CHAR                                 
         STCM  RF,3,PRTLEN                                                      
*                                                                               
GQPL030  CLI   R,X'8B'             IF CC CHAR IS EJECT BEFORE DATA              
         BNE   GPQL040                                                          
*                                  THEN BUMP PAGE AND RESET LINE NUMBER         
         SR    RF,RF                                                            
         ICM   RF,3,EIOPPAGE                                                    
         LA    RF,1(RF)                                                         
         STCM  RF,3,EIOPPAGE                                                    
         MVC   EIOPLNUM,=XL2'0000'                                              
*                                                                               
GPQL040  CLI   R,X'89'             IF CC CHAR IS DATA BEFORE EJECT              
         BNE   GPQL050                                                          
         MVI   EIOPEJCT,C'Y'       THEN SET FLAG TO EJECT NEXT TIME             
*                                                                               
GPQL050  SR    RF,RF               BUMP LINE NUMBER                             
         ICM   RF,3,EIOPLNUM                                                    
         LA    RF,1(RF)                                                         
         STCM  RF,3,EIOPLNUM                                                    
         MVC   ESSDDLN,PRTLEN                                                   
         BAS   RE,TRANSCC                                                       
         BAS   RE,TRANSBOX                                                      
         B     GPQLOK                                                           
*                                                                               
GPQLEOF  MVI   EIOPEOF,C'Y'        END OF FILE = 'Y'                            
         B     GPQLOK                                                           
*                                  DISK ERROR DURING SEQUENTIAL READ            
GPQLER1  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR PQ REPORT READ LINE'                      
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     GPQLNO                                                           
*                                                                               
GPQLOK   SR    RC,RC                                                            
GPQLNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R2,R4                                                            
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* THIS ROUTINE READS FOR THE BEGINNING OF THE PAGE SPECIFIED IN THE             
* THE FIRST PARAMETER.  IT SETS EIOPPAGE TO THE PAGE NUMBER REQUESTED,          
* EIOPLNUM TO 0, AND EJECT TO 'N'.  IT THEN SAVES THE SAVE DATA AT THE          
* END OF THE BUFFER.  A CALL TO FIRSTLIN WILL NOW RETURN THE FIRST              
* PRINT LINE OF THIS PAGE.                                                      
***********************************************************************         
                                                                                
RNDPAGE  NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         MVC   EIOPPAGE,0(R1)      EIOPPAGE = PARAMETER ONE                     
         MVC   EIOPLNUM,=XL2'0000' EIOPLNUM = 0                                 
         MVI   EIOPEJCT,C'N'       EJECT = 'N'                                  
*                                                                               
         XC    R,R                 READ TO BEGINNING OF PAGE                    
         MVC   R+0(4),EIOPPAGE                                                  
         MVC   R+4(4),=C'PAGE'                                                  
         GOTO1 ADATAMGR,DMCB,(X'01',RANDOM),EIOPFID,EIOPNDX,R,         +        
               AEPQBUFF,0                                                       
         CLI   8(R1),0                                                          
         BNE   RPAGER1             DISK ERROR                                   
*                                                                               
         L     RF,AEPQBUFF         POINT RF TO END OF BUFFER                    
         ICM   RE,15,EIOPSKSV                                                   
         AR    RF,RE                                                            
         USING SKBUFFD,RF                                                       
         MVC   EIOPSVPB,SKBCTRL    SAVE WHOLE END OF BUFFER                     
         DROP  RF                                                               
         B     RPAGOK              EXIT OK                                      
*                                  DISK ERROR DURING PQ ID CALL                 
RPAGER1  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ READ PAGE'                      
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     RPAGNO                                                           
*                                                                               
RPAGOK   SR    RC,RC                                                            
RPAGNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* READ FIRST PRINT LINE FOR THIS TRANSACTION.  DO SO BY RESTORING THE           
* PQ BUFFER TO ITS STATE FOLLOWING EITHER THE RANDOM READ OF A PAGE             
* OR THE LAST READ OF THE PREVIOUS TRANSACTION.  THEN READ                      
* SEQUENTIALLY FOR THE NEXT PRINT LINE.                                         
***********************************************************************         
                                                                                
FIRSTLIN NTR1  BASE=*                                                           
         USING EIOPQRD,R4                                                       
         L     R7,AEPQBUFF         R7 = A(PQ BUFFER)                            
         USING PQRECD,R7                                                        
*                                                                               
         ICM   R6,15,EIOPSKSV      RESTORE END OF BUFFER                        
         AR    R6,R7                                                            
         USING SKBUFFD,R6                                                       
         MVC   SKBUFFD(SKEND-SKBCTRL),EIOPSVPB                                  
         MVC   SKLABEL,EIOPLAB                                                  
         XC    SKFCTRL,SKFCTRL                                                  
         MVC   SKFSTCI,EIOPSVPB+SKFSTCI-SKLABEL                                 
*                                                                               
         XC    EIOPNDX,EIOPNDX     RESTORE INDEX                                
         MVC   EIOPNDX(L'SKINDEX),SKINDEX                                       
*                                                                               
         XC    R(4),R              READ HEADER RECORD FOR REPORT                
         MVC   R+4(4),=C'PAGE'                                                  
         GOTO1 ADATAMGR,DMCB,(X'00',RANDOM),EIOPFID,EIOPNDX,R,(R7),0            
         CLI   8(R1),0                                                          
         BNE   FLINER1             DISK ERROR                                   
*                                                                               
         CLC   EIOPUIDN,PQSRCID    ERROR IF NOT SAME REPORT AS BEFORE           
         BNE   FLINER2                                                          
         CLC   EIOPSID,PQSUBID                                                  
         BNE   FLINER2                                                          
         CLC   EIOPNUMB,PQREPNO                                                 
         BNE   FLINER2                                                          
*                                  READ LAST BLOCK INTO BUFFER                  
         MVC   SKBUFFD(SKEND-SKLABEL),EIOPSVPB                                  
         MVC   SKLABEL,EIOPLAB                                                  
         MVC   FULL,SKADDR                                                      
         OC    FULL,FULL                                                        
         BZ    FLINER2                                                          
         GOTO1 ADATAMGR,DMCB,(X'01',DMREAD),EIOPFID,FULL,(R7),0                 
         CLI   8(R1),0                                                          
         BNE   FLINER1             DISK ERROR                                   
*                                                                               
         CLC   EIOPUIDN,PQSRCID    ERROR IF NOT SAME REPORT AS BEFORE           
         BNE   FLINER2                                                          
         CLC   EIOPSID,PQSUBID                                                  
         BNE   FLINER2                                                          
*                                                                               
         GOTO1 =A(GETPQLIN)                                                     
         BNE   FLINNO                                                           
         B     FLINOK              EXIT OK                                      
*                                  DISK ERROR DURING PQ ID CALL                 
FLINER1  EQU   *                                                                
         MVC   P(40),=CL40'DISK ERROR DURING PQ READ PAGE'                      
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     FLINNO                                                           
*                                  FIRST LINE ERROR 2                           
FLINER2  EQU   *                                                                
         MVC   P(40),=CL40'FIRST LINE PQ REPORT ERROR 2'                        
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         B     FLINNO                                                           
*                                                                               
FLINOK   SR    RC,RC                                                            
FLINNO   LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R4,R6,R7                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* FIELD - BUILD REQUEST CARDS FROM FIELDS                             *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    PARAMETER 1 = A(L'DATA-1)                                        *         
*    PARAMETER 2 = A(PQ REPORT MESSAGE DATA)                          *         
*    PARAMETER 3 = A(EIOPQRD WORK AREA)                               *         
*    WHICH CONTAINS:                                                  *         
*      EIOPREQC                                                       *         
*      EIOPPGM                                                        *         
*      EIOPCPOS (AFTER 1ST PASS)                                      *         
*      EIOPCLFT (AFTER 1ST PASS)                                      *         
*      EIOPCCNT (AFTER 1ST PASS)                                      *         
*                                                                     *         
*  RETURNS:                                                           *         
*    UPDATED VALUES IN EIOPQRD, INLCUDING:                            *         
*      EIOPCRDS                                                       *         
*      EIOPCPOS                                                       *         
*      EIOPCLFT                                                       *         
*      EIOPCCNT                                                       *         
*                                                                     *         
***********************************************************************         
                                                                                
FIELD    NTR1  BASE=*                                                           
         SR    R1,R1                                                            
         ICM   R1,3,0(R1)          L'DATA-1                                     
         L     R7,4(R1)            A(DATA) ?? CHECK THIS ??                     
         L     R6,8(R1)            A(EIOPQRD WORK AREA)                         
         USING EIOPQRD,R6                                                       
         SR    R4,R4                                                            
         LA    R2,2(R7)                                                         
         TM    EIOPREQC,RCFLD      BEEN HERE BEFORE?                            
         BZ    *+16                 NO                                          
*                                                                               
         L     R3,EIOPCPOS         LAST COL OF CURRENT CARD USED                
         IC    R4,EIOPCLFT         COLS REMAINING ON CURRENT CARD               
         B     *+16                                                             
*                                                                               
         MVC   EIOPCRDS(2),EIOPPGM                                              
         MVC   EIOPCRDS+5(6),=C'999999' FAKE SIN                                
*                                                                               
         BCTR  R1,0                SUB 2 FOR LEN (WAS BCTR'D FOR EX)            
         LTR   R1,R1                                                            
         BZ    FLD30               NO DATA                                      
*                                                                               
         LA    R5,5(R1)            CARD COLUMNS REQUIRED                        
         CR    R5,R4               IS THERE ROOM FOR ENTIRE FIELD?              
         BNH   FLD10               YES                                          
         CH    R4,=H'6'            IS THERE ROOM TO BEGIN A NEW FIELD?          
         BL    *+12                NO                                           
         CH    R5,=H'9'            IS IT WORTH SPLITTING UP THE FIELD?          
         BH    FLD10               YES                                          
*                                                                               
         ZIC   R3,EIOPCCNT         NO NEED A NEW REQUEST CARD                   
         LA    R0,1(R3)            BUMP CARD COUNT                              
         STC   R0,EIOPCCNT         AND SAVE                                     
*                                                                               
         MH    R3,=H'80'                                                        
         LA    R3,EIOPCRDS(R3)     POINT TO NEW REQUEST                         
         MVC   0(12,R3),EIOPCRDS   MOVE REQ/AGY/SIN                             
         MVC   12(68,R3),SPACES                                                 
*                                                                               
         LA    R3,12(R3)           POINT TO OUTPUT COL                          
         LA    R4,68               AND SET REMAINING COLS                       
*                                                                               
FLD10    DS    0H                                                               
         MVC   0(2,R3),0(R7)       FIELD NUMBER                                 
         EDIT  (R1),(2,2(R3)),FILL=0         LENGTH                             
         CR    R5,R4               IS THERE ROOM FOR ENTIRE FIELD?              
         BNH   FLD20               YES                                          
*                                                                               
         SH    R4,=H'6'            4 FOR OVERHEAD, 1 FOR EX, 1 FOR EOR          
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R3),0(R2)       MOVE IN AS MUCH AS WILL FIT                  
         LA    R4,1(R4)            R4 = NUMBER OF CHARACTERS MOVED              
         EDIT  (R4),(2,2(R3)),FILL=0         PARTIAL LENGTH                     
*                                                                               
         ZIC   R3,EIOPCCNT         WE NEED A NEW REQUEST CARD                   
         LA    R0,1(R3)            BUMP CARD COUNT                              
         STC   R0,EIOPCCNT         AND SAVE                                     
*                                                                               
         MH    R3,=H'80'                                                        
         LA    R3,EIOPCRDS(R3)     POINT TO NEW REQUEST                         
         MVC   0(12,R3),EIOPCRDS   MOVE REQ/AGY/SIN                             
         MVC   12(68,R3),SPACES                                                 
         LA    R3,12(R3)           POINT TO OUTPUT COL                          
*                                                                               
         LA    RF,0(R4,R2)         RF = A(TWA FLD DATA AT CONTINUATION)         
         SR    R1,R4               R1 = NUMBER OF CHARACTERS REMAINING          
         MVC   0(2,R3),0(R7)       FIELD NUMBER                                 
         EDIT  (R1),(2,2(R3)),FILL=0         PARTIAL LENGTH                     
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R3),0(RF)       MOVE IN REMAINDER OF FIELD                   
         LA    R1,6(R1)                                                         
         LA    R4,68               SET REMAINING COLS                           
         SR    R4,R1                                                            
         AR    R3,R1                                                            
         B     FLD30                                                            
*                                                                               
FLD20    BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   4(0,R3),0(R2)                                                    
         AR    R3,R5                                                            
         SR    R4,R5                                                            
*                                                                               
FLD30    DS    0H                                                               
         ST    R3,EIOPCPOS                                                      
         STC   R4,EIOPCLFT                                                      
*                                                                               
FIELDX   XIT1                                                                   
         DROP  R6                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* SYSINFO - GET SYSTEM INFO BASED ON ESS DATA                         *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    SYSTEM                                                           *         
*                                                                     *         
*  RETURNS:                                                           *         
*    OVSYS                                                            *         
*    SYS1CHR                                                          *         
*                                                                     *         
***********************************************************************         
                                                                                
SYSINFO  NTR1  BASE=*                                                           
         CLI   SYSTEM,C' '                                                      
         BE    SINFOK                                                           
*                                                                               
* GET OVSYS & SYS1CHR FROM FASYSLST                                             
         L     R1,=A(SYSLST)                                                    
         ZICM  RE,0(R1),2                                                       
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING SYSLSTD,R1                                                       
         CLC   SYSTEM,SYSLSHRT     MATCH ON SHORT NAME?                         
         BE    *+12                                                             
         BXLE  R1,RE,*-10                                                       
         B     SINFNO              INVALID SYSTEM                               
*                                                                               
         MVC   OVSYS,SYSLNUM                                                    
         MVC   SYS1CHR,SYSLRPLT                                                 
         B     SINFOK                                                           
         DROP  R1                                                               
*                                                                               
SINFOK   SR    RC,RC                                                            
SINFNO   LTR   RC,RC                                                            
SYSINFOX XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* READID - READ USER ID RECORD FROM CTFILE                            *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    USERID                                                           *         
*    OVSYS                                                            *         
*                                                                     *         
*  RETURNS:                                                           *         
*    AGYALPHA                                                         *         
*    USERIDNO                                                         *         
*    SYSSE                                                            *         
*    FILSET                                                           *         
*    PWRCODE                                                          *         
*    LOGOS                                                            *         
*    ORIGNADD                                                         *         
*    CLASS       (FROM GETCLASS)                                      *         
*                                                                     *         
***********************************************************************         
                                                                                
READID   NTR1  BASE=*                                                           
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING CTIREC,R3                                                        
         MVI   CTIKTYP,C'I'                                                     
         MVC   CTIKID,USERID                                                    
         DROP  R3                                                               
         GOTO1 ADATAMGR,DMCB,=C'DMREAD',=C'CTFILE',KEY,AIDREC,0                 
         CLI   DMCB+8,0                                                         
         BNE   RDIDNO                                                           
         MVI   DATADISP+1,28                                                    
                                                                                
* GET ID NUMBER                                                                 
*                                                                               
         L     R6,AIDREC                                                        
         MVI   ELCODE,X'02'                                                     
         GOTO1 =A(GETEL)                                                        
         BNE   RDIDNO                                                           
         MVC   USERIDNO,2(R6)                                                   
                                                                                
* GET AGYALPHA                                                                  
*                                                                               
         L     R6,AIDREC                                                        
         MVI   ELCODE,X'06'                                                     
         GOTO1 =A(GETEL)                                                        
         BNE   RDIDNO                                                           
         MVC   AGYALPHA,2(R6)                                                   
                                                                                
* GET LOGOS                                                                     
*                                                                               
         L     R6,AIDREC                                                        
         MVI   ELCODE,X'30'                                                     
         GOTO1 =A(GETEL)                                                        
         BNE   RDIDNO                                                           
         USING CTDSTD,R6                                                        
         MVC   LOGOS,CTDSTLG1                                                   
         MVC   PWRCODE,CTDSTPOW                                                 
         DROP  R6                                                               
                                                                                
* GET ORIGIN ADDRESS                                                            
*                                                                               
         MVC   ORIGNADD,SPACES                                                  
         L     R6,AIDREC                                                        
         MVI   ELCODE,X'36'                                                     
         GOTO1 =A(GETEL)                                                        
         BNE   *+10                                                             
         MVC   ORIGNADD,2(R6)                                                   
                                                                                
* GET SYS AUTH EL FOR SYSTEM                                                    
*                                                                               
         USING CTSYSD,R6                                                        
         L     R6,AIDREC                                                        
         CLI   SYSTEM,C' '                                                      
         BE    RDID20                                                           
         CLI   SYSTEM,0                                                         
         BE    RDID20                                                           
         MVI   ELCODE,X'21'                                                     
         GOTO1 =A(GETEL)                                                        
         B     RDID12                                                           
RDID10   GOTO1 =A(NEXTEL)                                                       
*                                                                               
RDID12   BNE   RDIDNO                                                           
         CLC   CTSYSNUM,OVSYS                                                   
         BNE   RDID10                                                           
         MVC   SYSSE,CTSYSSE                                                    
         DROP  R6                                                               
                                                                                
* GET FILSET FROM FASELIST                                                      
*                                                                               
         L     R1,=V(SELIST)                                                    
         ZICM  RE,0(R1),2                                                       
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING SELISTD,R1                                                       
         CLC   SYSSE,SESYS         FOUND MINOR SYSTEM?                          
         BE    *+12                                                             
         BXLE  R1,RE,*-10                                                       
         B     RDIDNO              INVALID SYSTEM                               
         MVC   FILSET,SEFILSET                                                  
*                                                                               
RDID20   BAS   RE,GETCLASS                                                      
         B     RDIDOK                                                           
*                                                                               
         DROP  R1                                                               
RDIDOK   SR    RC,RC                                                            
RDIDNO   LTR   RC,RC                                                            
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* GETCLASS - BUILD CLASS CARD                                         *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    SYS1CHR                                                          *         
*    FILSET                                                           *         
*                                                                     *         
*  RETURNS:                                                           *         
*    CLASS                                                            *         
*                                                                     *         
***********************************************************************         
                                                                                
GETCLASS NTR1                                                                   
*&&UK                                                                           
         MVC   CLASS,=C'A     '    DEFAULT                                      
         CLI   SYS1CHR,C'M'        MEDIA SYSTEM DEFAULT                         
         BNE   GCLSX                                                            
         MVI   CLASS,C'M'                                                       
*&&                                                                             
*&&US                                                                           
         MVC   CLASS(1),SYS1CHR                                                 
         ZICM  RE,FILSET,1                                                      
         BZ    *+14                                                             
         LA    RE,FILLET(RE)                                                    
         MVC   CLASS+1(1),0(RE)                                                 
*                                                                               
         CLI   SYS1CHR,C'C'        CONTROL                                      
         BNE   *+12                                                             
         MVI   CLASS+1,C'1'                                                     
         B     GCLS10                                                           
*                                                                               
         CLI   SYS1CHR,C'M'        MEDIA PLANNING                               
         BNE   *+12                                                             
         MVI   CLASS,C'L'                                                       
         B     GCLS10                                                           
*                                                                               
         CLI   SYS1CHR,C'N'        NETWORK                                      
         BNE   GCLS10                                                           
         MVI   CLASS,C'S'          SWITCH NETWORK INTO SPOT CLASS               
         CLI   CLASS+1,C'1'        GUESS WHAT SPOT?=NET?                        
         BNE   *+12                                                             
         MVI   CLASS+1,C'8'                                                     
         B     GCLS10                                                           
         CLI   CLASS+1,C'2'                                                     
         BNE   *+12                                                             
         MVI   CLASS+1,C'9'                                                     
         B     GCLS10                                                           
         CLI   CLASS+1,C'3'                                                     
         BNE   *+12                                                             
         MVI   CLASS+1,C'A'                                                     
         B     GCLS10                                                           
         CLI   CLASS+1,C'4'                                                     
         BNE   *+12                                                             
         MVI   CLASS+1,C'C'                                                     
         B     GCLS10                                                           
         CLI   CLASS+1,C'5'                                                     
         BNE   *+12                                                             
         MVI   CLASS+1,C'J'                                                     
         B     GCLS10                                                           
         CLI   CLASS+1,C'6'                                                     
         BNE   *+12                                                             
         MVI   CLASS+1,C'K'                                                     
         B     GCLS10                                                           
         CLI   CLASS+1,C'W'                                                     
         BNE   *+8                                                              
         MVI   CLASS+1,C'W'                                                     
GCLS10   MVC   CLASS+2(4),=C'SOON'                                              
*&&                                                                             
GCLSX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
FILLET   DC    C' 123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'                          
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* READSOON - READ SOON RECORD FROM GENDIR/GENFIL                      *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    SYS1CHR                                                          *         
*    PROGRAM                                                          *         
*                                                                     *         
*  RETURNS:                                                           *         
*    TIME                                                             *         
*    PRIORITY (IF NON-ZERO ON ENTRY)                                  *         
*    EXECMOD                                                          *         
*    SOONDESC                                                         *         
*                                                                     *         
***********************************************************************         
                                                                                
READSOON NTR1  BASE=*                                                           
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
         USING SOONKEYD,R1                                                      
         MVI   SOONKSY,SOONKSYQ                                                 
         MVI   SOONKTYP,SOONKTY2                                                
         MVC   SOONKSYS,SYS1CHR                                                 
         MVC   SOONKPRG,PROGRAM                                                 
         DROP  R1                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 ADATAMGR,DMCB,=C'DMREAD',=C'GENDIR',KEYSAVE,KEY                  
         CLI   DMCB+8,0            ANY ERRORS?                                  
         BNE   RDSONO                                                           
*                                                                               
         GOTO1 ADATAMGR,DMCB,=C'GETREC',=C'GENFIL',KEY+36,ASOONREC,    X        
               DMWORK                                                           
         CLI   DMCB+8,0            ANY ERRORS?                                  
         BNE   RDSONO                                                           
         MVI   DATADISP+1,42                                                    
                                                                                
* GET DESCRIPTION                                                               
*                                                                               
         L     R6,ASOONREC                                                      
         MVI   ELCODE,SNDSCCDQ                                                  
         GOTO1 =A(GETEL)                                                        
         BNE   RDSONO                                                           
*                                                                               
         ZIC   R1,1(R6)            L'ELEM                                       
         SH    R1,=Y(SNDSCOV)       - OVERHEAD                                  
         BCTR  R1,0                 - 1 FOR EX                                  
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   SOONDESC(0),SNDSCTXT-SNDSCD(R6)                                  
                                                                                
* GET ATTRIBUTES (LOAD MOD, TIME, PRIORITY)                                     
*                                                                               
         L     R6,ASOONREC                                                      
         MVI   ELCODE,SNATTCDQ                                                  
         GOTO1 =A(GETEL)                                                        
         BNE   RDSONO                                                           
         USING SNATTD,R6                                                        
*                                                                               
         MVC   EXECMOD,SNATTLM                                                  
         EDIT  SNATTTIM,(5,TIME),FILL=0                                         
         OC    PRIORITY,PRIORITY                                                
         BNZ   *+10                                                             
         MVC   PRIORITY,SNATTPRT                                                
         B     RDSOOK                                                           
         DROP  R6                                                               
RDSOOK   SR    RC,RC                                                            
RDSONO   LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* BLDPQ - BUILD PQ ENTRY                                              *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    JOBID     (FOR INITPQW)                                          *         
*    USERIDNO                                                         *         
*    AGYALPHA                                                         *         
*    TIME                                                             *         
*    PRIORITY                                                         *         
*    CLASS                                                            *         
*    SYSTEM                                                           *         
*    SYS1CHR                                                          *         
*    PROGRAM                                                          *         
*    EXECMOD                                                          *         
*    SOONDESC                                                         *         
*    LOGOS                                                            *         
*    REPKEY    (FROM INITPQW)                                         *         
*                                                                     *         
*  RETURNS:                                                           *         
*    REPKEY    (FROM INITPQW)                                         *         
*                                                                     *         
***********************************************************************         
                                                                                
BLDPQ    NTR1  BASE=*                                                           
         LA    R3,PRTLIN                                                        
         USING PQPLD,R3                                                         
         BAS   RE,INITPQW                                                       
*                                                                               
         LA    R4,SOONCARD                                                      
BLDPQ10  LA    R2,PRTLIN+1                                                      
         BAS   RE,CLRPQL                                                        
         SR    RE,RE                                                            
*                                                                               
         IC    RE,0(R4)            L'CONSTANT ENTRY                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),1(R4)                                                    
         LA    R2,1(RE,R2)         NEXT SPACE ON PRTLIN                         
         LA    R4,2(RE,R4)                                                      
         ICM   RE,1,0(R4)          L'VARIABLE ENTRY                             
         BZ    BLDPQ20                                                          
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),1(R4)                                                    
         LA    R4,2(RE,R4)         NEXT FIXED ENTRY                             
         B     *+8                                                              
BLDPQ20  LA    R4,1(R4)                                                         
         BAS   RE,PUTLINE                                                       
*                                                                               
         LA    RF,SOONEND          TRAP LOOPS                                   
         CR    R4,RF                                                            
         BH    BLDPQNO                                                          
         CLI   0(R4),X'FF'         EOT?                                         
         BNE   BLDPQ10                                                          
         EJECT                                                                  
* PUT OUT CONTROLLER CARDS                                                      
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(12),=C'$$CONTROLLER'                                    
         BAS   RE,PUTLINE                                                       
         BAS   RE,CLRPQL                                                        
         MVI   IF,C'Y'                                                          
*                                                                               
         MVI   DATADISP+1,42                                                    
         L     R6,ASOONREC                                                      
         USING SNCRDD,R6                                                        
         MVI   ELCODE,SNCRDCDQ                                                  
         GOTO1 =A(GETEL)                                                        
         B     BLDPQ32                                                          
BLDPQ30  GOTO1 =A(NEXTEL)                                                       
*                                                                               
BLDPQ32  BNE   BLDPQ40                                                          
         ZIC   R1,SNCRDLEN         L'ELEM                                       
         SH    R1,=Y(SNCRDOV)       - OVERHEAD                                  
         BCTR  R1,0                 - 1 FOR EX                                  
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRTLIN+1(0),SNCRDTXT                                             
*                                                                               
         CLC   PRTLIN+1(13),=C'REQUESTS HERE'                                   
         BE    BLDPQ40                                                          
*                                                                               
         CLC   PRTLIN+1(6),=C'IF END' IF CONDITIONS                             
         BNE   *+12                                                             
         MVI   IF,C'Y'                                                          
         B     BLDPQ30                                                          
*                                                                               
         CLC   PRTLIN+1(2),=C'IF'                                               
         BNE   *+12                                                             
         MVI   IF,C'N'                                                          
         B     IFTEST                                                           
*                                                                               
         CLC   PRTLIN+1(3),=C'OR '                                              
         BE    IFTEST                                                           
         CLC   PRTLIN+1(3),=C'AND'                                              
         BNE   BLDPQ35                                                          
         CLI   IF,C'N'                                                          
         BE    BLDPQ30                                                          
         MVC   PRTLIN+1(79),PRTLIN+1+1 CHANGE AND TO ND                         
         MVI   IF,C'N'                                                          
         B     IFTEST                                                           
*                                                                               
BLDPQ35  CLI   IF,C'N'                                                          
         BE    BLDPQ30                                                          
         GOTO1 =V(REPLACE),DMCB,(80,PRTLIN+1),SOFTNTRY                          
         BAS   RE,PUTLINE                                                       
         BAS   RE,CLRPQL                                                        
         B     BLDPQ30                                                          
         DROP  R6                                                               
         EJECT                                                                  
*                                                                               
* OUTPUT SSPPAA                                                                 
BLDPQ40  DS    0H                                                               
         BAS   RE,CLRPQL                                                        
         MVI   PRTLIN+3,X'F0'                                                   
         MVC   PRTLIN+4(2),PROGRAM                                              
         MVC   PRTLIN+6(2),AGYALPHA                                             
         MVC   PRTLIN+1(2),=C'MP'  PLANNING/BUDGET                              
         CLI   OVSYS,X'05'                                                      
         BE    BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'MB'  MEDIABASE                                    
         CLI   OVSYS,X'09'                                                      
         BE    BLDPQ60                                                          
*&&UK                                                                           
         MVC   PRTLIN+1(2),=C'FE'  ARTISTE FEES                                 
         CLI   OVSYS,X'07'                                                      
         BE    BLDPQ60                                                          
*&&                                                                             
         MVC   PRTLIN+1(2),=C'PE'  PERSON                                       
         CLI   OVSYS,X'0E'                                                      
         BE    BLDPQ50                                                          
*&&US                                                                           
         MVC   PRTLIN+1(2),=C'ST'  SPOT TRAFFIC                                 
         CLI   OVSYS,X'0D'                                                      
         BE    BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'SP'  SPOT                                         
         CLI   OVSYS,X'02'                                                      
         BE    BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'NE'  NETWORK GETS ITS OWN FOR NOW                 
         CLI   OVSYS,X'03'                                                      
         BE    BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'PP'  PRINT                                        
         CLI   OVSYS,X'04'                                                      
         BE    BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'RE'  REP                                          
         CLI   OVSYS,X'08'                                                      
         BE    BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'TA'  TALENT                                       
         CLI   OVSYS,X'07'                                                      
         BE    BLDPQ60                                                          
*&&                                                                             
         MVC   PRTLIN+1(2),=C'CT'  CONTROL                                      
         CLI   OVSYS,X'0A'                                                      
         BNE   *+14                                                             
         MVC   PRTLIN+3(10),PRTLIN+4                                            
         B     BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'AC'  ACCPAK                                       
         B     *+10                *NOP*                                        
BLDPQ50  MVC   PRTLIN+6(2),SPACES                                               
         CLI   OVSYS,X'06'                                                      
         BE    BLDPQ60                                                          
*&&UK                                                                           
         MVC   PRTLIN+3(10),PRTLIN+4 MEDLINE                                    
         CLI   OVSYS,X'04'                                                      
         BNE   BLDPQ60                                                          
         MVC   PRTLIN+1(2),=C'ME'                                               
*&&                                                                             
BLDPQ60  BAS   RE,PUTLINE                                                       
         EJECT                                                                  
* OUTPUT LOGO & ORIGIN                                                          
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(5),=C'LOGO='                                            
         MVC   PRTLIN+6(14),LOGOS                                               
         MVC   PRTLIN+20(4),PWRCODE                                             
         MVC   PRTLIN+24(1),SYS1CHR                                             
         MVC   PRTLIN+25(2),PROGRAM                                             
*                                                                               
         CLI   OVSYS,X'05'         MPL?                                         
         BNE   *+8                                                              
         MVI   PRTLIN+24,C'L'                                                   
         CLI   OVSYS,X'09'   '     MBASE?                                       
         BNE   *+8                                                              
         MVI   PRTLIN+24,C'B'                                                   
*                                                                               
         MVI   PRTLIN+27,C'S'      S=SOON                                       
         CLI   PRTLIN+23,C' '      CATER FOR 3 CHR AGY ID                       
         BNE   BLDPQ70                                                          
         MVI   PRTLIN+23,C'X'                                                   
         CLI   PRTLIN+22,C' '      ALSO FOR 2 CHR                               
         BNE   BLDPQ70                                                          
         MVI   PRTLIN+22,C'X'                                                   
*                                                                               
BLDPQ70  MVC   PRTLIN+34(7),=C'ORIGIN='                                         
         ZICM  R1,USERIDNO,2                                                    
         LA    RF,PRTLIN+41                                                     
         EDIT  (R1),(5,(RF)),FILL=0                                             
         BAS   RE,PUTLINE                                                       
*                                                                               
         CLC   ORIGNADD,SPACES                                                  
         BE    BLDPQ80                                                          
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(7),=C'ORIGIN='                                          
         MVC   PRTLIN+8(66),ORIGNADD                                            
         BAS   RE,PUTLINE                                                       
         EJECT                                                                  
* OUTPUT INPUT & FACPAK                                                         
*                                                                               
BLDPQ80  BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(10),=C'INPUT=CARD'                                      
         BAS   RE,PUTLINE                                                       
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(8),=C'FACPAK=T'                                         
         BAS   RE,PUTLINE                                                       
         EJECT                                                                  
* OUTPUT DIRECT AND RETAIN CARDS                                                
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(16),=C'DIRECT=SPP,1S  ,'                                
         MVC   PRTLIN+8(3),=C'ESS'                                              
         MVI   PRTLIN+22,C','                                                   
         MVI   PRTLIN+28,C','                                                   
         MVI   PRTLIN+30,C','                                                   
         MVI   PRTLIN+32,C','                                                   
         MVI   PRTLIN+34,C','                                                   
         MVI   PRTLIN+56,C','                                                   
         MVC   PRTLIN+57(11),JOBID                                              
         MVI   PRTLIN+68,C','                                                   
*                                                                               
         XC    PROFKEY,PROFKEY     SET PARAM LIST FOR PQPROF                    
         MVC   PROFKEY(1),SYS1CHR  PROFILE RECORD KEY SYSTEM/PROGRAM            
         MVC   PROFKEY+1(2),PROGRAM                                             
         MVC   PROFKEY+3(2),USERIDNO                                            
         GOTO1 =V(PQPROF),DMCB,PROFKEY,0,COMFACS                                
         CLI   8(R1),0                                                          
         BNE   BLDPQ90             IO ERROR                                     
*                                                                               
         L     RF,4(R1)            PICK UP A(DATA) IN REMOTED FORMAT            
         LA    RF,0(RF)            AND FILL IN DIRECT= PRTLIN                   
         USING REMOTED,RF                                                       
         OC    REMOTSUB,REMOTSUB   REPORT 2 CHARACTER SUB CODE                  
         BZ    *+10                                                             
         MVC   PRTLIN+69(2),REMOTSUB                                            
         OC    REMOTFNO,REMOTFNO   REPORT FORMS CODE                            
         BZ    *+10                                                             
         MVC   PRTLIN+12(4),REMOTFNO                                            
         OC    REMOTCPY,REMOTCPY   NUMBER OF COPIES                             
         BZ    *+10                                                             
         MVC   PRTLIN+33(1),REMOTCPY                                            
         MVC   SVREMOTE,REMOTRET   SAVE PQ RETENTION CLASS                      
         MVI   PRTLIN+29,C' '      ALWAYS PUT BLANK INTO PQCLASS                
         MVI   PRTLIN+33,C'1'      TEMP FIX (ALWAYS GENERATE ONE COPY)          
         DROP  RF                                                               
*                                                                               
BLDPQ90  DS    0H                                                               
         LA    R1,USERIDNO                                                      
         LA    RF,PRTLIN+17                                                     
         EDIT  (2,(R1)),(5,(RF)),FILL=0                                         
*                                                                               
         MVC   PRTLIN+35(3),=C'ID='                                             
         GOTO1 AHEXOUT,DMCB,REPKEY,PRTLIN+38,9,=C'TOG'                          
*                                                                               
         BAS   RE,PUTLINE                                                       
*                                                                               
         CLI   SVREMOTE,0                                                       
         BE    BLDPQ100                                                         
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(7),=C'RETAIN=' SET RETAIN=X IF REMOTRET SET             
         MVC   PRTLIN+8(1),SVREMOTE                                             
         BAS   RE,PUTLINE                                                       
         EJECT                                                                  
* OUTPUT EZCTL= EDICT CONTROL HEADER CARD, PASSES ESSID TO EDICT SENDER         
*                                                                               
BLDPQ100 DS    0H                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(6),=C'EZCTL='                                           
         MVC   PRTLIN+7(4),=C'*DDS'                                             
         MVC   PRTLIN+11(5),=C'*HDR*'                                           
         MVC   PRTLIN+16(6),=C'EDICT='                                          
         MVC   PRTLIN+22(8),ESSNAME                                             
         BAS   RE,PUTLINE                                                       
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(6),=C'EZCTL='                                           
         MVC   PRTLIN+7(5),=C'++DDS'                                            
         MVC   PRTLIN+13(1),SYS1CHR                                             
         MVC   PRTLIN+14(3),=C'REQ'                                             
         MVC   PRTLIN+18(3),=C'TRN'                                             
         BAS   RE,PUTLINE                                                       
                                                                                
* ADD CONTROL CARD TERMINATION LINE                                             
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(2),=C'/*'                                               
         BAS   RE,PUTLINE                                                       
         EJECT                                                                  
* ADD RFHDR, & REQUEST CARDS                                                    
*                                                                               
         L     RF,ADRCARDS                                                      
         ZIC   R0,CARDCT                                                        
BLDPQ110 BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(80),0(RF)                                               
         TM    REQCARD,RCFLD       DID WE BUILD REQUEST CARD?                   
         BZ    *+10                 NO                                          
         MVC   PRTLIN+1+2(2),AGYALPHA  ELSE ADD AGYALPHA NOW                    
         BAS   RE,PUTLINE                                                       
         LA    RF,L'CARDS(RF)                                                   
         BCT   R0,BLDPQ110                                                      
                                                                                
* ADD END-OF-REQUEST INDICATOR                                                  
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(2),=C'**'                                               
         BAS   RE,PUTLINE                                                       
                                                                                
* END OF CONTROLLER CARDS                                                       
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(15),=C'$$CONTROLLEREND'                                 
         BAS   RE,PUTLINE                                                       
                                                                                
* END OF CARDS                                                                  
*                                                                               
         BAS   RE,CLRPQL                                                        
         MVC   PRTLIN+1(05),=C'$$END'                                           
         BAS   RE,PUTLINE                                                       
                                                                                
* CLOSE PQFILE                                                                  
*                                                                               
         MVC   PRTLIN,SPACES                                                    
         MVI   PLCC,X'FF'                                                       
         GOTO1 ADATAMGR,DMCB,=C'CLO/JOB',=C'PRTQUE',0,PQPLD,AEPQBUFF            
         B     BLDPQOK                                                          
*                                                                               
BLDPQOK  SR    RC,RC                                                            
BLDPQNO  LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
CLRPQL   MVC   PRTLIN,SPACES                                                    
         MVI   PLCC,X'09'          WHAT DOES THIS MEAN???                       
         BR    RE                                                               
*                                                                               
PUTLINE  NTR1                                                                   
         MVC   P(8),=CL8'PQ LINE:'                                              
         MVC   P+10(100),PRTLIN                                                 
         GOTO1 AESSLOG,DMCB,AESSATCB,P                                          
         GOTO1 ADATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,PQPLD,AEPQBUFF            
         XIT1                                                                   
         EJECT                                                                  
*CHECK IF CONDITIONS                                                            
*                                                                               
IFTEST   CLC   PRTLIN+1+3(7),=C'AGENCY='                                        
         BE    IFT2                                                             
         CLC   PRTLIN+1+3(12),=C'DESTINATION='                                  
         BE    IFT4                                                             
         CLC   PRTLIN+1+3(8),=C'HEXCOMP='                                       
         BE    IFT6                                                             
         CLC   PRTLIN+1+3(7),=C'OUTPUT='                                        
         BE    IFT8                                                             
         CLC   PRTLIN+1+3(6),=C'SUBID='                                         
         BE    IFT10                                                            
         CLC   PRTLIN+1+3(6),=C'FACID='                                         
         BE    IFT12                                                            
         B     IFX                                                              
*                                                                               
IFT2     CLC   PRTLIN+1+10(2),AGYALPHA                                          
         BNE   *+8                                                              
         MVI   IF,C'Y'                                                          
         B     IFX                                                              
*                                                                               
IFT4     MVC   DUB(2),USERIDNO                                                  
         LH    R0,DUB                                                           
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(6),DUB                                                      
         CLC   WORK(6),PRTLIN+1+15                                              
         BNE   *+8                                                              
         MVI   IF,C'Y'                                                          
         B     IFX                                                              
*                                                                               
IFT6     GOTO1 AHEXOUT,DMCB,AGYALPHA,DUB,1                                      
         CLC   PRTLIN+1+11(2),DUB                                               
         BNE   *+8                                                              
         MVI   IF,C'Y'                                                          
         B     IFX                                                              
*                                                                               
IFT8     DS    0H                                                               
*IFT8    L     RF,AREQCRDS         POINT TO REQUEST CARDS                       
*        LA    R0,REQEOH-REQOUT    GET DISPLACEMENT IN HEADER                   
*        LCR   R0,R0                                                            
*        AR    RF,R0               RF NOW POINTS TO OUTPUT                      
*        CLC   PRTLIN+1+10(6),0(RF)                                             
*        BNE   *+8                                                              
*        MVI   IF,C'Y'                                                          
         B     IFX                                                              
*                                                                               
IFT10    DS    0H                                                               
*IFT10   CLC   PRTLIN+1+9(3),SPOOKDID                                           
*        BNE   *+8                                                              
*        MVI   IF,C'Y'                                                          
         B     IFX                                                              
*                                                                               
IFT12    DS    0H                                                               
*IFT12   CLC   PRTLIN+1+9(1),FACID                                              
*        BNE   *+8                                                              
*        MVI   IF,C'Y'                                                          
         B     IFX                                                              
*                                                                               
IFX      BAS   RE,CLRPQL                                                        
         B     BLDPQ30                                                          
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                     *         
* INITPQW - OPEN PQ ENTRY FOR WRITE                                   *         
*                                                                     *         
*  REQUIRES:                                                          *         
*    USERIDNO                                                         *         
*    JOBID                                                            *         
*    AGYALPHA                                                         *         
*    PROGRAM                                                          *         
*    SYSTEM                                                           *         
*    LOGOS                                                            *         
*    USERIDNO                                                         *         
*                                                                     *         
*  RETURNS:                                                           *         
*    REPKEY                                                           *         
*    SOFTWORK                                                         *         
*    REPSUBID                                                         *         
*    REPRNO                                                           *         
*    REPRCI                                                           *         
*    REPCLASS                                                         *         
*                                                                     *         
***********************************************************************         
                                                                                
INITPQW  NTR1                                                                   
         LA    R3,PRTLIN                                                        
         USING PQPLD,R3                                                         
         XC    PRTLIN,PRTLIN                                                    
         MVI   PLCC,X'00'          SET START OF REPORT                          
         MVI   QLEXTRA,X'FF'       SET EXTRA DATA PASSED                        
         MVC   QLSRCID,USERIDNO                                                 
         MVC   QLSUBID,=C'ESS'                                                  
         MVI   QLCLASS,C'Q'                                                     
         MVI   QLTYPE,0                                                         
         OI    QLATTB,QLATJOBI     REPORT CONTAINS JCL                          
         MVC   QLDESC,JOBID                                                     
         MVC   QLRETNL,=AL2(12)                                                 
         MVC   QLRETND,=AL2(03)                                                 
         GOTO1 ADATAMGR,DMCB,=C'DMPRINT',=C'PRTQUE',0,PQPLD,AEPQBUFF            
         CLI   8(R1),0             TEST OK                                      
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   REPKEY+00(2),QLSRCID                                             
         MVC   REPKEY+02(3),QLSUBID                                             
         MVC   REPKEY+05(2),QLREPRNO                                            
         MVC   REPKEY+07(2),QLREPRCI                                            
         MVC   REPKEY+09(1),QLCLASS                                             
         MVC   REPSUBID,QLSUBID                                                 
         MVC   REPRNO,QLREPRNO                                                  
         MVC   REPRCI,QLREPRCI                                                  
         MVC   REPCLASS,QLCLASS                                                 
                                                                                
* SET UP SOFT TABLE ENTRIES                                                     
*                                                                               
         GOTO1 ADATCON,DMCB,(5,0),(10,TODAY)                                    
         LA    RF,SOFTABSZ                                                      
         ST    RF,SOFTNTRY                                                      
         L     R2,=A(SOFTABLE)                                                  
         LA    R3,SOFTWORK                                                      
SETSOFT2 CLI   0(R2),0                                                          
         BE    OPENPQX                                                          
         MVC   0(02,R3),0(R2)                                                   
         MVC   2(15,R3),5(R2)                                                   
         SR    RF,RF                                                            
         ICM   RF,7,2(R2)                                                       
*         A     RF,RELO            ??                                           
         MVC   WORK(15),SPACES                                                  
         BASR  RE,RF                                                            
         MVC   17(15,R3),WORK                                                   
         LA    R2,L'SOFTABLE(R2)                                                
         LA    R3,L'SOFTWORK(R3)                                                
         B     SETSOFT2                                                         
*                                                                               
SETAGY   MVC   WORK(2),AGYALPHA                                                 
         BR    RE                                                               
SETCMP   MVC   WORK(1),AGYALPHA                                                 
         BR    RE                                                               
SETLDG   MVC   WORK(1),AGYALPHA+1                                               
         BR    RE                                                               
SETREP   MVC   WORK(2),AGYALPHA                                                 
         BR    RE                                                               
SETDAT   MVC   WORK(8),TODAY                                                    
         BR    RE                                                               
SETPRG   MVC   WORK(2),PROGRAM                                                  
         BR    RE                                                               
SETSYS   MVC   WORK(2),SYSTEM                                                   
         BR    RE                                                               
SETLG1   MVC   WORK(7),LOGOS                                                    
         BR    RE                                                               
SETLG2   MVC   WORK(7),LOGOS+7                                                  
         BR    RE                                                               
SETORG   SR    R0,R0                                                            
         ICM   R0,3,USERIDNO                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(6),DUB                                                      
         BR    RE                                                               
SETDAY   DS    0H                                                               
*&&UK*&& MVC   WORK(2),TODAY                                                    
*&&US*&& MVC   WORK(2),TODAY+3                                                  
         BR    RE                                                               
SETHEX   LR    R0,RE                                                            
         GOTO1 AHEXOUT,DMCB,AGYALPHA,WORK,1                                     
         LR    RE,R0                                                            
         BR    RE                                                               
OPENPQX  XIT1                                                                   
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
                                                                                
GETEL    NTR1  BASE=*                                                           
         AH    R6,DATADISP                                                      
GTEL010  CLI   0(R6),0                                                          
         BE    GTELNO                                                           
         CLI   ELCODE,0                                                         
         BE    GTELOK                                                           
         CLC   ELCODE,0(R6)                                                     
         BE    GTELOK                                                           
         SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     GTEL010                                                          
GTELOK   SR    RC,RC                                                            
GTELNO   LTR   RC,RC                                                            
         XIT1  REGS=(R6)                                                        
         LTORG                                                                  
*                                                                               
NEXTEL   NTR1  BASE=*                                                           
         B     NTEL020                                                          
NTEL010  CLI   0(R6),0                                                          
         BE    NTELNO                                                           
         CLI   ELCODE,0                                                         
         BE    NTELOK                                                           
         CLC   ELCODE,0(R6)                                                     
         BE    NTELOK                                                           
NTEL020  SR    RF,RF                                                            
         IC    RF,1(R6)                                                         
         AR    R6,RF                                                            
         B     NTEL010                                                          
NTELOK   SR    RC,RC                                                            
NTELNO   LTR   RC,RC                                                            
         XIT1  REGS=(R6)                                                        
         LTORG                                                                  
                                                                                
         DS    0D                                                               
SOFTABLE DS    0XL20                                                            
         DC    AL1(7,2),AL3(SETAGY),CL15'&&AGENCY'                              
         DC    AL1(8,1),AL3(SETCMP),CL15'&&COMPANY'                             
         DC    AL1(7,1),AL3(SETLDG),CL15'&&LEDGER'                              
         DC    AL1(4,2),AL3(SETREP),CL15'&&REP'                                 
         DC    AL1(4,8),AL3(SETDAT),CL15'&&IPL'                                 
         DC    AL1(8,2),AL3(SETPRG),CL15'&&PROGRAM'                             
         DC    AL1(7,2),AL3(SETSYS),CL15'&&SYSTEM'                              
         DC    AL1(6,7),AL3(SETLG1),CL15'&&LOGO1'                               
         DC    AL1(6,7),AL3(SETLG2),CL15'&&LOGO2'                               
         DC    AL1(7,6),AL3(SETORG),CL15'&&ORIGIN'                              
         DC    AL1(4,2),AL3(SETDAY),CL15'&&DAY'                                 
         DC    AL1(8,2),AL3(SETHEX),CL15'&&HEXCOMP'                             
SOFTABSZ EQU   (*-SOFTABLE)/L'SOFTABLE                                          
         DC    AL1(0)                                                           
                                                                                
         EJECT                                                                  
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
PARAS    DS    10F                                                              
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
SAVERD   DS    D                                                                
FULL     DS    F                                                                
APARM    DS    A                                                                
RELO     DS    A                                                                
AESSATCB DS    A                                                                
ALSTRQEL DS    A                                                                
ALSTIQEL DS    A                                                                
PARM     DS    0XL(8*4)                                                         
         DS    XL(7*4)                                                          
HALF     DS    H                                                                
BYTE     DS    XL1                                                              
WORK     DS    CL256                                                            
RQSWORK  DS    CL256                                                            
BLOCK    DS    CL256                                                            
ESSID    DS    CL8                                                              
ERRFLAG  DS    CL1                                                              
ESSNAME  DS    CL8                                                              
ESSIDNUM DS    XL2                                                              
FTPFLAG  DS    CL1                                                              
COMFLAG  DS    CL1                                                              
ELCODE   DS    X                                                                
DATADISP DS    H                                                                
MDACTION DS    XL2                                                              
*                                                                               
AGYALPHA DS    CL2                 AGENCY ALPHA CODE                            
USERID   DS    CL10                UID FROM ESS ??                              
USERIDNO DS    XL2                 USER ID NUMBER                               
JOBID    DS    CL11                JOB ID OR ESS REFERENCE NUMBER               
OVSYS    DS    X                   SYSTEM MAJOR NUMBER                          
SYSSE    DS    X                   SYSTEM MINOR NUMBER                          
SYSTEM   DS    CL3                 SHORT SYSTEM NAME  ??                        
FILSET   DS    X                                                                
LOGOS    DS    CL14                                                             
PWRCODE  DS    CL4                                                              
ORIGNADD DS    CL66                                                             
SVREMOTE DS    C                   PQ RETENTION CLASS FROM PQPROF               
REPSUBID DS    CL3                                                              
REPRNO   DS    XL2                                                              
REPRCI   DS    XL2                                                              
REPCLASS DS    CL1                                                              
REPKEY   DS    XL10                REPORT KEY (FOR ID= ON DIRECT CARD)          
*                                                                               
SOONCARD DS    CL11                                                             
         DS    CL7                                                              
TIME     DS    CL5                                                              
         DS    CL11                                                             
PRIORITY DS    CL1                                                              
         DS    CL8                                                              
CLASS    DS    CL6                                                              
         DS    CL9                                                              
SYS1CHR  DS    CL1                                                              
         DS    CL10                                                             
PROGRAM  DS    CL2                                                              
         DS    CL7                                                              
EXECMOD  DS    CL8                                                              
         DS    CL14                                                             
SOONDESC DS    CL24                                                             
         DS    CL14                                                             
SOONEND  DS    CL1                                                              
*                                                                               
CARDCT   DS    X                   NUMBER OF REQUEST CARDS                      
CARDPOS  DS    A                   A(CURRENT CARD POSN)                         
CARDLEFT DS    X                   COLS REMAINING ON CARD                       
*                                                                               
* FLAGS FOR REQUIRED CARDS RECEIVED (RCALLREQ = ALL REQUIRED CARDS)             
*                                                                               
REQCARD  DS    X                   BITS FOR REQUIRED DATA FROM ESS              
RCALLREQ EQU   RCUID+RCSYS+RCPGM+RCEID+RCDATA                                   
RCUID    EQU   X'80'               UID RECV'D                                   
RCSYS    EQU   X'40'               SYS RECV'D                                   
RCPGM    EQU   X'20'               PGM RECV'D                                   
RCEID    EQU   X'10'               EID RECV'D                                   
RCDATA   EQU   X'08'               RQT OR FLD RECV'D                            
RCRQT    EQU   X'04'               RQT RECV'D                                   
RCFLD    EQU   X'02'               FLD RECV'D                                   
*                                                                               
MVSTIME  DS    F                   IBM TIME BINARY 100S SECS                    
MVSDATE  DS    F                   IBM DATE JULIAN                              
*                                                                               
CENTURY  DS    CL2                 CURRENT CENTURY EBCDIC VALUE                 
DATEN    DS    CL8                 DATE NOW EBCDIC                              
DATENC   DS    XL2                 DATE NOW COMPRESSED                          
DATENB   DS    XL3                 DATE NOW BINARY                              
TIMEN    DS    CL8                 TIME NOW EBCDIC (HHMMSSTH)                   
TIMEND   DS    XL4                 TIME NOW DECIMAL UNSIGNED PACKED             
TIMENB   DS    F                   TIME NOW BINARY FULL WORD                    
*                                                                               
NUMPAGES DS    A                   NUMBER OF PAGES                              
NUMLINES DS    A                   NUMBER OF LINE                               
TODAY    DS    CL8                 DATE C'DD/MM/YY' OR C'MM/DD/YY'              
IF       DS    C                                                                
DSTNAME  DS    CL8                                                              
DSTUIDN  DS    XL2                                                              
PRTLIN   DS    CL133                                                            
         DS    0H                                                               
KEY      DS    CL48                                                             
KEYSAVE  DS    CL48                                                             
PROFKEY  DS    CL5                                                              
COMFACS  DS    CL256                                                            
*                                                                               
VSAMERR  DS    XL1                                                              
         DS    0D                                                               
VSAMKEY  DS    XL(ERQKEYQ)                                                      
         DS    0D                                                               
REQAREA  DS    XL(ERQLENQ)                                                      
*                                                                               
         DS    0F                                                               
SOFTNTRY DS    AL4                                                              
SOFTWORK DS    (SOFTABSZ)XL32                                                   
*                                                                               
R        DS    CL250               PQ LINE DATA                                 
*                                                                               
P        DS    CL132               ESSLOG PRINT LINE                            
*                                                                               
PRTLEN   DS    XL2                 PRINT LINE LENGTH                            
*                                                                               
AEPQHDR  DS    A                                                                
AEEDHDR  DS    A                                                                
AEPQBUFF DS    A                                                                
AR       DS    A                                                                
ASOONREC DS    A                                                                
AIDREC   DS    A                                                                
ADRCARDS DS    A                                                                
*                                                                               
WTOMSG   DS    0CL128              WTO MESSAGE AREA                             
WTOMSGK  DS    CL28                KEY PART                                     
WTOMSGD  DS    CL100               DETAIL PART                                  
*                                                                               
CARDS    DS    (CARDMAX)CL80       ROOM FOR 20 REQUEST CARDS                    
CARDMAX  EQU   20                                                               
*                                                                               
         DS    0D                                                               
IDREC    DS    CL1000              I/O AREA FOR ID RECORD                       
*                                                                               
         DS    0D                                                               
SOONREC  DS    CL2000              I/O AREA FOR SOON REC                        
*                                                                               
WORKX    EQU   *                                                                
*                                                                               
         EJECT                                                                  
       ++INCLUDE DDESSD                                                         
         EJECT                                                                  
       ++INCLUDE DDEIOPQRD                                                      
         EJECT                                                                  
       ++INCLUDE FASYSLSTD                                                      
         EJECT                                                                  
       ++INCLUDE DXSUBLSTD                                                      
         EJECT                                                                  
         PRINT OFF                                                              
*DNPRTQD                                                                        
       ++INCLUDE DNPRTQD                                                        
*DMPRTQK                                                                        
       ++INCLUDE DMPRTQK                                                        
*DMPRTQL                                                                        
       ++INCLUDE DMPRTQL                                                        
*DMPRTQS                                                                        
       ++INCLUDE DMPRTQS                                                        
                                                                                
       ++INCLUDE CTGENFILE                                                      
                                                                                
       ++INCLUDE FASELIST                                                       
                                                                                
       ++INCLUDE DDREMOTED                                                      
                                                                                
       ++INCLUDE CTGENSOON                                                      
                                                                                
       ++INCLUDE DDCOMFACS                                                      
                                                                                
       ++INCLUDE CTMADEQUS                                                      
                                                                                
       ++INCLUDE DDGETRETD                                                      
                                                                                
       ++INCLUDE GEGENESS                                                       
                                                                                
         PRINT OFF                                                              
         DCBD  DSORG=PS,DEVD=DA                                                 
         EJECT                                                                  
         IEFZB4D2                                                               
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDEIOPQR  01/24/13'                                      
         END                                                                    
