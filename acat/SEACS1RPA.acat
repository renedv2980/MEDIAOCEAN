*          DATA SET SEACS1RPA  AT LEVEL 065 AS OF 01/24/01                      
*CATALP SEC1RP                                                                  
         TITLE 'SEC1RP - 1RP PERSON ACCOUNT INTERFACE FOR SECURITY'             
**********************************************************************          
* SEC1RP - 1RP PERSON ACCOUNT INTERFACE FOR SECURITY SYSTEM          *          
* PARAMETERS:                                                        *          
*                                                                    *          
* AL4 A(WORKD GENERAL WORK AREA)                                     *          
*                                                                    *          
* AL1 INDICATOR BYTE.                                                *          
*     ENTRY:                                                         *          
*     VALUE=0 - DISPLAY ACCOUNT CODE FUNCTION                        *          
*     VALUE=1 - VALIDATE ACCOUNT CODE FUNCTION                       *          
*     VALUE=2 - CHECK TO SEE IF TIME EXISTS                          *          
*     VALUE=3 - DELETE SWITCH LIST                                   *          
*     RETURN:                                                        *          
*     VALUES:  0 - OK, ELSE IO ERROR CODE                            *          
* AL3 ADR ACCOUNT CODE RECORD KEY VALUES                             *          
*                                                                    *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
SEC1RP   CSECT                                                                  
         NMOD1 RWRKX-RWRKD,*SEC1RP*,CLEAR=YES,RR=RA                             
         USING RWRKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         B     INIT                                                             
         SPACE 1                                                                
**********************************************************************          
* INITIALISE WORK AREAS                                              *          
**********************************************************************          
         SPACE 1                                                                
INIT     EQU   *                                                                
         MVC   PARM,0(R1)                                                       
         ICM   R7,15,AWORKD                                                     
         USING WORKD,R7                                                         
*                                                                               
         CLI   FUNCTION,DELSWTCH   DELETE SWITCH LIST                           
         BE    PROCSWT                                                          
         CLI   FUNCTION,CHKTIME    CHECK IF TIME EXISTS                         
         BE    PROCPID                                                          
         MVI   RETCODE,RETOKQ                                                   
         B     PROCESS                                                          
*                                                                               
EXIT     L     R1,APARM            EXIT WITH UPDATED PARAMETER LIST             
         MVC   0(L'PARM,R1),PARM                                                
         XMOD1 1                                                                
         EJECT                                                                  
**********************************************************************          
* PROCESS 1R @PERSON ACCOUNT RECORD                                  *          
**********************************************************************          
         SPACE 1                                                                
PROCESS  EQU   *                                                                
         LA    R2,IOKEY                                                         
         USING ACTRECD,R2          R2=A(1R PERSON ACCOUNT RECORD)               
         XC    ACTKEY,ACTKEY                                                    
         SR    R1,R1                                                            
         ICM   R1,7,A1RPKEY                                                     
         MVC   ACTKEY(ACTKACT+L'ACTKACT-ACTKEY),0(R1)                           
         LA    R2,RIO              READ ACCDIR FILE                             
         ST    R2,IOADDR                                                        
         LA    R1,IOHIGH+IOCONACD                                               
         GOTO1 AIO                                                              
         BE    *+12                                                             
         MVI   RETCODE,RETERRQ                                                  
         B     EXIT                                                             
*                                                                               
         LA    R2,RIO              READ ACCMST FILE                             
         ST    R2,IOADDR                                                        
         GOTO1 AIO,IORD+IOCONACM                                                
         BE    *+6                                                              
         DC    H'00'                                                            
         LA    R3,ACTRFST          GET ELEMENT DATA                             
         CLI   FUNCTION,VALIDQ     CHECK FUNCTION VLIDATE                       
         BE    PROCVAL                                                          
         CLI   FUNCTION,DISPLAYQ   CHECK FUNCTION DISPLAY                       
         BE    PROCDIS                                                          
         DC    H'00'                                                            
         EJECT                                                                  
**********************************************************************          
* VALIDATE 1R PERSON ACCOUNT RECORD                                  *          
**********************************************************************          
         SPACE 1                                                                
PROCVAL  EQU   *                                                                
         MVI   RBYTE,0             FLAG FOR ABLEL ELEMENT FOUND                 
PVAL010  CLI   0(R3),0                                                          
         BE    PVAL100                                                          
         CLI   0(R3),ABLELQ                                                     
         BE    PVAL030                                                          
PVAL020  IC    RF,1(R3)                                                         
         AR    R3,RF                                                            
         B     PVAL010                                                          
*                                                                               
         USING ABLELD,R3                                                        
PVAL030  MVI   RBYTE,X'FF'         FLAG ABLEL FOUND                             
         B     PVAL020                                                          
         DROP  R3                                                               
*                                                                               
PVAL100  CLI   RBYTE,X'FF'         INVALID IF ABLEL NOT FOUND                   
         BE    RETURN                                                           
         MVI   RETCODE,RETERRQ                                                  
         B     EXIT                                                             
         SPACE 1                                                                
         EJECT                                                                  
**********************************************************************          
* VALIDATE 1R PERSON ACCOUNT RECORD                                  *          
**********************************************************************          
         SPACE 1                                                                
PROCDIS  EQU   *                                                                
*                                                                               
         B     RETURN                                                           
         DROP  R2                                                               
         EJECT                                                                  
**********************************************************************          
* PROCESS SWITCH                                                     *          
*    DELETE THE SWITCH LIST SO THERE AREN'T ANY DUMPS IN FASWITCH    *          
**********************************************************************          
         SPACE 1                                                                
PROCSWT  DS    0H                                                               
         MVI   RETCODE,RETOKQ                                                   
         MVI   RDUB,X'FB'           SPECIAL DELETE BIT                          
         MVC   RDUB+1(3),=X'FFFFFF' SAME SPECIAL PLIST FOR FACPAK               
         MVI   RDUB+4,X'06'         KEEP ALL OF ACC FILES                       
         XC    RDUB+5(3),RDUB+5                                                 
         GOTO1 VSWITCH,RDUB                                                     
         CLI   4(R1),0                                                          
         BE    EXIT                                                             
         MVI   RETCODE,RETERRQ                                                  
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* PROCESS PID RECORDS TO FIND IF PID HAS TIME AGAINST IT             *          
**********************************************************************          
         SPACE 1                                                                
         USING PIDRECD,R2                                                       
PROCPID  DS    0H                                                               
         LA    R2,IOKEY                                                         
         MVI   PIDKEY,X'40'                                                     
         MVC   PIDKEY+1(L'PIDKEY-1),PIDKEY                                      
*                                                                               
         SR    R1,R1                                                            
         ICM   R1,15,ASEBLK        A(SE INFO BLOCK)                             
         MVC   SVSEBLK,0(R1)       SAVE OFF SE BLK                              
*        CLC   SVLIDSE,SVSYSSE     ARE THE SE NUMBERS THE SAME?                 
*        BE    PROCP10                                                          
         LA    R1,SVSYSSE                                                       
         BAS   RE,SWTCHSYS         MAKE CALL TO FASWITCH TO SWITCH SYS          
         BNE   RETURN              SYSTEM NOT ACCESSIBLE - SKIP IT              
*                                                                               
PROCP10  MVI   RETCODE,RETOKQ      SET RETURN AS OK                             
*                                                                               
         MVI   PIDKTYP,PIDKTYPQ    X'3E'                                        
         MVI   PIDKSUB,PIDKSUBQ    X'12'                                        
         MVC   PIDKCPY,SVBINID     FILL IN BINARY AGENCY ID                     
         SR    R1,R1                                                            
         ICM   R1,7,AOLDPID                                                     
         MVC   PIDKPID,0(R1)       OLD PID                                      
         LA    R2,RIO              READ ACCDIR FILE                             
         ST    R2,IOADDR                                                        
         LA    R1,IOHIGH+IOCONACD                                               
         MVC   SVKEY,IOKEY                                                      
         MVI   RETCODE,RETOKQ                                                   
         GOTO1 AIO                                                              
         BNE   PROCPX              IF NO RECORD-THERE'S NO PERSON               
         CLC   IOKEY(PIDKPER-PIDKEY),SVKEY        SAME KEY?                     
         BNE   PROCPX              IF NOT THE SAME DONT CONTINUE                
         DROP  R2                                                               
         MVI   RETCODE,RETERRQ     RETURN AN ERROR IF THERE IS A REC            
*                                                                               
PROCPX   DS    0H                                                               
*        CLC   SVLIDSE,SVSYSSE     ARE THE ACC SE NUMBERS THE SAME?             
*        BE    EXIT                                                             
         LA    R1,SVCNTLSE         POINT TO LOGIN CONTROL IDS FILE              
         BAS   RE,SWTCHSYS         MAKE CALL TO FASWITCH TO SWITCH SYS          
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* RETURN PQ PROFILE DATA IN APPRORIATE AREA                          *          
**********************************************************************          
         SPACE 1                                                                
RETURN   EQU   *                                                                
         MVI   RETCODE,RETOKQ                                                   
         B     EXIT                EXIT TO CALLER                               
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
         XIT1                                                                   
         EJECT                                                                  
**********************************************************************          
* SWITCH SYSTEMS (ACC ONLY)                                          *          
*        R1 = A(SE NUMBER TO SWITCH TO)                              *          
**********************************************************************          
         SPACE 1                                                                
SWTCHSYS NTR1                                                                   
         MVC   RDUB(1),0(R1)        SWITCH TO CONNECTED SYSTEM                  
         MVC   RDUB+1(3),=X'FFFFFF' SET SPECIAL PLIST FOR FACPAK                
         XC    RDUB+4(4),RDUB+4                                                 
         GOTO1 VSWITCH,RDUB                                                     
         CLI   4(R1),0                                                          
         BE    YES                                                              
         B     NO                                                               
         EJECT                                                                  
**********************************************************************          
* LITERALS                                                           *          
**********************************************************************          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
* STORAGE                                                            *          
**********************************************************************          
         SPACE 1                                                                
RWRKD    DSECT                                                                  
*                                                                               
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
RELO     DS    A                                                                
RDUB     DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
PARM     DS    0XL16               PARAMETER LIST                               
*                                                                               
AWORKD   DS    AL4                 A(WORKD GENERAL WORK AREA)                   
*                                                                               
FUNCTION DS    XL1                 SUBROUTINE FUNCTION                          
VALIDQ   EQU   0                                                                
DISPLAYQ EQU   1                                                                
CHKTIME  EQU   2                   CHECK TO SEE IF TIME EXISTS                  
DELSWTCH EQU   3                   DELETE SWITCH LIST FOR CHKTIME               
         ORG   FUNCTION                                                         
RETCODE  DS    XL1                 RETURN CODE                                  
RETOKQ   EQU   0                                                                
RETERRQ  EQU   X'FF'                                                            
A1RPKEY  DS    AL3                 A(1R PERSON ACCOUNT RECORD KEY DATA)         
         ORG   A1RPKEY                                                          
AOLDPID  DS    AL3                 A(OLD PID NUMBER)                            
ASEBLK   DS    AL4                 A(SE INFO BLOCK AND BINARY ID)               
*                                                                               
SVKEY    DS    CL44                SAVED AREA FOR IOKEY                         
*                                                                               
SVSEBLK  DS    0XL4                BLOCK-LOGIN ID SE#/SYS SE#/BIN ID            
SVCNTLSE DS    XL1                 CURRENT LOGIN IDS SE#                        
SVLIDSE  DS    XL1                 CURRENT LOGIN IDS SE#                        
SVSEBID  DS    0CL2                SAVED ARE FOR SYS SE# AND BIN ID             
SVSYSSE  DS    XL1                 SYSTEM SE #                                  
SVBINID  DS    XL1                 AGENCY BINARY ID                             
*                                                                               
RWORK    DS    XL64                                                             
RBYTE    DS    XL1                                                              
RIO      DS    2000C                                                            
RWRKX    EQU   *                                                                
         EJECT                                                                  
         SPACE 1                                                                
         EJECT                                                                  
* GEGENWRK                                                                      
         PRINT OFF                                                              
       ++INCLUDE GEGENWRK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'065SEACS1RPA 01/24/01'                                      
         END                                                                    
