*          DATA SET SPXROUTS   AT LEVEL 217 AS OF 02/24/21                      
*CATALP SPXROUTS                                                                
         TITLE 'EXTRACT ROUTINES FOR SPOT XTRACT'                               
         PRINT NOGEN                                                            
SPXROUTS CSECT                                                                  
*                                                                               
         ENTRY SPTMDC            MEDIA                                          
         ENTRY SPTCNC            CLIENT                                         
         ENTRY SPTPDC            PRODUCT                                        
         ENTRY SPTPLC            PRODUCT LIST                                   
         ENTRY SPTESC            ESTIMATE                                       
         ENTRY SPTMKC            MARKET                                         
         ENTRY SPTSTC            STATION                                        
         ENTRY SPTEDC            ESTIMATE DEMO LIST                             
         ENTRY SPTSAC            STATION ADDRESS                                
         ENTRY SPTEQV            EQUIVALENCE FACTORS                            
         ENTRY SPTCML            COMMERCIAL RECORD                              
         ENTRY SPTSOW            STATION OWNERSHIP                              
         ENTRY SPTFOR            STATION FORMAT                                 
         ENTRY SPTOIN            OWNER INFO                                     
         ENTRY SPTMGP            MARKET GROUP DEFINITIONS                       
         ENTRY SPTMGR            MARKET GROUPS                                  
         ENTRY SPTMGA            MARKET GROUPS ASSIGNMENTS                      
         ENTRY SPTCDF            CLIENT GROUP DEFINITIONS                       
         ENTRY SPTCGP            CLIENT GROUPS                                  
         ENTRY SPTCGC            CLIENT GROUP CLIENTS                           
         ENTRY SPTSDF            STATION GROUP DEFINITIONS                      
         ENTRY SPTSGP            STATION GROUPS                                 
         ENTRY SPTSGS            STATION GROUP STATIONS                         
         ENTRY SPTPGF            PROCUCT GROUP DEFINITIONS                      
         ENTRY SPTPGR            PRODUCT GROUPS                                 
         ENTRY SPTPGP            PRODUCT GROUP PRODUCTS                         
         ENTRY SPTPAR            PARENT INFORMATION                             
         ENTRY SPTDEA            DEAL RECORDS                                   
         ENTRY SPTDST            DEAL STATIONS                                  
         ENTRY SPTREP            REP RECORDS                                    
         ENTRY SPTNL             SYSCODE/NETWORK LIST                           
         ENTRY SPTUCM            SPOT UCOM RECORD                               
         ENTRY SPTSLK            STATION LOCKIN RECORD                          
         ENTRY SPTDPM            DAYPART MENU RECORD                            
         ENTRY SPTDEM            DEMO CODES AND NAMES                           
         ENTRY SPTDEMT           TRADITIONAL DEMO CODES AND NAMES               
         ENTRY SPTDEMC           NON-TRADITIONAL DEMO CODES AND NAMES           
         ENTRY SPTADB            ADBUYER                                        
         ENTRY SPTEAD            ESTIMATE AUTHORIZED DOLLARS                    
         ENTRY SPTDAO            DARE ORDERS                                    
         ENTRY SPTDMN            DARE MAKEGOOD NOTICE                           
         ENTRY SPTDMX            DARE MAKEGOOD NOTICE - XSPOT                   
         ENTRY SPTBXQ            BUY EXTRACT REQUEST                            
         ENTRY SPTBIL            BILLING INFORMATION                            
         ENTRY SPTFLT            FLIGHT RECORDS                                 
         ENTRY SPTFLF            FLIGHT RECORDS FLIGHTS                         
         ENTRY SPTCMC            COMSCORE MARKET RECORD                         
         ENTRY SPTSPC            SPILLDEF                                       
         ENTRY SPTSQC            SQAD DAYPART MENUS                             
         ENTRY SPTDMU            DEMO MENU                                      
         ENTRY SPTBFM            BILL FORMULAS                                  
         ENTRY SPTPPH            PATTERN RECORD HEADER                          
         ENTRY SPTPCM            PATTERN COMMERCIAL                             
         ENTRY SPTPMK            PATTERN MARKETS                                
         ENTRY SPTPST            PATTERN STATIONS                               
         ENTRY SPTPMG            PATTERN MARKET RECORDS                         
         ENTRY SPTPMA            PATTERN AFFILIATES                             
         ENTRY SPTPTP            PATTERN STATION TYPES                          
         ENTRY SPTPCN            PATTERN COMMENTS                               
         ENTRY SPTSTP            STEXT PAGE                                     
         ENTRY SPTSTL            STEXT LINE                                     
*                                                                               
         ENTRY DISBFM            DISBFM (BILL FORMULA DISPLAY)                  
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
EXIT     XIT1                                                                   
*                                                                               
*                                                                               
*                                                                               
* USINGS COMMON THROUGHOUT THE PROGRAM.  DO NOT USE THESE REGISTERS             
*                                                                               
         USING WORKD,RC                                                         
         USING LITERAL,RA                                                       
         USING SXDTABD,R4                                                       
         USING MEDPARAM,R8                                                      
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTMDC - EXTRACT MEDIA INFORMATION                                            
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)   (AGENCY 2 RECORD)                           *         
* AL4 0=PRINT-TO-EXTRACT                                            *           
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTMDC   NMOD1 WORKL,*SPTMDC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTMDD,R2           R2=A(EXTRACT RECORD)                         
         USING AGYHDR,R3           R3=A(SPOT RECORD)                            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTMDLEN,SPTMDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTMDLEN(2),=AL2(SPTMDDL)                                        
         MVC   SPTMDTYP,SPMDQ         CLIENT RECORD EQU                         
*                                                                               
         MVC   SPTMDAGY,SXDTAGY                                                 
*                                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTMDACT,C'A'                                                    
*                                                                               
         BRAS  RE,CHKKILL                                                       
         BE    SPTM10                                                           
*                                                                               
SPTM01   DS    0H                                                               
         LR    R6,R3                                                            
         MVI   ELCODE,X'01'        AGENCY ELEMENT                               
         BRAS  RE,GETEL                                                         
         BNE   SPTM03                                                           
         USING AGYEL,R6                                                         
         MVC   SPTMDAGN,AGYNAME                                                 
*                                                                               
         MVI   SPTMDCTR,C'N'                                                    
         CLI   AGYPROF+7,C'Y'                                                   
         BNE   *+8                                                              
         MVI   SPTMDCTR,C'Y'                                                    
         DROP  R6                                                               
*                                                                               
SPTM03   DS    0H                                                               
         MVI   ELCODE,X'02'        MEDIA ELEMENT                                
         BRAS  RE,FINDELEM                                                      
         BE    SPTM06                                                           
*                                                                               
         BRAS  RE,DELIMS                                                        
         J     NEQXIT                                                           
*                                                                               
         USING AGYMEDEL,R6                                                      
SPTM06   MVC   SPTMDMDN,AGYMEDEX   MEDIA NAME                                   
         MVC   SPTMDMED,AGYMEDCD   MEDIA 1-LETTER CODE                          
         DROP  R6                                                               
*                                                                               
SPTM10   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*        DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTCNC - EXTRACT CLIENT RECORD                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTCNC   NMOD1 WORKL,*SPTCNC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTCND,R2           R2=A(EXTRACT RECORD)                         
         USING CLTHDR,R3           R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTCNLEN,SPTCNLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTCNLEN(2),=AL2(SPTCNDL)                                        
         MVC   SPTCNTYP,SPCNQ         CLIENT RECORD EQU                         
*                                                                               
         MVC   SPTCNAGY,SXDTAGY                                                 
         MVC   BYTE,CKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTCNMED,BYTE                                                    
*                                                                               
         MVI   BYTE,X'00'                                                       
         CLI   CPROF+6,C'Y'                                                     
         BNE   *+8                                                              
         MVI   BYTE,C'Y'                                                        
*                                                                               
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,CKEYCLT),SPTCNCNT                                
*                                                                               
         MVC   SPTCNNME,CNAME                                                   
         MVC   SPTCN2OF,SPACES                                                  
*                                                                               
         XC    WORK,WORK                                                        
         LA    R6,WORK                                                          
         USING OFFICED,R6                                                       
         MVC   OFCSYS,SYSCODE                                                   
         MVC   OFCAGY,SXDTAGY                                                   
         MVC   OFCOFC,COFFICE                                                   
*                                                                               
         L     RF,AOFCR                                                         
         GOTO1 (RF),DMCB,(C'2',OFFICED),(0,COMFACS)                             
*                                                                               
         MVC   SPTCN2OF(1),COFFICE                                              
         TM    OFCINDS,OFCIOINV                                                 
         BO    *+10                                                             
         MVC   SPTCN2OF,OFCOFC2                                                 
         DROP  R6                                                               
*                                                                               
         MVC   SPTCNOFF,COFFICE                                                 
         MVC   SPTCNPU1,CPU1                                                    
         MVC   SPTCNPU2,CPU2                                                    
         MVC   SPTCNEU1,CEU1                                                    
         MVC   SPTCNEU2,CEU2                                                    
         MVC   SPTCNTAX,CEXTRA+11                                               
         MVC   SPTCNMN,CMEDNAME                                                 
         MVC   SPTCNINT,CCLTIFC                                                 
*                                                                               
         MVI   SPTCNTBE,C'N'                                                    
         TM    COPT4,COP4TBEX                                                   
         BZ    *+8                                                              
         MVI   SPTCNTBE,C'Y'                                                    
*                                                                               
         MVI   SPTCNRAT,C' '                                                    
         CLI   CPROF+14,X'00'                                                   
         BNH   *+10                                                             
         MVC   SPTCNRAT,CPROF+14   CLIENT RATE CONTROL                          
*                                                                               
         MVC   SPTCNAOF,CACCOFC                                                 
*                                                                               
         MVC   SPTCNAAG,CACCAGY                                                 
*                                                                               
         MVI   SPTCNPOL,C'T'       SET TRUE POL CLIENT                          
         CLI   CPROF+0,C'0'                                                     
         JE    *+8                                                              
         MVI   SPTCNPOL,C'B'       ELSE SET BRAND POL                           
*                                                                               
         MVC   SPTCNBAG,CBUYAGIS   BUYING AGENCY ID                             
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'SDAR'                                                 
         NI    WORK,X'FF'-X'40'    LOWERCASE 'S'                                
         MVC   WORK+4(2),SPTCNAGY                                               
         MVC   WORK+6(1),SPTCNMED                                               
         MVC   WORK+7(3),SPTCNCNT                                               
         L     RF,COMFACS                                                       
         MVC   DMCB+8(4),0(RF)        A(DATAMGR)                                
         GOTO1 =V(GETPROF),DMCB,WORK,PROFDAR                                    
         MVC   SPTCNDAR,PROFDAR+6                                               
*                                                                               
         MVC   SPTCNEXN,CEXTNAME   EXT CLIENT NAME                              
*                                                                               
         MVC   SPTCNLBX,CPROF+1    LOCKBOX NUMBER                               
*                                                                               
         MVC   SPTCNBEC,CPROF+5    BILL ESTIMATE CONTROL                        
*                                                                               
         MVI   SPTCN2CS,C'N'       SECOND COST REQUIRED                         
         TM    COPT1,COP1COSQ                                                   
         BZ    *+8                                                              
         MVI   SPTCN2CS,C'Y'                                                    
*                                                                               
         MVI   SPTCNMGR,C'N'       MARKET GROUPS?                               
         TM    COPT1,COP1MGRQ                                                   
         BZ    *+8                                                              
         MVI   SPTCNMGR,C'Y'                                                    
*                                                                               
         MVI   SPTCNFRZ,C'N'       FREEZE CLIENT?                               
         TM    COPT2,COP2FRZ                                                    
         BZ    *+8                                                              
         MVI   SPTCNFRZ,C'Y'                                                    
*                                                                               
         MVI   SPTCNUBC,C'N'       UCOM BILL CONTROL                            
         TM    COPT4,COP4UCOM                                                   
         BZ    *+8                                                              
         MVI   SPTCNUBC,C'Y'                                                    
*                                                                               
         MVI   SPTCNMMM,C'N'                                                    
         CLI   CEXTRA+7,C' '                                                    
         BNH   *+10                                                             
         MVC   SPTCNMMM,CEXTRA+7   MKGDS IN MISSED MONTH                        
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTPDC - EXTRACT PRODUCT RECORD                                               
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTPDC   NMOD1 WORKL,*SPTPDC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPDD,R2           R2=A(EXTRACT RECORD)                         
         USING PRDHDR,R3           R3=A(SPOT PRODUCT RECORD)                    
*                                                                               
         XC    SPTPDLEN,SPTPDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPDLEN(2),=AL2(SPTPDDL)                                        
         MVC   SPTPDTYP,SPPDQ                                                   
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTPDAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,PKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTPDMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,PKEYCLT),SPTPDCNT                                
*                                                                               
         MVC   SPTPDPRD,PKEYPRD                                                 
*                                                                               
         MVC   SPTPDNME,PNAME                                                   
*                                                                               
         MVI   SPTPDBBG,C'Y'                                                    
         TM    PBILLBAS,X'F0'      BILL BASE GROSS??                            
         BZ    *+8                                                              
         MVI   SPTPDBBG,C'N'                                                    
*                                                                               
         MVI   SPTPDCBG,C'Y'                                                    
         TM    PBILLBAS,X'0F'      COMM BASE GROSS??                            
         BZ    *+8                                                              
         MVI   SPTPDCBG,C'N'                                                    
*                                                                               
         ICM   R5,15,PBILLCOM                                                   
         EDIT  (R5),(L'SPTPDBCM-1,SPTPDBCM+1),FILL=0                            
         MVI   SPTPDBCM,C'0'                                                    
         TM    PBILLCOM,X'80'                                                   
         BNO   *+8                                                              
         MVI   SPTPDBCM,C'-'                                                    
*                                                                               
         MVC   SPTPDOFF,POFFICE                                                 
         MVC   SPTPDUV1,PUSER1                                                  
         MVC   SPTPDUV2,PUSER2                                                  
         MVC   SPTPDTAX,PGSTCODE                                                
         MVC   SPTPDINT,PACCT                                                   
*                                                                               
         OC    PBILLBAS(L'PBILLBAS+L'PBILLCOM),PBILLBAS                         
         BZ    SPTPDC90                                                         
*                                                                               
         GOTO1 =A(DISBFM),DMCB,PBILLBAS,SPTPDBFM                                
*                                                                               
SPTPDC90 DS    0H                                                               
         CLC   SPTPDBFM,SPACES     NO BILL FORMULA?                             
         BH    *+10                                                             
         MVC   SPTPDBFM,SVAAABFM   COPY AAA'S BILL FORMULA                      
*                                                                               
         MVC   SPTPDAD1,PADDR1     BILL TO NAME                                 
         MVC   SPTPDAD2,PADDR2     ADDRESS LINE 2                               
         MVC   SPTPDAD3,PADDR3     ADDRESS LINE 3                               
         MVC   SPTPDAD4,PADDR4     ADDRESS LINE 4                               
*                                                                               
SPTPDC99 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPLC - EXTRACT PRODUCT LIST                                                 
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTPLC   NMOD1 WORKL,*SPTPLC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPLD,R2           R2=A(EXTRACT RECORD)                         
         USING PRDHDR,R3           R3=A(SPOT PRODUCT RECORD)                    
*                                                                               
         XC    SPTPLLEN,SPTPLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPLLEN(2),=AL2(SPTPLDL)                                        
         MVC   SPTPLTYP,SPPLQ                                                   
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTPLAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,PKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTPLMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,PKEYCLT),SPTPLCNT                                
*                                                                               
         MVC   SPTPLPRD,PKEYPRD                                                 
*                                                                               
         EDIT  (B1,PCODE+1),SPTPLNUM                                            
*                                                                               
SPTPLC20 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTEST - EXTRACT ESTIMATE RECORD                                              
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTESC   NMOD1 WORKL,*SPTESC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTESD,R2           R2=A(EXTRACT RECORD)                         
         USING ESTHDR,R3           R3=A(SPOT PRODUCT RECORD)                    
*                                                                               
         XC    SPTESLEN,SPTESLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTESLEN(2),=AL2(SPTESDL)                                        
         MVC   SPTESTYP,SPESQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTESAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,EKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTESMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,EKEYCLT),SPTESCNT                                
*                                                                               
         MVC   SPTESPRD,EKEYPRD                                                 
*                                                                               
         EDIT  EKEYEST,SPTESEST,FILL=0                                          
*                                                                               
         MVC   SPTESNAM,EDESC                                                   
*                                                                               
         GOTO1 DATCON,DMCB,(0,ESTART),(10,SPTESSRT)                             
         GOTO1 DATCON,DMCB,(0,EEND),(10,SPTESEND)                               
*                                                                               
         OC    EREP,EREP                                                        
         BZ    SPTES10                                                          
         MVC   HALF,EREP                                                        
         LH    R0,HALF                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTESREP,DUB                                                     
*                                                                               
SPTES10  DS    0H                                                               
*                                                                               
         MVI   SPTESBBG,C'Y'                                                    
         TM    EBILLBAS,X'F0'      BILL BASE GROSS??                            
         BZ    *+8                                                              
         MVI   SPTESBBG,C'N'                                                    
*                                                                               
         MVI   SPTESCBG,C'Y'                                                    
         TM    EBILLBAS,X'0F'      COMM BASE GROSS??                            
         BZ    *+8                                                              
         MVI   SPTESCBG,C'N'                                                    
*                                                                               
         ICM   R5,15,EBILLCOM                                                   
         EDIT  (R5),(L'SPTESBCM-1,SPTESBCM+1),FILL=0                            
         MVI   SPTESBCM,C'0'                                                    
         TM    EBILLCOM,X'80'                                                   
         BNO   *+8                                                              
         MVI   SPTESBCM,C'-'                                                    
*                                                                               
         MVC   SPTESUV1,EUSER1                                                  
         MVC   SPTESUV2,EUSER2                                                  
*                                                                               
         MVC   SPTESTPE,ETYPE                                                   
         MVC   SPTESBTY,EBKTYPE                                                 
         MVC   SPTESFIL,EPROF                                                   
*                                                                               
         MVI   SPTESRTP,C' '                                                    
         CLI   ERATE,X'00'                                                      
         BNH   *+10                                                             
         MVC   SPTESRTP,ERATE                                                   
*                                                                               
         MVC   SPTESDPT,EDAYMENU                                                
*                                                                               
         MVI   SPTESOOW,C' '                                                    
         CLI   EOWSDAY,X'01'                                                    
         JNH   *+14                                                             
         MVC   SPTESOOW,EOWSDAY                                                 
         OI    SPTESOOW,X'F0'                                                   
*                                                                               
         MVC   SPTESDLY,EDAILY                                                  
         CLI   SPTESDLY,C' '                                                    
         JH    *+8                                                              
         MVI   SPTESDLY,C'N'                                                    
*                                                                               
         OC    EBILLBAS(L'EBILLBAS+L'EBILLCOM),EBILLBAS                         
         BZ    SPTES90                                                          
*                                                                               
         GOTO1 =A(DISBFM),DMCB,EBILLBAS,SPTESBFM                                
*                                                                               
SPTES90  DS    0H                                                               
         CLC   SPTESBFM,SPACES     NO BILL FORMULA?                             
         BH    *+10                                                             
         MVC   SPTESBFM,SVPRDBFM   COPY PRODUCT'S BILL FORMULA                  
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTMKT - EXTRACT MARKET RECORD                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTMKC   NMOD1 WORKL,*SPTMKC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTMKD,R2           R2=A(EXTRACT RECORD)                         
         USING MKTREC,R3           R3=A(SPOT MARKET RECORD)                     
*                                                                               
         XC    SPTMKLEN,SPTMKLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTMKLEN(2),=AL2(SPTMKDL)                                        
         MVC   SPTMKTYP,SPMKQ                                                   
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTMKAGY,MKTKAGY                                                 
         MVC   SPTMKMED,MKTKMED                                                 
         MVC   SPTMKMKT,MKTKMKT                                                 
         MVC   SPTMKNAM,MKTNAME                                                 
         MVC   SPTMKRNK,MKTRANK                                                 
         OC    MKTWT,MKTWT                                                      
         BZ    SPTMK10                                                          
         MVC   SPTMKWGH,MKTWT                                                   
*                                                                               
SPTMK10  DS    0H                                                               
         EDIT  (2,MKTRSM1),(4,SPTMKMK1),FILL=0                                  
         EDIT  (2,MKTRSM2),(4,SPTMKMK2),FILL=0                                  
         MVC   SPTMKTZO,MKTZONE                                                 
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTMKC - EXTRACT COMSCORE MARKET RECORD                                       
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTCMC   NMOD1 WORKL,*SPTCMC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTMCD,R2           R2=A(EXTRACT RECORD)                         
         USING MKTREC,R3           R3=A(SPOT MARKET RECORD)                     
*                                                                               
         XC    SPTMCLEN,SPTMCLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTMCLEN(2),=AL2(SPTMCDL)                                        
         MVC   SPTMCTYP,SPMCQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTMCAGY,MKTKAGY                                                 
         MVC   SPTMCMED,MKTKMED                                                 
         MVC   SPTMCMKT,MKTKMKT                                                 
         MVC   SPTMCNAM,MKTNAME                                                 
*                                                                               
SPTMC10  DS    0H                                                               
         EDIT  (1,MKTRS1),(3,SPTMCRS1),FILL=0                                   
         EDIT  (2,MKTRSM1),(4,SPTMCRM1),FILL=0                                  
         EDIT  (1,MKTRS2),(3,SPTMCRS2),FILL=0                                   
         EDIT  (2,MKTRSM2),(4,SPTMCRM2),FILL=0                                  
*                                                                               
         EDIT  (2,MKTCSMKN),(5,SPTMCCOM),FILL=0                                 
*                                                                               
         MVC   SPTMCBKT,MKTCSBTY                                                
*                                                                               
         L     RF,ADXBLOCK                                                      
         USING DXBLOCKD,RF                                                      
         MVC   SPTMCTOK,DXUSER                                                  
         DROP  RF                                                               
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSTC - EXTRACT STATION RECORD                                               
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
*                                                                               
***********************************************************************         
*                                                                               
SPTSTC   NMOD1 WORKL,*SPTSTC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSTD,R2           R2=A(EXTRACT RECORD)                         
         USING STAREC,R3           R3=A(SPOT STATION RECORD)                    
*                                                                               
         XC    SPTSTLEN,SPTSTLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSTLEN(2),=AL2(SPTSTDL)                                        
         MVC   SPTSTTYP,SPSTQ                                                   
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTSTAGY,SXDTAGY                                                 
         MVC   SPTSTMED,STAKMED                                                 
         MVC   SPTSTSTA,STAKCALL                                                
*                                                                               
* READ STATION ADDRESS RECORD HERE                                              
         MVC   STAKNEW,STAKEY                                                   
         MVI   STAKNEW,C'A'                                                     
         MVC   STAKNEW+9(3),=C'000'    GET RID OF CLIENT                        
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'STATION',STAKNEW,STAIO,WORK          
         CLC   STAKNEW,STAIO                                                    
         BNE   SPTSTC10                                                         
         LA    R6,STAIO                                                         
         USING ADDRREC,R6                                                       
         MVC   SPTSTNAM,ANAME                                                   
         DROP  R6                                                               
*                                                                               
SPTSTC10 DS    0H                                                               
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'STATION',STAKEY,STAIO,WORK           
*                                                                               
SPTSTC20 DS    0H                                                               
         CLC   STAKCLT,=C'000'                                                  
         BE    *+10                                                             
         MVC   SPTSTCLT,STAKCLT                                                 
         MVC   SPTSTMKT,SMKT                                                    
         MVC   SPTSTSIZ,SSIZE                                                   
         MVC   SPTSTCAN,SCANNTWK                                                
         MVC   SPTSTFAX,SFAX                                                    
*                                                                               
         MVI   SPTSTDEA,C' '                                                    
         CLI   SSYSDACT,X'FF'                                                   
         BNE   *+8                                                              
         MVI   SPTSTDEA,C'Y'                                                    
*                                                                               
* 05/13/10 - WE'RE NOW EXTRACTING THE DEACTIVATED STATIONS                      
         B     SPTSTC30                                                         
*        CLI   SSYSDACT,X'FF'                                                   
*        BNE   SPTSTC30                                                         
*                                                                               
         CLI   SPTSTACT,C'L'                                                    
         BNE   *+12                                                             
         OI    FLAGS2,NOWRITEQ                                                  
         B     SPTSTC30                                                         
*                                                                               
         CLI   SPTSTACT,C'C'                                                    
         BNE   *+8                                                              
         MVI   SPTSTACT,C'D'                                                    
*                                                                               
SPTSTC30 DS    0H                                                               
         MVC   SPTSTNTP,SNETTYPE                                                
*                                                                               
         MVC   SPTSTPRP,SPAYREP                                                 
*                                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTEDC - EXTRACT ESTIMATE DEMO LIST                                           
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTEDC   NMOD1 WORKL,*SPTEDC*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTEDD,R2           R2=A(EXTRACT RECORD)                         
         USING ESTHDR,R3           R3=A(SPOT ESTIMATE RECORD)                   
*                                                                               
         OC    D29ATAB,D29ATAB     READ D29 RECS YET                            
         JNZ   *+8                                                              
         BRAS  RE,BLDD29                                                        
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTEDLEN,SPTEDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTEDLEN(2),=AL2(SPTEDDL)                                        
         MVC   SPTEDTYP,SPEDQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTEDAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,EKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTEDMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,EKEYCLT),SPTEDCNT                                
*                                                                               
         MVC   SPTEDPRD,EKEYPRD                                                 
*                                                                               
         LLC   R0,EKEYEST                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTEDEST,DUB                                                     
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTED10                                                          
*                                                                               
         MVI   SPTEDACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTED40                                                          
*                                                                               
SPTED10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTEDACT,C'A'                                                    
*                                                                               
         XC    WORK,WORK                                                        
*                                                                               
         LA    R0,EDEMLSTN                                                      
         LA    R5,EDEMOS                                                        
         LA    R6,WORK                                                          
*                                                                               
SPTED12  OC    0(3,R5),0(R5)       TEST EOL                                     
         JZ    SPTED20                                                          
*                                                                               
* PROTECT AGAINST BAD DATA ON SJR THAT LOOKS LIKE 05C901                        
* ALL VALID DEMOS SHOULD START WITH X'00'                                       
* ALL NONT DEMOS SHOULD END WITH X'00'                                          
*                                                                               
         CLI   0(R5),0                                                          
         JNE   SPTED16                                                          
*                                                                               
         MVC   0(3,R6),0(R5)       MOVE DEMO TO LIST                            
*                                                                               
         CLI   2(R6),0             TEST NONT DEMO                               
         JNE   SPTED14             NO                                           
*                                                                               
         BAS   RE,GETNONT          LOOK UP DEMO NAME                            
*                                                                               
         CLI   DEMOMOD,C'Z'        TEST INVALID                                 
         JNE   *+14                NO                                           
         XC    0(3,R6),0(R6)       CLEAR ENTRY                                  
         J     SPTED16             AND CARRY ON                                 
*                                                                               
         MVC   0(1,R6),DEMOMOD     MOVE DEMO MODIFIER                           
         MVC   1(2,R6),DEMONUM     AND NUMBER                                   
*                                                                               
SPTED14  LA    R6,3(R6)                                                         
*                                                                               
SPTED16  LA    R5,3(R5)                                                         
         JCT   R0,SPTED12                                                       
*                                                                               
SPTED20  LA    R9,WORK                                                          
         LLC   R7,ELCOUNT          DEMO POSITION COUNTER -1                     
         LR    R0,R7                                                            
         MHI   R0,3                                                             
         AR    R9,R0               POINT TO  DEMO                               
*                                                                               
SPTED22  DS    0H                                                               
         OC    0(3,R9),0(R9)       REACHED EOL YET?                             
         JNZ   SPTED30             NO                                           
*                                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
SPTED30  DS    0H                                                               
         LA    R7,1(R7)                                                         
         STC   R7,ELCOUNT                                                       
         CVD   R7,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTEDPOS,DUB                                                     
*                                                                               
         CLI   0(R9),0             TEST NONT DEMO                               
         JNE   SPTED32             YES                                          
*                                                                               
         MVC   SPTEDMOD,1(R9)      E.G., 00 D9 01                               
         LLC   R0,2(R9)                                                         
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTEDASC,DUB                                                     
         J     SPTED40                                                          
*                                                                               
SPTED32  MVC   SPTEDMOD,0(R9)      NONT E.G., D9 01 01                          
         SR    R0,R0                                                            
         ICM   R0,3,1(R9)                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTEDASC,DUB                                                     
*                                                                               
SPTED40  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
                                                                                
*==========================================================                     
* LOOK UP THE DEMO SEQNUM FOR NONT DEMO AT 0(R5)                                
* DEMOS ARE SUPPOSED TO LOOK LIKE RX.... OR X ....                              
* SO JUST MATCH ON CHARACTERS FOLLOWING THE X                                   
* AND RETURN IT IN HALF                                                         
*==========================================================                     
                                                                                
GETNONT  NTR1                                                                   
         LLC   R0,1(R5)            GET NONT SEQNUM                              
         BCTR  R0,0                                                             
         SLL   R0,3                X 8                                          
         LA    R5,ENONTDMS                                                      
         AR    R5,R0               POINT TO NONT NAME                           
         MVC   DEMOMOD,0(R5)       SET MODIFIER                                 
                                                                                
         CLI   0(R5),C'R'                                                       
         JNE   *+8                                                              
         LA    R5,1(R5)                                                         
         CLI   0(R5),C'X'                                                       
         JNE   GETNONT4            THEN IT'S AN UNREPORTED DEMO                 
         LA    R5,1(R5)                                                         
*                                                                               
         XC    SVD29,SVD29                                                      
         LA    RF,SVD29                                                         
         USING D29RECD,RF                                                       
         MVC   D29AM,EKEYAM                                                     
         NI    D29AM,X'F0'                                                      
         MVC   D29DEMNM,0(R5)                                                   
         DROP  RF                                                               
*                                                                               
         MVI   D29PAR4,0          FIND A RECORD                                 
         SAM31                                                                  
         GOTO1 =V(BINSRCH),D29BSPARS,SVD29                                      
         SAM24                                                                  
         TM    D29PAR1,X'80'      TEST IF RECORD NOT FOUND                      
         JZ    GETNONT6                                                         
*                                                                               
GETNONT4 MVI   DEMOMOD,C'Z'        SET CATEGORY AND                             
         XC    DEMONUM,DEMONUM     DEMO INVALID                                 
         J     GETNONTX            EXIT WITH DEMONUM=0                          
*                                                                               
GETNONT6 DS    0H                                                               
         SAM31                                                                  
         L     RF,D29PAR1                                                       
         USING D29RECD,RF                                                       
         MVC   DEMONUM,D29DEMSQ    SET DEMO NUMBER                              
         SAM24                                                                  
         DROP  RF                                                               
*                                                                               
GETNONTX J     EQXIT                                                            
         LTORG                                                                  
         DROP  R2,R3                                                            
                                                                                
*&&DO                                                                           
*                                                                               
* GETNONT ROUTINE REWRITTEN TO USE BINSR31 INSTEAD OF LINEAR SEARCH             
*                                                                               
*==========================================================                     
* LOOK UP THE DEMO SEQNUM FOR NONT DEMO AT 0(R5)                                
* DEMOS ARE SUPPOSED TO LOOK LIKE RX.... OR X ....                              
* SO JUST MATCH ON CHARACTERS FOLLOWING THE X                                   
* AND RETURN IT IN HALF                                                         
*==========================================================                     
                                                                                
GETNONT  NTR1                                                                   
         LLC   R0,1(R5)            GET NONT SEQNUM                              
         BCTR  R0,0                                                             
         SLL   R0,3                X 8                                          
         LA    R5,ENONTDMS                                                      
         AR    R5,R0               POINT TO NONT NAME                           
         MVC   DEMOMOD,0(R5)       SET MODIFIER                                 
                                                                                
         SAM31                                                                  
*                                                                               
         LRL   RF,D29COUNT                                                      
         LRL   R1,D29ATAB                                                       
         USING D29RECD,R1                                                       
*                                                                               
         CLI   0(R5),C'R'                                                       
         JNE   *+8                                                              
         LA    R5,1(R5)                                                         
         CLI   0(R5),C'X'                                                       
         JNE   GETNONT4            THEN IT'S AN UNREPORTED DEMO                 
         LA    R5,1(R5)                                                         
*                                                                               
GETNONT2 CLC   D29DEMNM,0(R5)                                                   
         JE    GETNONT6                                                         
         LA    R1,D29RECL(R1)                                                   
         JCT   RF,GETNONT2                                                      
*                                                                               
GETNONT4 MVI   DEMOMOD,C'Z'        SET CATEGORY AND                             
         XC    DEMONUM,DEMONUM     DEMO INVALID                                 
         J     GETNONTX            EXIT WITH DEMONUM=0                          
*                                                                               
GETNONT6 MVC   DEMONUM,D29DEMSQ    SET DEMO NUMBER                              
*                                                                               
GETNONTX SAM24                                                                  
         XIT1                                                                   
         LTORG                                                                  
         DROP  R1,R2,R3                                                         
*&&                                                                             
*                                                                               
***********************************************************************         
* SPTSAC - EXTRACT STATION ADDRESS                                              
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTSAC   NMOD1 WORKL,*SPTSAC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSAD,R2           R2=A(EXTRACT RECORD)                         
         USING ADDRREC,R3          R3=A(SPOT STATION ADDRESS RECORD)            
*                                                                               
         XC    SPTSALEN,SPTSALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSALEN(2),=AL2(SPTSADL)                                        
         MVC   SPTSATYP,SPSAQ                                                   
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTSAAGY,SXDTAGY                                                 
         MVC   SPTSAMED,ADDKMED                                                 
         MVC   SPTSASTA,ADDKCALL                                                
         MVC   SPTSANAM,ANAME                                                   
         MVC   SPTSAAD1,A1LINE                                                  
         MVC   SPTSAAD2,A1LINE2                                                 
         MVC   SPTSACIT,A2LINE                                                  
         MVC   SPTSASTT,A3LINE                                                  
         MVC   SPTSAZIP,AZIP                                                    
         MVC   SPTSAPOS,ABIGZIP                                                 
         MVC   SPTSAEML,ADREMAIL                                                
         MVC   SPTSACOM,ADDRCOM                                                 
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTEQV - EXTRACT EQUIVALENCE FACTORS                                          
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTEQV   NMOD1 WORKL,*SPTEQV*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTEFD,R2           R2=A(EXTRACT RECORD)                         
         USING EQUHDR,R3           R3=A(SPOT STATION ADDRESS RECORD)            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTEFLEN,SPTEFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTEFLEN(2),=AL2(SPTEFDL)                                        
         MVC   SPTEFTYP,SPEFQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTEFAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTEFMED,EQUKMED                                                 
*                                                                               
         OC    EQUKCLT,EQUKCLT                                                  
         BZ    SPTEQV30                                                         
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,EQUKCLT),SPTEFCNT                                
*                                                                               
SPTEQV30 DS    0H                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTEQV40                                                         
*                                                                               
         MVI   SPTEFACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         BRAS  RE,DELIMS                                                        
         B     SPTEQVX                                                          
*                                                                               
SPTEQV40 DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTEFACT,C'A'                                                    
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'09'        AGENCY ELEMENT                               
         BRAS  RE,GETEL                                                         
         BNE   SPTEQVX                                                          
         USING EQUEL,R6                                                         
*                                                                               
         ZIC   R1,ELCOUNT          SPOT LENGTH (STORED IN ELCOUNT)              
         ICM   R5,15,ADSLNTAB      A(SLENTAB)                                   
         LR    R0,R5                                                            
         AHI   R0,500              R0 = A(EOT)                                  
*                                                                               
         LR    RE,R1               SPOT LENGTH                                  
         AR    RE,RE               X2                                           
         AR    R5,RE                                                            
*                                                                               
SPTEQV45 CR    R0,R5                                                            
         BNH   SPTEQV50                                                         
         CLI   1(R5),X'00'                                                      
         BH    *+16                                                             
         LA    R5,2(R5)                                                         
         AHI   R1,1                                                             
         B     SPTEQV45                                                         
*                                                                               
         EDIT  (R1),SPTEFSEC,FILL=0                                             
*                                                                               
         AHI   R1,1                NEXT SPOT LENGTH                             
         STC   R1,ELCOUNT          SAVE IT IN MEDPARAM                          
*                                                                               
         LLC   R1,0(R5)                                                         
         LA    R5,EQUSECT1(R1)                                                  
*                                                                               
         OC    0(2,R5),0(R5)                                                    
         BNZ   SPTEQV49                                                         
*                                                                               
         BRAS  RE,CKFIRST4                                                      
         BNE   *+12                                                             
         LA    R5,=X'03E8'         1.000                                        
         B     SPTEQV49                                                         
*                                                                               
* CALCULATE THE EQU FACTOR AS FRACTION OF 30-SEC SPOT'S EQU                     
*                                                                               
         XR    RE,RE                                                            
         LLC   RF,ELCOUNT          SPOT LENGTH                                  
         BCTR  RF,0                                                             
         MHI   RF,1000                                                          
         LHI   R0,30                                                            
         DR    RE,R0                                                            
         STCM  RF,3,HALF                                                        
         LA    R5,HALF                                                          
*                                                                               
SPTEQV49 DS    0H                                                               
         EDIT  (B2,0(R5)),SPTEFEQF,FILL=0                                       
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         B     SPTEQVX                                                          
*                                                                               
SPTEQV50 DS    0H                                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
SPTEQVX  DS    0H                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3,R6                                                         
*                                                                               
SPSLNTAB DS    0X                                                               
       ++INCLUDE SPSLNTAB                                                       
SPSLNTLQ EQU   *-SPSLNTAB                                                       
         DC    X'00'                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTCML - EXTRACT COMMERCIAL RECORD                                            
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTCML   NMOD1 WORKL,*SPTCML*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTCMD,R2           R2=A(EXTRACT RECORD)                         
         USING CMLRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTCMLEN,SPTCMLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTCMLEN(2),=AL2(SPTCMDL)                                        
         MVC   SPTCMTYP,SPCMQ         CLIENT RECORD EQU                         
*                                                                               
         MVC   SPTCMAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,CMLKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTCMMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,CMLKCLT),SPTCMCNT                                
*                                                                               
         TM    CMLRSTAT,X'01'      CMLKCML PACKED?                              
         BO    *+10                YES, DON'T FILL IN THE 8-CHAR FIELD          
         MVC   SPTCMCOM,CMLKCML                                                 
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'10'        COMMERCIAL DATA ELEMENT                      
         BRAS  RE,GETEL                                                         
         JNE   NEQXIT                                                           
*                                                                               
         USING CMLDTAEL,R6                                                      
         MVC   SPTCMDS1,CMLTITLE                                                
         MVC   SPTCMCCN,CMLCLTNO                                                
         CLC   CMLCLASS,SPACES                                                  
         BNH   *+16                                                             
         MVC   SPTCMCL1,CMLCLASS                                                
         MVC   SPTCMCP1,=C'100.00'                                              
*                                                                               
         EDIT  CMLSLN,SPTCMSEC,ZERO=NOBLANK                                     
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'30'        EXTRA DESCTIPTION ELEMENT                    
         BRAS  RE,GETEL                                                         
         BNE   SPTCML10                                                         
         USING CMLDSCEL,R6                                                      
         MVC   SPTCMDS2,CMLDSC                                                  
*                                                                               
         BRAS  RE,NEXTEL                                                        
         BNE   SPTCML10                                                         
         USING CMLDSCEL,R6                                                      
         MVC   SPTCMDS3,CMLDSC                                                  
*                                                                               
SPTCML10 DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'A0'        AD-ID ELEMENT                                
         BRAS  RE,GETEL                                                         
         BNE   SPTCML20                                                         
         USING CMLADIEL,R6                                                      
         MVC   SPTCMAID,CMLADID                                                 
*                                                                               
SPTCML20 DS    0H                                                               
         MVI   SPTCMCP1,C'0'                                                    
         MVI   SPTCMCP2,C'0'                                                    
         MVI   SPTCMCP3,C'0'                                                    
         MVI   SPTCMCP4,C'0'                                                    
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         LA    R5,SPTCMCL1                                                      
*                                                                               
         MVI   ELCODE,X'21'        COMMERCIAL CLASS ELEMENT                     
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
SPTCML25 DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         BNE   SPTCML30                                                         
*                                                                               
         USING CMLCLSEL,R6                                                      
*                                                                               
         MVC   0(L'SPTCMCL1,R5),SPACES                                          
         MVC   L'SPTCMCL1+1(L'SPTCMCP1,R5),SPACES                               
*                                                                               
         MVC   0(L'SPTCMCL1,R5),CMLCLS                                          
         EDIT  CMLCLSPC,(L'SPTCMCP1,L'SPTCMCL1+1(R5)),2                         
*                                                                               
         AHI   R5,L'SPTCMCL1+L'SPTCMCP1+2                                       
         B     SPTCML25                                                         
         DROP  R6                                                               
*                                                                               
* NOW EXTRACT PRODUCTS                                                          
*                                                                               
SPTCML30 DS    0H                                                               
         MVI   SPTCMAP,C'N'                                                     
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        CMLPRDEL, PRODUCT LIST ELEMENT               
         BRAS  RE,GETEL                                                         
         BNE   SPTCML50                                                         
*                                                                               
         USING CMLPRDEL,R6                                                      
*                                                                               
         CLI   CMLPRDS,X'FF'       PRODUCT=ALL?                                 
         BNE   *+12                                                             
         MVI   SPTCMAP,C'Y'                                                     
         B     SPTCML50                                                         
*                                                                               
         LA    R5,SPTCMP01                                                      
         LLC   R0,CMLPRDLN         ELEMENT LENGTH                               
         AHI   R0,-2               MINUS ELEM CODE, ELEM LEN                    
         LA    R6,CMLPRDS                                                       
         DROP  R6                                                               
*                                                                               
SPTCML40 DS    0H                                                               
         CLI   0(R6),X'00'                                                      
         BE    SPTCML50                                                         
         LR    R1,R5               WHERE TO PUT 3-CHAR PRD                      
         ICM   R1,8,0(R6)          BINARY PRD CODE                              
         BRAS  RE,FINDAPRD                                                      
         LA    R6,1(R6)                                                         
         LA    R5,(L'SPTCMP01+1)(R5)                                            
         BCT   R0,SPTCML40                                                      
*                                                                               
SPTCML50 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSOW - EXTRACT STATION OWNERSHIP INFO                                       
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTSOW   NMOD1 WORKL,*SPTSOW*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSOD,R2           R2=A(EXTRACT RECORD)                         
         USING CT99RECD,R3         R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTSOLEN,SPTSOLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSOLEN(2),=AL2(SPTSODL)                                        
         MVC   SPTSOTYP,SPSOQ         CLIENT RECORD EQU                         
*                                                                               
         MVC   SPTSOSTA,CT99KSTA                                                
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,CRCLELQ      RADIO ELEMENT                                
         BRAS  RE,GETEL                                                         
         BNE   SPTSO10                                                          
*                                                                               
         USING CRCLD,R6                                                         
*                                                                               
         MVC   SPTSOPAR,CRCLPRNT                                                
         MVC   SPTSOFRQ,CRCLFRQ                                                 
         MVC   SPTSOCIT,CRCLCTY                                                 
         MVC   SPTSOST,CRCLSTE                                                  
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTSO10  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,CRFMELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTSO20                                                          
*                                                                               
         USING CRFMD,R6                                                         
*                                                                               
         MVC   SPTSOOWN,CRFMOWN                                                 
         MVC   SPTSOFOR,CRFMFMT                                                 
         CLI   CRFMLN,CRFMLNQ2                                                  
         BL    *+10                                                             
         MVC   SPTSOMIN,CRFMMIN                                                 
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTSO20  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,CMADRELQ                                                  
         BRAS  RE,GETEL                                                         
         BNE   SPTSO50                                                          
*                                                                               
         USING CMADREL,R6                                                       
*                                                                               
SPTSO25  DS    0H                                                               
         CLI   CMADREL,CMADRELQ                                                 
         BNE   SPTSO50                                                          
*                                                                               
         CLI   CMADRTY,C'T'                                                     
         BE    *+12                                                             
         CLI   CMADRTY,C'M'                                                     
         BE    SPTSO30                                                          
*                                                                               
         MVC   SPTSOTA1,CMADAD1                                                 
         MVC   SPTSOTA2,CMADAD2                                                 
         MVC   SPTSOTCI,CMADCITY                                                
         MVC   SPTSOTST,CMADSTAT                                                
         MVC   SPTSOTZI,CMADZIP                                                 
         MVC   SPTSOTCT,CMADCTRY                                                
*                                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTSO25                                                          
*                                                                               
SPTSO30  DS    0H                                                               
         MVC   SPTSOMA1,CMADAD1                                                 
         MVC   SPTSOMA2,CMADAD2                                                 
         MVC   SPTSOMCI,CMADCITY                                                
         MVC   SPTSOMST,CMADSTAT                                                
         MVC   SPTSOMZI,CMADZIP                                                 
         MVC   SPTSOMCT,CMADCTRY                                                
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTSO50  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
***********************************************************************         
* SPTFOR - EXTRACT STATION FORMAT DATA                                          
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTFOR   NMOD1 WORKL,*SPTFOR*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSFD,R2           R2=A(EXTRACT RECORD)                         
         USING CT99RECD,R3         R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTSFLEN,SPTSFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSFLEN(2),=AL2(SPTSFDL)                                        
         MVC   SPTSFTYP,SPSFQ         CLIENT RECORD EQU                         
*                                                                               
         MVC   SPTSFMED,CT99KFME                                                
         MVC   SPTSFFOR,CT99KFRM                                                
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,CRCLELQ      RADIO ELEMENT                                
         BRAS  RE,GETEL                                                         
         BNE   *+10                                                             
         MVC   SPTSFNAM,2(R6)                                                   
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
***********************************************************************         
* SPTOWN - EXTRACT OWNERSHIP INFO                                               
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTOIN   NMOD1 WORKL,*SPTOIN*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTOID,R2           R2=A(EXTRACT RECORD)                         
         USING CT99RECD,R3         R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTOILEN,SPTOILEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTOILEN(2),=AL2(SPTOIDL)                                        
         MVC   SPTOITYP,SPOIQ      OWNER INFO RECORD                            
*                                                                               
         MVC   SPTOIMED,CT99KOME                                                
         MVC   SPTOIOWN,CT99KOWN                                                
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,CRCLELQ      RADIO ELEMENT                                
         BRAS  RE,GETEL                                                         
         BNE   *+10                                                             
         MVC   SPTOINAM,2(R6)                                                   
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTMGD - EXTRACT MARKET GROUP DEFINITIONS                                     
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTMGP   NMOD1 WORKL,*SPTMGD*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTGDD,R2           R2=A(EXTRACT RECORD)                         
         USING MKGRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTGDLEN,SPTGDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTGDLEN(2),=AL2(SPTGDDL)                                        
         MVC   SPTGDTYP,SPGDQ      MARKET GROUP DEF                             
*                                                                               
         MVC   SPTGDAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,MKGKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTGDMED,BYTE                                                    
*                                                                               
         OC    MKGKCLT,MKGKCLT                                                  
         BZ    SPTMG10                                                          
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,MKGKCLT),SPTGDCLT                                
*                                                                               
SPTMG10  DS    0H                                                               
         MVC   SPTGDID(1),MKGKMID                                               
         LA    R1,SPTGDID                                                       
         ICM   R1,8,=C'M'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'01'        MKTGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPTMG20                                                          
         USING MKGEL01,R6                                                       
*                                                                               
         MVC   SPTGDB1D,MKGBK1                                                  
         EDIT  MKGBK1LN,SPTGDB1L,ZERO=BLANK                                     
*                                                                               
         MVC   SPTGDB2D,MKGBK2                                                  
         EDIT  MKGBK2LN,SPTGDB2L,ZERO=BLANK                                     
*                                                                               
         MVC   SPTGDB3D,MKGBK3                                                  
         EDIT  MKGBK3LN,SPTGDB3L,ZERO=BLANK                                     
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTMG20  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTMGR - EXTRACT MARKET GROUPS                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTMGR   NMOD1 WORKL,*SPTMGR*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTMGD,R2           R2=A(EXTRACT RECORD)                         
         USING MKGRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTMGLEN,SPTMGLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTMGLEN(2),=AL2(SPTMGDL)                                        
         MVC   SPTMGTYP,SPMGQ      MARKET GROUP                                 
*                                                                               
         MVC   SPTMGAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,MKGKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTMGMED,BYTE                                                    
*                                                                               
         OC    MKGKCLT,MKGKCLT                                                  
         BZ    SPMGR10                                                          
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,MKGKCLT),SPTMGCLT                                
*                                                                               
SPMGR10  DS    0H                                                               
         MVC   SPTMGID(1),MKGKMID                                               
         LA    R1,SPTMGID                                                       
         ICM   R1,8,=C'M'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LA    R0,MKGKMGRP                                                      
         LA    R1,SPTMGGRP                                                      
         BRAS  RE,GROUT                                                         
*                                                                               
         LA    R1,SPTMGMGR                                                      
         MVC   0(2,R1),SPTMGID                                                  
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '                                                       
         BNH   *+8                                                              
         LA    R1,1(R1)                                                         
                                                                                
         MVC   0(4,R1),SPTMGGRP                                                 
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'10'        MKTGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPMGR20                                                          
         USING MKGEL10,R6                                                       
*                                                                               
         MVC   SPTMGB1N,MKGNAM1                                                 
         MVC   SPTMGB2N,MKGNAM2                                                 
         MVC   SPTMGB3N,MKGNAM3                                                 
*                                                                               
         LA    R5,SPTMGGRP         MARKET GROUP CODE                            
         ZIC   RF,BREAKLS          SAVED BREAK LENGTHS                          
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPMGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTMGB1C(0),0(R5)                                                
*                                                                               
         AHI   RF,1                                                             
         LA    R5,SPTMGGRP(RF)                                                  
         ZIC   RF,BREAKLS+1        SAVED BREAK LENGTHS                          
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPMGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTMGB2C(0),0(R5)                                                
*                                                                               
         AHI   RF,1                                                             
         LA    R5,SPTMGGRP(RF)                                                  
         ZIC   RF,BREAKLS+2           SAVED BREAK LENGTHS                       
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPMGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTMGB3C(0),0(R5)                                                
*                                                                               
SPMGR20  DS    0H                                                               
         MVC   SPTMGBTR,BUYTRKER                                                
         CLI   SPTMGBTR,X'40'                                                   
         BH    *+8                                                              
         MVI   SPTMGBTR,C'N'                                                    
*                                                                               
         DROP  R6                                                               
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTMGA - EXTRACT MARKET GROUP ASSIGNMENTS                                     
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTMGA   NMOD1 WORKL,*SPTMGA*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTMAD,R2           R2=A(EXTRACT RECORD)                         
         USING MKARECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTMALEN,SPTMALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTMALEN(2),=AL2(SPTMADL)                                        
         MVC   SPTMATYP,SPMAQ      MARKET GROUP                                 
*                                                                               
         MVC   SPTMAAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,MKAKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTMAMED,BYTE                                                    
*                                                                               
         OC    MKAKCLT,MKAKCLT                                                  
         BZ    SPTMGA05                                                         
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,MKAKCLT),SPTMACLT                                
*                                                                               
SPTMGA05 DS    0H                                                               
         EDIT  MKAKMKT,(4,SPTMAMKT)                                             
         TM    FLAGS2,OPTICAQ                                                   
         BZ    SPTMGA06                                                         
         EDIT  MKAKMKT,(4,SPTMAMKT),FILL=0    ZERO-PAD FOR OPTICA               
*                                                                               
SPTMGA06 DS    0H                                                               
         SR    R9,R9               LOOP COUNTER                                 
         ZIC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'05'        MEDIA ELEMENT                                
         BRAS  RE,GETEL                                                         
*                                                                               
SPTMGA10 DS    0H                                                               
         BE    SPTMGA12                                                         
*                                                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         J     NEQXIT                                                           
*                                                                               
SPTMGA12 DS    0H                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTMGA20                                                         
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING MKAEL05,R6                                                       
*                                                                               
         OC    MKAPGRP,MKAPGRP                                                  
         BZ    SPTMGA15                                                         
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
         J     NEQXIT                                                           
*                                                                               
SPTMGA15 DS    0H                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING BRKTABD,R1                                                       
         MVC   BRKTAM,MKAKAGMD    A/M                                           
*                                                                               
         MVC   BRKTCLT,MKAKCLT     CLT                                          
         CLI   MKAMGRP,X'40'                                                    
         BL    *+12                                                             
         CLI   MKAMGRP,C'F'                                                     
         BNH   *+10                                                             
         XC    BRKTCLT,BRKTCLT                                                  
*                                                                               
         MVC   BRKTGID,MKAMGRP   MARKET GROUP ID                                
         DROP  R1                                                               
*                                                                               
         ICM   RF,15,ABREAKT       A(BREAK TABLE)                               
         LHI   R0,BRKTABNQ                                                      
         CLC   WORK(BRKTKLQ),0(RF)                                              
         BE    *+16                                                             
         LA    RF,BRKTABLQ(RF)                                                  
         BCT   R0,*-14                                                          
         B     SPTMGA20                                                         
*                                                                               
* FOUND THE GROUP IN THE BREAK TABLE                                            
*                                                                               
         USING BRKTABD,RF                                                       
         MVC   SPTMAMGP(1),BRKTGID                                              
         MVC   BREAKLS,BRKTBK1     SAVE BREAK LENGHTS FOR GROUT                 
         DROP  RF                                                               
*                                                                               
         LA    R1,SPTMAMGP                                                      
         ICM   R1,8,=C'M'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LA    R0,MKAMGRP+1                                                     
         LA    R1,SPTMAMGP+1                                                    
         CLI   0(R1),C' '                                                       
         BNH   *+8                                                              
         LA    R1,1(R1)                                                         
         BRAS  RE,GROUT                                                         
*                                                                               
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
         B     SPTMGA50                                                         
         DROP  R6                                                               
*                                                                               
SPTMGA20 DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTMGA10                                                         
*                                                                               
SPTMGA50 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTCGD - EXTRACT CLIENT GROUP DEFINITIONS                                     
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTCDF   NMOD1 WORKL,*SPTCDF*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTCDD,R2           R2=A(EXTRACT RECORD)                         
         USING GRPRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTCDLEN,SPTCDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTCDLEN(2),=AL2(SPTCDDL)                                        
         MVC   SPTCDTYP,SPCDQ      CLIENT GROUP DEF                             
*                                                                               
         MVC   SPTCDAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,GRPKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTCDMED,BYTE                                                    
*                                                                               
         MVC   SPTCDID(1),GRPKID                                                
         LA    R1,SPTCDID                                                       
         ICM   R1,8,=C'C'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,GRPBRKCQ     CLTGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPTCG20                                                          
         USING GRPBRKD,R6                                                       
*                                                                               
         MVC   SPTCDB1D,GRPBK1                                                  
         EDIT  GRPBK1LN,SPTCDB1L,ZERO=BLANK                                     
*                                                                               
         MVC   SPTCDB2D,GRPBK2                                                  
         EDIT  GRPBK2LN,SPTCDB2L,ZERO=BLANK                                     
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTCG20  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
***********************************************************************         
* SPTMGR - EXTRACT CLIENT GROUPS                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTCGP   NMOD1 WORKL,*SPTCGP*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTCGD,R2           R2=A(EXTRACT RECORD)                         
         USING GRPRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTCGLEN,SPTCGLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTCGLEN(2),=AL2(SPTCGDL)                                        
         MVC   SPTCGTYP,SPCGQ      CLIENT GROUP                                 
*                                                                               
         MVC   SPTCGAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,GRPKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTCGMED,BYTE                                                    
*                                                                               
         MVC   SPTCGID(1),GRPKID                                                
         LA    R1,SPTCGID                                                       
         ICM   R1,8,=C'C'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LA    R0,GRPKCODE                                                      
         LA    R1,SPTCGGRP                                                      
         BRAS  RE,GROUT                                                         
*                                                                               
         LA    R1,SPTCGCGR                                                      
         MVC   0(2,R1),SPTCGID                                                  
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '                                                       
         BNH   *+8                                                              
         LA    R1,1(R1)                                                         
*                                                                               
         MVC   0(4,R1),SPTCGGRP                                                 
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,GRPGRPCQ     CLTGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPCGR20                                                          
         USING GRPGRPD,R6                                                       
*                                                                               
         MVC   SPTCGB1N,GRPGNAM1                                                
         MVC   SPTCGB2N,GRPGNAM2                                                
*                                                                               
         LA    R5,SPTCGGRP         CLIENT GROUP CODE                            
         ZIC   RF,BREAKLS          SAVED BREAK LENGTHS                          
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPCGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTCGB1C(0),0(R5)                                                
*                                                                               
         AHI   RF,1                                                             
         LA    R5,SPTCGGRP(RF)                                                  
         ZIC   RF,BREAKLS+1                                                     
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPCGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTCGB2C(0),0(R5)                                                
*                                                                               
SPCGR20  DS    0H                                                               
         MVC   SPTCGBTR,BUYTRKER                                                
         CLI   SPTCGBTR,X'40'                                                   
         BH    *+8                                                              
         MVI   SPTCGBTR,C'N'                                                    
*                                                                               
         DROP  R6                                                               
*                                                                               
         OI    FLAGS2,BUMPQ                                                     
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTCGC - EXTRACT CLIENT GROUP CLIENTS                                         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTCGC   NMOD1 WORKL,*SPTCGC*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTCCD,R2           R2=A(EXTRACT RECORD)                         
         USING GRPRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTCCLEN,SPTCCLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTCCLEN(2),=AL2(SPTCCDL)                                        
         MVC   SPTCCTYP,SPCCQ      MARKET GROUP                                 
*                                                                               
         MVC   SPTCCAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,GRPKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTCCMED,BYTE                                                    
*                                                                               
         SR    R9,R9               LOOP COUNTER                                 
         ZIC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,GRPVALCQ     MEDIA ELEMENT                                
         BRAS  RE,GETEL                                                         
*                                                                               
SPTCGC10 DS    0H                                                               
         BE    *+16                                                             
         MVI   ELCOUNT,0             RE-SET ELEMENT COUNT                       
         NI    FLAGS,X'FF'-NORDSEQ                                              
         J     NEQXIT                                                           
*                                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTCGC20                                                         
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING GRPVALD,R6                                                       
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING BRKTABD,R1                                                       
         MVC   BRKTAM,GRPKAGMD    A/M                                           
         MVC   BRKTGID,GRPKID    GROUP ID                                       
         DROP  R1                                                               
*                                                                               
         ICM   RF,15,ABREAKT          A(BREAK TABLE)                            
         LHI   R0,BRKTABNQ         BRTABNQ IN SPXTRACT                          
         CLC   WORK(BRKTKLQ),0(RF)                                              
         BE    *+16                                                             
         LA    RF,BRKTABLQ(RF)                                                  
         BCT   R0,*-14                                                          
         B     SPTCGC20                                                         
*                                                                               
* FOUND THE GROUP IN THE BREAK TABLE                                            
*                                                                               
         USING BRKTABD,RF                                                       
         MVC   SPTCCCGP(1),BRKTGID                                              
         MVC   BREAKLS,BRKTBK1     SAVE BREAK LENGHTS FOR GROUT                 
         DROP  RF                                                               
*                                                                               
         LA    R1,SPTCCCGP                                                      
         ICM   R1,8,=C'C'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LA    R0,GRPKCODE                                                      
         LA    R1,SPTCCCGP+1                                                    
         CLI   0(R1),C' '                                                       
         BNH   *+8                                                              
         LA    R1,1(R1)                                                         
         BRAS  RE,GROUT                                                         
*                                                                               
         MVC   SPTCCCLT,GRPVALUE                                                
*                                                                               
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
         B     SPTCGC50                                                         
         DROP  R6                                                               
*                                                                               
SPTCGC20 DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTCGC10                                                         
*                                                                               
SPTCGC50 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTSDF - EXTRACT STATION GROUP DEFINITIONS                                    
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTSDF   NMOD1 WORKL,*SPTSDF*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSDD,R2           R2=A(EXTRACT RECORD)                         
         USING GRPRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTSDLEN,SPTSDLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSDLEN(2),=AL2(SPTSDDL)                                        
         MVC   SPTSDTYP,SPSDQ      STATION GROUP DEF                            
*                                                                               
         MVC   SPTSDAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,GRPKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTSDMED,BYTE                                                    
*                                                                               
         MVC   SPTSDID(1),GRPKID                                                
         LA    R1,SPTSDID                                                       
         ICM   R1,8,=C'C'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,GRPBRKCQ     CLTGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPTSG20                                                          
         USING GRPBRKD,R6                                                       
*                                                                               
         MVC   SPTSDB1D,GRPBK1                                                  
         EDIT  GRPBK1LN,SPTSDB1L,ZERO=BLANK                                     
*                                                                               
         MVC   SPTSDB2D,GRPBK2                                                  
         EDIT  GRPBK2LN,SPTSDB2L,ZERO=BLANK                                     
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTSG20  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTSGP - EXTRACT STATION GROUPS                                               
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTSGP   NMOD1 WORKL,*SPTSGP*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSGD,R2           R2=A(EXTRACT RECORD)                         
         USING GRPRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTSGLEN,SPTSGLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSGLEN(2),=AL2(SPTSGDL)                                        
         MVC   SPTSGTYP,SPSGQ      CLIENT GROUP                                 
*                                                                               
         MVC   SPTSGAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,GRPKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTSGMED,BYTE                                                    
*                                                                               
         MVC   SPTSGID(1),GRPKID                                                
         LA    R1,SPTSGID                                                       
         ICM   R1,8,=C'C'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LA    R0,GRPKCODE                                                      
         LA    R1,SPTSGGRP                                                      
         BRAS  RE,GROUT                                                         
*                                                                               
         LA    R1,SPTSGSGR                                                      
         MVC   0(2,R1),SPTSGID                                                  
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '                                                       
         BNH   *+8                                                              
         LA    R1,1(R1)                                                         
*                                                                               
         MVC   0(4,R1),SPTSGGRP                                                 
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,GRPGRPCQ     STAGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPSGR20                                                          
         USING GRPGRPD,R6                                                       
*                                                                               
         MVC   SPTSGB1N,GRPGNAM1                                                
         MVC   SPTSGB2N,GRPGNAM2                                                
*                                                                               
         LA    R5,SPTSGGRP         STATION GROUP CODE                           
         ZIC   RF,BREAKLS          SAVED BREAK LENGTHS                          
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPSGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTSGB1C(0),0(R5)                                                
*                                                                               
         AHI   RF,1                                                             
         LA    R5,SPTSGGRP(RF)                                                  
         ZIC   RF,BREAKLS+1                                                     
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPSGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTSGB2C(0),0(R5)                                                
*                                                                               
         DROP  R6                                                               
*                                                                               
SPSGR20  DS    0H                                                               
         OI    FLAGS2,BUMPQ                                                     
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSGS - EXTRACT STATION GROUP STATIONS                                       
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTSGS   NMOD1 WORKL,*SPTSGS*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSSD,R2           R2=A(EXTRACT RECORD)                         
         USING GRPRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTSSLEN,SPTSSLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSSLEN(2),=AL2(SPTSSDL)                                        
         MVC   SPTSSTYP,SPSSQ                                                   
*                                                                               
         MVC   SPTSSAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,GRPKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTSSMED,BYTE                                                    
*                                                                               
         SR    R9,R9               LOOP COUNTER                                 
         ZIC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,GRPVALCQ                                                  
         BRAS  RE,GETEL                                                         
*                                                                               
SPTSGS10 DS    0H                                                               
         BE    *+16                                                             
         MVI   ELCOUNT,0             RE-SET ELEMENT COUNT                       
         NI    FLAGS,X'FF'-NORDSEQ                                              
         J     NEQXIT                                                           
*                                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTSGS20                                                         
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING GRPVALD,R6                                                       
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING BRKTABD,R1                                                       
         MVC   BRKTAM,GRPKAGMD    A/M                                           
         MVC   BRKTGID,GRPKID    GROUP ID                                       
         DROP  R1                                                               
*                                                                               
         ICM   RF,15,ABREAKT          A(BREAK TABLE)                            
         LHI   R0,BRKTABNQ         BRTABNQ IN SPXTRACT                          
         CLC   WORK(BRKTKLQ),0(RF)                                              
         BE    *+16                                                             
         LA    RF,BRKTABLQ(RF)                                                  
         BCT   R0,*-14                                                          
         B     SPTSGS20                                                         
*                                                                               
* FOUND THE GROUP IN THE BREAK TABLE                                            
*                                                                               
         USING BRKTABD,RF                                                       
         MVC   SPTSSSGP(1),BRKTGID                                              
         MVC   BREAKLS,BRKTBK1    SAVE BREAK LENGHTS FOR GROUT                  
         DROP  RF                                                               
*                                                                               
         LA    R1,SPTSSSGP                                                      
         ICM   R1,8,=C'C'                                                       
         BRAS  RE,GETGRP                                                        
*                                                                               
         LA    R0,GRPKCODE                                                      
         LA    R1,SPTSSSGP+1                                                    
         CLI   0(R1),C' '                                                       
         BNH   *+8                                                              
         LA    R1,1(R1)                                                         
         BRAS  RE,GROUT                                                         
*                                                                               
         MVC   SPTSSSTA,GRPVALUE                                                
*                                                                               
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
         B     SPTSGS50                                                         
         DROP  R6                                                               
*                                                                               
SPTSGS20 DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTSGS10                                                         
*                                                                               
SPTSGS50 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPGF - EXTRACT PRODUCT GROUP DEFINITIONS                                    
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTPGF   NMOD1 WORKL,*SPTPGF*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPFD,R2           R2=A(EXTRACT RECORD)                         
         USING PRGRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTPFLEN,SPTPFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPFLEN(2),=AL2(SPTPFDL)                                        
         MVC   SPTPFTYP,SPPFQ      PRODUCT GROUP DEF                            
*                                                                               
         MVC   SPTPFAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,PRGKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTPFMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,PRGKCLT),SPTPFCLT                                
*                                                                               
         MVC   SPTPFID(1),PRGKID                                                
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'01'        PRDGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPTPF20                                                          
         USING PRGEL01,R6                                                       
*                                                                               
         MVC   SPTPFB1D,PRGBK1                                                  
         EDIT  PRGBK1LN,SPTPFB1L,ZERO=BLANK                                     
*                                                                               
         MVC   SPTPFB2D,PRGBK2                                                  
         EDIT  PRGBK2LN,SPTPFB2L,ZERO=BLANK                                     
*                                                                               
         MVC   SPTPFB3D,PRGBK3                                                  
         EDIT  PRGBK3LN,SPTPFB3L,ZERO=BLANK                                     
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTPF20  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTPGR - EXTRACT PRODUCT GROUPS                                               
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTPGR   NMOD1 WORKL,*SPTPGR*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPGD,R2           R2=A(EXTRACT RECORD)                         
         USING PRGRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTPGLEN,SPTPGLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPGLEN(2),=AL2(SPTPGDL)                                        
         MVC   SPTPGTYP,SPPGQ      PRODUCT GROUP                                
*                                                                               
         MVC   SPTPGAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,PRGKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTPGMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,PRGKCLT),SPTPGCLT                                
*                                                                               
         MVC   SPTPGID(1),PRGKID                                                
*                                                                               
         LA    R0,PRGKGRP                                                       
         LA    R1,SPTPGGRP                                                      
         BRAS  RE,GROUT                                                         
*                                                                               
         MVC   SPTPGPGR(1),SPTPGID                                              
         MVC   SPTPGPGR+1(4),SPTPGGRP                                           
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'10'        PRDGRP BREAK DESCRIPTION                     
         BRAS  RE,GETEL                                                         
         BNE   SPPGR20                                                          
         USING PRGEL10,R6                                                       
*                                                                               
         MVC   SPTPGB1N,PRGNAM1                                                 
         MVC   SPTPGB2N,PRGNAM2                                                 
         MVC   SPTPGB3N,PRGNAM3                                                 
*                                                                               
         LA    R5,SPTPGGRP         PRODUCT GROUP CODE                           
         ZIC   RF,BREAKLS          SAVED BREAK LENGTHS                          
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPPGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTPGB1C(0),0(R5)                                                
*                                                                               
         AHI   RF,1                                                             
         LA    R5,SPTPGGRP(RF)                                                  
         ZIC   RF,BREAKLS+1                                                     
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPPGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTPGB2C(0),0(R5)                                                
*                                                                               
         AHI   RF,1                                                             
         LA    R5,SPTPGGRP(RF)                                                  
         ZIC   RF,BREAKLS+1                                                     
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         BL    SPPGR20                                                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTPGB3C(0),0(R5)                                                
*                                                                               
         DROP  R6                                                               
*                                                                               
SPPGR20  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPGP - EXTRACT PRODUCT GROUP PRODUCTS                                       
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTPGP   NMOD1 WORKL,*SPTPGP*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPPD,R2           R2=A(EXTRACT RECORD)                         
         USING PRGRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTPPLEN,SPTPPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPPLEN(2),=AL2(SPTPPDL)                                        
         MVC   SPTPPTYP,SPPPQ                                                   
*                                                                               
         MVC   SPTPPAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,PRGPAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTPPMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,PRGPCLT),SPTPPCLT                                
*                                                                               
         MVC   SPTPPPRD,PRGPPRD                                                 
*                                                                               
         MVC   SPTPPPGP(1),PRGPID                                               
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING BRKTABD,R1                                                       
         MVC   BRKTAM,PRGKAGMD    A/M                                           
         MVC   BRKTCLT,PRGKCLT   CLIENT                                         
         MVC   BRKTGID,PRGKID    GROUP ID                                       
         DROP  R1                                                               
*                                                                               
         ICM   RF,15,ABREAKT          A(BREAK TABLE)                            
         LHI   R0,BRKTABNQ         BRTABNQ IN SPXTRACT                          
         CLC   WORK(BRKTKLQ),0(RF)                                              
         BE    *+16                                                             
         LA    RF,BRKTABLQ(RF)                                                  
         BCT   R0,*-14                                                          
         B     SPTPGP20                                                         
*                                                                               
* FOUND THE GROUP IN THE BREAK TABLE                                            
*                                                                               
         USING BRKTABD,RF                                                       
         MVC   BREAKLS,BRKTBK1    SAVE BREAK LENGHTS FOR GROUT                  
         DROP  RF                                                               
         LA    R0,PRGPGRP                                                       
         LA    R1,SPTPPPGP+1                                                    
         BRAS  RE,GROUT                                                         
*                                                                               
SPTPGP20 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTPAR - EXTRACT PARENT INFO                                                  
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTPAR   NMOD1 WORKL,*SPTPAR*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPRD,R2           R2=A(EXTRACT RECORD)                         
         USING CT99RECD,R3         R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTPRLEN,SPTPRLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPRLEN(2),=AL2(SPTPRDL)                                        
         MVC   SPTPRTYP,SPPRQ                                                   
*                                                                               
         MVC   SPTPRPCD,CT99KMMD                                                
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,CMMNMELQ     MULTIMEDIA NAME ELEMENT                      
         BRAS  RE,GETEL                                                         
         BNE   SPTPR10                                                          
*                                                                               
         USING CMMNAMD,R6                                                       
*                                                                               
         MVC   SPTPRPNM,CMMNAME                                                 
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTPR10  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
***********************************************************************         
***********************************************************************         
*************** GENERAL PURPOSE SUBROUTINES ***************************         
***********************************************************************         
***********************************************************************         
*                                                                               
*                                                                               
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =         
* BYTE IS EXPECTED TO HAVE A/M                                                  
* ON EXIT, BYTE CONTAINS MEDIA CODE                                             
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =         
GETMED   NTR1  BASE=*,LABEL=*                                                   
         NI    BYTE,X'0F'             TURN OFF AGENCY BITS                      
         LA    R5,MEDTAB                                                        
HD10     CLC   BYTE,1(R5)                                                       
         BE    HD20                                                             
         LA    R5,MEDTABLQ(R5)                                                  
         CLI   0(R5),X'FF'                                                      
         BNE   HD10                                                             
HD20     MVC   BYTE,0(R5)         COPY MEDIA CODE TO REPORT                     
         J     EQXIT                                                            
*                                                                               
MEDTAB   DS    0X                                                               
         DC    CL1'T',XL1'01'                                                   
MEDTABLQ EQU   *-MEDTAB                                                         
         DC    CL1'R',XL1'02'                                                   
         DC    CL1'N',XL1'03'                                                   
         DC    CL1'X',XL1'04'                                                   
         DC    CL1'C',XL1'08'                                                   
         DC    X'FF'                                                            
*                                                                               
*                                                                               
*                                                                               
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =         
* R1 IS EXPECTED TO ADDRESS THE GROUP (AS IN RECORD KEY)                        
* R1 HIGH ORDER BYTE - 'C' FOR CLIENT, 'M' FOR MARKET                           
* ON EXIT, TWO BYTES AT 0(R1) WILL BE REPLACED WITH                             
* THE TWO-CHARACTER MARKET GROUP                                                
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =         
GETGRP   NTR1  BASE=*,LABEL=*                                                   
         STCM  R1,8,BYTE                                                        
*                                                                               
         CLI   BYTE,C'C'                                                        
         BNE   *+16                                                             
         LA    RE,SPCGRTAB                                                      
         LHI   RF,(SPCGRTBX-SPCGRTAB)/3                                         
         B     GETGRP50                                                         
*                                                                               
         CLI   BYTE,C'M'                                                        
         BNE   *+16                                                             
         LA    RE,SPMGRTAB                                                      
         LHI   RF,(SPMGRTBX-SPMGRTAB)/3                                         
         B     GETGRP50                                                         
*                                                                               
         J     NEQXIT                                                           
*                                                                               
GETGRP50 DS    0H                                                               
         CLC   0(1,R1),2(RE)                                                    
         BE    *+16                                                             
         LA    RE,3(RE)                                                         
         BCT   RF,*-14                                                          
         J     NEQXIT                                                           
*                                                                               
         MVC   0(2,R1),0(RE)                                                    
         J     EQXIT                                                            
       ++INCLUDE SPMGRTAB                                                       
       ++INCLUDE SPCGRTAB                                                       
*                                                                               
*                                                                               
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =         
* R0 IS EXPECTED TO ADDRESS THE GROUP CODE (AS IN RECORD KEY)                   
* R1 IS EXPECTED TO ADDRESS THE OUTPUT AREA (4 BYTES AT LEAST)                  
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =         
GROUT    NTR1  BASE=*,LABEL=*                                                   
         LR    R2,R0                                                            
         LR    R3,R1                                                            
*                                                                               
         UNPK  DUB1,0(3,R2)                                                     
*                                                                               
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         LHI   R0,3                                                             
         LA    R1,BREAKLS          SAVED BREAK LENGTHS                          
*                                                                               
         IC    RE,0(R1)                                                         
         AR    RF,RE                                                            
         LA    R1,1(R1)                                                         
         BCT   R0,*-10                                                          
*                                                                               
         BCTR  RF,0                                                             
         CHI   RF,0                                                             
         JL    NEQXIT                                                           
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),DUB1+3                                                   
*                                                                               
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDEA - EXTRACT DEAL RECORD                                                  
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDEA   NMOD1 WORKL,*SPTDEA*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTDLD,R2           R2=A(EXTRACT RECORD)                         
         USING SDLKEY,R3           R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTDLLEN,SPTDLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTDLLEN(2),=AL2(SPTDLDL)                                        
         MVC   SPTDLTYP,SPDLQ         DEAL RECORD EQU                           
*                                                                               
         MVC   SPTDLAGY,SXDTAGY                                                 
         MVC   BYTE,SDLKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTDLMED,BYTE                                                    
*                                                                               
         MVC   HALF,SDLKDLNO                                                    
         XC    HALF,=X'FFFF'                                                    
         EDIT  HALF,(4,SPTDLNUM),FILL=0                                         
*                                                                               
         GOTO1 DATCON,DMCB,(2,SDLKSTDT),(10,SPTDLSDT)                           
*                                                                               
         GOTO1 DATCON,DMCB,(2,SDLKNDDT),(10,SPTDLEDT)                           
*                                                                               
         GOTO1 VRCPACK,DMCB,(C'U',SDLKREP),SPTDLREP                             
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,SDLIDELQ     PRIMARY ELEMENT                              
         BRAS  RE,GETEL                                                         
         BNE   SPTDL10                                                          
*                                                                               
         USING SDLIDELD,R6                                                      
         MVC   SPTDLNAM,SDLIDNAM                                                
*                                                                               
SPTDL10  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'F1'        ACTIVITY ELEMENT                             
         BRAS  RE,GETEL                                                         
         BNE   SPTDL50                                                          
*                                                                               
         USING ACTVD,R6                                                         
*                                                                               
         OC    ACTVADDT,ACTVADDT                                                
         BZ    SPTDL20                                                          
         GOTO1 DATCON,DMCB,(3,ACTVADDT),(10,SPTDLCDT)                           
*                                                                               
SPTDL20  DS    0H                                                               
         OC    ACTVADID,ACTVADID                                                
         BZ    SPTDL30                                                          
*                                                                               
         LA    R1,ACTVADID                                                      
         BRAS  RE,DISPID                                                        
         MVC   SPTDLCRE,WORK                                                    
*                                                                               
SPTDL30  DS    0H                                                               
         OC    ACTVCHDT,ACTVCHDT                                                
         BZ    SPTDL40                                                          
         GOTO1 DATCON,DMCB,(3,ACTVCHDT),(10,SPTDLLUD)                           
*                                                                               
SPTDL40  DS    0H                                                               
         OC    ACTVCHID,ACTVCHID                                                
         BZ    SPTDL50                                                          
*                                                                               
         LA    R1,ACTVCHID                                                      
         BRAS  RE,DISPID                                                        
         MVC   SPTDLUBY,WORK                                                    
*                                                                               
SPTDL50  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDST - EXTRACT DEAL STATIONS                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTDST   NMOD1 WORKL,*SPTDST*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTDSD,R2           R2=A(EXTRACT RECORD)                         
         USING SDLKEY,R3           R3=A(SPOT RECORD)                            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTDSLEN,SPTDSLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTDSLEN(2),=AL2(SPTDSDL)                                        
         MVC   SPTDSTYP,SPDSQ         DEAL STATIONS EQU                         
*                                                                               
         MVC   SPTDSAGY,SXDTAGY                                                 
         MVC   BYTE,SDLKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTDSMED,BYTE                                                    
*                                                                               
         MVC   HALF,SDLKDLNO                                                    
         XC    HALF,=X'FFFF'                                                    
         EDIT  HALF,(4,SPTDSNUM),FILL=0                                         
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTDST05                                                         
*                                                                               
         MVI   SPTDSACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTDS50                                                          
*                                                                               
* READ STATION ELEMENTS                                                         
*                                                                               
SPTDST05 DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTDSACT,C'A'                                                    
*                                                                               
         SR    R9,R9               LOOP COUNTER                                 
         ZIC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,SDLSTELQ     STATION ELEMENT                              
         BRAS  RE,GETEL                                                         
*                                                                               
SPTDST10 DS    0H                                                               
         BE    SPTDST15                                                         
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'FF'                                                      
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         J     NEQXIT                                                           
*                                                                               
SPTDST15 DS    0H                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTDST20                                                         
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING SDLSTEL,R6                                                       
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVC   STAPMED,SPTDSMED                                                 
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPSTA,SDLSTSTA                                                 
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK                                                          
         CLI   STAPERR,0                                                        
         BNE   *+10                                                             
         MVC   SPTDSSTA,STAPQSTA                                                
*                                                                               
         DROP  R1                                                               
*                                                                               
         MVC   SPTDSBYR,SDLSTBYR                                                
*                                                                               
         EDIT  SDLSTGRS,(10,SPTDSGBD),FILL=0                                    
         EDIT  SDLSTNET,(10,SPTDSNBD),FILL=0                                    
         EDIT  SDLSTCCM,(3,SPTDSPCS),FILL=0                                     
*                                                                               
         B     SPTDS50                                                          
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTDST20 DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTDST10                                                         
*                                                                               
SPTDS50  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
* ON ENTRANCE R1 EXPECTED TO ADDRESS 2 BYTE PID                                 
* ON EXIT WORK WILL HAVE THE 8-CHAR PID                                         
DISPID   NTR1  BASE=*,LABEL=*                                                   
         MVC   SVDTDISP,DATADISP                                                
*                                                                               
         CLC   0(2,R1),=X'0FFF'   DDS PID?                                      
         BH    *+14                NO                                           
         MVC   WORK(8),=CL8'DDS'                                                
         B     DISPIDX                                                          
*                                                                               
         LR    R2,R1               SAVE A(PID)                                  
         XC    WORK(8),WORK                                                     
*                                                                               
* GET THE SECURITY AGY FROM ACCESS RECORD                                       
*                                                                               
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
*                                                                               
         USING CT5REC,R1                                                        
         MVI   CT5KTYP,CT5KTYPQ                                                 
         MVC   CT5KALPH,SXDTAGY                                                 
         DROP  R1                                                               
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'CTFILE',KEY,STAIO                    
         CLC   KEY(L'CT5KEY),STAIO                                              
         BNE   DISPD10                                                          
*                                                                               
         LA    R6,STAIO                                                         
         MVI   ELCODE,CTSEAELQ                                                  
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
         BRAS  RE,GETEL                                                         
         BNE   DISPD10                  NO NAME ELEMENT                         
         MVC   SVSECAGY,2(R6)                                                   
*                                                                               
* READ PASSWORD RECORD                                                          
*                                                                               
DISPD10  DS    0H                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,C'0'                                                         
         MVC   KEY+1(2),SVSECAGY                                                
         CLC   SVSECAGY,=C'  '     DO WE HAVE SECURITY AGY?                     
         BH    *+10                YES, USE IT INSTEAD OF AGENCY                
         MVC   KEY+1(2),SXDTAGY                                                 
         MVC   KEY+23(2),0(R2)     PID                                          
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'CTFILE',KEY,STAIO                    
         LA    R6,STAIO                                                         
         CLC   KEY(25),0(R6)                                                    
         BNE   DISPIDX                                                          
*                                                                               
         MVI   ELCODE,X'C3'                                                     
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
         BRAS  RE,GETEL                                                         
         BNE   DISPIDX                  NO NAME ELEMENT                         
         MVC   WORK(8),2(R6)                                                    
*                                                                               
DISPIDX  DS    0H                                                               
         MVC   DATADISP,SVDTDISP                                                
         J     EQXIT                                                            
*                                                                               
*                                                                               
***********************************************************************         
* SPTREP - EXTRACT REP RECORD                                                   
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTREP   NMOD1 WORKL,*SPTREP*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTRPD,R2           R2=A(EXTRACT RECORD)                         
         USING REPREC,R3           R3=A(SPOT REP RECORD)                        
*                                                                               
         XC    SPTRPLEN,SPTRPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTRPLEN(2),=AL2(SPTRPDL)                                        
         MVC   SPTRPTYP,SPRPQ                                                   
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTRPAGY,SXDTAGY                                                 
         MVC   SPTRPMED,REPKMED                                                 
         MVC   SPTRPREP,REPKREP                                                 
         MVC   SPTRPNAM,RNAME                                                   
         MVC   SPTRPADR,R1LINE                                                  
         MVC   SPTRPCIT,R2LINE                                                  
         MVC   SPTRPSTT,R3LINE                                                  
         MVC   SPTRPZIP,RBIGZIP                                                 
*                                                                               
         MVI   SPTRPCTR,C'U'                                                    
         CLC   RZIP,=C'CANAD'                                                   
         BNE   *+8                                                              
         MVI   SPTRPCTR,C'C'                                                    
*                                                                               
         MVI   SPTRPUNW,C'N'                                                    
         CLI   RUNWNET,C'Y'                                                     
         BNE   *+8                                                              
         MVI   SPTRPUNW,C'Y'                                                    
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTNL - EXTRACT SYSCODE/NETWORK LIST FROM STATION RECORDS                     
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
*                                                                               
* BREAKTAB USED TO SAVE LIST OF NETWORKS                                        
* ELCOUNT - NETWORK COUNTER                                                     
***********************************************************************         
         SPACE 1                                                                
SPTNL    NMOD1 WORKL,*SPTNL*,CLEAR=Y                                            
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTNLD,R2           R2=A(EXTRACT RECORD)                         
         USING STAREC,R3           R3=A(SPOT STATION RECORD)                    
*                                                                               
* 05/13/10 - WE'RE NOW EXTRACTING THE DEACTIVATED STATIONS                      
         B     SPTNL05                                                          
*        CLI   SSYSDACT,X'FF'                                                   
*        BNE   SPTNL05                                                          
*                                                                               
         CLI   SPTNLACT,C'L'                                                    
         BNE   *+12                                                             
         OI    FLAGS2,NOWRITEQ                                                  
         J     EQXIT                                                            
*                                                                               
         CLI   SPTNLACT,C'C'                                                    
         BNE   *+12                                                             
         OI    FLAGS,KILLQ                                                      
         NI    FLAGS,X'FF'-MULTIQ                                               
*                                                                               
SPTNL05  DS    0H                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         L     R7,ABREAKT                                                       
         CLI   0(R7),C' '          NETWORK TABLE BUILT ALREADY?                 
         BH    SPTNL100            YES - GET NEXT NETWORK                       
*                                                                               
* PACK THE STATION W/O NETWORK                                                  
*                                                                               
         XC    WORK+3(L'WORK-3),WORK+3                                          
         LA    R1,WORK+3                                                        
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'P'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVI   STAPMED,C'T'                                                     
         MVC   STAPQMKT,=C'0000'                                                
         MVC   STAPQSTA,STAKCALL                                                
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK+3                                                        
         CLI   STAPERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   DUB1,STAPSTA                                                     
         DROP  R1                                                               
*                                                                               
* BUILD LIST OF NETWORKS                                                        
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(3),SCBL24      SAVE TOP24                                   
         LA    R5,WORK             1ST BYTE OF SAVED TOP24                      
         LHI   R6,1                POSITION COUNTER                             
*                                                                               
SPTNL10  TM    0(R5),X'80'         FIRST BIT SET?                               
         BZ    SPTNL20             NO-ADVANCE TO NEXT ONE                       
*                                                                               
         XC    WORK+3(L'WORK-3),WORK+3                                          
         LA    R1,WORK+3                                                        
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVI   STAPMED,C'T'                                                     
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPSTA,DUB1                                                     
         STC   R6,BYTE                                                          
         OC    STAPSTA+2(1),BYTE                                                
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK+3                                                        
         CLI   STAPERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   0(3,R7),STAPQNET                                                 
         LA    R7,3(R7)                                                         
         DROP  R1                                                               
*                                                                               
SPTNL20  SR    RE,RE                                                            
         ICM   RE,7,WORK                                                        
         SLL   RE,1                SHIFT ALL BITS LEFT BY ONE                   
         STCM  RE,7,WORK                                                        
         AHI   R6,1                ADVANCE POSITION COUNTER                     
         OC    WORK(3),WORK        ANYTHING LEFT?                               
         BNZ   SPTNL10             YES - KEEP GOING                             
*                                                                               
* TOP 24 NETWORKS DONE HERE                                                     
* DO THE NON-T24 NOW                                                            
*                                                                               
         LA    R5,SCBLSEQ                                                       
         LHI   R6,25                                                            
*                                                                               
SPTNL30  DS    0H                                                               
         CHI   R6,127                                                           
         BH    SPTNL100                                                         
         OC    0(2,R5),0(R5)                                                    
         BZ    SPTNL40                                                          
         CLC   0(2,R5),=X'FFFF'                                                 
         BE    SPTNL40                                                          
*                                                                               
         XC    WORK+3(L'WORK-3),WORK+3                                          
         LA    R1,WORK+3                                                        
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVI   STAPMED,C'T'                                                     
         MVC   STAPSTA,DUB1                                                     
         STC   R6,BYTE                                                          
         OC    STAPSTA+2(1),BYTE                                                
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK+3                                                        
         CLI   STAPERR,0                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   0(3,R7),STAPQNET                                                 
         LA    R7,3(R7)                                                         
SPTNL40  LA    R5,2(R5)                                                         
         AHI   R6,1                                                             
         B     SPTNL30                                                          
*                                                                               
* NETWORK LIST BUILT HERE.  EXTRACT INDIVIDUAL NETWORKS                         
*                                                                               
SPTNL100 DS    0H                                                               
*                                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTNLACT,C'A'                                                    
*                                                                               
         XC    SPTNLLEN,SPTNLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTNLLEN(2),=AL2(SPTNLDL)                                        
         MVC   SPTNLTYP,SPNLQ                                                   
         MVC   SPTNLAGY,SXDTAGY                                                 
         MVC   SPTNLMED,STAKMED                                                 
         MVC   SPTNLSYS,STAKCALL                                                
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTNL110                                                         
*                                                                               
         MVI   SPTNLACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     EQXIT                                                            
*                                                                               
SPTNL110 DS    0H                                                               
         L     R7,ABREAKT                                                       
         ZIC   RF,ELCOUNT                                                       
         MHI   RF,3                                                             
         AR    R7,RF                                                            
         OC    0(2,R7),0(R7)                                                    
         BNZ   SPTNL120                                                         
*                                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,X'00'                                                    
         NI    FLAGS,X'FF'-MULTIQ                                               
         J     NEQXIT                                                           
*                                                                               
SPTNL120 DS    0H                                                               
         ZIC   RF,ELCOUNT                                                       
         AHI   RF,1                                                             
         STC   RF,ELCOUNT                                                       
*                                                                               
         CLC   =C'ZYX',0(R7)                                                    
         BE    SPTNL110                                                         
*                                                                               
         MVC   SPTNLNET,0(R7)                                                   
*                                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTUCM - EXTRACT SPOT UCOM RECORD                                             
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTUCM   NMOD1 WORKL,*SPTUCM*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTUCMD,R2          R2=A(EXTRACT RECORD)                         
         USING UCOMHDR,R3          R3=A(SPOT UCOM RECORD)                       
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
         XC    SPTUCLEN,SPTUCLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTUCLEN(2),=AL2(SPTUCMDL)                                       
         MVC   SPTUCTYP,SPUCQ                                                   
*                                                                               
         TM    FLAGS2,UPDLOADQ                                                  
         BZ    *+8                                                              
         MVI   SPTUCACT,C'A'                                                    
*                                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTUCACT,C'A'                                                    
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTUCAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,UCOMKAGY                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTUCMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,UCOMKCLT),SPTUCCLT                               
*                                                                               
         OC    UCOMKPRD,UCOMKPRD                                                
         BZ    SPTUC25                                                          
*                                                                               
         MVC   SPTUCPRD,UCOMKPRD                                                
*                                                                               
         EDIT  UCOMKEST,SPTUCEST,FILL=0                                         
*                                                                               
         MVC   SPTUCMKT,=C'N/A '                                                
         OC    UCOMKMKT,UCOMKMKT                                                
         BZ    SPTUC25                                                          
         EDIT  UCOMKMKT,SPTUCMKT,FILL=0                                         
*                                                                               
SPTUC25  DS    0H                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTUC30                                                          
*                                                                               
         MVI   SPTUCACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTUC100                                                         
*                                                                               
* PROCESS THE ELEMENTS                                                          
*                                                                               
SPTUC30  DS    0H                                                               
         MVC   HALF,=X'414E'                                                    
*                                                                               
         OC    UCOMKEST,UCOMKEST                                                
         BZ    SPTUC35                                                          
         MVC   HALF,=X'515E'                                                    
*                                                                               
         OC    UCOMKMKT,UCOMKMKT                                                
         BZ    SPTUC35                                                          
         MVC   HALF,=X'616E'                                                    
*                                                                               
* FIRST, FIND THE FIELD DEFINITION IN BREAKTAB                                  
*                                                                               
SPTUC35  DS    0H                                                               
         ICM   R5,15,ABREAKT       SAVED FIELD DEFINITIONS                      
         SR    R9,R9               ELEMENT COUNT                                
*                                                                               
SPTUC40  CLI   0(R5),X'00'         EOR?                                         
         BNE   SPTUC50                                                          
*                                                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
SPTUC50  DS    0H                                                               
         CLC   0(1,R5),HALF                                                     
         BL    SPTUC55                                                          
         CLC   0(1,R5),HALF+1                                                   
         BH    SPTUC55                                                          
*                                                                               
         CLM   R9,1,ELCOUNT                                                     
         BNL   SPTUC60                                                          
*                                                                               
         AHI   R9,1                INCREMENT ELEMENT COUNTER                    
SPTUC55  DS    0H                                                               
         ZIC   R0,1(R5)                                                         
         AR    R5,R0                                                            
         B     SPTUC40                                                          
*                                                                               
SPTUC60  DS    0H                                                               
         ZIC   R0,ELCOUNT                                                       
         AHI   R0,1                                                             
         STC   R0,ELCOUNT                                                       
*                                                                               
         MVC   HALF,0(R5)                                                       
         NI    HALF,X'0F'                                                       
         EDIT  (B1,HALF),SPTUCSEQ       FIELD SEQUENCE NUMBER                   
*                                                                               
         ZIC   RE,1(R5)            ELEMENGT LENGTH                              
         SHI   RE,7                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SPTUCNAM(0),SUCMDT-SUCMELEM(R5) FIELD NAME                       
         OC    SPTUCNAM,SPACES                                                  
*                                                                               
         MVC   SPTUCEDT,SUCMEDIT-SUCMELEM(R5) EDITABLE                          
*                                                                               
         MVC   BYTE,SUCMLEN-SUCMELEM(R5)                                        
         EDIT  BYTE,SPTUCLN        FIELD LENGTH                                 
*                                                                               
         MVI   SPTUCMBI,C'N'                                                    
         TM    SUCMUSE1-SUCMELEM(R5),X'08'                                      
         BZ    *+8                                                              
         MVI   SPTUCMBI,C'Y'                                                    
*                                                                               
* FIELD DEFINITION DISPLAYED, NOW SEE IF THE RECORD HAS A COMMENT               
*                                                                               
         LA    R6,SUCMELEM         FIRST ELEMENT IN THE RECORD                  
*                                                                               
SPTUC70  DS    0H                                                               
         CLI   0(R6),X'00'         EOR                                          
         BE    SPTUC100                                                         
*                                                                               
         MVC   HALF+1(1),0(R6)                                                  
         NI    HALF+1,X'0F'                                                     
         CLC   HALF+1(1),HALF                                                   
         BE    SPTUC80                                                          
*                                                                               
         ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     SPTUC70                                                          
*                                                                               
SPTUC80  DS    0H                                                               
         ZIC   RE,1(R6)            ELEMENGT LENGTH                              
         SHI   RE,7                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SPTUCTXT(0),SUCMDT-SUCMELEM(R6)                                  
         OC    SPTUCTXT,SPACES                                                  
*                                                                               
SPTUC100 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSLK - EXTRACT STATION LOCKIN RECORDS                                       
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(XSPFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTSLK   NMOD1 WORKL,*SPTSLK*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSLD,R2           R2=A(EXTRACT RECORD)                         
         USING SLKRECD,R3          R3=A(STATION LOCKIN RECORD)                  
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
         XC    SPTSLLEN,SPTSLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSLLEN(2),=AL2(SPTSLDL)                                        
         MVC   SPTSLTYP,SPSLQ                                                   
*                                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTSLACT,C'A'                                                    
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
* AGENCY                                                                        
         MVC   SPTSLAGY,SXDTAGY                                                 
* MEDIA                                                                         
         MVC   BYTE,SLKKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTSLMED,BYTE                                                    
* CLIENT                                                                        
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,SLKKCLT),SPTSLCLT                                
*                                                                               
* MARKET, STATION                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVC   STAPMED,SPTSLMED                                                 
*                                                                               
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
*                                                                               
         MVC   STAPMKT,SLKKMKT                                                  
         MVC   STAPSTA,SLKKSTA                                                  
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         CLC   =C'ZYX',STAPQNET                                                 
         BNE   SPTSL05                                                          
*                                                                               
         OI    FLAGS2,NOWRITEQ                                                  
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
SPTSL05  DS    0H                                                               
         LA    R1,WORK                                                          
         CLI   STAPERR,0                                                        
         BNE   *+16                                                             
         MVC   SPTSLMKT,STAPQMKT                                                
         MVC   SPTSLSTA(4),STAPQSTA                                             
*                                                                               
         CLI   SPTSLMED,C'R'                                                    
         BNE   *+10                                                             
         MVC   SPTSLSTA+4(1),STAPQSTA+4                                         
*                                                                               
         CLI   STAPQSTA+4,C'L'                                                  
         BNE   *+8                                                              
         MVI   SPTSLSTA+4,C'L'                                                  
*                                                                               
         CLI   STAPQSTA+4,C'D'                                                  
         BNE   *+8                                                              
         MVI   SPTSLSTA+4,C'D'                                                  
*                                                                               
         CLI   SPTSLMED,C'X'                                                    
         BNE   *+8                                                              
         MVI   SPTSLSTA+4,C'X'                                                  
*                                                                               
         CLI   STAPQSTA,C'0'       CABLE?                                       
         BL    *+10                                                             
         MVC   SPTSLSTA+5(3),STAPQNET                                           
*                                                                               
         DROP  R1                                                               
*                                                                               
         MVC   SPTSLNET,=C'N/A '                                                
*                                                                               
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   SPTSL10                                                          
         CLI   SPTSLMED,C'N'                                                    
         BNE   SPTSL10                                                          
*                                                                               
         LA    R1,SLKKSTA                                                       
         BRAS  RE,GETCNET                                                       
         BNE   *+10                                                             
         MVC   SPTSLNET,WORK                                                    
*                                                                               
SPTSL10  DS    0H                                                               
* PRODUCT 1                                                                     
         LA    R1,SPTSLP1                                                       
         ICM   R1,8,SLKKPRD                                                     
         BRAS  RE,FINDAPRD                                                      
*                                                                               
* PRODUCT 2                                                                     
         CLI   SLKKPRD2,X'00'                                                   
         BNE   *+14                                                             
         MVC   SPTSLP2,=C'N/A'                                                  
         B     *+16                                                             
*                                                                               
         LA    R1,SPTSLP2                                                       
         ICM   R1,8,SLKKPRD2                                                    
         BRAS  RE,FINDAPRD                                                      
*                                                                               
* ESTIMATE                                                                      
         EDIT  SLKKEST,SPTSLEST,FILL=0                                          
*                                                                               
* DAYPART                                                                       
         MVC   SPTSLDP1,SLKKDPT                                                 
*                                                                               
* SPOT LENGTH                                                                   
         ZIC   R7,SLKKLEN                                                       
         ZIC   R0,SLKKLEN2                                                      
         AR    R7,R0                                                            
         EDIT  (R7),SPTSLSEC,FILL=0                                             
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTSL20                                                          
*                                                                               
         MVI   SPTSLACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTSL400                                                         
*                                                                               
* PROCESS THE ELEMENTS                                                          
*                                                                               
SPTSL20  DS    0H                                                               
         LA    R5,SLKFSTEL         SAVE A(DESC ELEM)                            
         USING SLKEL,R5                                                         
*                                                                               
* STAIO(13) - BINARY DEMO CODES                                                 
* STAIO+15(24) - 6-CHAR DEMO NAMES                                              
* STAIO+100 - DBLOCK                                                            
         LA    RE,STAIO                                                         
         LHI   RF,L'STAIO                                                       
         XCEFL                                                                  
*                                                                               
         LA    R7,STAIO                                                         
         MVC   0(3,R7),SLKDEM1                                                  
         MVC   3(3,R7),SLKDEM2                                                  
         MVC   6(3,R7),SLKDEM3                                                  
         MVC   9(3,R7),SLKDEM4                                                  
         MVI   12(R7),X'FF'                                                     
*                                                                               
* 01/29/09 - SKIP DEMOCON CALLS, JUST EDIT THE DEMO OUT                         
         B     SPTSL30                                                          
*                                                                               
         LA    R7,100(R7)                                                       
         USING DBLOCK,R7                                                        
         MVC   DBCOMFCS,COMFACS                                                 
         MVC   DBFILE,=C'TP '                                                   
         MVC   DBSELMED,=C'T'     SXDTCTRY, FOR NOW - US                        
         DROP  R7                                                               
*                                                                               
         LA    R7,STAIO                                                         
         GOTO1 VDEMOCON,DMCB,(4,0(R7)),(6,15(R7)),(C'S',100(R7)),0              
*                                                                               
SPTSL30  DS    0H                                                               
         SR    R9,R9               ELEMENT COUNT                                
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,LOKELCDQ     WEEKLY LOCKIN ELEMENT                        
         BRAS  RE,GETEL                                                         
*                                                                               
SPTSL40  DS    0H                                                               
         BE    *+16                                                             
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
         CLM   R9,1,ELCOUNT                                                     
         BNL   SPTSL50                                                          
*                                                                               
         AHI   R9,1                INCREMENT ELEMENT COUNTER                    
         BRAS  RE,NEXTEL                                                        
         B     SPTSL40                                                          
*                                                                               
SPTSL50  DS    0H                                                               
         AHI   R9,1                INCREMENT ELEMENT COUNTER                    
         STC   R9,ELCOUNT          NEXT ELEMENT TO READ                         
*                                                                               
         USING LOKEL,R6                                                         
*                                                                               
         GOTO1 DATCON,DMCB,(2,LOKWEEK),(10,SPTSLWK)                             
*                                                                               
         EDIT  LOKSPOTS,SPTSLSPT,FILL=0                                         
*                                                                               
         EDIT  LOKDOLS,SPTSLDOL,2,FILL=0                                        
*                                                                               
         EDIT  LOKDOL2,SPTSLC2,2,FILL=0                                         
*                                                                               
*        LA    R5,STAIO+15         FIRST DEMO NAME                              
         LA    R5,STAIO            FIRST DEMO NAME                              
         LA    R7,SPTSLDM1                                                      
         LA    R9,LOKDEM                                                        
         LA    RF,LOKDEM4                                                       
         ST    RF,FULL                                                          
*                                                                               
SPTSL60  DS    0H                                                               
         MVC   0(1,R7),1(R5)                                                    
         EDIT  (B1,2(R5)),(3,2(R7)),FILL=0                                      
*                                                                               
         ICM   R1,15,0(R9)                                                      
         SLA   R1,2                STRIP OFF POSSIBLE X'40' BIT                 
         BO    SPTSL70             ON OVERFLOW, IT'S A 2 DECIMAL DEMO           
         SRA   R1,2                                                             
         EDIT  (R1),(6,6(R7)),1,FILL=0                                          
         B     SPTSL80                                                          
*                                                                               
SPTSL70  SRL   R1,2                                                             
         EDIT  (R1),(6,6(R7)),2,FILL=0                                          
*                                                                               
SPTSL80  SRL   R1,2                                                             
         C     R9,FULL                                                          
         BNL   SPTSL100                                                         
*                                                                               
         LA    R7,(SPTSLDM2-SPTSLDM1)(R7)                                       
*        LA    R5,6(R5)                                                         
         LA    R5,3(R5)                                                         
         LA    R9,(L'LOKDEM)(R9)                                                
         B     SPTSL60                                                          
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTSL100 DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+1(3),SLKKAGMD   A/M,CLT                                      
         MVC   KEY+4(3),SPTSLP1                                                 
         MVC   KEY+7(1),SLKKEST                                                 
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(8),KEY                                                   
         BNE   SPTSL300                                                         
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R1,STAIO                                                         
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING DPTKEY,R6                                                        
         MVC   DPTKMENU,(EDAYMENU-ESTHDR)(R1)                                   
         MVI   DPTKTYPE,X'08'                                                   
         MVC   DPTKAGY,SPTSLAGY                                                 
         MVC   DPTKMED,SPTSLMED                                                 
         MVC   KEYSAVE,KEY                                                      
         DROP  R6                                                               
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(L'DPTKEY),KEY                                            
         BNE   SPTSL300                                                         
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,STAIO                                                         
         MVI   ELCODE,X'01'                                                     
         MVC   SVDTDISP,DATADISP                                                
         MVI   DATADISP+1,24       SPOT FILE                                    
         BRAS  RE,GETEL                                                         
         MVC   DATADISP,SVDTDISP                                                
         BNE   SPTSL300                                                         
*                                                                               
         LA    R6,2(R6)                                                         
SPTSL120 OC    0(2,R6),0(R6)                                                    
         BZ    SPTSL300            EOT                                          
         CLC   SLKKDPT,0(R6)                                                    
         BE    *+12                                                             
         LA    R6,5(R6)                                                         
         B     SPTSL120                                                         
*                                                                               
         MVC   SPTSLDP3,2(R6)                                                   
*                                                                               
SPTSL300 DS    0H                                                               
*                                                                               
SPTSL400 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDPM - EXTRACT DAYPART MENU RECORDS                                         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDPM   NMOD1 WORKL,*SPTDPM*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTDMD,R2           R2=A(EXTRACT RECORD)                         
         USING DPTHDR,R3           R3=A(SPOT STATION ADDRESS RECORD)            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTDMLEN,SPTDMLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTDMLEN(2),=AL2(SPTDMDL)                                        
         MVC   SPTDMTYP,SPDMQ                                                   
*                                                                               
*                                                                               
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTDMAGY,SXDTAGY                                                 
         MVC   SPTDMMED,DPTKMED                                                 
         MVC   SPTDMMEN,DPTKMENU                                                
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTDPM40                                                         
*                                                                               
         MVI   SPTDMACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTDPMX                                                          
*                                                                               
SPTDPM40 DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTDMACT,C'A'                                                    
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'01'        AGENCY ELEMENT                               
         BRAS  RE,GETEL                                                         
         BNE   SPTDPMX                                                          
         USING DPTEL,R6                                                         
*                                                                               
         LLC   R7,ELCOUNT                                                       
         MHI   R7,5                                                             
         LA    R5,DPTCODES(R7)                                                  
*                                                                               
         CLI   0(R5),X'00'                                                      
         BNE   SPTDPM50                                                         
*                                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
SPTDPM50 DS    0H                                                               
         MVC   SPTDMCOD,0(R5)                                                   
*        EDIT  (B1,1(R5)),SPTDMGCD,FILL=0                                       
         GOTO1 =V(HEXOUT),DMCB,1(R5),SPTDMGCD,1,0                               
         MVC   SPTDMDPT,2(R5)                                                   
*                                                                               
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
SPTDPMX  DS    0H                                                               
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3,R6                                                         
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDEM - EXTRACT DEMO CODES AND NAMES                                         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDEM   NMOD1 WORKL,*SPTDEM*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         CLI   SXDTCTRY,CTRYCANQ   TEST CANADA                                  
         JNE   SPTDE2              NO                                           
* NO DEMO RECORDS FOR CANADA                                                    
         OI    FLAGS,NOMOREQ       ELSE SET NO MORE DATA                        
         J     NEQXIT                                                           
*                                                                               
         USING SPTDED,R2           R2=A(EXTRACT RECORD)                         
*                                                                               
SPTDE2   OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTDELEN,SPTDELEN                                                
         MVC   SPTDELEN(2),=AL2(SPTDEDL)                                        
         MVC   SPTDETYP,SPDEQ                                                   
*                                                                               
         MVC   SPTDEAGY,SXDTAGY                                                 
*                                                                               
         OC    LASTDEM,LASTDEM     TEST TO DO COMSCORE NOW                      
         JNZ   SPTDE10                                                          
*                                                                               
         LLC   R0,ELCOUNT                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTDECOD,DUB                                                     
                                                                                
* DATA EXTRACT PART                                                             
                                                                                
         XC    WORK,WORK                                                        
         LA    R7,WORK                                                          
         USING DBLOCK,R7                                                        
         MVC   DBCOMFCS,COMFACS                                                 
         MVC   DBFILE,=C'TP '                                                   
         MVC   DBSELMED,=C'T'     SXDTCTRY, FOR NOW - US                        
         DROP  R7                                                               
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(1),ELCOUNT                                                
*                                                                               
         GOTO1 VDEMOCON,DMCB,(1,FULL),(2,WORK),(C'S',WORK),0                    
         MVC   SPTDENAM,WORK+1                                                  
*                                                                               
         CLI   SPTDENAM,C'*'                                                    
         BNE   *+8                                                              
         OI    FLAGS2,NOWRITEQ                                                  
*                                                                               
         LLC   RF,ELCOUNT                                                       
         AHI   RF,1                                                             
         STC   RF,ELCOUNT                                                       
*                                                                               
         CLI   ELCOUNT,X'FF'                                                    
         JNE   *+10                                                             
         MVC   LASTDEM,=X'FFFF'    SET A FLAG FOR NEXT TIME                     
         J     SPTDEX                                                           
                                                                                
*===========================================================                    
* READ OD2A POINTERS FOR COMSCORE DEMO CODES                                    
*===========================================================                    
                                                                                
SPTDE10  XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING NTSQKEY,R3                                                       
*                                                                               
         MVI   NTSQKTYP,NTSQKTYPQ  0D                                           
         MVI   NTSQKSUB,NTSQKSUBQ  2A                                           
         MVI   NTSQRTGSV,C'C'                                                   
         MVC   NTSQKAGMD,SXDTAGB   MOVE AGY CODE (LEFT ALIGNED)                 
         OI    NTSQKAGMD,X'01'     SET MEDIA=TV                                 
         XR    RE,RE                                                            
         CLC   LASTDEM,=X'FFFF'                                                 
         JE    *+8                                                              
         ICM   RE,3,LASTDEM                                                     
         LA    RE,1(RE)                                                         
         STCM  RE,3,LASTDEM                                                     
         STCM  RE,3,NTSQKSEQ                                                    
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY                      
         CLC   KEY(4),KEYSAVE                                                   
         JNE   SPTDE18                                                          
*                                                                               
SPTDE16  XR    R0,R0                                                            
         ICM   R0,3,NTSQKSEQ                                                    
         STCM  R0,3,LASTDEM        SAVE ACTUAL DEMO NUMBER                      
         X     R0,=X'0000FFFF'     COMPLEMENT                                   
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTDECOD(5),DUB                                                  
         MVI   SPTDENAM,C'X'                                                    
         MVC   SPTDENAM+1(6),NTSQKALPH                                          
                                                                                
* NEED TO SEE IF THERE ARE ANY MORE                                             
                                                                                
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=C'DMRSEQ',=C'SPTDIR',KEYSAVE,KEY                      
         J     SPTDEX                                                           
*                                                                               
SPTDE18  OI    FLAGS,NOMOREQ                                                    
         J     EQXIT                                                            
*                                                                               
SPTDEX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         DROP  R2                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDEMT - EXTRACT TRADITIONAL DEMO CODES AND NAMES                            
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDEMT  NMOD1 WORKL,*SPTDMT*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         CLI   SXDTCTRY,CTRYCANQ   TEST CANADA                                  
         JNE   SPTDT2              NO                                           
* NO DEMO RECORDS FOR CANADA                                                    
         OI    FLAGS,NOMOREQ       ELSE SET NO MORE DATA                        
         J     NEQXIT                                                           
*                                                                               
         USING SPTDED,R2           R2=A(EXTRACT RECORD)                         
*                                                                               
SPTDT2   OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTDELEN,SPTDELEN                                                
         MVC   SPTDELEN(2),=AL2(SPTDEDL)                                        
         MVC   SPTDETYP,SPDEQ                                                   
*                                                                               
         MVC   SPTDEAGY,SXDTAGY                                                 
*                                                                               
         LLC   R0,ELCOUNT                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTDECOD,DUB                                                     
                                                                                
* DATA EXTRACT PART                                                             
                                                                                
         XC    WORK,WORK                                                        
         LA    R7,WORK                                                          
         USING DBLOCK,R7                                                        
         MVC   DBCOMFCS,COMFACS                                                 
         MVC   DBFILE,=C'TP '                                                   
         MVC   DBSELMED,=C'T'     SXDTCTRY, FOR NOW - US                        
         DROP  R7                                                               
*                                                                               
         XC    FULL,FULL                                                        
         MVC   FULL+2(1),ELCOUNT                                                
*                                                                               
         GOTO1 VDEMOCON,DMCB,(1,FULL),(2,WORK),(C'S',WORK),0                    
         MVC   SPTDENAM,WORK+1                                                  
*                                                                               
         CLI   SPTDENAM,C'*'                                                    
         BNE   *+8                                                              
         OI    FLAGS2,NOWRITEQ                                                  
*                                                                               
         LLC   RF,ELCOUNT                                                       
         AHI   RF,1                                                             
         STC   RF,ELCOUNT                                                       
*                                                                               
         CLI   ELCOUNT,X'FF'                                                    
         JNE   SPTDTX                                                           
         OI    FLAGS,NOMOREQ       ELSE SET NO MORE DATA                        
         J     SPTDTX                                                           
                                                                                
SPTDTX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         DROP  R2                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDEMN - EXTRACT NON-TRADITIONAL DEMO CODES AND NAMES                        
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDEMC  NMOD1 WORKL,*SPTDMC*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         CLI   SXDTCTRY,CTRYCANQ   TEST CANADA                                  
         JNE   SPTDC2              NO                                           
* NO DEMO RECORDS FOR CANADA                                                    
         OI    FLAGS,NOMOREQ       ELSE SET NO MORE DATA                        
         J     NEQXIT                                                           
*                                                                               
         USING SPTDED,R2           R2=A(EXTRACT RECORD)                         
         USING NTSQKEY,R3                                                       
*                                                                               
SPTDC2   DS    0H                                                               
         XC    SPTDELEN,SPTDELEN                                                
         MVC   SPTDELEN(2),=AL2(SPTDEDL)                                        
         MVC   SPTDETYP,SPDEQ                                                   
*                                                                               
         MVC   SPTDEAGY,SXDTAGY                                                 
         ICM   R0,3,NTSQKSEQ                                                    
         X     R0,=X'0000FFFF'     COMPLEMENT                                   
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTDECOD(5),DUB                                                  
         MVI   SPTDENAM,C'X'                                                    
         MVC   SPTDENAM+1(6),NTSQKALPH                                          
*                                                                               
SPTDCX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
*                                                                               
         DROP  R2                                                               
*                                                                               
***********************************************************************         
* SPTADB - EXTRACT ADBUYER INFORMATION                                          
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)   (AGENCY 2 RECORD)                           *         
* AL4 0=PRINT-TO-EXTRACT                                            *           
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTADB   NMOD1 WORKL,*SPTADB*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTABD,R2           R2=A(EXTRACT RECORD)                         
         USING BYRREC,R3           R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTABLEN,SPTABLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTABLEN(2),=AL2(SPTABDL)                                        
         MVC   SPTABTYP,SPABQ     ADBUYER RECORD EQU                            
*                                                                               
         MVC   SPTABAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,BYRKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTABMED,BYTE                                                    
*                                                                               
         LR    R6,R3                                                            
         MVI   ELCODE,BYRDCDQ      BUYER DESCRIPTION ELEMENT                    
         BRAS  RE,GETEL                                                         
         BNE   SPTADB10                                                         
*                                                                               
         MVC   SPTABCOD,BYRKBYR    BUYER CODE                                   
*                                                                               
         USING BYRDSCD,R6                                                       
         MVC   SPTABNAM,BYRFNAME   BUYER FULL NAME                              
         DROP  R6                                                               
*                                                                               
SPTADB10 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* FINDAPRD - FIND ALPHA PRODUCT, GIVEN BINARY CODE                              
*                                                                               
* ON ENTRANCE: R1(HOB) - BINARY PRD CODE                                        
*              R1 = ADDR OF WHERE TO PUT THE ALPHA PRODUCT                      
*              BREAKTAB = SVCLIST                                               
***********************************************************************         
FINDAPRD NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         ICM   RF,15,ABREAKT       A(BREAK TABLE) = SVCLIST                     
         LHI   R0,255                                                           
*                                                                               
FBPRD10  CLI   0(RF),0                                                          
         BE    FBPRDX                                                           
*                                                                               
         CLM   R1,8,3(RF)                                                       
         BNE   *+14                                                             
         MVC   0(3,R1),0(RF)                                                    
         B     FBPRDX                                                           
*                                                                               
         LA    RF,4(RF)                                                         
         BCT   R0,FBPRD10                                                       
*                                                                               
FBPRDX   J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* NOTE:                                                                         
* REGISTERS ARE INITIALIZED BEFORE NTR1, SO THEY'RE NOT OVERWRITTEN             
* BY XIT1                                                                       
***********************************************************************         
COMSETUP DS    0H                                                               
         LM    R2,R3,0(R1)         A(EXTRACT AREA), A(RECORD)                   
         L     R4,12(R1)           A(SXDTABD)                                   
         L     R8,16(R1)           MEDPARAM                                     
         LARL  RA,LITERAL                                                       
         ST    R7,ADXBLOCK                                                      
*                                                                               
COMSETP  NTR1  BASE=*,LABEL=*                                                   
         ST    R1,APARM                                                         
*                                                                               
         MVC   DATADISP,=AL2(24)   FOR SPOTDIR/SPOTFIL (DEFAULT)                
*                                                                               
         TM    FLAGS2,XSPFILEQ                                                  
         BZ    *+10                                                             
         MVC   DATADISP,=AL2(42)   FOR XSPDIR/XSPFIL                            
*                                                                               
         TM    FLAGS,TRFFILEQ                                                   
         BZ    *+10                                                             
         MVC   DATADISP,=AL2(24)   FOR TRFDIR/TRFFIL                            
*                                                                               
         TM    FLAGS2,CTFILEQ                                                   
         BZ    *+10                                                             
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
*                                                                               
         TM    FLAGS,STAFILEQ                                                   
         BZ    *+10                                                             
         MVC   DATADISP,=AL2(00)   NO ELEMENTS ON STAFILE                       
*                                                                               
COMS10   DS    0H                                                               
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* R2 EXPECTED TO ADDRESS EXTRACT AREA                                           
* 5-CHAR RECORD TYPE EXPECTED TO BE FILLED IN                                   
***********************************************************************         
                                                                                
DELIMS   NTR1  BASE=*,LABEL=*                                                   
         USING DXHDRD,R2           R2=A(EXTRACT RECORD)                         
*                                                                               
         L     R1,APIDTAB          PIDTAB FROM SPXCNVX                          
*                                                                               
DELIMS10 CLI   0(R1),X'FF'         END OF PIDTAB?                               
         BNE   *+6                                                              
         DC    H'0'                TYPE NOT FOUND IN PIDTAB                     
*                                                                               
         ICM   RF,15,0(R1)         A(REC TYPE)                                  
         CLC   DXHDRTYP,0(RF)      SAME TYPE AS OUR RECORD IN 0(R2)?            
         BE    *+12                                                             
         LA    R1,8(R1)            PIDTAB ENTRIES ARE 8 BYTES LONG              
         B     DELIMS10                                                         
*                                                                               
         LHI   R4,9                FIELD TABLE LINE LENGTH                      
         ICM   R1,15,4(R1)         A(FIELD TABLE FOR GIVEN RECORD TYPE)         
* POINT R5 TO THE LAST ENTRY IN THE TABLE                                       
         LR    R5,R1                                                            
         AH    R5,0(R1) TABLE SIZE (TABLE DBL-WORD ALIGNED, AH IS OK)           
         SR    R5,R4               A(LAST ENTRY IN THE TABLE) FOR BXLE          
*                                                                               
         LA    R1,2(R1)            ADVANCE PAST TABLE SIZE                      
*                                                                               
         LA    R3,DXHDRCTI+L'DXHDRCTI  A(1ST DELIMITER) IN XTRACT BUFFR         
*                                                                               
DELIMS20 MVI   0(R3),MXTRTQ        INSERT DELIMITER                             
         XR    RF,RF                                                            
         ICM   RF,3,0(R1)          LENGTH OF FIELD                              
*                                                                               
* TRT THE FIELD HERE, WHILE WE'RE STILL POINTING AT IT                          
*                                                                               
         LR    RE,RF               FIELD LENGTH                                 
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         TR    1(0,R3),MXTRT                                                    
*                                                                               
         LA    R3,1(RF,R3)         ADVANCE PAST THE DELIMITER AND FIELD         
         BXLE  R1,R4,DELIMS20                                                   
*                                                                               
         MVI   0(R3),MXTRTQ        TWO SEMICOLONS AT EOR                        
         MVI   1(R3),MXTRTQ                                                     
*                                                                               
         J     EQXIT                                                            
         DROP  R2                                                               
*                                                                               
*                                                                               
*                                                                               
* GET CANADIAN NETWORK FROM PACKED STA                                          
* R1 EXPECTED TO ADDRESS PACKED STATON                                          
* ON EXIT WORK(4) HAS THE NETWORK CALL LETTERS                                  
GETCNET  NTR1  BASE=*,LABEL=*                                                   
         MVC   WORK(4),=C'    '    INIT WORK(4) TO SPACES                       
         LLC   R0,2(R1)            CANADIAN NETWORK BYTE                        
*                                                                               
         LTR   R0,R0                                                            
         JM    NEQXIT              ERROR IF NEGATIVE                            
         CHI   R0,MAXNETS                                                       
         JH    NEQXIT              ERROR IF HIGHER THAN MAXNETS                 
*                                                                               
         MHI   R0,NETTABL                                                       
         L     RF,ANETTABM                                                      
         AR    RF,R0                                                            
         CLC   0(4,RF),SPACES      ANYTHING AT THAT POSITION?                   
         JNH   NEQXIT                                                           
         MVC   WORK(4),0(RF)                                                    
         J     EQXIT                                                            
*                                                                               
*                                                                               
* CHECK FIRST FOUR EQU FACTORS IN EQUSECT                                       
* IF THEY'RE ALL 1.000 - THE CLIENT IS NOT DOING EQUIVALENCY,                   
* SO EXIT WITH EQUAL CONDITION                                                  
* UNEQUAL IF THRE IS A NON-ONE (MEANS THERE IS EQUIVALENCING)                   
* R6 EXPECTED TO ADDRESS EQUEL (X'09' ELEMENT)                                  
CKFIRST4 NTR1  BASE=*,LABEL=*                                                   
         USING EQUEL,R6                                                         
         LA    R0,4                                                             
         LA    R4,EQUSECT1                                                      
*                                                                               
         CLC   0(2,R4),=X'03E8'    1.000                                        
         JNE   NEQXIT                                                           
         LA    R4,4(R4)                                                         
         BCT   R0,*-14                                                          
         J     EQXIT                                                            
         DROP  R6                                                               
*                                                                               
*                                                                               
*                                                                               
CHKKILL  NTR1  BASE=*,LABEL=*                                                   
         TM    FLAGS,KILLQ                                                      
         JZ    NEQXIT                                                           
*                                                                               
         USING DXHDRD,R2                                                        
         MVI   DXHDRACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ-NORDSEQ                                        
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         DROP  R2                                                               
*                                                                               
         J     EQXIT                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* FINDS ELEMENT (ELCODE), SPECIFIED BY ELCOUNT                                  
***********************************************************************         
FINDELEM NTR1  BASE=*,LABEL=*                                                   
         SR    R9,R9               LOOP COUNTER                                 
         LR    R6,R3               A(RECORD)                                    
*                                                                               
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
*                                                                               
FINDEL10 BRAS  RE,NEXTEL                                                        
         BNE   FINDEL20            ELEMENT NOT FOUND                            
*                                                                               
         AHI   R9,1                                                             
         CLM   R9,1,ELCOUNT                                                     
         BNH   FINDEL10                                                         
*                                                                               
         STC   R9,ELCOUNT                                                       
         B     FINDELQX                                                         
*                                                                               
FINDEL20 MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
FINDELQX CR    RB,RB                                                            
         XIT1  REGS=(R6)                                                        
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* DISPLAYS BILLING FORMULA                                                      
* P1 EXPECTED TO POINT TO BILLBAS/BILLCOM FIELDS                                
* P2 EXPECTED TO POINT TO OUTPUT AREA                                           
***********************************************************************         
DISBFM   NTR1  BASE=*,LABEL=*                                                   
         LARL  RA,LITERAL                                                       
*                                                                               
         L     R3,0(R1)                                                         
         L     R4,4(R1)                                                         
*                                                                               
         MVC   0(L'SPTPDBFM,R4),SPACES                                          
*                                                                               
         MVC   0(2,R4),=C'CN'        X'10' AND X'40' BIT ON ...                 
         LHI   RF,2                                                             
         TM    0(R3),X'50'           SET BILL BASIS FIELD TO 'CNET'             
         BO    DISBF10                                                          
*                                                                               
         MVC   0(2,R4),=C'N '        X'10' BIT ON ...                           
         LHI   RF,1                                                             
         TM    0(R3),X'10'           SET BILL BASIS FIELD TO 'NET'              
         BO    DISBF10                                                          
*                                                                               
         MVC   0(2,R4),=C'CG'        X'40' BIT ON ...                           
         LHI   RF,2                                                             
         TM    0(R3),X'40'           SET BILL BASIS FIELD TO 'CROSS'            
         BO    DISBF10                                                          
*                                    OTHERWISE...                               
         MVC   0(2,R4),=C'G '        SET BILL BASIS FIELD TO 'GROSS'            
         LHI   RF,1                                                             
*                                                                               
DISBF10  DS    0H                                                               
         AR    R4,RF                                                            
         AHI   R4,1                SKIP A SPACE                                 
*                                                                               
         ICM   R5,15,1(R3)           COM. PERCENTAGE                            
         LTR   R5,R5                                                            
         BZ    DISBF90                                                          
*                                                                               
         MVI   0(R4),C'+'                                                       
         LTR   R5,R5                                                            
         BNM   *+8                                                              
         MVI   0(R4),C'-'                                                       
         AHI   R4,2                SKIP A SPACE                                 
*                                                                               
         LPR   R5,R5                                                            
*                                                                               
         CHI   R5,9999                                                          
         BH    *+12                                                             
         MVI   0(R4),C'0'                                                       
         AHI   R4,1                                                             
*                                                                               
         EDIT  (R5),(8,0(R4)),4,ALIGN=LEFT,WRK=DISBFWORK                        
         AHI   R4,8                                                             
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         AHI   R4,1                                                             
*                                                                               
         MVC   0(1,R4),=C'%'                                                    
         AHI   R4,1                SKIP A SPACE                                 
*                                                                               
         MVC   0(1,R4),=C'G'         X'01' BIT ON ...                           
         TM    0(R3),X'01'           SET COMM.BASIS FIELD TO 'GROSS'            
         BZ    *+10                                                             
         MVC   0(1,R4),=CL5'N'       OTHERWISE ...                              
*                                                                               
DISBF90  DS    0H                                                               
         J     EQXIT                                                            
DISBFWORK DC   17X'00'                                                          
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTEAD - EXTRACT ESTIMATE AUTHORIZED DOLLARS                                  
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTEAD   NMOD1 WORKL,*SPTEAD*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
         NI    FLAGS2,X'FF'-NOWRITEQ                                            
*                                                                               
         USING SPTELD,R2           R2=A(EXTRACT RECORD)                         
         USING ESTHDR,R3           R3=A(SPOT PRODUCT RECORD)                    
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTELLEN,SPTELLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTELLEN(2),=AL2(SPTELDL)                                        
         MVC   SPTELTYP,SPELQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
         MVC   SPTELAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,EKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTELMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,EKEYCLT),SPTELCNT                                
*                                                                               
         MVC   SPTELPRD,EKEYPRD                                                 
*                                                                               
         EDIT  EKEYEST,SPTELEST,FILL=0                                          
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTEAD10                                                         
*                                                                               
         MVI   SPTELACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTEAD60                                                         
*                                                                               
SPTEAD10 DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTELACT,C'A'                                                    
*                                                                               
         LLC   RF,ELCOUNT          CALCULATE MOS FROM ELCOUNT                   
         AHI   RF,1                                                             
         EDIT  (RF),(2,DUB+2),FILL=0 EDIT IT OUT AS C'MM'                       
         MVC   DUB+4(2),=C'01'     DUB = ??MM01                                 
*                                                                               
* GET ESTIMATE'S START, END MOS                                                 
* END OF MOS WILL NEVER SPILL INTO NEXT MONTH                                   
* SO, THE MONTH OF THE BROADCAST PERIOD'S LAST DAY IS OUR MOS                   
         GOTO1 =V(GETBROAD),DMCB,(1,ESTART),WORK,V(GETDAY),V(ADDAY)             
         GOTO1 =V(GETBROAD),DMCB,(1,EEND),WORK+12,V(GETDAY),V(ADDAY)            
*                                                                               
         MVC   DUB(2),WORK+6       INIT YEAR TO ESTART'S YEAR                   
*                                                                               
         CLC   WORK+6(2),WORK+18   ESTART, EEND YEAR SAME?                      
         BE    SPTEAD40            YES JUST USE ESTART YEAR                     
*                                                                               
* YEAR-CROSSING ESTIMATE HERE                                                   
         CLC   DUB+2(2),WORK+8     CURRENT MOS VS ESTART MOS                    
         BNL   SPTEAD40                                                         
         MVC   DUB(2),WORK+18                                                   
*                                                                               
SPTEAD40 DS    0H                                                               
         GOTO1 DATCON,DMCB,(0,DUB),(6,SPTELMY)                                  
*                                                                               
* INDEX INTO AUTHORIZED DOLLARS TABLE                                           
         LLC   RF,ELCOUNT                                                       
         MHI   RF,L'EAUTH                                                       
         LA    RF,EAUTH(RF)                                                     
         OC    0(6,RF),0(RF)                                                    
         BZ    *+14                                                             
         CP    0(6,RF),=P'0'                                                    
         BNE   *+8                                                              
         OI    FLAGS2,NOWRITEQ                                                  
*                                                                               
         EDIT  (P6,0(RF)),SPTELAUD,2                                            
*                                                                               
         LLC   RF,ELCOUNT                                                       
         AHI   RF,1                                                             
         STC   RF,ELCOUNT                                                       
*                                                                               
         CHI   RF,12                                                            
         JNH   SPTEAD60                                                         
*                                                                               
* WENT PAST 12 MONTHS = EOR                                                     
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
SPTEAD60 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
         DROP  R2,R3                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTBIL                                                              *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         SPACE 1                                                                
SPTBIL   NMOD1 WORKL,*SPTBIL*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
* DEFAULT = NO WRITE                                                            
* RE-SET THE FLAG ONLY IF APPROPRIATE STATION BUCKETS ARE FOUND                 
         OI    FLAGS2,NOWRITEQ                                                  
*                                                                               
* DEFAULT = SKIP READ SEQUENTIAL                                                
* PROCESS ALL POSSIBLE STABUCK RECORDS FOR THIS BILL HEADER                     
* NORDSEQ FLAG WILL BE TURNED OFF, IF NO MORE STABUCKS ARE FOUND                
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         USING AXG$TRN,R2          R2=A(EXTRACT RECORD)                         
         USING BILLRECD,R3          R3=A(SPOT BILL HEADER RECORD)               
*                                                                               
         XC    AXG$NET,AXG$NET                                                  
         XC    AXG$GRS,AXG$GRS                                                  
         XC    AXG$TAXA,AXG$TAXA                                                
         XC    AXG$NSPT,AXG$NSPT                                                
*                                                                               
         XC    AXG$LEN,AXG$LEN     RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   AXG$LEN(2),=AL2(AXG$TRNL)                                        
         MVC   AXG$TYPE,SPBHQ                                                   
*                                                                               
         MVC   AXG$RTYP(3),=C'RCV'                                              
         MVC   AXG$TTYP(3),=C'009'                                              
         MVC   AXG$SYS,=C'SP'                                                   
*                                                                               
         MVC   AXG$AGY,SXDTAGY                                                  
*                                                                               
         MVC   BYTE,BKEYAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   AXG$MED,BYTE                                                     
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,BKEYCLT),AXG$CLT                                 
*                                                                               
         MVC   AXG$PRD(L'BKEYPRD),AXG$CLT                                       
         MVC   AXG$PRD+3(L'BKEYPRD),BKEYPRD                                     
*                                                                               
         EDIT  BKEYEST,AXG$EST,ALIGN=LEFT                                       
*                                                                               
         XC    WORK(3),WORK                                                     
         MVC   WORK(2),BKEYYSRV                                                 
         GOTO1 DATCON,DMCB,(3,WORK),(X'20',WORK+3)                              
         MVC   AXG$MOA,WORK+3                                                   
         MVC   AXG$MOS,WORK+3                                                   
*                                                                               
         GOTO1 DATCON,DMCB,(0,BDATE),(20,WORK)                                  
         GOTO1 =A(TRNDAT),DMCB,WORK,AXG$TRDT                                    
         GOTO1 DATCON,DMCB,(0,BDATE),(20,WORK)                                  
         GOTO1 =A(TRNDAT),DMCB,WORK,AXG$ACDT                                    
         GOTO1 DATCON,DMCB,(0,BDATE),(2,SVBDTE)                                 
*                                                                               
         GOTO1 DATCON,DMCB,(3,BDUEDATE),(20,WORK)                               
         GOTO1 =A(TRNDAT),DMCB,WORK,AXG$DUDT                                    
*                                                                               
* READ CLIENT, PRODUCT, ESTIMATE RECORDS TO GET NAMES                           
* READ PROFILES                                                                 
*                                                                               
         OC    SVCLTNAM,SVCLTNAM   ALREADY READ?                                
         BNZ   SPTBIL08                                                         
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'S0B2'                                                 
         MVC   WORK+4(2),AXG$AGY                                                
         MVC   WORK+6(1),AXG$MED                                                
         MVC   WORK+7(3),AXG$CLT                                                
         L     RF,COMFACS                                                       
         MVC   DMCB+8(4),0(RF)        A(DATAMGR)                                
         GOTO1 =V(GETPROF),DMCB,WORK,SVB2PROF                                   
         MVC   WORK(4),=C'S0B1'                                                 
         GOTO1 =V(GETPROF),DMCB,WORK,SVB1PROF                                   
         MVC   WORK(4),=C'SB1X'                                                 
         NI    WORK,X'FF'-X'40'    LOWERCASE 'S'                                
         GOTO1 =V(GETPROF),DMCB,WORK,SVB1XPRF                                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING ESTHDR,R6                                                        
*                                                                               
         MVC   KEY(L'EKEY-5),BILLREC                                            
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(L'EKEY),KEY                                              
         JNE   *+2                                                              
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,STAIO                                                         
         MVC   SVPO,EUSER2                                                      
         MVC   SVESTNAM(L'EDESC),EDESC                                          
         MVC   SVEUSER1,EUSER1                                                  
         MVC   SVEUSER2,EUSER2                                                  
*                                                                               
         LA    R6,KEY                                                           
         XC    EKEYEST,EKEYEST                                                  
         DROP  R6                                                               
*                                                                               
         USING PRDHDR,R6                                                        
         MVC   KEYSAVE,KEY                                                      
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(L'PKEY),KEY                                              
         JNE   *+2                                                              
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,STAIO                                                         
         MVC   SVPRDNAM(L'PNAME),PNAME                                          
         MVC   SVBPRD,PCODE+1                                                   
         MVC   SVPUSER1,PUSER1                                                  
         MVC   SVPUSER2,PUSER2                                                  
*                                                                               
         LA    R6,KEY                                                           
         XC    PKEYPRD,PKEYPRD                                                  
         DROP  R6                                                               
*                                                                               
         USING CLTHDR,R6                                                        
         MVC   KEYSAVE,KEY                                                      
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(L'PKEY),KEY                                              
         JNE   *+2                                                              
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,STAIO                                                         
         MVC   SVCLTNAM(L'CNAME),CNAME                                          
         DROP  R6                                                               
*                                                                               
SPTBIL08 DS    0H                                                               
         MVC   AXG$CLNM(L'SVCLTNAM),SVCLTNAM                                    
         MVC   AXG$PRNM(L'SVPRDNAM),SVPRDNAM                                    
         MVC   AXG$ESNM(L'SVESTNAM),SVESTNAM                                    
         MVC   AXG$PONM(L'SVPO),SVPO                                            
         MVC   AXG$PUD1,SVPUSER1                                                
         MVC   AXG$PUD2,SVPUSER2                                                
         MVC   AXG$EUD1,SVEUSER1                                                
         MVC   AXG$EUD2,SVEUSER2                                                
*                                                                               
         MVC   SVBSTAT2,BILSTAT2   SAVE 2ND STATUS BYTE                         
*                                                                               
         GOTO1 =V(SPFMTINO),DMCB,(C'B',BDATE),(X'06',BINVNO),          X        
               (AXG$MED,SVB1PROF),SVB1XPRF,(R3)                                 
         L     RF,DMCB                                                          
         MVC   AXG$INV(10),0(RF)                                                
*                                                                               
SPTBIL09 DS    0H                                                               
         OC    SVSTAB,SVSTAB       ALREADY READ A STABUCK RECORD?               
         BZ    SPTBIL10            NO - BUILD FROM SPGENBILL KEY                
*                                                                               
* YES, STABUCK RECORD READ PREVIOUSLY, READ THE NEXT ONE                        
         LA    R6,KEY                                                           
         USING STABUCKD,R6                                                      
*                                                                               
         MVC   STABUCK(13),SVSTAB                                               
         LLC   RF,STABKCUR         INC BY 1 TO GET NEXT STABUCK KEY             
         AHI   RF,1                                                             
         STC   RF,STABKCUR                                                      
         B     SPTBIL20                                                         
         DROP  R6                                                               
*                                                                               
SPTBIL10 DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING STABUCKD,R6                                                      
*                                                                               
         MVC   STABKCOD,=X'0E01'                                                
         MVC   STABKAM,BKEYAM                                                   
         MVC   STABKCLT,BKEYCLT                                                 
         MVC   STABKPRD,SVBPRD                                                  
         MVC   STABKEST,BKEYEST                                                 
*                                                                               
SPTBIL20 DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(STABKMKT-STABUCKD),KEY AM/CLT/PRD/EST                    
         BE    SPTBIL23                                                         
*                                                                               
* NO MORE STABUCK RECORDS FOR THIS BILL HEADER                                  
* RESET ALL FLAGS AND GET OUT                                                   
*                                                                               
         XCEFL SVBILL,L'SVBILL                                                  
         NI    FLAGS,X'FF'-MULTIQ-NORDSEQ                                       
*                                                                               
* RESTORE READ SEQUENCE IF RUNNING IN LOAD MODE                                 
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         CLI   DXMODE,DXLOADQ                                                   
         JNE   EQXIT                                                            
         DROP  R7                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(13),0(R3)       BILL RECORD KEY                              
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         J     EQXIT                                                            
*                                                                               
SPTBIL23 DS    0H                                                               
         MVC   SVSTAB,KEY          SAVE LAST STABUCK KEY                        
*                                                                               
* MAKE SURE BOTH BILL AND STABUCK USE REGULAR COST, OR BOTH USE COST 2          
*                                                                               
         XC    HALF,HALF                                                        
         TM    SVBSTAT2,BSTC2Q     BILL HEADER IS COST 2?                       
         BZ    *+8                                                              
         MVI   HALF,X'01'                                                       
         MVC   HALF+1(1),STABKCUR  CURRENT STABUCK FLAG                         
         NI    HALF+1,X'01'        ISOLATE THE X'01' COST2 FLAG                 
         CLC   HALF(1),HALF+1                                                   
         JNE   SPTBIL09            READ NEXT STABUCK RECORD                     
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,STAIO                                                         
         EDIT  STABKMKT,(4,AXG$MKTC),FILL=0,ALIGN=LEFT                          
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVC   STAPMED,AXG$MED                                                  
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPSTA,STABKSTA                                                 
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK                                                          
         CLI   STAPERR,0                                                        
         BNE   *+10                                                             
         MVC   AXG$VNDC(L'STAPQSTA),STAPQSTA                                    
         DROP  R1                                                               
*                                                                               
         CLI   AXG$MED,C'T'                                                     
         BNE   *+14                                                             
         MVC   AXG$SBMT(10),=CL10'TELEVISION'                                   
         B     SPTBIL25                                                         
         CLI   AXG$MED,C'R'                                                     
         BNE   *+14                                                             
         MVC   AXG$SBMT(10),=CL10'RADIO'                                        
         B     SPTBIL25                                                         
         CLI   AXG$MED,C'C'                                                     
         BNE   *+14                                                             
         MVC   AXG$SBMT(10),=CL10'COMBINED'                                     
         B     SPTBIL25                                                         
         CLI   AXG$MED,C'N'                                                     
         BNE   *+14                                                             
         MVC   AXG$SBMT(10),=CL10'NETWORK'                                      
         B     SPTBIL25                                                         
         CLI   AXG$MED,C'X'                                                     
         BNE   *+14                                                             
         MVC   AXG$SBMT(10),=CL10'NET RADIO'                                    
         B     SPTBIL25                                                         
*                                                                               
SPTBIL25 DS    0H                                                               
         MVC   AXG$SBMN(9),=CL9'BROADCAST'                                      
*                                                                               
         CLI   AXG$VNDC,C'0'                                                    
         BL    *+14                                                             
         MVC   AXG$SBMN(9),=CL9'CABLE'                                          
         B     SPTBIL27                                                         
*                                                                               
         CLI   AXG$VNDC+4,C'D'                                                  
         BNE   *+14                                                             
         MVC   AXG$SBMN(9),=CL9'STREAMING'                                      
         B     SPTBIL27                                                         
*                                                                               
         CLI   AXG$VNDC+4,C'S'                                                  
         BNE   *+14                                                             
         MVC   AXG$SBMN(9),=CL9'STREAMING'                                      
         B     SPTBIL27                                                         
*                                                                               
SPTBIL27 DS    0H                                                               
         LA    R6,STABELEM                                                      
         DROP  R6                                                               
         USING STABELEM,R6                                                      
*                                                                               
SPTBIL30 DS    0H                                                               
         CLI   0(R6),0                                                          
         BE    SPTBIL60                                                         
*                                                                               
         CLI   0(R6),X'0E'         NORMAL                                       
         BE    SPTBIL40                                                         
         CLI   0(R6),X'1E'         UNBILLED                                     
         BE    SPTBIL40                                                         
*                                                                               
SPTBIL31 LLC   R0,1(R6)            NEXT ELEMENT                                 
         LTR   R0,R0                                                            
         BZ    SPTBIL60            EOR                                          
         AR    R6,R0                                                            
         B     SPTBIL30                                                         
*                                                                               
SPTBIL40 DS    0H                                                               
         CLC   STABPER,BKEYYSRV       SAME MOS?                                 
         BNE   SPTBIL31                                                         
         CLC   STABBDT,SVBDTE         SAME BILL DATE?                           
         BNE   SPTBIL31                                                         
         MVC   HALF,STABINV                                                     
         NI    HALF,X'3F'          DROP X'80'+X'40'                             
         CLC   HALF,BKEYINV        RIGHT INVOICE?                               
         BNE   SPTBIL31                                                         
*                                                                               
* FOUND A BILLING ELEMENT THAT TIES TO THE BILL HEADER RECORD                   
*                                                                               
         NI    FLAGS2,X'FF'-NOWRITEQ                                            
         ICM   R0,15,SVITMNUM                                                   
         AHI   R0,1                                                             
         STCM  R0,15,SVITMNUM                                                   
         EDIT  (R0),(5,AXG$ITMN),ALIGN=LEFT                                     
*                                                                               
*        L     R0,STABGRS                                                       
*        ICM   RF,15,AXG$GRS                                                    
*        AR    RF,R0                                                            
*        STCM  RF,15,AXG$GRS                                                    
*                                                                               
         L     R0,STABNET                                                       
         ICM   RF,15,AXG$NET                                                    
         AR    RF,R0                                                            
         STCM  RF,15,AXG$NET                                                    
*                                                                               
         CLI   SVB2PROF+12,C'Y'                                                 
         BNE   SPTBIL50                                                         
*                                                                               
         CLI   1(R6),19            NO TAX FIELD IF ELEM LEN <21                 
         BNH   SPTBIL50                                                         
*                                                                               
* TAX IS 3(!) BYTES SIGNED                                                      
         XR    R0,R0                                                            
         ICM   R0,7,STABTAX                                                     
         ICM   RF,15,AXG$TAXA                                                   
         TM    STABTAX,X'80'                                                    
         BZ    *+8                                                              
         ICM   R0,8,=X'FF'         PROPAGATE NEGATIVE BIT                       
         AR    RF,R0                                                            
         STCM  RF,15,AXG$TAXA                                                   
*                                                                               
SPTBIL50 DS    0H                                                               
         XR    R0,R0                                                            
         ICM   R0,3,STABSPTS                                                    
         ICM   RF,15,AXG$NSPT                                                   
         AR    RF,R0                                                            
         STCM  RF,15,AXG$NSPT                                                   
*                                                                               
         B     SPTBIL31                                                         
*                                                                               
SPTBIL60 DS    0H                  STATION BUCKET EOR                           
* PER CONVERSATION WITH SONNY:                                                  
* FLIP SIGN(!) FOR ALL THE DOLLAR FIGURES                                       
* PER CONVERSATION WITH SONNY:                                                  
* INTEAD OF GROSS, SEND NET+COMMISSION                                          
*        ICM   R0,15,AXG$GRS                                                    
*        LCR   R0,R0                                                            
*        EDIT  (R0),AXG$GRS,2,ALIGN=LEFT,FLOAT=-                                
*                                                                               
* DO *NOT* FLIP THE SIGN FOR NET HEADER DOLLARS                                 
         EDIT  (B4,AXG$NET),AXG$NETH,2,ALIGN=LEFT                               
*                                                                               
         ICM   R0,15,AXG$NET                                                    
         LCR   R0,R0                                                            
         EDIT  (R0),AXG$NET,2,ALIGN=LEFT,FLOAT=-                                
*                                                                               
* DO NOT FLIP SIGN FOR HEADER TAX DOLLARS                                       
         EDIT  (B4,AXG$TAXA),AXG$TAXH,2,ALIGN=LEFT                              
*                                                                               
         ICM   R0,15,AXG$TAXA                                                   
         LCR   R0,R0                                                            
         EDIT  (R0),AXG$TAXA,2,ALIGN=LEFT,FLOAT=-                               
*                                                                               
         ICM   R0,15,AXG$NSPT                                                   
         EDIT  (R0),AXG$NSPT,ALIGN=LEFT                                         
*                                                                               
         CLC   SVITMNUM,=F'1'                                                   
         BNE   SPTBIL70                                                         
*                                                                               
* DO NOT FLIP THE SIGN FOR COMMISSION FOR HEADER DOLLARS                        
         ZAP   P16,BACTP                                                        
         SP    P16,BNETP                                                        
         EDIT  P16,AXG$COMH,2,ALIGN=LEFT,FLOAT=-                                
*                                                                               
         ZAP   WORK(6),BACTP                                                    
         SP    WORK(6),BNETP                                                    
         ZAP   P16,=P'0'                                                        
         SP    P16,WORK(6)                                                      
         EDIT  P16,AXG$BINC,2,ALIGN=LEFT,FLOAT=-                                
*                                                                               
* PER CONVERSATION WITH SONNY:                                                  
* INTEAD OF GROSS, SEND ACTUAL                                                  
* DO NOT FLIP THE SIGN                                                          
*        EDIT  BACTP,AXG$GRS,2,ALIGN=LEFT,FLOAT=-                               
         EDIT  BGRSP,AXG$GRS,2,ALIGN=LEFT,FLOAT=-                               
*                                                                               
SPTBIL70 DS    0H                  EOR                                          
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING MKTREC,R6                                                        
*                                                                               
         MVI   MKTKTYPE,MKTKTYPQ                                                
         MVC   MKTKMED,AXG$MED                                                  
         MVC   MKTKMKT,AXG$MKTC                                                 
         MVC   MKTKAGY,SXDTAGY                                                  
         MVC   MKTKFILL,=7C'0'                                                  
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'STATION',KEY,STAIO                   
         CLC   KEY(L'MKTKEY),STAIO                                              
         JNE   *+2                                                              
         LA    R6,STAIO                                                         
         MVC   AXG$MKTN,MKTNAME                                                 
*                                                                               
         DROP  R6                                                               
*                                                                               
         XC    WORK,WORK                                                        
         LA    R6,WORK                                                          
*                                                                               
         GOTO1 ,WORK,A(L'AXG$AGY),AXG$AGY,A(1),=C'-',                  X        
               A(L'AXG$CLT),AXG$CLT,A(1),=C'-',                        X        
               A(L'AXG$PRD-3),AXG$PRD+3,A(1),=C'-',                    X        
               A(L'AXG$INV),AXG$INV,X'FFFFFFFF'                                 
*                                                                               
         GOTO1 =A(CONCAT),DMCB,WORK,AXG$VOUC                                    
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING CT5REC,R6           SYSTEM ACCESS RECORD                         
         MVI   CT5KTYP,CT5KTYPQ                                                 
         MVC   CT5KALPH,SXDTAGY                                                 
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'CTFILE',KEY,STAIO                    
         CLC   KEY(L'CT5KEY),STAIO                                              
         BNE   SPTBIL80                                                         
         DROP  R6                                                               
*                                                                               
         LA    R6,STAIO                                                         
         MVI   ELCODE,CTDSCELQ     X'02', DESCRIPTION ELEMENT                   
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTBIL80                                                         
*                                                                               
         LR    RF,R6                                                            
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING CTIREC,R6           ID RECORD                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,CTDSC-CTDSCD(RF) ID                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'CTFILE',KEY,STAIO                    
         CLC   KEY(L'CTIKEY),STAIO                                              
         BNE   SPTBIL80                                                         
         DROP  R6                                                               
*                                                                               
         LA    R6,STAIO                                                         
         MVI   ELCODE,CTDSCELQ     X'02', DESCRIPTION ELEMENT                   
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTBIL80                                                         
*                                                                               
         MVC   AXG$CPYI,CTDSC-CTDSCD(R6) ID                                     
*                                                                               
SPTBIL80 DS    0H                                                               
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
*                                                                               
         L     R6,DXARECB          RECORD BUFFER                                
         LA    R6,4(R6)            SKIP RDW                                     
*                                                                               
         CLI   RC$RRECTY-RC$RECVHDR(R6),RC$RRECTADD                             
         BNE   SPTBIL82                                                         
         MVC   AXG$AXN,=CL6'ADD'                                                
         B     SPTBIL90                                                         
*                                                                               
SPTBIL82 DS    0H                                                               
         CLI   RC$RRECTY-RC$RECVHDR(R6),RC$RRECTCHG                             
         BNE   SPTBIL90                                                         
*                                                                               
         MVC   AXG$AXN,=CL6'CHANGE'                                             
         TM    BCNTRL,X'80'        RECORD DELETED?                              
         BZ    SPTBIL84                                                         
         MVC   AXG$AXN,=CL6'DELETE'                                             
         B     SPTBIL90                                                         
*                                                                               
SPTBIL84 DS    0H                                                               
         L     R6,DXACPYB                                                       
         LA    R6,4(R6)            SKIP RDW                                     
         LA    R6,RC$RHLENQ(R6)                                                 
         TM    BCNTRL-BILLREC(R6),X'80'                                         
         BZ    SPTBIL90                                                         
         MVC   AXG$AXN,=CL6'RESTOR'                                             
                                                                                
         DROP  R7                                                               
*                                                                               
SPTBIL90 DS    0H                                                               
         GOTO1 DATCON,DMCB,(0,BDATE),(20,WORK)                                  
         GOTO1 =A(TRNDAT),DMCB,WORK,AXG$INSD                                    
*                                                                               
         MVC   AXG$CURR,=C'USD'                                                 
*                                                                               
SPTBILX  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
         DROP  R2,R3                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************SAVE********************************         
* SPTDAO - DARE ORDERS                                                *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDAO   NMOD1 WORKL,*SPTDAO*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTDOD,R2           R2=A(EXTRACT RECORD)                         
         USING DAREORDD,R3         R3=A(DARE ORDER RECORD)                      
*                                                                               
* EXTRACT MEDIA UP FRONT.  WE'LL NEED IT FOR ELEMENTS PROCESSING                
*                                                                               
         MVC   BYTE,DOKAGMD                                                     
         BRAS  RE,GETMED                                                        
         MVC   SPTDOMED,BYTE                                                    
*                                                                               
         XC    HALF,HALF                                                        
*                                                                               
* IF TRIGGERED BY MAKEGOOD NOTICE, DO NOT FILTER ON STATUS                      
         CLI   MGFLAG,C'Y'                                                      
         BE    SPTDAO07                                                         
*                                                                               
         LR    R6,R3                                                            
         MVI   ELCODE,DOSTELQ      X'12', NEW STATUS ELEMENT                    
         BRAS  RE,GETEL                                                         
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ     IF NOT FOUND - SKIP ENTIRE RECORD            
         B     SPTDAOXX                                                         
*                                                                               
         USING DOSTELD,R6          X'12', NEW STATUS ELEMENT                    
*                                                                               
* FOR PM360 DO NOT FILTER ON SPECIFIC ORDER STATUS, JUST EXTRACT                
* ORDER STATUS AS IS                                                            
         CLC   SVTYPCOD,=C'ORD'                                                 
         BNE   SPTDAO03                                                         
* TEMP - FOR NOW, JUST DISPLAY THE STATUS                                       
         MVC   SPTDOSTT,DOSTSTAT                                                
         B     SPTDAO06A                                                        
*                                                                               
SPTDAO03 DS    0H                                                               
         CLI   DOSTSTAT,QCFMD      C'C', CONFIRMED                              
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ     IF NOT - SKIP ENTIRE RECORD                  
         B     SPTDAOXX                                                         
         MVI   SPTDOSTT,C'C'       STATUS = CONFIRMED                           
*                                                                               
         CLI   DOSTLEN,DOSTLNQ3                                                 
         BL    SPTDAO05                                                         
         TM    DOSTTYPE,DCNFMCAN                                                
         BZ    SPTDAO05                                                         
         MVI   SPTDOSTT,C'K'       STATUS = CANCELLED CONFIRM                   
*                                                                               
SPTDAO05 DS    0H                                                               
         MVC   HALF(1),SPTDOSTT    SAVE STATUS                                  
         ST    R6,SVR6                                                          
*                                                                               
* CHECK COPY RECORD STATUS - MUST BE DIFFERENT                                  
*                                                                               
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         L     R6,DXACPYB                                                       
         DROP  R7                                                               
*                                                                               
         CLC   =AL2(0),0(R6)       ANYTHING IN COPY BUFFER?                     
         BZ    SPTDAO06                                                         
*                                                                               
         LA    R6,24+4(R6)         SKIP RECOVERY HEADER+REC LEN                 
         BRAS  RE,GETEL                                                         
         BNE   SPTDAO06                                                         
*                                                                               
         CLI   DOSTSTAT,QCFMD      C'C', CONFIRMED                              
         BNE   *+8                                                              
         MVI   HALF+1,C'C'         STATUS = CONFIRMED                           
*                                                                               
         CLI   DOSTLEN,DOSTLNQ3                                                 
         BL    SPTDAO05A                                                        
         TM    DOSTTYPE,DCNFMCAN                                                
         BZ    SPTDAO05A                                                        
         MVI   HALF+1,C'K'         STATUS = CANCELLED CONFIRM                   
*                                                                               
SPTDAO05A DS   0H                                                               
         CLC   HALF(1),HALF+1      DID STATUS CHANGE?                           
         BNE   SPTDAO06            YES IT DID - EXTRACT THIS RECORD             
         OI    FLAGS2,NOWRITEQ     STATUS DID NOT CHANGE - SKIP RECORD          
         B     SPTDAOXX                                                         
*                                                                               
SPTDAO06 DS    0H                                                               
         L     R6,SVR6                                                          
*                                                                               
SPTDAO06A DS   0H                                                               
         CLC   SVTYPCOD,=C'ORD'    PM360 ORDER?                                 
         BE    SPTDAO06B                                                        
*                                                                               
* FOR BLOCKCHAIN LAST CHANGE DATE, TIME ARE PLAIN TEXT                          
         GOTO1 DATCON,DMCB,(8,DOSTDATE),(10,SPTDOUDT)                           
         GOTO1 =A(SHOWTIME),DMCB,DOSTTIME,SPTDOUTM                              
         B     SPTDAO07                                                         
*                                                                               
* LAST CHANGE DATE, TIME FIELDS ARE DEFINED AS 'SMALLDATETIME'                  
* FOR PM360 ORDERS                                                              
SPTDAO06B DS   0H                                                               
         MVC   SPTDOUDT(4),=C'NULL'                                             
         MVC   SPTDOUTM(4),=C'NULL'                                             
         OC    DOSTDATE,DOSTDATE                                                
         BZ    SPTDAO07                                                         
         OC    DOSTTIME,DOSTTIME                                                
         BZ    SPTDAO07                                                         
         GOTO1 DATCON,DMCB,(8,DOSTDATE),(10,SPTDOUDT)                           
         GOTO1 =A(SHOWTIME),DMCB,DOSTTIME,SPTDOUDT+9                            
         MVC   SPTDOUTM,SPTDOUDT                                                
         DROP  R6                                                               
*                                                                               
SPTDAO07 DS    0H                                                               
         LA    R6,DORFRST          FIRST ELEMENT                                
         XC    ELCOUNT,ELCOUNT     COUNT THE X'12' ELEMENTS                     
*                                                                               
SPTDAO10 DS    0H                                                               
         CLI   0(R6),0                                                          
         BE    SPTDAO80            EOR                                          
*                                                                               
         CLI   0(R6),DOSTELQ       X'12', NEW STATUS ELEMENT                    
         BE    SPTDAO20                                                         
         CLI   0(R6),DOIDELQ       X'01', DARE ID ELEMENT                       
         BE    SPTDAO40                                                         
         CLI   0(R6),DOSPELQ       X'03', SUPPLEMENTARY ID ELEMENT              
         BE    SPTDAO50                                                         
*                                                                               
SPTDAO11 LLC   R0,1(R6)            NEXT ELEMENT                                 
         LTR   R0,R0                                                            
         BZ    SPTDAO80            EOR                                          
         AR    R6,R0                                                            
         B     SPTDAO10                                                         
*                                                                               
* INDIVIDUAL ELEMENT LOGIC                                                      
*                                                                               
SPTDAO20 DS    0H                                                               
         USING DOSTELD,R6          X'12', NEW STATUS ELEMENT                    
*                                                                               
         LLC   R0,ELCOUNT          UPDATE X'12' ELEMENT COUNT                   
         AHI   R0,1                                                             
         STC   R0,ELCOUNT                                                       
*                                                                               
* IF TRIGGERED BY MAKEGOOD NOTICE, DON'T SKIP THE FIRST ELEMENT                 
         CLI   MGFLAG,C'Y'                                                      
         BE    *+12                                                             
         CLI   ELCOUNT,1           IS IT THE FIRST X'12'?                       
         BE    SPTDAO11                                                         
*                                                                               
* HISTORICAL X'12' ELEMENTS                                                     
*                                                                               
         CLI   DOSTSTAT,DSENT      STATUS = X'81', SENT?                        
         BNE   SPTDAO22            CHECK IF DELIVERED                           
         CLC   SPTDOAGI,SPACES     ALREADY PROCESSED "SENT" ELEMENT?            
         BH    SPTDAO11            YES, SKIP ANY OLDER ONES                     
         LA    RF,SPTDOAGI                                                      
         STCM  RF,15,WORK          A(WHERE TO OUTPUT ID)                        
         B     SPTDAO26                                                         
*                                                                               
SPTDAO22 DS    0H                                                               
         CLI   DOSTSTAT,DDLVRD     STATUS = X'82', DELIVERED                    
         BNE   SPTDAO11            IGNORE ALL OTHERS                            
         CLC   SPTDOREP,SPACES     ALREADY PROCESSED "DELIV" ELEMENT?           
         BH    SPTDAO11            YES, SKIP ANY OLDER ONES                     
         LA    RF,SPTDOREP                                                      
         STCM  RF,15,WORK          A(WHERE TO OUTPUT ID)                        
*                                                                               
SPTDAO25 DS    0H                                                               
         CLI   SPTDOMED,C'R'                                                    
         BNE   SPTDAO26                                                         
*                                                                               
* FOR RADIO                                                                     
* IF RADIO EDI (REDI), THEN                                                     
* CONCATENATE REP PREFIX, WITH REP OFFICE                                       
*                                                                               
* FOR RADIO DARE READ THE ID RECORD                                             
*                                                                               
         CLI   1(R6),DOSTLNQ6                                                   
         BL    SPTDAO26                                                         
*                                                                               
         ST    R6,SVR6                                                          
*                                                                               
         MVC   WORK+4(L'DOSTDRPP),DOSTDRPP REP PREFIX                           
         LA    R1,WORK+4+L'DOSTDRPP-1 LAST CHARACTER OF REP PREFIX              
         LHI   R0,8                SAFETY                                       
         CLI   0(R1),C' '                                                       
         BH    *+10                                                             
         BCTR  R1,0                                                             
         BCT   R0,*-10                                                          
         LA    R1,1(R1)                                                         
         MVC   0(L'DOSTDRPO,R1),DOSTDRPO                                        
         B     SPTDAO29                                                         
*                                                                               
* READ ID RECORD                                                                
*                                                                               
SPTDAO26 DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R1,KEY                                                           
*                                                                               
         USING CTIREC,R1                                                        
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,DOSTIDNM                                                 
         DROP  R1                                                               
*                                                                               
         MVC   SVDTDISP,DATADISP                                                
         ST    R6,SVR6                                                          
         DROP  R6                                                               
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'CTFILE',KEY,STAIO                    
         CLC   KEY(L'CTIKEY),STAIO                                              
         BNE   SPTDAO30                                                         
*                                                                               
         LA    R6,STAIO                                                         
         MVI   ELCODE,CTDSCELQ     X'02', DESCRIPTION ELEMENT                   
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTDAO28                                                         
*                                                                               
         MVC   WORK+4(L'SPTDOAGI),SPACES                                        
         USING CTDSCD,R6           X'02',DESCRIPTION ELEMENT                    
         LLC   RF,CTDSCLEN                                                      
         BCTR  RF,0                                                             
         AHI   RF,-2                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   WORK+4(0),CTDSC                                                  
         DROP  R6                                                               
*                                                                               
SPTDAO28 DS    0H                                                               
         LA    R6,STAIO                                                         
         MVI   ELCODE,CTUSAELQ     X'33', AGENCY EXTRA INFO                     
         MVC   DATADISP,=AL2(28)   FOR CTFILE                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTDAO30                                                         
*                                                                               
         USING CTUSAD,R6           X'33', AGENCY EXTRA INFO                     
         MVC   SPTDORTE,CTUSADRC                                                
         DROP  R6                                                               
*                                                                               
* WORK+4 HAS THE USER ID HERE                                                   
*                                                                               
SPTDAO29 DS    0H                                                               
         ICM   RF,15,WORK          A(WHERE TO OUTPUT ID)                        
         MVC   0(L'SPTDOAGI,RF),WORK+4                                          
*                                                                               
* DONE WITH ID RECORD                                                           
* RESTORE DARE ORDER'S R6, AND PROCESS OTHER ELEMENTS                           
*                                                                               
SPTDAO30 DS    0H                                                               
         MVC   DATADISP,SVDTDISP                                                
         L     R6,SVR6                                                          
         B     SPTDAO11            NEXT ELEMENT                                 
*                                                                               
SPTDAO40 DS    0H                                                               
         USING DOIDELD,R6          X'01', DARE ID ELEMENT                       
*                                                                               
         MVC   BREAKLS(2),DOIDCLT  SAVE PACKED CLIENT                           
*                                                                               
* READ CLIENT RECORD                                                            
*                                                                               
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING CLTHDR,R5                                                        
         MVC   CKEYAM,DOKAGMD                                                   
         MVC   CKEYCLT,DOIDCLT                                                  
         MVC   HEXKEY+BUYKCLT-BUYREC(L'BUYKCLT),DOIDCLT                         
         MVI   HEXKEY+BUYKPRD-BUYREC,X'FF'                                      
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(L'CKEY),KEY                                              
         BNE   SPTDAO80                                                         
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* RESTORE KEY                                                                   
* ONLY IF RUNNING IN LOAD MODE                                                  
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         CLI   DXMODE,DXLOADQ                                                   
         JNE   SPTDAO41                                                         
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY(L'DOKEY),0(R3)                                               
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(L'DOKEY),KEY                                             
         JNE   *+2                                                              
*                                                                               
SPTDAO41 DS    0H                                                               
         LA    R5,STAIO                                                         
*                                                                               
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(CPROF+6,CKEYCLT),SPTDOCLT                             
*                                                                               
* SAVE CLIST                                                                    
*                                                                               
         L     RE,ABREAKT                                                       
         LHI   RF,(BRKTABNQ*BRKTABLQ)                                           
         XCEFL                                                                  
         L     R0,ABREAKT                                                       
         LHI   R1,880              L'CLIST                                      
         LA    RE,(CLIST-CLTHDR)(R5) A(CLIST)                                   
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
* SAVE CLIST2                                                                   
         L     R0,ABREAKT                                                       
         AHI   R0,880              ADVANCE PAST SAVED CLIST                     
         LHI   R1,140              L'CLIST2                                     
         LA    RE,(CLIST2-CLTHDR)(R5) A(CLIST2)                                 
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         DROP  R5                  CLTHDR                                       
*                                                                               
* FOR PM360, SEND ZERO, SINCE PB1 IS DEFINED AS BINARY                          
* FOR BLOCKCHAIN, SEND AN EMPTY FIELD                                           
         CLI   DOIDPRD,X'00'                                                    
         BNE   SPTDAO42                                                         
*                                                                               
         CLC   SVTYPCOD,=C'ORD'    PM360 ORDER?                                 
         BNE   SPTDAO43                                                         
         EDIT  (B1,DOIDPRD),SPTDOPB1,ZERO=NOBLANK                               
         B     SPTDAO43                                                         
*                                                                               
SPTDAO42 DS    0H                                                               
         EDIT  (B1,DOIDPRD),SPTDOPB1                                            
         LA    R1,SPTDOPR1         WHERE TO PUT 3-CHAR PRD                      
         ICM   R1,8,DOIDPRD        BINARY PRD CODE                              
         BRAS  RE,FINDAPRD                                                      
*                                                                               
SPTDAO43 DS    0H                                                               
         CLI   DOIDPRD2,X'00'                                                   
         BNE   SPTDAO44                                                         
*                                                                               
         CLC   SVTYPCOD,=C'ORD'    PM360 ORDER?                                 
         BNE   SPTDAO45                                                         
         EDIT  (B1,DOIDPRD2),SPTDOPB2,ZERO=NOBLANK                              
         B     SPTDAO45                                                         
*                                                                               
SPTDAO44 DS    0H                                                               
         EDIT  (B1,DOIDPRD2),SPTDOPB2                                           
         LA    R1,SPTDOPR2         WHERE TO PUT 3-CHAR PRD                      
         ICM   R1,8,DOIDPRD2       BINARY PRD CODE                              
         BRAS  RE,FINDAPRD                                                      
*                                                                               
SPTDAO45 DS    0H                                                               
         MVC   BREAKLS+2(1),DOIDEST SAVE ESTIMATE                               
         EDIT  DOIDEST,SPTDOEST,FILL=0                                          
         MVC   SPTDOCON,DOIDCON                                                 
         MVC   HEXKEY+BUYKEST-BUYREC(L'BUYKEST),DOIDEST                         
         B     SPTDAO11            NEXT ELEMENT                                 
         DROP  R6                                                               
*                                                                               
SPTDAO50 SR    R0,R0               X'03', SUPPLEMENTARY ID ELEMENT              
         USING DOSPELD,R6                                                       
         MVI   SPTDOCAT,C'C'       CASH                                         
         TM    DOSPFLG1,DOSPTRDE                                                
         BZ    *+8                                                              
         MVI   SPTDOCAT,C'T'       TRADE                                        
*                                                                               
         CLI   DOSPREVN,X'00'                                                   
         BE    SPTDAO51                                                         
         EDIT  DOSPREVN,SPTDOREV                                                
*                                                                               
SPTDAO51 DS    0H                                                               
         CLC   SVTYPCOD,=C'ORD'    PM360 ORDER?                                 
         BNE   SPTDAO51A                                                        
         EDIT  DOSPSPTS,SPTDOSPT,ALIGN=LEFT,ZERO=NOBLANK                        
         B     SPTDAO51B                                                        
*                                                                               
SPTDAO51A DS   0H                                                               
         EDIT  DOSPSPTS,SPTDOSPT,ALIGN=LEFT                                     
*                                                                               
SPTDAO51B DS   0H                                                               
         MVC   SPTDODOL(4),=CL4'NULL'                                           
         TP    DOSPTOTL                                                         
         BNZ   SPTDAO52                                                         
         EDIT  DOSPTOTL,SPTDODOL,2,ZERO=NOBLANK,ALIGN=LEFT,FILL=0               
*                                                                               
SPTDAO52 DS    0H                                                               
         CLI   DOSPLEN,DOSPTLNQ                                                 
         BL    SPTDAO55            NEXT ELEMENT                                 
*                                                                               
         MVC   SPTDOMTH,DOSPTMTH                                                
         MVC   SPTDOMTD,DOSPTDAT                                                
         OC    SPTDOMTD,SPACES                                                  
*                                                                               
         CLI   DOSPLEN,DOSPLNQ2                                                 
         BL    SPTDAO55            NEXT ELEMENT                                 
*                                                                               
         OC    DOSPFSTA,DOSPFSTA                                                
         BZ    SPTDAO55            NEXT ELEMENT                                 
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVC   STAPMED,SPTDOMED                                                 
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPSTA,DOSPFSTA                                                 
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK                                                          
         CLI   STAPERR,0                                                        
         BNE   *+10                                                             
         MVC   SPTDOPRE,STAPQSTA                                                
         DROP  R1                                                               
*                                                                               
SPTDAO55 DS    0H                                                               
         B     SPTDAO11            NEXT ELEMENT                                 
         DROP  R6                                                               
*                                                                               
* EOR HERE.  PROCESS THE KEY FIELDS NOW                                         
*                                                                               
SPTDAO80 SR    R0,R0                                                            
         XC    SPTDOLEN,SPTDOLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTDOLEN(2),=AL2(SPTDODL)                                        
         MVC   SPTDOTYP,SPDOQ                                                   
         CLC   SVTYPCOD,=C'ORD'    PM360 ORDER?                                 
         BNE   *+10                                                             
         MVC   SPTDOTYP,SPP3Q                                                   
*                                                                               
         MVC   SPTDOAGY,SXDTAGY                                                 
         MVC   HEXKEY+BUYKAM-BUYREC(L'BUYKAM),DOKAGMD A/M                       
         MVC   HEXKEY+BUYKSTA-BUYREC(L'BUYKSTA),DOKSTA STATION                  
*                                                                               
         MVC   WORK(4),DOKORDER                                                 
         XC    WORK(4),=X'FFFFFFFF'                                             
         TM    WORK,X'80'         NEW STYLE ORDER NUMBER?                       
         BZ    SPTDAO82                                                         
*                                                                               
         NI    WORK,X'FF'-X'80'                                                 
         ICM   R0,15,WORK                                                       
         CVD   R0,DUB                                                           
         AP    DUB,=P'04000000'                                                 
         OI    DUB+7,X'0F'                                                      
         UNPK  SPTDOORD,DUB                                                     
         B     SPTDAO84                                                         
*                                                                               
SPTDAO82 SR    R0,R0                                                            
         ICM   R0,3,WORK                                                        
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  WORK+8(4),DUB                                                    
         ICM   R0,3,WORK+2                                                      
         CVD   R0,DUB                                                           
         OI    DUB+L'DUB-1,X'0F'                                                
         UNPK  WORK+12(4),DUB                                                   
         MVC   SPTDOORD,WORK+8                                                  
*                                                                               
SPTDAO84 DS    0H                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVC   STAPMED,SPTDOMED                                                 
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPSTA,DOKSTA                                                   
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
*                                                                               
         GOTO1 VSTAPACK,(R1)                                                    
*                                                                               
         LA    R1,WORK                                                          
         CLI   STAPERR,0                                                        
         BNE   *+10                                                             
         MVC   SPTDOSTA,STAPQSTA                                                
         DROP  R1                                                               
*                                                                               
* READ STATION RECORD TO GET MARKET FOR BUY HEX KEY                             
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
*                                                                               
         USING STAREC,R6                                                        
         MVI   STAKTYPE,STAKTYPQ                                                
         MVC   STAKMED,SPTDOMED                                                 
         MVC   STAKCALL,SPTDOSTA                                                
         CLI   SPTDOSTA+4,C' '                                                  
         BH    *+8                                                              
         MVI   STAKCALL+4,C'T'                                                  
         MVC   STAKAGY,SPTDOAGY                                                 
         MVC   STAKCLT,SPTDOCLT                                                 
         MVC   STAKFILL,=3C'0'                                                  
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'STATION',KEY,STAIO                   
         CLC   KEY(L'STAKEY),STAIO                                              
         BE    SPTDAO90                                                         
*                                                                               
* CLIENT-SPECIFIC MASTER RECORD NOT FOUND, LOOK FOR AGENCY-WIDE                 
*                                                                               
         MVC   STAKCLT,=3C'0'                                                   
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'STATION',KEY,STAIO                   
         CLC   KEY(L'STAKEY),STAIO                                              
         BE    SPTDAO90                                                         
         OI    FLAGS2,NOWRITEQ     IF NOT FOUND - SKIP ENTIRE RECORD            
         B     SPTDAOXX                                                         
*                                                                               
* AGENCY-WIDE *OR* CLIENT-SPECIFIC STATION MASTER RECORD FOUND HERE             
*                                                                               
SPTDAO90 DS    0H                                                               
         LA    R6,STAIO                                                         
         MVC   SPTDOMKT,SMKT                                                    
         PACK  DUB,SMKT                                                         
         CVB   RF,DUB                                                           
         STCM  RF,3,HEXKEY+BUYKMKTN-BUYREC                                      
         DROP  R6                                                               
*                                                                               
* READ THE MARKET RECORD TO GET MARKET NAME                                     
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING MKTREC,R6                                                        
         MVI   MKTKTYPE,MKTKTYPQ                                                
         MVC   MKTKMED,SPTDOMED                                                 
         MVC   MKTKMKT,SPTDOMKT                                                 
         MVC   MKTKAGY,SPTDOAGY                                                 
         MVC   MKTKFILL,=7C'0'                                                  
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'STATION',KEY,STAIO                   
         CLC   KEY(L'MKTKEY),STAIO                                              
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ     IF NOT FOUND - SKIP ENTIRE RECORD            
         B     SPTDAOXX                                                         
*                                                                               
         LA    R6,STAIO                                                         
         MVC   SPTDOMKN,MKTNAME                                                 
*                                                                               
         DROP  R6                                                               
*                                                                               
         MVC   SPTDOXKY(2),=C'0X'  START OF HEX KEY                             
         NI    SPTDOXKY+1,X'FF'-X'40' LOWERCASE THE 'X'                         
         GOTO1 =V(HEXOUT),DMCB,HEXKEY,SPTDOXKY+2,L'BUYKEY                       
*                                                                               
SPTDAOX  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
SPTDAOXX DS    0H                                                               
         J     EQXIT                                                            
         DROP  R2                                                               
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDMN - DARE MAKEGOOD NOTICE                                       *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDMN   NMOD1 WORKL,*SPTDMN*,CLEAR=Y                                           
         MVC   SVDMCB,0(R1)        PARAMETERS PASSED TO                         
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
* NB:                                                                           
* RECORD IN R3 IS MAKEGOOD NOTICE, HOWEVER, THE OUTPUT WILL BE A                
* DARE ORDER RECORD, HENCE USING SPTDOD,R2                                      
         USING SPTDOD,R2           R2=A(EXTRACT RECORD)                         
         USING DAREMGND,R3         R3=A(DARE MAKEGOOD NOTICE)                   
*                                                                               
* CHECK RECORD STATUS                                                           
*                                                                               
         LR    R6,R3                                                            
         MVC   DATADISP,=AL2(MNRFRST-MNXKEY)                                    
         MVI   ELCODE,MNSTELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTDMN20                                                         
*                                                                               
         USING MNSTELD,R6          X'05', MAKEGOOD GROUP STATUS ELEM            
         MVC   BYTE,MNSTSTAT       STATUS                                       
         DROP  R6                                                               
*                                                                               
         CLI   BYTE,MNSTOKAY       C'O' - OKAYED                                
         BE    *+12                                                             
         CLI   BYTE,MNSTSAPP       C'S' - SELF APPLIED                          
         BNE   SPTDMN20                                                         
*                                                                               
* CHECK COPY RECORD STATUS - MUST BE DIFFERENT                                  
*                                                                               
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         L     R6,DXACPYB                                                       
         DROP  R7                                                               
*                                                                               
         CLC   =AL2(0),0(R6)       ANYTHING IN COPY BUFFER?                     
         BZ    SPTDMN                                                           
*                                                                               
         LA    R6,24+4(R6)         SKIP RECOVERY HEADER+REC LEN                 
         BRAS  RE,GETEL                                                         
         BNE   SPTDMN40                                                         
*                                                                               
         USING MNSTELD,R6          X'05', MAKEGOOD GROUP STATUS ELEM            
         CLC   BYTE,MNSTSTAT       STATUS                                       
         BNE   SPTDMN40 STATUS MUST BE DIFFERENT ON COPY/CHANGE RECORDS         
         DROP  R6                                                               
*                                                                               
SPTDMN20 DS    0H                                                               
         OI    FLAGS2,NOWRITEQ     SKIP ENTIRE RECORD                           
         B     SPTDMNXX                                                         
*                                                                               
* READ DARE ORDER RECORD, CORRESPONDING TO CURRENT MAKEGOOD NOTICE              
*                                                                               
SPTDMN40 DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING DAREORDD,R5                                                      
         MVI   DOKTYPE,DOKTYPQ                                                  
         MVI   DOKSUBTY,DOKSTYPQ                                                
         MVC   DOKAGMD,MNKAGMD                                                  
         MVC   DOKORDER,MNKORDER                                                
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(DOKSTA-DOKEY),KEY KEY W/O STATION                        
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ     IF NOT - SKIP ENTIRE RECORD                  
         B     SPTDMNXX                                                         
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* EXTRACT THE DARE ORDER RECORD                                                 
*                                                                               
         MVC   DMCB,SVDMCB                                                      
         MVI   MGFLAG,C'Y'                                                      
         GOTO1 =A(SPTDAO),DMCB,,STAIO                                           
         MVI   MGFLAG,C'N'                                                      
*                                                                               
         MVC   SPTDOTYP,SPMNQ      REPLACE RECORD TYPE                          
         MVI   SPTDOSTT,C'M'       STATUS="MAKEGOOD"                            
*                                                                               
         LR    R6,R3               MAKEGOOD NOTICE RECORD                       
         MVI   ELCODE,MNSTELQ      X'05', MAKEGOOD GROUP STATUS ELEMENT         
         BRAS  RE,GETEL                                                         
         BNE   SPTDMNX                                                          
*                                                                               
         USING MNSTELD,R6                                                       
         GOTO1 DATCON,DMCB,(8,MNSTDATE),(10,SPTDOUDT)                           
         GOTO1 =A(SHOWTIME),DMCB,MNSTTIME,SPTDOUTM                              
         DROP  R6                                                               
*                                                                               
SPTDMNX  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
SPTDMNXX DS    0H                                                               
         J     EQXIT                                                            
         DROP  R2,R3                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTDMX - DARE MAKEGOOD NOTICE - XSPOT                               *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTDMX   NMOD1 WORKL,*SPTDMX*,CLEAR=Y                                           
         MVC   SVDMCB,0(R1)        PARAMETERS PASSED TO                         
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
* NB:                                                                           
* RECORD IN R3 IS MAKEGOOD NOTICE, HOWEVER, THE OUTPUT WILL BE A                
* DARE ORDER RECORD, HENCE USING SPTDOD,R2                                      
         USING SPTDOD,R2           R2=A(EXTRACT RECORD)                         
         USING MNXKEY,R3           R3=A(DARE MAKEGOOD NOTICE)                   
*                                                                               
* CHECK RECORD STATUS                                                           
*                                                                               
         LR    R6,R3                                                            
         MVC   DATADISP,=AL2(42)   FOR XSPDIR/XSPFIL                            
         MVI   ELCODE,MNSTELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   SPTDMX20                                                         
*                                                                               
         USING MNSTELD,R6          X'05', MAKEGOOD GROUP STATUS ELEM            
         MVC   BYTE,MNSTSTAT       STATUS                                       
         DROP  R6                                                               
*                                                                               
         CLI   BYTE,MNSTOKAY       C'O' - OKAYED                                
         BE    *+12                                                             
         CLI   BYTE,MNSTSAPP       C'S' - SELF APPLIED                          
         BNE   SPTDMX20                                                         
*                                                                               
* CHECK COPY RECORD STATUS - MUST BE DIFFERENT                                  
*                                                                               
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         L     R6,DXACPYB                                                       
         DROP  R7                                                               
*                                                                               
         CLC   =AL2(0),0(R6)       ANYTHING IN COPY BUFFER?                     
         BZ    SPTDMX                                                           
*                                                                               
         LA    R6,24+4(R6)         SKIP RECOVERY HEADER+REC LEN                 
         BRAS  RE,GETEL                                                         
         BNE   SPTDMX40                                                         
*                                                                               
         USING MNSTELD,R6          X'05', MAKEGOOD GROUP STATUS ELEM            
         CLC   BYTE,MNSTSTAT       STATUS                                       
         BNE   SPTDMX40 STATUS MUST BE DIFFERENT ON COPY/CHANGE RECORDS         
         DROP  R6                                                               
*                                                                               
SPTDMX20 DS    0H                                                               
         OI    FLAGS2,NOWRITEQ     SKIP ENTIRE RECORD                           
         B     SPTDMXXX                                                         
*                                                                               
* READ DARE ORDER RECORD, CORRESPONDING TO CURRENT MAKEGOOD NOTICE              
*                                                                               
SPTDMX40 DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING DAREORDD,R5                                                      
         MVI   DOKTYPE,DOKTYPQ                                                  
         MVI   DOKSUBTY,DOKSTYPQ                                                
         MVC   DOKAGMD,MNXKAGMD                                                 
         MVC   DOKORDER,MNXKORDR                                                
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEY,KEY,WORK                 
         CLC   KEYSAVE(DOKSTA-DOKEY),KEY KEY W/O STATION                        
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ     IF NOT - SKIP ENTIRE RECORD                  
         B     SPTDMXXX                                                         
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
* EXTRACT THE DARE ORDER RECORD                                                 
*                                                                               
         MVC   DMCB,SVDMCB                                                      
         MVI   MGFLAG,C'Y'                                                      
         GOTO1 =A(SPTDAO),DMCB,,STAIO                                           
         MVI   MGFLAG,C'N'                                                      
*                                                                               
         MVC   SPTDOTYP,SPMNQ      REPLACE RECORD TYPE                          
         MVI   SPTDOSTT,C'M'       STATUS="MAKEGOOD"                            
*                                                                               
         LR    R6,R3               MAKEGOOD NOTICE RECORD                       
         MVI   ELCODE,MNSTELQ      X'05', MAKEGOOD GROUP STATUS ELEMENT         
         BRAS  RE,GETEL                                                         
         BNE   SPTDMXX                                                          
*                                                                               
         USING MNSTELD,R6                                                       
         GOTO1 DATCON,DMCB,(8,MNSTDATE),(10,SPTDOUDT)                           
         GOTO1 =A(SHOWTIME),DMCB,MNSTTIME,SPTDOUTM                              
         DROP  R6                                                               
*                                                                               
SPTDMXX  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
SPTDMXXX DS    0H                                                               
         J     EQXIT                                                            
         DROP  R2,R3                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTBXQ - BUY EXTRACT REQUEST                                        *         
*                                                                     *         
* DXAXREC (R2) EXPECTED TO HAVE AN EXTRACTED DARE ORDER/              *         
* MAKEGOOD NOTIFICATION RECORD                                        *         
* BUY EXTRACT REQUEST WILL BE BUILT FROM IT, AND WILL REPLACE IT      *         
* IN DXAXREC                                                          *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTBXQ   NMOD1 WORKL,*SPTBXQ*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
* SAVE THE EXTRACTED RECORD IN STAIO                                            
*                                                                               
         USING SPTDOD,R2           EXTRACTED DARE ORDER/MAKEGOOD RECORD         
         XR    RF,RF                                                            
         ICM   RF,3,SPTDOLEN                                                    
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   STAIO(0),0(R2)                                                   
         DROP  R2                                                               
*                                                                               
         USING SPTXQD,R2           R2=A(EXTRACT RECORD)                         
         LA    R3,STAIO                                                         
         USING SPTDOD,R3           EXTRACTED DARE ORDER/MAKEGOOD RECORD         
*                                                                               
         MVC   SPTXQLEN(2),=AL2(SPTXQDL)                                        
         MVC   SPTXQTYP,SPXQQ                                                   
         MVC   SPTXQREQ,SPACES                                                  
*                                                                               
         LA    R4,SPTXQREQ                                                      
         USING QRECORD,R4                                                       
*                                                                               
         MVC   QPROG,=C'DB'                                                     
         MVC   QAGY,SPTDOAGY                                                    
         MVC   QMED,SPTDOMED                                                    
         MVC   QCLT,SPTDOCLT                                                    
         MVC   QPRD,SPTDOPR1                                                    
         MVC   QSTA,SPTDOSTA                                                    
         CLI   QSTA,C'0'           CABLE?                                       
         BL    *+8                                                              
         MVI   QSTA+4,C'/'                                                      
         MVC   QEST,SPTDOEST                                                    
         MVC   QSTAUTO(6),=C'NOAUTO'                                            
         MVI   QOPT2,C'J'                                                       
         MVC   QUESTOR,=CL12'XTRBLK      '                                      
*                                                                               
* READ STATION RECORD TO GET MARKET                                             
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
*                                                                               
         USING STAREC,R6                                                        
         MVI   STAKTYPE,STAKTYPQ                                                
         MVC   STAKMED,SPTDOMED                                                 
         MVC   STAKCALL,SPTDOSTA                                                
         CLI   SPTDOSTA+4,C' '                                                  
         BH    *+8                                                              
         MVI   STAKCALL+4,C'T'                                                  
         MVC   STAKAGY,SPTDOAGY                                                 
         MVC   STAKCLT(6),=6C'0'                                                
*                                                                               
         DROP  R3                                                               
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'STATION',KEY,STAIO                   
         CLC   KEY(L'STAKEY),STAIO                                              
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ                                                  
         B     SPTBXQX                                                          
*                                                                               
         LA    R6,STAIO                                                         
         MVC   QMKT,SMKT                                                        
         DROP  R6                                                               
*                                                                               
* READ ESTIMATE TO GET START-END DATES                                          
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING ESTHDR,R6                                                        
*                                                                               
         L     R7,ADXBLOCK                                                      
         USING DXBLOCKD,R7                                                      
         L     R5,DXARECB                                                       
         LA    R5,4(R5)     SKIP REC LENGTH                                     
*                                                                               
         CLI   0(R5),X'37'  XSPOT?                                              
         BNE   SPTBXQ10                                                         
         LA    R5,24(R5)    SKIP RCV HEADER                                     
         MVC   EKEYAM,MNXKAGMD-MNXKEY(R5)                                       
         B     SPTBXQ20                                                         
*                                                                               
SPTBXQ10 DS    0H                                                               
         LA    R5,24(R5)    SKIP RCV HEADER                                     
         CLI   1(R5),DOKSTYPQ                                                   
         BNE   *+10                                                             
         MVC   EKEYAM,DOKAGMD-DOKEY(R5)                                         
         CLI   1(R5),MNKSTYPQ                                                   
         BNE   *+10                                                             
         MVC   EKEYAM,MNKAGMD-MNKEY(R5)                                         
*                                                                               
SPTBXQ20 DS    0H                                                               
         MVC   EKEYCLT,BREAKLS     SAVED PACKED CLIENT                          
         MVC   EKEYPRD,QPRD                                                     
         MVC   EKEYEST,BREAKLS+2   SAVED ESTIMATE                               
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'SPTDIR',KEY,KEY,WORK                 
         BE    *+12                                                             
         OI    FLAGS2,NOWRITEQ                                                  
         B     SPTBXQX                                                          
*                                                                               
         GOTO1 (RF),DMCB,(X'00',=C'GETREC'),=C'SPTFIL ',KEY+14,        X        
               STAIO,WORK                                                       
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R6,STAIO                                                         
         GOTO1 DATCON,DMCB,(0,ESTART),(X'20',QSTART)                            
         GOTO1 DATCON,DMCB,(0,EEND),(X'20',QEND)                                
*                                                                               
SPTBXQX  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
SPTBXQXX DS    0H                                                               
         J     EQXIT                                                            
         DROP  R2                                                               
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTFLT - FLIGHT RECORDS                                             *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         USING SXDTABD,R4                                                       
SPTFLT   NMOD1 WORKL,*SPTFLT*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTFLD,R2           R2=A(EXTRACT RECORD)                         
         USING DFLRECD,R3          R3=A(SPOT PRODUCT RECORD)                    
*                                                                               
         XC    SPTFLLEN,SPTFLLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTFLLEN(2),=AL2(SPTFLDL)                                        
         MVC   SPTFLTYP,SPFLQ                                                   
*                                                                               
         MVC   SPTFLAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,DFLKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTFLMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,DFLKCLT),SPTFLCLT                                
*                                                                               
         MVC   SPTFLPRD,DFLKPRD                                                 
*                                                                               
         EDIT  DFLKEST,SPTFLEST,FILL=0                                          
*                                                                               
         LA    R6,DFLEL                                                         
         USING DFINFEL,R6                                                       
*                                                                               
         CLI   0(R6),DFINFELQ                                                   
         BNE   SPTFLT5                                                          
*                                                                               
         GOTO1 DATCON,DMCB,(3,DFINFSDT),(10,SPTFLNFD)                           
*                                                                               
SPTFLT5  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3,R6                                                         
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTFLF - FLIGHT RECORD FLIGHTS                                      *         
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
         USING SXDTABD,R4                                                       
SPTFLF   NMOD1 WORKL,*SPTFLF*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTFFD,R2           R2=A(EXTRACT RECORD)                         
         USING DFLRECD,R3          R3=A(SPOT PRODUCT RECORD)                    
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTFFLEN,SPTFFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTFFLEN(2),=AL2(SPTFFDL)                                        
         MVC   SPTFFTYP,SPFFQ                                                   
*                                                                               
         MVC   SPTFFAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,DFLKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTFFMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,DFLKCLT),SPTFFCLT                                
*                                                                               
         MVC   SPTFFPRD,DFLKPRD                                                 
*                                                                               
         EDIT  DFLKEST,SPTFFEST,FILL=0                                          
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTFFF10                                                         
*                                                                               
         MVI   SPTFFACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTFFF50                                                         
*                                                                               
SPTFFF10 DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTFFACT,C'A'                                                    
*                                                                               
         XR    R7,R7                                                            
         LR    R6,R3                                                            
         MVI   ELCODE,DFFLTELQ                                                  
         BRAS  RE,GETEL                                                         
*                                                                               
SPTFFF20 DS    0H                                                               
         BE    *+16                                                             
         MVI   ELCOUNT,0             RE-SET ELEMENT COUN                        
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
         CLM   R7,1,ELCOUNT                                                     
         BL    SPTFFF30                                                         
*                                                                               
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
         EDIT  (B1,ELCOUNT),(3,SPTFFNUM),FILL=0                                 
*                                                                               
         USING DFFLTEL,R6                                                       
*                                                                               
         MVI   SPTFFLOK,C'N'                                                    
         TM    DFFLTSTA,DFFLTSLK                                                
         BZ    *+8                                                              
         MVI   SPTFFLOK,C'Y'                                                    
*                                                                               
         GOTO1 DATCON,DMCB,(3,DFFLTSTR),(10,SPTFFSDT)                           
         GOTO1 DATCON,DMCB,(3,DFFLTEND),(10,SPTFFEDT)                           
         B     SPTFFF50                                                         
*                                                                               
SPTFFF30 DS    0H                                                               
         AHI   R7,1                                                             
         BRAS  RE,NEXTEL                                                        
         B     SPTFFF20                                                         
*                                                                               
SPTFFF50 DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
         DROP  R2,R3,R6                                                         
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSPD - EXTRACT SPILLDEF RECORDS                                             
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTSPC   NMOD1 WORKL,*SPTSPC*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSPD,R2           R2=A(EXTRACT RECORD)                         
         USING SDEFRECD,R3         R3=A(SPOT SPILLDEF RECORD)                   
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTSPLEN,SPTSPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSPLEN(2),=AL2(SPTSPDL)                                        
         MVC   SPTSPTYP,SPSPQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTSPAGY,SXDTAGY                                                 
*                                                                               
         CLI   SXDTCTRY,CTRYCANQ   TEST CANADA                                  
         BNE   SPTSP01             NO                                           
*                                                                               
         MVC   SPTSPRTG,=C'BBM'                                                 
         CLI   SDEFKRSV,C'1'                                                    
         BE    SPTSP01X                                                         
         MVC   SPTSPRTG,=C'CSI'                                                 
         B     SPTSP01X                                                         
*                                                                               
SPTSP01  DS    0H                                                               
         MVC   SPTSPRTG,=C'ARB'                                                 
         CLI   SDEFKRSV,C'1'                                                    
         BE    SPTSP01X                                                         
         MVC   SPTSPRTG,=C'NSI'                                                 
*                                                                               
SPTSP01X DS    0H                                                               
         CLI   SDEFKSTA+4,C' '                                                  
         BH    *+12                                                             
         MVI   SPTSPMED,C'T'                                                    
         B     SPTSP02                                                          
*                                                                               
         CLI   SDEFKSTA+4,C'A'                                                  
         BE    *+12                                                             
         CLI   SDEFKSTA+4,C'F'                                                  
         BNE   SPTSP02                                                          
         MVI   SPTSPMED,C'R'                                                    
*                                                                               
SPTSP02  DS    0H                                                               
         MVC   SPTSPSTA,SDEFKSTA                                                
*                                                                               
         OC    SDEFKCLT,SDEFKCLT                                                
         BZ    SPTSP03                                                          
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,SDEFKCLT),SPTSPCNT                               
*                                                                               
SPTSP03  DS    0H                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTSP05                                                          
*                                                                               
         MVI   SPTSPACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTSP40                                                          
*                                                                               
SPTSP05  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTSPACT,C'A'                                                    
*                                                                               
         SR    R9,R9               LOOP COUNTER                                 
         LLC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,SDEFELQ                                                   
         BRAS  RE,GETEL                                                         
*                                                                               
SPTSP10  DS    0H                                                               
         BE    SPTSP15                                                          
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'FF'                                                      
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         J     NEQXIT                                                           
*                                                                               
SPTSP15  DS    0H                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTSP20                                                          
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
*                                                                               
         EDIT  (R9),(3,SPTSPSEQ),FILL=0                                         
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING SDEFEL05,R6                                                      
*                                                                               
         EDIT  (B2,SDEFAMKT),(4,SPTSPMKT),FILL=0                                
         EDIT  (B2,SDEFRMKT),(4,SPTSPNMK),FILL=0                                
         MVC   SPTSPALP,SDEFALPH                                                
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING MKTREC,R6                                                        
*                                                                               
         MVI   MKTKTYPE,MKTKTYPQ                                                
         MVC   MKTKMED,SPTSPMED                                                 
         MVC   MKTKMKT,SPTSPMKT                                                 
         MVC   MKTKAGY,SXDTAGY                                                  
         MVC   MKTKFILL,=7C'0'                                                  
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,(0,=C'DMRDHI'),=C'STATION',KEY,STAIO                   
         CLC   KEY(L'MKTKEY),STAIO                                              
         JNE   *+2                                                              
         LA    R6,STAIO                                                         
         MVC   SPTSPMKN,MKTNAME                                                 
*                                                                               
         DROP  R6                                                               
*                                                                               
         B     SPTSP40                                                          
*                                                                               
SPTSP20  DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTSP10                                                          
*                                                                               
SPTSP40  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSPD - EXTRACT SQAD DAYPART MENUS                                           
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTSQC   NMOD1 WORKL,*SPTSQC*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTSQD,R2           R2=A(EXTRACT RECORD)                         
         USING SQDRECD,R3          R3=A(SPOT SQAD DAYPART MENU)                 
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTSQLEN,SPTSQLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTSQLEN(2),=AL2(SPTSQDL)                                        
         MVC   SPTSQTYP,SPSQQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTSQAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,SQDKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTSQMED,BYTE                                                    
*                                                                               
         MVC   SPTSQDPM,SQDKDPT                                                 
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTSQ05                                                          
*                                                                               
         MVI   SPTSQACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTSQ40                                                          
*                                                                               
SPTSQ05  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTSQACT,C'A'                                                    
*                                                                               
         SR    R9,R9               LOOP COUNTER                                 
         LLC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,SQDELQ       TV ELEMENT                                   
         BRAS  RE,GETEL                                                         
*                                                                               
SPTSQ10  DS    0H                                                               
         BE    SPTSQ15                                                          
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'FF'                                                      
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         J     NEQXIT                                                           
*                                                                               
SPTSQ15  DS    0H                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTSQ20                                                          
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING SQDEL,R6                                                         
         MVC   SPTSQDDS,SQDDPT                                                  
*                                                                               
         CLI   SPTSQMED,C'T'                                                    
         BNE   SPTSQ17                                                          
*                                                                               
         LA    R1,TABTV                                                         
SPTSQ16  CLI   0(R1),X'FF'                                                      
         BE    SPTSQ40                                                          
         CLC   SQDSQAD,0(R1)                                                    
         BNE   SPTSQ16A                                                         
         MVC   SPTSQSQD(2),0(R1)                                                
         MVC   SPTSQSQN,2(R1)                                                   
         B     SPTSQ40                                                          
*                                                                               
SPTSQ16A DS    0H                                                               
         LA    R1,14(R1)                                                        
         B     SPTSQ16                                                          
*                                                                               
SPTSQ17  DS    0H                                                               
         LA    R1,TABRADIO                                                      
*                                                                               
SPTSQ18  CLI   0(R1),X'FF'                                                      
         BE    SPTSQ40                                                          
         CLC   SQDSQAD,17(R1)                                                   
         BNE   SPTSQ18A                                                         
         MVC   SPTSQSQD,0(R1)                                                   
         MVC   SPTSQSQN,5(R1)                                                   
         B     SPTSQ40                                                          
*                                                                               
SPTSQ18A DS    0H                                                               
         LA    R1,19(R1)                                                        
         B     SPTSQ18                                                          
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTSQ20  DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTSQ10                                                          
*                                                                               
SPTSQ40  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSPD - EXTRACT BILL FORMULAS                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTBFM   NMOD1 WORKL,*SPTBFM*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         USING SPTBFD,R2           R2=A(EXTRACT RECORD)                         
         USING BFREC,R3            R3=A(SPOT BILL FORMULA)                      
*                                                                               
         XC    SPTBFLEN,SPTBFLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTBFLEN(2),=AL2(SPTBFDL)                                        
         MVC   SPTBFTYP,SPBFQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTBFAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,BFKAGYMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTBFMED,BYTE                                                    
*                                                                               
         MVC   BYTE,CPROF7                                                      
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,BFKCLT),SPTBFCLT                                 
*                                                                               
         MVC   SPTBFPRD,=C'ALL'                                                 
         OC    BFKPRD,BFKPRD                                                    
         BZ    *+10                                                             
         MVC   SPTBFPRD,BFKPRD                                                  
*                                                                               
         MVC   SPTBFEST,=C'000'                                                 
         OC    BFKEST,BFKEST                                                    
         BZ    SPTBF02                                                          
         EDIT  BFKEST,SPTBFEST,FILL=0                                           
*                                                                               
SPTBF02  DS    0H                                                               
         MVC   SPTBFMKT,=CL4'ALL'                                               
         OC    BFKMKT,BFKMKT                                                    
         BZ    SPTBF03                                                          
         EDIT  BFKMKT,SPTBFMKT,FILL=0                                           
*                                                                               
SPTBF03  DS    0H                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTBF05                                                          
*                                                                               
         MVI   SPTBFACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTBF40                                                          
*                                                                               
SPTBF05  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTBFACT,C'A'                                                    
*                                                                               
         SR    R9,R9               LOOP COUNTER                                 
         LLC   R7,ELCOUNT          ELEMENT COUNTER                              
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,BFRCDELQ     BFRCDELD, COST DETAIL ELEMENTS               
         BRAS  RE,GETEL                                                         
*                                                                               
SPTBF10  DS    0H                                                               
         BE    SPTBF15                                                          
*                                                                               
         L     R1,APARM                                                         
         MVI   8(R1),X'FF'                                                      
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         J     NEQXIT                                                           
*                                                                               
SPTBF15  DS    0H                                                               
         AHI   R9,1                                                             
         CR    R9,R7                                                            
         BNH   SPTBF20                                                          
         STC   R9,ELCOUNT          SAVE ELEMENT COUNT                           
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING BFRCDELD,R6                                                      
*                                                                               
         CLC   BFRCDDTE,=X'FFFF'                                                
         BE    SPTBF18                                                          
*                                                                               
         MVC   FULL,BFRCDDTE                                                    
         XC    FULL(2),=2X'FF'                                                  
         MVI   FULL+2,X'01'                                                     
         GOTO1 DATCON,DMCB,(3,FULL),(9,SPTBFMOS)                                
*                                                                               
SPTBF18  DS    0H                                                               
         GOTO1 =A(DISBFM),DMCB,BFRCDFML,SPTBFBFM                                
*                                                                               
         B     SPTBF40                                                          
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTBF20  DS    0H                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTBF10                                                          
*                                                                               
SPTBF40  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSPD - EXTRACT DEMO MENUS                                                   
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
SPTDMU   NMOD1 WORKL,*SPTDMU*,CLEAR=Y                                           
*                                                                               
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTMUD,R2           R2=A(EXTRACT RECORD)                         
         USING DMNRECD,R3          R3=A(SPOT DEMU MENU)                         
*                                                                               
         XC    SPTMULEN,SPTMULEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTMULEN(2),=AL2(SPTMUDL)                                        
         MVC   SPTMUTYP,SPMUQ                                                   
*                                                                               
* DATA EXTRACT PART                                                             
*                                                                               
         MVC   SPTMUAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,DMNKAGMD                                                    
         BRAS  RE,GETMED                                                        
         MVC   SPTMUMED,BYTE                                                    
*                                                                               
         MVC   SPTMUMNU,DMNKCODE                                                
*                                                                               
         LA    R5,SPTMUD01                                                      
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'05'        TV ELEMENT                                   
         BRAS  RE,GETEL                                                         
*                                                                               
SPTDM10  DS    0H                                                               
         BNE   SPTDM40                                                          
*                                                                               
* FOUND THE NEXT ELEMENT                                                        
*                                                                               
         USING DMNEL05,R6                                                       
         MVC   0(L'SPTMUD01,R5),DMNRTG                                          
*                                                                               
         LA    R5,L'SPTMUD01+1(R5)                                              
*                                                                               
         DROP  R6                                                               
*                                                                               
         BRAS  RE,NEXTEL                                                        
         B     SPTDM10                                                          
*                                                                               
SPTDM40  DS    0H                                                               
         BRAS  RE,DELIMS                                                        
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
***********************************************************************         
*        SQAD DAYPARTS TABLE                                          *         
* !IMPORTANT!  THIS TABLE IS ALSO USED BY SPSYSDRV1                             
***********************************************************************         
*                                                                               
TABTV    DC   CL2'EM',CL12'EARLY AM'                                            
         DC   CL2'DA',CL12'DAY'                                                 
         DC   CL2'EF',CL12'EARLY FRINGE'                                        
         DC   CL2'EN',CL12'EARLY NEWS'                                          
         DC   CL2'PA',CL12'PRIME ACCESS'                                        
         DC   CL2'PR',CL12'PRIME'                                               
         DC   CL2'LN',CL12'LATE NEWS'                                           
         DC   CL2'LF',CL12'LATE FRINGE'                                         
         DC   X'FF'                                                             
***********************************************************************         
*        SQAD RADIO DAYPARTS TABLE                                              
* !IMPORTANT!  THIS TABLE IS ALSO USED BY SPSYSDRV1                             
***********************************************************************         
*                                                                               
TABRADIO DC   CL5'AMDRV',CL12'MORNING DRV ',CL2'AM'                             
         DC   CL5'DAY  ',CL12'DAYTIME     ',CL2'DA'                             
         DC   CL5'PMDRV',CL12'AFTERNOON DR',CL2'PM'                             
         DC   CL5'EVE  ',CL12'EVENING     ',CL2'EV'                             
         DC   CL5'AVG  ',CL12'AVERAGE     ',CL2'AV'                             
         DC   CL5'WE   ',CL12'WEEKEND     ',CL2'WE'                             
         DC   CL5'TAP  ',CL12'TOTAL AM-PM ',CL2'AP'                             
         DC   CL5'MSAVG',CL12'MON SUN AVG ',CL2'MS'                             
         DC   X'FF'                                                             
***********************************************************************         
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPPH - PATTERN HEADER RECORD                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPPH   NMOD1 WORKL,*SPTPPH*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPHD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTPHLEN,SPTPHLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPHLEN(2),=AL2(SPTPHDL)                                        
         MVC   SPTPHTYP,SPPHQ                                                   
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPHXNO                                                         
*&&                                                                             
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         X     R0,=XL4'00003FFF'                                                
         EDIT  (R0),(5,SPTPHREF),FILL=0                                         
*                                                                               
         MVC   SPTPHAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPHID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPHID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPHID+2,L'PATKEY                        
*                                                                               
         MVC   BYTE,PATKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTPHMED,BYTE                                                    
*                                                                               
         MVI   BYTE,X'00'                                                       
         CLI   CPROF7,C'Y'                                                      
         BNE   *+8                                                              
         MVI   BYTE,C'Y'                                                        
*                                                                               
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,PATKCLT),SPTPHCLT                                
*                                                                               
         CLI   PATKPRD,X'00'                                                    
         BE    SPTPH10                                                          
*                                                                               
         LA    R1,SPTPHP1C                                                      
         ICM   R1,8,PATKPRD                                                     
         BRAS  RE,FINDAPRD                                                      
*                                                                               
         EDIT  PATKSLN,(3,SPTPHP1L),FILL=0                                      
*                                                                               
SPTPH10  DS    0H                                                               
         CLI   PATKPRD2,X'00'                                                   
         BE    SPTPH20                                                          
*                                                                               
         LA    R1,SPTPHP2C                                                      
         ICM   R1,8,PATKPRD2                                                    
         BRAS  RE,FINDAPRD                                                      
*                                                                               
         EDIT  PATKSLN2,(3,SPTPHP2L),FILL=0                                     
*                                                                               
SPTPH20  DS    0H                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'S0T0'                                                 
         MVC   WORK+4(2),SPTPHAGY                                               
         MVC   WORK+6(1),SPTPHMED                                               
         MVC   WORK+7(3),SPTPHCLT                                               
         L     RF,COMFACS                                                       
         MVC   DMCB+8(4),0(RF)        A(DATAMGR)                                
         GOTO1 =V(GETPROF),DMCB,WORK,PROFT0                                     
*                                                                               
         CLI   PROFT0+10,C'E'                                                   
         BNE   SPTPH50                                                          
         EDIT  PATKCODE,(3,SPTPHEST),FILL=0                                     
         B     SPTPH60                                                          
*                                                                               
SPTPH50  DS    0H                                                               
         CLI   PROFT0+10,C'D'                                                   
         BNE   *+14                                                             
         MVC   SPTPHDPT,PATKCODE                                                
         B     SPTPH60                                                          
*                                                                               
         CLI   PROFT0+10,C'P'                                                   
         BNE   *+14                                                             
         MVC   SPTPHPDP,PATKCODE                                                
         B     SPTPH60                                                          
*                                                                               
         CLI   PROFT0+10,C'A'                                                   
         BNE   *+14                                                             
         MVC   SPTPHADJ,PATKCODE                                                
         B     SPTPH60                                                          
*                                                                               
SPTPH60  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'10'        PATTERN DATA ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPH80                                                          
*                                                                               
         USING PATDTAEL,R6                                                      
         MVC   SPTPHDSC,PATDESC                                                 
*                                                                               
         GOTO1 DATCON,DMCB,(3,PATSTART),(10,SPTPHSDT)                           
*                                                                               
         MVC   SPTPHEDT(4),=C'NULL'                                             
         CLC   =X'FFFFFF',PATEND                                                
         BE    SPTPH63                                                          
         GOTO1 DATCON,DMCB,(3,PATEND),(10,SPTPHEDT)                             
*                                                                               
SPTPH63  DS    0H                                                               
         OC    PATSTIM,PATSTIM                                                  
         BZ    SPTPH65                                                          
         XC    FULL,FULL                                                        
         MVC   FULL(2),PATSTIM                                                  
         GOTO1 AUNTIME,DMCB,FULL,SPTPHSTM                                       
*                                                                               
SPTPH65  DS    0H                                                               
         OC    PATETIM,PATETIM                                                  
         BZ    SPTPH70                                                          
         XC    FULL,FULL                                                        
         MVC   FULL(2),PATETIM                                                  
         GOTO1 AUNTIME,DMCB,FULL,SPTPHETM                                       
*                                                                               
SPTPH70  DS    0H                                                               
         MVI   SPTPHTDL,C'N'                                                    
         TM    PATSTAT1,PATSDLY                                                 
         BZ    *+8                                                              
         MVI   SPTPHTDL,C'Y'                                                    
*                                                                               
         MVI   SPTPHDEL,C'N'                                                    
         TM    PATSTAT,X'80'                                                    
         BZ    *+8                                                              
         MVI   SPTPHDEL,C'Y'                                                    
*                                                                               
         MVI   SPTPHINV,C'N'                                                    
         TM    PATSTAT,X'04'                                                    
         BZ    *+8                                                              
         MVI   SPTPHINV,C'Y'                                                    
*                                                                               
         DROP  R6                                                               
*                                                                               
SPTPH80  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        PATTERN LIST ELEMEN                          
         BRAS  RE,GETEL                                                         
         JNE   SPTPH90                                                          
*                                                                               
         USING PATLSTEL,R6         X'20', PATTERN LIST ELEMENT                  
         MVC   SPTPHTPE,PATLSTTY                                                
         DROP  R6                                                               
*                                                                               
SPTPH90  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'50'        LINK TO TEXT RECORD                          
         BRAS  RE,GETEL                                                         
         JNE   SPTPH95                                                          
*                                                                               
         USING PATTXTEL,R6         X'50', LINK TO TEXT RECORD                   
         MVC   SPTPHTXT,PATTXTKY                                                
         DROP  R6                                                               
*                                                                               
SPTPH95  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'32'        COMMERCIAL PATTERN ROTATION ELEMENT          
         BRAS  RE,GETEL                                                         
         JNE   SPTPH98                                                          
*                                                                               
         USING PATPTNEL,R6                                                      
         CLI   PATPTNLN,3                                                       
         BL    SPTPH98                                                          
         LLC   RF,PATPTNLN                                                      
         AHI   RF,-3               -ELCODE, -LEN, BCTR                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTPHROT(0),PATPTN                                               
         DROP  R6                                                               
*                                                                               
SPTPH98  DS    0H                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'30'        COMMERCIAL LIST ELEMENT                      
         BRAS  RE,GETEL                                                         
         JNE   SPTPHX                                                           
*                                                                               
         USING PATCMLEL,R6                                                      
         MVI   SPTPHHIA,C'N'                                                    
         CLC   =C'HIATUS',PATCML                                                
         BNE   SPTPHX                                                           
         MVI   SPTPHHIA,C'Y'                                                    
         DROP  R6                                                               
*                                                                               
SPTPHX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPHXNO DS    0H                                                               
         OI    FLAGS2,NOWRITEQ                                                  
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPCM - PATTERN COMMERCIAL                                                   
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPCM   NMOD1 WORKL,*SPTPCM*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPCD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPCLEN,SPTPCLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPCLEN(2),=AL2(SPTPCDL)                                        
         MVC   SPTPCTYP,SPPCQ                                                   
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPCXNO                                                         
*&&                                                                             
*                                                                               
         MVC   SPTPCAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPCID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPCID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPCID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPC10                                                          
*                                                                               
         MVI   SPTPCACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPCX                                                           
*                                                                               
SPTPC10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPCACT,C'A'                                                    
*                                                                               
* EXTRACT COMMERCIAL'S ROTATION LETTER                                          
         LLC   R9,ELCOUNT                                                       
         LA    R9,LTRTAB(R9)       INDEX INTO LETTER TABLE                      
         MVC   SPTPCLTR,0(R9)                                                   
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'10'        PATTERN DATA ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPCXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATDTAEL,R6                                                      
         MVC   BYTE,PATSTAT1       TO TEST IF COMMLS ARE ASCII/TRPACK           
         DROP  R6                                                               
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'30'        COMMERCIAL LIST ELEMENT                      
         BRAS  RE,GETEL                                                         
         JNE   SPTPCXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATCMLEL,R6                                                      
*                                                                               
         CLC   =C'HIATUS',PATCML                                                
         BE    SPTPCXNO                                                         
*                                                                               
         LLC   R7,PATCMLLN                                                      
         AR    R7,R6               R7 -> END OF COMMERCIAL ELEMENT              
*                                                                               
         LLC   R9,ELCOUNT          COMMERCIAL WE'RE ON RIGHT NOW                
         MHI   R9,L'PATCML         INDEX TO IT WITHIN THE ELEMENT               
         LA    R9,PATCML(R9)                                                    
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPCXNO            YES - DONE WITH THIS PATTERN                 
*                                                                               
         CLC   =X'C500',0(R9)      DELETED COMMERCIAL? (STARTS W C'*')          
         BNE   *+12                                                             
         MVI   SPTPCCM1,C'*'                                                    
         B     SPTPC50             DONE WITH THIS COMMERCIAL                    
*                                                                               
         TM    BYTE,PATSADID       X'80' = ALL CMMLS ARE PACKED?                
         BO    *+14                YES - UNPACK IT                              
         MVC   SPTPCCM1(8),0(R9)   COPY COMMERCIAL AS TEXT                      
         B     SPTPC15                                                          
*                                                                               
         GOTO1 ATRPACK,DMCB,(C'U',0(R9)),SPTPCCM1 UNPACK IT                     
*                                                                               
SPTPC15  DS    0H                                                               
         OC    8(8,R9),8(R9)       ANYTHING IN COMML 2?                         
         BZ    SPTPC20                                                          
*                                                                               
         TM    BYTE,PATSADID       X'80' = ALL CMMLS ARE PACKED?                
         BO    *+14                YES - UNPACK IT                              
         MVC   SPTPCCM2(8),8(R9)   COPY COMMERCIAL AS TEXT                      
         B     SPTPC20                                                          
*                                                                               
         GOTO1 ATRPACK,DMCB,(C'U',8(R9)),SPTPCCM2                               
*                                                                               
SPTPC20  DS    0H                                                               
         MVI   SPTPCPCT,C'0'                                                    
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'34'        COMMERCIAL PATTERN PERCENT ELEMENT           
         BRAS  RE,GETEL                                                         
         BNE   SPTPC50             NOT THERE - DONE WITH THIS COMML             
*                                                                               
         USING PATPCTEL,R6                                                      
         LLC   R7,PATPCTLN         ELEMENT LENGTH                               
         AR    R7,R6               R7 -> END OF PERCENT ELEMENT                 
         LA    R9,PATPCTLT         FIRST COMMERCIAL LETTER IN ELEMENT           
*                                                                               
SPTPC30  DS    0H                                                               
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPC50             DONE WITH THIS COMMERCIAL                    
         CLC   SPTPCLTR,0(R9)      FOUND OUR LETTER?                            
         BE    *+12                                                             
         LA    R9,(L'PATPCTLT+L'PATPCTPC)(R9) MOVE ON TO NEXT LETTER            
         B     SPTPC30                                                          
*                                                                               
         EDIT  (B2,1(R9)),(3,SPTPCPCT),0,ALIGN=LEFT                             
         DROP  R6                                                               
*                                                                               
SPTPC50  DS    0H                  INCREMENT ELCOUNT AND GET OUT                
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
SPTPCX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPCXNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
LTRTAB   DC    C'ABCDEFGHIJKLMNO'                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPMK - PATTERN MARKETS                                                      
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPMK   NMOD1 WORKL,*SPTPMK*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPMD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPMXNO                                                         
*&&                                                                             
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        PATTERN LIST ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPMXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATLSTEL,R6                                                      
         CLI   PATLSTTY,C'M'       TYPE=MARKET?                                 
         BNE   SPTPMXNO                                                         
         DROP  R6                                                               
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPMLEN,SPTPMLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPMLEN(2),=AL2(SPTPMDL)                                        
         MVC   SPTPMTYP,SPPMQ                                                   
*                                                                               
         MVC   SPTPMAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPMID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPMID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPMID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPM10                                                          
*                                                                               
         MVI   SPTPMACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPMX                                                           
*                                                                               
SPTPM10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPMACT,C'A'                                                    
*                                                                               
         LLC   R7,PATLSTLN                                                      
         AR    R7,R6               R7 -> END OF PATTERN LIST ELEMENT            
*                                                                               
         LLC   R9,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         MHI   R9,L'PATLST         INDEX TO IT WITHIN THE ELEMENT               
         LA    R9,PATLST(R9)                                                    
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPMXNO            YES - DONE WITH THIS PATTERN                 
*                                                                               
         LLC   R0,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         AHI   R0,1                                                             
         EDIT  (R0),(3,SPTPMSEQ),FILL=0,ALIGN=LEFT                              
*                                                                               
         OC    0(L'PATLST,R9),0(R9) MARKET = ALL?                               
         BNE   *+14                                                             
         MVC   SPTPMMKT,=CL4'ALL'                                               
         B     SPTPM50             DONE WITH THIS MARKET                        
*                                                                               
         EDIT  (B2,3(R9)),(4,SPTPMMKT),0,ALIGN=LEFT                             
*                                                                               
SPTPM50  DS    0H                  INCREMENT ELCOUNT AND GET OUT                
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
SPTPMX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPMXNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPST - PATTERN STATIONS                                                     
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPST   NMOD1 WORKL,*SPTPST*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPSD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPSXNO                                                         
*&&                                                                             
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        PATTERN LIST ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPSXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATLSTEL,R6                                                      
         CLI   PATLSTTY,C'S'       TYPE=STATION?                                
         BNE   SPTPSXNO                                                         
         DROP  R6                                                               
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPSLEN,SPTPSLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPSLEN(2),=AL2(SPTPSDL)                                        
         MVC   SPTPSTYP,SPPSQ                                                   
*                                                                               
         MVC   SPTPSAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPSID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPSID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPSID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPS10                                                          
*                                                                               
         MVI   SPTPSACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPSX                                                           
*                                                                               
SPTPS10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPSACT,C'A'                                                    
*                                                                               
         LLC   R7,PATLSTLN                                                      
         AR    R7,R6               R7 -> END OF PATTERN LIST ELEMENT            
*                                                                               
         LLC   R9,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         MHI   R9,L'PATLST         INDEX TO IT WITHIN THE ELEMENT               
         LA    R9,PATLST(R9)                                                    
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPSXNO            YES - DONE WITH THIS PATTERN                 
*                                                                               
         LLC   R0,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         AHI   R0,1                                                             
         EDIT  (R0),(3,SPTPSSEQ),FILL=0,ALIGN=LEFT                              
*                                                                               
         OC    0(L'PATLST,R9),0(R9) STATION = NULLS?                            
         BZ    SPTPSXNO            BAD DATA - DONE WITH THIS PATTERN            
*                                                                               
         CLC   =X'0000',0(R9)      FIRST 2 BYTES NULLS?                         
         BE    *+14                YES - STATION IS PACKED                      
         MVC   SPTPSSTA(5),0(R9)   STATION IS CHAR HERE                         
         B     SPTPS50                                                          
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         USING STAPACKD,R1                                                      
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SXDTAGY                                                  
         MVC   BYTE,PATKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   STAPMED,BYTE                                                     
         CLI   SXDTCTRY,CTRYCAN                                                 
         BNE   *+8                                                              
         MVI   STAPCTRY,C'C'                                                    
         MVC   STAPSTA,2(R9)                                                    
         L     RF,COMFACS                                                       
         ST    RF,STAPACOM                                                      
         GOTO1 VSTAPACK,(R1)                                                    
         LA    R1,WORK                                                          
         CLI   STAPERR,0                                                        
         BNE   SPTPS50                                                          
         MVC   SPTPSSTA(4),STAPQSTA                                             
         MVI   SPTPSSTA+4,C'/'                                                  
         MVC   SPTPSSTA+5(3),STAPQNET                                           
*                                                                               
SPTPS50  DS    0H                  INCREMENT ELCOUNT AND GET OUT                
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
SPTPSX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPSXNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPST - PATTERN MARKET GROUPS                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPMG   NMOD1 WORKL,*SPTPMG*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPKD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPGXNO                                                         
*&&                                                                             
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        PATTERN LIST ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPGXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATLSTEL,R6                                                      
         CLI   PATLSTTY,C'G'       TYPE=MARKET GROUPS?                          
         BNE   SPTPGXNO                                                         
         DROP  R6                                                               
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPKLEN,SPTPKLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPKLEN(2),=AL2(SPTPKDL)                                        
         MVC   SPTPKTYP,SPPKQ                                                   
*                                                                               
         MVC   SPTPKAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPKID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPKID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPKID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPG10                                                          
*                                                                               
         MVI   SPTPKACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPGX                                                           
*                                                                               
SPTPG10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPKACT,C'A'                                                    
*                                                                               
         LLC   R7,PATLSTLN                                                      
         AR    R7,R6               R7 -> END OF PATTERN LIST ELEMENT            
*                                                                               
         LLC   R9,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         MHI   R9,L'PATLST         INDEX TO IT WITHIN THE ELEMENT               
         LA    R9,PATLST(R9)                                                    
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPGXNO            YES - DONE WITH THIS PATTERN                 
*                                                                               
         LARL  RE,SPMGRTAB                                                      
         LHI   RF,(SPMGRTBX-SPMGRTAB)/3                                         
*                                                                               
         CLC   0(1,R9),2(RE)                                                    
         BE    SPTPG20                                                          
         LA    RE,3(RE)                                                         
         BCT   RF,*-14                                                          
         B     SPTPG50                                                          
*                                                                               
SPTPG20  DS    0H                                                               
         LLC   R0,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         AHI   R0,1                                                             
         EDIT  (R0),(3,SPTPKSEQ),FILL=0,ALIGN=LEFT                              
*                                                                               
         MVC   SPTPKMKG(2),0(RE)   MOVE IN LETTER OF MARKET GROUP               
         UNPK  DUB(5),1(3,R9)                                                   
         CLI   SPTPKMKG+1,C' '     SEE IF SECOND CHAR IS BLANK                  
         BH    *+14                                                             
         MVC   SPTPKMKG+1(4),DUB                                                
         B     SPTPG50                                                          
         MVC   SPTPKMKG+2(4),DUB                                                
*                                                                               
SPTPG50  DS    0H                  INCREMENT ELCOUNT AND GET OUT                
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
SPTPGX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPGXNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPMA - PATTERN AFFILIATES                                                   
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPMA   NMOD1 WORKL,*SPTPMA*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPAD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPAXNO                                                         
*&&                                                                             
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        PATTERN LIST ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPAXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATLSTEL,R6                                                      
         CLI   PATLSTTY,C'A'       TYPE=AFFILIATES?                             
         BNE   SPTPAXNO                                                         
         DROP  R6                                                               
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPALEN,SPTPALEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPALEN(2),=AL2(SPTPADL)                                        
         MVC   SPTPATYP,SPPAQ                                                   
*                                                                               
         MVC   SPTPAAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPAID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPAID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPAID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPA10                                                          
*                                                                               
         MVI   SPTPAACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPAX                                                           
*                                                                               
SPTPA10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPAACT,C'A'                                                    
*                                                                               
         LLC   R7,PATLSTLN                                                      
         AR    R7,R6               R7 -> END OF PATTERN LIST ELEMENT            
*                                                                               
         LLC   R9,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         MHI   R9,L'PATLST         INDEX TO IT WITHIN THE ELEMENT               
         LA    R9,PATLST(R9)                                                    
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPAXNO            YES - DONE WITH THIS PATTERN                 
*                                                                               
         LLC   R0,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         AHI   R0,1                                                             
         EDIT  (R0),(3,SPTPASEQ),FILL=0,ALIGN=LEFT                              
*                                                                               
         MVC   SPTPAAFF,0(R9)                                                   
*                                                                               
SPTPA50  DS    0H                  INCREMENT ELCOUNT AND GET OUT                
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
SPTPAX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPAXNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPMA - PATTERN STATION TYPES                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPTP   NMOD1 WORKL,*SPTPTP*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPTD,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPTXNO                                                         
*&&                                                                             
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        PATTERN LIST ELEMENT                         
         BRAS  RE,GETEL                                                         
         JNE   SPTPTXNO            NOT THERE - DONE WITH THIS PATTERN           
*                                                                               
         USING PATLSTEL,R6                                                      
         CLI   PATLSTTY,C'T'       TYPE=STATION TYPES?                          
         BNE   SPTPTXNO                                                         
         DROP  R6                                                               
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPTLEN,SPTPTLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPTLEN(2),=AL2(SPTPTDL)                                        
         MVC   SPTPTTYP,SPPTQ                                                   
*                                                                               
         MVC   SPTPTAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPTID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPTID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPTID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPT10                                                          
*                                                                               
         MVI   SPTPTACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPTX                                                           
*                                                                               
SPTPT10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPTACT,C'A'                                                    
*                                                                               
         LLC   R7,PATLSTLN                                                      
         AR    R7,R6               R7 -> END OF PATTERN LIST ELEMENT            
*                                                                               
         LLC   R9,ELCOUNT          PATTERN WE'RE ON RIGHT NOW                   
         MHI   R9,L'PATLST         INDEX TO IT WITHIN THE ELEMENT               
         LA    R9,PATLST(R9)                                                    
         CR    R9,R7               PAST END OF ELEMENT?                         
         BNL   SPTPTXNO            YES - DONE WITH THIS PATTERN                 
*                                                                               
         MVC   SPTPTSTP,0(R9)                                                   
*                                                                               
SPTPT50  DS    0H                  INCREMENT ELCOUNT AND GET OUT                
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
SPTPTX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPTXNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTPCN - PATTERN COMMENTS                                                     
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTPCN   NMOD1 WORKL,*SPTPCN*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTPND,R2           R2=A(EXTRACT RECORD)                         
         USING PATRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
*&&DO                                                                           
         ICM   R0,7,PATKREF                                                     
         SRDL  R0,10                                                            
         SRL   R1,22                                                            
         X     R1,=XL4'000003FF'                                                
         CHI   R1,X'0001'                                                       
         BNE   SPTPCNNO                                                         
*&&                                                                             
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         XC    SPTPNLEN,SPTPNLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTPNLEN(2),=AL2(SPTPNDL)                                        
         MVC   SPTPNTYP,SPPNQ                                                   
*                                                                               
         MVC   SPTPNAGY,SXDTAGY                                                 
*                                                                               
         MVC   SPTPNID(2),=C'0X'  START OF HEX KEY                              
         NI    SPTPNID+1,X'FF'-X'40' LOWERCASE THE 'X'                          
         GOTO1 =V(HEXOUT),DMCB,PATKEY,SPTPNID+2,L'PATKEY                        
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTPN10                                                          
*                                                                               
         MVI   SPTPNACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTPNX                                                           
*                                                                               
SPTPN10  DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTPNACT,C'A'                                                    
*                                                                               
         XR    R7,R7               LOCAL ELEMENT COUNT                          
         LR    R6,R3                                                            
         MVI   ELCODE,X'40'        COMMENT ELEMENT                              
         BRAS  RE,GETEL                                                         
*                                                                               
SPTPN20  DS    0H                                                               
         BE    *+16                                                             
         MVI   ELCOUNT,0             RE-SET ELEMENT COUN                        
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
         CLM   R7,1,ELCOUNT                                                     
         BL    SPTPN30                                                          
*                                                                               
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
         USING PATCMTEL,R6                                                      
*                                                                               
         EDIT  PATCMTNO,SPTPNNUM,ALIGN=LEFT,FILL=0                              
         LLC   RE,PATCMTLN                                                      
         AHI   RE,-4                                                            
         LTR   RE,RE                                                            
         BNP   SPTPNX                                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SPTPNCOM(0),PATCMT                                               
*                                                                               
         B     SPTPNX                                                           
*                                                                               
SPTPN30  DS    0H                                                               
         AHI   R7,1                                                             
         BRAS  RE,NEXTEL                                                        
         B     SPTPN20                                                          
*                                                                               
SPTPNX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTPCNNO DS    0H                                                               
         NI    FLAGS,X'FF'-NORDSEQ                                              
         NI    FLAGS,X'FF'-MULTIQ                                               
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         J     NEQXIT                                                           
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSTP - STEXT PAGE DEFINITION                                                
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTSTP   NMOD1 WORKL,*SPTSTP*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         USING SPTDPD,R2           R2=A(EXTRACT RECORD)                         
         USING DTXRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTDPLEN,SPTDPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTDPLEN(2),=AL2(SPTDPDL)                                        
         MVC   SPTDPTYP,SPTPQ                                                   
*                                                                               
         MVC   SPTDPAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,DTXKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTDPMED,BYTE                                                    
*                                                                               
         MVI   BYTE,X'00'                                                       
         CLI   CPROF7,C'Y'                                                      
         BNE   *+8                                                              
         MVI   BYTE,C'Y'                                                        
*                                                                               
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,DTXKCLT),SPTDPCNT                                
*                                                                               
         MVC   SPTDPDSC,DTXKDESC                                                
*                                                                               
         LLC   RE,DTXKTYP                                                       
         SHI   RE,C'L'-1                                                        
*                                                                               
         EDIT  (RE),(3,SPTDPPAG),ALIGN=LEFT,FILL=0                              
*                                                                               
         LR    R6,R3               A(RECORD)                                    
         MVI   ELCODE,X'20'        TITLE ELEMENT                                
         BRAS  RE,GETEL                                                         
         JNE   SPTDP80                                                          
*                                                                               
         USING DTXTLEEL,R6                                                      
         LLC   RF,DTXTLELN                                                      
         AHI   RF,-3                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTDPTTL(0),DTXTITLE                                             
         DROP  R6                                                               
*                                                                               
SPTDP80  DS    0H                                                               
*                                                                               
SPTDPX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTDPXNO DS    0H                                                               
         OI    FLAGS2,NOWRITEQ                                                  
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* SPTSTL - STEXT TEXT LINE                                                      
*                                                                     *         
* AL4 A(EXTRACT RECORD)                                               *         
* AL4 A(ACCFILE RECORD)                                               *         
* AL4 0=REPFILE-TO-EXTRACT                                            *         
* AL4 A(SXDTABD)                                                      *         
* AL4 A(MEDPARAM)                                                     *         
***********************************************************************         
*                                                                               
SPTSTL   NMOD1 WORKL,*SPTSTL*,CLEAR=Y                                           
         BRAS  RE,COMSETUP                                                      
*                                                                               
         OI    FLAGS,NORDSEQ                                                    
*                                                                               
         USING SPTLND,R2           R2=A(EXTRACT RECORD)                         
         USING DTXRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
         XC    SPTLNLEN,SPTLNLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SPTLNLEN(2),=AL2(SPTLNDL)                                        
         MVC   SPTLNTYP,SPTLQ                                                   
*                                                                               
         MVC   SPTLNAGY,SXDTAGY                                                 
*                                                                               
         MVC   BYTE,DTXKAM                                                      
         BRAS  RE,GETMED                                                        
         MVC   SPTLNMED,BYTE                                                    
*                                                                               
         MVI   BYTE,X'00'                                                       
         CLI   CPROF7,C'Y'                                                      
         BNE   *+8                                                              
         MVI   BYTE,C'Y'                                                        
*                                                                               
         L     RF,ACLUNPK                                                       
         GOTO1 (RF),DMCB,(BYTE,DTXKCLT),SPTLNCNT                                
*                                                                               
         MVC   SPTLNDSC,DTXKDESC                                                
*                                                                               
         LLC   RE,DTXKTYP                                                       
         SHI   RE,C'L'-1                                                        
*                                                                               
         EDIT  (RE),(3,SPTLNPAG),ALIGN=LEFT,FILL=0                              
*                                                                               
         TM    FLAGS,KILLQ                                                      
         BZ    SPTSTL10                                                         
*                                                                               
         MVI   SPTLNACT,C'K'                                                    
         NI    FLAGS,X'FF'-KILLQ                                                
         NI    FLAGS,X'FF'-NORDSEQ                                              
         MVI   ELCOUNT,0           RE-SET ELEMENT COUNT                         
         B     SPTLNX                                                           
*                                                                               
SPTSTL10 DS    0H                                                               
         TM    FLAGS,MULTIQ                                                     
         BZ    *+8                                                              
         MVI   SPTLNACT,C'A'                                                    
*                                                                               
         XR    R7,R7               LOCAL ELEMENT COUNT                          
         LR    R6,R3                                                            
         MVI   ELCODE,X'40'        TEXT ELEMENT                                 
         BRAS  RE,GETEL                                                         
*                                                                               
SPTLN20  DS    0H                                                               
         BE    *+16                                                             
         MVI   ELCOUNT,0             RE-SET ELEMENT COUN                        
         NI    FLAGS,X'FF'-NORDSEQ-MULTIQ                                       
         J     NEQXIT                                                           
*                                                                               
         CLM   R7,1,ELCOUNT                                                     
         BL    SPTLN30                                                          
*                                                                               
         LLC   R7,ELCOUNT                                                       
         AHI   R7,1                                                             
         STC   R7,ELCOUNT                                                       
*                                                                               
         USING DTXTXTEL,R6                                                      
         EDIT  (B1,DTXLNNUM),(3,SPTLNLNN),ALIGN=LEFT,FILL=0                     
         LLC   RF,DTXTXTLN                                                      
         AHI   RF,-4                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SPTLNLTX(0),DTXTXT                                               
         DROP  R6                                                               
*                                                                               
         B     SPTLNX                                                           
*                                                                               
SPTLN30  DS    0H                                                               
         AHI   R7,1                                                             
         BRAS  RE,NEXTEL                                                        
         B     SPTLN20                                                          
*                                                                               
SPTLN80  DS    0H                                                               
*                                                                               
SPTLNX   DS    0H                                                               
         BRAS  RE,DELIMS                                                        
         J     EQXIT                                                            
*                                                                               
SPTLNXNO DS    0H                                                               
         OI    FLAGS2,NOWRITEQ                                                  
         J     EQXIT                                                            
*                                                                               
         LTORG                                                                  
         DROP  R2,R3                                                            
*                                                                               
*                                                                               
*                                                                               
* CONVERTS DATCON TYPE 20 (YYYYMMDD) DATE TO                                    
* BLOCKCHAIN DDMMYYYY FORMAT                                                    
* P1 = A(INPUT DATE)                                                            
* P2 = A(OUTPUT DATE)                                                           
TRNDAT   NTR1  BASE=*,LABEL=*                                                   
         L     R2,4(R1)            A(OUTPUT)                                    
         L     R1,0(R1)            A(INPUT)                                     
*                                                                               
         MVC   0(2,R2),6(R1)       MOVE DD                                      
         MVC   2(2,R2),4(R1)       MOVE MM                                      
         MVC   4(4,R2),0(R1)       MOVE YYYY                                    
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
* STRING CONCATENATION                                                          
*                                                                               
* P1 - LIST OF STRINGS                                                          
*      LIST ENTRY FORMAT:                                                       
*          XL4 - LENGTH OF STRING                                               
*          XL4 - A(STRING)                                                      
*          LIST TERMINATED BY X'FFFFFFFF'                                       
* P2 - A(OUTPUT AREA) - BOUNDARY NOT CHECKED                                    
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
CONCAT   NTR1  BASE=*,LABEL=*                                                   
         L     R9,0(R1)           STRING LIST POINTER                           
         L     R5,4(R1)           A(OUTPUT AREA)                                
*                                                                               
         LTR   R5,R5                                                            
         JZ    EQXIT              NO OUTPUT AREA ADDRESS                        
*                                                                               
CONC10   CLC   =X'FFFFFFFF',0(R9) END OF STRING LIST?                           
         JE    EQXIT                                                            
*                                                                               
         MVC   DMCB(4),0(R9)                                                    
         MVC   DMCB+4(4),4(R9)                                                  
         GOTO1 =A(SQUEEZE),DMCB                                                 
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         ICM   RE,15,DMCB+4           START OF NEW STRING                       
         ICM   RF,15,DMCB             LENGTH OF STRING TO MOVE                  
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R5),0(RE)                                                    
*                                                                               
         AR    R5,RF                                                            
         AHI   R5,1                TO COMPENSATE FOR BCTR                       
*        MVI   0(R5),C' '          LEAVE A SPACE                                
*        AHI   R5,1                                                             
*                                                                               
CONC90   DS    0H                                                               
         LA    R9,8(R9)                                                         
         B     CONC10                                                           
*                                                                               
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
* SQUEEZE LEADING AND TRAILING WHITESPACES OUT OF THE STRING                    
*                                                                               
* P1 - MAX STRING LENGTH                                                        
* P2 - A(STRING)                                                                
*                                                                               
* ON EXIT                                                                       
* P1 - NEW STRING LENGTH                                                        
* P2 - A(NON-WHITESPACE STRING START)                                           
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *           
SQUEEZE  NTR1  BASE=*,LABEL=*                                                   
         LR    R9,R1              SAVE PARAMETERS                               
*                                                                               
         ICM   R2,15,4(R1)        A(STRING)                                     
         JZ    NEQXIT                                                           
         ICM   R3,15,0(R1)        L'STRING                                      
         JZ    NEQXIT                                                           
*                                                                               
* ELIMINATE LEADING SPACES                                                      
*                                                                               
         LR    RE,R2              ADDRESS OF STRING                             
         LR    R0,R2              ADDRESS OF STRING                             
         AR    R0,R3              R0 -> 1 BYTE PAST STRING'S END                
*                                                                               
SQZE20   DS    0H                                                               
         CR    RE,R0               WENT PAST END OF STRING?                     
         BNL   SQZE90              STRING CONSISTS OF ALL SPACES                
*                                                                               
         CLI   0(RE),C' '                                                       
         BH    SQZE30              FOUND FIRST NON-SPACE CHARACTER              
         LA    RE,1(RE)                                                         
         B     SQZE20                                                           
*                                                                               
SQZE30   DS    0H                  RE POINTS TO FIRST NON-SPACE CHAR            
*                                                                               
* NOW ELIMINATE TRAILING SPACES                                                 
*                                                                               
         LR    RF,R2               ADDRESS OF STRING                            
         AR    RF,R3               PLUS MAX LENGTH                              
         BCTR  RF,0                RF -> LAST CHARACTER OF THE STRING           
         LR    R0,R2               ADDRESS OF STRING                            
         BCTR  R0,0                R0 -> ONE CHAR BEFORE STRING START           
*                                                                               
SQZE40   DS    0H                                                               
         CR    RF,R0                                                            
         JH    *+6                 STRING CONSISTS OF ALL SPACES                
         DC    H'0'                THIS SHOULD HAVE BEEN CAUGHT EARLIER         
*                                                                               
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,SQZE40                                                        
*                                                                               
* AT THIS POINT RF POINTS TO LAST NON-SPACE CHARACTER OF THE STRING             
* CALCULATE RESULTING STRING LENGTH, UPDATE RETURN PARMS AND EXIT               
*                                                                               
         SR    RF,RE               RF = L'STRING TO - 1                         
         AHI   RF,1                                                             
         LTR   RF,RF                                                            
         BP    *+6                                                              
         DC    H'0'        STRING LENGTH NEGATIVE, SOMETHING WENT WRONG         
*                                                                               
         ST    RF,0(R9)            STORE LENGTH OF NEW STRING                   
         ST    RE,4(R9)            START OF NEW STRING                          
         J     EQXIT                                                            
*                                                                               
* HAVE ALL-SPACES STRING HERE                                                   
*                                                                               
SQZE90   DS    0H                                                               
         XC    0(4,R9),0(R9)       XC RESULTING STRING LENGTH                   
         J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
*============================================================                   
* BUILD TABLE OF 0D29 RECORDS IN 31-BIT STORAGE                                 
* AND SET BINSRCH PARMS                                                         
* TO TRANSLATE NON-TRADITIONAL DEMO NAMES TO THEIR SEQNUMS                      
*============================================================                   
*                                                                               
BLDD29   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LY    R0,D29TABL                                                       
         STORAGE OBTAIN,LENGTH=(R0),LOC=31,COND=YES                             
         LTR   RF,RF                                                            
         JNZ   *+2                                                              
         ST    R1,D29ATAB          SET TABLE ADDRESS                            
*                                                                               
         LR    R4,R1                GET ADDRESS OF TABLE                        
         LHI   R5,D29TABMAX                                                     
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING NTDRECD,R6                                                       
         MVI   NTDKTYP,NTDKTYPQ    X'0D'                                        
         MVI   NTDKSUB,NTDKSUBQ    X'29'                                        
         MVI   NTDKRTGSV,C'C'      SET FOR COMSCORE                             
*                                                                               
* NEED TO READ DEMOS FOR ALL AGENCIES, IF WE'RE RUNNING                         
* UPDATES FOR ALL AGENCIES ON FILE                                              
*        MVC   NTDKAGMD,1(R3)      MOVE A-M                                     
*        NI    NTDKAGMD,X'F0'      ISOLATE AGENCY NIBBLE                        
*        OI    NTDKAGMD,X'01'      FORCE MEDIA "T"                              
         MVC   KEYSAVE,KEY                                                      
*                                                                               
         L     RF,COMFACS                                                       
         L     RF,0(RF)            A(DATAMGR)                                   
         GOTO1 (RF),DMCB,=CL7'DMRDHI',=CL7'SPTDIR',KEYSAVE,KEY                  
         J     BLDD29B                                                          
*                                                                               
BLDD29A  GOTO1 (RF),(R1),=CL7'DMRSEQ'                                           
*                                                                               
BLDD29B  DS    0H                                                               
         CLC   KEY(NTDKAGMD-NTDRECD),KEYSAVE REC TYP,SUBTYP,COMSCORE            
*        CLC   KEY(4),KEYSAVE                                                   
         JNE   BLDD29X                                                          
*                                                                               
         USING D29RECD,R4                                                       
*                                                                               
         SAM31                                                                  
         MVC   D29AM,NTDKAGMD      A/M                                          
         NI    D29AM,X'F0'         TURN OFF MEDIA                               
         MVC   D29DEMNM,NTDKALPH   MOVE ALPHA DEMO CODE                         
         MVC   D29DEMSQ,NTDKSEQ    DEMO SEQUENCE NUMBER                         
         LA    R4,D29RECL(R4)                                                   
         SAM24                                                                  
         JCT   R5,BLDD29A                                                       
         DC    H'0'                TABLE FULL!                                  
*                                                                               
BLDD29X  LHI   R0,D29TABMAX                                                     
         SR    R0,R5                                                            
         ST    R0,D29COUNT                                                      
         J     EXIT                                                             
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* DISPLAY THE TIME IN THE SPECIFIED DISPLAY FIELD                               
*                                                                               
* ON ENTRY:    PARAM 1             A(2 BYTE TIME)                               
*              PARAM 2             A(TIME FIELD)                                
***********************************************************************         
SHOWTIME NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
*                                                                               
         MVC   0(8,R3),=C'00:00:00'                                             
         GOTO1 =V(HEXOUT),DMCB,0(R2),WORK,L'DOSTTIME                            
*                                                                               
         CLC   =C'23',WORK                                                      
         JL    EQXIT                                                            
         MVC   0(2,R3),WORK                                                     
*                                                                               
         CLC   =C'59',WORK+2                                                    
         JL    EQXIT                                                            
         MVI   2(R3),C':'                                                       
         MVC   3(2,R3),WORK+2                                                   
STIMX    J     EQXIT                                                            
         LTORG                                                                  
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
* LITERALS AND CONSTANTS REQUIRED FOR ALL ROUTINES                    *         
***********************************************************************         
*                                                                               
LITERAL  DS    0D                                                               
DATCON   DC    V(DATCON)                                                        
COMFACS  DC    V(COMFACS)                                                       
ACLUNPK  DC    V(CLUNPK)                                                        
ADXBLOCK DC    A(0)                                                             
DATADISP DC    X'0000'                                                          
SVDTDISP DC    X'0000'                                                          
SPACES   DC    CL132' '                                                         
MGFLAG   DC    C'N'                                                             
*MXTRT                                                                          
       ++INCLUDE MXTRT                                                          
       ++INCLUDE SPXRECID                                                       
         LTORG                                                                  
*                                                                               
D29TABMAX EQU  32000                                                            
D29TABL  DC    A(D29TABMAX*D29RECL*15) 15 AGENCIES PER FILE                     
*                                                                               
D29BSPARS DS   0H                                                               
D29PAR1  DC    A(0)                                                             
D29PAR2  DC    A(0)                A(TABLE)                                     
D29ATAB  EQU   D29PAR2                                                          
D29PAR3  DC    F'0'                RECORD COUNT                                 
D29COUNT EQU   D29PAR3                                                          
D29PAR4  DC    A(D29RECL)          RECORD LENGTH                                
D29PAR5  DC    AL1(0),AL3(D29DEMSQ-D29RECD) KEYDSPL/KEYL                        
D29PAR6  DC    A(D29TABMAX)                                                     
*                                                                               
D29RECD  DSECT                                                                  
D29AM    DS    X                   AGENCY/MEDIA                                 
D29DEMNM DS    CL5                 ALPHA DEMO CODE                              
D29DEMSQ DS    XL2                 DEMO SEQ. NUMBER                             
D29RECL  EQU   *-D29RECD                                                        
*                                                                               
***********************************************************************         
* WORKING STORAGE REQUIREMENTS                                        *         
***********************************************************************         
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
*                                                                               
DMCB     DS    0CL24               PARAMETER LIST                               
DM1      DS    F                                                                
DM2      DS    F                                                                
DM3      DS    F                                                                
DM4      DS    F                                                                
DM5      DS    F                                                                
DM6      DS    F                                                                
*                                                                               
SVDMCB   DS    XL24                                                             
HEXKEY   DS    XL13                                                             
*                                                                               
FULL     DS    F                                                                
SVR6     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
ELCODE   DS    X                                                                
DEMOMOD  DS    C                                                                
DEMONUM  DS    H                                                                
STAKNEW  DS    CL15                                                             
KEY      DS    XL48                                                             
KEYSAVE  DS    XL48                                                             
SVSECAGY DS    CL2                                                              
SVBDTE   DS    CL2                                                              
SVBSTAT2 DS    X              BILL HEADER'S 2ND STATUS BYTE (FOR COS2)          
SVD29    DS    XL(D29DEMSQ-D29RECD)                                             
*                                                                               
PROFDAR  DS    XL16                                                             
PROFT0   DS    XL16                                                             
*                                                                               
         DS    D                                                                
WORK     DS    XL256                                                            
P16      DS    PL16                                                             
         DS    XL024               DBLOCK IS BUILT AT WORK+10 FOR 280           
STAIO    DS    XL4096                                                           
*                                                                               
WORKL    EQU   *-WORKD                                                          
*                                                                               
*                                                                               
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
*SPXUSERD                                                                       
       ++INCLUDE RXUSERD                                                        
*SPXRECD                                                                        
       ++INCLUDE SPXRECD                                                        
       ++INCLUDE SPGENAGY                                                       
       ++INCLUDE SPGENCLT                                                       
       ++INCLUDE SPGENPRD                                                       
       ++INCLUDE SPGENEST                                                       
       ++INCLUDE SPGENMKT                                                       
       ++INCLUDE SPGENSTA                                                       
       ++INCLUDE SPGENADD                                                       
       ++INCLUDE SPTRCMML                                                       
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE CTGENRAD                                                       
       ++INCLUDE SPGENMKG                                                       
       ++INCLUDE SPGENMKA                                                       
       ++INCLUDE SPGENGRP                                                       
       ++INCLUDE SPGENPRG                                                       
       ++INCLUDE SPGENDEAL                                                      
       ++INCLUDE DDACTIVD                                                       
       ++INCLUDE SPSTAPACKD                                                     
       ++INCLUDE SPGENREP                                                       
       ++INCLUDE SPGENUCOM                                                      
       ++INCLUDE SPGENXLK                                                       
       ++INCLUDE SPGENDAYPT                                                     
*DXDSECTS                                                                       
       ++INCLUDE DXDSECTS                                                       
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE FASYSFAC                                                       
       ++INCLUDE FAFACTS                                                        
       ++INCLUDE FACTRYEQUS                                                     
CMMNT    DSECT                                                                  
       ++INCLUDE PCOMELEM                                                       
*                                                                               
       ++INCLUDE DDOFFICED                                                      
*                                                                               
       ++INCLUDE SPGENEQU                                                       
*                                                                               
       ++INCLUDE SPXBTABD                                                       
       ++INCLUDE SPXPARM                                                        
       ++INCLUDE DDCTRYEQUS                                                     
       ++INCLUDE SPADBUYER                                                      
       ++INCLUDE SPGENNTDEM                                                     
       ++INCLUDE SPGENDRORD                                                     
       ++INCLUDE SPGENDRMKN                                                     
*                                                                               
*PREFIX=SW$                                                                     
       ++INCLUDE SPREPWORKD                                                     
*PREFIX=                                                                        
       ++INCLUDE SPXREQD                                                        
       ++INCLUDE SPGENBUY                                                       
*                                                                               
BILLRECD DSECT                                                                  
       ++INCLUDE SPGENBILL                                                      
       ++INCLUDE SPGENSTAB                                                      
*                                                                               
       ++INCLUDE AXTRBDST                                                       
*                                                                               
       ++INCLUDE SPGENDRFLT                                                     
*                                                                               
*PREFIX=RC$                                                                     
       ++INCLUDE DMRCVRHDR                                                      
*PREFIX=                                                                        
*                                                                               
       ++INCLUDE SPGENSDEF                                                      
       ++INCLUDE SPGENSQAD                                                      
       ++INCLUDE SPGENDMN                                                       
       ++INCLUDE SPGENBFML                                                      
       ++INCLUDE SPTRPAT                                                        
       ++INCLUDE SPTRDTXT                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'217SPXROUTS  02/24/21'                                      
         END                                                                    
