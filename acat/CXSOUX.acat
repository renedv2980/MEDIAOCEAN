*          DATA SET CXSOUX     AT LEVEL 010 AS OF 05/01/02                      
*CATALP CXSOUX                                                                  
         TITLE 'CXSOUX - CONTROL SECURITY - OCONTROL USERID RECORDS'            
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(SQL BUFFER)                                                  *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSOUX   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSOUX*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         XC    0(4,R3),0(R3)                                                    
         LA    R3,4(R3)            BUMP PAST LENGTH IN BUFFER                   
         USING CXSOUD,R2           R2=A(EXTRACT RECORD)                         
         MVI   SEPARATE,DEFSEP     SET DEFAULT FIELD SEPARATOR CHAR             
         MVI   XFLAG,0             INITIALISE CONVERT INPUT FLAG                
         CLI   CXSOUACT,C'L'       TEST IF IN LOAD MODE                         
         BNE   *+12                                                             
         MVI   SEPARATE,LOADSEP    SET LOAD MODE FIELD SEPARATOR CHAR           
         OI    XFLAG,LOADMODE      SET CONVERT FLAG TO LOAD MODE                
*                                                                               
OUTYP    NI    XFLAG,REQUIRED      CONVERT RECORD TYPE - REQUIRED               
         GOTO1 XMXCNVX,DMCB,(L'CXSOUTYP,CXSOUTYP),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUACT    NI    XFLAG,REQUIRED      CONVERT RECORD ACTION - REQUIRED             
         GOTO1 XMXCNVX,DMCB,(L'CXSOUACT,CXSOUACT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUDTM    NI    XFLAG,REQUIRED      CONVERT ACTION DATE TIME - REQUIRED          
         LA    R0,L'CXSOUDAT+L'CXSOUTIM+1                                       
         GOTO1 XMXCNVX,DMCB,((R0),CXSOUDAT),(XFLAG,(R3)),(1,DTIM)               
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUAID    NI    XFLAG,REQUIRED      AGENCY ALPHA ID - REQUIRED                   
         GOTO1 XMXCNVX,DMCB,(L'CXSOUAID,CXSOUAID),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUUIN    NI    XFLAG,REQUIRED      USER ID NUMBER - REQUIRED                    
         GOTO1 XMXCNVX,DMCB,(L'CXSOUUIN,CXSOUUIN),(XFLAG,(R3)),(1,INT)          
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUSYS    NI    XFLAG,REQUIRED      SYSTEM CODE - REQUIRED                       
         GOTO1 XMXCNVX,DMCB,(L'CXSOUSYS,CXSOUSYS),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUPGM    NI    XFLAG,REQUIRED      PROGRAM CODE - REQUIRED                      
         GOTO1 XMXCNVX,DMCB,(L'CXSOUPGM,CXSOUPGM),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUOPT    NI    XFLAG,REQUIRED      OPTION CODE - REQUIRED                       
         CLI   CXSOUACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSOUOPT,CXSOUOPT),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
OUCOD    NI    XFLAG,REQUIRED      CONTROL CODE - REQUIRED                      
         CLI   CXSOUACT,C'K'       TEST IF SPECIAL DELETE KEY ACTION            
         BNE   *+8                                                              
         OI    XFLAG,OPTIONAL      SET TO OPTIONAL TO OUTPUT NULL VALUE         
         GOTO1 XMXCNVX,DMCB,(L'CXSOUCOD,CXSOUCOD),(XFLAG,(R3)),(1,CHAR)         
         BAS   RE,PUTSEP           OUTPUT SEPARATOR                             
*                                                                               
DXBUFLEN L     RF,ASQLBUFF         STORE BUFFER DATA LENGTH                     
         SR    R3,RF               IN TOP 2 BYTES                               
         SLL   R3,16                                                            
         STCM  R3,15,0(RF)                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
PUTSEP   SR    RF,RF               PUT OUT SEPARATOR CHARACTER                  
         ICM   RF,1,4(R1)          GET RETURN LENGTH                            
         LA    R3,0(RF,R3)         BUMP DATA BUFFER POINTER                     
         MVC   0(1,R3),SEPARATE                                                 
         LA    R3,1(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
XMXCNVX  DC    V(MXCNVX)                                                        
         SPACE 1                                                                
CHAR     DC    CL8'CHAR    '                                                    
DTIM     DC    CL8'DATETIME'                                                    
TINY     DC    CL8'TINYINT '                                                    
SMAL     DC    CL8'SMALLINT'                                                    
INT      DC    CL8'INT     '                                                    
REAL     DC    CL8'REAL    '                                                    
FLOT     DC    CL8'FLOAT   '                                                    
BIT      DC    CL8'BIT     '                                                    
MONY     DC    CL8'MONEY   '                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
ASQLBUFF DS    A                   A(SQL BUFFER)                                
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
DMCB     DS    6F                                                               
SEPARATE DS    CL1                 FIELD SEPARATOR CHARACTER                    
DEFSEP   EQU   C';'                                                             
LOADSEP  EQU   C';'                                                             
XFLAG    DS    XL1                 CONVERT INPUT CONTROL FLAGS                  
LOADMODE EQU   X'80'                 LODE MODE                                  
OPTIONAL EQU   X'40'                 OPTIONAL FIELD                             
REQUIRED EQU   X'FF'-X'40'           REQUIRED FIELD                             
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSOUD                                                         
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010CXSOUX    05/01/02'                                      
         END                                                                    
