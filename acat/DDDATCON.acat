*          DATA SET DDDATCON   AT LEVEL 004 AS OF 10/17/18                      
*CATALP DATCON                                                                  
***********************************************************************         
* AL1 INPUT DATE TYPE  - PLUS CONTROL INFO IN HIGH ORDER FOUR BITS    *         
* AL3 A(INPUT DATE)                                                   *         
*                                                                     *         
* AL1 OUTPUT DATE TYPE - SET TO OUTPUT LEN IF X'80' IN PARAM1(1)      *         
* AL3 A(OUTPUT DATE)                                                  *         
*                                                                     *         
* AL1 DATE MODIFIER    - IF PARAM1(1) HAS X'30' BITS ON               *         
* AL3 A(SECOND INPUT DATE IF FIRST PARAM1(1) HAS X'20' BIT ON)        *         
*                                                                     *         
* XL4 DAYS/MONTHS/YEARS TO ADD/SUBTACT IF NEEDED BY DATE MODIFIER     *         
*                                                                     *         
* IF PARAM1(1)=X'80' PASS BACK OUTPUT FIELD LENGTH IN PARAM2(1)       *         
* IF PARAM1(1)=X'40' SET ALPHA MONTH DATA TO LOWER CASE               *         
* IF PARAM1(1)=X'20' SECOND DATE IS POINTED TO BY PARAM3              *         
* IF PARAM1(1)=X'10' SECOND DATE FOLLOWS FIRST DATE IN PARAM1         *         
* IF PARAM1(1)=X'30' DATE MODIFIER PASSED IN PARAM3(1)                *         
*                                                                     *         
* IF PARAM2(1)=X'80' FORMAT PAIR OF DATES WITH PERIOD TEXT            *         
* IF PARAM2(1)=X'40' OUTPUT TYPE 21-22 ARE ACTIVE FOR YY LE 59        *         
* IF PARAM2(1)=X'20' DONT OUTPUT DDS INTERNAL DATES FOR 00-03         *         
*                                                                     *         
* AS AN AID TO CONVERSION DATCON WILL ACCEPT INPUT TYPE 0 DATES       *         
* C'YYMMDD' IN NON STANDARD DDS INTERNAL FORMAT.THE ASSUMPTION WILL BE*         
* THAT DATES C'00....' THRU C'59....' WILL BE TREATED AS 2000 THRU    *         
* 2059. OUTPUT WILL HOWEVER BE IN STANDARD DDS INTERNAL FORMAT UNLESS *         
* PARAM2(1) IS SET TO X'20'.                                          *         
* THIS IS ALSO TRUE FOR INPUT TYPE 4 DATES C'DD/MM/YY' OR C'MM/DD/YY' *         
***********************************************************************         
         TITLE 'DATCON - GENERALIZED DATE CONVERSION MODULE'                    
         PRINT NOGEN                                                            
DATCON   CSECT                                                                  
         NMOD1 DATX-DATD,**DTCN**,R7,RR=RA                                      
         USING DATD,RC                                                          
         SAM24                     SET 24-BIT MODE                              
         ST    RA,RELO                                                          
         LR    RA,R1               RA=A(PARAM LIST)                             
*                                                                               
         CTRY  ,                   R0=X'OOAACCLL'                               
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         L     R0,DAGCOPT                                                       
         ST    R0,AGCOPT           STORE AGCOPT/AGCTRY/CTRY/LANG CODES          
*                                                                               
         CLI   LANG,7              SET DEFAULT LANGUAGE IF INVALID              
         BNH   *+8                                                              
         MVI   LANG,0                                                           
         MVC   LINGO,LANG                                                       
*&&UK*&& MVI   DATYPE,DATYPEUK     SET ENGLISH DATE FORMAT                      
*&&US*&& MVI   DATYPE,DATYPEUS     SET AMERICAN DATE FORMAT                     
         MVI   CENTURY,20          SET DEFAULT CENTURY                          
*                                                                               
         MVC   INPFMT,0(RA)        SAVE INPUT FORMAT                            
         MVC   SVINPFMT,INPFMT                                                  
         NI    INPFMT,X'0F'        TURN OFF CONTROL BITS                        
         NI    SVINPFMT,X'F0'                                                   
         XC    SECS,SECS                                                        
*                                                                               
DC02     CLI   INPFMT,5            TEST TODAY REQUEST                           
         BE    DC03                                                             
         CLI   INPFMT,15           TEST BILLING DATE REQUEST                    
         BNE   DC04                                                             
*&&US*&& B     DC03                TODAY FOR BILLING DATE IN US                 
*                                                                               
DC02A    ICM   R1,15,=V(SSB)       MUST HAVE =V(SSB)                            
         JZ    *+2                                                              
         USING SSBD,R1                                                          
         CLC   SSBCNTL,=X'0000'    CHECK ONLINE OR OFFLINE                      
         BE    DC02B                                                            
*                                                                               
         LAM   AR2,AR2,SSBALET     ONLINE - GET DMGR ALET                       
         ICM   R1,15,SSBTKADR      FIND TASK                                    
         JZ    *+2                 MUST BE IN A TASK TO DO THIS                 
         USING TCBD,R1                                                          
         ICM   R1,15,TCBUTL        FIND UTL FROM TCB                            
         BNZ   DC02C                                                            
         DC    H'0'                MUST HAVE A UTL TOO                          
         DROP  R1                                                               
*                                                                               
DC02B    LAM   AR2,AR2,SSOALET-SSOOFF(R1)                                       
         OC    SSOALET-SSOOFF(4,R1),SSOALET-SSOOFF(R1)                          
         BNZ   DC02B2              GOT AN ALET THEN GOOD                        
*                                                                               
DC02B1   ICM   RF,15,=V(UTL)       MUST HAVE A UTL                              
         JZ    *+2                                                              
         XC    DMCB(24),DMCB       DMCB=DUB/DUB1/WORK                           
         MVI   DMCB+4,X'20'        ENQUIRE ON SYSTEM TO GET ALET                
         MVC   DMCB+7(1),4(RF)                                                  
         GOTO1 =V(DATAMGR),DMCB,=C'LOCKSP'                                      
         STAM  AR2,AR2,FULL                                                     
         OC    FULL,FULL                                                        
         BNZ   DC02B2                                                           
         GOTO1 =V(DATAMGR),DMCB,=C'LOCKSP',X'00120000'                          
*NOP*    WTO   'ALET AQUIRED BY DATCON',MCSFLAG=HRDCPY                          
         B     DC02B1                                                           
*                                                                               
DC02B2   ICM   R1,15,=V(UTL)       MUST HAVE A UTL                              
         JZ    *+2                                                              
*                                                                               
DC02C    PROT  OFF                 THIS DOES NOT USE R1                         
         SR    RF,RF                                                            
         ICM   RF,1,4(R1)          SET SE IN RF                                 
         BZ    DC02D                                                            
         SAC   512                                                              
         SLL   RF,6                SHIFT SENUM TO GET OFFSET TO DSPACE          
         LR    R2,RF                                                            
         ICM   R2,7,61(R2)                                                      
         JZ    *+2                 SYSTEM MUST BE DEFINED                       
*                                                                               
         USING DMSYSHDR,R2                                                      
         ICM   R1,15,DSYBDATE      GET BILLING DATE FROM DATASPACE              
         JZ    *+2                 BILLING DATE MUST NOT BE ZERO                
*                                                                               
         SAC   0                                                                
         DROP  R2                                                               
         XR    R0,R0               ZERO SECONDS FOR BILLING DATE                
         PROT  ON                                                               
         B     DC03A                                                            
*                                                                               
DC02D    SAC   0                                                                
         PROT  ON                                                               
*                                                                               
DC03     STAM  ARE,AR1,DUB         GET MACHINE DATE AND TIME NOW                
         TIME  TU                                                               
         LAM   ARE,AR1,DUB                                                      
*&&US                                                                           
         CLI   INPFMT,5            TEST TODAY REQUESTED                         
         BNE   DC03A                                                            
         CLC   1(3,RA),=X'000001'  TEST REAL DATE/TIME WANTED                   
         BNE   DC03A                                                            
         OI    SVINPFMT,X'01'      SET WANT REAL DATE/TIME                      
*&&                                                                             
DC03A    ST    R1,TODAY            TODAY=X'0CYYDDDF'                            
         SRDL  R0,32                                                            
         D     R0,=F'38400'                                                     
         ST    R1,TIMENOW          TIME NOW IN SECONDS                          
         ST    R1,SECS                                                          
         OC    TODAY(3),TODAY      DID MACRO FAIL?                              
         BZ    DC02                IF ZERO THEN YES, DO IT AGAIN.               
*&&US                                                                           
DC03B    TM    SVINPFMT,X'01'      TEST IF WANT REAL DATE/TIME                  
         BZ    DC03T                                                            
         C     R1,=F'64800'        TEST LPAR TIME TO 18:00:00                   
         BNL   DC03C                                                            
         AHI   R1,21600            IF BEFORE 18:00:00 ADD 6 HOURS               
         ST    R1,SECS                                                          
         B     DC03F                                                            
DC03C    S     R1,=F'64800'        IF AFTER 18:00:00 SUBTRACT 18 HOURS          
         ST    R1,SECS                                                          
         CP    TODAY+2(2),=P'365'                                               
         BE    DC03D                                                            
         BH    DC03E                                                            
         AP    TODAY+2(2),=P'1'    ADD 1 TO DAY IF NOT END OF YEAR              
         B     DC03F                                                            
DC03D    LLC   R1,TODAY+1          EXTRACT YEAR INTO DUB AS PL8                 
         SLL   R1,4                                                             
         XC    DUB(4),DUB                                                       
         ST    R1,DUB+4                                                         
         OI    DUB+7,X'0C'                                                      
         CVB   R1,DUB                                                           
         STC   R1,DUB              DUB(1) HAS YEAR IN BINARY                    
         TM    DUB,X'03'           TEST IF LEAP YEAR                            
         BNZ   DC03E                                                            
         AP    TODAY+2(2),=P'1'    YES-CAN ADD ONE TO DAY TO GIVE 366           
         B     DC03F                                                            
DC03E    ZAP   TODAY+2(2),=P'1'    SET DAY TO 1 AND ADD 1 TO YEAR               
         AP    TODAY+1(3),=P'1000'                                              
DC03F    OI    TODAY+3,X'0F'       ADJUST SIGN OF JULIAN REAL DATE              
         MVC   TIMENOW,SECS        RETURN REAL TIME AFTER REAL DATE             
*&&                                                                             
DC03T    LA    R0,TODAY            SET INPUT ADDRESS AND TYPE 6                 
         STCM  R0,7,1(RA)                                                       
         MVI   INPFMT,6                                                         
         TM    SVINPFMT,X'30'      IF THIRD PARAMETER LEAVE 10 BIT ON           
         BO    *+8                                                              
         NI    SVINPFMT,X'E0'      SET NO SECOND DATE AFTER FIRST               
         B     DC06                                                             
*                                                                               
DC04     CLI   INPFMT,7            TEST FOR DAYS/MONTHS LIST                    
         BE    DCD                                                              
*                                                                               
DC06     MVC   OUTFMT,4(RA)        SAVE OUTPUT FORMAT                           
         MVC   SVOUTFMT,OUTFMT                                                  
         NI    OUTFMT,X'1F'        TURN OFF CONTROL BITS                        
         NI    SVOUTFMT,X'E0'                                                   
*                                                                               
         SR    R2,R2               R2=A(INPUT AREA)                             
         ICM   R2,7,1(RA)                                                       
         SR    R3,R3               R3=A(OUTPUT AREA)                            
         ICM   R3,7,5(RA)                                                       
*                                                                               
         LA    RE,DATEINF1         SET PROCESSING FIRST DATE                    
         ST    RE,DATEINFA                                                      
         XC    DATEINFO,DATEINFO                                                
         TM    SVOUTFMT,X'80'      TEST IF CALLER WANTS PERIOD INFO             
         BZ    DC08                                                             
         LA    R3,ODAYS            YES-POINT TO INTERNAL INPUT AREA             
         MVC   0(21,R3),SPACES                                                  
*                                                                               
DC08     LLC   R4,INPFMT           R4=INPUT FORMAT                              
         SLL   R4,2                                                             
         LLC   R5,OUTFMT           R5=OUTPUT FORMAT                             
*                                                                               
         LA    RE,OUTADJ(R5)       TEST IF OUTPUT NEEDS ADJUSTING               
         CLI   0(RE),C'N'                                                       
         BE    DC14                DATE IS PURE NO ADJUSTMENT NEEDED            
*                                                                               
         CLI   DATYPE,DATYPEUS     AMERICAN DATES?                              
         BNE   *+12                                                             
         LA    R5,X'0B'            ALL AMERICAN OUTPUT DATES HERE               
         B     DC14                                                             
*                                                                               
         CLI   OUTFMT,X'0A'        TEST IPL FORMAT                              
         BE    DC14                                                             
         CLI   OUTFMT,X'0B'        TEST AMERICAN FORMAT                         
         BE    DC14                                                             
*                                                                               
DC10     CLI   LINGO,3             ADJUST OUTPUT FOR EURO STYLE                 
         BL    DC14                                                             
         CLI   LINGO,7                                                          
         BH    DC14                                                             
         CLI   OUTFMT,X'0C'        UNLESS ALREADY ASKED FOR                     
         BNL   DC14                                                             
         CLI   OUTFMT,X'06'        OUTPUT TYPE 7,8,9 TO C,D,E                   
         BH    DC12                                                             
         LA    R5,3(R5)            OUTPUT TYPE 4,5,6 TO C,D,E                   
*                                                                               
DC12     LA    R5,5(R5)                                                         
         STC   R5,4(RA)                                                         
         STC   R5,OUTFMT           STORE NEW VALUE IN 2ND PARAM                 
*                                                                               
DC14     SLL   R5,2                R5 IS MODIFIED OUTPUT TYPE                   
         LA    RF,INTAB(R4)                                                     
         LA    R6,SAVE                                                          
         BASR  RE,RF               CONVERT INPUT TO BINARY                      
*                                                                               
         TM    SVOUTFMT,X'40'      TEST IF CALLER YYYY OPTION                   
         BNO   DC16                                                             
         CLI   SAVE,59             STANDARD FORM IF LE 1959                     
         BH    DC16                IS DDMMYYYY                                  
         CLI   OUTFMT,X'11'                                                     
         BNE   *+8                                                              
         MVI   OUTFMT,X'15'                                                     
         CLI   OUTFMT,X'12'                                                     
         BNE   *+8                                                              
         MVI   OUTFMT,X'16'                                                     
*                                                                               
DC16     L     RF,DATEINFA         POINT TO DATE INFO                           
         ST    R3,0(RF)            SAVE A(START OF DATE)                        
         OC    SAVE(3),SAVE        TEST IF ZERO DATE                            
         BNZ   DC18                                                             
         OI    4(RF),X'80'         YES-SET NO DATE PRESENT                      
         LLC   RE,OUTFMT           AND POINT TO DEFAULT OUTPUT                  
         SLL   RE,2                                                             
         L     RF,=A(DEFOUT)                                                    
         A     RF,RELO                                                          
         AR    RE,RF                                                            
         XR    RF,RF                                                            
         ICM   RF,1,0(RE)          GET LENGTH OF DEFAULT OUTPUT                 
         JZ    *+2                 REAL TROUBLE HERE                            
*                                                                               
         ICM   RE,7,1(RE)          GET ADDRESS OF DEFAULT OUTPUT                
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         LA    R3,1(R3,RF)                                                      
         B     DC24                                                             
*                                                                               
DC18     TM    SVINPFMT,X'30'      TEST THIRD PARAMETER                         
         BNO   DC20                                                             
         BAS   RE,DAYS             DO DATE MODIFICATION                         
*                                                                               
DC20     CLI   SAVE+2,0            TEST IF ZERO DAY                             
         BNE   DC22                                                             
         SRL   R5,2                YES-ADJUST OUTPUT TYPE                       
         LLC   RF,ADJOUTND(R5)     EG DDMMMYY TO MMMYY                          
         LR    R5,RF                                                            
         SLL   R5,2                                                             
*                                                                               
DC22     LA    RF,OUTAB(R5)                                                     
         BASR  RE,RF               CONVERT BINARY TO OUTPUT                     
         L     RF,DATEINFA                                                      
         LR    RE,R3                                                            
         S     RE,0(RF)                                                         
         STH   RE,6(RF)            SET LENGTH OF OUTPUT DATE                    
*                                                                               
DC24     TM    SVINPFMT,X'30'      TEST DATE MODIFIER                           
         BO    DC30                                                             
         TM    SVINPFMT,X'20'      TEST PARAM3 SENT                             
         BO    DC28                                                             
         TM    SVINPFMT,X'10'      SECOND DATE FOLLOWING FIRST                  
         BZ    DC30                                                             
*                                                                               
         LLC   R8,INPLEN           SECOND DATE FOLLOWS FIRST                    
         SR    R2,R2                                                            
         ICM   R2,7,1(RA)                                                       
         AR    R2,R8                                                            
         B     *+10                                                             
*                                                                               
DC28     SR    R2,R2               SECOND DATE IS IN THIRD PARAM                
         ICM   R2,7,9(RA)                                                       
         NI    SVINPFMT,X'C0'                                                   
         LA    RE,DATEINF2         SET SECOND DATE ADDRESS INFO                 
         ST    RE,DATEINFA                                                      
         MVC   INPFMT,0(RA)        RESET INPUT FORMAT FOR 2ND DATE              
         NI    INPFMT,X'0F'                                                     
         MVC   OUTFMT,4(RA)        RESET OUTPUT FORMAT FOR 2ND DATE             
         NI    OUTFMT,X'1F'                                                     
         MVI   0(R3),C'-'          SET HYPHEN AFTER FIRST DATE                  
         LA    R3,1(R3)                                                         
         B     DC08                BACK TO PROCESS SECOND DATE                  
*                                                                               
DC30     TM    SVOUTFMT,X'80'      TEST IF CALLER WANTS TEXT OUT                
         BZ    DC52                                                             
         LA    R3,OMONTHS          YES-POINT TO W/S AREA TO BUILD TEXT          
         MVC   OMONTHS,SPACES                                                   
         MVI   OUTFMT,0                                                         
         TM    DATEINF1+4,X'80'    DATE 1 VALID?                                
         BO    DC32                                                             
         MVI   OUTFMT,1            YES-'PERIOD FROM' 1ST DATE                   
*                                                                               
         TM    DATEINF2+4,X'80'    DATE 2 VALID?                                
         BO    DC34                                                             
         MVI   OUTFMT,3            YES-'PERIOD' 1ST DATE '-' 2ND DATE           
         B     DC34                                                             
*                                                                               
DC32     TM    DATEINF2+4,X'80'    DATE 2 VALID?                                
         BO    DC34                                                             
         MVI   OUTFMT,2            YES-'PERIOD TO' 2ND DATE                     
*                                                                               
DC34     LLC   R1,LINGO            GET CORRECT TEXT FOR LANGUAGE                
         CLI   OUTFMT,0            ANY DATES AT ALL?                            
         BNE   DC36                                                             
         LA    R3,16(R3)           NO-SET DEFAULT MESSAGE LENGTH                
         B     DC50                                                             
*                                                                               
DC36     XR    RF,RF                                                            
         CLI   OUTFMT,1            TEST 'PERIOD FROM' 1ST DATE                  
         BNE   DC40                                                             
         L     RE,=A(PERFROML)                                                  
         A     RE,RELO                                                          
         MH    R1,0(RE)                                                         
         LA    RE,2(RE,R1)         POINT TO TEXT ENTRY FOR LANGUAGE             
         ICM   RF,1,0(RE)          GET LENGTH OF TEXT ENTRY                     
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),1(RE)                                                    
*                                                                               
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    DC38                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         NC    0(0,R3),LOWER                                                    
*                                                                               
DC38     LA    R3,2(RF,R3)                                                      
         L     RE,DATEINF1         POINT TO 1ST DATE                            
         ICM   RF,1,DATEINF1+7     GET LENGTH OF 1ST DATE                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         LA    R3,1(RF,R3)                                                      
         B     DC50                                                             
*                                                                               
DC40     CLI   OUTFMT,2            TEST 'PERIOD TO' 2ND DATE                    
         BNE   DC44                                                             
         L     RE,=A(PERTOL)                                                    
         A     RE,RELO                                                          
         MH    R1,0(RE)                                                         
         LA    RE,2(RE,R1)         POINT TO TEXT ENTRY FOR LANGUAGE             
         ICM   RF,1,0(RE)          GET LENGTH OF TEXT ENTRY                     
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),1(RE)                                                    
*                                                                               
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    DC42                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         NC    0(0,R3),LOWER                                                    
*                                                                               
DC42     LA    R3,2(RF,R3)                                                      
         L     RE,DATEINF2         POINT TO 2ND DATE                            
         IC    RF,DATEINF2+7       GET LENGTH OF 2ND DATE                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         LA    R3,1(RF,R3)                                                      
         B     DC50                                                             
*                                                                               
DC44     CLI   OUTFMT,3            TEST 'PERIOD' 1ST DATE '-' 2ND DATE          
         BNE   DC48                                                             
         L     RE,=A(PERFRTOL)                                                  
         A     RE,RELO                                                          
         MH    R1,0(RE)                                                         
         LA    RE,2(RE,R1)         POINT TO TEXT ENTRY FOR LANGUAGE             
         IC    RF,0(RE)            GET LENGTH                                   
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),1(RE)                                                    
*                                                                               
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    DC46                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         NC    0(0,R3),LOWER                                                    
*                                                                               
DC46     LA    R3,2(RF,R3)                                                      
         L     RE,DATEINF1         POINT TO 1ST DATE                            
         IC    RF,DATEINF1+7       GET LENGTH OF 1ST DATE                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
*                                                                               
         LA    R3,2(RF,R3)         SET HYPHEN BETWEEN DATES                     
         MVI   0(R3),C'-'                                                       
         LA    R3,2(R3)                                                         
*                                                                               
         L     RE,DATEINF2         POINT TO 2ND DATE                            
         IC    RF,DATEINF2+7       GET LENGTH OF 2ND DATE                       
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(RE)                                                    
         LA    R3,1(RF,R3)                                                      
         B     DC50                                                             
*                                                                               
DC48     DC    H'0'                UNDEFINED OUTPUT FORMAT                      
*                                                                               
DC50     LA    R2,OMONTHS          GET OUTPUT LENGTH                            
         LR    R1,R3                                                            
         SR    R1,R2                                                            
         SR    R3,R3                                                            
         ICM   R3,7,5(RA)          GET A(START OF OUTPUT AREA)                  
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)                                                    
         LA    R3,1(R3,R1)                                                      
*                                                                               
DC52     TM    SVINPFMT,X'80'      TEST IF CALLER WANTS LEN RETURNED            
         BZ    DATEND                                                           
         SR    RF,RF                                                            
         ICM   RF,7,5(RA)          GET A(START OF OUTPUT AREA)                  
         SR    R3,RF                                                            
         STC   R3,4(RA)            RETURN OUTPUT LEN IN P2                      
*                                                                               
DATEND   XMOD1 1                                                                
         EJECT                                                                  
***********************************************************************         
* INPUT AND OUTPUT ROUTINE TABLES                                     *         
***********************************************************************         
INTAB    B     INZERO              X'00'=6 BYTE YYMMDD                          
         B     INONE               X'01'=3 BYTE PACKED YMD                      
         B     INTWO               X'02'=2 BYTE COMPRESSED                      
         B     INTHREE             X'03'=3 BYTE BINARY YMD                      
         B     INFOUR              X'04'=8 BYTE MM/DD/YY OR DD/MM/YY            
         B     STOP                X'05'=USED FOR TODAYS DATE/TIME              
         B     INJUL               X'06'=4 BYTE MVS JULIAN X'0CYYDDDF'          
         B     STOP                X'07'=USED FOR DAYS/MONTHS LIST              
         B     INJULA              X'08'=3 BYTE MVS JULIAN X'CYYDDD'            
         B     INISO               X'09'=8 BYTE YYYYMMDD                        
         B     INISO               X'0A'=YYYY-MM-DD ISO 8601                    
         B     INEURO              X'0B'=EURO START DATE BINARY YMD             
         B     INSECS              X'0C'=SECONDS SINCE 1/1/0                    
         B     INYMDHMS            X'0D'=6 BYTE BINARY YMDHMS                   
         B     INTWON              X'0E'=2 BYTE COMPRESSED NEW BASE YR          
         B     STOP                X'0F'=USED FOR BILLING DATE                  
*                                                                               
OUTAB    B     OUTZERO             X'00'=6 BYTE YYMMDD                          
         B     OUTZERO             X'01'=3 BYTE PACKED YMD                      
         B     OUTTWO              X'02'=2 BYTE COMPRESSED                      
         B     OUTTHREE            X'03'=3 BYTE BINARY                          
         B     OUTFOUR             X'04'=EDITED DD MMM                          
         B     OUTFOUR             X'05'=EDITED DD MMM YY                       
         B     OUTSIX              X'06'=EDITED MMM YY                          
         B     OUTFOUR             X'07'=EDITED DDMMM                           
         B     OUTFOUR             X'08'=EDITED DDMMMYY                         
         B     OUTSIX              X'09'=EDITED MMMYY                           
         B     OUTZERO             X'0A'=8 BYTE MM/DD/YY                        
         B     AMEROUT             X'0B'=AMERICAN DATES C'MMMDD/YY'             
         B     OUTZERO             X'0C'=EDITED DD.MM                           
         B     OUTZERO             X'0D'=EDITED DD.MM.YY                        
         B     OUTZERO             X'0E'=EDITED MM.YY                           
         B     OUTJUL              X'0F'=4 BYTE MVS JULIAN X'0CYYDDDF'          
         B     STD10               X'10'=STANDARD DAY MONTH                     
         B     STD11               X'11'=STANDARD DAY MONTH YEAR                
         B     STD12               X'12'=STANDARD MONTH YEAR                    
         B     OUTJUL              X'13'=3 BYTE MVS JULIAN X'CYYDDD'            
         B     OUTZERO             X'14'=8 BYTE YYYYMMDD                        
         B     STD11               X'15'=STANDARD DAY MONTH FULL YEAR           
         B     STD12               X'16'=STANDARD MONTH FULL YEAR               
         B     OUTZERO             X'17'=YYYY-MM-DD ISO 8601                    
         B     OUTJUL              X'18'=SECONDS SINCE 1/1/0                    
         B     OUTTHREE            X'19'=6 BYTE BINARY YMDHMS                   
         B     OUTZERO             X'1A'=14 BYTE YYYYMMDDHHMMSS                 
         B     STOP                X'1B'=N/D                                    
         B     STOP                X'1C'=N/D                                    
         B     OUTMULTI            X'1D'=00,01,0E,03 MULTI NEW BASE YR          
         B     OUTTWON             X'1E'=2 BYTE COMPRESSED NEW BASE YR          
         B     OUTMULTI            X'1F'=00,01,02,03 MULTIPLE DATES             
*                                                                               
OUTADJ   DC    C'NNNNYYYYYYYYYYYNYYYNNYYNNNNNNNNN'                              
*                                                                               
STOP     DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* INPUT ROUTINES - CONVERT DATE TO 3 BYTE BINARY IN SAVE              *         
***********************************************************************         
INZERO   MVI   INPLEN,6            6 BYTE YYMMDD                                
         CLC   0(6,R2),SPACES                                                   
         BNE   *+12                                                             
         XC    0(3,R6),0(R6)       ZERO DATE IF NOT DEFINED                     
         BR    RE                                                               
*                                                                               
INZERO1  MVI   CENTURY,19                                                       
         CLI   0(R2),C'9'          TEST IF COLLATABLE DATE                      
         BNH   INZERO2                                                          
         MVI   CENTURY,20                                                       
         MVC   FULL(2),0(R2)                                                    
         LLC   R8,FULL             FIRST YEAR CHR ALSO HAS DECADE               
         AHI   R8,-10                                                           
         STC   R8,FULL                                                          
         PACK  DUB,FULL(2)                                                      
         CVB   R8,DUB                                                           
         STC   R8,0(R6)                                                         
         B     INZERO3                                                          
*                                                                               
INZERO2  PACK  DUB,0(2,R2)         NORMAL C'YY' 1ST TWO CHRS                    
         CVB   R8,DUB                                                           
         STC   R8,0(R6)                                                         
         CLI   INPFMT,0            TEST C'YYMMDD' INPUT FORMAT                  
         BE    INZERO2A                                                         
         CLI   INPFMT,4            TEST C'DD/MM/YY' INPUT FORMAT                
         BNE   INZERO3                                                          
INZERO2A CHI   R8,59               ASSUME C'00'-C'59' ARE 2000-2059             
         BH    INZERO3                                                          
         MVI   CENTURY,20                                                       
*                                                                               
INZERO3  PACK  DUB,2(2,R2)                                                      
         CVB   R8,DUB                                                           
         STC   R8,1(R6)                                                         
         SR    R8,R8                                                            
         CLC   4(2,R2),SPACES      ZERO DAY IF NOT DEFINED                      
         BE    *+14                                                             
         PACK  DUB,4(2,R2)                                                      
         CVB   R8,DUB                                                           
         STC   R8,2(R6)                                                         
         BR    RE                                                               
*                                                                               
INONE    MVI   INPLEN,3            3 BYTE PACKED YMD                            
         UNPK  WORK(7),0(4,R2)                                                  
         CLI   0(R2),X'FF'                                                      
         BNE   *+10                                                             
         MVC   WORK(6),=C'991231'                                               
         LA    R2,WORK                                                          
         B     INZERO1                                                          
*                                                                               
INTWO    MVI   INPLEN,2            2 BYTE COMPRESSED                            
         MVI   CENTURY,19                                                       
         SR    R8,R8                                                            
         ICM   R9,12,0(R2)                                                      
         SLDL  R8,7                R8=YEAR,R9=B'MMMMDDDDD....'                  
*                                                                               
INTWO1   TM    0(R2),X'80'         TEST INPUT YEAR < 1964                       
         B     INTWO2              *NOP BO                                      
         CHI   R8,36               NEW STYLE DATE WITH BASE YEAR 1964           
         BNL   *+12                                                             
         AHI   R8,64                                                            
         B     INTWO3                                                           
         MVI   CENTURY,20                                                       
         AHI   R8,-36              MUST BE A NEW BASE 1964 DATE                 
         B     INTWO3                                                           
*                                                                               
INTWO2   CHI   R8,100              TEST FOR YR 2000                             
         BL    *+12                                                             
         MVI   CENTURY,20                                                       
         AHI   R8,-100                                                          
*                                                                               
INTWO3   STC   R8,SAVE                                                          
         SR    R8,R8                                                            
         SLDL  R8,4                                                             
         STC   R8,SAVE+1                                                        
         SR    R8,R8                                                            
         SLDL  R8,5                                                             
         STC   R8,SAVE+2                                                        
         BR    RE                                                               
*                                                                               
INTWON   MVI   INPLEN,2            2 BYTE COMPRESSED BASE YEAR 1964             
         MVI   CENTURY,19                                                       
         SR    R8,R8                                                            
         ICM   R9,12,0(R2)                                                      
         SLDL  R8,7                R8=YEAR,R9=B'MMMMDDDDD....'                  
*                                                                               
INTWON1  CHI   R8,36               TEST FOR YEAR 2000                           
         BNL   INTWON2                                                          
         AHI   R8,64                                                            
         B     INTWON3                                                          
INTWON2  MVI   CENTURY,20                                                       
         AHI   R8,-36                                                           
*                                                                               
INTWON3  STC   R8,SAVE                                                          
         SR    R8,R8                                                            
         SLDL  R8,4                                                             
         STC   R8,SAVE+1                                                        
         SR    R8,R8                                                            
         SLDL  R8,5                                                             
         STC   R8,SAVE+2                                                        
         BR    RE                                                               
*                                                                               
INTHREE  MVI   INPLEN,3            3 BYTE BINARY YMD                            
INTHREE1 MVI   CENTURY,19                                                       
         MVC   SAVE(3),0(R2)                                                    
         CLI   SAVE,100                                                         
         BLR   RE                                                               
         MVI   CENTURY,20                                                       
         LLC   R8,SAVE                                                          
         AHI   R8,-100                                                          
         STC   R8,SAVE                                                          
         BR    RE                                                               
*                                                                               
INFOUR   MVI   INPLEN,8            8 BYTE DD/MM/YY OR DD.MM.YY                  
         MVC   WORK(2),6(R2)                                                    
         MVC   WORK+2(2),3(R2)                                                  
         MVC   WORK+4(2),0(R2)                                                  
         CLI   DATYPE,DATYPEUS     TEST ENGLISH/AMERICAN STYLE                  
         BNE   *+16                                                             
         MVC   WORK+2(2),0(R2)     AMERICAN IPL TYPE MM/DD/YY                   
         MVC   WORK+4(2),3(R2)                                                  
         LA    R2,WORK             POINT TO C'YYMMDD' IN WORK                   
         B     INZERO1                                                          
*                                                                               
INJULA   MVI   INPLEN,3            3 BYTE JULIAN X'CYYDDD'                      
         LA    R1,X'F0'                                                         
         ICM   R1,14,0(R2)                                                      
         SRL   R1,4                                                             
         B     INJUL1                                                           
*                                                                               
INJUL    MVI   INPLEN,4            4 BYTE JULIAN X'0CYYDDDF'                    
         L     R1,0(R2)                                                         
INJUL1   MVI   CENTURY,19          R1=X'0CYYDDDF'                               
         XC    DUB,DUB                                                          
         STCM  R1,3,DUB+6                                                       
         CVB   R0,DUB              R0=BINARY DAY IN YEAR NUMBER                 
         SRL   R1,12               R1=X'0000CYYD'                               
         STCM  R1,3,DUB+6                                                       
         OI    DUB+7,X'0F'         TURN ON SIGN BITS                            
         TM    DUB+6,X'F0'         TEST CENTURY NIBBLE                          
         BZ    *+12                                                             
         NI    DUB+6,X'0F'                                                      
         MVI   CENTURY,20                                                       
         CVB   R1,DUB              R1=BINARY YEAR NUMBER                        
         STC   R1,SAVE                                                          
         LA    R1,1                R1=JANUARY                                   
         CH    R0,DAYSINMO+0                                                    
         BNH   INJUL4                                                           
         SH    R0,DAYSINMO+0                                                    
         LA    RF,DAYSINMO+2       RF=POINTS TO NUMBER OF DAYS IN MONTH         
         LA    R1,1(R1)            R1=FEBRUARY                                  
         CH    R0,DAYSINMO+2                                                    
         BNH   INJUL4                                                           
         SH    R0,DAYSINMO+2                                                    
         TM    SAVE,3              TEST LEAP YEAR                               
         BNZ   INJUL3              NO                                           
         CHI   R0,1                                                             
         BH    *+12                                                             
         AH    R0,DAYSINMO+2                                                    
         B     INJUL4                                                           
         AHI   R0,-1                                                            
         B     INJUL3                                                           
INJUL2   CH    R0,0(RF)                                                         
         BNH   INJUL4                                                           
         SH    R0,0(RF)                                                         
INJUL3   LA    R1,1(R1)            BUMP MONTH NUMBER                            
         LA    RF,2(RF)                                                         
         B     INJUL2                                                           
INJUL4   STC   R1,SAVE+1           SET BINARY MONTH NUMBER                      
         STC   R0,SAVE+2           SET BINARY DAY NUMBER                        
         BR    RE                                                               
*                                                                               
INISO    MVI   INPLEN,8            YYYYMMDD OR YYYY-MM-DD                       
         MVI   CENTURY,19                                                       
         CLC   0(2,R2),=C'19'      VALIDATE DDS DATE RANGE 1900-2059            
         BL    STOP                                                             
         BE    INISO1                                                           
         CLC   0(4,R2),=C'2059'                                                 
         BH    STOP                                                             
         MVI   CENTURY,20                                                       
INISO1   PACK  DUB,2(2,R2)         SET YEAR                                     
         CVB   R8,DUB                                                           
         STC   R8,0(R6)                                                         
INISO2   LA    RF,4(R2)            POINT TO MONTH                               
         CLI   INPFMT,X'0A'                                                     
         BNE   *+12                                                             
         MVI   INPLEN,10           SET LENGTH OF ISO 8601 DATE                  
         LA    RF,1(RF)                                                         
         PACK  DUB,0(2,RF)         SET MONTH                                    
         CVB   R8,DUB                                                           
         STC   R8,1(R6)                                                         
INISO3   LA    RF,2(RF)            POINT TO DAY                                 
         CLI   INPFMT,X'0A'                                                     
         BNE   *+8                                                              
         LA    RF,1(RF)                                                         
         PACK  DUB,0(2,RF)         SET DAY                                      
         CVB   R8,DUB                                                           
         STC   R8,2(R6)                                                         
         BR    RE                                                               
*                                                                               
INEURO   MVC   WORK(3),=X'660101'  EURO START DAY 20020101                      
         LA    R2,WORK                                                          
         B     INTHREE                                                          
*                                                                               
INSECS   MVI   INPLEN,4            TIME IN SECONDS SINCE 1/1/0                  
         SR    R0,R0                                                            
         L     R1,0(R2)                                                         
         D     R0,=F'86400'        R0=SECS,R1=DAYS                              
         ST    R0,SECS                                                          
         SR    R0,R0                                                            
         D     R0,=F'1461'         R0=RESIDUAL DAYS,R1=4-YEARS                  
*                                                                               
         LA    RF,INSECT           SEARCH DAYS IN 4-YEAR PERIOD TABLE           
         LA    R8,2(RF)                                                         
INSECS1  CH    R0,0(R8)            COMPARE RESIDUAL DAYS                        
         BL    INSECS2                                                          
         LA    R8,2(R8)                                                         
         B     INSECS1                                                          
*                                                                               
INSECS2  AHI   R8,-2               BACK UP TO CORRECT YEAR ENTRY                
         SH    R0,0(R8)                                                         
         AHI   R0,1                R0=DAY NUMBER IN YEAR                        
         SR    R8,RF                                                            
         SRL   R8,1                R8=YEAR NUMBER IN 4-YEAR PERIOD              
         SLL   R1,2                R1=NUMBER OF YEARS IN PERIOD                 
         AR    R1,R8               R1=YEAR NUMBER                               
*                                                                               
INSECS3  CVD   R1,DUB              DUB+4=X'00000YY+'                            
         OI    DUB+6,X'10'         DUB+4=X'0000CYY+'                            
         L     R1,DUB+4                                                         
         SRL   R1,4                                                             
         SLL   R1,16               R1=X'0CYY0000'                               
         CVD   R0,DUB              DUB+4=X'0000DDD+'                            
         OI    DUB+7,X'0F'                                                      
         ICM   R1,3,DUB+6          R1=X'0CYYDDDF'                               
         B     INJUL1              TREAT AS JULIAN DATE INPUT                   
*                                                                               
INSECT   DC    H'0000',H'0366',H'0731',H'1096',H'1461'                          
*                                                                               
INYMDHMS MVI   INPLEN,6            6 BYTE BINARY YMDHMS                         
         LLC   R1,3(R2)            HOURS                                        
         MHI   R1,3600                                                          
         LR    R0,R1                                                            
         LLC   R1,4(R2)            MINUTES                                      
         MHI   R1,60                                                            
         AR    R0,R1                                                            
         LLC   R1,5(R2)            SECONDS                                      
         AR    R0,R1                                                            
         ST    R0,SECS                                                          
         B     INTHREE1            TREAT AS YMD BINARY INPUT                    
         EJECT                                                                  
***********************************************************************         
* OUTPUT ROUTINES - CONVERT DATE FROM 3 BYTE BINARY IN SAVE           *         
***********************************************************************         
OUTZERO  LLC   R8,SAVE+0           6 BYTE YYMMDD                                
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+0(2),DUB+6(2)                                               
         IC    R8,SAVE+1                                                        
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+2(2),DUB+6(2)                                               
         IC    R8,SAVE+2                                                        
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+4(2),DUB+6(2)                                               
*                                                                               
         CLI   OUTFMT,X'01'        PACKED OUTPUT WANTED                         
         BE    OUTONE                                                           
         BL    OUTZERO0                                                         
         CLI   OUTFMT,X'14'        YYYYMMDD WANTED                              
         BE    OUTISO                                                           
         CLI   OUTFMT,X'17'        YYYY-MM-DD WANTED                            
         BE    OUTISO                                                           
         CLI   OUTFMT,X'1A'        YYYYMMDDHHMMSS WANTED                        
         BE    OUTISO                                                           
         B     OUTZERO2                                                         
*                                                                               
OUTZERO0 MVC   0(6,R3),WORK        YYMMDD WANTED                                
         CLI   CENTURY,20                                                       
         BNE   OUTZERO1                                                         
         TM    SVOUTFMT,X'20'      TEST DONT OUTPUT FUNNY YEAR                  
         BO    OUTZERO1                                                         
         IC    R1,0(R3)            CONVERT 1ST YEAR BYTE                        
         LA    R1,10(R1)                                                        
         STC   R1,0(R3)                                                         
OUTZERO1 LA    R3,6(R3)                                                         
         BR    RE                                                               
OUTZERO2 CLI   OUTFMT,X'0A'        TEST IPL FORMAT WANTED                       
         BNE   OUTZERO3                                                         
         MVC   0(2,R3),WORK+4      DD/MM/YY                                     
         MVI   2(R3),C'/'                                                       
         MVC   3(2,R3),WORK+2                                                   
         MVI   5(R3),C'/'                                                       
         MVC   6(2,R3),WORK                                                     
         LA    R3,8(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTZERO3 CLI   OUTFMT,X'0E'        MM.YY REQUIRED                               
         BNE   OUTZERO4                                                         
         MVC   0(2,R3),WORK+2                                                   
         MVI   2(R3),C'.'                                                       
         MVC   3(2,R3),WORK+0                                                   
         LA    R3,5(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTZERO4 MVC   0(2,R3),WORK+4      DD.MM.YY                                     
         MVI   2(R3),C'.'                                                       
         MVC   3(2,R3),WORK+2                                                   
         LA    R3,5(R3)                                                         
         CLI   OUTFMT,X'0C'        EXIT IF DD.MM REQUIRED                       
         BER   RE                                                               
         MVI   0(R3),C'.'                                                       
         MVC   1(2,R3),WORK+0                                                   
         LA    R3,3(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTONE   PACK  DUB(4),WORK(7)      3 BYTE PACKED YMD                            
         MVC   0(3,R3),DUB                                                      
         CLI   CENTURY,20                                                       
         BNE   OUTONE1                                                          
         TM    SVOUTFMT,X'20'      TEST DONT OUTPUT FUNNY YEAR                  
         BO    OUTONE1                                                          
         LLC   R8,0(R3)                                                         
         SRDL  R8,4                                                             
         LA    R8,10(R8)                                                        
         SLDL  R8,4                                                             
         STC   R8,0(R3)                                                         
OUTONE1  LA    R3,3(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTTWO   LLC   R8,SAVE+2           2 BYTE COMPRESSED                            
         SRDL  R8,5                                                             
         IC    R8,SAVE+1                                                        
         SRDL  R8,4                                                             
         IC    R8,SAVE                                                          
         CLI   CENTURY,20                                                       
         BNE   OUTTWO1                                                          
         TM    SVOUTFMT,X'20'      TEST DONT OUTPUT FUNNY YEAR                  
         BO    OUTTWO1                                                          
         LA    R8,100(R8)                                                       
         CHI   R8,127              MAX YEAR IS 2027 FOR COMPRESSED              
         BNH   OUTTWO1                                                          
         CLI   INPFMT,3            ALLOW X'FFFFFF' FOR INPUT TYPE 3             
         BNE   STOP                                                             
         SR    RF,RF               RF=A(INPUT AREA)                             
         ICM   RF,7,1(RA)                                                       
         CLC   0(3,RF),=X'FFFFFF'                                               
         BNE   STOP                                                             
         MVC   0(2,R3),=X'FFFFFF'                                               
         B     OUTTWO2                                                          
OUTTWO1  SRDL  R8,23                                                            
         STC   R9,1(R3)                                                         
         SRL   R9,8                                                             
         STC   R9,0(R3)                                                         
OUTTWO2  LA    R3,2(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTTWON  LLC   R8,SAVE+2           2 BYTE COMPRESSED BASE YEAR 1964             
         SRDL  R8,5                                                             
         IC    R8,SAVE+1                                                        
         SRDL  R8,4                                                             
         IC    R8,SAVE                                                          
         CLI   CENTURY,20                                                       
         BNE   *+12                                                             
         AHI   R8,36               ADJUST FOR BASE YEAR                         
         B     OUTTWON0                                                         
         AHI   R8,-64                                                           
         BM    STOP                DIE IF YEAR LESS THAN 1964                   
OUTTWON0 CLI   INPFMT,3            ALLOW X'FFFFFF' FOR INPUT TYPE 3             
         BNE   OUTTWON1                                                         
         SR    RF,RF               RF=A(INPUT AREA)                             
         ICM   RF,7,1(RA)                                                       
         CLC   0(3,RF),=X'FFFFFF'                                               
         BNE   OUTTWON1                                                         
         MVC   0(2,R3),=X'FFFFFF'                                               
         B     OUTTWON2                                                         
OUTTWON1 SRDL  R8,23                                                            
         STC   R9,1(R3)                                                         
         SRL   R9,8                                                             
         STC   R9,0(R3)                                                         
OUTTWON2 LA    R3,2(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTTHREE MVC   0(3,R3),SAVE        3 BYTE BINARY YMD                            
         CLI   CENTURY,20                                                       
         BNE   OUTTHRE1                                                         
         TM    SVOUTFMT,X'20'      TEST DONT OUTPUT FUNNY YEAR                  
         BO    OUTTHRE1                                                         
         IC    R8,0(R3)                                                         
         LA    R8,100(R8)                                                       
         STC   R8,0(R3)                                                         
OUTTHRE1 LA    R3,3(R3)                                                         
         CLI   OUTFMT,X'19'        6 BYTE BINARY YMDHMS                         
         BE    OUTHMS                                                           
         BR    RE                                                               
*                                                                               
OUTFOUR  LLC   R8,SAVE+2           EDIT DAY                                     
         CVD   R8,DUB                                                           
         UNPK  0(2,R3),DUB                                                      
         OI    1(R3),X'F0'                                                      
         CLI   0(R3),X'F0'                                                      
         BNE   *+8                                                              
         MVI   0(R3),C' '                                                       
         LA    R3,2(R3)                                                         
         CLI   OUTFMT,X'06'                                                     
         BH    OUTSIX                                                           
         MVI   0(R3),C' '                                                       
         LA    R3,1(R3)                                                         
*                                                                               
OUTSIX   LLC   R8,SAVE+1           EDIT MONTH                                   
         BCTR  R8,0                                                             
         MHI   R8,3                                                             
         A     R8,=A(MONTAB)                                                    
         A     R8,RELO                                                          
         MVC   0(3,R3),0(R8)                                                    
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    *+10                                                             
         NC    0(3,R3),LOWERDM                                                  
         LA    R3,3(R3)                                                         
         CLI   OUTFMT,X'04'                                                     
         BER   RE                                                               
         CLI   OUTFMT,X'07'                                                     
         BER   RE                                                               
         CLI   OUTFMT,X'06'                                                     
         BH    *+12                                                             
         MVI   0(R3),C' '                                                       
         LA    R3,1(R3)                                                         
*                                                                               
         LLC   R8,SAVE             EDIT YEAR                                    
         CVD   R8,DUB                                                           
         UNPK  0(2,R3),DUB                                                      
         OI    1(R3),X'F0'                                                      
         LA    R3,2(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTJUL   LLC   R8,SAVE             MVS JULIAN X'0CYYDDDF'                       
         CVD   R8,DUB              DUB+4=X'00000YY+'                            
         CLI   CENTURY,20                                                       
         BNE   *+8                                                              
         OI    DUB+6,X'10'         DUB+4=X'0000CYY+'                            
         L     R9,DUB+4                                                         
         SRL   R9,4                                                             
         SLL   R9,16               R9=X'0CYY0000'                               
         IC    R8,SAVE+1                                                        
         BCTR  R8,0                                                             
         SLL   R8,1                                                             
         LH    R1,TOTDAYS(R8)                                                   
         IC    R8,SAVE+2                                                        
         AR    R8,R1                                                            
         TM    SAVE,X'03'          TEST LEAP YEAR                               
         BNZ   OUTJUL1                                                          
         CLC   SAVE+1(2),=X'0229'  YES-TEST FEB 29                              
         BNH   OUTJUL1                                                          
         LA    R8,1(R8)                                                         
OUTJUL1  CVD   R8,DUB              DUB+4=X'0000DDD+'                            
         OI    DUB+7,X'0F'                                                      
         ICM   R9,3,DUB+6          R9=X'0CYYDDDF'                               
         CLI   OUTFMT,X'13'                                                     
         BE    OUTJUL2                                                          
         CLI   OUTFMT,X'18'                                                     
         BE    OUTSECS                                                          
         ST    R9,0(R3)                                                         
         BR    RE                                                               
OUTJUL2  SRL   R9,4                                                             
         STCM  R9,7,0(R3)          R9=X'00CYYDDD'                               
         BR    RE                                                               
TOTDAYS  DC    AL2(000,031,059,090,120,151,181,212,243,273,304,334)             
*                                                                               
OUTISO   MVC   0(2,R3),=C'19'      YYYYMMDD OR YYYY-MM-DD                       
         CLI   CENTURY,20                                                       
         BNE   *+10                                                             
         MVC   0(2,R3),=C'20'                                                   
         CLI   OUTFMT,X'17'                                                     
         BE    OUTISO2                                                          
         MVC   2(4,R3),WORK                                                     
         LA    R3,6(R3)                                                         
         CLC   WORK+4(2),=C'00'    TEST DAY NOT DEFINED                         
         BE    OUTISO1                                                          
         MVC   0(2,R3),WORK+4                                                   
         LA    R3,2(R3)                                                         
OUTISO1  CLI   OUTFMT,X'1A'        YYYYMMDDHHMMSS                               
         BE    OUTHMS                                                           
         BR    RE                                                               
OUTISO2  MVC   2(2,R3),WORK        YYYY-MM-DD ISO 8601 FORMAT                   
         MVI   4(R3),C'-'                                                       
         MVC   5(2,R3),WORK+2                                                   
         LA    R3,7(R3)                                                         
         CLC   WORK+4(2),=C'00'                                                 
         BER   RE                                                               
         MVI   0(R3),C'-'                                                       
         MVC   1(2,R3),WORK+4                                                   
         LA    R3,3(R3)                                                         
         BR    RE                                                               
*                                                                               
OUTHMS   SR    R0,R0               BINARY HMS                                   
         L     R1,SECS                                                          
         D     R0,=F'3600'                                                      
         STC   R1,0(R3)            HOURS                                        
         SRDL  R0,32                                                            
         D     R0,=F'60'                                                        
         STC   R1,1(R3)            MINUTES                                      
         STC   R0,2(R3)            SECONDS                                      
         LA    R3,3(R3)                                                         
OUTHMS1  CLI   OUTFMT,X'19'        EXIT IF WANT BINARY                          
         BER   RE                                                               
         AHI   R3,-3                                                            
         MVC   SAVE(3),0(R3)       SAVE BINARY YMD                              
         LA    R1,3                                                             
         SR    R8,R8                                                            
         LA    R9,SAVE                                                          
OUTHMS2  IC    R8,0(R9)            CONVERT BINARY YMD TO ALPHA                  
         CVD   R8,DUB                                                           
         UNPK  0(2,R3),DUB                                                      
         OI    1(R3),X'F0'                                                      
         LA    R9,1(R9)                                                         
         LA    R3,2(R3)                                                         
         BCT   R1,OUTHMS2                                                       
         BR    RE                                                               
*                                                                               
OUTSECS  ST    R9,FULL             TIME IN SECONDS SINCE 1/1/0                  
         XC    DUB(6),DUB          CONVERT P'DDD' TO BINARY SECONDS             
         MVC   DUB+6(2),FULL+2                                                  
         CVB   R1,DUB                                                           
         AHI   R1,-1                                                            
         M     R0,=F'86400'        R1=SECONDS TO START OF THIS DAY              
         A     R1,SECS                                                          
         ST    R1,SECS                                                          
*                                                                               
         L     R1,FULL             EXTRACT YEAR FROM DATE                       
         SLL   R1,8                                                             
         SRL   R1,24                                                            
         SLL   R1,4                R1=00000YY0                                  
         XC    DUB(4),DUB                                                       
         ST    R1,DUB+4                                                         
         OI    DUB+7,X'0C'                                                      
         CVB   R1,DUB              R1=BINARY YEAR FROM 2000                     
*                                                                               
         LR    R9,R1               COMPUTE NUMBER OF DAYS                       
         SR    R8,R8                                                            
         D     R8,=F'4'            R9=NUMBER OF LEAP YEARS                      
         LTR   R8,R8                                                            
         BZ    *+8                                                              
         AHI   R9,1                R9=NUMBER OF EXTRA DAYS                      
         MHI   R1,365                                                           
         AR    R1,R9                                                            
         M     R0,=F'86400'        R1=SECONDS TO START OF THIS YEAR             
         A     R1,SECS                                                          
         ST    R1,0(R3)                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* OUTPUT MULTIPLE DATES 00,01,02,03 - CL6,PL3,XL2,XL3                 *         
* EXIT BACK TO CALLER AS NO SPECIAL OPTIONS ARE AVAILABLE WITH THIS   *         
***********************************************************************         
OUTMULTI LLC   R8,SAVE+0           6 BYTE C'YYMMDD' AT +00                      
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+0(2),DUB+6(2)                                               
         IC    R8,SAVE+1                                                        
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+2(2),DUB+6(2)                                               
         IC    R8,SAVE+2                                                        
         CVD   R8,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK+4(2),DUB+6(2)                                               
         MVC   0(6,R3),WORK                                                     
*                                                                               
OUTMU1   PACK  DUB(4),WORK(7)      3 BYTE PACKED YMD AT +06                     
         MVC   6(3,R3),DUB                                                      
*                                                                               
OUTMU2   LLC   R8,SAVE+2           2 BYTE COMPRESSED AT +09                     
         SRDL  R8,5                                                             
         IC    R8,SAVE+1                                                        
         SRDL  R8,4                                                             
         IC    R8,SAVE                                                          
         CLI   OUTFMT,X'1D'        TEST IF WANT NEW BASE DATE 1964              
         BE    OUTMU2B                                                          
         CLI   CENTURY,20                                                       
         BNE   OUTMU2A                                                          
         TM    SVOUTFMT,X'20'      TEST DONT OUTPUT FUNNY YEAR                  
         BO    OUTMU2A                                                          
         LA    R8,100(R8)          CONVERT 1ST YEAR BYTE OF COMPRESSED          
         CHI   R8,127              MAX YEAR IS 2027 FOR COMPRESSED              
         BH    STOP                                                             
OUTMU2A  SRDL  R8,23                                                            
         STC   R9,10(R3)                                                        
         SRL   R9,8                                                             
         STC   R9,09(R3)                                                        
         B     OUTMU3                                                           
*                                                                               
OUTMU2B  CLI   CENTURY,20          NEW COMPRESSED DATE BASE YEAR 1964           
         BNE   *+12                                                             
         AHI   R8,36               ADJUST FOR BASE YEAR                         
         B     OUTMU2C                                                          
         AHI   R8,-64                                                           
         BM    STOP                DIE IF YEAR LESS THAN 1964                   
OUTMU2C  SRDL  R8,23                                                            
         STC   R9,10(R3)                                                        
         SRL   R9,8                                                             
         STC   R9,09(R3)                                                        
*                                                                               
OUTMU3   MVC   11(3,R3),SAVE       3 BYTE BINARY YMD AT +11                     
*                                                                               
OUTMU5   CLI   CENTURY,20          FIX FOR CENTURY                              
         BNE   OUTMU6                                                           
         TM    SVOUTFMT,X'20'      TEST DONT OUTPUT FUNNY YEAR                  
         BO    OUTMU6                                                           
*                                                                               
         IC    R1,0(R3)            CONVERT 1ST YEAR BYTE OF C'YYMMDD'           
         LA    R1,10(R1)                                                        
         STC   R1,0(R3)                                                         
*                                                                               
         LLC   R8,6(R3)            CONVERT 1ST YEAR BYTE OF P'YMD'              
         SRDL  R8,4                                                             
         LA    R8,10(R8)                                                        
         SLDL  R8,4                                                             
         STC   R8,6(R3)                                                         
*                                                                               
         IC    R8,11(R3)           CONVERT 1ST YEAR BYTE OF X'YMD'              
         LA    R8,100(R8)                                                       
         STC   R8,11(R3)                                                        
*                                                                               
OUTMU6   LA    R3,14(R3)           BUMP BY LENGTH OF OUTPUT                     
         B     DATEND              EXIT DIRECTLY BACK TO CALLER                 
         EJECT                                                                  
***********************************************************************         
* OUTPUT STANDARD DATE FORMATS                                        *         
***********************************************************************         
STD10    LA    RF,STDFOUR          DAY.MONTH STANDARD FORMAT                    
         CLI   LINGO,3                                                          
         BLR   RF                                                               
         CLI   LINGO,7                                                          
         BHR   RF                                                               
         LA    RF,STDZERO                                                       
         BR    RF                                                               
*                                                                               
STD11    LA    RF,STDFOUR          DAY.MONTH.YEAR STANDARD FORMAT               
         CLI   LINGO,3                                                          
         BLR   RF                                                               
         CLI   LINGO,7                                                          
         BHR   RF                                                               
         LA    RF,STDZERO                                                       
         BR    RF                                                               
*                                                                               
STD12    LA    RF,STDSIX           MONTH.YEAR STANDARD FORMAT                   
         CLI   LINGO,3                                                          
         BLR   RF                                                               
         CLI   LINGO,7                                                          
         BHR   RF                                                               
         LA    RF,STDZERO                                                       
         BR    RF                                                               
*                                                                               
STDZERO  LA    R6,WORK             6 BYTE YYMMDD                                
         LA    R1,3                                                             
         SR    R8,R8                                                            
         LA    R9,SAVE                                                          
STDZERO1 IC    R8,0(R9)                                                         
         CVD   R8,DUB                                                           
         UNPK  0(2,R6),DUB                                                      
         OI    1(R6),X'F0'                                                      
         LA    R9,1(R9)                                                         
         LA    R6,2(R6)                                                         
         BCT   R1,STDZERO1                                                      
*                                                                               
STDZERO2 CLI   OUTFMT,X'12'        MM.YY OR MM.YYYY REQUIRED                    
         BE    *+12                                                             
         CLI   OUTFMT,X'16'                                                     
         BNE   STDZERO4                                                         
         MVC   0(2,R3),WORK+2                                                   
         MVI   2(R3),C'.'                                                       
         LA    R3,3(R3)                                                         
         CLI   OUTFMT,X'16'                                                     
         BNE   STDZERO3                                                         
         MVC   0(2,R3),=C'19'                                                   
         CLI   CENTURY,20                                                       
         BNE   *+10                                                             
         MVC   0(2,R3),=C'20'                                                   
         LA    R3,2(R3)                                                         
STDZERO3 MVC   0(2,R3),WORK+0                                                   
         LA    R3,2(R3)                                                         
         BR    RE                                                               
*                                                                               
STDZERO4 MVC   0(2,R3),WORK+4      DD.MM.YY                                     
         MVI   2(R3),C'.'                                                       
         MVC   3(2,R3),WORK+2                                                   
         LA    R3,5(R3)                                                         
         CLI   OUTFMT,X'10'        EXIT IF DD.MM REQUIRED                       
         BER   RE                                                               
         MVI   0(R3),C'.'                                                       
         CLI   OUTFMT,X'15'        IF TYPE 15 OR 16                             
         BE    *+12                                                             
         CLI   OUTFMT,X'16'                                                     
         BNE   STDZERO5                                                         
         MVC   1(2,R3),=C'19'                                                   
         CLI   CENTURY,20                                                       
         BNE   *+10                                                             
         MVC   1(2,R3),=C'20'                                                   
         LA    R3,2(R3)                                                         
STDZERO5 MVC   1(2,R3),WORK+0                                                   
         LA    R3,3(R3)                                                         
         BR    RE                                                               
*                                                                               
STDFOUR  LLC   R8,SAVE+2           EDIT DAY                                     
         CVD   R8,DUB                                                           
         UNPK  0(2,R3),DUB                                                      
         OI    1(R3),X'F0'                                                      
         LA    R3,2(R3)                                                         
*                                                                               
STDSIX   LLC   RF,LANG             EDIT MONTH                                   
         MHI   RF,36                                                            
         A     RF,=A(MONTAB)                                                    
         A     RF,RELO                                                          
         LLC   R8,SAVE+1                                                        
         BCTR  R8,0                                                             
         MHI   R8,3                                                             
         AR    R8,RF                                                            
         MVC   0(3,R3),0(R8)                                                    
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    *+10                                                             
         NC    0(3,R3),LOWERDM                                                  
         LA    R3,3(R3)                                                         
         CLI   OUTFMT,X'10'        EXIT IF DDMMM                                
         BER   RE                                                               
*                                                                               
         CLI   OUTFMT,X'15'        IF TYPE 15 OR 16                             
         BE    *+12                                                             
         CLI   OUTFMT,X'16'                                                     
         BNE   STDSIX1                                                          
         MVC   0(2,R3),=C'19'      OUTPUT CENTURY                               
         CLI   CENTURY,20                                                       
         BNE   *+10                                                             
         MVC   0(2,R3),=C'20'                                                   
         LA    R3,2(R3)                                                         
STDSIX1  LLC   R8,SAVE             EDIT YEAR                                    
         CVD   R8,DUB                                                           
         UNPK  0(2,R3),DUB                                                      
         OI    1(R3),X'F0'                                                      
         LA    R3,2(R3)                                                         
         BR    RE                                                               
*                                                                               
AMEROUT  L     RF,=A(AMER)         PASS CONTROL TO AMERICAN DATES               
         A     RF,RELO                                                          
         BR    RF                  ROUTINE WILL RETURN DIRECT TO CALLER         
         EJECT                                                                  
***********************************************************************         
* PERFORM DAYS MANIPULATION FOR FIRST/LAST OF MONTH                   *         
* PERFORM DATE MANIPULATION BY DAYS,MONTHS,YEARS PASSED IN P4         *         
* ASSUMES SAVE HOLDS 3 BYTE YYMMDD IN BINARY                          *         
* P3(1) HAS ONE BYTE FORMAT NUMBER TO PERFORM ON INPUT DATE           *         
*                                                                     *         
* V(ADDAY) FUNCTIONALLY IS INCLUDED IN DAY/MONTH/YEAR ADD/SUBTRACT    *         
* V(ADDAY) X'80'=ALWAYS SET OUTPUT DAY TO LAST DAY IN MONTH           *         
* V(ADDAY) X'40'=DONT SET OUTPUT DAY TO LAST IF INPUT DAY IS LAST     *         
*                                                                     *         
* P3(1)=08 IS SAME AS V(ADDAY) CALL FOR MONTHS WITH X'80' SET         *         
* P3(1)=09 IS SAME AS V(ADDAY) CALL FOR MONTHS WITH X'40' SET         *         
* P3(1)=11 IS SAME AS V(ADDAY) CALL FOR YEARS WITH X'80' SET          *         
* P3(1)=12 IS SAME AS V(ADDAY) CALL FOR YEARS WITH X'40' SET          *         
***********************************************************************         
                                                                                
DAYS     ST    RE,DAYSRE           SAVE CALLERS RETURN REGISTER                 
         MVC   SVDAYFMT,8(RA)                                                   
         NI    SVDAYFMT,X'0F'      GET DATE MANIPULATION ROUTINE NUMBER         
         MVI   DAYADJ,0            CLEAR DATE ADJUST FLAGS                      
         LLC   RF,SVDAYFMT         RF=FORMAT NUMBER FOR INPUT DATE              
         SLL   RF,2                                                             
         LA    RF,DAYMODS(RF)                                                   
         BR    RF                                                               
                                                                                
DAYMODS  B     DAY1ST              0 - FIRST DAY OF GIVEN MONTH                 
         B     DAYLST              1 - LAST DAY OF GIVEN MONTH                  
         B     NDAY1ST             2 - FIRST DAY OF NEXT MONTH                  
         B     NDAYLST             3 - LAST DAY OF NEXT MONTH                   
         B     PDAY1ST             4 - FIRST DAY OF PREV MONTH                  
         B     PDAYLST             5 - LAST DAY OF PREV MONTH                   
         B     ADAY                6 = ADD/SUB NUM OF DAYS  IN P4 AD=00         
         B     AMON                7 = ADD/SUB NUM OF MNTHS IN P4 AD=00         
         B     AMONLST             8 = ADD/SUB NUM OF MNTHS IN P4 AD=80         
         B     AMONLSX             9 = ADD/SUB NUM OF MNTHS IN P4 AD=40         
         B     AYEAR               10= ADD/SUB NUM OF YEARS IN P4 AD=00         
         B     AYEARLST            11= ADD/SUB NUM OF YEARS IN P4 AD=80         
         B     AYEARLSX            12= ADD/SUB NUM OF YEARS IN P4 AD=40         
         B     DAYSX               13- N/D                                      
         B     DAYSX               14- N/D                                      
         B     DAYSX               15- N/D                                      
                                                                                
DAYSX    L     RE,DAYSRE           RESTORE DAYS ROUTINE RETURN REG              
         BR    RE                                                               
                                                                                
DAY1ST   MVI   SAVE+2,1            SET DD TO '01'                               
         B     DAYSX                                                            
                                                                                
DAYLST   LLC   RF,SAVE+1           GET MM                                       
         BCTR  RF,0                                                             
         SLL   RF,1                                                             
         LA    RF,DAYSINMO(RF)     INDEX INTO DAYS TABLE                        
         XR    R1,R1                                                            
         ICM   R1,3,0(RF)          SET NUMBER OF DAYS IN R1                     
         LR    RF,R1               SAVE NUMBER IN RF                            
*                                                                               
         CLI   SAVE+1,2            IS IT FEBRUARY?                              
         BNE   DLST02              NO                                           
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         IC    R0,SAVE             GET YY                                       
         SRDL  R0,2                                                             
         LTR   R1,R1               LEAP YEAR TEST                               
         BNZ   *+8                 ONLY WORKS BECAUSE Y2000 IS A LEAP           
         LA    RF,1(RF)            YEAR,ELSE NEED TO INTERCEPT YY=00            
*                                                                               
DLST02   STC   RF,SAVE+2           SAVE LAST DAY OF MONTH                       
         B     DAYSX                                                            
                                                                                
NDAY1ST  LLC   RF,SAVE+1           GET MM & INCREMENT IT                        
         LA    RF,1(RF)                                                         
         STC   RF,SAVE+1                                                        
         CLI   SAVE+1,12           SEE IF YEAR CHANGED                          
         BNH   DAY1ST              NO                                           
*                                                                               
         MVI   SAVE+1,1            SET MM TO JANUARY                            
         IC    RF,SAVE             GET YY & INCREMENT IT                        
         LA    RF,1(RF)                                                         
         STC   RF,SAVE                                                          
         CLI   SAVE,99             WAS OLD YEAR 1999?                           
         BNH   DAY1ST              NO                                           
*                                                                               
         MVI   CENTURY,20          SET WHOLE NEW CENTURY                        
         AHI   RF,-100                                                          
         STC   RF,SAVE                                                          
         B     DAY1ST                                                           
                                                                                
NDAYLST  LLC   RF,SAVE+1           GET MM & INCREMENT IT                        
         LA    RF,1(RF)                                                         
         STC   RF,SAVE+1                                                        
         CLI   SAVE+1,12           SEE IF YEAR CHANGED                          
         BNH   DAYLST              NO                                           
*                                                                               
         MVI   SAVE+1,1            SET MM TO JANUARY                            
         IC    RF,SAVE             GET YY & INCREMENT IT                        
         LA    RF,1(RF)                                                         
         STC   RF,SAVE                                                          
         CLI   SAVE,99             WAS OLD YEAR 1999?                           
         BNH   DAYLST              NO                                           
*                                                                               
         MVI   CENTURY,20          SET WHOLE NEW CENTURY                        
         AHI   RF,-100                                                          
         STC   RF,SAVE                                                          
         B     DAYLST                                                           
                                                                                
PDAY1ST  LLC   RF,SAVE+1           GET MM & DECREMENT IT                        
         BCTR  RF,0                                                             
         STC   RF,SAVE+1                                                        
         CLI   SAVE+1,1            SEE IF YEAR CHANGED                          
         BNL   DAY1ST              NO                                           
*                                                                               
         MVI   SAVE+1,12           SET MM TO DECEMBER                           
         ICM   RF,1,SAVE           GET YY & DECREMENT IT                        
         BZ    PD1ST02             Y2000                                        
         BCTR  RF,0                                                             
         STC   RF,SAVE                                                          
         B     DAY1ST                                                           
*                                                                               
PD1ST02  MVI   CENTURY,19          SET LAST CENTURY                             
         MVI   SAVE,99                                                          
         B     DAY1ST                                                           
                                                                                
PDAYLST  LLC   RF,SAVE+1           GET MM & DECREMENT IT                        
         BCTR  RF,0                                                             
         STC   RF,SAVE+1                                                        
         CLI   SAVE+1,1            SEE IF YEAR CHANGED                          
         BNL   DAYLST              NO                                           
*                                                                               
         MVI   SAVE+1,12           SET MM TO DECEMBER                           
         ICM   RF,1,SAVE           GET YY & DECREMENT IT                        
         BZ    PDLST02             Y2000                                        
         BCTR  RF,0                                                             
         STC   RF,SAVE                                                          
         B     DAYLST                                                           
*                                                                               
PDLST02  MVI   CENTURY,19          SET LAST CENTURY                             
         MVI   SAVE,99                                                          
         B     DAYLST                                                           
                                                                                
ADAY     ICM   R0,15,12(RA)        R0=DAYS TO ADD OR SUBTRACT                   
         BZ    DAYSX                                                            
*                                                                               
ADAY1    STM   R2,R4,DAYSR234      SAVE MAIN CODE REGISTERS                     
         LLC   R2,SAVE+2           R2=DAY                                       
         LLC   R3,SAVE+1           R3=MONTH                                     
         LLC   R4,SAVE             R4=YEAR                                      
         LTR   R0,R0                                                            
         BM    SDAY                NEGATIVE NUMBER PASSED SO SUBTRACT           
*                                                                               
ADAY2    LA    RF,DAYSMON(R3)      GET NUMBER OF DAYS IN MONTH                  
         LLC   R8,0(RF)            R8=MAX DAYS IN MONTH                         
         CHI   R3,2                                                             
         BNE   ADAY4                                                            
         TMLL  GR4,X'0003'         TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   ADAY4                                                            
         AHI   R8,1                ADJUST DAYS IN FEB FOR LEAP YEAR             
*                                                                               
ADAY4    CLR   R2,R8               TEST DAY NUMBER WITH MAX IN MONTH            
         BE    ADAY6                                                            
         AHI   R2,1                INCREMENT INPUT DAYS                         
         BCT   R0,ADAY4            DECREMENT DAYS TO ADD AND LOOP               
         B     ADAYX                                                            
*                                                                               
ADAY6    LHI   R2,1                END OF MONTH - CONVERT TO NEXT               
         AHI   R3,1                INCREMENT MONTH                              
         CHI   R3,12                                                            
         BH    ADAY8                                                            
         BCT   R0,ADAY2            DECREMENT DAYS TO ADD AND LOOP               
         B     ADAYX                                                            
*                                                                               
ADAY8    LHI   R3,1                END OF YEAR - CONVERT TO NEXT                
         AHI   R4,1                INCREMENT YEAR                               
         BCT   R0,ADAY2            DECREMENT DAYS TO ADD AND LOOP               
*                                                                               
ADAYX    STC   R2,SAVE+2           DAY                                          
         STC   R3,SAVE+1           MONTH                                        
         STC   R4,SAVE             YEAR                                         
         LM    R2,R4,DAYSR234      RESTORE CALLERS REGISTERS                    
         B     DAYSX                                                            
*                                                                               
SDAY     LPR   R0,R0               SUBTRACT DAYS - CONVERT TO POSITIVE          
SDAY4    AHI   R2,-1               DECREMENT DAY NUMBER                         
         BNP   SDAY6                                                            
         BCT   R0,SDAY4            DECREMENT DAYS TO SUBTRACT AND LOOP          
         B     ADAYX                                                            
*                                                                               
SDAY6    AHI   R3,-1               ADJUST TO END OF PREVIOUS MONTH              
         BNP   SDAY8                                                            
         LA    RF,DAYSMON(R3)                                                   
         LLC   R2,0(RF)            SET DAY NUM TO MAX DAYS IN MONTH             
         CHI   R3,2                                                             
         BNE   SDAY10                                                           
         TMLL  GR4,X'0003'         TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   SDAY10                                                           
         AHI   R2,1                ADJUST DAYS IN FEB FOR LEAP YEAR             
         B     SDAY10                                                           
*                                                                               
SDAY8    AHI   R4,-1               CONVERT TO END OF LAST YEAR                  
         BNM   *+8                                                              
         LA    R4,99               WRAP AROUND FOR CENTURY                      
         LA    R2,31               DAY NUMBER                                   
         LA    R3,12               MONTH NUMBER                                 
*                                                                               
SDAY10   BCT   R0,SDAY4            DECREMENT DAYS TO SUBTRACT AND LOOP          
         B     ADAYX                                                            
                                                                                
AMONLST  OI    DAYADJ,X'80'        SET OUTPUT DAY TO LAST DAY OF MONTH          
         ICM   R0,15,12(RA)        MONTHS CAN BE ZERO FOR THIS OPTION           
         B     AMON1                                                            
AMONLSX  OI    DAYADJ,X'40'        DONT SET TO LAST IF INPUT DAY LAST           
*                                                                               
AMON     ICM   R0,15,12(RA)        R0=MONTHS TO ADD OR SUBTRACT                 
         BZ    DAYSX                                                            
*                                                                               
AMON1    STM   R2,R4,DAYSR234      SAVE MAIN CODE REGISTERS                     
         LLC   R2,SAVE+2           R2=DAY                                       
         LLC   R3,SAVE+1           R3=MONTH                                     
         LLC   R4,SAVE             R4=YEAR                                      
*                                                                               
AMON2    LA    RF,DAYSMON(R3)      ADD/SUBTRACT MONTHS                          
         LLC   R8,0(RF)            R8=MAX DAYS IN INPUT MONTH                   
         CHI   R3,2                                                             
         BNE   AMON3                                                            
         TMLL  GR4,X'0003'         TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   *+8                                                              
         AHI   R8,1                                                             
AMON3    LTR   R0,R0               TEST NEGATIVE MONTHS PASSED                  
         BM    SMON                                                             
*                                                                               
AMON4    SR    RE,RE               COMPUTE YEARS AND MONTHS                     
         LR    RF,R0               R0=NUMBER OF MONTHS (INPUT VALUE)            
         D     RE,=F'12'           RE=MONTHS,RF=YEARS                           
         AR    R3,RE               R3=OUTPUT MONTH                              
         CHI   R3,12                                                            
         BNH   *+12                                                             
         AHI   R3,-12                                                           
         AHI   RF,1                                                             
         AR    R4,RF               R4=OUTPUT YEAR                               
*                                                                               
AMON6    LA    RF,DAYSMON(R3)      GET NUMBER OF DAYS IN OUTPUT MONTH           
         LLC   R9,0(RF)            R9=MAX DAYS IN OUTPUT MONTH                  
         CHI   R3,2                                                             
         BNE   AMON10                                                           
         TMLL  GR4,X'0003'         TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   AMON10                                                           
         AHI   R9,1                                                             
*                                                                               
AMON10   CLR   R2,R9               IS DAY > MAXIMUM FOR OUTPUT MONTH            
         BNH   AMON12                                                           
         LR    R2,R9               YES-WE HAVE TO SET TO MAXIMUM                
         B     ADAYX                                                            
AMON12   TM    DAYADJ,X'80'        TEST IF ALWAYS SET LAST DAY OF MONTH         
         BO    AMON14                                                           
         CLR   R2,R8               IS DAY=MAXIMUM FOR INPUT MONTH               
         BNE   ADAYX                                                            
         TM    DAYADJ,X'40'        TEST IF DONT WANT DAYS ADJUST                
         BO    ADAYX                                                            
AMON14   LR    R2,R9               SET DAY TO MAXIMUM FOR OUTPUT MONTH          
         B     ADAYX                                                            
*                                                                               
SMON     LPR   R0,R0               SUBTRACT MONTHS - COMPLEMENT R0              
SMON4    SR    RE,RE               COMPUTE YEARS AND MONTHS                     
         LR    RF,R0               R0=NUMBER OF MONTHS (INPUT VALUE)            
         D     RE,=F'12'           RE=MONTHS,RF=YEARS                           
         SR    R3,RE               R3=OUTPUT MONTH                              
         BP    SMON6                                                            
         AHI   R3,12                                                            
         AHI   RF,1                                                             
SMON6    SR    R4,RF               R4=OUTPUT YEAR                               
         BNM   SMON8                                                            
         LA    R4,99               WRAP IF YEAR GOES NEGATIVE                   
*                                                                               
SMON8    B     AMON6               FUSS WITH OUTPUT DAY - SAME AS ADD           
                                                                                
AYEARLST OI    DAYADJ,X'80'        SET OUTPUT DAY TO LAST DAY OF MONTH          
         ICM   R0,15,12(RA)        YEARS CAN BE ZERO FOR THIS OPTION            
         B     AYEAR1                                                           
AYEARLSX OI    DAYADJ,X'40'        DONT SET TO LAST IF INPUT DAY LAST           
*                                                                               
AYEAR    ICM   R0,15,12(RA)        R0=YEARS TO ADD OR SUBTRACT                  
         BZ    DAYSX                                                            
*                                                                               
AYEAR1   STM   R2,R4,DAYSR234      SAVE MAIN CODE REGISTERS                     
         LLC   R2,SAVE+2           R2=DAY                                       
         LLC   R3,SAVE+1           R3=MONTH                                     
         LLC   R4,SAVE             R4=YEAR                                      
*                                                                               
AYEAR2   LA    RF,DAYSMON(R3)      ADD/SUBTRACT YEARS                           
         LLC   R8,0(RF)            R8=MAX DAYS IN INPUT MONTH                   
         CHI   R3,2                                                             
         BNE   AYEAR3                                                           
         TMLL  GR4,X'0003'         TEST LEAP YEAR (DIVISIBLE BY 4)              
         BNZ   *+8                                                              
         AHI   R8,1                                                             
*                                                                               
AYEAR3   LTR   R0,R0               TEST NEGATIVE YEARS PASSED                   
         BM    SYEAR                                                            
         AR    R4,R0               ADD YEARS                                    
         B     AMON6               PROCESS DAYS IN OUTPUT MONTH                 
*                                                                               
SYEAR    LPR   R0,R0               SUBTRACT YEARS - COMPLEMENT R0               
         SR    R4,R0                                                            
         BNM   *+8                                                              
         LA    R4,99               WRAP AROUND FOR CENTURY                      
         B     AMON6               PROCESS DAYS IN OUTPUT MONTH                 
         EJECT                                                                  
***********************************************************************         
* PASS BACK TABLE OF DAY NAMES AND MONTH NAMES BASED ON LANGUAGE      *         
* LANGUAGE PASSED IN P2 HIGH ORDER BYTE OR FF FOR DEFAULT             *         
***********************************************************************         
DCD      LLC   R5,LANG             SET R5 TO DEFAULT LANGUAGE                   
         CLI   4(RA),7                                                          
         BH    *+8                                                              
         IC    R5,4(RA)            GET LANGUAGE PASSED IN P2                    
         LR    RF,R5                                                            
         MHI   RF,21               INDEX INTO DAYS TABLE                        
         A     RF,=A(DAYTAB)                                                    
         A     RF,RELO                                                          
         MVC   ODAYS(21),0(RF)                                                  
         LR    RF,R5                                                            
         MHI   RF,36                                                            
         A     RF,=A(MONTAB)       INDEX INTO MONTHS TABLE                      
         A     RF,RELO                                                          
         MVC   OMONTHS(36),0(RF)                                                
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    DCD2                                                             
         NC    ODAYS(21),LOWERDM                                                
         NC    OMONTHS(36),LOWERDM                                              
*                                                                               
DCD2     LA    R2,ODAYS            PASS BACK A(DAYS TABLE)                      
         STCM  R2,7,1(RA)                                                       
         STC   R5,4(RA)                                                         
         LA    R3,OMONTHS          PASS BACK A(MONTHS TABLE)                    
         STCM  R3,7,5(RA)                                                       
         B     DATEND                                                           
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         DS    0F                                                               
DAYSINMO DC    AL2(31,28,31,30,31,30,31,31,30,31,30,31)                         
DAYSMON  DC    AL1(00,31,28,31,30,31,30,31,31,30,31,30,31,00,00,00)             
*&&UK                                                                           
DAGCOPT  DC    X'01'                                                            
DAGCTRY  DC    X'01'                                                            
DCTRY    DC    X'01'                                                            
DLANG    DC    X'01'                                                            
*&&                                                                             
*&&US                                                                           
DAGCOPT  DC    X'02'                                                            
DAGCTRY  DC    X'02'                                                            
DCTRY    DC    X'02'                                                            
DLANG    DC    X'02'                                                            
*&&                                                                             
LOWERDM  DC    12X'FFBFBF'                                                      
LOWER    DC    X'FF',35X'BF'                                                    
SPACES   DC    CL36' '                                                          
ADJOUTND DC    X'000102030406060709090A0B0C0E0E0F'                              
         DC    X'101212131416161718191A1B1C1D1E1F'                              
         LTORG                                                                  
         EJECT                                                                  
MONTAB   DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'  LANG=0                  
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'  LANG=1                  
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'  LANG=2                  
         DC    C'JANFEBMRZAPRMAIJUNJULAUGSEPOKTNOVDEZ'  LANG=3                  
         DC    C'JANFEVMARAVRMAIJUNJULAOUSEPOCTNOVDEC'  LANG=4                  
         DC    C'ENEFEBMARABRMAYJUNJULAGOSEPOCTNOVDIC'  LANG=5                  
         DC    C'GENFEBMARAPRMAGJUNLUGAGOSETOTTNOVDIC'  LANG=6                  
         DC    C'JANFEBMRTAPRMEIJUNJULAUGSEPOKTNOVDEC'  LANG=7                  
*                                                                               
DAYTAB   DC    C'MONTUEWEDTHUFRISATSUN'                 LANG=0                  
         DC    C'MONTUEWEDTHUFRISATSUN'                 LANG=1                  
         DC    C'MONTUEWEDTHUFRISATSUN'                 LANG=2                  
         DC    C'MONDIEMITDONFRESAMSON'                 LANG=3                  
         DC    C'LUNMARMERJEUVENSAMDIM'                 LANG=4                  
         DC    C'LUNMARMIEJUEVIESABDOM'                 LANG=5                  
         DC    C'LUNMARMERGIOVENSABDOM'                 LANG=6                  
         DC    C'MAADINWOEDONVRIZATZON'                 LANG=7                  
*                                                                               
PERFROML DC    H'16'                                                            
PERFROM  DC    AL1(11),CL15'PERIOD FROM'                LANG=0                  
         DC    AL1(11),CL15'PERIOD FROM'                LANG=1                  
         DC    AL1(11),CL15'PERIOD FROM'                LANG=2                  
         DC    AL1(15),CL15'ZEITRAUM ANFANG'            LANG=3                  
         DC    AL1(10),CL15'PERIODE DE'                 LANG=4                  
         DC    AL1(13),CL15'PERIODO DESDE'              LANG=5                  
         DC    AL1(10),CL15'PERIODO DA'                 LANG=6                  
         DC    AL1(11),CL15'PERIODE VAN'                LANG=7                  
*                                                                               
PERTOL   DC    H'16'                                                            
PERTO    DC    AL1(09),CL15'PERIOD TO'                  LANG=0                  
         DC    AL1(09),CL15'PERIOD TO'                  LANG=1                  
         DC    AL1(09),CL15'PERIOD TO'                  LANG=2                  
         DC    AL1(12),CL15'ZEITRAUMENDE'               LANG=3                  
         DC    AL1(11),CL15'PERIODE FIN'                LANG=4                  
         DC    AL1(13),CL15'PERIODO HASTA'              LANG=5                  
         DC    AL1(09),CL15'PERIODO A'                  LANG=6                  
         DC    AL1(11),CL15'PERIODE T/M'                LANG=7                  
*                                                                               
PERFRTOL DC    H'16'                                                            
PERFRTO  DC    AL1(06),CL15'PERIOD'                     LANG=0                  
         DC    AL1(06),CL15'PERIOD'                     LANG=1                  
         DC    AL1(06),CL15'PERIOD'                     LANG=2                  
         DC    AL1(08),CL15'ZEITRAUM'                   LANG=3                  
         DC    AL1(07),CL15'PERIODE'                    LANG=4                  
         DC    AL1(07),CL15'PERIODO'                    LANG=5                  
         DC    AL1(07),CL15'PERIODO'                    LANG=6                  
         DC    AL1(07),CL15'PERIODE'                    LANG=7                  
         EJECT                                                                  
***********************************************************************         
* TABLE OF DEFAULT LENGTHS/A(VALUES) FOR ZERO INPUT DATE              *         
* INDEXED BY THE OUTPUT TYPE DATE FORMAT VALUE                        *         
***********************************************************************         
*&&US                                                                           
DEFOUT   DS    0F                                                               
         DC    X'06',AL3(DEFOUT00)                                              
         DC    X'03',AL3(DEFOUT01)                                              
         DC    X'02',AL3(DEFOUT02)                                              
         DC    X'03',AL3(DEFOUT03)                                              
         DC    X'05',AL3(DEFOUT04)                                              
         DC    X'08',AL3(DEFOUT05)                                              
         DC    X'06',AL3(DEFOUT06)                                              
         DC    X'05',AL3(DEFOUT07)                                              
         DC    X'08',AL3(DEFOUT08)                                              
         DC    X'06',AL3(DEFOUT09)                                              
         DC    X'08',AL3(DEFOUT0A)                                              
         DC    X'08',AL3(DEFOUT0B)                                              
         DC    X'05',AL3(DEFOUT0C)                                              
         DC    X'08',AL3(DEFOUT0D)                                              
         DC    X'06',AL3(DEFOUT0E)                                              
         DC    X'04',AL3(DEFOUT0F)                                              
         DC    X'05',AL3(DEFOUT10)                                              
         DC    X'08',AL3(DEFOUT11)                                              
         DC    X'06',AL3(DEFOUT12)                                              
         DC    X'03',AL3(DEFOUT13)                                              
         DC    X'08',AL3(DEFOUT14)                                              
         DC    X'0A',AL3(DEFOUT15)                                              
         DC    X'08',AL3(DEFOUT16)                                              
         DC    X'0A',AL3(DEFOUT17)                                              
         DC    X'04',AL3(DEFOUT18)                                              
         DC    X'06',AL3(DEFOUT19)                                              
         DC    X'02',AL3(DEFOUT1A)                                              
         DC    X'02',AL3(DEFOUT1B)                                              
         DC    X'02',AL3(DEFOUT1C)                                              
         DC    X'02',AL3(DEFOUT1D)                                              
         DC    X'02',AL3(DEFOUT1E)                                              
         DC    X'02',AL3(DEFOUT1F)                                              
*                                                                               
DEFOUT00 DC    CL6' '                                                           
DEFOUT01 DC    XL3'00'                                                          
DEFOUT02 DC    XL2'00'                                                          
DEFOUT03 DC    XL3'00'                                                          
DEFOUT04 DC    CL5' '                                                           
DEFOUT05 DC    CL8' '                                                           
DEFOUT06 DC    CL6' '                                                           
DEFOUT07 DC    CL5' '                                                           
DEFOUT08 DC    CL8' '                                                           
DEFOUT09 DC    CL6' '                                                           
DEFOUT0A DC    CL8' '                                                           
DEFOUT0B DC    CL8' '                                                           
DEFOUT0C DC    CL5' '                                                           
DEFOUT0D DC    CL8' '                                                           
DEFOUT0E DC    CL6' '                                                           
DEFOUT0F DC    XL4'0000000F'                                                    
DEFOUT10 DC    CL5' '                                                           
DEFOUT11 DC    CL8' '                                                           
DEFOUT12 DC    CL6' '                                                           
DEFOUT13 DC    XL3'00'                                                          
DEFOUT14 DC    CL8' '                                                           
DEFOUT15 DC    CL10' '                                                          
DEFOUT16 DC    CL8' '                                                           
DEFOUT17 DC    CL10' '                                                          
DEFOUT18 DC    XL4'00'                                                          
DEFOUT19 DC    XL6'00'                                                          
DEFOUT1A DC    XL2'00'                                                          
DEFOUT1B DC    XL2'00'                                                          
DEFOUT1C DC    XL2'00'                                                          
DEFOUT1D DC    XL2'00'                                                          
DEFOUT1E DC    XL2'00'                                                          
DEFOUT1F DC    XL2'00'                                                          
*&&                                                                             
*&&UK                                                                           
DEFOUT   DS    0F                                                               
         DC    X'06',AL3(DEFOUT00)                                              
         DC    X'03',AL3(DEFOUT01)                                              
         DC    X'02',AL3(DEFOUT02)                                              
         DC    X'03',AL3(DEFOUT03)                                              
         DC    X'06',AL3(DEFOUT04)                                              
         DC    X'09',AL3(DEFOUT05)                                              
         DC    X'06',AL3(DEFOUT06)                                              
         DC    X'08',AL3(DEFOUT07)                                              
         DC    X'08',AL3(DEFOUT08)                                              
         DC    X'05',AL3(DEFOUT09)                                              
         DC    X'08',AL3(DEFOUT0A)                                              
         DC    X'08',AL3(DEFOUT0B)                                              
         DC    X'05',AL3(DEFOUT0C)                                              
         DC    X'08',AL3(DEFOUT0D)                                              
         DC    X'05',AL3(DEFOUT0E)                                              
         DC    X'04',AL3(DEFOUT0F)                                              
         DC    X'05',AL3(DEFOUT10)                                              
         DC    X'08',AL3(DEFOUT11)                                              
         DC    X'05',AL3(DEFOUT12)                                              
         DC    X'03',AL3(DEFOUT13)                                              
         DC    X'08',AL3(DEFOUT14)                                              
         DC    X'09',AL3(DEFOUT15)                                              
         DC    X'07',AL3(DEFOUT16)                                              
         DC    X'0A',AL3(DEFOUT17)                                              
         DC    X'04',AL3(DEFOUT18)                                              
         DC    X'06',AL3(DEFOUT19)                                              
         DC    X'02',AL3(DEFOUT1A)                                              
         DC    X'02',AL3(DEFOUT1B)                                              
         DC    X'02',AL3(DEFOUT1C)                                              
         DC    X'02',AL3(DEFOUT1D)                                              
         DC    X'02',AL3(DEFOUT1E)                                              
         DC    X'02',AL3(DEFOUT1F)                                              
*                                                                               
DEFOUT00 DC    CL6' '                                                           
DEFOUT01 DC    XL3'00'                                                          
DEFOUT02 DC    XL2'00'                                                          
DEFOUT03 DC    XL3'00'                                                          
DEFOUT04 DC    CL6' '                                                           
DEFOUT05 DC    CL9' '                                                           
DEFOUT06 DC    CL6' '                                                           
DEFOUT07 DC    CL8' '                                                           
DEFOUT08 DC    CL8' '                                                           
DEFOUT09 DC    CL5' '                                                           
DEFOUT0A DC    CL8' '                                                           
DEFOUT0B DC    CL8' '                                                           
DEFOUT0C DC    CL5' '                                                           
DEFOUT0D DC    CL8' '                                                           
DEFOUT0E DC    CL5' '                                                           
DEFOUT0F DC    XL4'0000000F'                                                    
DEFOUT10 DC    CL5' '                                                           
DEFOUT11 DC    CL8' '                                                           
DEFOUT12 DC    CL5' '                                                           
DEFOUT13 DC    XL3'00'                                                          
DEFOUT14 DC    CL8' '                                                           
DEFOUT15 DC    CL9' '                                                           
DEFOUT16 DC    CL7' '                                                           
DEFOUT17 DC    CL10' '                                                          
DEFOUT18 DC    XL4'00'                                                          
DEFOUT19 DC    XL6'00'                                                          
DEFOUT1A DC    XL2'00'                                                          
DEFOUT1B DC    XL2'00'                                                          
DEFOUT1C DC    XL2'00'                                                          
DEFOUT1D DC    XL2'00'                                                          
DEFOUT1E DC    XL2'00'                                                          
DEFOUT1F DC    XL2'00'                                                          
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* OUTPUT FOR AMERICAN DATES - RETURN VIA RE DIRECT TO CALLER          *         
***********************************************************************         
         DROP  RB                  RF=BASE REGISTER FOR AMER ROUTINE            
         USING AMER,RF                                                          
AMER     CLI   OUTFMT,X'0A'        TEST IPL FORMAT WANTED                       
         BNE   AMER1                                                            
         LA    R6,WORK             6 BYTE YYMMDD                                
         LA    R1,3                                                             
         SR    R8,R8                                                            
         LA    R9,SAVE                                                          
AMER0    IC    R8,0(R9)                                                         
         CVD   R8,DUB                                                           
         UNPK  0(2,R6),DUB                                                      
         OI    1(R6),X'F0'                                                      
         LA    R9,1(R9)                                                         
         LA    R6,2(R6)                                                         
         BCT   R1,AMER0                                                         
         MVC   0(2,R3),WORK+2      MM/DD/YY                                     
         MVI   2(R3),C'/'                                                       
         MVC   3(2,R3),WORK+4                                                   
         MVI   5(R3),C'/'                                                       
         MVC   6(2,R3),WORK                                                     
         LA    R3,8(R3)                                                         
         B     AMERX                                                            
*                                                                               
AMER1    LLC   R1,LANG             GET ALPHABETIC MONTH                         
         MHI   R1,36                                                            
         A     R1,=A(MONTAB)                                                    
         A     R1,RELO                                                          
         LLC   R8,SAVE+1                                                        
         BCTR  R8,0                                                             
         MHI   R8,3                                                             
         AR    R8,R1                                                            
         MVC   0(3,R3),0(R8)                                                    
         TM    SVINPFMT,X'40'      TEST LOWER CASE                              
         BZ    *+10                                                             
         NC    0(3,R3),=X'FFBFBF'                                               
         LA    R3,3(R3)                                                         
         CLI   OUTFMT,X'06'        TEST MONTH.YEAR FORMAT                       
         BE    AMER2                                                            
         CLI   OUTFMT,X'09'                                                     
         BE    AMER2                                                            
         CLI   OUTFMT,X'12'                                                     
         BE    AMER2                                                            
         CLI   OUTFMT,X'0E'                                                     
         BE    AMER2                                                            
*                                                                               
         LLC   R8,SAVE+2           DAY                                          
         CVD   R8,DUB                                                           
         UNPK  0(2,R3),DUB                                                      
         OI    1(R3),X'F0'                                                      
         LA    R3,2(R3)                                                         
         CLI   OUTFMT,X'04'        TEST DAY.MONTH FORMAT                        
         BE    AMERX                                                            
         CLI   OUTFMT,X'07'                                                     
         BE    AMERX                                                            
         CLI   OUTFMT,X'10'                                                     
         BE    AMERX                                                            
         CLI   OUTFMT,X'0C'                                                     
         BE    AMERX                                                            
         CLI   SAVE+2,0                                                         
         BNE   *+8                                                              
         AHI   R3,-2                                                            
*                                                                               
AMER2    LLC   R8,SAVE             YEAR                                         
         CVD   R8,DUB                                                           
         MVI   0(R3),C'/'                                                       
         CLI   OUTFMT,X'15'        IF TYPE 15 OR 16                             
         BE    *+12                                                             
         CLI   OUTFMT,X'16'                                                     
         BNE   AMER3                                                            
         MVC   1(2,R3),=C'19'      OUTPUT CENTURY                               
         CLI   CENTURY,20                                                       
         BNE   *+10                                                             
         MVC   1(2,R3),=C'20'                                                   
         LA    R3,2(R3)                                                         
AMER3    UNPK  1(2,R3),DUB                                                      
         OI    2(R3),X'F0'                                                      
         LA    R3,3(R3)                                                         
*                                                                               
AMERX    BR    RE                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
DATD     DSECT                                                                  
DMCB     DS    0XL24                                                            
DUB      DS    D                                                                
DUB1     DS    D                                                                
WORK     DS    D                                                                
*                                                                               
SAVE     DS    D                                                                
FULL     DS    F                                                                
RELO     DS    A                                                                
SAVERE   DS    A                                                                
DAYSRE   DS    F                                                                
DAYSR234 DS    3F                                                               
*                                                                               
DATEINFA DS    A                                                                
DATEINFO DS    0XL16                                                            
DATEINF1 DS    2F                                                               
DATEINF2 DS    2F                                                               
*                                                                               
AGCOPT   DS    X                                                                
AGCTRY   DS    X                                                                
CTRY     DS    X                                                                
LANG     DS    X                                                                
*                                                                               
TODAY    DS    PL4                                                              
TIMENOW  DS    F                                                                
SECS     DS    F                                                                
*                                                                               
LINGO    DS    X                                                                
DATYPE   DS    C                                                                
DATYPEUK EQU   C'E'                                                             
DATYPEUS EQU   C'A'                                                             
INPFMT   DS    X                                                                
SVINPFMT DS    X                                                                
INPLEN   DS    X                                                                
OUTFMT   DS    X                                                                
SVOUTFMT DS    X                                                                
OUTLEN   DS    X                                                                
ODAYS    DS    CL21                                                             
OMONTHS  DS    CL36                                                             
CENTURY  DS    X                                                                
SVDAYFMT DS    X                                                                
DAYADJ   DS    X                                                                
DATX     EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* OTHER SYSTEM DSECTS                                                 *         
***********************************************************************         
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
* FASSBOFF                                                                      
       ++INCLUDE FASSBOFF                                                       
* FATCB                                                                         
       ++INCLUDE FATCB                                                          
* DMDSYHDR                                                                      
       ++INCLUDE DMDSYSHDR                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDDATCON  10/17/18'                                      
         END                                                                    
