*          DATA SET CXSUPC     AT LEVEL 012 AS OF 05/01/02                      
*CATALP CXSUPC                                                                  
         TITLE 'CXSUPC - CONTROL SEC. EXTRACT - USER ID PROGRAM RECORD'         
**********************************************************************          
* AL4 A(EXTRACT RECORD)                                              *          
* AL4 A(CONFILE RECORD)                                              *          
**********************************************************************          
         SPACE 1                                                                
         PRINT NOGEN                                                            
CXSUPC   CSECT                                                                  
         NMOD1 WORKX-WORKD,*CXSUPC*,RR=RA                                       
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         ST    RA,RELO                                                          
         MVC   PARM,0(R1)          EXTRACT PARAMETER LIST                       
         LM    R2,R3,0(R1)                                                      
         USING CXSUPD,R2           R2=A(EXTRACT RECORD)                         
         USING CTIREC,R3           R3=A(CONFILE RECORD)                         
*                                                                               
CXSLEN   XC    CXSUPLEN,CXSUPLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   CXSUPLEN(2),=AL2(CXSUPDL)                                        
         MVC   CXSUPTYP,CXSUPDQ                                                 
         MVI   CXSUPDX,MXTRTQ                                                   
         MVI   CXSUPDX+1,MXTRTQ                                                 
*                                                                               
CXSNUM   MVI   CXSUPNUM-1,MXTRTQ   USER ID NUMBER                               
         LA    R5,CTIDATA          R4=A(FIRST ELEMENT)                          
*                                                                               
CXSN010  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BNE   *+6                                                              
         DC    H'00'                                                            
         CLI   0(R5),X'02'         GET USER ID NUMBER ELEMENT                   
         BE    CXSN030                                                          
CXSN020  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BH    *+6                                                              
         DC    H'00'                                                            
         AR    R5,RF                                                            
         B     CXSN010                                                          
*                                                                               
CXSN030  SR    RF,RF                                                            
         ICM   RF,3,2(R5)                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  CXSUPNUM,DUB+5(3)                                                
         B     CXSSYS                                                           
*                                                                               
         USING SPATABD,R4                                                       
CXSSYS   MVI   CXSUPSYS-1,MXTRTQ                                                
         MVI   CXSUPPGM-1,MXTRTQ                                                
         MVI   CXSUPLAC-1,MXTRTQ                                                
         MVI   CXSUPAUT-1,MXTRTQ                                                
         ICM   R4,15,ASPATAB       GET LAST ADDRESS SPATAB ENTRY                
         BZ    *+12                                                             
         LA    R4,SPATLEN(R4)      BUMP ADDRESS SPATAB ENTRY                    
         B     CXSS010                                                          
         ICM   R4,15,=V(CXSSPAT)   ELSE INITIALISE TO START                     
         BNZ   *+6                                                              
         DC    H'00'                                                            
*                                                                               
CXSS010  CLI   SPATOVS,0                                                        
         BE    EOFEXIT                                                          
         ST    R4,ASPATAB                                                       
*                                  SYSTEM CODE                                  
         GOTO1 XHEXOUT,DMCB,SPATOVS,CXSUPSYS,1,=C'TOG'                          
*                                  PROGRAM CODE                                 
         GOTO1 XHEXOUT,DMCB,SPATPGM,CXSUPPGM,1,=C'TOG'                          
         MVC   CXSUPLAC,ZEROES                                                  
         MVC   CXSUPAUT,ZEROES                                                  
         LA    R5,CTIDATA          R4=A(FIRST ELEMENT)                          
*                                                                               
         USING CTSYSD,R5                                                        
CXSS020  CLI   0(R5),0             GET ELEMENT FROM RECORD                      
         BE    CXSSNO                                                           
         CLI   0(R5),CTSYSELQ      GET SYSTEM ELEMENT                           
         BNE   CXSS030                                                          
         CLC   CTSYSNUM,SPATOVS                                                 
         BE    CXSS040                                                          
CXSS030  SR    RF,RF               BUMP TO NEXT ELEMENT                         
         IC    RF,1(R5)                                                         
         CH    RF,=H'1'                                                         
         BH    *+6                                                              
         DC    H'00'                                                            
         AR    R5,RF                                                            
         B     CXSS020                                                          
*                                                                               
CXSS040  GOTO1 XHEXOUT,DMCB,CTSYSLMT,CXSUPLAC,4,=C'TOG'                         
         MVC   PROGRAM,SPATPGM                                                  
         BAS   RE,GETPAVAL                                                      
         OC    PACCVAL,PACCVAL                                                  
         BZ    CXSSNO                                                           
         GOTO1 XHEXOUT,DMCB,PACCVAL,CXSUPAUT,2,=C'TOG'                          
         B     RETEXIT                                                          
         DROP  R5                                                               
*                                                                               
CXSSNO   MVC   CXSUPAUT,ZEROES                                                  
         B     RETEXIT                                                          
*                                                                               
RETEXIT  L     R1,APARM                                                         
         OI    8(R1),X'40'         SET RECORD RETURNED                          
         B     EXIT                                                             
*                                                                               
EOFEXIT  XC    ASPATAB,ASPATAB                                                  
         L     R1,APARM                                                         
         OI    8(R1),X'88'         SET EOF RETURN                               
         B     EXIT                                                             
*                                                                               
DELREC   L     R1,APARM            DELETE THIS RECORD                           
         MVI   8(R1),X'80'                                                      
         B     EXIT                                                             
*                                                                               
EXIT     XMOD1 1                                                                
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
* SUBROUTINE TO GET PROGRAM ACCESS CODE FROM SYSTEM ELEMENT                     
* PROGRAM CODE IN PROGRAM, ACCESS CODE IN PACCVAL                               
* R5=A(SYSTEM ELEMENT)                                                          
*                                                                               
         USING CTSYSD,R5                                                        
GETPAVAL NTR1                                                                   
         MVC   PACCVAL,CTSYSALL                                                 
         LA    R1,CTSYSPGM                                                      
         ZIC   RE,CTSYSLEN                                                      
*                                                                               
GPAV010  CH    RE,=H'16'                                                        
         BNH   GPAVNO                                                           
         CLC   PROGRAM,0(R1)                                                    
         BE    GPAVYES                                                          
         LA    R1,3(R1)            GET NEXT PROGAM CODE                         
         SH    RE,=H'3'                                                         
         B     GPAV010                                                          
*                                                                               
GPAVNO   B     GPAVX                                                            
GPAVYES  MVC   PACCVAL,1(R1)       SAVE ACCESS CODE                             
         ST    R1,PACCADR          SAVE ADDRESS OF ENTRY IN ELEMENT             
         SR    RC,RC                                                            
GPAVX    LTR   RC,RC                                                            
         XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
       ++INCLUDE MXTRT                                                          
       ++INCLUDE CXSRECID                                                       
         SPACE 1                                                                
         DS    0F                                                               
ASPATAB  DC    A(0)                                                             
XHEXOUT  DC    V(HEXOUT)                                                        
         SPACE 1                                                                
SPACES   DC    80C' '                                                           
ZEROES   DC    80C'0'                                                           
         SPACE 1                                                                
         EJECT                                                                  
* INCLUDE CXTRACT LOCAL DSECTS                                                  
         SPACE 1                                                                
       ++INCLUDE CXTRACTD                                                       
         EJECT                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
PARM     DS    0XL16               PARAMETER LIST                               
DMCB     DS    6F                                                               
AXREC    DS    A                   A(EXTRACT RECORD)                            
CFREC    DS    A                   A(CONFILE RECORD)                            
         DS    A                                                                
         DS    A                                                                
*                                                                               
DUB      DS    D                                                                
APARM    DS    A                   A(PARAMETER LIST)                            
RELO     DS    A                                                                
*                                                                               
PACCADR  DS    A                                                                
PROGRAM  DS    XL1                                                              
PACCVAL  DS    XL2                                                              
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE CXSUPD                                                         
         EJECT                                                                  
       ++INCLUDE CTGENFILE                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012CXSUPC    05/01/02'                                      
         END                                                                    
