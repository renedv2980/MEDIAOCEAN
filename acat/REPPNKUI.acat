*          DATA SET REPPNKUI   AT LEVEL 123 AS OF 10/11/00                      
*CATALP REPPNKUI                                                                
***********************************************************************         
*                                                                     *         
*         REPPNTAB --- REP PERSONAL NOTIFICATION TABLE BUILDER        *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 18MAY99 SKU INITIAL ENTRY                                           *         
*                                                                     *         
***********************************************************************         
         TITLE 'REPPNTAB - REP PERSONAL NOTIFICATION TABLE BUILDER'             
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 = BYTE 1   : 1 = ALLOCATE/INIT TSAR BUFFER           
*                                       2 = BUILD RECORDS                       
*                                       3 = READ HIGH                           
*                                       4 = READ NEXT                           
*                                 2-4   A(BLOCK WITH FOLLOWING:)                
*                                           A(DATAMGR)                          
*                                           A(CALLOFF)                          
*                                           A(TSAROFF)                          
*                                           A(TSAR BUFFER)                      
*                                           A(TSAR CONTROL BLOCK)               
*              PARAMETER 2 = BYTE 1   : LENGTH OF KEY                           
*                                 2-4   KEY                                     
*  OUTPUT:                                                                      
*              IF INPUT PARAMETER 1, BYTE 1 = 1 INIT TSAR                       
*                  PARAMETER 1 = A(TSAR BUFFER)                                 
*                  PARAMETER 2 = BYTE 1   = ERROR FLAGS                         
*                                     2-4 = A(TSAROFF)                          
*                                                                               
*              IF INPUT PARAMETER 1, BYTE 1 = 2 BUILD RECORDS                   
*                  PARAMETER 1 = RECORD COUNT                                   
*                  PARAMETER 2 = BYTE 1 = ERROR FLAGS                           
*                                                                               
*              IF INPUT PARAMETER 1, BYTE 1 = 3/4 READ HIGH/NEXT                
*                  PARAMETER 2 = BYTE 1 = ERROR FLAGS                           
*                                                                               
***********************************************************************         
REPPNTAB CSECT                                                                  
         PRINT GEN                                                              
         NMOD1 WORKX-WORKD,REPPNTAB                                             
         USING WORKD,RC                                                         
*                                                                               
         ST    R1,SAVER1                                                        
         MVC   MODE,0(R1)                                                       
         MVC   ATSARREC,4(R1)                                                   
*                                                                               
         L     R1,0(R1)                                                         
         MVC   DATAMGR,0(R1)                                                    
         MVC   CALLOFF,4(R1)                                                    
         MVC   ATSAROFF,8(R1)                                                   
         MVC   ABUFFER,12(R1)                                                   
         MVC   ATSARBLK,16(R1)                                                  
*                                                                               
MAIN10   DS    0H                                                               
         MVC   DATADISP,=H'34'                                                  
*                                                                               
         L     R5,ATSARBLK                                                      
         USING TSARD,R5                                                         
*                                                                               
         OC    0(TSARDL,R5),0(R5)  CONTROL BLOCK ALREADY INITIALIZED?           
         BNZ   MAIN20                                                           
*                                                                               
         LA    RF,DATAMGR                                                       
         ST    RF,TSACOM                                                        
         MVI   TSKEYL,TSARRECL                                                  
         MVC   TSRECL,=AL2(TSARRECL)                                            
         MVC   TSABUF,ABUFFER                                                   
*                                                                               
MAIN20   DS    0H                                                               
         CLI   MODE,1              INITIALIZE TSAR BUFFER                       
         BE    INITTSAR                                                         
         CLI   MODE,2              READ RECORDS INTO TSAR BUFFER                
         BE    BLDTABLE                                                         
         CLI   MODE,3              READ HIGH FOR KEY                            
         BE    READHIGH                                                         
         CLI   MODE,4              READ NEXT                                    
         BE    READNEXT                                                         
         CLI   MODE,X'FF'                                                       
         BE    DUMPBUFF                                                         
*                                                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC               SET CONDITION CODE AT EXIT                   
EXIT     XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* INITIALIZE TSAROFF BUFFER AND RESOLVE ADDRESSES                               
*********************************************************************           
INITTSAR DS    0H                                                               
         L     R0,=A(BUFFSIZE)     SET SIZE OF BUFFER                           
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         ST    R1,ABUFFER          SAVE ADDRESSS OF BUFFER                      
*                                                                               
         MVC   TSABUF,ABUFFER                                                   
         MVC   TSAREC,=A(BUFFSIZE) CLEAR BUFFER                                 
*                                                                               
         MVI   TSOFFACT,TSAINI     INITIALIZE BUFFER                            
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
         L     R1,SAVER1                                                        
         MVC   0(4,R1),ABUFFER     RETURN TSAR BUFFER ADDRESS                   
         MVC   4(4,R1),ATSAROFF    RETURN TSAROFF ADDRESS                       
         MVC   4(1,R1),TSERRS      RETURN TSAR ERROR CODES, IF ANY              
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* BUILD TABLE OF DARE PERSONAL NOTIFICATION RECORDS IN TSAR BUFFER              
*********************************************************************           
BLDTABLE DS    0H                                                               
*                                                                               
* CHECK REP PROFILE FOR PERSONAL NOTIFICATION                                   
* IF PROFILE IS ON:                                                             
*   ADD A TSAR RECORD WITH REP CODE ONLY                                        
* WHEN BUILDING DNA BUFFER:                                                     
*   CHECK IF RECORD WITH REP CODE IN BUFFER, IF FOUND, ADD DNA RECORD           
*   TO TSAR BUFFER, ELSE SKIP                                                   
*                                                                               
         XC    KEY,KEY                                                          
         MVI   KEY,X'01'                                                        
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEY,KEY,0                     
*                                                                               
BLT10    DS    0H                                                               
         CLI   KEY,X'01'                                                        
         BNE   BLT40                                                            
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'REPFILE',KEY+28,IOAREA,DMWORK         
*                                                                               
         LA    R6,IOAREA                                                        
         MVI   ELCODE,X'04'        PROFILE ELEMENT                              
         BAS   RE,GETEL                                                         
         BNE   BLT35                                                            
         USING RREPPGMP,R6                                                      
         ZIC   RF,RREPPGM#         # OF PROGRAM UNITS (LOOP COUNTER)            
         LA    R4,RREPPGM1                                                      
         DROP  R6                                                               
*                                                                               
         USING RREPPGM1,R4                                                      
BLT20    CLI   RREPPGM1,RREPQDAR   DARE?                                        
         BE    BLT30                                                            
         AHI   R4,RREPPGML                                                      
         BCT   RF,BLT20                                                         
         B     BLT35               DARE NOT FOUND. USE DEFAULTS.                
*                                                                               
BLT30    TM    RREPPGM1+2,X'80'    CHECK IF USER WANTS PERSONAL                 
         BZ    BLT35               NOTIFICATION                                 
         DROP  R4                                                               
*                                                                               
         LA    R6,IOAREA                                                        
         USING RREPREC,R6                                                       
         XC    BLOCK,BLOCK                                                      
         LA    R4,BLOCK                                                         
         USING TSARRECD,R4                                                      
         MVC   TSREP,RREPKREP                                                   
         DROP  R4,R6                                                            
*                                                                               
* ADD RECORD TO TSAR BUFFER                                                     
*                                                                               
         MVI   TSOFFACT,TSAADD                                                  
         LA    R0,BLOCK                                                         
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
BLT35    DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=C'REPDIR',KEY,KEY,0                     
         B     BLT10                                                            
*                                                                               
* LOOP THRU THE REP FILES AND READ THE X'53' RECORDS                            
*                                                                               
BLT40    DS    0H                                                               
         XC    COUNTER,COUNTER                                                  
         XC    KEY,KEY                                                          
         MVI   KEY,X'53'                                                        
BLT50    MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEY,KEY,0                     
*                                                                               
         XC    BLOCK,BLOCK                                                      
         LA    R4,BLOCK                                                         
         USING TSARRECD,R4                                                      
         LA    R6,KEY                                                           
         USING RDNAKEY,R6                                                       
         MVC   TSREP,RDNAKREP                                                   
*                                                                               
         MVI   TSOFFACT,TSARDH                                                  
         LA    R0,BLOCK                                                         
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
         CLC   TSREP,RDNAKREP                                                   
         BE    BLT60               RECORD FOUND WITH THIS REP?                  
         MVC   RDNAKSAL,=X'FFFFFF' NO ,SKIP ALL DNA RECS FOR THIS REP           
         B     BLT50                                                            
         DROP  R4,R6                                                            
*                                                                               
BLT60    DS    0H                                                               
         CLI   KEY,X'53'                                                        
         BNE   BLTX                                                             
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'GETREC',=C'REPFILE',KEY+28,IOAREA,DMWORK         
*                                                                               
         LA    R6,IOAREA                                                        
         USING RDNAREC,R6                                                       
         XC    BLOCK,BLOCK                                                      
         LA    R4,BLOCK                                                         
         USING TSARRECD,R4                                                      
         MVC   TSREP,RDNAKREP                                                   
         MVC   TSSAL,RDNAKSAL                                                   
*                                                                               
* CHECK IF NOTIFY BY TEAM                                                       
*                                                                               
         CLI   RDNAKSAL,X'FF'                                                   
         BNE   BLT70                                                            
         BAS   RE,GETTEAM                                                       
         B     BLT100                                                           
         DROP  R6                                                               
*                                                                               
BLT70    DS    0H                                                               
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                                                         
         BNE   BLT100                                                           
         USING RDNASTED,R6                                                      
*                                                                               
BLT80    DS    0H                                                               
         MVC   TSSTA,RDNASTST                                                   
         CLI   TSSTA+4,C' '                                                     
         BNE   *+8                                                              
         MVI   TSSTA+4,C'T'                                                     
*                                                                               
         LA    R7,IOAREA                                                        
         MVI   ELCODE2,X'20'                                                    
         BAS   RE,GETEL2                                                        
         BNE   BLT100                                                           
         USING RDNAAGED,R7                                                      
*                                                                               
BLT90    DS    0H                                                               
         MVC   TSAGY,RDNAAGAG                                                   
*                                                                               
* ADD RECORD TO TSAR BUFFER                                                     
*                                                                               
         MVI   TSOFFACT,TSAADD                                                  
         LA    R0,BLOCK                                                         
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
         L     RF,COUNTER                                                       
         LA    RF,1(RF)                                                         
         ST    RF,COUNTER                                                       
*                                                                               
         BAS   RE,NEXTEL2                                                       
         BE    BLT90                                                            
*                                                                               
         BAS   RE,NEXTEL                                                        
         BE    BLT80                                                            
         DROP  R4,R6,R7                                                         
*                                                                               
BLT100   DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=C'REPDIR',KEY,KEY,0                     
         B     BLT60                                                            
*                                                                               
BLTX     DS    0H                                                               
         L     R1,SAVER1                                                        
         MVC   0(4,R1),COUNTER     RETURN TSAR RECORD COUNTS                    
         MVC   4(1,R1),TSERRS      RETURN TSAR ERROR CODES, IF ANY              
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* CHECK IF NOTIFY BY TEAM                                                       
*********************************************************************           
GETTEAM  NTR1                                                                   
         MVC   KEYSAVE2,KEY                                                     
KEYSVD   USING RDNAKEY,KEYSAVE2                                                 
*                                                                               
         XC    KEY,KEY                                                          
KEYD     USING RSALKEY,KEY                                                      
*                                                                               
         MVC   KEY(2),=X'8601'                                                  
         MVC   KEYD.RSALTREP,KEYSVD.RDNAKREP                                    
         MVC   KEYD.RSALTTEM,KEYSVD.RDNAKTEM                                    
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEY,KEY,0                     
*                                                                               
GT05     DS    0H                                                               
         CLC   KEY(RSALTSAL-RSALTTYP),KEYSAVE                                   
         BNE   GTX                                                              
*                                                                               
         XC    BLOCK,BLOCK                                                      
         LA    R4,BLOCK                                                         
         USING TSARRECD,R4                                                      
         MVC   TSREP,KEYD.RSALTREP                                              
         MVC   TSSAL,KEYD.RSALTSAL                                              
         DROP  KEYSVD,KEYD                                                      
*                                                                               
         LA    R6,IOAREA                                                        
*                                                                               
GT08     DS    0H                                                               
         MVI   ELCODE,X'10'                                                     
         BAS   RE,GETEL                                                         
         BNE   GT15                NO STA? LEAVE AS NULLS IN SORT RECS          
         USING RDNASTED,R6                                                      
*                                                                               
GT10     DS    0H                                                               
         MVC   TSSTA,RDNASTST                                                   
         CLI   TSSTA+4,C' '                                                     
         BNE   *+8                                                              
         MVI   TSSTA+4,C'T'                                                     
*                                                                               
GT15     DS    0H                                                               
         LA    R7,IOAREA                                                        
         MVI   ELCODE2,X'20'                                                    
         BAS   RE,GETEL2                                                        
         BNE   GT50                                                             
         USING RDNAAGED,R7                                                      
*                                                                               
GT20     DS    0H                                                               
         MVC   TSAGY,RDNAAGAG                                                   
*                                                                               
* ADD RECORD TO TSAR BUFFER                                                     
*                                                                               
         MVI   TSOFFACT,TSAADD                                                  
         LA    R0,BLOCK                                                         
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
         L     RF,COUNTER                                                       
         LA    RF,1(RF)                                                         
         ST    RF,COUNTER                                                       
*                                                                               
         BAS   RE,NEXTEL2                                                       
         BE    GT20                                                             
*                                                                               
         BAS   RE,NEXTEL                                                        
         BE    GT10                                                             
         DROP  R4,R6,R7                                                         
*                                                                               
GT50     DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DATAMGR,DMCB,=C'DMRSEQ',=C'REPDIR',KEY,KEY,0                     
         B     GT05                                                             
*                                                                               
GTX      DS    0H                                                               
         MVC   KEY,KEYSAVE2        RESTORE SEQUENCE                             
         GOTO1 DATAMGR,DMCB,=C'DMRDHI',=C'REPDIR',KEY,KEY,0                     
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* READ HIGH FOR KEY AND PASS BACK FIRST RECORD                                  
*********************************************************************           
READHIGH DS    0H                                                               
         MVI   TSOFFACT,TSARDH                                                  
         L     R0,ATSARREC                                                      
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
RHX      DS    0H                                                               
         L     R1,SAVER1                                                        
         MVC   4(1,R1),TSERRS      RETURN TSAR ERROR CODES, IF ANY              
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
* READ NEXT FOR KEY AND PASS BACK NEXT RECORD                                   
*********************************************************************           
READNEXT DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
         L     R0,ATSARREC                                                      
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
         TM    TSERRS,TSEEOF       EOF FOUND?                                   
         BNO   RNX                                                              
         L     R1,ATSARREC                                                      
         MVI   0(R1),0                                                          
*                                                                               
RNX      DS    0H                                                               
         L     R1,SAVER1                                                        
         MVC   4(1,R1),TSERRS      RETURN TSAR ERROR CODES, IF ANY              
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*********************************************************************           
*********************************************************************           
DUMPBUFF DS    0H                                                               
*                                                                               
         XC    BLOCK,BLOCK                                                      
         MVI   TSOFFACT,TSARDH                                                  
         LA    R0,BLOCK                                                         
         ST    R0,TSAREC                                                        
         GOTO1 ATSAROFF,(R5)                                                    
*                                                                               
         LA    R0,BLOCK                                                         
         L     R1,SAVER1                                                        
         ST    R0,0(R1)                                                         
*                                                                               
DBX      DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
         GETEL R6,DATADISP,ELCODE                                               
         GETELN R7,DATADISP,ELCODE2,2                                           
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
* EQUATES                                                                       
NUMENTRY EQU   10000               10,000 TABLE ENTRIES                         
RECSIZE  EQU   TSARRECL            RECORD SIZE IN BYTES                         
BUFFSIZE EQU   NUMENTRY*RECSIZE    CALCULATED BUFFER SIZE                       
         EJECT                                                                  
*                                                                               
* DSECT TO COVER RECORDS ADDED TO TSAR BUFFER                                   
*                                                                               
TSARRECD DSECT                                                                  
TSREP    DS    CL2                                                              
TSAGY    DS    CL5                                                              
TSSTA    DS    CL5                                                              
TSSAL    DS    CL3                                                              
TSARRECL EQU   *-TSARRECD                                                       
*                                                                               
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
KEYSAVE2 DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
ELCODE2  DS    X                                                                
WORK     DS    CL17                FOR EDIT                                     
BLOCK    DS    XL256                                                            
MODE     DS    X                                                                
SAVER1   DS    A                   A(CALLERS ACTUAL DMCB)                       
DATAMGR  DS    A                                                                
CALLOFF  DS    A                                                                
ABUFFER  DS    A                   TSAR BUFFER ADDRESS                          
ATSAROFF DS    A                   TSAROFF ADDRESS                              
ATSARBLK DS    A                   TSAR CONTROL BLOCK ADDRESS                   
ATSARREC DS    A                   TSAR RECORD ADDRESS                          
COUNTER  DS    F                   NUMBER OF ENTRIES IN TSAR BUFFER             
IOAREA   DS    XL4000                                                           
IOAREA2  DS    XL4000                                                           
WORKX    EQU   *                                                                
*                                                                               
       ++INCLUDE DDTSARD                                                        
       ++INCLUDE REGENREPA                                                      
       ++INCLUDE REGENDNA                                                       
       ++INCLUDE REGENSAL                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'123REPPNKUI  10/11/00'                                      
         END                                                                    
