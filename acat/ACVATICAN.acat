*          DATA SET ACVATICAN  AT LEVEL 052 AS OF 08/14/18                      
*CATALP VATICAN                                                                 
VATICAN  TITLE '- VALIDATE AND LOOK UP TAX RATES/VALUES'                        
VATICAN  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**VTCN**,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL WORKLNG STORAGE)                  
         LR    R9,R1                                                            
*                                                                               
         USING VTCD,R9             R9=A(VATICAN CONTROL BLOCK)                  
         XC    VTCERR,VTCERR       CLEAR ERROR RETURN BYTES                     
         L     RE,VTCCOMF                                                       
         MVC   VDATAMGR,CDATAMGR-COMFACSD(RE)                                   
*                                                                               
         L     RF,=V(DICTATE)                                                   
         LTR   RF,RF                                                            
         BNZ   VAT0                                                             
         MVC   VDICTATE,CDICTATE-COMFACSD(RE)                                   
         L     RF,VDICTATE                                                      
*                                                                               
VAT0     GOTO1  (RF),DMCB,C'I   '                                               
         MVC    DMCB(4),=C'L   '                                                
         CLI    VTCLANG,X'00'      ANY  LANGUAGE REQUESTED ?                    
         BE     VAT1               NO,  SKIP                                    
         MVC    DMCB+3(1),VTCLANG  USE  LANGUAGE REQUESTED                      
*                                                                               
VAT1     GOTO1  (RF),DMCB,,DICI,DICO                                            
                                                                                
         GOTO1 VDATAMGR,DMCB,DMKEY,ACCDIR,SAVEKEY                               
*                                                                               
         CLI   VTCACTN,VTCAIVAL    VALIDATE INPUT TAX RATE?                     
         BE    VATIVAL                                                          
         CLI   VTCACTN,VTCAOVAL    VALIDATE OUTPUT TAX RATE?                    
         BE    VATOVAL                                                          
         CLI   VTCACTN,VTCAOOPT    VALIDATE OUTPUT TAX RATE (OPT MAINT)         
         BE    VATOVAL                                                          
         CLI   VTCACTN,VTCALOOK    LOOK UP TAX VALUES (OUTPUT)?                 
         BE    VATLOOK                                                          
         CLI   VTCACTN,VTCAILUP    LOOK UP TAX VALUES (INPUT)?                  
         BE    VATLOOK                                                          
*&&US*&& CLI   VTCACTN,VTCANAME    LOOK UP PROVINCE NAME?                       
*&&US*&& BE    VATNAME                                                          
         B     ERR1                                                             
         EJECT                                                                  
***********************************************************************         
* VALIDATE INPUT TAX TYPE FOR BATCH OVERLAYS                          *         
***********************************************************************         
         SPACE 1                                                                
VATIVAL  LA    R2,KEY                                                           
         USING TAXRECD,R2                                                       
         XC    TAXKEY,TAXKEY       BUILD KEY OF RECORD                          
         MVI   TAXKTYP,TAXKTYPQ                                                 
         MVC   TAXKCPY,VTCCPY                                                   
*&&US*&& MVC   TAXKPRV,VTCPRV      PROVINCE                                     
         MVC   TAXKOFF,EFFS        SET OFFICE TO COMPANY LEVEL                  
         CLI   VTCOFFC,C' '        TEST OFFICE PASSED                           
         BNH   *+10                                                             
         MVC   TAXKOFF,VTCOFFC     YES-PLUG IT INTO KEY                         
         CLI   TAXKOFF+1,C' '      TEST FOR VALID SECOND BYTE                   
         BNL   *+8                                                              
         MVI   TAXKOFF+1,C' '      CLEAN UP A FUNNY                             
         MVC   TAXKDATE,VTCINVD                                                 
         XC    TAXKDATE,EFFS       COMPLEMENT THE INVOICE DATE                  
*                                                                               
VATI2    GOTO1 HIGH                                                             
         BNE   ERR3                HARDWARE ERROR                               
         CLC   TAXKEY(TAXKDATE-TAXKEY),IO                                       
         BE    VATI4                                                            
*                                                                               
         CLC   TAXKOFF,EFFS        TEST READ FOR OFFICE                         
         BE    VATI3               NO-NO APPLICABLE RECORD FOR COMPANY          
         MVC   TAXKOFF,EFFS                                                     
*&&US*&& B     VATI2                                                            
*&&UK                                                                           
         TM    VTCCPYS1,CPYSOROE   TEST COMPANY ON OFFICES                      
         BZ    VATI2               NO - USE DEFAULT RECORD                      
         MVC   VTCERR,=AL2(AE$INVAP)                                            
         B     XITMOD                                                           
*&&                                                                             
VATI3    OI    VTCINDS,VTCINA      SET TAX NOT APPLICABLE                       
         GOTO1 FVAL                                                             
         BE    *+10                NO INPUT-OK                                  
         MVC   VTCERR,=AL2(AE$VINAP) SET ERROR MESSAGE                          
         B     XITMOD                                                           
*                                                                               
VATI4    MVC   DA,IO+(TAXKDA-TAXRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR3                DISK ERROR                                   
*                                                                               
VATI6    GOTO1 FVAL                                                             
*                                                                               
         LA    R2,IO               R2=A(RECORD)                                 
         MVC   VTCTYPE,FVIFLD      SET INPUT TAX TYPE                           
         MVC   VTCEFFD,TAXKDATE                                                 
         XC    VTCEFFD,EFFS        REVERSE DATE COMPLEMENT                      
         XC    VTCREG,VTCREG       CLEAR REGISTRATION NUMBER                    
         LA    R2,TAXRFST                                                       
         SR    R0,R0               CLEAR R0=WORK REGISTER                       
*                                                                               
         USING TAXELD,R2                                                        
VATI8    CLI   TAXEL,TAXIELQ       TEST FOR INPUT TAX ELEMENT                   
         BNE   VATI10                                                           
         CLC   VTCTYPE,TAXCODE     TEST FOR MATCH ON TAX CODE                   
         BE    VATI14                                                           
         TM    TAXINDS,TAXIDFLT    TEST FOR DEFAULT INPUT TYPE                  
         BZ    *+8                                                              
         ST    R2,ADFLTEL          SAVE A(DEFAULT ELEMENT)                      
         B     VATI12              NEXT ELEMENT                                 
*                                                                               
         USING FFTELD,R2                                                        
VATI10   CLI   FFTEL,FFTELQ        TEST FOR TEXT ELEMENT                        
         BNE   VATI12                                                           
         MVC   VTCREG,SPACES       EXTRACT REGISTRATION NUMBER                  
         ZIC   R1,FFTDLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     VATI12                                                           
         MVC   VTCREG(0),FFTDATA                                                
*                                                                               
VATI12   IC    R0,FFTLN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   0(R2),0             TEST END OF RECORD                           
         BNE   VATI8                                                            
*                                                                               
         CLI   VTCTYPE,C' '        TEST IF TAX TYPE INPUT                       
         BE    *+14                NO                                           
         MVC   VTCERR,=AL2(AE$INVVI) INVALID TAX TYPE                           
         B     XITMOD                                                           
*                                                                               
         ICM   R2,15,ADFLTEL       CHECK FOR DEFAULT IF NO INPUT                
         BNZ   *+14                YES-DEFAULT IS PRESENT                       
         MVC   VTCERR,=AL2(AE$MISIF)                                            
         B     XITMOD                                                           
*                                                                               
         USING TAXELD,R2                                                        
         MVC   VTCTYPE,TAXCODE     EXTRACT DEFAULT TYPE                         
*                                                                               
VATI14   MVC   VTCRATE,TAXRATE                                                  
         NI    VTCINDS,X'FF'-VTCIDC3  CLEAR 3DP INDICATOR                       
         TM    TAXINDS,X'20'       IS THIS RATE 3 DECIMALS?                     
         BZ    *+8                 NO                                           
         OI    VTCINDS,VTCIDC3     YES, PASS IT ON                              
         MVC   VTCACT(L'ACTKCPY),VTCCPY                                         
         MVC   VTCACT+L'ACTKCPY(L'TAXACT),TAXACT                                
         MVC   VTCTTYPE,TAXTYPE                                                 
         MVI   VTCACTNM,C' '                                                    
         MVC   VTCACTNM+1(L'VTCACTNM-1),VTCACTNM                                
         SR    RE,RE                                                            
         IC    RE,TAXLN                                                         
         SH    RE,=Y(TAXLN1Q+1)                                                 
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   VTCACTNM(0),TAXNAME TAX ACCOUNT NAME                             
*                                                                               
VATIX    B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* VALIDATE OUTPUT TAX TYPE FOR FILE MAINTENANCE PROGRAMS              *         
***********************************************************************         
         SPACE 1                                                                
VATOVAL  LA    R2,KEY                                                           
         USING TAXRECD,R2                                                       
         XC    TAXKEY,TAXKEY       BUILD KEY OF RECORD                          
         MVI   TAXKTYP,TAXKTYPQ                                                 
         MVC   TAXKCPY,VTCCPY                                                   
*&&US*&& MVC   TAXKPRV,VTCPRV      PROVINCE                                     
         MVC   TAXKOFF,EFFS        SET OFFICE TO COMPANY LEVEL                  
*                                                                               
VATO2    GOTO1 HIGH                                                             
         BNE   ERR3                HARDWARE ERROR                               
         CLC   TAXKEY(TAXKDATE-TAXKEY),IO                                       
         BE    VATO4                                                            
*                                                                               
         OI    VTCINDS,VTCINA      SET TAX NOT APPLICABLE                       
         GOTO1 FVAL                                                             
         BE    *+10                NO INPUT-OK                                  
         MVC   VTCERR,=AL2(AE$VINAP) SET ERROR MESSAGE                          
         B     XITMOD                                                           
*                                                                               
VATO4    MVC   DA,IO+(TAXKDA-TAXRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR3                DISK ERROR                                   
*                                                                               
VATO6    CLI   VTCACTN,VTCAOOPT    VALIDATE OUTPUT TAX RATE (OPT MAINT)         
         BE    VATO7                                                            
         GOTO1 FVAL                                                             
         BNE   *+14                MUST HAVE INPUT                              
         MVC   VTCERR,=AL2(AE$MISIF)                                            
         B     XITMOD                                                           
         MVC   VTCTYPE,FVIFLD      SET INPUT TAX TYPE                           
*                                                                               
VATO7    LA    R2,IO               R2=A(RECORD)                                 
         MVC   VTCEFFD,TAXKDATE                                                 
         XC    VTCEFFD,EFFS        REVERSE DATE COMPLEMENT                      
         XC    VTCREG,VTCREG       CLEAR REGISTRATION NUMBER                    
         LA    R2,TAXRFST                                                       
         SR    R0,R0               CLEAR R0=WORK REGISTER                       
*                                                                               
         USING TAXELD,R2                                                        
VATO8    CLI   TAXEL,TAXOELQ       TEST FOR OUTPUT TAX ELEMENT                  
         BNE   VATO10                                                           
         CLC   VTCTYPE,TAXCODE     TEST FOR MATCH ON TAX TYPE                   
         BE    VATO14                                                           
         B     VATO12              NEXT ELEMENT                                 
*                                                                               
         USING FFTELD,R2                                                        
VATO10   CLI   FFTEL,FFTELQ        TEST FOR TEXT ELEMENT                        
         BNE   VATO12                                                           
         MVC   VTCREG,SPACES       EXTRACT REGISTRATION NUMBER                  
         ZIC   R1,FFTDLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     VATO12                                                           
         MVC   VTCREG(0),FFTDATA                                                
*                                                                               
VATO12   IC    R0,FFTLN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   0(R2),0             TEST END OF RECORD                           
         BNE   VATO8                                                            
         MVC   VTCERR,=AL2(AE$INVVI) INVALID TAX TYPE                           
         B     XITMOD                                                           
*                                                                               
         USING TAXELD,R2                                                        
VATO14   MVC   VTCRATE,TAXRATE                                                  
         NI    VTCINDS,X'FF'-VTCIDC3  CLEAR 3DP INDICATOR                       
         TM    TAXINDS,X'20'       IS THIS RATE 3 DECIMALS?                     
         BZ    *+8                 NO                                           
         OI    VTCINDS,VTCIDC3     YES, PASS IT ON                              
         MVC   VTCACT(L'ACTKCPY),VTCCPY                                         
         MVC   VTCACT+L'ACTKCPY(L'TAXACT),TAXACT                                
         MVC   VTCTTYPE,TAXTYPE                                                 
         MVI   VTCACTNM,C' '                                                    
         MVC   VTCACTNM+1(L'VTCACTNM-1),VTCACTNM                                
         SR    RE,RE                                                            
         IC    RE,TAXLN                                                         
         SH    RE,=Y(TAXLN1Q+1)                                                 
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   VTCACTNM(0),TAXNAME TAX ACCOUNT NAME                             
*                                                                               
VATOX    B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* LOOK UP TAX RULES FOR A TAX TYPE                                    *         
***********************************************************************         
         SPACE 1                                                                
VATLOOK  LA    R2,KEY                                                           
         USING TAXRECD,R2                                                       
         XC    TAXKEY,TAXKEY       BUILD KEY OF RECORD                          
         MVI   TAXKTYP,TAXKTYPQ                                                 
         MVC   TAXKCPY,VTCCPY                                                   
*&&US*&& MVC   TAXKPRV,VTCPRV      PROVINCE                                     
         MVC   TAXKOFF,EFFS        SET OFFICE TO COMPANY LEVEL                  
         CLI   VTCOFFC,C' '        TEST OFFICE PASSED                           
         BNH   *+10                                                             
         MVC   TAXKOFF,VTCOFFC     YES-PLUG IT INTO KEY                         
         CLI   TAXKOFF+1,C' '      TEST FOR VALID SECOND BYTE                   
         BNL   *+8                                                              
         MVI   TAXKOFF+1,C' '      CLEAN UP A FUNNY                             
         MVC   TAXKDATE,VTCINVD                                                 
         XC    TAXKDATE,EFFS       COMPLEMENT THE INVOICE DATE                  
*                                                                               
VATL2    GOTO1 HIGH                                                             
         BNE   ERR3                HARDWARE ERROR                               
         CLC   TAXKEY(TAXKDATE-TAXKEY),IO                                       
         BE    VATL4                                                            
*                                                                               
         CLC   TAXKOFF,EFFS        TEST READ FOR OFFICE                         
         BE    VATL3               NO-NO APPLICABLE RECORD FOR COMPANY          
         MVC   TAXKOFF,EFFS                                                     
*&&US*&& B     VATL2                                                            
*&&UK*&& TM    VTCCPYS1,CPYSOROE   TEST COMPANY ON OFFICES                      
*&&UK*&& BZ    VATL2               NO - USE DEFAULT RECORD                      
*                                                                               
VATL3    OI    VTCINDS,VTCINA      SET TAX NOT APPLICABLE                       
         B     XITMOD                                                           
*                                                                               
VATL4    MVC   DA,IO+(TAXKDA-TAXRECD)                                           
         GOTO1 GET                                                              
         BNE   ERR3                DISK ERROR                                   
*                                                                               
VATL6    LA    R2,IO               R2=A(RECORD)                                 
         MVC   VTCEFFD,TAXKDATE                                                 
         XC    VTCEFFD,EFFS        REVERSE DATE COMPLEMENT                      
         XC    VTCREG,VTCREG       CLEAR REGISTRATION NUMBER                    
         LA    R2,TAXRFST                                                       
         SR    R0,R0               CLEAR R0=WORK REGISTER                       
*                                                                               
         USING TAXELD,R2                                                        
VATL8    CLI   TAXEL,TAXIELQ       TEST FOR INPUT/OUTPUT TAX ELEMEN             
         BL    VATL10                                                           
         CLI   TAXEL,TAXOELQ                                                    
         BH    VATL10                                                           
         CLI   VTCACTN,VTCAOLUP    TEST OUTPUT TAX LOOKUP                       
         BNE   *+16                                                             
         CLI   TAXEL,TAXOELQ       TEST FOR OUTPUT TAX ELEMENT                  
         BNE   VATL10                                                           
         B     *+12                                                             
         CLI   TAXEL,TAXIELQ       TEST FOR INPUT TAX ELEMENT                   
         BNE   VATL10                                                           
         CLC   VTCTYPE,TAXCODE     TEST FOR MATCH ON TAX TYPE                   
         BE    VATL14                                                           
         B     VATL12              NEXT ELEMENT                                 
*                                                                               
         USING FFTELD,R2                                                        
VATL10   CLI   FFTEL,FFTELQ        TEST FOR TEXT ELEMENT                        
         BNE   VATL12                                                           
         MVC   VTCREG,SPACES       EXTRACT REGISTRATION NUMBER                  
         ZIC   R1,FFTDLEN                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     VATL12                                                           
         MVC   VTCREG(0),FFTDATA                                                
*                                                                               
VATL12   IC    R0,FFTLN            BUMP TO NEXT ELEMENT                         
         AR    R2,R0                                                            
         CLI   0(R2),0             TEST END OF RECORD                           
         BNE   VATL8                                                            
         MVC   VTCERR,=AL2(AE$INVVI)                                            
         B     XITMOD                                                           
*                                                                               
         USING TAXELD,R2                                                        
VATL14   MVC   VTCRATE,TAXRATE                                                  
         NI    VTCINDS,X'FF'-VTCIDC3  CLEAR 3DP INDICATOR                       
         TM    TAXINDS,X'20'       IS THIS RATE 3 DECIMALS?                     
         BZ    *+8                 NO                                           
         OI    VTCINDS,VTCIDC3     YES, PASS IT ON                              
         MVC   VTCACT(L'ACTKCPY),VTCCPY                                         
         MVC   VTCACT+L'ACTKCPY(L'TAXACT),TAXACT                                
         MVC   VTCTTYPE,TAXTYPE                                                 
         MVI   VTCACTNM,C' '                                                    
         MVC   VTCACTNM+1(L'VTCACTNM-1),VTCACTNM                                
         SR    RE,RE                                                            
         IC    RE,TAXLN                                                         
         SH    RE,=Y(TAXLN1Q+1)                                                 
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   VTCACTNM(0),TAXNAME TAX ACCOUNT NAME                             
*&&US                                                                           
         OC    VTCPRV,VTCPRV       DO WE HAVE A PROVINCE CODE?                  
         BZ    VATLX               NO                                           
         BAS   RE,GETNAME          YES, GET THE NAME                            
*&&                                                                             
*                                                                               
VATLX    B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* LOOK UP PROVINCE NAME                                               *         
***********************************************************************         
         SPACE 1                                                                
VATNAME  LA    R2,KEY                                                           
         USING TAXRECD,R2                                                       
         XC    VTCPRVD,VTCPRVD     CLEAR NAME                                   
         OC    VTCPRV,VTCPRV       DO WE HAVE A PROVINCE CODE?                  
         BZ    VATNX               NO                                           
         BAS   RE,GETNAME          YES, LOOK UP THE NAME                        
*                                                                               
VATNX    B     XITMOD                                                           
         EJECT                                                                  
***********************************************************************         
* NAME LOOKUP ROUTINE                                                 *         
***********************************************************************         
*                                                                               
GETNAME  LA    R1,PRVTAB           YES, LOOK FOR DESCRIPTION                    
*                                                                               
GETN02   CLI   0(R1),X'FF'         END OF TABLE                                 
         BE    GETNX                                                            
         CLC   0(L'VTCPRV,R1),VTCPRV LOOK FOR PROVINCE                          
         BE    GETN04                                                           
         LA    R1,L'PRVTAB(R1)                                                  
         B     GETN02                                                           
*                                                                               
GETN04   MVC   WORK(2),LARF        PASS DESCRIPTION BACK                        
         MVC   WORK+2(2),2(R1)                                                  
         EX    0,WORK                                                           
         MVC   VTCPRVD,0(RF)                                                    
*                                                                               
GETNX    BR    RE                                                               
*                                                                               
LARF     LA    RF,0                                                             
         EJECT                                                                  
***********************************************************************         
* EXTRACT TWA INPUT FIELD INTO SPACE FILLED WORKING STORAGE FIELD AND *         
* CALCULATE INPUT LENGTH                                              *         
*                                                                     *         
* EXIT - CC EQUAL IF NO INPUT, NOT EQUAL IF INPUT                     *         
***********************************************************************         
         SPACE 1                                                                
FVAL     ICM   R1,15,VTCAFLDH      R1=A(INPUT FIELD HEADER)                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   FVIHDR,0(R1)        EXTRACT FIELD HEADER                         
         MVI   FVIFLD,C' '                                                      
         MVC   FVIFLD+1(L'FVIFLD-1),FVIFLD                                      
         SR    RF,RF                                                            
         IC    RF,FVTLEN                                                        
         LA    R0,L'FVIHDR+1                                                    
         TM    FVATRB,FVAXTND                                                   
         BZ    *+8                                                              
         LA    R0,L'FVIHDR+L'FVIHDR+1                                           
         SR    RF,R0               RF=MAXIMUM INPUT LENGTH-1                    
         BNM   *+6                                                              
         DC    H'0'                THIS IS A BAD TWA FIELD                      
         EX    RF,*+8              EXTRACT FIELD DATA                           
         B     *+10                                                             
         MVC   FVIFLD(0),L'FVIHDR(R1)                                           
*                                                                               
         LA    R1,FVIFLD(RF)       R1=A(END OF INPUT FIELD)                     
         LA    RF,1(RF)            RF=LOOP COUNT                                
FVAL2    CLI   0(R1),C' '          LOCATE LAST INPUT CHARACTER IN FIELD         
         BH    *+14                                                             
         MVI   0(R1),C' '          SET FUNNIES TO SPACES                        
         BCTR  R1,0                                                             
         BCT   RF,FVAL2                                                         
         STC   RF,FVILEN           SET ACTUAL INPUT LENGTH                      
*                                                                               
FVALX    CLI   FVILEN,0            SET CC ON EXIT                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* DATA MANAGER INTERFACE                                              *         
***********************************************************************         
         SPACE 1                                                                
HIGH     LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 VDATAMGR,DMCB,DMRDHI,ACCDIR,KEY,IO                               
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 1                                                                
GET      LR    R0,RE               SAVE RETURN POINT                            
         GOTO1 VDATAMGR,DMCB,GETREC,ACCMST,DA,IO,WORK                           
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
* RESTORE LAST KEY READ FROM DIRECTORY FOR APPLICATION IF THIS        *         
* MODULE CHANGED THE READ SEQUENCE                                    *         
***********************************************************************         
                                                                                
XITMOD   OC    SAVEKEY(L'ACCKEY),SAVEKEY                                        
         BZ    XITMOD2                                                          
         GOTO1 VDATAMGR,DMCB,DMKEY,ACCDIR,WORK                                  
         CLC   SAVEKEY(L'ACCKEY),WORK                                           
         BE    XITMOD2                                                          
         GOTO1 VDATAMGR,DMCB,(X'08',DMREAD),ACCDIR,SAVEKEY,SAVEKEY              
         BE    XITMOD2                                                          
         CLI   DMCB+8,X'02'                                                     
         BNE   ERR2                                                             
*                                                                               
XITMOD2  OC    VTCERR,VTCERR       SET CONDITION CODE FOR CALLER                
         SPACE 2                                                                
XIT      XIT1  ,                                                                
         EJECT                                                                  
ERR1     DC    H'0',C'INVALID ACTION CALL '                                     
ERR2     DC    H'0',C'CANT READ LAST DIRECTORY RECORD '                         
ERR3     DC    H'0',C'DISK ERROR'                                               
ERR4     DC    H'0',C'BAD TAX TYPE'                                             
         SPACE 2                                                                
DMKEY    DC    C'DMKEY  '                                                       
DMRDHI   DC    C'DMRDHI '                                                       
DMREAD   DC    C'DMREAD '                                                       
GETREC   DC    C'GETREC '                                                       
ACCDIR   DC    C'ACCDIR '                                                       
ACCMST   DC    C'ACCMST '                                                       
EFFS     DC    4X'FF'                                                           
SPACES   DC    CL16' '                                                          
         SPACE 2                                                                
DICI     DCDDL AC#ONT,3,L                                                       
         DCDDL AC#PST,3,L                                                       
         DCDDL AC#QST,3,L                                                       
         DCDDL AC#HST,3,L                                                       
         DCDDL AC#HBC,6,L                                                       
         DCDDL AC#HAL,6,L                                                       
         DCDDL AC#HSA,6,L                                                       
         DCDDL AC#HMA,6,L                                                       
         DCDDL AC#HPQ,6,L                                                       
         DCDDL AC#HON,6,L                                                       
         DCDDL AC#HPE,6,L                                                       
         DCDDL AC#HNB,6,L                                                       
         DCDDL AC#HNS,6,L                                                       
         DCDDL AC#HNF,6,L                                                       
         DC    X'00'                                                            
         SPACE 2                                                                
         EJECT                                                                  
       ++INCLUDE ACPRVTABN                                                      
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                     ** WORKING STORAGE DSECT **                  
VDATAMGR DS    V                                                                
VDICTATE DS    V                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
SAVEKEY  DS    XL64                SAVED DIRECTORY KEY AREA                     
*                                                                               
FVIHDR   DS    0XL8                ** EXTRACTED INPUT FIELD HEADER **           
FVTLEN   DS    XL1                 L'FIELD HEADER+L'FIELD                       
FVATRB   DS    XL1                 FIELD ATTRIBUTE                              
FVAPROT  EQU   X'20'               PROTECTED FIELD                              
FVAHIGH  EQU   X'08'               HIGH INTENSITY                               
FVAXTND  EQU   X'02'               EXTENDED HEADER                              
FVAMODF  EQU   X'01'               MODIFIED INPUT FIELD                         
FVABSA   DS    XL2                 SCREEN ABSOLUTE ADDRESS                      
FVIIND   DS    XL1                 INPUT VALIDATION INDICATORS                  
FVITHIS  EQU   X'80'               FIELD INPUT THIS TIME                        
FVILAST  EQU   X'40'               FIELD HAS BEEN INPUT PREVIOUSLY              
FVIVAL   EQU   X'20'               FIELD HAS BEEN VALIDATED PREVIOUSLY          
FVIINV   EQU   X'10'               FIELD IS INVALID                             
FVINUM   EQU   X'08'               FIELD IS VALID NUMERIC                       
FVIALF   EQU   X'04'               FIELD IS VALID ALPHA                         
FVIHEX   EQU   X'02'               FIELD IS VALID HEXADECIMAL                   
FVILEN   DS    XL1                 ACTUAL INPUT DATA LENGTH                     
FVOIND   DS    XL1                 OUTPUT INDICATORS                            
FVOXMT   EQU   X'80'               TRANSMIT FIELD                               
FVOCUR   EQU   X'40'               INSERT CURSOR TO FIELD                       
FVXLEN   DS    XL1                 ACTUAL INPUT DATA LENGTH-1                   
FVIFLD   DS    CL80                EXTRACTED & SPACE FILLED INPUT FIELD         
*                                                                               
ADFLTEL  DS    A                   A(DEFAULT INPUT TYPE ELEM) OR A(0)           
*                                                                               
DA       DS    XL4                 DISK ADDRESS                                 
KEY      DS    XL64                KEY AREA                                     
IO       DS    2000X               I/O AREA                                     
*                                                                               
DICO     DS    0C                                                               
         DSDDL PRINT=YES                                                        
WORKX    EQU   *                                                                
         EJECT                                                                  
       ++INCLUDE ACVATICAND                                                     
         EJECT                                                                  
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* ACMSGEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACMSGEQUS                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFIS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* DMDTFPH                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'052ACVATICAN 08/14/18'                                      
         END                                                                    
