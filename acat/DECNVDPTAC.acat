*          DATA SET DECNVDPTAC AT LEVEL 005 AS OF 05/01/02                      
*CATALP DEDPTAC                                                                 
         TITLE 'GET/PUT MARKET TOTAL DATA FOR DPT CONVERSION'                   
DEDPTAC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDEND-WORKD,**DPTAC                                           
         USING WORKD,RC            RC=A(W/S)                                    
         L     R7,0(R1)                                                         
         USING DEMCOND,R7          R7=A(DEMCON GLOBAL W/S)                      
         L     R2,4(R1)            R2=A(MODIFIER LIST)                          
         L     RA,ACOMFACS                                                      
         USING COMFACSD,RA         RA=A(COMFACS)                                
         MVC   ACTION,0(R1)        SAVE ACTION CODE                             
         L     R6,AIREC                                                         
         CLI   ACTION,C'G'                                                      
         BNE   *+8                                                              
         L     R6,ASREC                                                         
         USING INTERD,R6           R6=A(INTERIM OR SORT RECORD)                 
         CLI   ACTION,C'I'                                                      
         BE    INIT                                                             
*                                  CHECK FOR FIRST TIME FOR MARKET/BOOK         
         MVC   MYDUB(2),INTMRKT                                                 
         MVC   MYDUB+2(2),INTBOOK                                               
         CLC   PREVKEY,MYDUB                                                    
         BE    MKT2                                                             
         L     RE,VPUTBUFF                                                      
         L     RF,PUTBUFFL                                                      
         XCEF                                                                   
         MVC   PREVKEY,MYDUB                                                    
*                                                                               
MKT2     LA    RE,1                FORCE TO ONE DIMENSIONAL ARRAY               
         LA    RF,1                                                             
         ST    RF,POSTDAYS         SET NUMBER OF DAYS/START DAY                 
         ST    RE,POSTDAYS+4                                                    
         ZIC   RF,INTDPT           GET DAYPART NUMBER                           
         LA    RE,1                ONLY COUNT AS 1 QUARTER HR                   
         STM   RE,RF,POSTQHRS      SET NUMBER OF QHRS/START QHR                 
*                                                                               
         CLI   ACTION,C'G'                                                      
         BE    GET                                                              
         MVI   ACTION,C'P'                                                      
         B     PUT                                                              
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
* INITIALIZE FOR PUTS                                                           
*                                                                               
INIT     XC    PREVKEY,PREVKEY                                                  
         LA    R5,MYDBLOCK                                                      
         USING DBLOCKD,R5                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBFILE,=C'DPT'                                                   
         MVC   DBSELSRC,OUTSRC                                                  
         MVC   DBSELMED,MEDIA                                                   
         MVC   DBSELBK,INTBOOK                                                  
         ST    RA,DBCOMFCS                                                      
         XC    OFORMAT,OFORMAT                                                  
         MVC   OFORMAT+7(2),INTBOOK                                             
         GOTO1 CDEMEL,MYDMCB,(C'D',OFORMAT),DBLOCKD                             
         CLI   DBERROR,0           TEST FOR ERRORS                              
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AODISP,16(R1)                                                    
         L     R3,AODISP                                                        
         USING DSPHDRD,R3          SET BXLE REGS (R3-R5)                        
         SR    R4,R4                                                            
         ICM   R4,3,DSPLDE                                                      
         ICM   R5,7,DSPAET                                                      
         LA    R5,0(R3,R5)                                                      
         BCTR  R5,0                                                             
         LA    R3,DSPHDRLN(R3)                                                  
         STM   R3,R5,MYDMCB                                                     
         LA    R8,IDISPS           R8=A(INPUT DISP LIST)                        
         LA    R9,ODISPS           R9=A(OUTPUT DISP LIST)                       
         SR    R0,R0               R0=N'ENTRIES IN LIST                         
*                                                                               
INIT2    CLI   0(R2),X'FF'         TEST E-O-L                                   
         BE    INIT12                                                           
         LM    R3,R5,MYDMCB                                                     
         USING DSPDTAD,R3                                                       
         LA    R1,POSTLINE                                                      
*                                  BUILD LIST OF OUTPUT DEMOS &                 
*                                  INPUT DEMO DISPS                             
INIT4    CLC   DSPMOD,0(R2)        MATCH MODIFIER WITH INPUT DEMO               
         BNE   INIT6                                                            
         MVC   0(1,R1),1(R2)       SET OUTPUT MODIFIER                          
         MVC   1(1,R1),DSPDEMO     SET OUTPUT DEMO NUMBER                       
         LR    RF,R3                                                            
         S     RF,MYDMCB                                                        
         SR    RE,RE                                                            
         D     RE,MYDMCB+4                                                      
         SLL   RF,2                                                             
         STCM  RF,3,2(R1)                                                       
         LA    R1,4(R1)            BUMP LIST POINTER                            
*                                                                               
INIT6    BXLE  R3,R4,INIT4                                                      
         MVI   0(R1),X'FF'         SET E-O-L                                    
         LA    R1,POSTLINE         RESET LIST PTR.                              
*                                                                               
INIT8    CLI   0(R1),X'FF'         TEST E-O-L                                   
         BE    INIT10                                                           
         LM    R3,R5,MYDMCB        RESET BXLE REGS.                             
         CLC   DSPMOD(2),0(R1)     LOCATE OUTPUT DEMO ENTRY                     
         BE    *+12                                                             
         BXLE  R3,R4,*-10                                                       
         B     INIT9                                                            
         LR    RF,R3                                                            
         S     RF,MYDMCB                                                        
         SR    RE,RE                                                            
         D     RE,MYDMCB+4                                                      
         SLL   RF,2                                                             
         STCM  RF,3,0(R9)                                                       
         MVC   0(2,R8),2(R1)       SET INPUT DEMO DISP.                         
         LA    R8,2(R8)            BUMP LIST PTRS. & ENTRY COUNT                
         LA    R9,2(R9)                                                         
         AH    R0,=H'1'                                                         
*                                                                               
INIT9    LA    R1,4(R1)                                                         
         B     INIT8                                                            
*                                                                               
INIT10   LA    R2,2(R2)            BUMP TO NEXT INPUT LIST ENTRY                
         B     INIT2                                                            
*                                                                               
INIT12   MVI   0(R8),X'FF'         SET E-O-LS                                   
         MVI   0(R9),X'FF'                                                      
         LR    R1,R0               R1=N'FIELDS TO ACCUMULATE                    
         CH    R1,=H'100'          TEST NGR MAX ALLOWED                         
         BNH   *+6                                                              
         DC    H'0'                                                             
         ST    R1,POSTACCS         SET N'ACCUMS                                 
         SLL   R1,2                                                             
         ST    R1,DAYBUFFL         SET L'ACCUM BUFFERS                          
         LA    R0,7                                                             
         MR    R0,R0                                                            
         ST    R1,QHRBUFFL                                                      
         LA    R0,80                                                            
         MR    R0,R0                                                            
         ST    R1,PUTBUFFL                                                      
         B     EXIT                                                             
         EJECT                                                                  
* PUT VALUES TO BUFFER                                                          
*                                                                               
PUT      LA    R1,IDISPS                                                        
         BAS   RE,EXTRACT                                                       
         LM    R0,R1,POSTQHRS                                                   
PUT2     LM    R8,R9,POSTDAYS                                                   
         BAS   RE,GETPUT                                                        
         LA    R9,1(R9)                                                         
         BCT   R8,*-8                                                           
         LA    R1,1(R1)                                                         
         BCT   R0,PUT2                                                          
         B     EXIT                                                             
         SPACE 2                                                                
* GET VALUES FROM BUFFER                                                        
*                                                                               
GET      XC    POSTLINE(200),POSTLINE                                           
         XC    POSTLINE+200(200),POSTLINE+200                                   
         LM    R0,R1,POSTQHRS                                                   
GET2     LM    R8,R9,POSTDAYS                                                   
         BAS   RE,GETPUT                                                        
         LA    R9,1(R9)                                                         
         BCT   R8,*-8                                                           
         LA    R1,1(R1)                                                         
         BCT   R0,GET2                                                          
*                                                                               
         L     R9,POSTDAYS                                                      
         M     R8,POSTQHRS         R9=AVERAGING FACTOR (DAYS*QTR HRS)           
         LA    RE,POSTLINE                                                      
         L     RF,POSTACCS                                                      
*                                  AVERAGE ACCUMULATOR VALUES                   
GET4     L     R0,0(RE)                                                         
         SR    R1,R1                                                            
         SRDA  R0,31                                                            
         DR    R0,R9                                                            
         LTR   R1,R1                                                            
         BM    *+8                                                              
         AH    R1,=H'1'                                                         
         SRA   R1,1                                                             
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         BCT   RF,GET4                                                          
         LA    R1,ODISPS                                                        
         BAS   RE,EXTRACT                                                       
         TM    INTSTA,X'F0'        TEST FOR MARKET TOTAL RECORD                 
         BNO   *+12                                                             
         LA    R1,IDISPS                                                        
         BAS   RE,EXTRACT                                                       
         B     EXIT                                                             
         EJECT                                                                  
* ADD A LINE OF VALUES FROM POSTLINE INTO BUFFER OR VICE-VERSA.                 
*                                                                               
* ON ENTRY R1=QTR HOUR NUMBER (0-79)                                            
*          R9=DAY NUMBER (0-6)                                                  
*                                                                               
GETPUT   NTR1                                                                   
         M     R0,QHRBUFFL                                                      
         M     R8,DAYBUFFL                                                      
         AR    R1,R9                                                            
         A     R1,VPUTBUFF         R1=A(DAY/QTR HOUR LINE)                      
         LA    RE,POSTLINE         RE=A(ACCUM LINE)                             
         L     RF,POSTACCS         RF=N'ACCUMS                                  
*                                                                               
GETPUT2  L     R0,0(R1)                                                         
         A     R0,0(RE)                                                         
         CLI   ACTION,C'P'                                                      
         BNE   *+12                                                             
         ST    R0,0(R1)            PUT VALUE TO BUFFER                          
         B     *+8                                                              
         ST    R0,0(RE)            GET VALUE INTO LINE                          
         LA    RE,4(RE)                                                         
         LA    R1,4(R1)                                                         
         BCT   RF,GETPUT2                                                       
         B     EXIT                                                             
         SPACE 1                                                                
* EXTRACT VALUES FROM POSTLINE OR INTACCS                                       
*                                                                               
EXTRACT  ST    RE,MYDUB                                                         
         LR    RE,R1               RE=A(DISPLACEMENT LIST)                      
         LA    R1,POSTLINE                                                      
EXTRACT2 SR    RF,RF                                                            
         ICM   RF,3,0(RE)                                                       
         BM    EXTRACTX                                                         
         LA    RF,INTACCS(RF)                                                   
         CLI   ACTION,C'P'                                                      
         BNE   *+14                                                             
         MVC   0(4,R1),0(RF)                                                    
         B     *+10                                                             
         MVC   0(4,RF),0(R1)                                                    
         LA    RE,2(RE)                                                         
         LA    R1,4(R1)                                                         
         B     EXTRACT2                                                         
EXTRACTX L     RE,MYDUB                                                         
         BR    RE                                                               
         EJECT                                                                  
* LITERALS ETC.                                                                 
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
PREVKEY  DS    XL4                                                              
POSTACCS DS    F                                                                
DAYBUFFL DS    F                                                                
QHRBUFFL DS    F                                                                
PUTBUFFL DS    F                                                                
POSTDAYS DS    2F                                                               
POSTQHRS DS    2F                                                               
IDISPS   DS    100XL2,X                                                         
ODISPS   DS    100XL2,X                                                         
         EJECT                                                                  
* DEINTD/DEINTDPTD                                                              
       ++INCLUDE DEINTD                                                         
       ++INCLUDE DEINTDPTD                                                      
         EJECT                                                                  
* DSECT TO COVER TEMPORARY WORKING STORAGE                                      
*                                                                               
WORKD    DSECT                                                                  
MYDUB    DS    D                                                                
MYDMCB   DS    6F                                                               
AODISP   DS    A                                                                
POSTLINE DS    100F                                                             
OFORMAT  DS    XL10                                                             
MYDBLOCK DS    CL256                                                            
ACTION   DS    CL1                                                              
WORKDEND EQU   *                                                                
         SPACE 1                                                                
* DEDEMCNVD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMCNVD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DEDBLOCK                                                                      
         PRINT OFF                                                              
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
         PRINT ON                                                               
         SPACE 1                                                                
* DEDEMTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DEDEMTABD                                                      
         PRINT ON                                                               
         SPACE 1                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DECNVDPTAC05/01/02'                                      
         END                                                                    
