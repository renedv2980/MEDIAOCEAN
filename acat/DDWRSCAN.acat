*          DATA SET DDWRSCAN   AT LEVEL 041 AS OF 10/30/13                      
*CATALP WRSCAN                                                                  
         TITLE '$WR - SCAN'                                                     
         PRINT NOGEN                                                            
WRSCAN   CSECT                                                                  
         NMOD1 WORKX-WORKD,**WRSCAN,RR=R4,CLEAR=YES                             
         USING WORKD,RC                                                         
*                                                                               
         ST    R1,APARM                                                         
         L     R4,0(R1)                                                         
         USING WSBLOCKD,R4         R4=A(FILTERS)                                
         L     R7,4(R1)                                                         
         USING WSCOUNTD,R7         R7=A(COUNTERS)                               
         ZAP   0(3,R7),=P'0'                                                    
         MVC   3(45,R7),0(R7)      ZAP COUNTS TO ZERO                           
         MVI   WSFILES,1           SET NO OF FILES 1                            
*                                                                               
         L     R6,8(R1)            R6=A(BUFFER)                                 
         ST    R6,ABUFFER                                                       
         USING WSEDATAD,R6         ENTRY DSECT                                  
         MVC   WSEDATA,FFS         MARK LAST ENTRY                              
*                                                                               
         LA    RF,L'WKINDEX                                                     
         STH   RF,CINDXLN                                                       
*                                                                               
         L     RF,16(R1)           GET COMFACS ROUTINES                         
         USING COMFACSD,RF                                                      
         MVC   ADATAMGR,CDATAMGR                                                
         MVC   ADATCON,CDATCON                                                  
         MVC   AXSORT,CXSORT                                                    
         DROP  RF                                                               
*                                                                               
         MVC   WRKFID,WSFILE       SET WKFILE DATA                              
*                                                                               
         TBIN  SECS                GET TIME & DATE                              
         SR    R0,R0                                                            
         D     R0,=F'600'                                                       
         ST    R1,TIMENOW          TIME NOW BIN 10MINS                          
*                                                                               
         GOTO1 ADATCON,DMCB,(5,0),(2,TODAY)                                     
*                                                                               
SRCH0B   MVI   FIND,1              SET SEARCHING PART1 INDEX                    
         GOTO1 ADATAMGR,WKDMCB,(0,=C'BUFFER'),WRKFID,NDX,SAVE,CXREC             
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R1,CXREC                                                         
         MVC   BUFFDATA,0(R1)      SAVE V(WKTAB)/V(WKFILE)                      
         MVC   CIDATA,12(R1)       SAVE FILE DATA FOR THIS WRKF FILE            
*                                                                               
         BAS   RE,CXLOOPI          INITIALISE INDEX SEARCH                      
         USING WKRECD,R5           R5=A(WRKFIL INDEX ENTRY)                     
*                                                                               
SRCH1    BAS   RE,GETXAD                                                        
         GOTO1 ADATAMGR,WKDMCB,=C'DMREAD',WRKFID,CXADDR,CXREC                   
         CLI   8(R1),0                                                          
         BNE   SRCHURR                                                          
*                                                                               
SRCH2    CLI   WKSTAT,WKSTPU       IGNORE PURGED                                
         BE    SRCHX                                                            
         OC    WKBKEY,WKBKEY       IGNORE PURGED                                
         BZ    SRCHX                                                            
         TM    WKSTAT,WKSTTE       TEST IF TEMPORARY                            
         BO    SRCH2D              COUNT AS NOT ACTIVE & NOT AVAIL              
*                                                                               
         TM    WKSTAT,WKSTAC+WKSTKE     ACTIVE KEEPS DON'T TEST RETAIN          
         BO    SRCH2A                                                           
*                                                                               
SRCH2A   TM    WKSTAT,WKSTKE       UNEXPIRED KEEPS ALWAYS COUNT ACTIVE          
         BO    *+12                                                             
         TM    WKSTAT,WKSTAC       TEST FOR ACTIVE CI                           
         BZ    SRCH2D                                                           
         CLI   FIND,1              TEST PART 1 OR 2                             
         BNE   *+14                                                             
         AP    WSC3,=P'1'          BUMP PART1 ACTIVE                            
         B     SRCH2D                                                           
         AP    WSC4,=P'1'          BUMP PART2 ACTIVE                            
*                                                                               
SRCH2D   CLI   FIND,1              TEST PART 1 OR 2                             
         BNE   SRCH2E                                                           
         AP    WSC1,=P'1'          BUMP PART1 NOTAVAIL                          
         B     SRCH2G                                                           
SRCH2E   AP    WSC2,=P'1'          BUMP PART2 NOTAVAIL                          
         B     SRCHX                                                            
*                                                                               
SRCH2F   TM    WSFLAGS,WSFEXPD     DO WE WANT EXPIRED                           
         BZ    SRCHX                                                            
*                                                                               
SRCH2G   EQU   *                                                                
*                                                                               
SRCH3    OC    WSSRCID,WSSRCID     TEST USER ID DEFINED                         
         BZ    SRCH3G                                                           
         TM    WSSRCID,X'80'       TEST GENERIC USER ID                         
         BZ    SRCH3B                                                           
         CLI   AGENIDS,X'FF'       TEST VGENIDS PRESENT                         
         BE    SRCHX                                                            
         GOTO1 AGENIDS,DMCB,WSSRCID,ADATAMGR                                    
         BNE   SRCHX                                                            
         LM    RE,RF,0(R1)         RE=N'ENTRIES,RF=A(ENTRIES)                   
         CLC   WKUSRID,0(RF)                                                    
         BE    SRCH3G              MATCH FOUND                                  
         LA    RF,2(RF)                                                         
         BCT   RE,*-14                                                          
         B     SRCHX               NO MATCH ON GENERIC                          
*                                                                               
SRCH3B   CLC   WKUSRID,WSSRCID     ID WAS SPECIFIED                             
         BNE   SRCHX                                                            
         CLI   WKSTAT,WKSTPU       IGNORE PURGED                                
         BE    SRCHX                                                            
*                                                                               
SRCH3G   OC    WSCLASN,WSCLASN     FILTER ON CLASS                              
         BZ    SRCH3GA                                                          
         LA    R1,8                WSCLASN IS 8 CHRS                            
         LA    RF,WSCLASN-1(R1)                                                 
         CLC   WKCLASS,0(RF)       TEST FOR EXCLUDE                             
         BE    SRCHX                                                            
         BCT   R1,*-14                                                          
*                                                                               
SRCH3GA  OC    WSCLAS,WSCLAS       ZERO MEANS INCLUDE ALL                       
         BZ    SRCH3H                                                           
         LA    R1,8                WSCLAS IS 8 CHRS                             
         LA    RF,WSCLAS-1(R1)                                                  
         CLC   WKCLASS,0(RF)       TEST FOR INCLUDE                             
         BE    SRCH3H                                                           
         BCT   R1,*-14                                                          
         B     SRCHX                                                            
*                                                                               
SRCH3H   OC    WSSYSPRG(4),WSSYSPRG   TEST ALL                                  
         BZ    SRCH3I                                                           
         LA    R0,4                                                             
         LA    RF,WSSYSPRG                                                      
         LA    RE,WKSYSPRG                                                      
SRCH3HA  CLI   0(RF),C'*'          * IS WILDCARD                                
         BE    SRCH3HB                                                          
         CLI   0(RF),X'0'          ZERO ALSO                                    
         BE    SRCH3HB                                                          
         CLC   0(1,RE),0(RF)       ELSE MUST MATCH                              
         BNE   SRCHX                                                            
SRCH3HB  LA    RF,1(RF)                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SRCH3HA          TEST NEXT CHAR                               
*                                                                               
SRCH3I   CLI   WSDAY,C'*'          * DAY MATCHES ALL                            
         BE    SRCH3K                                                           
         CLC   WSDAY,WKDAY                                                      
         BNE   SRCHX                                                            
*                                                                               
SRCH3K   OC    WSEXTRA,WSEXTRA     * EXTRA MATCHES ALL                          
         BZ    SRCH3X                                                           
         CLC   WSEXTRA,WKEXTRA                                                  
         BNE   SRCHX                                                            
*                                                                               
SRCH3X   AP    WSR,=P'1'           BUMP TOTAL REPORTS                           
         TM    WKSTAT,WKSTAC                                                    
         BZ    *+10                                                             
         AP    WSA,=P'1'           BUMP ACTIVE                                  
         TM    WKSTAT,WKSTHO                                                    
         BZ    *+10                                                             
         AP    WSH,=P'1'           BUMP HOLD                                    
         TM    WKSTAT,WKSTKE                                                    
         BZ    *+10                                                             
         AP    WSK,=P'1'           BUMP KEEP                                    
*                                                                               
WKSRCH4B SR    R1,R1               ANY STATUS TO EXCLUDE                        
         IC    R1,WSSTATN                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    WKSTAT,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,WSSTAT                                                      
         BZ    WKSRCH5             ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    WKSTAT,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
WKSRCH5  CLI   WSAGESV,0           TEST FOR NO LIMIT                            
         BE    WKSRCH5X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,WSAGESF        GET BC FILTER IN R1                          
         BZ    WKSRCH5X                                                         
         CLC   WKAGES,WSAGESV      TEST AGE FILTER                              
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,WKSRCH5X                                                       
WKSRCH5X EQU   *                                                                
*                                                                               
WKSRCH6  OC    WSAGELV,WSAGELV     TEST LIVE DATE                               
         BZ    WKSRCH6X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,WSAGELF        GET BC FILTER IN R1                          
         BZ    WKSRCH6X                                                         
         CLC   WKAGED,WSAGELV      TEST AGE FILTER                              
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,WKSRCH6X                                                       
WKSRCH6X EQU   *                                                                
*                                                                               
         TM    WSFLAGS,WSFTOTAL                                                 
         BZ    SRCHT                                                            
*                                                                               
TOTAL01  ST    R6,FULL             SAVE CURRENT R6                              
         L     R6,ABUFFER                                                       
TOTAL02  C     R6,FULL                                                          
         BE    SRCHT               NOT FOUND                                    
         CLC   WSEUSER,WKUSRID     SAME USERID                                  
         BE    TOTAL03                                                          
         LA    R6,L'WSEDATA(R6)    NEXT ENTRY                                   
         B     TOTAL02                                                          
*                                                                               
TOTAL03  ICM   R1,15,WSESORT       BUMP COUNT                                   
         LA    R1,1(R1)                                                         
         STCM  R1,15,WSESORT                                                    
         L     R6,FULL             RESTORE R6                                   
         B     SRCHX                                                            
*                                                                               
SRCHT    OC    WSSMAX,WSSMAX                                                    
         BZ    SRCHTA                                                           
         CP    WST,WSSMAX          ROOM IN TABLE                                
         BL    SRCHU                                                            
         BH    SRCHT1                                                           
SRCHTA   CP    WST,QTMAX           ROOM IN TABLE                                
         BL    SRCHU               YES                                          
SRCHT1   AP    WSX,=P'1'           WSX=COUNT OF NO ROOM ENTRYS                  
         B     SRCHX                                                            
*                                                                               
SRCHU    BAS   RE,GETCAD           SET UP TABLE ENTRY                           
         XC    0(L'WSEDATA,R6),0(R6)                                            
         MVC   WSEUSER,WKUSRID                                                  
         MVC   WSESTAT,WKSTAT                                                   
         MVC   WSECIAD,CIADDR      RETURN CI ADDR                               
*                                                                               
SORT     TM    WSFLAGS,WSFTOTAL    TEST FOR TOTALS                              
         BNO   SORT1                                                            
         MVC   WSESORT,=F'1'       SET TO 1                                     
         B     SORTX                                                            
*                                                                               
SORT1    CLI   WSSORT,1                                                         
         BNE   SORT2                                                            
         MVC   WSESORT+0(4),WKSYSPRG     ALPHA SORT                             
         B     SORTX                                                            
*                                                                               
SORT2    CLI   WSSORT,3                                                         
         BH    SORT4                                                            
         MVC   WSESORT+0(3),WKAGED                                              
         MVC   WSESORT+3(1),WKAGET                                              
         CLI   WSSORT,2                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -CREATE                                
         B     SORTX                                                            
*                                                                               
SORT4    CLI   WSSORT,5                                                         
         BH    SORT6                                                            
*NOP     MVC   WSESORT+2(2),WKAGEDD      SENT DATE SORT                         
*        CLI   WSSORT,4                                                         
*        BE    SORTX                                                            
*        XC    WSESORT,FFS               -SENT                                  
*        B     SORTX                                                            
*                                                                               
SORT6    CLI   WSSORT,7                                                         
         BH    SORT8                                                            
         CLI   WSSORT,6                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -RETAIN                                
         B     SORTX                                                            
*                                                                               
SORT8    CLI   WSSORT,9                                                         
         BH    SORT10                                                           
         MVC   WSESORT+3(1),WKAGES       SIZE SORT                              
         CLI   WSSORT,8                                                         
         BE    SORTX                                                            
         XC    WSESORT,FFS               -SIZE                                  
         B     SORTX                                                            
*                                                                               
SORT10   DC    H'0'                INVALID SORT VALUE                           
*                                                                               
SORTX    LA    R6,L'WSEDATA(R6)    NEXT ENTRY                                   
         AP    WST,=P'1'                                                        
         B     SRCHX                                                            
*                                                                               
SRCHX    BAS   RE,CXLOOPX          BUMP TO NEXT INDEX ENTRY                     
         B     SRCH2                                                            
         B     SRCH1               END OF PAGE                                  
*                                                                               
SRCHXX   CLI   FIND,2                                                           
         BE    SRCHY                                                            
         TM    WSFLAGS,WSFPART2    TEST SEARCH PART 2S                          
         BZ    SRCHY                                                            
         MVI   FIND,2              SET TO SEARCH PART2 FOR SIZE ACTION          
         BAS   RE,CXLOOPJ          POINT TO START OF PART2 INDEX                
         B     SRCH1                                                            
*                                                                               
SRCHY    B     SORT010                                                          
*                                                                               
SRCHURR  L     R1,APARM                                                         
         MVI   8(R1),X'80'         FLAG ERROR                                   
         B     EXITX                                                            
*                                                                               
SORT010  MVC   0(L'WSEDATA,R6),FFS SET END OF TABLE AND SORT ON KEY             
         XC    FULL,FULL                                                        
         ZAP   DUB,WST                                                          
         CVB   R6,DUB                                                           
         LA    R6,1(R6)                                                         
         CH    R6,=H'2'                                                         
         BNH   SORT01X                                                          
*                                                                               
         L     R0,ABUFFER                                                       
         XC    DMCB(24),DMCB                                                    
         MVI   DMCB+15,6           LENGTH                                       
         MVI   DMCB+19,0           DISPLACEMENT                                 
         TM    WSFLAGS,WSFXUSER                                                 
         BNO   SORT011             EXCLUDE USERID                               
*                                                                               
         MVI   DMCB+15,4           ON XSORT                                     
         MVI   DMCB+19,2                                                        
SORT011  GOTO1 AXSORT,DMCB,(R0),(R6),L'WSEDATA                                  
*                                                                               
SORT01X  LR    R1,R0                                                            
*                                                                               
EXITX    XMOD1                                                                  
         EJECT ,                                                                
**********************************************************************          
* CONSTANTS AND ROUTINES                                                        
**********************************************************************          
QTMAX    DC    PL3'800'            MAX NUMBER OF REPORTS                        
       ++INCLUDE DMWRKRR           WORKER ROUTINES                              
         LTORG                                                                  
FFS      DC    16X'FF'                                                          
WRKFIL   DC    C'WRKFIL'                                                        
         EJECT                                                                  
**********************************************************************          
* DSECTS                                                                        
**********************************************************************          
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FIND     DS    X                                                                
DMCB     DS    6F                                                               
WKDMCB   DS    6F                                                               
FLAG     DS    X                                                                
TODAY    DS    XL2                                                              
*                                                                               
TIMENOW  DS    F                                                                
*                                                                               
ABUFFER  DS    A                                                                
APARM    DS    A                                                                
ADATAMGR DS    A                                                                
ADATCON  DS    A                                                                
AGENIDS  DS    A                                                                
AXSORT   DS    A                                                                
*                                                                               
SAVE     DS    360C                SAVE AREA FOR DATAMGR CALLS                  
*                                                                               
AWRKFLST DS    A                                                                
WRKFIDSV DS    CL9                 SAVED WRKR NAME                              
WRKFID   DS    CL9                 NAME OF WRKR FOR DMGR                        
*                                                                               
BUFFDATA DS    0XL12               WRKFIL DATA GIVEN BY BUFFER ACTION           
BUWKTAB  DS    V                                                                
BUWKFILE DS    V                                                                
BUSAVE   DS    XL4                                                              
*                                                                               
       ++INCLUDE DMWRKRW                                                        
*                                                                               
         DS    0F                                                               
NDX      DS    0XL40               INDEX ENTRY                                  
NXSRCID  DS    XL2                                                              
NXSYSPRG DS    CL3                                                              
NXSUBPRG DS    XL1                                                              
NXDAY    DS    CL1                                                              
NXCLASS  DS    CL1                                                              
NXEXTRA  DS    CL1                                                              
         DS    XL1                                                              
NXFILENO DS    CL2                                                              
NXSTAT   DS    XL1                                                              
NXSEQ    DS    XL1                                                              
NXAGED   DS    PL3                                                              
NXAGET   DS    PL2                                                              
NXAGES   DS    XL1                                                              
*                                                                               
         DS    XL2                                                              
NXINFO   DS    XL2                                                              
NXFILNOX DS    XL2                                                              
NXCIADDR DS    XL2                                                              
NXFLAG   DS    XL1                                                              
         DS    XL1                                                              
NXUSRINF DS    XL8                                                              
         DS    CL24                                                             
*                                                                               
CXREC    DS    14336C                                                           
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
*DDCOMFACS                                                                      
*DDWKSCAND                                                                      
*DMWRKRD                                                                        
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE DDWRSCAND                                                      
       ++INCLUDE DMWRKRD                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'041DDWRSCAN  10/30/13'                                      
         END                                                                    
