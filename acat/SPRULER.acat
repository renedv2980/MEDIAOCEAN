*          DATA SET SPRULER    AT LEVEL 055 AS OF 09/11/19                      
*CATALP SPRULER                                                                 
         TITLE 'SPRULER - SPOTPAK BUY RULE ENFORCER'                            
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   NOTE- SEE SPRULERD FOR PARAMETER LIST AND DESCRIPTION OF                    
*         PROGRAM FUNCTION                                                      
*                                                                               
*   REGISTER USAGE - R9-TSARD, R8-COMMONS, R7-SBLOCK, RA-SPRULERD               
*                    R6-BUYREC                                                  
*                                                                               
***********************************************************************         
***********************************************************************         
* USER    JIRA      DATE                 CHANGE LOG                   *         
* ---- ---------- -------- ------------------------------------------ *         
* AKAT SPEC-32039 01/28/19 SUPPORT FOR 2 DECIMAL IMPRESSIONS          *         
* AKAT SPEC-8664  12/21/16 FIX 2 DECIMAL DEMOS BUG                    *         
***********************************************************************         
         SPACE 2                                                                
SPRULER  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**RULE,CLEAR=YES,RR=RE                                    
*                                                                               
         USING WORKD,RC                                                         
         MVC   CALLRD,4(RD)        SAVE BACK POINTER FOR HOOKS                  
         ST    RE,RELO                                                          
*                                                                               
         LA    R8,COMMONS          COMMON ROUTINES AND DATA                     
         USING COMMONS,R8                                                       
*                                                                               
         ST    RC,AWORKD                                                        
         L     RA,0(R1)            A(CONTROL BLOCK)                             
         USING SPRULRDD,RA                                                      
         L     R7,SPRUASBL         A(SBLOCK)                                    
         USING SBLOCKD,R7                                                       
         L     R6,SPRUABUY                                                      
         USING BUYREC,R6                                                        
*                                  SET ADDRESSES                                
         L     RF,SPRUACOM                                                      
         MVC   VDMGR,CDATAMGR-COMFACSD(RF)                                      
         MVC   VHEXOUT,CHEXOUT-COMFACSD(RF)                                     
         MVC   VDATCON,CDATCON-COMFACSD(RF)                                     
         MVC   VGETDAY,CGETDAY-COMFACSD(RF)                                     
         MVC   VCALLOV,CCALLOV-COMFACSD(RF)                                     
         MVC   VADDAY,CADDAY-COMFACSD(RF)                                       
*                                                                               
         GOTO1 VCALLOV,DMCB,0,X'D9000A0F'  GET A(DAYUNPK)                       
         MVC   VDAYUNPK,DMCB                                                    
         GOTO1 (RF),DMCB,0,X'D9000A7A'     GET A(STAPACK)                       
         MVC   VSTAPACK,DMCB                                                    
         GOTO1 (RF),DMCB,0,X'D9000AE0'     GET A(DEMOCON)                       
         MVC   VDEMOCON,DMCB                                                    
*                                                                               
         XC    SPRURTRN(SPRURTNL),SPRURTRN   CLEAR RETURN                       
*                                                                               
         OC    BSPARS(24),BSPARS   UNLESS THEY ARE ALREADY SET                  
         BNZ   SR14                                                             
         SR    R0,R0               SET BINSRCH PARS FOR SPOT DATE TBL           
         LA    R1,DTLIST                                                        
         SR    R2,R2                                                            
         LA    R3,DTLISTL                                                       
         LA    R4,3                KEY LENGTH                                   
         LA    R5,DTLMAX                                                        
         STM   R0,R5,BSPARS                                                     
SR14     DS    0H                                                               
*                                                                               
         CLI   CURAGM,0            FIRST TIME?                                  
         BNE   SR16                                                             
         BAS   RE,INITSAR          SOME TSAR INITIALIZATION                     
*                                                                               
         MVI   MYESTCTL,0          SET ESTIMATE CTL, NOT 'TOGETHER'             
         CLC   SBQEST,SBQESTND     IF START=END EST (SINGLE EST REQ)            
         BE    SR15                                                             
         CLI   SBQSEPES,C'Y'       OR IF SBQSEPES IS ON                         
         BE    SR15                                                             
         MVI   MYESTCTL,X'80'      SET 'TOGETHER'                               
*                                                                               
SR15     DS    0H                                                               
SR16     DS    0H                                                               
         L     RF,SPRUABUY         A(BUY RECORD)                                
         MVC   FULL(3),0(RF)       A/M/C                                        
         CLC   FULL(3),CURAGM      CHANGE OF AGY/MED/CLIENT?                    
         BE    SR18                NO, DONE                                     
         MVC   CURAGM(3),FULL                                                   
         XC    CURDATE,CURDATE                                                  
         BAS   RE,BLDRUBUF         BUILD RULE BUFFER                            
         TM    SPRUCTL,SPRUCGLQ    IF WILL DO GLOBALS                           
         BZ    SR18                                                             
         BAS   RE,INIDSBUF         INIT DPT/STATION TYPE BUFFER                 
         BAS   RE,INIPGBUF         INIT PROGRAM BUFFER                          
*                                                                               
SR18     DS    0H                                                               
         CLI   SPRUMODE,C'B'       PROCESS BUY MODE                             
         BNE   SR20                                                             
         BAS   RE,PRBUY                                                         
         B     EXIT                                                             
*                                                                               
SR20     DS    0H                                                               
         CLI   SPRUMODE,C'S'       PROCESS SPOT                                 
         BNE   SR30                                                             
         BAS   RE,PRSPOT                                                        
         B     EXIT                                                             
*                                                                               
SR30     DS    0H                                                               
         CLI   SPRUMODE,C'G'       PROCESS GLOBALS                              
         BNE   SR40                                                             
         BAS   RE,PRGLB                                                         
         B     EXIT                                                             
*                                                                               
SR40     DS    0H                                                               
         DC    H'0'                BAD SPRUMODE                                 
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INITSAR - INITIALIZE TSAR/TSAROFF AND BUILD BUFFER                     
*                                                                               
***********************************************************************         
         SPACE 2                                                                
INITSAR  NTR1                                                                   
         OC    ATSAR,ATSAR         HAVE WE GOT TSAR/TSAROFF YET?                
         BNZ   IT04                                                             
         MVC   WORK(8),=CL8'T00A7D' LOAD TSAR/TSAROFF                           
         L     RF,SPRUMAST                                                      
         L     RF,MCVLOADR-MASTD(RF)                                            
         GOTO1 (RF),DMCB,WORK,0,0                                               
         OC    DMCB+4(4),DMCB+4                                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   ATSAR,4(R1)                                                      
*                                                                               
*                               ************ FOR PATCH                          
         L     RF,ATSAR                                                         
         B     IT04                                                             
         DC    32X'00'                                                          
*                                                                               
IT04     DS    0H                                                               
         OC    ARUBUF,ARUBUF        DO WE HAVE RULE BUFFER YET                  
         BNZ   ITX                                                              
*                          ******  FOR TESTING                                  
*        L     RF,=A(TSTRUBUF)     USE TEST BUFFER AREA                         
*        A     RF,RELO                                                          
*        ST    RF,ARUBUF                                                        
*        B     ITX                                                              
*                                                                               
         L     R0,=A(RUBUFL)        SIZE OF RULE BUFFER                         
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R1,ARUBUF                                                        
*                                                                               
ITX      DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INIDSBUF- INITIALIZE DPT/STATION TYPE BUFFER                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
INIDSBUF NTR1                                                                   
         OC    ADSBUF,ADSBUF       DO WE HAVE BUY DPT/STYPE BUFF YET            
         BNZ   INDS04                                                           
*                          ******  FOR TESTING                                  
*        L     RF,=A(TSTDSBUF)     USE TEST BUFFER AREA                         
*        A     RF,RELO                                                          
*        ST    RF,ADSBUF                                                        
*        B     INDS04                                                           
*                                                                               
         L     R0,=A(DSBUFL)       SIZE OF BUY BUFFER                           
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R1,ADSBUF                                                        
*                                                                               
         L     R1,SPRUMAST                                                      
         USING MASTD,R1                                                         
         L     RE,ADSBUF           GET OFFLINE BUFFER IN DUMPS                  
         ST    RE,MCUSRDMP+8                                                    
         L     RF,=F'20000'                                                     
         LA    RE,0(RE,RF)                                                      
         ST    RE,MCUSRDMP+12                                                   
         DROP  R1                                                               
*                                                                               
INDS04   DS    0H                 INITIALIZE DS BUFFER                          
         LA    R9,TSARBDS         DS TSAR BLOCK                                 
         USING TSARD,R9                                                         
         MVI   TSOFFACT,TSAINI                                                  
         MVC   TSACOM,SPRUACOM                                                  
         MVC   TSAREC,=A(DSBUFL)                                                
         MVC   TSABUF,ADSBUF                                                    
         MVI   TSKEYL,DSBUFKYL                                                  
         MVC   TSRECL,=Y(DSBUFRCL)                                              
         GOTO1 ATSAR,TSARD                                                      
         LA    R0,DSBUFRC        SET RECORD ADDRESS                             
         ST    R0,TSAREC                                                        
INDSX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INIPGBUF- INITIALIZE PGR/TYPE BUFFER                                   
*                                                                               
***********************************************************************         
         SPACE 2                                                                
INIPGBUF NTR1                                                                   
         OC    APGBUF,APGBUF       DO WE HAVE BUFFER ALREADY                    
         BNZ   INPG04                                                           
*                          ******  FOR TESTING                                  
*        L     RF,=A(TSTPGBUF)     USE TEST BUFFER AREA                         
*        A     RF,RELO                                                          
*        ST    RF,APGBUF                                                        
*        B     INPG04                                                           
*                                                                               
         L     R0,=A(PGBUFL)       SIZE OF BUY BUFFER                           
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R1,APGBUF                                                        
*                                                                               
INPG04   DS    0H                 INITIALIZE PG BUFFER                          
         LA    R9,TSARBPG         PG TSAR BLOCK                                 
         USING TSARD,R9                                                         
         MVI   TSOFFACT,TSAINI                                                  
         MVC   TSACOM,SPRUACOM                                                  
         MVC   TSAREC,=A(PGBUFL)                                                
         MVC   TSABUF,APGBUF                                                    
         MVI   TSKEYL,PGBUFKYL                                                  
         MVC   TSRECL,=Y(PGBUFRCL)                                              
         GOTO1 ATSAR,TSARD                                                      
         LA    R0,PGBUFRC        SET RECORD ADDRESS                             
         ST    R0,TSAREC                                                        
*                                                                               
INPGX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BLDRBUF - BUILD RULE BUFFER                                            
*                                                                               
***********************************************************************         
         SPACE 2                                                                
BLDRUBUF NTR1                                                                   
*                                 INITIALIZE RULE BUFFER                        
         LA    R9,TSARBRU          RULES TSAR BLOCK                             
         USING TSARD,R9                                                         
         MVI   TSOFFACT,TSAINI                                                  
         MVC   TSACOM,SPRUACOM                                                  
         MVC   TSAREC,=A(RUBUFL)                                                
         MVC   TSABUF,ARUBUF                                                    
         OI    TSRECI,TSRVAR       VARIABLE LENGTH RECS                         
         MVI   TSKEYL,RUBUFKYL                                                  
         MVC   TSRECL,=Y(RUBUFRCL)   MAXIMUM LENGTH                             
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
         LA    R5,RUBUFRC         SET RECORD ADDRESS                            
         ST    R5,TSAREC                                                        
         USING RUBUFRCD,R5                                                      
         MVI   GLBCTL,0                                                         
*                                                                               
         XC    KEY,KEY          READ RULE RECORDS AND ADD TO TSAR BUFF          
         LA    R2,KEY                                                           
         USING RULKEY,R2                                                        
         MVC   RULKTYP,=X'0D7C'                                                 
         MVC   RULKAGMD,CURAGM                                                  
         MVC   RULKCLT,CURCLT                                                   
*                                                                               
         BAS   RE,HIGH                                                          
         B     BRB04B                                                           
*                                                                               
BRB04    DS    0H                                                               
         BAS   RE,SEQ                                                           
BRB04B   DS    0H                                                               
         CLC   KEY(5),KEYSAVE   TEST THRU CLIENT                                
         BNE   BRB40                                                            
*                                                                               
         XC    RUBRCD(30),RUBRCD   CLEAR KEY+                                   
         MVC   RUBRTYP,RULKRUL     RULE TYPE                                    
         MVC   RUBPRD,RULKPRD      PRODUCT                                      
         CLI   RUBPRD,0            0=FF                                         
         BNE   *+8                                                              
         MVI   RUBPRD,X'FF'                                                     
         MVC   RUBEST+1(1),RULKEST    ESTIMATE                                  
         CLI   RULKEST,0              0=FFFF                                    
         BNE   *+10                                                             
         MVC   RUBEST,=X'FFFF'                                                  
*                                                                               
         BAS   RE,GET                                                           
         LA    R4,IOA                                                           
         LA    R4,RULEL-RULRECD(R4)                                             
         LA    R3,RUBRULES                                                      
*                                                                               
BRB10    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BRB38                                                            
         CLI   0(R4),X'05'         ALL RULE ELEMS HAVE SAME CODE                
         BE    BRB20                                                            
*                                                                               
BRB12    DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BRB10                                                            
*                                                                               
BRB20    DS    0H                  MOVE ELEMENT TO TSAR RECORD                  
         USING RULDATAD,R4                                                      
*                                                                               
         CLI   RULKRUL,RULKRDTQ  DAY/TIME                                       
         BNE   *+8               NO                                             
         OI    RUDTSTYP,X'40'    SPACE PAD                                      
*                                                                               
         CLI   RULKRUL,RULKRPGQ  FOR PROG RULE                                  
         BNE   BRB22                                                            
         OC    RUPGMIN,RUPGMIN   IF HAVE MIN                                    
         BNZ   BRB20B                                                           
         OC    RUPGMAX,RUPGMAX   OR MAX > 0                                     
         BZ    BRB36                                                            
*                                                                               
BRB20B   DS    0H                                                               
         OI    GLBCTL,X'80'        SET WILL NEED PROGRAM TABLE                  
         B     BRB36                                                            
*                                                                               
BRB22    DS    0H                                                               
         CLI   RULKRUL,RULKRSWQ  FOR SPOTS/WEEK RULE                            
         BNE   BRB24                                                            
         OI    GLBCTL,X'80'        SET WILL NEED PROGRAM TABLE                  
         B     BRB36                                                            
*                                                                               
BRB24    DS    0H                                                               
         CLI   RULKRUL,RULKRDSQ  FOR DAYPART/STATION TYPE RULE                  
         BNE   BRB26                                                            
         OC    RUDSMIN,RUDSMIN   IF HAVE MIN                                    
         BNZ   BRB24B                                                           
         OC    RUDSMAX,RUDSMAX   OR MAX > 0                                     
         BZ    BRB36                                                            
*                                                                               
BRB24B   DS    0H                                                               
         OI    GLBCTL,X'40'        SET WILL SPT/STYPE TABLE                     
         B     BRB36                                                            
*                                                                               
BRB26    DS    0H                                                               
BRB36    DS    0H                                                               
         ZIC   RF,RULDLN                                                        
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R4)                                                    
*                                                                               
         CLI   RULKRUL,RULKRRTQ    DOING RATINGS?                               
         BNE   BRB36B              NO                                           
****                                                                            
* BECAUSE OF 2 DECIMAL IMPS SUPPORT, WE NEED TO LEAVE                           
* RURTMIN & RURTMAX AS IS AND ADJUST WHEN BEFORE WE TEST AGAINST                
* THE BUY'S RATING/IMP                                                          
****                                                                            
TSAR     USING RULDATAD,R3                                                      
***      TM    TSAR.RURTMIN,X'40'  DEMO HAVE 2 DEC                              
***      BO    BRB36A              YES                                          
*                                                                               
***      TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
***      BZ    BRB36B               NO - LEAVE IT ALONE                         
*                                                                               
***      L     R0,TSAR.RURTMIN     ADJUST 1 DECIMAL PRECISION TO 2              
***      MHI   R0,10                                                            
***      ST    R0,TSAR.RURTMIN                                                  
***      L     R0,TSAR.RURTMAX     ADJUST 1 DECIMAL PRECISION TO 2              
***      MHI   R0,10                                                            
***      ST    R0,TSAR.RURTMAX                                                  
***      B     BRB36B                                                           
*                                                                               
***BRB36A   NI    TSAR.RURTMIN,X'FF'-X'40'   TURN OFF 2 DEC FLAG                
***      NI    TSAR.RURTMAX,X'FF'-X'40'   TURN OFF 2 DEC FLAG                   
***      TM    SBEFLAG4,SBE42DEC   USER WANT 2 DEC                              
***      BNZ   BRB36B               YES - LEAVE IT ALONE                        
*                                                                               
***      L     R0,TSAR.RURTMIN     ADJUST 2 DECIMAL PRECISION TO 1              
***      SRDA  R0,31               R1 = 2*RURTMIN                               
***      D     R0,=F'10'                                                        
***      AHI   R1,1                                                             
***      SRA   R1,1                                                             
***      ST    R1,TSAR.RURTMIN                                                  
*                                                                               
***      L     R0,TSAR.RURTMAX     ADJUST 2 DECIMAL PRECISION TO 1              
***      SRDA  R0,31               R1 = 2*RURTMAX                               
***      D     R0,=F'10'                                                        
***      AHI   R1,1                                                             
***      SRA   R1,1                                                             
***      ST    R1,TSAR.RURTMAX                                                  
***      DROP  TSAR                                                             
*                                                                               
BRB36B   LA    R3,1(R3,RF)                                                      
         MVI   0(R3),0             SET EOR                                      
         B     BRB12                                                            
*                                                                               
BRB38    DS    0H                                                               
         MVI   TSOFFACT,TSAADD                                                  
         LA    R0,RUBUFRCD         CALCULATE RECORD LENGTH                      
         LA    RF,1(R3)                                                         
         SR    RF,R0                                                            
         STCM  RF,3,RUBRLEN                                                     
         GOTO1 ATSAR,TSARD                                                      
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     BRB04               NEXT RECORD                                  
*                                                                               
BRB40    DS    0H                  END OF RECORDS                               
         TM    SPRUTRCE,X'80'      TEST TO TRACE RULES                          
         BZ    BRBX                                                             
         MVC   PL(9),=C'<<RULES>>'                                              
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
         MVI   RUBRTYP,0                                                        
         MVI   TSOFFACT,TSARDH                                                  
         B     BRB42B                                                           
*                                                                               
BRB42    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
BRB42B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
         TM    TSERRS,TSEEOF                                                    
         BO    BRB46                                                            
*                                                                               
         LA    R2,RUBUFKYL+2                                                    
         GOTO1 VHEXOUT,DMCB,RUBRCD,PL,(R2),=C'N'                                
         MVI   SPRUMSGT,SPRUMRUQ    RULE                                        
         BAS   RE,PLHOOK                                                        
         LA    R4,RUBRULES                                                      
*                                                                               
BRB43    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BRB42                                                            
         ZIC   R2,1(R4)                                                         
         GOTO1 VHEXOUT,DMCB,(R4),PL+2,(R2),=C'N'                                
         MVI   SPRUMSGT,SPRUMRUQ    RULE                                        
         BAS   RE,PLHOOK                                                        
*                                                                               
         AR    R4,R2               NEXT ELEM                                    
         B     BRB43                                                            
*                                                                               
BRB46    DS    0H                                                               
BRBX     DS    0H                                                               
         B     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        PRBUY - PROCESS BUY RECORD                                             
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PRBUY    NTR1                                                                   
         XC    CURDATE,CURDATE    CLEAR SPOT DATE SAVE                          
         EDIT  (B2,BUYMSTA),(4,SVMKT),FILL=0 SAVE MARKET                        
         L     R3,SBACHUNK                                                      
         USING SCHUNKD,R3                                                       
*                                                                               
         TM    SPRUTRCE,X'40'      TEST TO TRACE BUYS                           
         BZ    PRB10                                                            
         MVC   PL(7),=C'<<BUY>>'                                                
         BAS   RE,SETBUY                                                        
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
*                                                                               
PRB04    DS    0H                                                               
         OC    SCNEXT,SCNEXT                                                    
         BZ    PRB10                                                            
         MVC   PL(9),=C'<<CHUNK>>'                                              
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         GOTO1 VHEXOUT,DMCB,(R3),PL+10,60,=C'N'                                 
         BAS   RE,PLHOOK                                                        
         GOTO1 (RF),(R1),60(R3)                                                 
         BAS   RE,PLHOOK                                                        
         GOTO1 (RF),(R1),120(R3)                                                
         BAS   RE,PLHOOK                                                        
         MVI   PL+10,C'*'                                                       
         BAS   RE,PLHOOK                                                        
*                                                                               
         L     R3,SCNEXT                                                        
         B     PRB04                                                            
*                                                                               
PRB10    DS    0H                                                               
         MVI   SDDLST,X'FF'        CLEAR SPOTS/DAY DUPE LIST                    
         L     R3,SBACHUNK                                                      
         OC    SCNEXT,SCNEXT       SKIP BUY IF NO CHUNKS                        
         BZ    PRBX                                                             
         TM    GLBCTL,X'80'        DO WE NEED DPT/STYPE BUFFER?                 
         BC    0,PRB40           **NOOP TEST**                                  
*                                                                               
         LA    R9,TSARBDS         DPT/STATION TYPE TSAR                         
         USING TSARD,R9                                                         
         LA    R5,DSBUFRC                                                       
         USING DSBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
*                                                                               
         L     R3,SBACHUNK                                                      
         USING SCHUNKD,R3                                                       
*                                                                               
PRB14    DS    0H                                                               
         OC    SCNEXT,SCNEXT       LAST CHUNK?                                  
         BZ    PRB40                                                            
         CLI   SCPRD1,X'FF'        ALWAYS SKIP FF                               
         BE    PRB38                                                            
         CLI   SBQBPRD,0           PRODUCT FILTER                               
         BE    PRB15                                                            
         CLC   SCPRD1,SBQBPRD                                                   
         BNE   PRB38                                                            
*                                                                               
PRB15    DS    0H                                                               
         LA    R5,SVDSBK           BUILD KEY IN SVDSBK                          
         MVC   DSBPRD,SCPRD1                                                    
         MVC   DSBEST,BUYKEST      EST                                          
         TM    MYESTCTL,X'80'      TEST 'TOGETHER'                              
         BZ    *+8                                                              
         MVI   DSBEST,0                                                         
         MVC   DSBDPT,BDDAYPT                                                   
         MVC   DSBSTYP,SBSTYPE                                                  
*                                                                               
         MVC   WORK(4),SCSPOTS     SPOT COUNT                                   
         MVC   WORK+4(4),SCDEMOS   USING UNEQUIVALENCED FIRST DEMO??            
*                                                                               
         BAS   RE,PRBDSPUT                                                      
         MVI   DSBSTYP,0          ALL STYPES FOR DAYPART                        
         BAS   RE,PRBDSPUT                                                      
         MVI   DSBDPT,0           ALL STYPES FOR ALL DPTS                       
         BAS   RE,PRBDSPUT                                                      
         MVC   DSBSTYP,SBSTYPE    ALL DPTS FOR THIS STYPE                       
         BAS   RE,PRBDSPUT                                                      
*                                                                               
PRB38    DS    0H                                                               
         L     R3,SCNEXT           NEXT CHUNK                                   
         B     PRB14                                                            
*                                                                               
PRB40    DS    0H                                                               
         TM    GLBCTL,X'40'        DO WE NEED PROGRAM BUFFER?                   
         BC    0,PRB80           **NOOP TEST**                                  
*                                                                               
         LA    R9,TSARBPG         PROGRAM BUFFER                                
         USING TSARD,R9                                                         
         LA    R5,PGBUFRC                                                       
         USING PGBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
*                                                                               
         L     R3,SBACHUNK                                                      
         USING SCHUNKD,R3                                                       
*                                                                               
PRB44    DS    0H                                                               
         OC    SCNEXT,SCNEXT       LAST CHUNK?                                  
         BZ    PRB80                                                            
         CLI   SCPRD1,X'FF'        ALWAYS SKIP FF                               
         BE    PRB48                                                            
         CLI   SBQBPRD,0            PRODUCT FILTER                              
         BE    PRB45                                                            
         CLC   SCPRD1,SBQBPRD                                                   
         BNE   PRB48                                                            
         MVI   DPTCTL,X'FF'        START WITH DPT=ALL (AND ALWAYS DO)           
*                                                                               
PRB45    DS    0H                                                               
         LA    R5,SVPGBK          BUILD KEY IN SVPGBK                           
         MVC   PGBPRD,SCPRD1                                                    
         MVC   PGBEST,BUYKEST                                                   
         TM    MYESTCTL,X'80'      TEST 'TOGETHER'                              
         BZ    *+8                                                              
         MVI   PGBEST,0                                                         
         MVC   PGBWEEK,SCDATE     SCDATE IS WEEK                                
         MVC   PGBPGM(17),BDPROGRM                                              
         MVI   PGBPGM+17,C' '                                                   
         MVI   PGBPGM+18,C' '                                                   
         MVI   PGBPGM+19,C' '                                                   
         MVC   PGBDPT,DPTCTL                                                    
*                                                                               
         MVC   WORK(4),SCSPOTS     SPOT COUNT                                   
         MVC   WORK+4(4),SCDEMOS   USING UNEQUIVALENCED FIRST DEMO??            
*                                                                               
         MVC   PGBSTA,SBBSTA       STATION                                      
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBSTA,FFS          ALL STATIONS                                 
         BAS   RE,PRBPGPUT                                                      
         XC    PGBWEEK,PGBWEEK     TOTAL ACROSS WEEKS                           
         MVC   PGBSTA,SBBSTA       STATION                                      
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBSTA,FFS          ALL STATIONS                                 
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBPGM,SPACES                                                    
         CLI   BDPROGT,C' '       ANY PROGRAM TYPE?                             
         BNH   PRB46                                                            
         MVI   PGBPGM,X'FD'       =PROGRAM TYPE                                 
         MVC   PGBPGM+1(1),BDPROGT                                              
         MVC   PGBWEEK,SCDATE                                                   
         MVC   PGBSTA,SBBSTA       STATION                                      
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBSTA,FFS          ALL STATIONS                                 
         BAS   RE,PRBPGPUT                                                      
         XC    PGBWEEK,PGBWEEK     TOTAL ACROSS WEEKS                           
         MVC   PGBSTA,SBBSTA       STATION                                      
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBSTA,FFS          ALL STATIONS                                 
         BAS   RE,PRBPGPUT                                                      
*                                                                               
PRB46    DS    0H                                                               
         XC    PGBPGM,PGBPGM        ALL PROGRAMS                                
         MVC   PGBWEEK,SCDATE                                                   
         MVC   PGBSTA,SBBSTA       STATION                                      
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBSTA,FFS          ALL STATIONS                                 
         BAS   RE,PRBPGPUT                                                      
         XC    PGBWEEK,PGBWEEK     TOTAL ACROSS WEEKS                           
         MVC   PGBSTA,SBBSTA       STATION                                      
         BAS   RE,PRBPGPUT                                                      
         MVC   PGBSTA,FFS          ALL STATIONS                                 
         BAS   RE,PRBPGPUT                                                      
*                                                                               
         CLI   DPTCTL,X'FF'        HAVE WE JUST DONE DPT=ALL?                   
         BNE   PRB48               NO, DONE - HAVE JUST DONE BUY DPT            
         CLI   SPRUBGPR+1,C'Y'     IF NOT CHECKING BY DPT                       
         BNE   PRB48               ALSO, DONE                                   
*                                                                               
         MVC   DPTCTL,BDDAYPT      ELSE DO IT NOW                               
         B     PRB45                                                            
*                                                                               
PRB48    DS    0H                                                               
         L     R3,SCNEXT           NEXT CHUNK                                   
         B     PRB44                                                            
*                                                                               
PRB80    DS    0H                                                               
*                                                                               
*************************                                                       
*   APPLY BUYLINE RULES                                                         
*************************                                                       
*                                                                               
         MVC   GREST,BUYKEST                                                    
*                                                                               
         XC    CHKPRDL,CHKPRDL     CLEAR PROD CHECK-OFF LIST                    
         L     R3,SBACHUNK         GO THROUGH CHUNKS                            
         USING SCHUNKD,R3                                                       
*                                                                               
PRB84    DS    0H                                                               
         OC    SCNEXT,SCNEXT       END OF CHUNKS                                
         BZ    PRB90               YES, DONE                                    
*                                                                               
         CLI   SCPRD1,X'FF'        ALLWAYS SKIP POL                             
         BE    PRB88                                                            
         CLI   SBQBPRD,0           ALL PRODUCTS?                                
         BE    PRB86                                                            
         CLC   SCPRD1,SBQBPRD      RIGHT PRODUCT?                               
         BNE   PRB88                                                            
*                                                                               
PRB86    DS    0H                                                               
         ZIC   RF,SCPRD1           BUT HAVE WE ALREADY DONE THIS PROD?          
         LA    RE,CHKPRDL(RF)                                                   
         CLI   0(RE),0                                                          
         BNE   PRB88               YES, NOT AGAIN                               
         STC   RF,0(RE)                                                         
*                                                                               
         MVC   GRPRD,SCPRD1        SET PROD FOR RULE LOOKUP                     
         MVC   SBBPRD,GRPRD                                                     
         MVC   BYTE,GRPRD          GET ALPHA                                    
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         GOTO1 =A(BTPGM),RR=RELO     BUY TEST - PROGRAM                         
         GOTO1 =A(BTDST),RR=RELO     BUY TEST - DAYPART/STATION TYPE            
         GOTO1 =A(BTRTG),RR=RELO     BUY TEST - RATING MIN/MAX                  
         GOTO1 =A(BT2SD),RR=RELO     BUY TEST - SPOTS/DAY 2ND METHOD            
*                                                                               
PRB88    DS    0H                  NEXT CHUNK                                   
         L     R3,SCNEXT                                                        
         B     PRB84                                                            
*                                  SOME ROUTINES DON'T USE CHUNKS               
PRB90    DS    0H                                                               
         GOTO1 =A(BTSD),RR=RELO      BUY TEST - SPOTS/DAY 1ST METHOD            
         GOTO1 =A(BTHOL),RR=RELO     BUY TEST - HOLIDAYS                        
         GOTO1 =A(BTDAYTM),RR=RELO   BUY TEST - DAY/TIME                        
         XC    BSPARS+8(4),BSPARS+8      CLEAR SPOT DATE LIST                   
*                                                                               
PRBX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        PRSPOT - PROCESS SPOT ELEM                                             
*                                                                               
*        NOTE- SPOTS BEFORE BUYS                                                
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PRSPOT   NTR1                                                                   
         L     R3,SBASPOT                                                       
         USING REGELEM,R3                                                       
         TM    SPRUTRCE,X'20'      TEST TO TRACE SPOTS                          
         BZ    PRS10                                                            
         L     RF,SPRUABUY                                                      
*                                                                               
         MVC   PL(8),=C'<<SPOT>>'                                               
         ZIC   R2,1(R3)                                                         
         GOTO1 VHEXOUT,DMCB,(R3),PL+10,(R2),=C'N'                               
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
*                                                                               
PRS10    DS    0H                                                               
         BAS   RE,DTLBLD           BUILD SPOT DATE TABLE                        
*                                                                               
PRSX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
****************************                                                    
*   BUILD SPOT DATE TABLE                                                       
****************************                                                    
*                                                                               
DTLBLD   NTR1                                                                   
         MVI   WPRD1,0                                                          
         MVI   WPRD2,0                                                          
         CLI   BUYKPRD,X'FF'       IF POL                                       
         BNE   DB30                                                             
         TM    RSTATUS,X'C0'       SKIP MINUSED AND MINUS SPOTS                 
         BNZ   DBX                                                              
         CLI   RLEN,10             SKIP UNALLOCATED                             
         BE    DBX                                                              
         MVC   WPRD1,RPPRD                                                      
         CLI   RLEN,14                                                          
         BE    DB36                                                             
         MVC   WPRD2,RPPRD+4                                                    
         B     DB36                                                             
*                                                                               
DB30     DS    0H                  NON-POOL                                     
         MVC   WPRD1,BUYKPRD                                                    
         LA    R2,BDELEM           LOOK FOR PIGGY                               
*                                                                               
DB32     DS    0H                                                               
         CLI   0(R2),0             EOR - NON-PIGGY                              
         BE    DB36                                                             
         CLI   0(R2),4                                                          
         BE    DB32D                                                            
         ZIC   R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     DB32                                                             
*                                                                               
DB32D    DS    0H                                                               
         MVC   WPRD2,PBPROD-PBELEM(R2)                                          
*                                                                               
DB36     DS    0H                  BUILD TABLE ENTRY                            
         LA    RF,WORK                                                          
         USING DTLISTD,RF                                                       
         MVC   DTLDAT,RDATE                                                     
         MVC   DTLCOUNT,=H'1'                                                   
*                                                                               
         CLI   WPRD1,0                                                          
         BE    DB40                                                             
         CLI   SBQBPRD,0           ALL PRODUCTS, OK                             
         BE    DB38                                                             
         CLC   WPRD1,SBQBPRD       ELSE MUST BE RIGHT PRODUCT                   
         BNE   DB40                                                             
*                                                                               
DB38     DS    0H                                                               
         MVC   DTLPRD,WPRD1                                                     
         BAS   RE,DBBINS           DO BINSRCH CALL                              
*                                                                               
DB40     DS    0H                                                               
         CLI   WPRD2,0                                                          
         BE    DB44                                                             
         CLI   SBQBPRD,0           ALL PRODUCTS, OK                             
         BE    DB42                                                             
         CLC   WPRD2,SBQBPRD       ELSE MUST BE RIGHT PRODUCT                   
         BNE   DB44                                                             
*                                                                               
DB42     DS    0H                                                               
         MVC   DTLPRD,WPRD2                                                     
         BAS   RE,DBBINS           DO BINSRCH CALL                              
*                                                                               
DB44     DS    0H                  SPOT NOT IN LIST                             
*                                                                               
DBX      DS    0H                                                               
         B     EXIT                                                             
*                                                                               
DBBINS   NTR1                                                                   
         GOTO1 SPRUBINS,BSPARS,(1,WORK)                                         
         SR    RF,RF                                                            
         ICM   RF,7,1(R1)                                                       
         BNZ   *+6                                                              
         DC    H'0'                TABLE FULL                                   
         CLI   0(R1),1             NEW RECORD?                                  
         BE    DBBINSX             YES, OK                                      
*                                                                               
         SR    RE,RE               ALREADY IN LIST, SO BUMP COUNT               
         ICM   RE,3,DTLCOUNT                                                    
         LA    R1,1                                                             
         TM    RSTATUS,X'80'       MINUS SPOT?                                  
         BZ    *+6                                                              
         LCR   R1,R1                                                            
         AR    RE,R1                                                            
         STCM  RE,3,DTLCOUNT                                                    
*                                                                               
DBBINSX  DS    0H                                                               
         B     DBX                                                              
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        PRGLB - PROCESS TOTALS                                                 
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PRGLB    NTR1                                                                   
         TM    SPRUTRCE,X'10'      TEST TO TRACE GLOBALS                        
         BZ    PRG03                                                            
         MVC   PL(10),=C'<<GLOBAL>>'                                            
         MVC   PL+11(4),=C'MKT='                                                
         MVC   PL+16(4),SVMKT                                                   
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
*                                                                               
PRG03    DS    0H                                                               
         BAS   RE,DSPRT            PRINT DS BUFFER                              
         BAS   RE,PGPRT            PRINT PGM BUFFER                             
         BAS   RE,PGSPRT           PRINT PGM/STA BUFFER                         
*                                                                               
***********************                                                         
*   APPLY GLOBAL RULES                                                          
***********************                                                         
*                                                                               
PRG10    DS    0H                                                               
         GOTO1 =A(GTDST),RR=RELO   GLOBAL LEVEL - DPT/STYPE                     
         GOTO1 =A(GTPGM),RR=RELO   GLOBAL LEVEL - PROGRAM RULE                  
         GOTO1 =A(GTSPW),RR=RELO   GLOBAL LEVEL - SPOTS/WEEK                    
*                                                                               
PRGLBX   DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        DSPRT - PRINT DS BUFFER                                                
*                                                                               
***********************************************************************         
         SPACE 2                                                                
DSPRT    NTR1                                                                   
         TM    SPRUTRCE,X'10'      TEST TO TOTAL TABLES                         
         BZ    DSP03                                                            
         MVC   PL(18),=C'<<DPT/SLN BUFFER>>'                                    
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
*                                                                               
DSP03    DS    0H                                                               
         LA    R9,TSARBDS         DPT/STATION TYPE TSAR                         
         USING TSARD,R9                                                         
         LA    R5,DSBUFRC                                                       
         USING DSBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
         MVI   DSBKEY,0                                                         
         MVI   TSOFFACT,TSARDH                                                  
         B     DSP04B                                                           
*                                                                               
DSP04    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
DSP04B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
         TM    TSERRS,TSEEOF                                                    
         BO    DSP10                                                            
*                                                                               
         CLI   DSBDPT,0            IF TOTAL                                     
         BNE   DSP06                                                            
         CLI   DSBSTYP,0                                                        
         BNE   DSP06                                                            
         MVC   FULL,DSBDEMV        SAVE IT                                      
*                                                                               
DSP06    DS    0H                                                               
         MVC   BYTE,DSBDPT         DAYPART                                      
         LA    R2,WORK                                                          
         BAS   RE,DSPDPT2                                                       
         MVC   PL+02(4),WORK                                                    
         MVC   BYTE,DSBSTYP        STATION TYPE                                 
         LA    R2,WORK                                                          
         BAS   RE,DSPSTYP2                                                      
         MVC   PL+13(4),WORK                                                    
*                                                                               
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BNZ   DSP07               YES - DISPLAY IN 2 DECIMAL                   
*                                                                               
DSP06A   EDIT  (B4,DSBDEMV),(7,PL+22),1                                         
         B     DSP07A                                                           
*                                                                               
DSP07    EDIT  (B4,DSBDEMV),(7,PL+22),2                                         
*                                                                               
DSP07A   EDIT  (B4,DSBSPTS),(4,PL+39)                                           
         ICM   R1,15,DSBDEMV       CALC % OF TOTAL                              
         M     R0,=F'10000'                                                     
         L     RF,FULL                                                          
         BAS   RE,DIV                                                           
         EDIT  (R1),(6,PL+31),2                                                 
*                                                                               
         MVI   SPRUMSGT,SPRUMDSQ    DPT/STYP TABLE                              
         MVC   SPRUMTKY,DSBDPT      'KEY'- DPT/STYP                             
         CLI   DSBDPT,0            TOTAL 0=>FF                                  
         BNE   *+8                                                              
         MVI   SPRUMTKY,X'FF'                                                   
         CLI   DSBSTYP,0           TOTAL 0=>FF                                  
         BNE   *+8                                                              
         MVI   SPRUMTKY+1,X'FF'                                                 
*                                                                               
         MVC   BYTE,DSBPRD         SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,DSBPRD                                                    
         MVC   SBBEST,DSBEST                                                    
*                                                                               
         BAS   RE,PLHOOK                                                        
*                                                                               
         CLC   SPRUMTKY(2),=X'FFFF' IF DPT AND STYP = ALL                       
         BNE   DSP08                                                            
*                                                                               
         MVI   SPRUMSGT,SPRUMSUQ   DO A 'MARKET SUMMARY' ENTRY                  
         MVI   PL,0                ONE DUMMY LINE                               
         BAS   RE,PLHOOK                                                        
*                                                                               
DSP08    DS    0H                                                               
         B     DSP04                                                            
*                                                                               
DSP10    DS    0H                                                               
*                                                                               
DSPX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        PGPRT - PRINT PG BUFFER                                                
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PGPRT    NTR1                                                                   
         TM    SPRUTRCE,X'10'      TEST TO TRACE TABLES                         
         BZ    PGP03                                                            
         MVC   PL(18),=C'<<PROGRAM BUFFER>>'                                    
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
*                                                                               
PGP03    DS    0H                                                               
         LA    R9,TSARBPG         PROGRAM TSAR                                  
         USING TSARD,R9                                                         
         LA    R5,PGBUFRC                                                       
         USING PGBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
         MVI   PGBKEY,0                                                         
         MVI   TSOFFACT,TSARDH                                                  
         B     PGP04B                                                           
*                                                                               
PGP04    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
PGP04B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
         TM    TSERRS,TSEEOF                                                    
         BO    PGP10                                                            
         CLI   PGBDPT,X'FF'        SKIP INDIVIDUAL DAYPART RECORDS              
         BNE   PGP04                                                            
*                                                                               
         LA    R2,PL+0             PRINT LINE                                   
         CLI   PGBPGM,0            TOTALS ONLY                                  
         BNE   PGP06                                                            
         CLI   PGBWEEK,0           AND ONLY FOR ALL WEEKS                       
         BNE   PGP06                                                            
         CLC   PGBSTA,FFS          AND ALL STATIONS                             
         BNE   PGP06                                                            
*                                                                               
         MVC   0(6,R2),=C'*TOTAL'                                               
         MVC   FULL,PGBDEMV        SAVE IT                                      
         B     PGP06B                                                           
*                                                                               
PGP06    DS    0H                                                               
         CLI   PGBPGM,X'FD'        PROGRAM TYPE                                 
         BNE   PGP06B                                                           
         MVC   0(5,R2),=C'TYPE='                                                
         MVC   5(1,R2),PGBPGM+1                                                 
         B     PGP07                                                            
*                                                                               
PGP06B   DS    0H                                                               
         CLI   PGBPGM,0                                                         
         BNE   PGP06D                                                           
         TM    SPRUTRCE,X'10'                                                   
         BZ    PGP07                                                            
         MVC   0(3,R2),=C'ALL'                                                  
         B     *+10                                                             
PGP06D   DS    0H                                                               
         MVC   0(20,R2),PGBPGM       PROGRAM                                    
*                                                                               
PGP07    DS    0H                                                               
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BNZ   PGP07B              YES - DISPLAY IN 2 DECIMAL                   
*                                                                               
PGP07A   EDIT  (B4,PGBDEMV),(7,22(R2)),1                                        
         B     PGP07C                                                           
*                                                                               
PGP07B   EDIT  (B4,PGBDEMV),(7,22(R2)),2                                        
*                                                                               
PGP07C   EDIT  (B4,PGBSPTS),(4,39(R2))                                          
*                                                                               
         ICM   R1,15,PGBDEMV       CALC % OF TOTAL                              
         M     R0,=F'10000'                                                     
         L     RF,FULL                                                          
         BAS   RE,DIV                                                           
         EDIT  (R1),(6,31(R2)),2                                                
         TM    SPRUTRCE,X'10'                                                   
         BZ    *+10                                                             
         MVC   44(3,R2),=C'ALL'                                                 
         CLI   PGBWEEK,0                                                        
         BE    PGP07D                                                           
         GOTO1 VDATCON,DMCB,(2,PGBWEEK),(5,44(R2))                              
*                                                                               
PGP07D   DS    0H                                                               
         MVI   SPRUMSGT,SPRUMPGQ    PROGRAM                                     
         TM    SPRUTRCE,X'10'                                                   
         BZ    PGP07E                                                           
         MVC   54(3,R2),=C'ALL'                                                 
         CLI   PGBSTA,X'FF'                                                     
         BE    PGP07E                                                           
*                                                                               
         XC    DUB(2),DUB                                                       
         MVC   DUB+2(3),PGBSTA                                                  
*                                                                               
         LA    R4,WORK                                                          
         USING STAPACKD,R4                                                      
         XC    0(32,R4),0(R4)                                                   
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SPRUAGY                                                  
         MVC   STAPCTRY,SPRUCTRY                                                
         MVC   STAPMED,SPRUMED                                                  
         MVC   STAPACOM,SPRUACOM                                                
         MVC   STAPMKST,DUB                                                     
*                                                                               
         GOTO1 VSTAPACK,(R4)                                                    
         CLI   STAPERR,0                                                        
         B     *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   54(8,R2),STAPQSTA                                                
         DROP  R4                                                               
*                                                                               
PGP07E   DS    0H                                                               
         MVC   SPRUMTKY(1),PGBPGM   'KEY' - 1ST BYTE PF PGM                     
         CLI   PGBPGM,0             TOTAL 00=>FF                                
         BNE   *+8                                                              
         MVI   SPRUMTKY,X'FF'                                                   
         CLI   PGBWEEK,0            SKIP WEEKLY RECORDS                         
         BNE   PGP08                                                            
         CLC   PGBSTA,FFS           AND STATION RECORDS                         
         BE    PGP08D                                                           
*                                                                               
PGP08    DS    0H                                                               
         MVI   SPRUMSGT,SPRUMTRQ    BUT TRACE                                   
*                                                                               
PGP08D   DS    0H                                                               
         MVC   BYTE,PGBPRD         SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,PGBPRD                                                    
         MVC   SBBEST,PGBEST                                                    
*                                                                               
         BAS   RE,PLHOOK                                                        
         B     PGP04                                                            
*                                                                               
PGP10    DS    0H                                                               
*                                                                               
PGPX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        COMMONS - COMMONS ROUTINES AND DATA                                    
*                                                                               
***********************************************************************         
         SPACE 2                                                                
COMMONS  DS    0D                                                               
*                                                                               
AWORKD   DS    F                   A(WORKD)                                     
DAYBITS  DC    X'0040201008040201'   DAY BITS                                   
SPACES   DC    CL132' '                                                         
FFS      DC    8X'FF'                                                           
PL       DC    CL132' '                                                         
PLWORK   DC    CL132' '                                                         
*                                                                               
STYPLST  DS    0X                                                               
         DC    C'AAFF '            AFFILIATE                                    
         DC    C'IIND '            INDEPENDENT                                  
         DC    C'FFOX '            FOX                                          
         DC    C'CCBL '            CABLE                                        
         DC    C'UUPN '            UPN                                          
         DC    C'WWB  '            WB                                           
         DC    C'PPAX '            PAX                                          
         DC    X'FF',C'ALL*'       ALL                                          
         DC    X'00',C'ALL*'       ALL                                          
STYPLSTN EQU   (*-STYPLST)/5                                                    
         DC    C'?????'            UNKNOWN                                      
*                                                                               
CHKPRDL  DC    XL256'00'           PRODUCT CHECKK OFF LIST                      
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        DATAMGR CALLS                                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
HIGH     DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'                                             
         B     DIR                                                              
         SPACE 2                                                                
SEQ      DS    0H                                                               
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIR                                                              
*                                                                               
DIR      DS    0H                                                               
         LR    R0,RE                                                            
         MVC   FILE,=CL8'SPTDIR'                                                
         GOTO1 VDMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY,KEY,(0,DMWORK)             
         B     DMCHK                                                            
         SPACE 2                                                                
GET      DS    0H                                                               
         LR    R0,RE                                                            
         MVC   COMMAND,=CL8'GETREC'                                             
         MVC   FILE,=CL8'SPTFIL'                                                
         GOTO1 VDMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY+14,IOA,(0,DMWORK)          
         B     DMCHK                                                            
*                                                                               
DMCHK    DS    0H                                                               
         LR    RE,R0                                                            
         MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BZR   RE                                                               
         DC    H'0'                                                             
         SPACE 2                                                                
***********************************************************************         
*        HOOK - HOOK BACK TO CALLER                                             
***********************************************************************         
         SPACE 2                                                                
PLHOOK   NTR1                      RETURNS PRINT LINE PL                        
*                                                                               
         CLI   SBPRD,C' '          TEMPORARY TRAP                               
         BH    PLH02                                                            
         CLI   SPRUMSGT,SPRUMRUQ                                                
         BNH   PLH02                                                            
         DC    H'0'                                                             
*                                                                               
PLH02    DS    0H                                                               
         MVC   PLWORK,PL                                                        
         MVC   PL,SPACES                                                        
         LA    RE,PLWORK                                                        
         ST    RE,SPRUMSGA                                                      
         SR    RE,RE               LENGTH                                       
         CLC   PLWORK,SPACES                                                    
         BE    PLH04                                                            
         LA    RE,PLWORK+L'PLWORK-1                                             
         CLI   0(RE),C' '                                                       
         BNE   *+8                                                              
         BCT   RE,*-8                                                           
         LA    R0,PLWORK-1                                                      
         SR    RE,R0                                                            
PLH04    DS    0H                                                               
         STH   RE,SPRUMSGL                                                      
         OC    SPRUMTKY,SPRUMTKY   IF 'TABLE KEY' NULLS                         
         BNZ   *+8                                                              
         MVI   SPRUMTKY,X'FF'      GIVE IT SOMETHING (FOR DRIVER )              
         L     RF,SPRUHOOK                                                      
         L     RE,CALLRD           CALLERS SAVE AREA                            
         LM    R0,RC,20(RE)        CALLERS REGS                                 
         BASR  RE,RF                                                            
         XIT1                                                                   
         SPACE 2                                                                
HOOK     NTR1                      GENERIC HOOK                                 
         L     RF,SPRUHOOK                                                      
         L     RE,CALLRD           CALLERS SAVE AREA                            
         LM    R0,RC,20(RE)        CALLERS REGS                                 
         BASR  RE,RF                                                            
         XIT1                                                                   
*                                                                               
DIV      DRNDR (R0),(RF)                                                        
         BR    RE                                                               
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        SETBUY - SET BUY RECORD DATA IN PL                                     
*                                                                               
***********************************************************************         
         SPACE 2                                                                
SETBUY   NTR1                                                                   
         LA    R2,PL                                                            
*                                                                               
         MVC   BYTE,BUYKPRD                                                     
         BAS   RE,GETPCOD                                                       
         MVC   PL+10(3),PCOD                                                    
         EDIT  (B1,BUYKEY+9),(3,PL+14)                                          
         SR    R1,R1                                                            
         ICM   R1,3,BUYMSTA                                                     
         EDIT  (R1),(4,PL+18)                                                   
*                                                                               
         MVC   PL+25(5),SBSTA                                                   
         L     RF,SPRUABUY                                                      
         ZIC   R0,BUYKEY+10-BUYKEY(RF)                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PL+31(3),DUB                                                     
         MVC   PL+35(1),BDDAYPT                                                 
         MVC   PL+37(1),SBSTYPE                                                 
*                                                                               
SETBX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GETRULE - GET RULE RECORD FOR RULE TYPE (GRTYPE),                      
*                  PRODUCT (GRPRD) AND EST (GREST)                              
*                  ON OUTPUT RECORD WILL BE IN RUBUFRC                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
GETRULE  NTR1                                                                   
         LA    R9,TSARBRU                                                       
         USING TSARD,R9                                                         
*                                                                               
         LA    R3,RUBUFRC                                                       
         USING RUBRCD,R3                                                        
         ST    R3,TSAREC                                                        
         XC    RUBRCD(RUBUFKYL+4),RUBRCD                                        
*                                                                               
         MVC   RUBRTYP,GRTYPE                                                   
         MVC   RUBPRD,GRPRD                                                     
         MVC   RUBEST+1(1),GREST                                                
         CLI   GREST,0             EST=0 MEANS ALL                              
         BNE   *+10                                                             
         MVC   RUBEST,=X'FFFF'                                                  
         MVC   SVREST,RUBEST       SAVE LOOKED FOR EST                          
*                                                                               
GR10     DS    0H                                                               
         MVC   SVRUBK,RUBKEY      SAVE LOOKED FOR KEY                           
         MVI   TSOFFACT,TSARDH                                                  
         GOTO1 ATSAR,TSARD                                                      
         TM    TSERRS,TSEEOF       HOW ABOUT TESTING EOF                        
         BO    GR60                                                             
*                                                                               
         CLC   RUBRTYP,SVRUBK     RIGHT RULE TYPE?                              
         BNE   GR60                NO, DONE                                     
*                                                                               
         CLC   RUBPRD,SVRUBK+1    PRODUCT                                       
         BE    GR20                                                             
*                                                                               
GR18     DS    0H                                                               
         CLI   SVRUBK+1,X'FF'     IF ALREADY LOOKING FOR 'ALL'                  
         BE    GR60                THEN DONE                                    
         MVI   RUBPRD,X'FF'        ELSE TRY 'ALL' PRODUCT NOW                   
         MVC   RUBEST,SVREST       SET ESTIMATE                                 
         B     GR10                                                             
*                                                                               
GR20     DS    0H                  PRODUCT OK, NOW TRY EST                      
         CLC   RUBEST,SVRUBK+2                                                  
         BE    GR50                YES, OK                                      
         CLC   SVRUBK+2(2),=X'FFFF'   NO, HAVE WE TRIED FOR 'ALL'               
         BE    GR18                                                             
         MVC   RUBEST,=X'FFFF'     DO IT NOW                                    
         B     GR10                                                             
*                                                                               
GR50     DS    0H                  HAVE GOOD RECORD                             
         B     GRX                 JUST RETURN                                  
*                                                                               
GR60     DS    0H                  RECORD NOT FOUND                             
         XC    RUBUFRC(RUBUFKYL+4),RUBUFRC    CLEAR RECORD                      
*                                                                               
GRX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        DSPDAYS - DISPLAY DAYS (IN HALF) AT R2                                 
*                  DAYS(1), CONTROL(1)                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
DSPDAYS  DS    0H                                                               
         LR    R0,RE                                                            
         TM    HALF+1,X'40'        CONTROL BYTE                                 
         BZ    *+12                                                             
         MVI   0(R2),C'='          EXACT MATCH                                  
         LA    R2,1(R2)                                                         
         GOTO1 VDAYUNPK,DMCB,HALF,(R2)                                          
         OC    0(13,R2),SPACES                                                  
         LA    R2,13(R2)           REPOSITION R2                                
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         LA    R2,1(R2)                                                         
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        DSPTIM - DISPLAY START-END TYPE (IN FULL) AT R2                        
*                                                                               
***********************************************************************         
         SPACE 2                                                                
DSPTIM   DS    0H                                                               
         LR    R0,RE                                                            
         GOTO1 SPRUUNTM,DMCB,(0,FULL),(R2),=C'N'   UNTIME                       
         LA    R2,12(R2)           REPOSITION R2                                
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         LA    R2,1(R2)                                                         
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        DSPDPT - DISPLAY DAYPART (IN BYTE) AT R2                               
*                                                                               
***********************************************************************         
         SPACE 2                                                                
DSPDPT   DS    0H                  ENTRY FOR LABEL                              
         MVC   0(7,R2),=C'DAYPART'                                              
         LA    R2,8(R2)                                                         
*                                                                               
DSPDPT2  DS    0H                  NO LABEL                                     
         LR    R0,RE                                                            
         LR    RF,R2                                                            
         MVC   0(1,RF),BYTE                                                     
         LA    R2,1(RF)                                                         
         CLI   BYTE,X'FF'                                                       
         BE    DSPDPT4                                                          
         CLI   BYTE,0                                                           
         BNE   DSPDPTX                                                          
DSPDPT4  DS    0H                                                               
         MVC   0(4,RF),=C'ALL*'                                                 
         LA    R2,4(RF)                                                         
*                                                                               
DSPDPTX  DS    0H                                                               
                                                                                
         LR    RE,R0                                                            
         BR    RE                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        DSPTZ - DISPLAY TIME ZONE (IN DUB) AT R2                               
*                                                                               
***********************************************************************         
         SPACE 2                                                                
DSPTZ    NTR1                                                                   
         MVC   0(9,R2),=C'TIME ZONE'                                            
         LA    R2,9(R2)                                                         
         CLI   DUB+1,C' '          MORE THAN ONE ZONE?                          
         BNH   *+12                                                             
         MVI   0(R2),C'S'                                                       
         LA    R2,1(R2)                                                         
         CLI   DUB,C' '                                                         
         BH    DSPTZ1                                                           
         MVC   2(3,R2),=C'ALL'                                                  
         LA    R2,4(R2)                                                         
         B     DSPTZX                                                           
*                                                                               
DSPTZ1   DS    0H                                                               
         LA    R0,8                                                             
         LA    R3,DUB                                                           
*                                                                               
DSPTZ2   DS    0H                                                               
         CLI   0(R3),C' '          EOL                                          
         BNH   DSPTZ4                                                           
*                                                                               
         MVC   1(1,R2),0(R3)                                                    
         MVI   2(R2),C'+'                                                       
         LA    R3,1(R3)                                                         
         LA    R2,2(R2)                                                         
         BCT   R0,DSPTZ2                                                        
*                                                                               
DSPTZ4   DS    0H                                                               
         MVI   0(R2),C' '          CLEAR EXTRA +                                
*                                                                               
DSPTZX   DS    0H                                                               
         XIT1  REGS=(R2)                                                        
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        DSPSTYP - DISPLAY STATION TYPE (IN BYTE) AT R2                         
*                                                                               
***********************************************************************         
         SPACE 2                                                                
DSPSTYP  DS    0H                  ENTRY FOR LABEL                              
         MVC   0(12,R2),=C'STATION TYPE'                                        
         LA    R2,13(R2)                                                        
*                                                                               
DSPSTYP2 NTR1                      NO LABLE                                     
         LA    RF,STYPLST          STATION TYPE LIST                            
         LA    R0,STYPLSTN         FOR BCT                                      
*                                                                               
DSPST2   DS    0H                                                               
         CLC   0(1,RF),BYTE                                                     
         BE    DSPST4                                                           
         LA    RF,5(RF)                                                         
         BCT   R0,DSPST2                                                        
         MVC   0(4,R2),=C'*** '                                                 
         B     *+10                                                             
*                                                                               
DSPST4   DS    0H                                                               
         MVC   0(4,R2),1(RF)                                                    
         LA    R2,6(R2)                                                         
*                                                                               
DSPSTX   DS    0H                                                               
         XIT1  REGS=(R2)                                                        
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        GETPCOD - BYTE(1) => PCOD(3)                                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
GETPCOD  DS    0H                                                               
         CLI   BYTE,X'FE'          UNALLOCATED                                  
         BNE   *+12                                                             
         MVC   PCOD,=C'UNA'                                                     
         BR    RE                                                               
*                                                                               
         ZIC   RF,BYTE                                                          
         BCTR  RF,0                                                             
         MHI   RF,PRDBUFFL                                                      
         A     RF,SBAPRDBF                                                      
         USING PRDBUFFD,RF                                                      
*                                                                               
         MVC   PCOD,PBALPH                                                      
         BR    RE                                                               
*                                                                               
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        CHKPGM - COMPARE PROGRAM NAMES                                         
*                 BUY/TABLE PROG IN P1, RULE PROG IN P2                         
*                                                                               
***********************************************************************         
         SPACE 2                                                                
CHKPGM   NTR1                                                                   
         L     R2,0(R1)            BUY/TABLE PROG                               
         L     R3,4(R1)            RULE PROG                                    
         MVI   CHKSTAT,0                                                        
*                                                                               
         LA    R4,L'RUPGPGM(R3)    FIND FINAL CHAR OF RULE PROG                 
         CLI   0(R4),C' '                                                       
         BH    *+8                                                              
         BCT   R4,*-8                                                           
         CLI   0(R4),C'*'          IS IT A *?                                   
         BNE   CHKP04                                                           
         OI    CHKSTAT,X'40'                                                    
         BCTR  R4,0                MOVE BACK TO LAST REAL CHAR                  
*                                                                               
CHKP04   DS    0H                                                               
         CLI   0(R3),C'*'          IF FIRST CHAR A *?                           
         BNE   CHKP06                                                           
         OI    CHKSTAT,X'80'                                                    
         LA    R3,1(R3)            BUMP PAST *                                  
*                                                                               
CHKP06   DS    0H                                                               
         LR    R5,R4                                                            
         SR    R5,R3               GET LENGTH OF RULE PROG WITHOUT *'S          
         BM    CHKPYES             R5 < R3 MEANS PROG IS * ALONE                
*                                                                               
         TM    CHKSTAT,X'C0'       * EITHER BEFORE OR AFTER?                    
         BNZ   CHKP08                                                           
         CLC   0(L'RUPGPGM,R3),0(R2)      NO, COMPARE WHOLE FIELDS              
         BE    CHKPYES                                                          
         B     CHKPNO                                                           
*                                                                               
CHKP08   DS    0H                                                               
         TM    CHKSTAT,X'80'       * BEFORE                                     
         BZ    CHKP12                                                           
         TM    CHKSTAT,X'40'       AND AFTER                                    
         BZ    CHKP10                                                           
*                                                                               
         LA    R0,L'RUPGPGM        SET BCT FOR COMPARE LOOP                     
         SR    R0,R5                                                            
*                                                                               
CHKP09   DS    0H                                                               
         EX    R5,CHKCLC                                                        
         BE    CHKPYES                                                          
         LA    R2,1(R2)                                                         
         BCT   R0,CHKP09                                                        
         B     CHKPNO                                                           
*                                                                               
CHKP10   DS    0H                  * BEFORE BUT NOT AFTER                       
         LA    R2,L'RUPGPGM(R2)    COMAPRE LAST N CHARACTERS                    
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         SR    R2,R5                                                            
         EX    R5,CHKCLC                                                        
         BE    CHKPYES                                                          
         B     CHKPNO                                                           
*                                                                               
CHKP12   DS    0H                  * AFTER BUT NOT BEFORE                       
         EX    R5,CHKCLC           COMPARE FIRST N CHARACTERS                   
         BNE   CHKPNO                                                           
*                                                                               
CHKPYES  CR    RE,RE               CC = ON MATCH                                
         B     CHKPX                                                            
CHKPNO   LTR   RB,RB               NOT = ON MISMATCH                            
CHKPX    XIT1                                                                   
*                                                                               
CHKCLC   CLC   0(0,R3),0(R2)                                                    
CHKSTAT  DS    X                                                                
         SPACE 2                                                                
*                                                                               
***********************************************************************         
*        POST TO DS BUFFER                                                      
***********************************************************************         
*                                                                               
PRBDSPUT NTR1                                                                   
         LA    R5,DSBUFRC          POINT BACK TO RECORD AREA                    
         USING DSBUFRCD,R5                                                      
         MVC   DSBKEY(DSBUFKYL),SVDSBK       SET KEY                            
         MVI   TSOFFACT,TSARDH                                                  
         GOTO1 ATSAR,TSARD                                                      
         TM    TSERRS,TSERNF       IF REC NOT THERE, ADD IT                     
         BNZ   PRBDS04                                                          
         CLC   SVDSBK,DSBKEY                                                    
         BE    PRBDS06                                                          
*                                                                               
PRBDS04  DS    0H                                                               
         MVC   DSBKEY(DSBUFKYL),SVDSBK       SET KEY                            
         MVC   DSBSPTS,WORK        SPOT COUNT                                   
         MVC   DSBDEMV,WORK+4      DEMO VALUE                                   
         MVI   TSOFFACT,TSAADD                                                  
         GOTO1 ATSAR,TSARD                                                      
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     PRBDSPX                                                          
*                                                                               
PRBDS06  DS    0H                  IF REC ALREADY THERE                         
         ICM   RF,15,DSBSPTS       ADD THESE VALUES                             
         A     RF,WORK                                                          
         STCM  RF,15,DSBSPTS                                                    
         ICM   RF,15,DSBDEMV                                                    
         A     RF,WORK+4                                                        
         STCM  RF,15,DSBDEMV                                                    
         MVI   TSOFFACT,TSAPUT                                                  
         GOTO1 ATSAR,TSARD                                                      
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PRBDSPX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
***********************************************************************         
*        POST TO PG BUFFER                                                      
***********************************************************************         
*                                                                               
PRBPGPUT NTR1                                                                   
         LA    R5,PGBUFRC          POINT BACK TO RECORD AREA                    
         USING PGBUFRCD,R5                                                      
         MVC   PGBKEY(PGBUFKYL),SVPGBK       SET KEY                            
         MVI   TSOFFACT,TSARDH                                                  
         GOTO1 ATSAR,TSARD                                                      
         TM    TSERRS,TSERNF       IF REC NOT THERE, ADD IT                     
         BNZ   PRBPG04                                                          
         CLC   SVPGBK,PGBKEY                                                    
         BE    PRBPG06                                                          
*                                                                               
PRBPG04  DS    0H                                                               
         MVC   PGBKEY(PGBUFKYL),SVPGBK       SET KEY                            
         MVC   PGBSPTS,WORK        SPOT COUNT                                   
         MVC   PGBDEMV,WORK+4      DEMO VALUE                                   
         MVI   TSOFFACT,TSAADD                                                  
         GOTO1 ATSAR,TSARD                                                      
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     PRBPGPX                                                          
*                                                                               
PRBPG06  DS    0H                  IF REC ALREADY THERE                         
         ICM   RF,15,PGBSPTS       ADD THESE VALUES                             
         A     RF,WORK                                                          
         STCM  RF,15,PGBSPTS                                                    
         ICM   RF,15,PGBDEMV                                                    
         A     RF,WORK+4                                                        
         STCM  RF,15,PGBDEMV                                                    
         MVI   TSOFFACT,TSAPUT                                                  
         GOTO1 ATSAR,TSARD                                                      
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
PRBPGPX  DS    0H                                                               
         XIT1                                                                   
         DROP  R3,R5                                                            
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        PGSPRT - PRINT PGM/STA BUFFER                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PGSPRT   NTR1                                                                   
         TM    SPRUTRCE,X'10'      TEST TO TRACE TABLES                         
         BZ    PGS03                                                            
         MVC   PL(18),=C'<<PGM/STA BUFFER>>'                                    
         MVI   SPRUMSGT,SPRUMTRQ    TRACER                                      
         BAS   RE,PLHOOK                                                        
*                                                                               
PGS03    DS    0H                                                               
         LA    R9,TSARBPG         PROGRAM TSAR                                  
         USING TSARD,R9                                                         
         LA    R5,PGBUFRC                                                       
         USING PGBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
         MVI   PGBKEY,0                                                         
         MVI   TSOFFACT,TSARDH                                                  
         B     PGS04B                                                           
*                                                                               
PGS04    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
PGS04B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
         TM    TSERRS,TSEEOF                                                    
         BO    PGS10                                                            
*                                                                               
         CLI   PGBPGM,0            TOTAL                                        
         BE    PGS09               SKIP                                         
         CLI   PGBWEEK,0           ONLY FOR ALL WEEKS                           
         BNE   PGS09                                                            
         CLC   PGBSTA,FFS          AND NOT ALL STATIONS                         
         BE    PGS09                                                            
         CLI   PGBDPT,X'FF'        AND ONLY ALL DPTS                            
         BNE   PGS09                                                            
*                                                                               
         CLI   PGBPGM,X'FD'        PROGRAM TYPE                                 
         BNE   PGS06B                                                           
         MVC   PL+0(05),=C'TYPE='                                               
         MVC   PL+5(1),PGBPGM+1                                                 
         B     PGS07                                                            
*                                                                               
PGS06B   DS    0H                                                               
         MVC   PL+0(20),PGBPGM       PROGRAM                                    
*                                                                               
PGS07    DS    0H                                                               
         XC    DUB(2),DUB                                                       
         MVC   DUB+2(3),PGBSTA                                                  
*                                                                               
         LA    R4,WORK                                                          
         USING STAPACKD,R4                                                      
         XC    0(32,R4),0(R4)                                                   
         MVI   STAPACT,C'U'                                                     
         MVC   STAPAGY,SPRUAGY                                                  
         MVC   STAPCTRY,SPRUCTRY                                                
         MVC   STAPMED,SPRUMED                                                  
         MVC   STAPACOM,SPRUACOM                                                
         MVC   STAPMKST,DUB                                                     
*                                                                               
         GOTO1 VSTAPACK,(R4)                                                    
         CLI   STAPERR,0                                                        
         B     *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PL+22(8),STAPQSTA                                                
         DROP  R4                                                               
*                                                                               
         MVI   SPRUMSGT,SPRUMPSQ    PROGRAM/STATION                             
*                                                                               
         MVC   BYTE,PGBPRD         SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,PGBPRD                                                    
         MVC   SBBEST,PGBEST                                                    
*                                                                               
         BAS   RE,PLHOOK                                                        
*                                                                               
PGS09    DS    0H                                                               
         B     PGS04                                                            
*                                                                               
PGS10    DS    0H                                                               
*                                                                               
PGSX     DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
**********************************************************************          
*        GET MINUTES LENGTH                                                     
**********************************************************************          
         SPACE 2                                                                
BTSDGLN  NTR1                                                                   
         MVC   DUB(4),BDTIMST                                                   
         OC    DUB+2(2),DUB+2      IF END TIME ZERO                             
         BNZ   *+10                                                             
         MVC   DUB+2(2),DUB        SET END = START                              
         LA    R2,DUB                                                           
         LA    R3,FULL                                                          
         LA    R0,2                                                             
*                                                                               
BTSDGL4  DS    0H                                                               
         LH    R5,0(R2)                                                         
         SR    R4,R4                                                            
         D     R4,=F'100'                                                       
         MHI   R5,60                                                            
         AR    R4,R5               R4 = MINUTES                                 
         STH   R4,0(R3)                                                         
         LA    R2,2(R2)                                                         
         LA    R3,2(R3)                                                         
         BCT   R0,BTSDGL4                                                       
*                                                                               
         LH    RE,FULL             START                                        
         LH    RF,FULL+2           END                                          
         CR    RE,RF               IF END BEFORE START                          
         BNH   *+8                                                              
         LA    RF,1440(RF)         ADD 24 HOURS                                 
         SR    RF,RE                                                            
         STH   RF,NMINS                                                         
*                                                                               
         XIT1                                                                   
*                                                                               
         SPACE 2                                                                
**********************************************************************          
*        GET DAY COUNT                                                          
**********************************************************************          
         SPACE 2                                                                
BTSDGDC  NTR1                                                                   
         ZIC   R2,BDDAY                                                         
         SR    R1,R1                                                            
         LA    R3,X'80'                                                         
*                                                                               
BTSDGD4  DS    0H                                                               
         LR    R4,R3                                                            
         NR    R4,R2                                                            
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
         SRA   R3,1                                                             
         BNZ   BTSDGD4                                                          
*                                                                               
         STC   R1,NDAYS                                                         
         XIT1                                                                   
*                                                                               
         SPACE 2                                                                
**********************************************************************          
*        SDDUPE - CHECK FOR DUPES IN SPOTS/DAY ERRORS                           
**********************************************************************          
         SPACE 2                                                                
SDDUPE   NTR1                                                                   
         LA    RF,SDDLST                                                        
         LR    R1,RF                                                            
         LA    R1,L'SDDLST(R1)    SET END OF LIST                               
*                                                                               
SDD4     DS    0H                                                               
         CR    RF,R1               EOL?                                         
         BNL   SDDNO               YES, NOT A DUPE                              
         CLI   0(RF),X'FF'         EMPTY SLOT                                   
         BE    SDD6                                                             
         CLC   PL(SDDLSTL),0(RF)                                                
         BE    SDDYES              IS A DUPE, NOT OK TO PRINT                   
         LA    RF,SDDLSTL(RF)      NEXT ENTRY                                   
         B     SDD4                                                             
*                                                                               
SDD6     DS    0H                                                               
         MVC   0(SDDLSTL,RF),PL    SAVE CURRENT LINE                            
         MVI   SDDLSTL(RF),X'FF'                                                
*                                                                               
SDDNO    LTR   RD,RD                                                            
         B     *+6                                                              
SDDYES   CR    RD,RD                                                            
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
BSPARS   DS    6F                                                               
         DC    C'*DTLIST*'                                                      
DTLMAX   EQU   75                  MAX DATES IN LIST (FOR 1 BUYREC)             
DTLIST   DS    XL(DTLISTL*DTLMAX)  SPOT DATE LIST                               
         DC    C'*SDDLST*'                                                      
SDDMAX   EQU   15        MAX ENTRIES (NOT CRITICAL - THE WORST THAT)            
*                        CAN HAPPEN IS THAT SOME DUPES PRINT)                   
SDDLSTL  EQU   28        CHECK FIRST 35 CHARACTERS OF LINE                      
SDDLST   DS    XL(SDDLSTL*SDDMAX)  SPOT DATE LIST                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BTPGM - BUY TEST - PROGRAM                                             
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BTPGM    NMOD1 0,**BTPG**                                                       
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRPGQ     PROGRAM RULE                                 
         BAS   RE,GETRULE                                                       
*                                                                               
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                                                               
BTPG4    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BTPG60                                                           
         CLC   =C'TYPE=',RUPGPGM   TYPE TEST?                                   
         BNE   BTPG5                                                            
         CLC   BDPROGT,RUPGPGM+5                                                
         BNE   BTPG40                                                           
         B     BTPG6                                                            
*                                                                               
BTPG5    DS    0H                                                               
         MVC   WORK(20),SPACES                                                  
         MVC   WORK(L'BDPROGRM),BDPROGRM                                        
         GOTO1 CHKPGM,DMCB,WORK,RUPGPGM    DO NAMES AGREE?                      
         BNE   BTPG40              NO, NEXT ELEM                                
*                                                                               
BTPG6    DS    0H                                                               
         OC    RUPGMAX,RUPGMAX     ERROR ONLY IF MAX IS 0                       
         BNZ   BTPG40                                                           
*                                                                               
         MVI   SPRUMTKY,SPRUBPGM   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         MVC   0(7,R2),=C'PROGRAM'                                              
         MVC   8(5,R2),RUPGPGM                                                  
         CLC   RUPGPGM(5),=C'TYPE='                                             
         BE    BTPG7                                                            
         MVC   8(L'BDPROGRM,R2),BDPROGRM                                        
*                                                                               
BTPG7    DS    0H                                                               
         LA    R2,L'BDPROGRM+09-1(R2)                                           
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         MVC   2(11,R2),=C'NOT ALLOWED'                                         
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
*                                                                               
         BAS   RE,PLHOOK                                                        
         B     BTPGX               ONLY ONE RULE PER BUY                        
*                                                                               
BTPG40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BTPG4                                                            
*                                                                               
BTPG60   DS    0H                                                               
BTPGX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BTDST - BUY TEST - DAYPART/STATION TYPE                                
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BTDST    NMOD1 0,**BTDS**                                                       
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRDSQ     PROGRAM RULE                                 
         BAS   RE,GETRULE                                                       
*                                                                               
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                                                               
BTDS4    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BTDS60                                                           
*                                                                               
         CLC   BDDAYPT,RUDSDPT     TEST DAYPART =                               
         BE    BTDS4B                                                           
         CLI   RUDSDPT,X'FF'       OR ALL                                       
         BNE   BTDS40                                                           
*                                                                               
BTDS4B   DS    0H                                                               
         CLC   SBSTYPE,RUDSSTP     TEST STATION TYPE =                          
         BE    BTDS6                                                            
         CLI   RUDSSTP,X'FF'       OR ALL                                       
         BNE   BTDS40                                                           
*                                                                               
BTDS6    DS    0H                                                               
         OC    RUDSMAX,RUDSMAX     ERROR ONLY IF MAX IS 0                       
         BNZ   BTDS40                                                           
*                                                                               
         MVI   SPRUMTKY,SPRUBDST   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         LR    R3,R2                                                            
         MVC   BYTE,RUDSDPT                                                     
         BAS   RE,DSPDPT           DISPLAY DAYPART (IN BYTE) AT R2              
*                                                                               
         MVI   0(R2),C','                                                       
         LA    R2,13(R3)                                                        
         LR    R3,R2                                                            
         MVC   BYTE,RUDSSTP        STATION TYPE                                 
         BAS   RE,DSPSTYP                                                       
*                                                                               
         MVC   1(11,R2),=C'NOT ALLOWED'                                         
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
*                                                                               
         BAS   RE,PLHOOK                                                        
         B     BTDSX                                                            
*                                                                               
BTDS40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BTDS4                                                            
*                                                                               
BTDS60   DS    0H                                                               
BTDSX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BTHOL - SPOT TEST - HOLIDAYS                                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BTHOL    NMOD1 0,**BTHO**                                                       
         L     RC,AWORKD                                                        
         MVI   GRPRD,0                                                          
         XC    CURDATE,CURDATE                                                  
*                                                                               
         ZIC   R0,BDSEDAY          GET START DAY OF ROTATION                    
         SRL   R0,4                                                             
         STC   R0,STRTDAY                                                       
*                                                                               
         ICM   R0,15,BSPARS+8      COUNT OF DATES                               
         BZ    BTHLX                                                            
         ST    R0,DTCOUNT                                                       
         L     R3,BSPARS+4         A(SPOT DATE TABLE)                           
         ST    R3,DTPOINT                                                       
         USING DTLISTD,R3                                                       
*                                                                               
BTHL3    DS    0H                                                               
         CLC   DTLDAT,CURDATE      DATE CHANGE?                                 
         BE    BTHL3B                                                           
         GOTO1 VDATCON,DMCB,(2,DTLDAT),RDATEBC  NEED 6 BYTE DATE                
         GOTO1 VGETDAY,(R1),RDATEBC,FULL       AND DAY OF WEEK                  
         MVC   WKDAY,DMCB                                                       
         ZIC   RF,WKDAY                                                         
         LA    RF,DAYBITS(RF)                                                   
         MVC   WKDAYB,0(RF)        AND DAY BIT                                  
         MVC   CURDATE,DTLDAT                                                   
*                                                                               
BTHL3B   DS    0H                                                               
         CLC   GRPRD,DTLPRD        NEW PRODUCT?                                 
         BE    BTHL3D              NO, SKIP GETRULE                             
*                                                                               
         MVI   GRTYPE,RULKRHLQ     HOLIDAY RULE                                 
         MVC   GRPRD,DTLPRD                                                     
         BAS   RE,GETRULE                                                       
*                                                                               
BTHL3D   DS    0H                                                               
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                                                               
BTHL4    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BTHL60                                                           
         CLI   0(R4),X'FF'         SKIP IF ALREADY USED                         
         BE    BTHL40                                                           
*                                                                               
         TM    BDSTAT2,X'80'       IF DAILY SCHED                               
         BNZ   BTHL5               JUST CHECK SINGLE DATE                       
         CLC   WKDAY,STRTDAY       OR IF ELEM DATE                              
         BNE   BTHL5               NOT = ROTATION START                         
*                                  ELSE LOOK AT ROTATION                        
         GOTO1 VDATCON,DMCB,(2,RUHLSDT),RULSDT  WILL NEED 6-BYTE DATES          
         GOTO1 (RF),(R1),(2,RUHLEDT),RULEDT                                     
         LA    R2,1                START WITH MONDAY                            
         LA    R0,X'40'                                                         
         LA    R3,7                                                             
*                                                                               
BTHL4D   DS    0H                                                               
         ZIC   RE,BDDAY                                                         
         NR    RE,R0                                                            
         BZ    BTHL4M                                                           
*                                                                               
         ZIC   RF,STRTDAY                                                       
         LR    R5,R2                                                            
         SR    R5,RF                                                            
         BNM   *+8                                                              
         A     R5,=F'7'                                                         
*                                                                               
         GOTO1 VADDAY,DMCB,RDATEBC,WORK,(R5)                                    
         BAS   RE,BTHLCKD                                                       
         BNE   BTHL6                                                            
*                                                                               
BTHL4M   DS    0H                                                               
         LA    R2,1(R2)                                                         
         SRL   R0,1                                                             
         BCT   R3,BTHL4D                                                        
         B     BTHL40              SPOT PASSES RULE                             
*                                                                               
BTHL5    DS    0H                  SINGLE DAY                                   
         CLC   DTLDAT,RUHLSDT      IS SPOT WITHIN DATE RANGE?                   
         BL    BTHL40                                                           
         BE    BTHL6                                                            
         OC    RUHLEDT,RUHLEDT     ANY END DATE                                 
         BZ    BTHL40                                                           
         CLC   DTLDAT,RUHLEDT                                                   
         BH    BTHL40                                                           
*                                                                               
BTHL6    DS    0H                  SPOT VIOLATES RULE                           
         MVI   SPRUMTKY,SPRUBHOL   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         GOTO1 VDATCON,DMCB,(2,RUHLSDT),(5,0(R2))                               
         LA    R2,8(R2)                                                         
         OC    RUHLEDT,RUHLEDT     ANY END DATE                                 
         BZ    BTHL7                                                            
         MVI   0(R2),C'-'                                                       
         GOTO1 VDATCON,DMCB,(2,RUHLEDT),(5,1(R2))                               
         LA    R2,9(R2)                                                         
*                                                                               
BTHL7    DS    0H                                                               
         MVC   01(14,R2),=C'DATE EXCLUSION'                                     
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
*                                                                               
         MVC   BYTE,GRPRD          SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,GRPRD                                                     
         MVC   SBBEST,GREST                                                     
*                                                                               
         BAS   RE,PLHOOK                                                        
         MVI   RULDCD,X'FF'        PREVENT USING RULE OVER AGAIN                
*                                                                               
BTHL40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BTHL4                                                            
*                                                                               
BTHL60   DS    0H                                                               
         L     R3,DTPOINT                                                       
         LA    R3,DTLISTL(R3)      NEXT DATE TABLE ENTRY                        
         ST    R3,DTPOINT                                                       
         L     R0,DTCOUNT                                                       
         SHI   R0,1                                                             
         ST    R0,DTCOUNT                                                       
         BP    BTHL3                                                            
         DROP  R3                                                               
*                                                                               
BTHLX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
BTHLCKD  DS    0H          CHECK DATE IN WORK VS RULE START/END                 
         CLC   WORK(6),RULSDT                                                   
         BL    BTHLCOK                                                          
         BE    BTHLCNOK                                                         
         CLC   WORK(6),RULEDT                                                   
         BH    BTHLCOK                                                          
*                                                                               
BTHLCNOK LTR   RE,RE               CC NOT = MEANS DATE DOES NOT PASS            
         BR    RE                                                               
BTHLCOK  CR    RE,RE               CC = MEANS DATE PASSES                       
         BR    RE                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BTDAYTM - BUY TEST - DAY/TIME                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BTDAYTM  NMOD1 0,**BTDT**                                                       
         L     RC,AWORKD                                                        
         MVI   GRPRD,0                                                          
         XC    CURDATE,CURDATE                                                  
*                                                                               
         ZIC   R0,BDSEDAY          GET START DAY OF ROTATION                    
         SRL   R0,4                                                             
         STC   R0,STRTDAY                                                       
*                                                                               
         ICM   R0,15,BSPARS+8      COUNT OF DATES                               
         BZ    BTDTX                                                            
         ST    R0,DTCOUNT                                                       
         L     R3,BSPARS+4         A(SPOT DATE TABLE)                           
         ST    R3,DTPOINT                                                       
         USING DTLISTD,R3                                                       
*                                                                               
BTDT3    DS    0H                                                               
         CLC   DTLDAT,CURDATE      DATE CHANGE?                                 
         BE    BTDT3B                                                           
         GOTO1 VDATCON,DMCB,(2,DTLDAT),RDATEBC  NEED 6 BYTE DATE                
         GOTO1 VGETDAY,(R1),RDATEBC,FULL       AND DAY OF WEEK                  
         MVC   WKDAY,DMCB                                                       
         ZIC   RF,WKDAY                                                         
         LA    RF,DAYBITS(RF)                                                   
         MVC   WKDAYB,0(RF)        AND DAY BIT                                  
         MVC   CURDATE,DTLDAT                                                   
*                                                                               
         MVI   BSMODE,C'S'         INDIVIDUAL SPOT DATE MODE                    
         TM    BDSTAT2,X'80'       IF DAILY SCHED                               
         BNZ   BTDT3B                                                           
         CLC   WKDAY,STRTDAY       OR IF ELEM DATE                              
         BNE   BTDT3B              NOT = ROTATION START                         
         MVI   BSMODE,C'B'         ELSE 'BUY' MODE                              
*                                                                               
BTDT3B   DS    0H                                                               
         CLC   GRPRD,DTLPRD        NEW PRODUCT?                                 
         BE    BTDT3D              NO, SKIP GETRULE                             
*                                                                               
         MVI   GRTYPE,RULKRDTQ     DAY/TIME RULE                                
         MVC   GRPRD,DTLPRD                                                     
         BAS   RE,GETRULE                                                       
*                                                                               
BTDT3D   DS    0H                                                               
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                                                               
*                                  TIME ZONE                                    
         MVC   TZONE,MKTZONE-MKTREC+SBMKTREC                                    
*                                                                               
BTDT14   DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BTDT60                                                           
         CLI   0(R4),X'FF'         SKIP IF ALREADY USED                         
         BE    BTDT40                                                           
         CLI   RUDTTZ,C' '         NO TIME ZONE                                 
         BNH   BTDT15D             RULE IS OK                                   
*                                                                               
         LA    RF,RUDTTZ           ELSE TZ MUST BE IN RULE                      
         LA    R0,L'RUDTTZ                                                      
*                                                                               
BTDT15   DS    0H                                                               
         CLC   TZONE,0(RF)                                                      
         BE    BTDT15D                                                          
         LA    RF,1(RF)                                                         
         BCT   R0,BTDT15                                                        
         B     BTDT40              REJECT                                       
*                                                                               
BTDT15D  DS    0H                  STATION TYPE                                 
         CLI   RUDTSTYP,X'FF'      ALL TYPES (IF GET TO THIS ONE WILL           
         BE    BTDT16              NOT HAVE HAD SPECIFIC MATCH)                 
         CLC   SBSTYPE,RUDTSTYP                                                 
         BNE   BTDT40                                                           
*                                                                               
BTDT16   DS    0H                                                               
         LA    RF,WKDAYB           USE SPOT ELEM DAY                            
         CLI   BSMODE,C'S'         IF IN SPOT MODE                              
         BE    *+8                                                              
         LA    RF,BDDAY            ELSE USE BUY DAYS                            
         MVC   BYTE,RUDTDAYS                                                    
         NC    BYTE,0(RF)          DOES ANY DAY MATCH RULE?                     
         BZ    BTDT40              NO, SKIP RULE                                
*                                                                               
         OC    BDTIMEND,BDTIMEND   IF NO END TIME                               
         BNZ   *+10                                                             
         MVC   BDTIMEND,BDTIMST    SET END=START                                
*                                                                               
         CLC   RUDTSTIM,RUDTETIM   DOES RULE SPAN MIDNIGHT?                     
         BH    BTDT22              YES                                          
         CLC   BDTIMST,BDTIMEND    NO, DOES BUY SPAN MIDNIGHT?                  
         BH    BTDT20              YES                                          
*                                                                               
*              NEITHER RULE NOR BUY SPAN MIDNIGHT                               
*                                                                               
         CLC   BDTIMST,RUDTETIM    IF BUY START AFTER RULE END                  
         BNL   BTDT40              OK                                           
         CLC   BDTIMEND,RUDTSTIM   IF BUY END BEFORE RULE START                 
         BNH   BTDT40              OK                                           
         B     BTDT26                                                           
*                                                                               
*              RULE DOESN'T SPAN, BUY DOES                                      
*                                                                               
BTDT20   DS    0H                                                               
*                                                                               
         CLC   RUDTSTIM,BDTIMST    IF RULE START AFTER BUY START                
         BNL   BTDT26              REJECT BUY                                   
         CLC   RUDTSTIM,BDTIMEND   IF RULE START BEFORE BUY END                 
         BL    BTDT26              REJECT BUY                                   
         CLC   RUDTETIM,BDTIMST    IF RULE END AFTER BUY START                  
         BH    BTDT26              REJECT BUY                                   
         CLC   RUDTETIM,BDTIMEND   IF RULE END BEFORE BUY END                   
         BL    BTDT26              REJECT BUY                                   
         B     BTDT40                                                           
*                                                                               
*              RULE SPANS MIDNIGHT                                              
*                                                                               
BTDT22   DS    0H                                                               
         CLC   BDTIMST,BDTIMEND    DOES BUY SPAN MIDNIGHT?                      
         BH    BTDT24              YES                                          
*                                                                               
*              RULE SPANS MIDNIGHT, BUY DOESN'T                                 
*                                                                               
         CLC   BDTIMST,RUDTSTIM    IF BUY START AFTER RULE START                
         BNL   BTDT26              REJECT BUY                                   
         CLC   BDTIMST,RUDTETIM    IF BUY START BEFORE RULE END                 
         BL    BTDT26              REJECT BUY                                   
         CLC   BDTIMEND,RUDTSTIM   IF BUY END AFTER RULE START                  
         BH    BTDT26              REJECT BUY                                   
         CLC   BDTIMEND,RUDTETIM   IF BUY END BEFORE RULE END                   
         BL    BTDT26              REJECT BUY                                   
         B     BTDT40                                                           
*                                                                               
*              RULE SPANS MIDNIGHT, SO DOES BUY                                 
*                                                                               
BTDT24   DS    0H                  BUY CAN'T PASS RULE BECAUSE MIDNIGHT         
         B     BTDT26              IS IN BOTH BUY AND RULE                      
*                                                                               
*                                                                               
BTDT26   DS    0H                  BUY DOES NOT PASS RULE                       
         MVI   SPRUMTKY,SPRUBDTM   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         MVC   0(08,R2),=C'DAY/TIME'                                            
         LA    R2,09(R2)                                                        
         MVC   HALF(1),RUDTDAYS                                                 
         MVI   HALF+1,0            CONTROL BYTE                                 
         BAS   RE,DSPDAYS          DISPLAY DAYS (IN BYTE) AT R2                 
*                                  (R2 REPOSITIONED BY DSPDAYS)                 
         MVC   FULL,RUDTSTIM       START AND END TIMES                          
         LA    R2,1(R2)                                                         
         BAS   RE,DSPTIM           DISPLAY TIMES (IN FULL) AT R2                
         LA    R2,1(R2)            (R2 REPOSITIONED BY DSPTIM)                  
         MVC   0(11,R2),=C'NOT ALLOWED'                                         
         CLI   RUDTTZ,C' '         ANY TIME ZONE?                               
         BH    BTDT27                                                           
         CLI   RUDTSTYP,C' '       OR STATION TYPE?                             
         BNH   BTDT30                                                           
BTDT27   DS    0H                                                               
         LA    R2,12(R2)                                                        
         MVC   0(3,R2),=C'FOR'                                                  
         LA    R2,4(R2)                                                         
         CLI   RUDTTZ,C' '         ANY TIME ZONE?                               
         BNH   BTDT28                                                           
         MVC   DUB,RUDTTZ                                                       
         BAS   RE,DSPTZ            DISPLAY TIME ZONE (IN BYTE) AT R2            
*                                                                               
BTDT28   DS    0H                                                               
         CLI   RUDTSTYP,0          ANY STATION TYPE                             
         BE    BTDT30                                                           
         CLI   RUDTTZ,C' '         IF HAD TIME ZONE                             
         BNH   *+12                                                             
         MVI   0(R2),C','          NEED COMMA                                   
         LA    R2,1(R2)                                                         
         LA    R2,1(R2)                                                         
         MVC   BYTE,RUDTSTYP                                                    
         BAS   RE,DSPSTYP          DISPLAY STN TYPE (IN BYTE) AT R2             
*                                                                               
BTDT30   DS    0H                                                               
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
*                                                                               
         MVC   BYTE,GRPRD          SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,GRPRD                                                     
         MVC   SBBEST,GREST                                                     
*                                                                               
         BAS   RE,PLHOOK                                                        
         MVI   RULDCD,X'FF'        PREVENT USING RULE OVER AGAIN                
*                                                                               
BTDT40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BTDT14                                                           
*                                                                               
BTDT60   DS    0H                                                               
         L     R3,DTPOINT                                                       
         LA    R3,DTLISTL(R3)      NEXT DATE TABLE ENTRY                        
         ST    R3,DTPOINT                                                       
         L     R0,DTCOUNT                                                       
         SHI   R0,1                                                             
         ST    R0,DTCOUNT                                                       
         BP    BTDT3                                                            
         DROP  R3                                                               
*                                                                               
BTDTX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BTSD    - BUY TEST - SPOTS/DAY                                         
*                                                                               
*                FIRST WAY, BASED ON DATE TABLE                                 
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BTSD     NMOD1 0,**BTSD**                                                       
         L     RC,AWORKD                                                        
*                                                                               
         MVI   GRPRD,0                                                          
         XC    CURDATE,CURDATE                                                  
*                                                                               
         ZIC   R0,BDSEDAY          GET START DAY OF ROTATION                    
         SRL   R0,4                                                             
         STC   R0,STRTDAY                                                       
*                                                                               
         BAS   RE,BTSDGLN          GET PROGRAM MINUTES IN NMINS                 
         BAS   RE,BTSDGDC          GET DAY COUNT IN NDAYS                       
*                                                                               
         ICM   R0,15,BSPARS+8      COUNT OF DATES                               
         BZ    BTSDX                                                            
         ST    R0,DTCOUNT                                                       
         L     R3,BSPARS+4         A(SPOT DATE TABLE)                           
         ST    R3,DTPOINT                                                       
         USING DTLISTD,R3                                                       
*                                                                               
BTSD3    DS    0H                                                               
         CLC   DTLDAT,CURDATE      DATE CHANGE?                                 
         BE    BTSD3B                                                           
         GOTO1 VDATCON,DMCB,(2,DTLDAT),RDATEBC  NEED 6 BYTE DATE                
         GOTO1 VGETDAY,(R1),RDATEBC,FULL       AND DAY OF WEEK                  
         MVC   WKDAY,DMCB                                                       
         ZIC   RF,WKDAY                                                         
         LA    RF,DAYBITS(RF)                                                   
         MVC   WKDAYB,0(RF)        AND DAY BIT                                  
         MVC   CURDATE,DTLDAT                                                   
*                                                                               
         MVI   BSMODE,C'S'         INDIVIDUAL SPOT DATE MODE                    
         TM    BDSTAT2,X'80'       IF DAILY SCHED                               
         BNZ   BTSD3B                                                           
         CLC   WKDAY,STRTDAY       OR IF ELEM DATE                              
         BNE   BTSD3B              NOT = ROTATION START                         
         MVI   BSMODE,C'B'         ELSE 'BUY' MODE                              
*                                                                               
BTSD3B   DS    0H                                                               
         CLC   GRPRD,DTLPRD        NEW PRODUCT?                                 
         BE    BTSD3D              NO, SKIP GETRULE                             
*                                                                               
         MVI   GRTYPE,RULKRSDQ     SPOTS/DAY RULE                               
         MVC   GRPRD,DTLPRD                                                     
         BAS   RE,GETRULE                                                       
*                                                                               
BTSD3D   DS    0H                                                               
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                                                               
BTSD14   DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BTSD60                                                           
*                                                                               
         CLC   NMINS,RUSDLEN       IS RULE FOR RIGHT LENGTH?                    
         BNE   BTSD40                                                           
*                                                                               
BTSD22   DS    0H                                                               
         TM    RUSDCTL,X'80'       N-DAY RULE?                                  
         BZ    BTSD23                                                           
         LA    RF,=X'01'           ONE DAY                                      
         CLI   BSMODE,C'S'         IF 'SPOT' MODE                               
         BE    *+8                                                              
         LA    RF,NDAYS            ELSE ROTATION DAY COUND                      
         CLC   0(1,RF),RUSDDAYS    RIGHT NUMBER OF DAYS?                        
         BNE   BTSD40              NO, NEXT RULE                                
         B     BTSD24                                                           
*                                                                               
BTSD23   DS    0H                                                               
         LA    RF,WKDAYB           SINGLE DAY IF 'SPOT' MODE                    
         CLI   BSMODE,C'S'                                                      
         BE    *+8                                                              
         LA    RF,BDDAY            ELSE USE ROTATOR                             
         CLC   0(1,RF),RUSDDAYS      RIGHT ROTATION?                            
         BE    BTSD24                                                           
         TM    RUSDCTL,X'40'       EXACT MATCH REQUIRED?                        
         BNZ   BTSD40              YES, REJECT IF NOT =                         
         MVC   FULL(1),RUSDDAYS    ELSE TEST THAT ALL BUY DAYS                  
         NC    FULL(1),0(RF)       ARE IN RULE MASK                             
         CLC   FULL(1),0(RF)                                                    
         BNE   BTSD40                                                           
*                                                                               
BTSD24   DS    0H                  CHECK SPOT COUNT                             
         SR    R1,R1                                                            
         ICM   R1,3,RUSDSPTS       RULES SPOT LIMIT                             
         ZIC   R0,NDAYS            X NDAYS                                      
         CLI   BSMODE,C'B'         (IF 'BUY' MODE)                              
         BE    *+8                                                              
         LA    R0,1                ELSE 1 DAY                                   
         MR    R0,R0                                                            
         STC   R1,NDAYSW                                                        
         CLM   R1,3,DTLCOUNT       COMPARE TO SPOT COUNT                        
         BNL   BTSD40                                                           
*                                                                               
BTSD26   DS    0H                  DATE DOES NOT PASS RULE                      
         MVI   SPRUMTKY,SPRUBSPD   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         TM    RUSDCTL,X'80'       DAY COUNT INSTEAD OF ROTATION?               
         BZ    BTSD27                                                           
         MVC   BYTE,RUSDDAYS       SHOW AS NDAY                                 
         NI    BYTE,X'7F'                                                       
         EDIT  (B1,BYTE),(1,0(R2))                                              
         MVC   1(3,R2),=C'DAY'                                                  
         LA    R2,4(R2)                                                         
         B     BTSD28                                                           
*                                                                               
BTSD27   DS    0H                                                               
         MVC   HALF(1),RUSDDAYS                                                 
         MVC   HALF+1(1),RUSDCTL   CONTROL BYTE                                 
         BAS   RE,DSPDAYS          DISPLAY DAYS (IN BYTE) AT R2                 
*                                  (R2 REPOSITIONED BY DSPDAYS)                 
BTSD28   DS    0H                                                               
         MVI   0(R2),C','                                                       
         EDIT  (B2,RUSDLEN),(3,2(R2))                                           
         MVC   6(5,R2),=C'MINS.'                                                
         LA    R2,12(R2)                                                        
*                                                                               
         MVC   0(34,R2),=C'    MMMDD HAS NN SPOTS, MAXIMUM IS'                  
         GOTO1 VDATCON,DMCB,(2,DTLDAT),(4,4(R2))                                
         EDIT  (B2,DTLCOUNT),(2,14(R2))                                         
         EDIT  (B2,RUSDSPTS),(2,35(R2))                                         
         CLI   BSMODE,C'S'         FOR SINGLE DATE MODE                         
         BE    BTSD30              SKIP NUMBER PER DAY                          
         MVC   0(3,R2),=C'W/O'                                                  
*                                  BACK UP TO PREV MONDAY(?) FOR W/O            
         ZIC   RF,WKDAY                                                         
         SHI   RF,1                                                             
         BNP   BTSD29                                                           
         LCR   RF,RF                                                            
         ST    RF,DMCB+8                                                        
         GOTO1 VADDAY,DMCB,RDATEBC,DUB                                          
         GOTO1 VDATCON,DMCB,(0,DUB),(4,4(R2))                                   
*                                                                               
BTSD29   DS    0H                                                               
         MVC   37(7,R2),=C'/DAY (='                                             
         LA    R2,44(R2)                                                        
         EDIT  (B1,NDAYSW),(3,0(R2)),ALIGN=LEFT                                 
         AR    R2,R0                                                            
         MVI   0(R2),C')'                                                       
*                                                                               
BTSD30   DS    0H                                                               
         BAS   RE,SDDUPE          IF SPOTS/DAY ERROR LINE IS DUPLICATED         
         BNE   *+14                                                             
         MVC   PL,SPACES                                                        
         B     BTSD40             SKIP IT (MAY RESULT FROM 2 S/D MODES)         
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
*                                                                               
         MVC   BYTE,GRPRD          SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,GRPRD                                                     
         MVC   SBBEST,GREST                                                     
*                                                                               
         BAS   RE,PLHOOK                                                        
*                                                                               
BTSD40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BTSD14                                                           
*                                                                               
BTSD60   DS    0H                                                               
         L     R3,DTPOINT                                                       
         LA    R3,DTLISTL(R3)      NEXT DATE TABLE ENTRY                        
         ST    R3,DTPOINT                                                       
         L     R0,DTCOUNT                                                       
         SHI   R0,1                                                             
         ST    R0,DTCOUNT                                                       
         BP    BTSD3                                                            
*                                                                               
BTSDX    DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BT2SD   - BUY TEST - SPOTS/DAY                                         
*                                                                               
*                  SECOND WAY, BASED ON WEEKLY CHUNKS                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BT2SD    NMOD1 0,**BT2SD**                                                      
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRSDQ     SPOTS/DAY RULE                               
         BAS   RE,GETRULE                                                       
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
         CLI   0(R4),0             EOR                                          
         BE    BT2SD60                                                          
*                                                                               
         BAS   RE,BTSDGLN          GET PROGRAM MINUTES IN NMINS                 
         BAS   RE,BTSDGDC          GET DAY COUNT IN NDAYS                       
*                                                                               
BT2SD14  DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BT2SD60                                                          
*                                                                               
         CLC   NMINS,RUSDLEN       IS RULE FOR RIGHT LENGTH?                    
         BNE   BT2SD40                                                          
*                                                                               
         L     R3,SBACHUNK         GO THROUGH CHUNKS                            
         USING SCHUNKD,R3                                                       
*                                                                               
BT2SD18  DS    0H                                                               
         OC    SCNEXT,SCNEXT       END OF CHUNKS                                
         BZ    BT2SD40             NEXT RULE                                    
*                                                                               
         CLC   SCPRD1,GRPRD        RIGHT PRODUCT?                               
         BNE   BT2SD25                                                          
*                                                                               
         MVC   WKDAY,BDDAY         BUY ROTATION DAYS                            
         MVC   WKNDAYS,NDAYS       AND NUMBER OF DAYS                           
*                                                                               
BT2SD22  DS    0H                                                               
         TM    RUSDCTL,X'80'       N-DAY RULE?                                  
         BZ    BT2SD23                                                          
         CLC   WKNDAYS,RUSDDAYS    RIGHT NUMBER OF DAYS?                        
         BNE   BT2SD25             NO, NEXT CHUNCK                              
         B     BT2SD24                                                          
*                                                                               
BT2SD23  DS    0H                                                               
         CLC   WKDAY,RUSDDAYS      RIGHT ROTATION?                              
         BE    BT2SD24                                                          
         TM    RUSDCTL,X'40'       EXACT MATCH REQUIRED?                        
         BNZ   BT2SD25             YES, REJECT IF NOT =                         
         MVC   FULL(1),RUSDDAYS    ELSE TEST THAT ALL BUY DAYS                  
         NC    FULL(1),WKDAY       ARE IN RULE MASK                             
         CLC   FULL(1),WKDAY                                                    
         BNE   BT2SD25                                                          
*                                                                               
BT2SD24  DS    0H                  CHECK SPOT COUNT                             
         SR    R1,R1                                                            
         ICM   R1,3,RUSDSPTS       RULES SPOT LIMIT                             
         ZIC   R0,WKNDAYS          X NDAYS                                      
         MR    R0,R0                                                            
         STC   R1,NDAYSW                                                        
         C     R1,SCSPOTS          COMPARE TO SPOT COUNT                        
         BL    BT2SD26                                                          
*                                                                               
BT2SD25  DS    0H                  NEXT CHUNK                                   
         L     R3,SCNEXT                                                        
         B     BT2SD18                                                          
*                                                                               
BT2SD26  DS    0H                  CHUNK DOES NOT PASS RULE                     
         MVI   SPRUMTKY,SPRUBSPD   RULE TYPE                                    
         LA    R2,PL+2                                                          
         TM    RUSDCTL,X'80'       DAY COUNT INSTEAD OF ROTATION?               
         BZ    BT2SD27                                                          
         MVC   BYTE,RUSDDAYS       SHOW AS NDAY                                 
         NI    BYTE,X'7F'                                                       
         EDIT  (B1,BYTE),(1,0(R2))                                              
         MVC   1(3,R2),=C'DAY'                                                  
         LA    R2,4(R2)                                                         
         B     BT2SD28                                                          
*                                                                               
BT2SD27  DS    0H                                                               
         MVC   HALF(1),RUSDDAYS                                                 
         MVC   HALF+1(1),RUSDCTL   CONTROL BYTE                                 
         BAS   RE,DSPDAYS          DISPLAY DAYS (IN BYTE) AT R2                 
*                                  (R2 REPOSITIONED BY DSPDAYS)                 
BT2SD28  DS    0H                                                               
         MVI   0(R2),C','                                                       
         EDIT  (B2,RUSDLEN),(3,2(R2))                                           
         MVC   6(5,R2),=C'MINS.'                                                
         LA    R2,12(R2)                                                        
*                                                                               
         MVC   0(34,R2),=C'W/O MMMDD HAS NN SPOTS, MAXIMUM IS'                  
         GOTO1 VDATCON,DMCB,(2,SCDATE),(4,4(R2))                                
         EDIT  (B4,SCSPOTS),(2,14(R2))                                          
         EDIT  (B2,RUSDSPTS),(2,35(R2))                                         
         MVC   37(7,R2),=C'/DAY (='                                             
         LA    R2,44(R2)                                                        
         EDIT  (B1,NDAYSW),(3,0(R2)),ALIGN=LEFT                                 
         AR    R2,R0                                                            
         MVI   0(R2),C')'                                                       
*                                                                               
BT2SD30  DS    0H                                                               
         BAS   RE,SDDUPE          IF SPOTS/DAY ERROR LINE IS DUPLICATED         
         BNE   *+14                                                             
         MVC   PL,SPACES                                                        
         B     BT2SD40            SKIP IT (MAY RESULT FROM 2 S/D MODES)         
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
         BAS   RE,PLHOOK                                                        
         B     BT2SD25             NEXT CHUNK                                   
*                                                                               
BT2SD40  DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BT2SD14                                                          
*                                                                               
BT2SD60  DS    0H                                                               
BT2SDX   DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        BTRTG   - BUY TEST - RATINGS                                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
BTRTG    NMOD1 0,**BTRTG*                                                       
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRRTQ     SPOTS/DAY RULE                               
         BAS   RE,GETRULE                                                       
*                                                                               
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
         CLI   0(R4),0             EOR                                          
         BE    BTRT60                                                           
*                                                                               
BTRT14   DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    BTRT60                                                           
*                                                                               
         CLC   BDDAYPT,RURTDPT     TEST DAYPART =                               
         BE    BTRT4B                                                           
         CLI   RURTDPT,X'FF'       OR ALL                                       
         BNE   BTRT40                                                           
*                                                                               
BTRT4B   DS    0H                                                               
         CLC   SBSTYPE,RURTSTP     TEST STATION TYPE =                          
         BE    BTRT6                                                            
         CLI   RURTSTP,X'FF'       OR ALL                                       
         BNE   BTRT40                                                           
*                                                                               
BTRT6    DS    0H                                                               
****                                                                            
* BECAUSE OF 2 DEC IMPS, WE NEED TO ADJUST RURTMIN/RURTMAX NOW SINCE            
* WE ONLY KNOW IF THE BUY'S PRIMARY DEMO IS A RTG/IMP AT THIS POINT             
****                                                                            
         TM    RURTMIN,X'40'       DEMO HAVE 2 DEC                              
         BO    BTRT6A              YES                                          
*                                                                               
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BZ    BTRT16              NO - LEAVE IT ALONE                          
*                                                                               
         L     R0,RURTMIN          MINIMUM RATING                               
         MHI   R0,10               MULTIPLY BY 10                               
         ST    R0,RURTMIN          ADJUST 1 DECIMAL PRECISION TO 2              
         L     R0,RURTMAX          MAXIMUM RATING                               
         MHI   R0,10               MULTIPLY BY 10                               
         ST    R0,RURTMAX          ADJUST 1 DECIMAL PRECISION TO 2              
         B     BTRT16              DONE                                         
*                                                                               
BTRT6A   NI    RURTMIN,X'FF'-X'40' TURN OFF 2 DEC FLAG                          
         NI    RURTMAX,X'FF'-X'40' TURN OFF 2 DEC FLAG                          
*                                                                               
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BNZ   BTRT16              YES - LEAVE IT ALONE                         
*                                                                               
         L     R0,RURTMIN          MINIMUM RATING                               
         SRDA  R0,31               MULTIPLY BY 2                                
         D     R0,=F'10'           DIVIDE BY 10                                 
         AHI   R1,1                ADD 1                                        
         SRA   R1,1                DIVIDE BY 2                                  
         ST    R1,RURTMIN          ADJUST 2 DECIMAL PRECISION TO 1              
*                                                                               
         L     R0,RURTMAX          MAXIMUM RATING                               
         SRDA  R0,31               MULTIPLY BY 2                                
         D     R0,=F'10'           DIVIDE BY 10                                 
         AHI   R1,1                ADD 1                                        
         SRA   R1,1                DIVIDE BY 2                                  
         ST    R1,RURTMAX          ADJUST 2 DECIMAL PRECISION TO 1              
*                                                                               
BTRT16   DS    0H                                                               
         L     R3,SBACHUNK         GO THROUGH CHUNKS                            
         USING SCHUNKD,R3                                                       
         XC    SPTS,SPTS                                                        
         XC    DEMV,DEMV                                                        
*                                                                               
BTRT18   DS    0H                                                               
         OC    SCNEXT,SCNEXT       END OF CHUNKS                                
         BZ    BTRT26              APPLY RULE TO TOTAL                          
*                                                                               
         CLC   SCPRD1,GRPRD        RIGHT PRODUCT?                               
         BNE   BTRT25                                                           
*                                                                               
BTRT24   DS    0H                  TOTAL SPTOS AND DEMO                         
         L     RF,SPTS                                                          
         A     RF,SCSPOTS                                                       
         ST    RF,SPTS                                                          
         L     RF,DEMV                                                          
         A     RF,SCDEMOS          UNEQUIVALENCED FIRST DEMO??                  
         ST    RF,DEMV                                                          
*                                                                               
BTRT25   DS    0H                  NEXT CHUNK                                   
         L     R3,SCNEXT                                                        
         B     BTRT18                                                           
*                                                                               
BTRT26   DS    0H                                                               
         MVC   DEMVAL,DEMV         DEMO VALUE                                   
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BNZ   BTRT26A             YES - VALUE IS ALREADY IN 2 DECIMAL          
*                                                                               
         L     R0,DEMVAL           DEMO VALUE IN 1 DECIMAL PRECISION            
         MHI   R0,10               MULTIPLY BY 10                               
         ST    R0,DEMVAL           DEMO VALUE IN 2 DECIMAL PRECISION            
*                                                                               
BTRT26A  L     R1,DEMVAL           GET RTG/SPOT                                 
         M     R0,=F'1'                                                         
         L     RF,SPTS                                                          
         CLI   SPRUBGPR+2,C'Y'     NO RTG ERROR FOR 0 SPOTS?                    
         BNE   *+10                                                             
         LTR   RF,RF                                                            
         BZ    BTRT40                                                           
         BAS   RE,DIV                                                           
*                                                                               
         MVI   MINMAX,0                                                         
         ICM   RE,15,RURTMIN       COMPARE TO MINIMUM                           
         BZ    BTRT27                                                           
         CR    R1,RE                                                            
         BNL   BTRT27                                                           
         OI    MINMAX,X'80'        MIN ERROR                                    
*                                                                               
BTRT27   DS    0H                                                               
         ICM   RE,15,RURTMAX        COMPARE TO MAXIMUM                          
         BZ    BTRT27D                                                          
         CR    R1,RE                                                            
         BNH   BTRT27D                                                          
         OI    MINMAX,X'40'                                                     
*                                  PRINT RULE                                   
BTRT27D  DS    0H                                                               
         CLI   MINMAX,0            ANY ERROR?                                   
         BE    BTRT40              NO, NEXT RULE                                
*                                                                               
         L     R1,DEMV             RESET R1 TO UNADJUSTED RATING                
         M     R0,=F'1'            GET POINTS PER SPOT                          
         BAS   RE,DIV              POINTS PER SPOT                              
*                                                                               
         MVI   SPRUMTKY,SPRUBRTG   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         MVC   PL,SPACES                                                        
         MVC   0(11,R2),=C'RTG/SPOT IS'                                         
         LA    R2,12(R2)                                                        
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BNZ   BTRT28              YES - DISPLAY IN 2 DECIMAL                   
*                                                                               
BTRT27E  EDIT  (R1),(7,0(R2)),1                                                 
         B     BTRT29                                                           
*                                                                               
BTRT28   EDIT  (R1),(7,0(R2)),2                                                 
*                                                                               
BTRT29   MVC   7(13,R2),=C', MINIMUM FOR'                                       
         TM    MINMAX,X'80'                                                     
         BNZ   *+10                                                             
         MVC   09(7,R2),=C'MAXIMUM'                                             
         LA    R2,22(R2)                                                        
         LR    R3,R2                                                            
         MVC   BYTE,RURTDPT                                                     
         BAS   RE,DSPDPT           DISPLAY DAYPART (IN BYTE) AT R2              
*                                                                               
         MVI   1(R2),C','                                                       
         LA    R2,14(R3)                                                        
         LR    R3,R2                                                            
         MVC   BYTE,RURTSTP        STATION TYPE                                 
         BAS   RE,DSPSTYP                                                       
*                                                                               
         MVC   0(2,R2),=C'IS'                                                   
         LA    R3,RURTMIN                                                       
         TM    MINMAX,X'80'        MIN ERROR?                                   
         BNZ   *+8                                                              
         LA    R3,RURTMAX          OR MAX                                       
         LA    R2,3(R2)                                                         
         GOTO1 =A(CKRTG),RR=RELO   IS PRIMARY DEMO A RATING?                    
         BE    *+12                YES                                          
         TM    SBEFLAG9,SBE92DEC   REPORT SUPPORTS 2 DEC IMPRESSIONS?           
         B     *+8                 GO TEST CONDITION CODE                       
         TM    SBEFLAG4,SBE42DEC   REPORT SUPPORT 2 DEC                         
         BNZ   BTRT29A             YES - DISPLAY IN 2 DECIMAL                   
         EDIT  (B4,(R3)),(6,0(R2)),1                                            
         B     BTRT30                                                           
*                                                                               
BTRT29A  EDIT  (B4,(R3)),(6,0(R2)),2                                            
*                                                                               
BTRT30   DS    0H                                                               
         MVI   SPRUMSGT,SPRUMBSQ    BUY/SPOT                                    
         BAS   RE,PLHOOK                                                        
*                                                                               
BTRT40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     BTRT14                                                           
*                                                                               
BTRT60   DS    0H                                                               
BTRTX    DS    0H                                                               
         XIT1                                                                   
         DROP  R3                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GTDST - GLOBAL TEST - DPT/STYPE                                        
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
GTDST    NMOD1 0,**GTDS**                                                       
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRDSQ     DSP/STYPE RULE                               
         LA    R9,TSARBDS         DPT/STATION TYPE TSAR                         
         USING TSARD,R9                                                         
         LA    R5,DSBUFRC                                                       
         USING DSBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
*                                                                               
         XC    SVDSBK,SVDSBK                                                    
         XC    DSBKEY(DSBUFKYL),DSBKEY                                          
         MVI   TSOFFACT,TSARDH                                                  
         B     GTDS2B                                                           
*                                                                               
GTDS2    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
*                                                                               
GTDS2B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
         TM    TSERRS,TSEEOF       END OF TABLE                                 
         BO    GTDS60                                                           
*                                                                               
         CLC   DSBPRD(2),SVDSBK    SAME PRD/EST?                                
         BE    GTDS3                                                            
         OC    DSBDPT(2),DSBDPT    NO, SHOULD BE A TOTALS RECORD                
         BZ    *+6                                                              
         DC    H'0'                                                             
         MVC   TOTSPTS,DSBSPTS     SAVE TOTALS                                  
         MVC   TOTDEMV,DSBDEMV                                                  
         MVC   SVDSBK,DSBKEY                                                    
*                                  GET RULES FOR THIS PRD/EST                   
         MVC   GRPRD,DSBPRD                                                     
         MVC   GREST,DSBEST                                                     
         BAS   RE,GETRULE                                                       
         B     GTDS2               NEXT RECORD                                  
*                                                                               
GTDS3    DS    0H                  SAME PRD/EST                                 
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
         MVC   HDSST,DSBDPT        HOLD DPT/STYPE                               
         CLI   HDSST,0             DPT NULL = ALL = FF IN RULE                  
         BNE   *+8                                                              
         MVI   HDSST,X'FF'                                                      
         CLI   HDSST+1,0           STYPE NULL = ALL = FF IN RULE                
         BNE   *+8                                                              
         MVI   HDSST+1,X'FF'                                                    
*                                  GO THRU RULE ELEMS                           
GTDS4    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    GTDS2               YES, NEXT TABLE ENTRY                        
*                                                                               
         CLC   HDSST,RUDSDPT       IS RULE FOR THIS DPT/STYPE?                  
         BNE   GTDS40              NO, SKIP IT                                  
*                                                                               
         MVC   SPTS,DSBSPTS        SET VALUES                                   
         MVC   DEMV,DSBDEMV                                                     
*                                                                               
GTDS8    DS    0H                  CALC % OF TOTAL DEMV                         
         L     R1,DEMV                                                          
         M     R0,=F'10000'        % TO 2 DEC                                   
         L     RF,TOTDEMV                                                       
         BAS   RE,DIV                                                           
         ST    R1,DEMPCT           SAVE %                                       
*                                                                               
         MVI   MINMAX,0                                                         
         SR    RE,RE               COMPARE TO MINIMUM                           
         ICM   RE,3,RUDSMIN                                                     
         BZ    GTDS9                                                            
         CR    R1,RE                                                            
         BNL   GTDS9                                                            
         OI    MINMAX,X'80'        MIN ERROR                                    
*                                                                               
GTDS9    DS    0H                                                               
         ICM   RE,3,RUDSMAX        COMPARE TO MAXIMUM                           
         BZ    GTDS9D                                                           
         CR    R1,RE                                                            
         BNH   GTDS9D                                                           
         OI    MINMAX,X'40'                                                     
*                                  PRINT RULE                                   
GTDS9D   DS    0H                                                               
         CLI   MINMAX,0            ANY ERROR?                                   
         BE    GTDS2               NO, NEXT TABLE ENTRY                         
*                                                                               
*                                  RULE IS VIOLATED                             
         MVI   SPRUMTKY,SPRUGDST   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         MVC   PL,SPACES                                                        
*                                                                               
         LR    R3,R2                                                            
         MVC   BYTE,RUDSDPT                                                     
         BAS   RE,DSPDPT           DISPLAY DAYPART (IN BYTE) AT R2              
*                                                                               
         MVI   0(R2),C','                                                       
         LA    R2,13(R3)                                                        
         LR    R3,R2                                                            
         MVC   BYTE,RUDSSTP        STATION TYPE                                 
         BAS   RE,DSPSTYP                                                       
*                                                                               
         MVC   0(2,R2),=C'IS'                                                   
         LA    R2,3(R2)                                                         
         EDIT  (B4,DEMPCT),(6,0(R2)),2                                          
         MVC   6(2,R2),=C'%,'                                                   
         LA    R2,9(R2)                                                         
*                                                                               
         MVC   0(10,R2),=C'MINIMUM IS'                                          
         LA    R3,RUDSMIN                                                       
         TM    MINMAX,X'80'        MIN ERROR?                                   
         BNZ   *+14                                                             
         MVC   0(07,R2),=C'MAXIMUM'    OR MAX                                   
         LA    R3,RUDSMAX                                                       
         LA    R2,11(R2)                                                        
         EDIT  (B2,(R3)),(6,0(R2)),2                                            
         MVI   6(R2),C'%'                                                       
*                                                                               
GTDS26   DS    0H                                                               
         MVI   SPRUMSGT,SPRUMGLQ    GLOBAL                                      
*                                                                               
         MVC   BYTE,GRPRD          SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,GRPRD                                                     
         MVC   SBBEST,GREST                                                     
*                                                                               
         BAS   RE,PLHOOK                                                        
         B     GTDS2                                                            
*                                                                               
GTDS40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     GTDS4                                                            
*                                                                               
GTDS60   DS    0H                                                               
         MVI   TSOFFACT,TSAINI     CLEAR DS BUFFER                              
         MVC   TSAREC,=A(DSBUFL)   RESET BUFFER LENGTH                          
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
GTDSX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GTPGM - GLOBAL TEST - PROGRAM                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
GTPGM    NMOD1 0,**GTPG**                                                       
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRPGQ    PROGRAM RULE                                  
         LA    R9,TSARBPG         PROGRAM TABLE TSAR                            
         USING TSARD,R9                                                         
         LA    R5,PGBUFRC                                                       
         USING PGBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
*                                                                               
         XC    SVPGBK,SVPGBK                                                    
         XC    PGBKEY(PGBUFKYL),PGBKEY                                          
         MVI   TSOFFACT,TSARDH                                                  
         B     GTPG2B                                                           
*                                                                               
GTPG2    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
*                                                                               
GTPG2B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
         TM    TSERRS,TSEEOF       END OF TABLE                                 
         BO    GTPG60                                                           
*                                                                               
         CLI   PGBPGM,0            TOTALS ONLY                                  
         BNE   GTPG2F                                                           
         CLI   PGBWEEK,0           AND ONLY FOR ALL WEEKS                       
         BNE   GTPG2F                                                           
         CLC   PGBSTA,FFS          AND ALL STATIONS                             
         BNE   GTPG2F                                                           
         CLI   PGBDPT,X'FF'        AND ALL DAYPARTS                             
         BNE   GTPG2F                                                           
*                                                                               
         MVC   TOTSPTS,PGBSPTS     SAVE TOTALS                                  
         MVC   TOTDEMV,PGBDEMV                                                  
         B     GTPG2                                                            
*                                  GET RULES FOR THIS PRD/EST                   
GTPG2F   DS    0H                                                               
         CLC   PGBPRD(2),SVPGBK    SAME PRD/EST?                                
         BE    GTPG3                                                            
         MVC   GRPRD,PGBPRD        NO, GET RULES                                
         MVC   GREST,PGBEST                                                     
         BAS   RE,GETRULE                                                       
         MVC   SVPGBK,PGBKEY                                                    
*                                                                               
GTPG3    DS    0H                  SAME PRD/EST                                 
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                  GO THRU RULE ELEMS                           
GTPG4    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    GTPG2               YES, NEXT TABLE ENTRY                        
*                                                                               
         OC    PGBWEEK,PGBWEEK     SKIP WEEKLY ENTRIES                          
         BNZ   GTPG40                                                           
         CLC   PGBSTA,FFS          SKIP STATION ENTRIES                         
         BNE   GTPG40                                                           
         CLI   PGBPGM,0            SKIP PROGRAM TOTALS                          
         BE    GTPG40                                                           
*                                                                               
         CLI   PGBPGM,X'FD'        IS TABLE ENTRY FOR TYPE= ?                   
         BNE   GTPG4H                                                           
         CLC   =C'TYPE=',RUPGPGM   AND RULE?                                    
         BNE   GTPG40                                                           
*                                                                               
         CLC   PGBPGM+1(1),RUPGPGM+5    DOES TYPE MATCH ?                       
         BNE   GTPG40                  NO, SKIP IT                              
         B     GTPG5                                                            
*                                                                               
GTPG4H   DS    0H                                                               
         GOTO1 CHKPGM,DMCB,PGBPGM,RUPGPGM    DO NAMES AGREE?                    
         BNE   GTPG40              NO, SKIP IT                                  
*                                                                               
GTPG5    DS    0H                                                               
         MVC   SPTS,PGBSPTS        SET VALUES                                   
         MVC   DEMV,PGBDEMV                                                     
*                                                                               
GTPG8    DS    0H                  CALC % OF TOTAL DEMV                         
         L     R1,DEMV                                                          
         M     R0,=F'10000'        % TO 2 DEC                                   
         L     RF,TOTDEMV                                                       
         BAS   RE,DIV                                                           
         ST    R1,DEMPCT           SAVE %                                       
*                                                                               
         MVI   MINMAX,0                                                         
         SR    RE,RE               COMPARE TO MINIMUM                           
         ICM   RE,3,RUPGMIN                                                     
         BZ    GTPG9                                                            
         CR    R1,RE                                                            
         BNL   GTPG9                                                            
         OI    MINMAX,X'80'        MIN ERROR                                    
*                                                                               
GTPG9    DS    0H                                                               
         ICM   RE,3,RUPGMAX        COMPARE TO MAXIMUM                           
         BZ    GTPG9D                                                           
         CR    R1,RE                                                            
         BNH   GTPG9D                                                           
         OI    MINMAX,X'40'                                                     
*                                  PRINT RULE                                   
GTPG9D   DS    0H                                                               
         CLI   MINMAX,0            ANY ERROR?                                   
         BE    GTPG40              NO, NEXT RULE                                
*                                                                               
*                                  RULE IS VIOLATED                             
         MVI   SPRUMTKY,SPRUGPGM   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         MVC   PL,SPACES                                                        
*                                                                               
         MVC   0(7,R2),=C'PROGRAM'                                              
         CLI   PGBPGM,X'FD'       PROGRAM TYPE?                                 
         BNE   GTPG10                                                           
         MVC   08(5,R2),=C'TYPE='                                               
         MVC   13(1,R2),PGBPGM+1                                                
         B     *+10                                                             
*                                                                               
GTPG10   DS    0H                                                               
         MVC   08(L'PGBPGM,R2),PGBPGM                                           
         LA    R2,L'PGBPGM+09(R2)                                               
         LR    R3,R2                                                            
*                                                                               
         MVC   0(2,R2),=C'IS'                                                   
         LA    R2,3(R2)                                                         
         EDIT  (B4,DEMPCT),(6,0(R2)),2                                          
         MVC   6(2,R2),=C'%,'                                                   
         LA    R2,9(R2)                                                         
*                                                                               
         MVC   0(10,R2),=C'MINIMUM IS'                                          
         LA    R3,RUPGMIN                                                       
         TM    MINMAX,X'80'        MIN ERROR?                                   
         BNZ   *+14                                                             
         MVC   0(07,R2),=C'MAXIMUM'    OR MAX                                   
         LA    R3,RUPGMAX                                                       
         LA    R2,11(R2)                                                        
         EDIT  (B2,(R3)),(6,0(R2)),2                                            
         MVI   6(R2),C'%'                                                       
*                                                                               
GTPG26   DS    0H                                                               
         MVI   SPRUMSGT,SPRUMGLQ    GLOBAL                                      
*                                                                               
         MVC   BYTE,GRPRD          SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,GRPRD                                                     
         MVC   SBBEST,GREST                                                     
*                                                                               
         BAS   RE,PLHOOK                                                        
         B     GTPG2               ONLY ONE RULE FOR TABLE ENTRY                
*                                                                               
GTPG40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     GTPG4                                                            
*                                                                               
GTPG60   DS    0H                                                               
*              NOTE- DON'T CLEAR PGM BUFFER HERE BECAUSE                        
*                    IT'S NEEDED FOR SPW RULE                                   
GTPGX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GTSPW - GLOBAL TEST - SPOTS/WEEK                                       
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
GTSPW    NMOD1 0,**GTSPW*                                                       
         L     RC,AWORKD                                                        
         MVI   GRTYPE,RULKRSWQ    SPOTS/WEEK RULE                               
         LA    R9,TSARBPG         PROGRAM TABLE TSAR                            
         USING TSARD,R9                                                         
         LA    R5,PGBUFRC                                                       
         USING PGBUFRCD,R5                                                      
         ST    R5,TSAREC                                                        
*                                                                               
         XC    SVPGBK,SVPGBK                                                    
         XC    PGBKEY(PGBUFKYL),PGBKEY                                          
         MVI   TSOFFACT,TSARDH                                                  
         B     GTSW2B                                                           
*                                                                               
GTSW2    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
*                                                                               
GTSW2B   DS    0H                                                               
         GOTO1 ATSAR,TSARD                                                      
         TM    TSERRS,TSEEOF       END OF TABLE                                 
         BO    GTSW60                                                           
*                                                                               
         OC    PGBWEEK,PGBWEEK     SKIP TOTAL ENTRIES                           
         BZ    GTSW2                                                            
         OC    PGBPGM,PGBPGM                                                    
         BZ    GTSW2                                                            
         CLC   PGBSTA,FFS           AND USE ONLY IF STATION=ALL                 
         BNE   GTSW2                                                            
*                                                                               
         CLI   SPRUBGPR+1,C'Y'      IF CHECKING BY PGM/DPT                      
         BNE   GTSW2F                                                           
         CLI   PGBDPT,X'FF'         THEN NOT FOR 'ALL DPTS'                     
         BE    GTSW2                                                            
*                                                                               
GTSW2F   DS    0H                                                               
         CLC   PGBPRD(2),SVPGBK    SAME PRD/EST?                                
         BE    GTSW3                                                            
         MVC   SVPGBK,PGBKEY                                                    
*                                  GET RULES FOR THIS PRD/EST                   
         MVC   GRPRD,PGBPRD                                                     
         MVC   GREST,PGBEST                                                     
         BAS   RE,GETRULE                                                       
*                                                                               
GTSW3    DS    0H                  SAME PRD/EST                                 
         LA    R4,RUBUFRC+RUBRULES-RUBRCD                                       
         USING RULDATAD,R4                                                      
*                                  GO THRU RULE ELEMS                           
GTSW4    DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    GTSW2               YES, NEXT TABLE ENTRY                        
*                                                                               
         CLI   PGBPGM,X'FD'        IS TABLE ENTRY FOR TYPE= ?                   
         BNE   GTSW4H                                                           
         CLC   =C'TYPE=',RUSWPGM   AND RULE?                                    
         BNE   GTSW4H                                                           
*                                                                               
         CLC   PGBPGM+1(1),RUSWPGM+5    DOES TYPE MATCH ?                       
         BNE   GTSW40                  NO, SKIP IT                              
         B     GTSW5                                                            
*                                                                               
GTSW4H   DS    0H                                                               
         GOTO1 CHKPGM,DMCB,PGBPGM,RUSWPGM   DO PROG NAMES MATCH?                
         BNE   GTSW40              NO, SKIP IT                                  
*                                                                               
GTSW5    DS    0H                                                               
         ICM   R1,15,PGBSPTS                                                    
*                                                                               
         SR    RE,RE               COMPARE TO MAXIMUM                           
         ICM   RE,3,RUSWSPTS                                                    
         BZ    GTSW40                                                           
         CR    R1,RE                                                            
         BNH   GTSW40                                                           
*                                                                               
GTSW10   DS    0H                  RULE IS VIOLATED                             
         MVI   SPRUMTKY,SPRUGSPW   SET RULE TYPE                                
         LA    R2,PL+2                                                          
         MVC   PL,SPACES                                                        
*                                                                               
         MVC   0(7,R2),=C'PROGRAM'                                              
         CLI   PGBPGM,X'FD'        PROGRAM TYPE?                                
         BNE   GTSW11                                                           
         MVC   08(5,R2),=C'TYPE='                                               
         MVC   13(1,R2),PGBPGM+1                                                
         B     *+10                                                             
*                                                                               
GTSW11   DS    0H                                                               
         MVC   08(L'PGBPGM,R2),PGBPGM                                           
*                                                                               
         CLI   SPRUBGPR+1,C'Y'     IF DOING PGM/DPT CHECK                       
         BNE   GTSW12                                                           
         MVC   WORK(L'PGBPGM),8(R2)   MAKE ROOM FOR D/                          
         MVC   10(L'PGBPGM,R2),WORK                                             
         MVC   8(1,R2),PGBDPT                                                   
         MVI   9(R2),C'/'                                                       
         LA    R2,2(R2)                                                         
*                                                                               
GTSW12   DS    0H                                                               
         LA    R2,L'PGBPGM+07(R2)                                               
         MVC   2(10,R2),=C'IN WEEK OF'                                          
         LA    R2,13(R2)                                                        
         LR    R3,R2                                                            
*                                                                               
         GOTO1 VDATCON,DMCB,(2,PGBWEEK),(5,0(R2))                               
         LA    R2,9(R2)                                                         
         MVC   0(3,R2),=C'HAS'                                                  
         LA    R2,4(R2)                                                         
         EDIT  (B4,PGBSPTS),(3,0(R2))                                           
         LA    R2,4(R2)                                                         
*                                                                               
         MVC   0(17,R2),=C'SPOTS, MAXIMUM IS'                                   
         LA    R2,18(R2)                                                        
         EDIT  (B2,RUSWSPTS),(3,0(R2))                                          
*                                                                               
GTSW26   DS    0H                                                               
         MVI   SPRUMSGT,SPRUMGLQ    GLOBAL                                      
*                                                                               
         MVC   BYTE,GRPRD          SET PRD AND EST FOR DRV INPUT ROUTS          
         BAS   RE,GETPCOD                                                       
         MVC   SBPRD,PCOD                                                       
         MVC   SBBPRD,GRPRD                                                     
         MVC   SBBEST,GREST                                                     
*                                                                               
         BAS   RE,PLHOOK                                                        
         B     GTSW2               ONLY ONE RULE FOR TABLE ENTRY                
*                                                                               
GTSW40   DS    0H                  NEXT RULE ELEM                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     GTSW4                                                            
*                                                                               
GTSW60   DS    0H                                                               
         MVI   TSOFFACT,TSAINI     CLEAR PG BUFFER                              
         MVC   TSAREC,=A(PGBUFL)   RESET BUFFER LENGTH                          
         GOTO1 ATSAR,TSARD                                                      
*                                                                               
GTSWX    DS    0H                                                               
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
***********************************************************************         
*                                                                               
*        CKRTG - CHECK IF RATING OR EXTENDED RATING FOR 2 DECIMAL DEMO          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
CKRTG    NMOD1 0,**CKRTG*                                                       
         L     RC,AWORKD                                                        
*                                                                               
         MVC   FULL2,FULL          SAVE OFF FULL                                
         ZIC   RE,SBBPRD           PROD                                         
         BCTR  RE,0                MINU 1                                       
         SLL   RE,8                TIMES 256                                    
         ZIC   R0,SBBEST                                                        
         BCTR  R0,0                                                             
         AR    RE,R0               PLUST EST MINUS 1                            
         A     RE,SBAESTTB         PLUS A(TABLE) FINDS POINTER                  
         CLI   0(RE),0             TEST PRESENT                                 
         BNE   CKRTG05                                                          
         LA    RE,1(RE)            IF NOT FOUND AT ONCE SHOULD BE ABLE          
         B     *-12                TO FIND LOWEST ACTIVE ESTIMATE               
*                                  (IN EST=NO OR RANGE SITUATIONS)              
CKRTG05  DS    0H                                                               
         L     R1,SBAESTBF         A(EST BUFFER)                                
         ZIC   RF,0(RE)            INDEX                                        
         BCTR  RF,0                                                             
         MHI   RF,ESTBUFFL                                                      
         AR    R1,RF                                                            
*                                                                               
         MVC   FULL(3),EBDEMOS-ESTBUFFD(R1)   GET FIRST DEMO                    
*                                                                               
         CLI   FULL+2,0            COMSCORE DEMO?                               
         BNE   CKRTG06             NO                                           
         XR    RF,RF               CLEAR RF                                     
         ICM   RF,1,FULL+1         GET INDEX                                    
         JZ    CKRTG06             IF 0, SOMETHING IS WRONG                     
         BCTR  RF,0                -1                                           
         MHI   RF,8                INDEX INTO DEMO LIST                         
         ICM   RE,15,SBCOMDEM      HAVE A(COMSCORE DEMO NAMES)                  
         BZ    CKRTG06             NO                                           
         AR    RE,RF               A(COMSCORE DEMO NAME)                        
         CLI   0(RE),C'R'          DEMO NAME A RATING?                          
         BE    CKRTGYES            YES                                          
         CLI   0(RE),C'E'          DEMO NAME AN EXTENDED RATING?                
         BE    CKRTGYES            YES                                          
         B     CKRTGNO             NO                                           
*                                                                               
CKRTG06  LA    R0,NMYDEMOS         MIGHT HAVE FOUND NAME ALREADY                
         LA    R2,MYDEMOS                                                       
*                                                                               
CKRTG10  OC    0(3,R2),0(R2)                                                    
         BZ    CKRTG15                                                          
         CLC   0(3,R2),FULL                                                     
         BE    CKRTG20             NAME FOUND                                   
         LA    R2,10(R2)                                                        
         BCT   R0,CKRTG10                                                       
         LA    R2,MYDEMOS                                                       
         XC    0(L'MYDEMOS,R2),0(R2)                                            
*                                                                               
CKRTG15  MVC   0(3,R2),FULL        GET THE NAME                                 
*                                                                               
         LA    R5,DBAREA                                                        
         USING DBLOCKD,R5                                                       
         XC    DBLOCK,DBLOCK                                                    
         MVI   DBFUNCT,DBGETDEM                                                 
         MVI   DBTPTT,C'T'                                                      
         MVC   DBCOMFCS,SBCOMFAC                                                
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'                                                    
         CLI   SPRUCTRY,C'C'       CANADA?                                      
         BNE   *+8                 NO                                           
         MVI   DBSELMED,C'C'                                                    
         GOTO1 VDEMOCON,DMCB,(1,FULL),(2,3(R2)),(C'S',(R5)),0                   
         DROP  R5                                                               
*                                                                               
CKRTG20  CLI   3(R2),C'R'          DEMO NAME A RATING?                          
         BE    CKRTGYES            YES                                          
         CLI   3(R2),C'E'          DEMO NAME AN EXTENDED RATING?                
         BNE   CKRTGNO             NO                                           
*                                                                               
CKRTGYES CR    RE,RE               CC = ON MATCH                                
         B     CKRTGX                                                           
CKRTGNO  LTR   RB,RB               NOT = ON MISMATCH                            
CKRTGX   MVC   FULL,FULL2                                                       
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
MYDEMOS  DS    XL80                                                             
NMYDEMOS EQU   8                                                                
*                                                                               
DBAREA   DS    XL(L'DBLOCK)                                                     
         DS    0D                                                               
*         DS    0D                  BUFFER AREAS FOR TESTING                    
*         DC    C'**RUBUF*'                                                     
*TSTRUBUF DC    20000X'00'                                                      
*         DC    C'**DSBUF*'                                                     
*TSTDSBUF DC    20000X'00'                                                      
*         DC    C'**PGBUF*'                                                     
*TSTPGBUF DC    20000X'00'                                                      
*                                                                               
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
SAVR1    DS    F                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
STAT     DS    X                                                                
BYTE     DS    X                                                                
MINMAX   DS    X                                                                
TZONE    DS    C                                                                
         DS    0D                                                               
KEY      DS    XL32                                                             
KEYSAVE  DS    XL32                                                             
GRTYPE   DS    X                                                                
GRPRD    DS    X                                                                
GREST    DS    XL2                                                              
SVREST   DS    XL2                                                              
VDMGR    DS    A                                                                
VHEXOUT  DS    A                                                                
VDATCON  DS    A                                                                
VGETDAY  DS    A                                                                
VCALLOV  DS    A                                                                
VADDAY   DS    A                                                                
VDAYUNPK DS    A                                                                
VSTAPACK DS    A                                                                
VDEMOCON DS    A                                                                
CALLRD   DS    A                                                                
RELO     DS    A                                                                
*                                                                               
SPTS     DS    F                                                                
DEMV     DS    F                                                                
DEMPCT   DS    F                                                                
TOTSPTS  DS    F                                                                
TOTDEMV  DS    F                                                                
*                                                                               
DMINBTS  DS    X                                                                
DMOUTBTS DS    X                                                                
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
ELDAY    DS    X                                                                
HDSST    DS    XL2                                                              
PCOD     DS    CL3                                                              
NDAYS    DS    C                                                                
NDAYSW   DS    C                                                                
NMINS    DS    H                                                                
STRTDAY  DS    X                                                                
RDATEBC  DS    CL6                                                              
RULSDT   DS    CL6                                                              
RULEDT   DS    CL6                                                              
WKDAY    DS    XL1                                                              
WKDAYB   DS    XL1                                                              
WKNDAYS  DS    XL1                                                              
WPRD1    DS    X                                                                
WPRD2    DS    X                                                                
BSMODE   DS    C                                                                
DTCOUNT  DS    F                                                                
DTPOINT  DS    F                                                                
DEMVAL   DS    F                                                                
DPTCTL   DS    CL1                                                              
*                                                                               
DMWORK   DS    12D                                                              
IOA      DS    XL3500                                                           
SVDSBK   DS    XL(DSBUFKYL)                                                     
SVPGBK   DS    XL(PGBUFKYL)                                                     
SVRUBK   DS    XL(RUBUFKYL)                                                     
DSBUFRC  DS    XL(DSBUFRCL)                                                     
PGBUFRC  DS    XL(PGBUFRCL)                                                     
RUBUFRC  DS    XL(RUBUFRCL)                                                     
*                                                                               
WORKDL   EQU   *-WORKD                                                          
RUBUFL   EQU   30000                                                            
DSBUFL   EQU   20000                                                            
PGBUFL   EQU   1000000                                                          
*                                                                               
RUBUFRCD DSECT         DSECT FOR RULE BUFFER RECORD                             
RUBRCD   DS    0X                                                               
RUBRLEN  DS    XL2                 RECORD LENGTH                                
RUBKEY   DS    0X                                                               
RUBRTYP  DS    XL1                                                              
RUBPRD   DS    XL1                                                              
RUBEST   DS    XL2                                                              
RUBUFKYL EQU   *-RUBKEY                                                         
RUBRULES DS    0X                                                               
         ORG   RUBUFRCD+3548       MAX FOR VARIABLE LENGTH RECS                 
RUBUFRCL EQU   *-RUBUFRCD                                                       
*                                                                               
DSBUFRCD DSECT         DSECT FOR DPT/STYPE BUFFER                               
DSBRCD   DS    0X                                                               
DSBKEY   DS    0X                                                               
DSBPRD   DS    XL1                                                              
DSBEST   DS    XL1                                                              
DSBDPT   DS    XL1                 DAYPART                                      
DSBSTYP  DS    XL1                 STATION TYPE                                 
DSBUFKYL EQU   *-DSBUFRCD                                                       
DSBDEMV  DS    XL4                 RATINGS                                      
DSBSPTS  DS    XL4                 SPOTS                                        
DSBUFRCL EQU   *-DSBUFRCD                                                       
*                                                                               
PGBUFRCD DSECT         DSECT FOR PGM/TYPE BUFFER                                
PGBRCD   DS    0X                                                               
PGBKEY   DS    0X                                                               
PGBPRD   DS    XL1                                                              
PGBEST   DS    XL1                                                              
PGBDPT   DS    CL1                                                              
PGBPGM   DS    CL20                PROGRAM                                      
PGBWEEK  DS    XL2                                                              
PGBSTA   DS    XL3                                                              
PGBUFKYL EQU   *-PGBUFRCD                                                       
PGBDEMV  DS    XL4                 RATINGS                                      
PGBSPTS  DS    XL4                 SPOTS                                        
PGBUFRCL EQU   *-PGBUFRCD                                                       
*                                                                               
*                                                                               
DTLISTD  DSECT                     DSECT TO COVER SPOT DATE LIST                
DTLPRD   DS    XL1                                                              
DTLDAT   DS    XL2                                                              
DTLCOUNT DS    XL2                                                              
DTLISTL  EQU   *-DTLISTD                                                        
*                                                                               
         EJECT                                                                  
*                                                                               
       ++INCLUDE DDTSARD                                                        
*                                                                               
         EJECT                                                                  
SPRULRDD DSECT                                                                  
       ++INCLUDE SPRULERD                                                       
         ORG SPRUSAVS                                                           
CURAGM   DS    XL1                 CURRENT AGY/MED                              
CURCLT   DS    XL2                 CURRENT CLIENT                               
CURDATE  DS    XL2                 CURRENT SPOT DATE                            
SETBSW   DS    XL1                                                              
MYESTCTL DS    XL1                 ESTIMATE CTL - X'80'=LUMP TOGETHER           
SVMKT    DS    CL4                                                              
FULL2    DS    F                                                                
GLBCTL   DS    XL1                 GLOBAL BUFFERS NEEDED CONTROL                
ARUBUF   DS    A                   A(RULE BUFFER)                               
ADSBUF   DS    A                   A(BUY DPT/STYPE BUFFER)                      
APGBUF   DS    A                   A(BUY PGM/TYPE BUFFER)                       
         DS    0D                                                               
TSARBRU  DS    XL(TSPNEWL)         TSAROFF BLOCK FOR RULES                      
         DS    0D                                                               
TSARBDS  DS    XL(TSPNEWL)         TSAROFF BLOCK FOR BUY DPT/STYPES             
         DS    0D                                                               
TSARBPG  DS    XL(TSPNEWL)         TSAROFF BLOCK FOR BUY PGM/TYPES              
ATSAR    DS    A                   A(TSAR/TSAROFF)                              
         ORG   SPRULERD+256                                                     
*                                                                               
       ++INCLUDE SPGENRULE                                                      
         SPACE 2                                                                
       ++INCLUDE SPSTAPACKD                                                     
*                                                                               
BUYRECD  DSECT                                                                  
*** ++INCLUDE SPGENBUY/DDCOMFACSD/SPGENMKT/DEDBLOCK                             
*** ++INCLUDE DDMASTD/FATWA/SPOTBLOCK                                           
         PRINT OFF                                                              
       ++INCLUDE SPGENBUY                                                       
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE SPGENMKT                                                       
DBLOCKD  DSECT                                                                  
       ++INCLUDE DEDBLOCK                                                       
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE FATWA                                                          
SBLOCKD  DSECT                                                                  
       ++INCLUDE SPOTBLOCK                                                      
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'055SPRULER   09/11/19'                                      
         END                                                                    
