*          DATA SET DRIVRANK   AT LEVEL 044 AS OF 02/24/15                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 045108.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
*PROCESS USING(WARN(15))                                                        
*CATALP DRIVRANK                                                                
         TITLE 'DRIVRANK - RANK NUMBER CONTROLLER'                              
DRIVRANK CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYDEND-MYD,**DRRANK,R9                                           
         USING MYD,RC                                                           
         L     RA,0(R1)            P1=A(GLOBAL)                                 
         USING GLOBALD,RA                                                       
         L     R2,4(R1)            P2=A(GET OR PUT)                             
         CLC   0(3,R2),=C'CLR'                                                  
         BNE   RANK1                                                            
         XC    INITSW,INITSW                                                    
         B     XIT                                                              
RANK1    L     R5,8(R1)            P3=A(RECORD)                                 
         L     R2,12(R1)           P4=A(RECORD NUMBER)                          
*                                                                               
         ZAP   DUB,0(8,R2)         CVB RECORD NUMBER                            
         NI    DUB,X'FF'-X'80'     KILL LAST RECORD SWITCH                      
         CVB   RF,DUB                                                           
         STCM  RF,3,RECCOUNT                                                    
*                                                                               
*****    MVC   RECCOUNT,2(R2)                                                   
         ZIC   R2,0(R5)            RECORD NUMBER                                
         LA    R2,INITSW-1(R2)     ADDRESS INTO INITIALIZATION SWS.             
         CLI   0(R2),0             0=NOT INITIALIZED                            
         BNE   *+8                                                              
         BAS   RE,INIT               SO DO IT!                                  
         MVC   WORK(32),RANKCOLS                                                
         MVC   WORK+32(12),INITSW                                               
         CLI   0(R2),1             1=INITIALIZED - NO RANKS                     
         BE    XIT                                                              
         L     R2,4(R1)            P2=A(GET OR PUT)                             
         CLC   0(3,R2),=C'PUT'                                                  
         BNE   RANK2                                                            
         BAS   RE,PUTRANK                                                       
         B     XIT                                                              
         SPACE 1                                                                
RANK2    CLC   0(3,R2),=C'GET'                                                  
         BNE   XIT                                                              
         BAS   RE,GETRANK                                                       
         B     XIT                                                              
         EJECT                                                                  
*              PUT VALUES INTO RANK POOL                                        
         SPACE 3                                                                
PUTRANK  NTR1                                                                   
         MVI   FIRSTGET,C'Y'       WILL NEED TO SORT ON FIRST GET               
         CLI   FIRSTPUT,C'Y'                                                    
         BNE   *+10                                                             
         XC    PUTCOUNT,PUTCOUNT   RESET RECORD COUNT ON FIRST PUT              
         MVI   FIRSTPUT,C'N'                                                    
         SPACE 1                                                                
         LH    R3,PUTCOUNT         NUMBER IN POOL SO FAR                        
         CH    R3,MAXPOOL          CHECK AGAINST MAX                            
         BE    *+8                                                              
         LA    R3,1(R3)                                                         
         STH   R3,PUTCOUNT                                                      
         BCTR  R3,0                                                             
         MH    R3,LPOOLENT                                                      
         LA    R3,RANKPOOL(R3)                                                  
         LA    R2,RANKCOLS         R2=COLUMN DATA                               
         USING COLD,R2                                                          
         USING POOLD,R3                                                         
         MVC   POOLLNO,PUTCOUNT    COUNT=LINE NUMBER                            
         LA    R3,POOLRANK         R3=A(RANK DATA)                              
         DROP  R3                                                               
         LH    R4,NCOLS            R4=N(ACTIVE COLUMNS)                         
         SPACE 1                                                                
PUTRANK2 LH    R6,COLINDSP         DISP TO INPUT DATA                           
         AR    R6,R5                                                            
******   MVC   0(4,R3),0(R6)       ASSUME BINARY FOR THE MO.                    
         ICM   RF,15,0(R6)         ASSUME BINARY FOR THE MOMENT                 
         CVD   RF,DUB                                                           
*                                                                               
         CLI   COLTYPE,C'P'        UNLESS PACKED DATA                           
         BNE   PUTRANK4                                                         
*                                                                               
         ZAP   DUB,=P'0'           INIT WORK FIELD                              
*                                                                               
         ZIC   R1,COLLEN           L'PACKED DATA                                
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         OC    0(0,R6),0(R6)       PROTECT AGAINST BINARY 0                     
         BZ    PUTRANK4                                                         
*                                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         ZAP   DUB,0(0,R6)                                                      
*                                                                               
PUTRANK4 DS    0H                                                               
*                                                                               
         AP    DUB,=P'100000000000000'  MAKE ALL NUMBERS SAME SIGN              
*                                                                               
         CP    DUB,=P'200000000000000'  CAN'T HANDLE HUGE NUMBERS               
         BL    *+10                                                             
         DC    XL6'00'                                                          
****     ZAP   DUB,=P'200000000000000'  SO JUST EQUATE THEM                     
*                                                                               
******   CVB   R1,DUB                                                           
******   ST    R1,0(R3)                                                         
*                                                                               
         OI    DUB+7,X'0F'         FORCE COMMON SIGN                            
*                                                                               
         ZAP   0(8,R3),DUB         SET VALUE TO RANK                            
*                                                                               
         CLI   COLATT,1            COLUMN ATTRIBUTE 1 = INVERTED                
         BNE   *+16                                                             
         ZAP   0(8,R3),=P'200000000000000' REVERSE ORDER                        
         SP    0(8,R3),DUB                                                      
*                                                                               
*****    XC    0(4,R3),=X'FFFFFFFF'                                             
*                                                                               
         AH    R2,LCOLENT                                                       
         LA    R3,8(R3)                                                         
         BCT   R4,PUTRANK2                                                      
*                                                                               
         B     XIT                                                              
*                                                                               
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
*              GET RANK NUMBERS FROM RANK POOL                                  
         SPACE 3                                                                
GETRANK  NTR1                                                                   
         MVI   FIRSTPUT,C'Y'                                                    
         CLI   FIRSTGET,C'Y'       DO WE NEED A SORT                            
         BNE   *+8                                                              
         BAS   RE,SORT                                                          
         MVI   FIRSTGET,C'N'                                                    
         SPACE 1                                                                
         LH    R3,RECCOUNT         PICK UP RECORD NUMBER                        
         CH    R3,MAXPOOL          CHECK FOR TRUE MAX                           
         BH    GETRNK2A              AND PUNT IF MORE                           
         BCTR  R3,0                OTHERWISE SETUP TO PROCESS                   
         B     GETRANK3                                                         
         CH    R3,MAXPOOL                                                       
         BNH   GETRANK3                                                         
GETRNK2A LA    R2,RANKCOLS         R2=COLUMN DATA                               
****     USING COLD,R2                                                          
****     LA    R3,OVERRNKB         GET LAST BUCKET                              
****     CLI   COLTYPE,C'P'                                                     
****     BNE   GETRANK4                                                         
         LA    R3,OVERRNKP         GET LAST BUCKET                              
         B     GETRANK4                                                         
GETRANK3 MH    R3,LPOOLENT                                                      
         LA    R3,RANKPOOL(R3)                                                  
GETRANK4 LA    R2,RANKCOLS         R2=COLUMN DATA                               
         USING COLD,R2                                                          
         USING POOLD,R3                                                         
         LA    R3,POOLRANK         R3=A(RANK DATA)                              
         DROP  R3                                                               
         LH    R4,NCOLS            R4=N(ACTIVE COLUMNS)                         
         SPACE 1                                                                
GETRANK5 LH    R1,COLRKDSP         DISP TO RANK DATA                            
         AR    R1,R5                                                            
         ZAP   DUB,0(8,R3)         GET RANKING                                  
         NI    DUB,X'FF'-X'80'     KILL LAST RECORD SWITCH                      
         CVB   RF,DUB                                                           
         STCM  RF,15,0(R1)         PASS BACK TO CALLER                          
*                                                                               
         AH    R2,LCOLENT                                                       
         LA    R3,8(R3)                                                         
         BCT   R4,GETRANK5                                                      
         B     XIT                                                              
         DROP  R2                                                               
         EJECT                                                                  
*              SORT RANKPOOL                                                    
*                                                                               
*              INPUT               PUTCOUNT: N'LINES IN POOL                    
*                                  NCOLS:    N'COLS TO SORT                     
*                                                                               
SORT     NTR1                                                                   
*                                  FIRST SORT FOR THE COLUMNS                   
         LA    R2,RANKPOOL         R2=A(RECORDS)                                
         LH    R3,PUTCOUNT         R3=N'RECORDS                                 
         LH    R5,LPOOLENT         R5=L'RECORD                                  
         LA    R6,2                R6=DISPLACEMENT OF KEY                       
         LH    R7,NCOLS            R7=NUMBER OF COLUMNS TO SORT                 
*                                                                               
SORT2    GOTO1 XSORT,DMCB,(1,(R2)),(R3),(R5),8,(R6)                             
*                                                                               
         BAS   RE,VALTONUM         CONVERT VALUES TO RANK NUMBERS               
*                                                                               
         LA    R6,8(R6)                                                         
         BCT   R7,SORT2                                                         
*                                  FINALLY RESORT ON LINE NUMBER                
         GOTO1 XSORT,DMCB,(0,(R2)),(R3),(R5),2,0                                
*                                                                               
         B     XIT                                                              
*                                                                               
*                                  CONVERT VALUES TO RANK NUMBERS               
*                                                                               
VALTONUM NTR1                                                                   
*                                                                               
         AR    R2,R6               R2=A(THIS COLUMN)                            
         ZAP   LASTVAL,=P'0'       INIT                                         
         ZAP   PLACE,=P'1'         PLACE NUMBER                                 
         ZAP   RESET,=P'1'         RESET NUMBER                                 
*                                                                               
VTN2     CP    LASTVAL,0(8,R2)     IS THIS VALUE = LAST                         
         BNE   VTN4                                                             
*                                                                               
         ZAP   0(8,R2),PLACE       RETURN PRESENT PLACE NUMBER                  
*                                                                               
         CP    RESET,=P'1'         AND, UNLESS THIS IS THE FIRST                
         BE    *+8                                                              
         OI    0(R2),X'80'              TURN ON EQUALITY BIT                    
*                                                                               
         B     VTNX                                                             
*                                                                               
VTN4     ZAP   PLACE,RESET         THIS NOT = LAST, SO PLACE=RESET              
         ZAP   LASTVAL,0(8,R2)     SAVE CURRENT VALUE                           
         ZAP   0(8,R2),PLACE       ASSIGN NEXT RANKING NUMBER                   
*                                                                               
         CH    R3,=H'1'                                                         
         BE    VTNX                IF THIS IS NOT THE LAST LINE                 
*                                                                               
         LA    R1,0(R2,R5)                                                      
*                                                                               
         CP    LASTVAL,0(8,R1)     IS 'THIS' VALUE = NEXT VALUE                 
         BNE   *+8                                                              
         OI    0(R2),X'80'            YES: TURN ON EQUALITY BIT                 
*                                                                               
VTNX     CLC   PUTCOUNT,MAXPOOL    MORE THAN OR EQ MAX LINES                    
         BNE   *+10                                                             
         ZAP   0(8,R2),=P'0'       KILL THE RANKS                               
*                                                                               
         AP    RESET,=P'1'         BUMP RESET NUMBER                            
         AR    R2,R5               GET TO NEXT COLUMN VALUE                     
         BCT   R3,VTN2                                                          
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*              INITIALIZATION FOR THIS REPORT NUMBER                            
*                                                                               
*              INPUT               R2=A(INITIALIZATION SWITCH)                  
*                                  R5=A(RECORD)                                 
*                                                                               
INIT     NTR1                                                                   
*                                                                               
         MVI   0(R2),1             PRESET TO 'NO RANK FOUND'                    
*                                                                               
         XC    RANKCOLS,RANKCOLS                                                
         LA    R4,RANKCOLS                                                      
         USING COLD,R4                                                          
*                                                                               
         XC    NCOLS,NCOLS                                                      
*                                                                               
         ZIC   R3,0(R5)            PICK UP RECORD NUMBER                        
         BCTR  R3,0                                                             
         SLL   R3,2                                                             
*                                                                               
         LA    R3,GLAINTD(R3)                                                   
         L     R3,0(R3)                                                         
         USING GLINTD,R3                                                        
*                                                                               
         L     R3,GLAFIN                                                        
*                                                                               
INIT20   CLI   0(R3),X'20'                                                      
         BNE   INITNEXT                                                         
*                                                                               
         USING DRIND,R3                                                         
*                                                                               
         CLC   DRINROUT(7),=C'COLRANK '                                         
         BE    INIT20RK                                                         
*                                                                               
INIT20RE MVC   COLTYPE,DRINTYPE    REGULAR IN - SAVE DATA IN CASE               
         MVC   COLLEN,DRINFLEN                  NEXT IN IS A RANK               
         LH    R1,DRINDISP                                                      
         STH   R1,COLINDSP                                                      
         CLI   COLTYPE,C'M'        ADJUST MASKED TO PACKED                      
         BNE   INITNEXT                                                         
         MVI   COLTYPE,C'P'                                                     
         LA    R1,1(R1)                                                         
         STH   R1,COLINDSP                                                      
         ZIC   R1,COLLEN                                                        
         BCTR  R1,0                                                             
         STC   R1,COLLEN                                                        
         B     INITNEXT                                                         
         SPACE 1                                                                
INIT20RK MVI   0(R2),2             RANK COLUMN FOUND - SET SWITCH               
         MVC   COLATT,DRINARGS     SAVE INPUT ARGUMENTS                         
         MVC   COLRKDSP,DRINDISP                                                
         LH    R1,NCOLS            BUMP N'COLUMNS FOUND                         
         LA    R1,1(R1)                                                         
         STH   R1,NCOLS                                                         
         CLC   NCOLS,MAXCOLS       CHECK AGAINST MAX                            
         BE    INITSEED                                                         
         AH    R4,LCOLENT                                                       
         SPACE 1                                                                
INITNEXT ZIC   R1,1(R3)                                                         
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R3,R1                                                            
         CLI   0(R3),0                                                          
         BE    INITSEED                                                         
         CLI   0(R3),X'10'                                                      
         BE    INITSEED                                                         
         B     INIT20                                                           
         SPACE 1                                                                
INITSEED CLC   NCOLS,=H'0'                                                      
         BE    INITX                                                            
         L     RF,LPOLBUF                                                       
         LH    R1,NCOLS            NUMBER OF COLUMNS                            
         SLL   R1,3                *8                                           
         LA    R1,2(R1)            + THE KEY                                    
         STH   R1,LPOOLENT         = TOTAL ITEM LENGTH                          
         SR    RE,RE                                                            
         DR    RE,R1               NUMBER OF ENTRIES                            
         STH   RF,MAXPOOL          SAVE MAXIMUM LINES                           
         STH   RF,OVERRNKB                                                      
         STH   RF,OVERRNKP                                                      
INITX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*              ODDMENTS AND RANK POOL                                           
         SPACE 3                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
INITSW   DC    XL12'00'                                                         
FIRSTPUT DC    C'Y'                                                             
FIRSTGET DC    C'Y'                                                             
MAXCOLS  DC    H'12'               MAX 12 COLUMNS                               
LCOLENT  DC    H'8'                8 BYTE PER ENTRY                             
         DS    0F                                                               
RANKCOLS DS    CL96                FOR EACH RANK COLUMN FOUND                   
*                                  8 BYTE ENTRY (SEE COLD BELOW)                
NCOLS    DC    H'0'                                                             
PUTCOUNT DC    H'0'                                                             
RECCOUNT DC    H'0'                                                             
OVERRNKB DC    H'0000'                                                          
         DC    12AL4(0,999)                                                     
OVERRNKP DC    H'0000'                                                          
         DC    12PL8'999'                                                       
         LTORG                                                                  
         SPACE 1                                                                
         DS    0F                                                               
LPOLBUF  DC    AL4(RANKPOLE-RANKPOOL)                                           
MAXPOOL  DC    H'0000'             VARIABLE MAX LINES                           
LPOOLENT DC    H'00'               LENGTH OF EACH LINE                          
RANKPOOL DC    220000X'00'                                                      
RANKPOLE DS    0C                                                               
         SPACE 1                                                                
*              DSECTS FOR MODULE                                                
         SPACE 3                                                                
COLD     DSECT                                                                  
COLENT   DS    0CL8                EACH ENTRY IS 8 BYTES                        
COLTYPE  DS    CL1                 B OR P SUPPORTED                             
COLLEN   DS    CL1                                                              
COLATT   DS    CL2                                                              
COLINDSP DS    CL2                 DISP TO COLUMN INPUT                         
COLRKDSP DS    CL2                 DISP TO RANK INPUT                           
         SPACE 3                                                                
POOLD    DSECT                                                                  
POOLENT  DS    0CL98               EACH ENTRY IS 50 BYTES                       
POOLLNO  DS    H                   LINE NUMBER 1-1000                           
POOLRANK DS    12PL8               12 VALUES/RANK NUMBERS                       
         SPACE 3                                                                
MYD      DSECT                                                                  
         DS    D                                                                
BORROW   DS    CL2                                                              
LASTVAL  DS    PL8                                                              
FULL     DS    F                                                                
PLACE    DS    PL8                                                              
RESET    DS    PL8                                                              
*                                                                               
MYDEND   EQU   *                                                                
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'044DRIVRANK  02/24/15'                                      
         END                                                                    
