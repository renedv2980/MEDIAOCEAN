*          DATA SET PUBEDITR   AT LEVEL 023 AS OF 05/01/02                      
*CATALP PUBEDIT                                                                 
         TITLE 'PUBEDIT - CHANGE LOG'                                           
*                                                                               
* BPLA 11/18/92  IF ZONE/EDT = X'FFFF' DISPLAY AS ",ALL"                        
*                FOR EDITION EXPANSION DISPLAY AS "ALL"                         
*                                                                               
* ROSA 12/27/88  A/ CHANGE DESCRIPTION PAINTS TO PAINTED            L01         
*                B/ ADD EDITION CODES X- PERMANENT                  L01         
*                                     N- PRINTED                    L01         
         TITLE 'PUBEDIT - FORMAT PUB NUMBER'                                    
PUBEDIT  CSECT                                                                  
         NMOD1 2,**PBED                                                         
         SPACE 2                                                                
*                                                                               
* PARAM 1   BYTE  0      ZERO FILL TO 'N' PLACES  (N BINARY OR EBCDIC)          
*           BYTES 1-3    A(INPUT)                                               
*                                                                               
* PARAM 2   BYTE  0      S = SHORT OUTPUT (NO DASHES)                           
*                        E = 11 CHAR EDIT EXPANSION                             
*                        Q = Q FORMAT                                           
*           BYTES 1-3    A(OUTPUT) - N-NNN-NNNN,ZZ,EDT                          
*                                   DASHES ONLY IF FORCE TO 8                   
*                                                                               
*                                  -OR EDIT EXPANSION                           
         EJECT                                                                  
         USING WRKD,RC                                                          
         LM    R2,R3,0(R1)                                                      
         CLI   4(R1),C'E'          EDIT EXP                                     
         BE    ED4                                                              
         CLI   4(R1),C'Q'                                                       
         BE    ED10                                                             
         UNPK  WORK(9),0(5,R2)                                                  
         MVC   BYTE,0(R1)                                                       
         NI    BYTE,X'0F'                                                       
         CLI   BYTE,8                                                           
         BNE   ED2                                                              
         CLI   4(R1),C'S'                                                       
         BE    ED2                                                              
*                                  1-234-5678                                   
         MVC   0(1,R3),WORK                                                     
         MVI   1(R3),C'-'                                                       
         MVC   2(3,R3),WORK+1                                                   
         MVI   5(R3),C'-'                                                       
         MVC   6(4,R3),WORK+4                                                   
         LA    R4,10(R3)                                                        
         B     ED3                                                              
*                                  01111111                                     
ED2      DS    0H                                                               
         MVC   0(8,R3),WORK                                                     
         LA    R4,8(R3)                                                         
         SR    R0,R0                                                            
         IC    R0,BYTE                                                          
         LA    R5,8                                                             
         SR    R5,R0                                                            
         BNP   ED3                                                              
ED2A     DS    0H                                                               
         CLI   0(R3),C'0'                                                       
         BNE   ED3                                                              
         MVC   0(7,R3),1(R3)                                                    
         MVI   7(R3),C' '                                                       
         BCTR  R4,R0                                                            
         BCT   R5,ED2A                                                          
*                                  ZONE                                         
ED3      DS    0H                                                               
         CLC   4(2,R2),=X'FFFF'    SPECIAL CHECK FOR ALL ZONES/EDTS             
         BNE   ED3D                                                             
         MVI   0(R4),C','                                                       
         MVC   1(3,R4),=C'ALL'                                                  
         B     EDEXT                                                            
*                                                                               
ED3D     CLI   4(R2),0                                                          
         BE    ED4                                                              
         MVI   0(R4),C','                                                       
         BAS   RE,EDZON                                                         
         MVC   1(2,R4),WORK                                                     
         LA    R4,3(R4)                                                         
*                                  EDITION                                      
ED4      DS    0H                                                               
         CLI   4(R1),C'E'          SEE IF DOING EDITION EXPANSION               
         BNE   ED40                                                             
         CLI   5(R2),X'FF'         ALL EDITIONS                                 
         BNE   ED40                                                             
         MVC   0(11,R3),=CL11'ALL'                                              
         B     EDEXT                                                            
*                                                                               
ED40     LA    R5,EDTAB                                                         
ED4A     DS    0H                                                               
         CLC   0(1,R5),5(R2)                                                    
         BE    ED4B                FOUND                                        
         BH    ED4C                NOT IN TABLE                                 
         LA    R5,15(R5)                                                        
         B     ED4A                                                             
ED4B     DS    0H                                                               
         CLI   4(R1),C'E'                                                       
         BNE   ED4B2                                                            
         MVC   0(11,R3),4(R5)      EDITION EXPANSION                            
         B     ED4C                                                             
ED4B2    DS    0H                                                               
         MVI   0(R4),C','                                                       
         MVC   1(3,R4),1(R5)                                                    
ED4C     DS    0H                                                               
EDEXT    DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
EDTAB    DS    0C                                                               
         DC    CL15'AM  MORNING'                                                
         DC    CL15'BE  EVENING'                                                
         DC    CL15'CD  DAILY'                                                  
         DC    CL15'DME MORN/EVE'                                               
         DC    CL15'ESAMSAT MORN'                                               
         DC    CL15'FSAESAT EVE'                                                
         DC    CL15'GSD SAT DAILY'                                              
         DC    CL15'HSMESAT M/E'                                                
         DC    CL15'ISU SUNDAY'                                                 
         DC    CL15'JOM MORN COMBO'                                             
         DC    CL15'KOE EVE COMBO'                                              
         DC    CL15'LOD DAILY COMBO'                                            
         DC    CL15'MOMEM/E COMBO'                                              
         DC    CL15'NPR PRINTED'                                    L01         
         DC    CL15'PP  PAINTED'                                    L01         
         DC    CL15'RR  ROTARY'                                                 
         DC    CL15'SS  SUNDAY'                                                 
         DC    CL15'TT  TRANSIT'                                                
         DC    CL15'UMONMONTHLY'                                                
         DC    CL15'WW  WEEKLY'                                                 
         DC    CL15'XPM PERMANENT'                                  L01         
         DC    X'FF'               EOL                                          
         SPACE 2                                                                
ED10     DS    0H                                                               
         UNPK  0(9,R3),0(5,R2)                                                  
         MVC   8(3,R3),=3C' '                                                   
         CLI   4(R2),0                                                          
         BE    ED11                NO ZONE                                      
         BAS   RE,EDZON                                                         
         MVC   8(2,R3),WORK                                                     
ED11     DS    0H                                                               
         CLI   5(R2),0                                                          
         BE    *+10                NO EDIT                                      
         MVC   10(1,R3),5(R2)                                                   
         B     EDEXT                                                            
         SPACE 2                                                                
EDZON    DS    0H                                                               
*                                  'HEXOUT'                                     
         SR    RF,RF                                                            
         MVC   BYTE,4(R2)                                                       
         NI    BYTE,X'0F'                                                       
         IC    RF,BYTE                                                          
         LA    RF,183(RF)          A-F                                          
         CLI   BYTE,10                                                          
         BNL   *+8                                                              
         LA    RF,57(RF)           0-9                                          
         STC   RF,WORK+1                                                        
         IC    RF,4(R2)                                                         
         SRL   RF,4                                                             
         LA    RF,183(RF)          A-F                                          
         CLI   4(R2),160                                                        
         BNL   *+8                                                              
         LA    RF,57(RF)           0-9                                          
         STC   RF,WORK                                                          
         BR    RE                                                               
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 3                                                                
WRKD     DSECT                                                                  
WORK     DS    CL10                                                             
BYTE     DS    X                                                                
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'023PUBEDITR  05/01/02'                                      
         END                                                                    
