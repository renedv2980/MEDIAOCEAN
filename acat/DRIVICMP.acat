*          DATA SET DRIVICMP   AT LEVEL 006 AS OF 09/11/03                      
*CATALP DRIVICMP                                                                
         TITLE 'DRIVICMP - CONTROL INTERNAL COMPUTES'                           
DRIVICMP CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MYDEND-MYD,**DRICMP                                              
         L     RA,0(R1)                                                         
         USING GLOBALD,RA                                                       
******** L     R5,GLAIO            R5=A(RECORD)                                 
         CLC   RECNUM,0(R5)        IF THIS IS A NEW RECORD TYPE                 
         BE    *+8                                                              
         MVI   COMPSTAT,0          RESET COMPUTES STATUS                        
         CLI   COMPSTAT,1          NONE FOUND PREVIOUSLY                        
         BE    XIT                                                              
         BAS   RE,COMPREC                                                       
         CLI   COMPSTAT,0          NONE FOUND THIS TIME                         
         BNE   XIT                                                              
         MVI   COMPSTAT,1                                                       
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE CONTROLS COMPUTES ON RECORD                              
         SPACE 3                                                                
*              INPUT           OLD GLAIO=A(RECORD TO BE COMPUTED)               
*              INPUT               R5=A(RECORD TO BE COMPUTED)                  
         SPACE 1                                                                
COMPREC  NTR1                                                                   
******** L     R5,GLAIO                                                         
         ZIC   R2,0(R5)            RECORD NUMBER                                
         BCTR  R2,0                                                             
         SLL   R2,2                                                             
         LA    R2,GLAINTD(R2)                                                   
         L     R2,0(R2)                                                         
******** ST    R2,GLATHID                                                       
         USING GLINTD,R2                                                        
         L     R3,GLAFIN                                                        
         XC    GLAIFLD,GLAIFLD                                                  
         DROP  R2                                                               
         SPACE 1                                                                
         USING DRIND,R3                                                         
COMP20   CLI   DRINTYPE+1,C'+'     MUST BE ADDITIVE                             
         BNE   COMPNEXT                                                         
         LH    R1,DRINDISP         INPUT FIELD                                  
         AR    R1,R5                                                            
         ST    R1,GLAIFLD          A(INPUT FIELD)                               
         B     COMPNEXT                                                         
         SPACE 1                                                                
         USING DROD,R3                                                          
COMP30   OC    DRORADD,DRORADD     IN ORDER TO QUALIFY FIELD                    
         BZ    COMP30X             MUST HAVE AN OUTPUT ROUTINE                  
         OC    GLAIFLD,GLAIFLD     AND ALSO A PREVIOUS 'IN'                     
         BZ    COMP30X                                                          
         MVI   GLHOOK,GLINCOMP     YES SO SET UP FOR A HOOK                     
         MVC   GLLABEL,DROROUT                                                  
         MVC   GLAROUT,DRORADD                                                  
         MVC   GLARGS,DROARGS                                                   
         LA    R1,DUMMYA                                                        
         MVI   DUMMYA,C' '                                                      
         MVC   DUMMYA+1(249),DUMMYA                                             
         MVC   DUMMYB,DUMMYA                                                    
         ST    R1,GLAOFLD                                                       
         ST    R3,GLADTENT                                                      
         BAS   RE,GOHOOK                                                        
         CLI   GLHOOK,GLIDID       DID USER COMPUTE FOR US                      
         BNE   COMP30X                                                          
         CLI   COMPSTAT,0                                                       
         BNE   COMP30X                                                          
         MVI   COMPSTAT,2          NOTE THERE IS SOME INTERNAL                  
         SPACE 1                                                                
COMP30X  XC    GLAIFLD,GLAIFLD                                                  
         SPACE 1                                                                
COMPNEXT ZIC   R1,1(R3)                                                         
         AR    R3,R1                                                            
         CLI   0(R3),X'20'                                                      
         BE    COMP20                                                           
         CLI   0(R3),X'30'                                                      
         BE    COMP30                                                           
         CLI   0(R3),X'10'                                                      
         BE    XIT                                                              
         CLI   0(R3),0                                                          
         BNE   COMPNEXT                                                         
         B     XIT                                                              
         DROP  R3                                                               
         SPACE 1                                                                
COMPSTAT DC    X'00'               CHANGED TO 1 IF NO COMPUTES                  
*                                  CHANGED TO 2 IF COMPUTES OK                  
RECNUM   DC    X'00'               RECORD NUMBER                                
         EJECT                                                                  
*              HOOK TO APPLICATION OR SYSTEM DRIVER                             
         SPACE 3                                                                
GOHOOK   NTR1                                                                   
         CLI   GLAROUT,0                                                        
         BE    XIT                                                              
         CLI   GLAROUT,2           FIRST BYTE INDICATES WHER                    
*                                  1=APPLICATION 2=SYSDRIVER                    
         BE    GOH2                                                             
         BH    GOH3                                                             
         L     RF,GLAHOOK          PICK UP APPLICATION HOOK                     
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         L     RE,USERRD           USERS RD                                     
         LM    R0,RC,20(RE)        RESTORE USERS R0-RC                          
         BASR  RE,RF               RF CONTAINED A(HOOK ROUTINE)                 
         XIT1                                                                   
         SPACE 1                                                                
GOH2     L     RF,GLASYSDR                                                      
         LTR   RF,RF                                                            
         BZ    XIT                                                              
         GOTO1 (RF),DMCB,(RA)                                                   
         B     XIT                                                              
         SPACE 1                                                                
GOH3     GOTO1 =V(DRIVROUT),DMCB,(RA)                                           
         B     XIT                                                              
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
         LTORG                                                                  
         DS    CL10   NEED STORAGE SO WONDERFUL ROUTINES SUCH AS EDITOR         
*                     DO NOT CLOBBER THE LTORG WHEN THE NUMBER DOES NOT         
*                     FIT. EDITOR SEEMS TO THINK IT IS OK TO CLOBBER            
*                     ONE BYTE OF STORAGE BEFORE THE OUTPUT AREA WHICH          
*                     IN THIS CASE IS A(DRIVROUT).  NOT A GOOD THING!!          
DUMMYA   DS    CL250                                                            
DUMMYB   DS    CL250                                                            
         SPACE 1                                                                
*              DSECT FOR MODULE                                                 
         SPACE 3                                                                
MYD      DSECT                                                                  
         DS    D                                                                
         SPACE 1                                                                
MYDEND   EQU   *                                                                
         SPACE 1                                                                
         PRINT OFF                                                              
       ++INCLUDE DRGLOBAL                                                       
       ++INCLUDE DRLOCAL                                                        
       ++INCLUDE DRINTRECD2                                                     
       ++INCLUDE DRIVETABLE                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DRIVICMP  09/11/03'                                      
         END                                                                    
