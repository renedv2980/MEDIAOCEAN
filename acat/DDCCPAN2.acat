*          DATA SET DDCCPAN2   AT LEVEL 027 AS OF 08/18/06                      
*&&      SET   NOP=N                                                            
*CATALP CCPAN2                                                                  
         TITLE 'CCPAN2 - XREF FROM PAN TAPE'                                    
CCPAN2   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE WORKX-WORKD,**PAN2**,R9,WORK=V(REGSAVE)                          
*                                                                               
         USING WORKD,RC            SET UP WORKING STORAGE USINGS                
         USING PLINED,PLINE                                                     
*                                                                               
         BAS   RE,PRINTI                                                        
         BAS   RE,INIT                                                          
         BAS   RE,MAIN                                                          
         BAS   RE,PRINTX                                                        
*                                                                               
XBASE    XBASE                                                                  
         EJECT                                                                  
*************************************************************                   
*        INITIALISE                                         *                   
*************************************************************                   
         SPACE 1                                                                
INIT     NTR1                                                                   
         MVC   PROGNOW,SPACES                                                   
         LA    R3,IOAREA                                                        
*                                                                               
INIT010  GOTO1 =V(CARDS),DMCB,(R3),=C'RE00'                                     
         CLC   =C'/*',0(R3)                                                     
         BE    EXIT                                                             
         MVC   PLINE+1(80),0(R3)                                                
         BAS   RE,PRINTL           PRINT PARAMETER CARD                         
*                                                                               
         LR    R1,R3               PASS TO VALCARD                              
         BAS   RE,VALCARD          READ KEYWORD=VALUES                          
         BNE   XBASE               NEQ MEANS INVALID KEYWORD                    
         B     INIT010                                                          
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
************************************************************                    
*        MAIN PROGRAM                                      *                    
************************************************************                    
         SPACE 1                                                                
MAIN     NTR1                                                                   
         BAS   RE,PRINTT                                                        
         MVC   PLINE,M1                                                         
         BAS   RE,PRINTL                                                        
         BAS   RE,PANBLD           BUILD A DRECTORY LIST                        
*                                                                               
         BAS   RE,ZAPSYS           CLEAR ALL COUNTS                             
         BAS   RE,ZAPPROG                                                       
         BAS   RE,ZAPBOOK                                                       
*                                                                               
         OPEN  (TMPFILE,INPUT)     OPEN DIR LIST FILE                           
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OPEN  (PANFILE,OUTPUT)    OPEN PAN OUT FILE                            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MAIN020  XC    PANINF,PANINF       CLEAR INFO BLOCK                             
         BAS   RE,PANDIR           READ AN ENTRY                                
         CLI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         BE    MAIN999                                                          
         CLC   PANBK(2),=C'XX'                                                  
         BE    MAIN999                                                          
         CLC   PANBK(2),=C'RM'                                                  
         BE    MAIN020                                                          
         CLC   PANBK(2),=C'LM'                                                  
         BE    MAIN020                                                          
         CLC   PANBK(2),=C'LK'                                                  
         BE    MAIN020                                                          
*                                                                               
         AP    PGCODE,BKCODE       UPDATE PROGRAM ACCUMS                        
         AP    PGFLD,BKFLD                                                      
         AP    PGDC,BKDC                                                        
         AP    PGDS,BKDS                                                        
         AP    PGCOMM,BKCOMM                                                    
*                                                                               
         AP    SCCODE,BKCODE       UPDATE SYSTEM ACCUMS                         
         AP    SCFLD,BKFLD                                                      
         AP    SCDC,BKDC                                                        
         AP    SCDS,BKDS                                                        
         AP    SCCOMM,BKCOMM                                                    
*                                                                               
         CLC   PROGNOW,PANBK       SAME PROG                                    
         BNE   MAIN021                                                          
*                                                                               
         LA    RF,9                IGNORE BOOKS ENDING IN ABCXS                 
CHECKA1  LA    R1,PANBK                                                         
         AR    R1,RF                                                            
         CLI   0(R1),C' '                                                       
         BNE   *+8                                                              
         BCT   RF,CHECKA1                                                       
         CLI   0(R1),C'S'                                                       
         BE    MAIN020                                                          
         CLI   0(R1),C'X'                                                       
         BE    MAIN020                                                          
         CLI   0(R1),C'A'                                                       
         BE    MAIN020                                                          
         CLI   0(R1),C'B'                                                       
         BE    MAIN020                                                          
         CLI   0(R1),C'C'                                                       
         BE    MAIN020                                                          
*                                                                               
MAIN021  CLC   PROGNOW,PANBK       SAME PROG CARRYON                            
         BE    MAIN025                                                          
         CLC   PROGNOW,SPACES                                                   
         BE    MAIN025                                                          
*                                                                               
         MVC   PLINE,BK                                                         
         MVC   PLBOOK(5),PROGNOW                                                
         EDIT  (P4,PGCODE),(8,PLCODE)                                           
         CLI   PLCODE+7,C' '                                                    
         BNE   *+8                                                              
         MVI   PLCODE+7,C'-'                                                    
         EDIT  (P4,PGFLD),(8,PLFLD)                                             
         CLI   PLFLD+7,C' '                                                     
         BNE   *+8                                                              
         MVI   PLFLD+7,C'-'                                                     
         EDIT  (P4,PGDC),(8,PLDC)                                               
         CLI   PLDC+7,C' '                                                      
         BNE   *+8                                                              
         MVI   PLDC+7,C'-'                                                      
         EDIT  (P4,PGDS),(8,PLDS)                                               
         CLI   PLDS+7,C' '                                                      
         BNE   *+8                                                              
         MVI   PLDS+7,C'-'                                                      
         EDIT  (P4,PGCOMM),(8,PLCOMM)                                           
         CLI   PLCOMM+7,C' '                                                    
         BNE   *+8                                                              
         MVI   PLCOMM+7,C'-'                                                    
*NOP     BAS   RE,PRINTL                                                        
         BAS   RE,ZAPPROG                                                       
*                                                                               
         CLC   SYSNOW,PANBK        SAME SYSTEM CARRYON                          
         BE    MAIN025                                                          
*                                                                               
         MVC   PLINE,M1                                                         
         BAS   RE,PRINTL                                                        
         MVC   PLINE,BK                                                         
         MVC   PLBOOK(2),SYSNOW                                                 
         EDIT  (P4,BOOKCNT),(5,PLBOOK+3)                                        
         EDIT  (P4,BOOKCOD),(5,PLCOD+3)                                         
         EDIT  (P4,BOOKSCR),(5,PLSCR+3)                                         
         EDIT  (P4,BOOKDSE),(5,PLDSE+3)                                         
         EDIT  (P4,SCCODE),(8,PLCODE)                                           
         CLI   PLCODE+7,C' '                                                    
         BNE   *+8                                                              
         MVI   PLCODE+7,C'-'                                                    
         EDIT  (P4,SCFLD),(8,PLFLD)                                             
         CLI   PLFLD+7,C' '                                                     
         BNE   *+8                                                              
         MVI   PLFLD+7,C'-'                                                     
         EDIT  (P4,SCDC),(8,PLDC)                                               
         CLI   PLDC+7,C' '                                                      
         BNE   *+8                                                              
         MVI   PLDC+7,C'-'                                                      
         EDIT  (P4,SCDS),(8,PLDS)                                               
         CLI   PLDS+7,C' '                                                      
         BNE   *+8                                                              
         MVI   PLDS+7,C'-'                                                      
         EDIT  (P4,SCCOMM),(8,PLCOMM)                                           
         CLI   PLCOMM+7,C' '                                                    
         BNE   *+8                                                              
         MVI   PLCOMM+7,C'-'                                                    
         BAS   RE,PRINTL                                                        
         BAS   RE,ZAPSYS                                                        
         MVC   PLINE,B1                                                         
         BAS   RE,PRINTL                                                        
         BAS   RE,PRINTT                                                        
         MVC   PLINE,M1                                                         
         BAS   RE,PRINTL                                                        
*                                                                               
MAIN025  MVC   PROGNOW,PANBK                                                    
         MVC   SYSNOW,PANBK                                                     
         BAS   RE,ZAPBOOK                                                       
*                                                                               
MAIN030  BAS   RE,PANLIN           GET A LINE OF PAN INPUT                      
         BE    MAIN035                                                          
         TM    DMCB+8,X'80'                                                     
         BO    MAIN040                                                          
         B     MAIN030                                                          
*                                                                               
MAIN035  BAS   RE,EXTRACT          EXTRACT USEFUL INFO                          
         B     MAIN030                                                          
*                                                                               
MAIN040  MVC   PLINE,BK                                                         
         MVC   PLBOOK,PANBK                                                     
         EDIT  (P4,BKCODE),(8,PLCODE)                                           
         EDIT  (P4,BKFLD),(8,PLFLD)                                             
         EDIT  (P4,BKDC),(8,PLDC)                                               
         EDIT  (P4,BKDS),(8,PLDS)                                               
         EDIT  (P4,BKCOMM),(8,PLCOMM)                                           
*NOP     BAS   RE,PRINTL                                                        
         AP    BOOKCNT,=P'1'                                                    
*                                                                               
         CP    BKCODE,=P'10'       MORE THAN 10 LOC                             
         BL    *+14                                                             
         AP    BOOKCOD,=P'1'       COUNT AS CODE BOOK                           
         B     MAIN020                                                          
*                                                                               
         CP    BKFLD,=P'3'         MORE THAN 3 FIELDS                           
         BL    *+14                                                             
         AP    BOOKSCR,=P'1'       COUNT AS SCREEN BOOK                         
         B     MAIN020                                                          
*                                                                               
         CP    BKDS,=P'10'         MORE THAN 10 DS                              
         BL    *+10                                                             
         AP    BOOKDSE,=P'1'       COUNT AS DSECT BOOK                          
*                                                                               
         B     MAIN020             NEXT BOOK                                    
*                                                                               
MAIN999  CLOSE TMPFILE             CLOSE FILES AND EXIT                         
         CLOSE PANFILE                                                          
         B     EXIT                                                             
         EJECT                                                                  
*************************************************************                   
*        ZAP COUNTS                                         *                   
*************************************************************                   
         SPACE 1                                                                
ZAPBOOK  ZAP   BKCODE,=P'0'        CLEAR ALL COUNTERS                           
         ZAP   BKFLD,=P'0'                                                      
         ZAP   BKDC,=P'0'                                                       
         ZAP   BKDS,=P'0'                                                       
         ZAP   BKCOMM,=P'0'                                                     
         BR    RE                                                               
ZAPPROG  ZAP   PGCODE,=P'0'                                                     
         ZAP   PGFLD,=P'0'                                                      
         ZAP   PGDC,=P'0'                                                       
         ZAP   PGDS,=P'0'                                                       
         ZAP   PGCOMM,=P'0'                                                     
         BR    RE                                                               
ZAPSYS   ZAP   SCCODE,=P'0'                                                     
         ZAP   SCFLD,=P'0'                                                      
         ZAP   SCDC,=P'0'                                                       
         ZAP   SCDS,=P'0'                                                       
         ZAP   SCCOMM,=P'0'                                                     
         ZAP   BOOKCNT,=P'0'                                                    
         ZAP   BOOKCOD,=P'0'                                                    
         ZAP   BOOKSCR,=P'0'                                                    
         ZAP   BOOKDSE,=P'0'                                                    
         BR    RE                                                               
         EJECT                                                                  
*************************************************************                   
*        BUILD FILE OF DIR ENTRIES                          *                   
*************************************************************                   
         SPACE 1                                                                
PANBLD   NTR1                      FIRST READ ALL DIR ENTRIES                   
         OPEN  (TMPFILE,OUTPUT)                                                 
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PANBK,=CL10'A'      START FROM A                                 
*                                                                               
PANBLD3  GOTO1 VPANIC,DMCB,(X'40',READ),(C'T',DIR),PANBK,PANCARD                
*                                                                               
         CLC   PANCARD(2),=C'/*'   END OF DIRECTORY?                            
         BE    PANBLD4             YES                                          
*                                                                               
         PUT   TMPFILE,PANCARD     SAVE DIR TO TMPFILE                          
         B     PANBLD3                                                          
*                                                                               
PANBLD4  CLOSE TMPFILE             CLOSE FILES AND EXIT                         
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ A DIRECTORY ENTRY FROM THE LIST IN THE FILE   *                   
*************************************************************                   
         SPACE 1                                                                
PANDIR   NTR1                                                                   
         GET   TMPFILE,PANCARD     READ IN DIR ENTRY                            
         MVC   PANBK,PANCARD       SET PANBK TO NAME OF BOOK READ               
         MVC   PANLEV,PANCARD+10                                                
         MVC   PANDATE,PANCARD+26                                               
         GOTO1 VDATCON,DMCB,(4,PANDATE),(0,PANDAT1)                             
         B     XIT1                                                             
*                                                                               
PANDIRX  DS    0H                  NO MORE ITEMS IN DIRECTORY                   
         GOTO1 VPANIC,DMCB,CLOSE,0,0,0                                          
         CLOSE TMPFILE                                                          
         MVI   DMCB,X'FF'          RETURN CODE: END OF PAN LIBRARY              
         XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        READ A LINE OF THE PANBOOK                         *                   
*************************************************************                   
         SPACE 1                                                                
PANLIN   NTR1                                                                   
         XC    DMCB(16),DMCB                                                    
         GOTO1 VPANIC,DMCB,READ,(C'T',PAN),PANBK,PANCARD                        
*                                                                               
         TM    DMCB+8,X'80'        END OF BOOK?                                 
         BO    XIT1                                                             
         CLI   DMCB+8,0            MAKE SURE NO ERRS RDG FILE                   
XIT1     XIT1                                                                   
         EJECT                                                                  
*************************************************************                   
*        EXTRACT LINES AND COUNT                            *                   
*************************************************************                   
         SPACE 1                                                                
EXTRACT  NTR1                                                                   
*                                                                               
         CLC   PANBK(2),=C'DC'     ALL COMMENTS ON THESE                        
         BE    ADDCOM                                                           
         CLC   PANBK(2),=C'DO'                                                  
         BE    ADDCOM                                                           
*                                                                               
EXT001   CLC   PANCARD+0(1),=C'*'  COUNT COMMENTS                               
         BE    ADDCOM                                                           
         CLC   PANCARD+9(5),=C'SPACE'                                           
         BE    ADDCOM                                                           
         CLC   PANCARD+9(5),=C'EJECT'                                           
         BE    ADDCOM                                                           
         CLC   PANCARD+9(5),=C'PRINT'                                           
         BE    ADDCOM                                                           
         CLC   PANCARD+9(7),=C'EQU   *'                                         
         BE    ADDCOM                                                           
         CLC   PANCARD+9(3),=C'ORG'                                             
         BE    ADDCOM                                                           
         CLC   PANCARD+9(7),=C'INCLUDE'                                         
         BE    ADDCOM                                                           
*                                                                               
EXT002   CLC   PANCARD+9(3),=C'DC '                                             
         BE    ADDDC                                                            
         CLC   PANCARD+9(3),=C'EQU'                                             
         BE    ADDDS                                                            
         CLC   PANCARD+9(3),=C'DS '                                             
         BE    ADDDS                                                            
         CLC   PANCARD+9(5),=C'DSECT'                                           
         BE    ADDDS                                                            
         CLC   PANCARD+0(3),=C'F  '                                             
         BE    ADDFLD                                                           
         CLC   PANCARD+0(1),=C'F'                                               
         BNE   ADDCODE                                                          
         CLI   PANCARD+3,C' '                                                   
         BE    ADDFLD              THE REST ARE CODE                            
         BNE   ADDCODE                                                          
*                                                                               
         EJECT                                                                  
*************************************************************                   
*        UPDATE COUNTERS                                    *                   
*************************************************************                   
         SPACE 1                                                                
ADDCOM   AP    BKCOMM,=P'1'                                                     
         B     EXT500                                                           
ADDDC    AP    BKDC,=P'1'                                                       
         B     EXT500                                                           
ADDDS    AP    BKDS,=P'1'                                                       
         B     EXT500                                                           
ADDFLD   AP    BKFLD,=P'1'                                                      
         B     EXT500                                                           
ADDCODE  AP    BKCODE,=P'1'                                                     
         B     EXT500                                                           
         EJECT                                                                  
*************************************************************                   
*        NON AC BOOKS                                       *                   
*************************************************************                   
         SPACE 1                                                                
EXT500   EQU   *                                                                
         B     XIT1                                                             
         EJECT                                                                  
*************************************************************                   
*        REPORT LINES                                       *                   
*************************************************************                   
         SPACE 1                                                                
T1       DS    CL166                                                            
         ORG   T1                                                               
         DC    C'1'                                                             
         DC    AL1(TL),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TM),10AL1(HB)                                                
         DC    AL1(TR)                                                          
         ORG                                                                    
*                                                                               
T2       DS    CL166                                                            
         ORG   T2                                                               
         DC    C' '                                                             
         DC    AL1(VB),CL10'BookTotal  '                                        
         DC    AL1(VB),CL10'Book Code  '                                        
         DC    AL1(VB),CL10'Book Screen'                                        
         DC    AL1(VB),CL10'Book Dsect '                                        
         DC    AL1(VB),CL10'   Code    '                                        
         DC    AL1(VB),CL10'  Fields   '                                        
         DC    AL1(VB),CL10'   DCs     '                                        
         DC    AL1(VB),CL10'   DSs     '                                        
         DC    AL1(VB),CL10' Comments  '                                        
         DC    AL1(VB)                                                          
         ORG                                                                    
*                                                                               
BK       DS    CL166                                                            
         ORG   BK                                                               
         DC    C' '                                                             
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB),10AL1(BB)                                                
         DC    AL1(VB)                                                          
         ORG                                                                    
*                                                                               
M1       DS    CL166                                                            
         ORG   M1                                                               
         DC    C' '                                                             
         DC    AL1(ML),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MM),10AL1(HB)                                                
         DC    AL1(MR)                                                          
         ORG                                                                    
*                                                                               
B1       DS    CL166                                                            
         ORG   B1                                                               
         DC    C' '                                                             
         DC    AL1(BL),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BM),10AL1(HB)                                                
         DC    AL1(BR)                                                          
         ORG                                                                    
         EJECT                                                                  
*************************************************************                   
*        BOX EQUATES                                        *                   
*************************************************************                   
         SPACE 1                                                                
TL       EQU   X'AC'               TOP LEFT                                     
TM       EQU   X'CC'               TOP MIDDLE                                   
TR       EQU   X'BC'               TOP RIGHT                                    
HB       EQU   X'BF'               HORIZONTAL BAR                               
VB       EQU   X'FA'               VERTICAL  BAR                                
ML       EQU   X'EB'               MIDDLE LEFT                                  
MM       EQU   X'8F'               MIDDLE MIDDLE                                
MR       EQU   X'EC'               MIDDLE RIGHT                                 
BL       EQU   X'AB'               BOTTOM LEFT                                  
BM       EQU   X'CB'               BOTTOM MIDDLE                                
BR       EQU   X'BB'               BOTTOM RIGHT                                 
BB       EQU   X'40'               BLANK LINE                                   
         EJECT                                                                  
*************************************************************                   
*        PRINT ROUTINES                                     *                   
*************************************************************                   
         SPACE 1                                                                
PRINTI   ST    RE,SAVERE                                                        
         OPEN  (SYSPRINT,OUTPUT)   PRINT INIT                                   
         ZAP   LINE,=P'0'                                                       
         ZAP   PAGE,=P'1'                                                       
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTT   ST    RE,SAVERE           PRINT TITLES                                 
PRINTT1  ZAP   LINE,=P'3'          RESET LINECOUNT                              
         AP    PAGE,=P'1'          BUMP PAGECOUNT                               
*                                                                               
         PUT   SYSPRINT,T1         PRINT TITLES                                 
         PUT   SYSPRINT,T2                                                      
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTM   ST    RE,SAVERE           PRINT LINE                                   
         AP    LINE,=P'1'          BUMP LINECOUNT                               
         CP    LINE,MAXLINE        TEST FOR MAX LINES                           
         BNL   PRINTT1                                                          
*                                                                               
         PUT   SYSPRINT,M1         PRINT MIDLINE                                
         L     RE,SAVERE                                                        
         BR    RE                                                               
*                                                                               
PRINTL   ST    RE,SAVERE           PRINT LINE                                   
         AP    LINE,=P'1'          BUMP LINECOUNT                               
         CP    LINE,MAXLINE        TEST FOR MAX LINES                           
         BL    PRINTL2                                                          
*                                                                               
         ZAP   LINE,=P'4'          RESET LINECOUNT                              
         AP    PAGE,=P'1'          BUMP PAGECOUNT                               
*                                                                               
         PUT   SYSPRINT,T1         PRINT TITLES                                 
         PUT   SYSPRINT,T2                                                      
         PUT   SYSPRINT,M1                                                      
*                                                                               
PRINTL2  PUT   SYSPRINT,PLINE      PRINT LINE                                   
*        MVC   PLINE,SPACES                                                     
         L     RE,SAVERE                                                        
         BR    RE                  EXIT                                         
*                                                                               
PRINTX   ST    RE,SAVERE           CLOSE PRINT                                  
         CLOSE SYSPRINT                                                         
         L     RE,SAVERE                                                        
         BR    RE                                                               
         EJECT                                                                  
************************************************************                    
*        VALIDATE PARAMETER CARDS                          *                    
************************************************************                    
*                                                                               
*        CL7'KEYWORD',AL1(KEYWRD LEN-1,OP LEN-1),X'FLAGS',AL3(OUTPUT)           
*                                                                               
*FLAGS   X'8000'                   A(OUTPUT) IS A(ROUTINE)                      
*        X'4000'                   ACCEPT =,/=                                  
*        X'2000'                   ACCEPT <,>,<=,=>                             
*        X'1000'                   HEX VALUE                                    
*        X'0800'                   DEC VALUE                                    
*        X'0400'                   OUTPUT IS A LIST                             
*        X'0200'                   TIME VALUE                                   
*                                                                               
         SPACE 1                                                                
CARDTAB  DS    0F                                                               
         DC    C'SYSTEM ',AL1(5,7),X'0000',AL3(SYSTEM)                          
         DC    C'PROGRAM',AL1(6,7),X'0000',AL3(PROGRAM)                         
         DC    X'0000'                                                          
*                                                                               
SYSTEM   DC    C'********'                                                      
PROGRAM  DC    C'********'                                                      
*                                                                               
VALCARD  NTR1                                                                   
         ST    RD,CARDRD                                                        
         LR    R2,R1                                                            
         LA    R1,79(R1)                                                        
         ST    R1,CARDEND          SAVE LAST CHR ADDR                           
         CLI   0(R2),C'*'          * IN COL 1 IS A COMMENT                      
         BE    EXITEQU                                                          
*                                                                               
VALC001  LA    R4,CARDTAB                                                       
         ST    R2,CARDR2                                                        
VALC010  SR    R1,R1               GET LEN FOR COMPARE                          
         IC    R1,7(R4)                                                         
         EX    R1,*+8              EXECUTE KEYWORD TEST                         
         B     *+10                                                             
         CLC   0(0,R2),0(R4)                                                    
         BE    VALC020                                                          
         LA    R4,14(R4)           TRY NEXT ENTRY                               
         CLI   0(R4),0                                                          
         BNE   VALC010                                                          
         B     CERRKEY             ERROR INVALID KEYWORD                        
*                                                                               
VALC020  LA    R2,1(R2,R1)         POINT TO DELIMITER                           
*                                                                               
         LA    RF,VALCDELS         DELIMITER TABLE                              
         B     *+8                                                              
VALC021  LA    RF,5(RF)                                                         
         CLI   0(RF),0                                                          
         BE    CERRDEL             END OF TABLE INVALID DELIMITER               
*                                                                               
         MVC   BYTE,4(RF)          AUTH BIT MUST BE ON                          
         CLI   BYTE,0              EXCEPT WHEN ZERO                             
         BE    *+14                                                             
         NC    BYTE,9(R4)                                                       
         BZ    VALC021                                                          
*                                                                               
         SR    R1,R1                                                            
         IC    R1,2(RF)            GET EX LEN                                   
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),0(RF)       TEST DELIMITERS                              
         BNE   VALC021                                                          
*                                                                               
         MVC   BYTE,3(RF)          SAVE COMPARE CHR                             
         LA    R2,1(R1,R2)                                                      
         B     VALC025                                                          
*                                                                               
VALCDELS DC    C'= ',AL1(0),X'80',X'00'                                         
         DC    C'>=',AL1(1),X'B0',X'20'                                         
         DC    C'<=',AL1(1),X'D0',X'20'                                         
         DC    C'/=',AL1(1),X'70',X'40'                                         
         DC    C'< ',AL1(0),X'40',X'20'                                         
         DC    C'> ',AL1(0),X'20',X'20'                                         
         DC    X'00'                                                            
*                                                                               
VALC025  LR    R1,R2               GET LEN FOR MOVE                             
VALC026  CLI   0(R1),C','                                                       
         BE    VALC030                                                          
         CLI   0(R1),C' '                                                       
         BE    VALC030                                                          
         CLI   0(R1),0                                                          
         BE    VALC030                                                          
         LA    R1,1(R1)                                                         
         B     VALC026                                                          
*                                                                               
VALC030  SR    R1,R2                                                            
*                                                                               
VALC031  BCTR  R1,0                                                             
         SR    RF,RF                                                            
         ICM   RF,7,11(R4)         GET ADDRESS FOR MOVE                         
*                                                                               
         TM    9(R4),X'80'         IF ROUTINE                                   
         BZ    *+10                                                             
         BASR  RE,RF               GOTO ROUTINE                                 
         B     VALC090                                                          
*                                                                               
         TM    9(R4),X'04'         IF LIST                                      
         BNO   VALC050                                                          
VALC040  CLI   0(RF),X'FF'         CHECK NOT FULL                               
         BE    CERRMAN                                                          
         CLI   0(RF),0             EMPTY ENTRY                                  
         BE    VALC050                                                          
         SR    R0,R0                                                            
         IC    R0,8(R4)                                                         
         AR    RF,R0                                                            
         TM    9(R4),X'60'         /<=>                                         
         BZ    VALC040                                                          
         LA    RF,1(RF)            ONE MORE FOR CODE                            
         B     VALC040                                                          
*                                                                               
VALC050  TM    9(R4),X'60'         IF /<=>                                      
         BZ    *+14                                                             
         MVC   0(1,RF),BYTE        SAVE COMP CODE                               
         LA    RF,1(RF)                                                         
*                                                                               
         TM    9(R4),X'10'         HEX INPUT                                    
         BNO   VALC060                                                          
         LA    R0,1(R1)            SET R0 HEX INPUT LEN                         
         GOTO1 =V(HEXIN),DMCB,(R2),(RF),(R0)                                    
         ICM   R1,15,12(R1)                                                     
         BZ    CERRHEX                                                          
         B     VALC090                                                          
*                                                                               
VALC060  TM    9(R4),X'08'         DEC INPUT                                    
         BZ    VALC070                                                          
         LR    R4,R2                                                            
         LA    R3,1(R1)                                                         
         DC    H'0'                NO-OP                                        
*        BAS   RE,VALNUM           VALIDATE NUMBER                              
         CLI   DUB,X'FF'                                                        
         BE    CERRDEC                                                          
         CVB   R1,DUB                                                           
         STH   R1,0(RF)            SAVE HALFWORD (DEFAULT)                      
         B     VALC090                                                          
*                                                                               
VALC070  TM    9(R4),X'02'         TIME INPUT                                   
         BZ    VALC080                                                          
         DC    H'0'                NO-OP                                        
*        BAS   RE,VALTIME                                                       
         MVC   0(4,RF),FULL                                                     
         B     VALC090                                                          
*                                                                               
VALC080  CLI   8(R4),0             DONT CARE                                    
         BE    *+12                                                             
         CLM   R1,1,8(R4)          CHECK MAX LEN                                
         BNL   CERRMAX                                                          
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R2)       MOVE TO OUTPUT AREA                          
*                                                                               
VALC090  CLI   0(R2),C','          TEST FOR ANOTHER                             
         LA    R2,1(R2)                                                         
         BE    VALC001             GO FIND TABLE ENTRY                          
         C     R2,CARDEND          TEST FOR END OF CARD                         
         BL    VALC090                                                          
*                                                                               
EXITEQU  CR    RB,RB               SET CC EQU                                   
         B     EXIT                                                             
*                                                                               
         EJECT                                                                  
CERRSYS  LA    R1,=C'INVALID SYSTEM  '                                          
         B     CERRX                                                            
CERRPRG  LA    R1,=C'INVALID PROGRAM '                                          
         B     CERRX                                                            
CERRSES  LA    R1,=C'INVALID SESYS   '                                          
         B     CERRX                                                            
CERRTIM  LA    R1,=C'INVALID TIME    '                                          
         B     CERRX                                                            
CERRDEC  LA    R1,=C'MUST BE HEX     '                                          
         B     CERRX                                                            
CERRHEX  LA    R1,=C'MUST BE DECIMAL '                                          
         B     CERRX                                                            
CERRKEY  LA    R1,=C'INVALID KEYWORD '                                          
         B     CERRX                                                            
CERRDEL  LA    R1,=C'INVALID DELIMITR'                                          
         B     CERRX                                                            
CERRMAX  LA    R1,=C'VALUE TOO LONG  '                                          
         B     CERRX                                                            
CERRMAN  LA    R1,=C'TOO MANY FILTERS'                                          
         B     CERRX                                                            
*                                                                               
CERRX    L     RD,CARDRD                                                        
         L     R2,CARDR2                                                        
         LA    RF,PLINE+1                                                       
CERRX1   MVC   0(1,RF),0(R2)                                                    
         CLI   0(RF),C' '                                                       
         BE    CERRX2                                                           
         CLI   0(RF),C','                                                       
         BE    CERRX2                                                           
         LA    R2,1(R2)                                                         
         LA    RF,1(RF)                                                         
         B     CERRX1                                                           
*                                                                               
CERRX2   LA    RF,1(RF)                                                         
         MVC   0(13,RF),=C'*** ERROR ***'                                       
         LA    RF,14(RF)                                                        
         MVC   0(16,RF),0(R1)                                                   
         BAS   RE,PRINTL                                                        
*                                                                               
EXITNEQ  LTR   RB,RB               SET CC NEQ                                   
         B     EXIT                                                             
         EJECT                                                                  
*************************************************************                   
*        ADDCONS                                            *                   
*************************************************************                   
         SPACE 1                                                                
VPANIC   DC    V(PANIC)                                                         
VSORTER  DC    V(SORTER)                                                        
VDATCON  DC    V(DATCON)                                                        
         EJECT                                                                  
*************************************************************                   
*        CONSTANTS TABLES AND DCBS                          *                   
*************************************************************                   
         SPACE 1                                                                
TMPFILE  DCB   DDNAME=TMPFILE,DSORG=PS,RECFM=FB,MACRF=(GM,PM),         X        
               BLKSIZE=3120,LRECL=80,EODAD=PANDIRX                              
PANFILE  DCB   DDNAME=PANFILE,DSORG=PS,RECFM=FB,MACRF=(PM),            X        
               BLKSIZE=3120,LRECL=80                                            
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=(166)          
*                                                                               
*        NOTE  RECORD LEN=166 WITH CHARS=(BX15)                                 
*                                                                               
*                                                                               
PUT      DC    CL8'PUT'                                                         
GET      DC    CL8'GET'                                                         
CLOSE    DC    CL8'CLOSE'                                                       
READ     DC    CL8'READ'                                                        
PAN      DC    CL8'PAN'                                                         
DIR      DC    CL8'DIR'                                                         
SPACES   DC    166C' '                                                          
MAXLINE  DC    PL4'60'                                                          
         EJECT                                                                  
*************************************************************                   
*        WORKING STORAGE                                    *                   
*************************************************************                   
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FLAG     DS    X                                                                
DMCB     DS    6F                                                               
CARDRD   DS    A                                                                
SAVERE   DS    A                                                                
CARDR2   DS    A                                                                
*                                                                               
PLINE    DS    CL166                                                            
SAVELINE DS    CL166                                                            
*                                                                               
WORK     DS    CL166                                                            
*                                                                               
CARDEND  DS    F                                                                
*                                                                               
LINE     DS    PL4                                                              
PAGE     DS    PL4                                                              
*                                                                               
SYSNOW   DS    CL2                                                              
PROGNOW  DS    CL5                                                              
*                                                                               
PANINF   DS    0CL28                                                            
PANBK    DS    CL10                                                             
PANLEV   DS    CL3                                                              
PANDATE  DS    CL8                                                              
PANDAT1  DS    CL7                                                              
CSECTSV  DS    CL8                                                              
*                                                                               
PANCARD  DS    CL80                                                             
SAVECARD DS    CL160                                                            
CARD     DS    CL80                                                             
*                                                                               
SCCODE   DS    PL4                 SYSTEM COUNTERS                              
SCFLD    DS    PL4                                                              
SCDC     DS    PL4                                                              
SCDS     DS    PL4                                                              
SCCOMM   DS    PL4                                                              
*                                                                               
PGCODE   DS    PL4                 PROGRAM COUNTERS                             
PGFLD    DS    PL4                                                              
PGDC     DS    PL4                                                              
PGDS     DS    PL4                                                              
PGCOMM   DS    PL4                                                              
*                                                                               
BKCODE   DS    PL4                 BOOK COUNTERS                                
BKFLD    DS    PL4                                                              
BKDC     DS    PL4                                                              
BKDS     DS    PL4                                                              
BKCOMM   DS    PL4                                                              
*                                                                               
BOOKCNT  DS    PL4                                                              
BOOKCOD  DS    PL4                                                              
BOOKSCR  DS    PL4                                                              
BOOKDSE  DS    PL4                                                              
*                                                                               
IOAREA   DS    4096C                                                            
*                                                                               
WORKX    EQU   *                                                                
         EJECT                                                                  
************************************************************                    
*        OTHER DSECTS                                      *                    
************************************************************                    
PLINED   DSECT                                                                  
         DS    CL1                                                              
         DS    CL1                                                              
PLBOOK   DS    CL10                                                             
         DS    CL1                                                              
PLCOD    DS    CL10                                                             
         DS    CL1                                                              
PLSCR    DS    CL10                                                             
         DS    CL1                                                              
PLDSE    DS    CL10                                                             
         DS    CL1                                                              
PLCODE   DS    CL10                                                             
         DS    CL1                                                              
PLFLD    DS    CL10                                                             
         DS    CL1                                                              
PLDC     DS    CL10                                                             
         DS    CL1                                                              
PLDS     DS    CL10                                                             
         DS    CL1                                                              
PLCOMM   DS    CL10                                                             
         DS    CL1                                                              
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'027DDCCPAN2  08/18/06'                                      
         END                                                                    
