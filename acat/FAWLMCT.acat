*          DATA SET FAWLMCT    AT LEVEL 001 AS OF 04/26/10                      
*CATALP FAWLMCT                                                                 
         TITLE 'WLMCT - INTERFACE TO IBM WLM CONNECT SERVICE'                   
********************************************************************            
* FACPACK INTERFACE TO WORKLOAD MANAGER CONNECT SERVICE                         
* MODULE: FAWLMCT                                                               
*                                                                               
* PROPERTIES:                                                                   
*    PROBLEM STATE                                                              
*    ADDRESSING MODE= 31                                                        
*    PRIMARY ASC MODE SAC 0                                                     
*                                                                               
* INVOCATION:                                                                   
*        SAM31                                                                  
*        SAC   0                                                                
*        GOTO1 VWLMCT,WLMPARM,C'WLMC'                                           
*                                                                               
* PROCESSING:                                                                   
*   USNG THE SUBSYSTEM TYPE AND SUBSYSTEM NAME PASSED BY THE                    
*   CALLING ROUTINE, ISSUE AN IWM4CONN MACRO TO ESTABLISH                       
*   CONNECTIVITY TO WORK LOAD MANAGER.                                          
* - PRESERVE STANDARD DDS LINKAGE (REGISTER SAVE AREA CHAINING)                 
* - UPON ENTRY REGISTER 1 CONTAINS THE ADDRESS OF THE PARAMETER                 
*   REQUEST BLOCK (WLMPARM).                                                    
* - ESTABLISH REGISTER 7 AS THE REQUEST BLOCK BASE REGISTER                     
* - SWITCH TO SUPERVISOR STATE                                                  
* - PRESERVE THE PSW KEY WE ARE RUNNING UNDER                                   
* - ISSUE IWM4CONN WITH SUBSYSTEM TYPE, SUBSYSTEM NAME, AND                     
* - CURRENT PSW KEY.                                                            
* - UPON RETURN FROM IWM4CONN, WE MUST PRESERVE THE CONNECTION                  
*   TOKEN (WLMTOKEN).                                                           
* - SWITCH BACK TO PROBLEM STATE                                                
* - RETURN TO CALLER                                                            
*                                                                               
* The purpose of this routine is to connect a calling address                   
* space to WLM.  This service returns a token which is needed                   
* to invoke other services.                                                     
*                                                                               
*                                                                               
********************************************************************            
         USING WLMD,R7            INFORM ASSEMBLER                              
FAWLMCT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**WLMCT                                                        
         LR   R7,R1               PARAMETER LIST ADDRESS                        
         XR   R0,R0                                                             
         ST   R0,WLMTOKEN         INITIALIZE CONNECTION TOKEN                   
         ST   R0,WLMCONKEY        CLEAR CURRENT PSW KEY                         
         ST   R0,WLMECLKEY        CLEAR ENCLAVE PSW KEY                         
         ST   R0,WLMSRVCL         Clear Service Class Token                     
*                                                                               
         MVC  MSG3T+25(04),WLMSYSTYP     subsystem type                         
         MVC  MSG4T+25(08),WLMSYSNAM     subsystem name                         
         XR   R0,R0               SET MULTI LINE INDICATOR                      
         WTO  TEXT=((MSG2,D),(MSG3,D),(MSG4,DE))                                
*                                                                               
*  Switch to supervisor state                                                   
*  Preserve the PSW key we are running under                                    
*                                                                               
         MODESET MODE=SUP          SWITCH TO SUPERVISOR STATE                   
         MODESET SAVEKEY=WLMCKEY,WORKREG=1   SAVE PSW KEY                       
*                                                                               
         IWM4CON WORK_MANAGER=YES,                                    XX        
               CONNTKN=WLMTOKEN,                                      XX        
               CONNTKNKEYP=VALUE,                                     XX        
               CONNTKNKEY=KEY0,                                       XX        
               EXPTIMPT=NO,                                           XX        
               QUEUE_MANAGER=NO,                                      XX        
               ROUTER=NO,                                             XX        
               SERVER_MANAGER=NO,                                     XX        
               SUBSYS=WLMSYSTYP,                                      XX        
               SUBSYSNM=WLMSYSNAM,                                    XX        
               PLISTVER=IMPLIED_VERSION,                              XX        
               RETCODE=WLMRETC,                                       XX        
               RSNCODE=WLMRSNC,                                       XX        
               MF=S                                                             
*                                                                               
         MODESET MODE=PROB         Switch back to problem state                 
*                                                                               
         LM    RF,R0,WLMRETC        GET RETURN AND REASON CODE                  
         LTR   RF,RF                RETURN CODE OK ?                            
         BNZ   ABENDIT              NO, ABORT                                   
         LTR   R0,R0                REASON CODE OK ?                            
         BNZ   ABENDIT              NO, ABORT                                   
         WTO   'FAWLMCT connected okay'                                         
         J     EXIT                                                             
*                                                                               
ABENDIT  DS    0H                                                               
         STM   R0,RF,DUMPREGS                                                   
         WTO   'FAWLMCT FAILED 910'                                             
         ABEND 910,DUMP                                                         
                                                                                
EXIT     XMOD1 1                                                                
         LTORG ,                  GENERTE LOCAL LITTERL POOL                    
         EJECT ,                                                                
***********************************************************************         
* Constants                                                                     
***********************************************************************         
KEY0     DC    X'00'                                                            
         DS    0D                                                               
         DC    C'DUMPREGS'                                                      
DUMPREGS DC    18A(0)                                                           
*                                                                               
         DS    0D                  ALIGNMENT                                    
MSG2     DC    AL2(L'MSG2T)                                                     
MSG2T    DC    C'FAWLMCT - CONNECTING TO WLM AS'                                
MSG3     DC    AL2(L'MSG3T)                                                     
MSG3T    DC    C'        - SUBSYSTYPE     XXXX '                                
MSG4     DC    AL2(L'MSG4T)                                                     
MSG4T    DC    C'        - SUBSYSTEM NAME XXXXXXXX '                            
*                                                                               
********************************************************************            
*  DDS WLM SERVICES REQUEST BLOCK                                  *            
********************************************************************            
* FAWLMD                                                                        
       ++INCLUDE FAWLMD                                                         
*                                                                               
* CVT                                                                           
         CVT DSECT=YES                                                          
* IWMYCON                                                                       
         IWMYCON                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001FAWLMCT   04/26/10'                                      
         END                                                                    
