*          DATA SET DDMINIO    AT LEVEL 060 AS OF 01/07/11                      
*CATALP MINIO                                                                   
MINIO    TITLE '- MULTI-RECORD, KEYED ELEMENT BUFFER MAINTENANCE'               
MINIO    RSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**MINI**,R9,CLEAR=YES                                      
         USING WORKD,RC                                                         
         MVC   PARAMS(PARAMSL),0(R1)                                            
         SR    RA,RA                                                            
         ICM   RA,7,PABLOCK                                                     
         USING MINBLKD,RA                                                       
         OC    MINREGD,MINREGD     TEST RD SET IN BLOCK                         
         BNZ   *+10                                                             
         MVC   MINREGD,4(RD)       NO - SAVE CALLER'S RD                        
*                                                                               
         SAM31 ,                                                                
         CLI   PCOMMAND,CMNDLSTN   TEST COMMAND NUMBER IN RANGE                 
         BNH   *+6                                                              
         DC    H'0'                                                             
         SR    RF,RF                                                            
         ICM   RF,1,PCOMMAND                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MHI   RF,L'CMNDLST                                                     
         LA    RE,CMNDLST-L'CMNDLST(RF)                                         
         MVC   COMMAND,0(RE)       SET INTERNAL COMMAND CODE                    
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,1(RE)                                                       
         LA    RF,MINIO(RF)                                                     
*                                                                               
         BRAS  RE,INIT             DO INITIALIZATION                            
         BNE   EXIT                                                             
*                                                                               
         ST    RF,SVRF             RF = Routine to run                          
         GOTO1 VPROTOFF            Clobbers RF                                  
         L     RF,SVRF             Restores RF                                  
         BASR  RE,RF               Do command                                   
         IPM   R0                  Save condition code                          
         GOTO1 VPROTON                                                          
         SPM   R0                  Restore condition code                       
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* INITIALISATION                                                      *         
***********************************************************************         
                                                                                
INIT     NTR1  ,                                                                
         MVI   MINUPDT,0           SET NO UPDATE HAS TAKEN PLACE                
         MVI   MINERR,0            RESET ERROR CODE                             
*                                                                               
         MVC   AREC,MINBUFF        SET I/O AREA TO USER BUFFER                  
         MVC   RECUP,MINRECUP                                                   
*                                                                               
         USING COMFACSD,RF                                                      
         L     RF,MINCOMF          GET COMFACS ADDRESSES                        
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   VPROTON,CPROTON                                                  
         MVC   VPROTOFF,CPROTOFF                                                
         DROP  RF                                                               
*                                                                               
         MVI   DMIBTS,X'08'        SET TO PASS DELETES                          
         MVI   DMOBTS,X'FD'                                                     
*                                                                               
         LHI   R0,KEYMAX           USE MAX # BUFFERS IN NORMAL MODE             
         STH   R0,BUFFCNT                                                       
         LH    RF,MINFRCLM                                                      
         ST    RF,BUFFLEN                                                       
*                                                                               
         CLI   MINMODE,C'B'        IF BUFFER MODE                               
         BNE   INIT02                                                           
*                                                                               
         MVI   MINRDUP,C'N'        SET OFF UPDATE SWITCH                        
         LHI   R0,1                                                             
         STH   R0,BUFFCNT          SET TO 1 BUFFER                              
         XR    RF,RF               SET LENGTH OF A SINGLE BUFFER                
         ICM   RF,7,MINFRCM3                                                    
         C     RF,SIZECAP          CAP AT 1/2 A MEG                             
         BNH   *+8                                                              
         L     RF,SIZECAP                                                       
         ST    RF,BUFFLEN                                                       
*                                                                               
INIT02   GOTO1 DATAMGR,DMCB,SSBAD,0,0,TO24=Y                                    
         SAM31 ,                                                                
         ICM   R2,15,4(R1)         GET SSB                                      
         BNZ   *+6                                                              
         DC    H'0'                WHITHER SSB?                                 
*                                                                               
         USING SSBD,R2                                                          
         ICM   RF,3,SSBCNTL        TEST ONLINE OR OFFLINE                       
         BZ    INIT04              OFFLINE                                      
*                                                                               
         MVI   OFFLINE,C'N'        ONLINE TRANSACTION                           
         ICM   RF,15,SSBTKADR                                                   
         BNZ   *+6                                                              
         DC    H'0'                REQUIRE A TASK                               
         XR    R2,R2                                                            
         DROP  R2                                                               
*                                                                               
         USING TCBD,RF                                                          
         ICM   R1,15,TCBMINIO                                                   
         BNZ   *+6                                                              
         DC    H'0'                INCOMPATIBLE FACPAK/MINIO VERSION            
*                                                                               
         CLI   MINBF2,C'Y'         SECOND BUFFER REQUIRED?                      
         BNE   *+8                 NO                                           
         ICM   R1,15,TCBMINI2                                                   
*                                                                               
         ST    R1,AKEYMTRX         SET A(KEY MATRIX)                            
         A     R1,LKEYS                                                         
         ST    R1,ARECMTRX         SET A(RECORD MATRIX)                         
         MVC   MYSIN,TCBSIN                                                     
         B     INIT08                                                           
*                                                                               
INIT04   MVI   OFFLINE,C'Y'        OFFLINE TRANSACTION                          
         MVC   MYSIN,EFFS                                                       
         LA    RF,RETURN           Dummy return                                 
         ST    RF,VPROTOFF         Just return from call                        
         ST    RF,VPROTON          Just return from call                        
*                                                                               
         CLC   XABUFF,NULLS        GET XA BUFFER VIA A GETMAIN                  
         BNE   INIT05              IF NOT ALREADY THERE                         
         GOTO1 =A(GETMAIN)                                                      
*                                                                               
INIT05   CLI   MINBF2,C'Y'         SECOND BUFFER REQUIRED?                      
         BE    INIT06                                                           
*                                                                               
         L     RF,XABUFF                                                        
         ST    RF,AKEYMTRX         SET A(KEY MATRIX)                            
         A     RF,LKEYS                                                         
         ST    RF,ARECMTRX         SET A(RECORD MATRIX)                         
         B     INIT08                                                           
*                                                                               
INIT06   L     RF,XABUFFL          SECOND BUFFER                                
         SRL   RF,1                                                             
         A     RF,XABUFF                                                        
         ST    RF,AKEYMTRX         SET A(KEY MATRIX)                            
         A     RF,LKEYS                                                         
         ST    RF,ARECMTRX         SET A(RECORD MATRIX)                         
*                                                                               
INIT08   CLI   COMMAND,OPEN        TEST FORCE TO OPEN                           
         BE    INIT10                                                           
         CLI   COMMAND,CLOSE       TEST FORCE TO CLOSE                          
         BNE   *+12                                                             
         CLI   MINOPEN,C'Y'        TEST ALREADY OPENED                          
         BNE   EXITL               NO - THEN BUGGER OFF                         
*                                                                               
         GOTOR VPROTOFF                                                         
         L     R2,AKEYMTRX                                                      
         USING KEYHDRD,R2                                                       
         LH    RF,MINMKLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8              CHECK MASTER KEY IS STILL THE SAME           
         BNE   INIT10              NO - REBUILD TABLES                          
         CLC   KEYHKEY(0),MINMKEY                                               
*                                                                               
         CLC   KEYHSIN,MYSIN       TEST SAME SIN (ONLINE ONLY REALLY)           
         BNE   INIT10                                                           
*                                                                               
         CLI   MINOPEN,C'Y'        Test already opened                          
         BNE   INIT10              No                                           
         GOTOR VPROTON                                                          
         B     EXITOK              Yes                                          
*                                                                               
INIT10   GOTOR VPROTON                                                          
         BRAS  RE,OPENSET          SET MINWORK VARIABLES                        
         BRAS  RE,BLDKEYS          Build keys                                   
         BNE   EXITL                                                            
         BRAS  RE,BLDRECS          Build records                                
         BNE   EXITL               (Don't see how you get this error)           
*                                                                               
         CLI   COMMAND,OPEN        Test force to open                           
         BNE   EXITOK                                                           
         BRAS  RE,TSTSET           Set weather there or not                     
         B     EXIT                                                             
*                                                                               
RETURN   BR    RE                  Just returns to caller                       
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET MINWORK VARIABLES ON NEW OPEN                        *         
***********************************************************************         
                                                                                
OPENSET  NTR1  ,                                                                
         XC    MINWORK,MINWORK                                                  
*                                                                               
         LHI   R0,KEYMAX           USE MAX # BUFFERS IN NORMAL MODE             
         STH   R0,BUFFCNT                                                       
         LH    RF,MINFRCLM                                                      
         ST    RF,BUFFLEN                                                       
*                                                                               
         CLI   MINMODE,C'B'        IF BUFFER MODE                               
         BNE   OSET02                                                           
*                                                                               
         MVI   MINRDUP,C'N'        SET OFF UPDATE SWITCH                        
         LHI   R0,1                                                             
         STH   R0,BUFFCNT          SET TO 1 BUFFER                              
         XR    RF,RF               SET LENGTH OF A SINGLE BUFFER                
         ICM   RF,7,MINFRCM3                                                    
         C     RF,SIZECAP          CAP AT 1/2 A MEG                             
         BNH   *+8                                                              
         L     RF,SIZECAP                                                       
         ST    RF,BUFFLEN                                                       
*                                                                               
OSET02   XR    RF,RF                                                            
         IC    RF,MINEKDSP         DISP INTO FILE KEY OF ELEM KEY               
         STH   RF,MINMKLEN         = MASTER KEY LENGTH                          
         IC    RF,MINFKLEN         FILE KEY LENGTH                              
         STH   RF,MINDCDSP         = DISP IN DIRECTORY TO CTL BYTES             
         XR    RE,RE                                                            
         IC    RE,MINNCTL          + NUMBER OF CONTROL BYTES                    
         AR    RF,RE                                                            
         STH   RF,MINDADSP         = DISP IN DIR TO DISK ADDRESS                
*                                                                               
         IC    RF,MINFKLEN         FILE KEY LENGTH                              
         AHI   RF,2                +2 = DISP IN FILE TO CTL BYTES               
         STH   RF,MINFCDSP                                                      
*                                                                               
         IC    RE,MINNCTL          + NUMBER OF CONTROL BYTES                    
         LA    RF,4(RE,RF)         + 4 FOR LINK                                 
         CLC   =C'SPT',MINFIL                                                   
         BNE   *+8                 + 4 FOR SPTFIL                               
         AHI   RF,4                                                             
         STH   RF,MINELDSP         = DISP IN REC TO FIRST ELEM                  
*                                                                               
         MVC   RCUFILD(2),MINELDSP SET RECUP FILE DEF PARM                      
         MVI   RCUFILD+2,0                                                      
         MVC   RCUFILD+3(1),MINFKLEN                                            
         XR    RF,RF                                                            
         ICM   RF,3,MINFRCLM       MAX RECORD LENGTH                            
         SHI   RF,3                LESS 3 TO ALLOW FOR DMGR CLEARED             
         STCM  RF,3,RCUFILD+4      BYTES AT END OF REC                          
         MVI   RCUMODE,X'FE'       NORMAL 2-BYTE MODE                           
*                                                                               
         CLI   MINMODE,C'B'            IF BUFFER MODE                           
         BNE   *+14                                                             
         MVC   RCUFILD+4(3),MINFRCM3   USE 3-BYTE MAX LENGTH                    
         MVI   RCUMODE,X'FD'           AND SET 3-BYTE MODE                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO READ AND STORE ELEMENT KEYS UP FRONT                     *         
***********************************************************************         
                                                                                
BLDKEYS  NTR1  ,                                                                
         GOTOR VPROTOFF                                                         
         MVI   HAVDEL,C'N'         SET NO DELETED RECORDS FOUND                 
         MVI   HAVFF,C'N'          SET NO FF RECORD FOUND                       
*                                                                               
         L     R0,AKEYMTRX         CLEAR THE STUPID THING                       
         L     R1,LKEYS                                                         
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R2,AKEYMTRX                                                      
         USING KEYHDRD,R2                                                       
         MVC   KEYEYE+00(08),=CL8'*MINBLK*'                                     
         CLI   MINBF2,C'Y'         SECOND BUFFER?                               
         BNE   *+10                NO                                           
         MVC   KEYEYE+00(08),=CL8'*MINBLK2'                                     
         MVC   KEYEYE+08(24),KEYEYE                                             
*                                                                               
         LH    RF,MINMKLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   KEYHKEY(0),MINMKEY  SET MASTER KEY                               
*                                                                               
         MVC   KEYHSIN,MYSIN                                                    
         MVI   MINOPEN,C'Y'        SET OPENED                                   
*                                                                               
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
         XR    R3,R3               RECORD COUNTER                               
*                                                                               
         CLI   MINMODE,C'B'        IF BUFFER MODE                               
         BE    BKEY10              SKIP FILE READING                            
*                                                                               
         XC    KEY,KEY                                                          
         LH    RF,MINMKLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,BKMVC                                                         
         BRAS  RE,HIGH             READ FIRST RECORD                            
         B     BKEY04                                                           
*                                                                               
BKMVC    MVC   KEY(0),MINMKEY      SET MASTER KEY                               
*                                                                               
BKEY02   BRAS  RE,SEQ                                                           
*                                                                               
BKEY04   LH    RF,MINMKLEN         TEST PART OF THIS KEYSET                     
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         BNE   BKEY06              NO - FINISHED                                
         CLC   KEY(0),KEYSAVE                                                   
*                                                                               
         CLI   MINDELSW,C'Y'       WANT DELETED RECORD SET SWITCH ON?           
         BE    *+12                YES                                          
         TM    DMSTAT,X'80'        TEST DELETED                                 
         BO    BKEY02              YES                                          
*                                                                               
         AHI   R3,1                ONE MORE KEY                                 
         CHI   R3,KEYMAX           TEST TOO MANY RECORDS                        
         BNH   *+12                                                             
         MVI   MINERR,MINETOVF     TABLE OVERFLOW                               
         B     BKEYERR                                                          
*                                                                               
         LA    RF,KEY              SET ELEMENT KEY FROM POINTER                 
         AH    RF,MINMKLEN         DISP TO ELEM KEY                             
         XR    RE,RE                                                            
         IC    RE,MINEKLEN         ELEM KEY LENGTH                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   KEYMKEY(0),0(RF)    SAVE ELEMENT KEY                             
*                                                                               
         CLI   KEYMKEY,X'FF'       FF (HIGH) KEY FOUND?                         
         BNE   BKEY05              NO                                           
         MVI   HAVFF,C'Y'          SET GOT X'FF' ELEMENT                        
         MVC   MNFFSTAT,DMSTAT                                                  
         MVC   MINMKEY,KEY         SET KEY OF FF REC IN MASTER KEY              
*                                                                               
BKEY05   MVC   KEYMDA,DMDA         SAVE D/A                                     
*                                                                               
         CLI   HAVFF,C'Y'          SET GOT X'FF' ELEMENT                        
         BNE   BKEY05A                                                          
         TM    DMSTAT,X'80'        TEST DELETED                                 
         BZ    *+8                                                              
         MVI   HAVDEL,C'Y'                                                      
*                                                                               
BKEY05A  TM    DMSTAT,X'80'        TEST DELETED                                 
         BZ    *+8                 NO                                           
         OI    KEYMSTA,KEYMSDEL    SAVE DELETED STATUS                          
         AHI   R2,KEYMTRXL         NEXT TABLE ENTRY                             
         B     BKEY02              NEXT RECORD                                  
*                                                                               
BKEY06   STH   R3,MINNRECS         SET RECORD COUNT                             
         NI    MINSTAT,X'FF'-MINDELQ                                            
         CLI   HAVDEL,C'Y'         DELETED RECORDS FOUND?                       
         BNE   *+8                                                              
         OI    MINSTAT,MINDELQ                                                  
         LTR   R3,R3               TEST ANY RECORDS                             
         BZ    BKEY10              NO - NEW SET                                 
*                                                                               
         CLI   MINDELSW,C'Y'       WANT DELETED RECORD SET SWITCH ON?           
         BNE   BKEY08              NO - MORE TESTS NEEDED                       
*                                                                               
         CLI   HAVFF,C'Y'          YES - ONLY REQUIRE FF KEY PRESENT            
         BE    BKEYOKAY                                                         
         MVI   MINERR,MINESNF      ELSE RETURN SET NOT FOUND                    
         B     BKEYERR                                                          
*                                                                               
BKEY08   CLI   HAVFF,C'Y'          FF RECORD FOUND?                             
         BE    *+12                YES                                          
         MVI   MINERR,MINESNF      RETURN NOT FOUND                             
         B     BKEYERR                                                          
*                                                                               
         TM    MNFFSTAT,X'80'      FF RECORD DELETED?                           
         BZ    BKEYOKAY            NO  - THAT'S GOOD                            
         MVI   MINERR,MINESNF      YES - RETURN SET NOT FOUND                   
         B     BKEYERR                                                          
*                                                                               
BKEY10   XR    RE,RE                                                            
         IC    RE,MINEKLEN         ELEM KEY LENGTH                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   KEYMKEY(0),EFFS     SET FF KEY                                   
         LHI   R0,1                                                             
         STH   R0,MINNRECS         SET 1 RECORD IN THIS SET                     
         B     BKEYOKAY                                                         
*                                                                               
BKEYERR  L     R2,AKEYMTRX                                                      
         USING KEYHDRD,R2                                                       
         MVC   KEYHKEY,BKEYFMSG    OUTPUT IDENTIFER FOR BUILD FAIL              
         GOTOR VPROTON                                                          
         B     EXITL               (SO KEYS ARE REBUILT ON NEXT CALL)           
         DROP  R2                                                               
*                                                                               
BKEYOKAY GOTOR VPROTON                                                          
         B     EXITOK                                                           
*                                                                               
BKEYFMSG DC    8X'FF',CL48'LAST BUILD FAILED'                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO BUILD BUFFERS UPFRONT USING KEY MATRIX                   *         
***********************************************************************         
                                                                                
BLDRECS  NTR1  ,                                                                
         GOTOR VPROTOFF                                                         
*                                                                               
         L     R2,AKEYMTRX                                                      
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
         L     R3,ARECMTRX                                                      
*                                                                               
BREC02   ST    R3,CURBUFA          SET CURRENT BUFFER                           
         STCM  R3,15,KEYMADR       SET ADDRESS OF THIS BUFFER                   
         OC    KEYMDA,KEYMDA       TEST RECORD ON DISK                          
         BNZ   BREC04              YES                                          
*                                                                               
         BRAS  RE,INIBUFF          INITIALISE BUFFER                            
         BRAS  RE,FNDREC           COPY RECORD BACK TO I/O AREA                 
         B     BREC06                                                           
*                                                                               
BREC04   MVC   DMDA,KEYMDA         GET RECORD INTO I/O AREA                     
         BRAS  RE,GETREC                                                        
*                                                                               
         MVI   MINNEW,C'N'         SET OFF NEW RECORD SWITCH                    
         L     R0,CURBUFA          MOVE RECORD INTO BUFFER                      
         L     R1,BUFFLEN                                                       
         L     RE,AREC                                                          
         L     RF,BUFFLEN                                                       
         MVCL  R0,RE                                                            
*                                                                               
BREC06   A     R3,BUFFLEN          NEXT BUFFER                                  
         ST    R3,BUFFNXT                                                       
         CLI   KEYMKEY,X'FF'       TEST IF LAST ONE                             
         BE    BRECOKAY            YES                                          
         AHI   R2,KEYMTRXL         DO NEXT ONE THEN                             
         B     BREC02                                                           
*                                                                               
BRECOKAY GOTOR VPROTON                                                          
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* MNREAD - READ COMMAND PROCESSING                                    *         
***********************************************************************         
                                                                                
MNREAD   NTR1  ,                                                                
         XC    CURVALS,CURVALS     CLEAR VALUES FOR CURRENT RECORD              
         BRAS  RE,TSTSET           TEST WHETHER SET EXISTS                      
         BNE   EXITL                                                            
*                                                                               
         BRAS  RE,SRCHTAB          SEARCH TABLE FOR RECORD                      
         BNE   EXITL                                                            
*                                                                               
         MVI   SMODE,C'E'          SET SEARCH MODE EQUAL                        
         BRAS  RE,SRCHREC          SEARCH RECORD FOR ELEM                       
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MNSEQ - SEQ COMMAND PROCESSING                                      *         
***********************************************************************         
                                                                                
MNSEQ    NTR1  ,                                                                
         ICM   R4,15,CURNEXT       CURRENT NEXT ELEM                            
         BNZ   *+6                                                              
         DC    H'0'                INVALID COMMAND SEQUENCE                     
*                                                                               
MNSQ02   CLI   0(R4),0             EOR                                          
         BE    MNSQ04              YES- TRY FOR ANOTHER RECORD                  
         BRAS  RE,TSTFILT          FILTER                                       
         BE    *+12                                                             
         MVI   MINERR,MINEEOF      END OF FILE                                  
         B     EXITL                                                            
*                                                                               
         BRAS  RE,SETMIN           SET CLUSTER IN MINELEM                       
         B     EXIT                                                             
*                                                                               
MNSQ04   L     R2,CURTAB           CURRENT TABLE ENTRY                          
         USING KEYMTRXD,R2                                                      
         CLI   KEYMKEY,X'FF'       TEST AT LAST KEY                             
         BNE   *+12                NO                                           
         MVI   MINERR,MINEEOF      RETURN EOF                                   
         B     EXITL                                                            
*                                                                               
         AHI   R2,KEYMTRXL         NEXT ENTRY                                   
         BRAS  RE,FNDREC                                                        
         L     R4,AREC                                                          
         AH    R4,MINELDSP         POINT TO FIRST ELEM                          
         B     MNSQ02                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* MNHIGH - HIGH COMMAND PROCESSING                                    *         
***********************************************************************         
                                                                                
MNHIGH   NTR1  ,                                                                
         XC    CURVALS,CURVALS     CLEAR VALUES FOR CURRENT REC                 
         BRAS  RE,TSTSET           TEST WHETHER SET EXISTS                      
         BNE   EXITL                                                            
*                                                                               
         BRAS  RE,SRCHTAB          SEARCH TABLE FOR RECORD                      
         BNE   EXITL                                                            
         MVI   SMODE,C'H'          SET SEARCH MODE HIGH                         
*                                                                               
MNHI02   BRAS  RE,SRCHREC          SEARCH RECORD FOR ELEM                       
         CLI   MINEKEY,X'FF'       CONVENTION FOR LAST ELEM                     
         BNE   MNHI04                                                           
         L     R4,LASTKELM         A(LAST KEY ELEMENT)                          
         BRAS  RE,SETMIN                                                        
         B     EXIT                                                             
*                                                                               
MNHI04   L     R4,MINELEM          SEE IF ANY ELEM FOUND                        
         CLI   0(R4),0                                                          
         BE    MNHI06              NO, TRY NEXT RECORD                          
         BRAS  RE,TSTFILT          FILTER                                       
         BE    *+8                                                              
         MVI   MINERR,MINEEOF      RETURN EOF                                   
         B     EXIT                                                             
*                                                                               
MNHI06   L     R2,CURTAB           CURRENT TABLE ENTRY                          
         USING KEYMTRXD,R2                                                      
         CLI   KEYMKEY,X'FF'       TEST AT LAST KEY                             
         BNE   *+12                NO                                           
         MVI   MINERR,MINEEOF      RETURN EOF                                   
         B     EXITL                                                            
*                                                                               
         AHI   R2,KEYMTRXL         NEXT ENTRY                                   
         BRAS  RE,FNDREC                                                        
         L     R4,AREC                                                          
         AH    R4,MINELDSP         POINT TO FIRST ELEM                          
         B     MNHI02                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* MNBSEQ - BSEQ COMMAND PROCESSING - BACKWARDS SEQUENTIAL             *         
***********************************************************************         
                                                                                
MNBSEQ   NTR1  ,                                                                
         ICM   R5,15,CURMIN        TEST HAVE CURRENT POINTER                    
         BNZ   *+6                                                              
         DC    H'0'                INVALID COMMAND SEQUENCE                     
*                                                                               
BSEQ02   L     R4,AREC                                                          
         AH    R4,MINELDSP         POINT TO FIRST ELEM                          
         CR    R4,R5               IF FIRST ELEM IN REC IS ONE                  
         BE    BSEQ14              JUST DONE, THEN GET NEW RECORD               
         SR    R6,R6                                                            
*                                                                               
BSEQ04   CR    R4,R5               TEST HAVE REACHED CURRENT                    
         BNL   BSEQ12              YES, DONE WITH LOOP                          
         CLI   0(R4),0             EOR                                          
         BNE   *+6                                                              
         DC    H'0'                SHOULD NEVER HAPPEN                          
*                                                                               
         CLC   0(1,R4),MINNKLO     TEST WITH NON-KEY RANGE                      
         BL    BSEQ08              NO                                           
         CLC   0(1,R4),MINNKHI                                                  
         BNH   BSEQ10                                                           
*                                                                               
BSEQ08   LR    R6,R4               SAVE LATEST KEY ELEM                         
*                                                                               
BSEQ10   XR    RF,RF               NEXT ELEM                                    
         IC    RF,1(R4)                                                         
         BXH   R4,RF,BSEQ04                                                     
*                                                                               
BSEQ12   LTR   R4,R6               RE-POINT TO CLUSTER BEFORE CURRENT           
         BP    *+6                                                              
         DC    H'0'                SHOULD ALWAYS HAVE FOUND SOMETHING           
         BRAS  RE,TSTFILT          FILTER                                       
         BNE   BSEQ16              NO, DONE                                     
         BRAS  RE,SETMIN           SET CLUSTER IN MINELEM                       
         B     EXIT                                                             
*                                                                               
BSEQ14   L     R2,CURTAB           TRY ANOTHER RECORD                           
         USING KEYMTRXD,R2                                                      
         L     RE,AKEYMTRX                                                      
         AHI   RE,KEYHDRL                                                       
         CR    R2,RE                                                            
         BE    BSEQ16              YES - RETURN END OF FILE                     
         AHI   R2,-(KEYMTRXL)      NEXT ENTRY                                   
         BRAS  RE,FNDREC           GET BUFFER OR READ RECORD                    
         SR    RF,RF                                                            
         IC    RF,MINFKLEN         FILE KEY LENGTH                              
         A     RF,AREC             POINT TO RECORD LENGTH                       
         LH    R5,0(RF)                                                         
         A     R5,AREC             POINT TO EOR                                 
         BCTR  R5,0                                                             
         B     BSEQ02                                                           
*                                                                               
BSEQ16   MVI   MINERR,MINEEOF      END OF FILE                                  
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* MNDEL - DELETE COMMAND PROCESSING                                   *         
***********************************************************************         
                                                                                
MNDEL    NTR1  ,                                                                
         ICM   R4,15,CURMIN        MUST HAVE CURRENT POINTER                    
         BNZ   *+6                                                              
         DC    H'0'                INVALID COMMAND SEQUENCE                     
*                                                                               
         BRAS  RE,DELELS           DELETE ELEMENT CLUSTER                       
         MVC   CURNEXT,CURMIN      FOR SEQ AFTER DELETE                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MNADD - ADD COMMAND PROCESSING                                      *         
***********************************************************************         
                                                                                
MNADD    NTR1  ,                                                                
         BRAS  RE,CKCLUST          TEST VALID CLUSTER                           
         BNE   EXITL                                                            
         MVI   MINNEW,C'N'         SET OFF NEW SWITCH                           
         BRAS  RE,ADDMIN                                                        
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MNWRT - WRITE COMMAND PROCESSING                                    *         
***********************************************************************         
                                                                                
MNWRT    NTR1  ,                                                                
         ICM   R4,15,CURMIN        MUST HAVE CURRENT POINTER                    
         BNZ   *+6                                                              
         DC    H'0'                INVALID COMMAND SEQUENCE                     
*                                                                               
         BRAS  RE,CKCLUST          TEST VALID CLUSTER                           
         BNE   EXITL                                                            
*                                                                               
* IF OLD ELEM = NEW FOR SHORTEST OF OLD LEN, NEW LEN, AND KEY LEN               
* JUST REPLACE, ELSE DELETE AND ADD CHECK OLD VS NEW                            
*                                                                               
         XR    RF,RF                                                            
         IC    RF,1(R4)            LENGTH OF OLD                                
         LA    R6,0(R4,RF)         IF OLD IS NOT SINGLE ELEM                    
         CLC   0(1,R6),MINNKLO                                                  
         BL    MNWRT02                                                          
         CLC   0(1,R6),MINNKHI                                                  
         BNH   MNWRT04             FORGET THE WHOLE THING                       
*                                                                               
MNWRT02  LR    R2,RF               SAVE LENGTH                                  
         L     R3,MINELEM          NEW ELEM                                     
         SR    RE,RE                                                            
         IC    RE,1(R3)            LENGTH OF NEW                                
         LA    R6,0(R3,RE)         IF NEW IS NOT SINGLE ELEM                    
         CLI   0(R6),0                                                          
         BNE   MNWRT04             FORGET IT                                    
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               RF HAS SHORTER                               
         IC    RE,MINEKLEN                                                      
         AHI   RE,1                KEY LEN PLUS 1                               
         CR    RF,RE                                                            
         BNH   *+8                                                              
         LR    RF,RE               RF HAS SHORTEST                              
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R4),0(R3)       TEST OLD VS NEW                              
         BNE   MNWRT04             NOT =, MUST DEL AND ADD                      
*                                                                               
         BCTR  R2,0               ELSE JUST SET NEW IN RECORD                   
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(R3)                                                    
*                                                                               
         L     R2,CURTAB           CURRENT TABLE ENTRY                          
         USING KEYMTRXD,R2                                                      
         OI    KEYMSTA,KEYMSCHG    SET WRITE PENDING                            
         OI    MINUPDT,MINUPCQ+MINUPCCQ                                         
         BRAS  RE,PUTBACK          MOVE RECORD BACK TO XA                       
         B     EXIT                                                             
*                                                                               
MNWRT04  BRAS  RE,DELELS           DELETE ELEMENT CLUSTER                       
         BRAS  RE,ADDMIN           ADD NEW                                      
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MNCLSE - CLOSE COMMAND PROCESSING                                   *         
***********************************************************************         
                                                                                
MNCLSE   NTR1  ,                                                                
         CLI   MINOPEN,C'Y'        SKIP IF NOT OPEN                             
         BNE   CLSE20                                                           
         CLI   MINMODE,C'B'        SKIP IF IN BUFFER MODE                       
         BE    CLSE20                                                           
                                                                                
         L     R2,AKEYMTRX         RECORD TABLE                                 
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
CLSE02   OC    KEYMKEY,KEYMKEY                                                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLI   KEYMKEY,X'FF'       FIND FF RECORD                               
         BE    *+12                                                             
         AHI   R2,KEYMTRXL         NEXT ENTRY                                   
         B     CLSE02                                                           
*                                                                               
         ST    R2,CURTAB                                                        
         LA    R3,MINMKEY          SEE IF STATUS BYTES CHANGED                  
         AH    R3,MINDCDSP                                                      
         CLI   MINMULTB,0          TEST MULTI-RECORD BIT USED                   
         BE    CLSE10              NO                                           
*                                                                               
*                                  YES, COUNT ACTIVE RECORDS                    
         L     R2,AKEYMTRX         RECORD TABLE                                 
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
         XR    RF,RF                                                            
CLSE04   OC    KEYMKEY,KEYMKEY                                                  
         BZ    CLSE06                                                           
         TM    KEYMSTA,KEYMSDEL    TEST RECORD HAS NO ELEMS                     
         BNZ   *+8                 (AND WILL BE DELETED)                        
         AHI   RF,1                BUMP ACTIVE RECORD COUNT                     
         AHI   R2,KEYMTRXL         NEXT ENTRY                                   
         B     CLSE04                                                           
*                                                                               
CLSE06   MVC   BYTE,0(R3)          GET 1ST CONTROL BYTE                         
         CHI   RF,1                IF MORE THAT ONE RECORD                      
         BNH   *+14                                                             
         OC    BYTE,MINMULTB       OR IN MULTI-RECORD BIT                       
         B     CLSE08                                                           
*                                                                               
         MVC   DUB(1),MINMULTB                                                  
         XI    DUB,X'FF'           ELSE AND IN COMPLEMENT                       
         NC    BYTE,DUB                                                         
*                                                                               
CLSE08   MVC   0(1,R3),BYTE        RETURN AMENDED BYTE                          
*                                                                               
CLSE10   XR    R4,R4                                                            
         IC    R4,MINNCTL                                                       
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         BE    CLSE12                                                           
         CLC   MNFFSTAT(0),0(R3)   TEST STATUS BYTES HAVE CHANGED               
*                                                                               
         MVI   MINEKEY,X'FF'       FORCE READ OF FF REC                         
         BRAS  RE,MNHIGH                                                        
*                                                                               
         L     R2,CURTAB           CURRENT TABLE ENTRY                          
         USING KEYMTRXD,R2                                                      
         OI    KEYMSTA,KEYMSCHG    SET WRITE PENDING                            
*                                                                               
         ICM   RF,15,KEYMADR                                                    
         AH    RF,MINFCDSP                                                      
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R3)       SET NEW STATUS BYTES                         
*                                                                               
         L     RF,AREC                                                          
         SR    RE,RE                                                            
         IC    RE,MINFKLEN                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   MINMKEY(0),0(RF)    SET FULL FF REC KEY                          
*                                                                               
         OC    KEYMDA,KEYMDA       UNLESS BRAND NEW SET                         
         BZ    CLSE12                                                           
         MVC   KEY,MINMKEY         SET NEW BYTES IN DIRECTORY                   
         BRAS  RE,DMREAD           (FOR FF RECORD ONLY)                         
         MVC   KEY,MINMKEY                                                      
         LA    RF,KEY                                                           
         AH    RF,MINDADSP         POINT TO DISK ADDR                           
         OC    0(4,RF),0(RF)       IF NONE, SET FROM TABLE                      
         BNZ   *+10                WHY IS DA EVER MISSING FROM                  
         MVC   0(4,RF),KEYMDA      MINMKEY?                                     
         BRAS  RE,DMWRT                                                         
*                                                                               
CLSE12   L     R2,AKEYMTRX         RECORD TABLE                                 
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
*                                                                               
CLSE14   OC    KEYMKEY,KEYMKEY     TEST END OF LIST                             
         BZ    CLSE20              YES                                          
         TM    KEYMSTA,KEYMSCHG    TEST RECORD CHANGED                          
         BZ    *+8                                                              
         BRAS  RE,WRTREC           WRITE RECORD BACK TO FILE                    
         AHI   R2,KEYMTRXL         NEXT ENTRY                                   
         B     CLSE14                                                           
*                                                                               
CLSE20   MVI   MINOPEN,C'N'        FORCE RE-OPEN NEXT TIME                      
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* MNDELF - DELETE AN ENTIRE FILE                                      *         
***********************************************************************         
                                                                                
MNDELF   NTR1  ,                                                                
         MVC   AREC,MINBUFF        USE BUFFER 1                                 
         XC    KEY,KEY                                                          
         LH    RE,MINMKLEN         MASTER KEY LENGTH                            
         BCTR  RE,0                                                             
         EX    RE,MNDMVC                                                        
*                                                                               
         XR    R8,R8               R8 = COUNT OF RECORDS IN SET                 
         OI    DMIBTS,X'80'        SET RFU                                      
         BRAS  RE,HIGH                                                          
         B     MNDF04                                                           
*                                                                               
MNDMVC   MVC   KEY(0),MINMKEY      SET MASTER KEY                               
*                                                                               
MNDF02   BRAS  RE,SEQ              NEXT RECORD                                  
*                                                                               
MNDF04   BRAS  RE,KEYCHK           TEST KEY MATCHES                             
         BNE   MNDF06                                                           
*                                                                               
         AHI   R8,1                BUMP RECORD COUNT                            
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP         POINT TO CTL BYTES                           
         TM    0(RF),X'80'         TEST DELETED                                 
         BNZ   MNDF02              YES- SKIP                                    
*                                                                               
         OI    0(RF),X'80'         SET DELETED                                  
         BRAS  RE,DMWRT                                                         
*                                                                               
         BRAS  RE,GETREC           ALSO MARK RECORD DELETED                     
         L     R4,AREC                                                          
         AH    R4,MINFCDSP                                                      
         OI    0(R4),X'80'                                                      
         BRAS  RE,PUTREC                                                        
         B     MNDF02                                                           
*                                                                               
MNDF06   LTR   R8,R8               TEST ANY RECORDS FOUND                       
         BP    *+8                                                              
         MVI   MINERR,MINESNF      SET NOT FOUND                                
*                                                                               
         MVI   MINOPEN,C'N'        FORCE RE-OPEN NEXT TIME                      
         CLI   MINERR,0                                                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MNRESF - RESTORE AN ENTIRE FILE                                     *         
***********************************************************************         
                                                                                
MNRESF   NTR1  ,                                                                
         MVC   AREC,MINBUFF        USE BUFFER 1                                 
         XC    KEY,KEY                                                          
         LH    RE,MINMKLEN         MASTER KEY LENGTH                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   KEY(0),MINMKEY      SET MASTER KEY                               
         SR    R8,R8                                                            
         SR    R7,R7                                                            
         OI    DMIBTS,X'80'        SET RFU                                      
         BRAS  RE,HIGH                                                          
         B     MNRESF04                                                         
*                                                                               
MNRESF02 BRAS  RE,SEQ                                                           
MNRESF04 BRAS  RE,KEYCHK                                                        
         BNE   MNRESF10                                                         
         AHI   R8,1                BUMP RECORD COUNT                            
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP         POINT TO CTL BYTES                           
         TM    0(RF),X'80'         TEST DELETED                                 
         BNZ   MNRESF06                                                         
         AHI   R7,1                BUMP NON-DELETE COUNT                        
         B     MNRESF02            NEXT RECORD                                  
*                                                                               
MNRESF06 NI    0(RF),X'7F'         SET POINTER UNDELETED                        
         BRAS  RE,GETREC           GET RECORD                                   
         L     R4,AREC                                                          
         AH    R4,MINELDSP         SEE IF ANY ELEMENTS                          
         CLI   0(R4),0                                                          
         BNE   MNRESF08            YES, RESTORE                                 
         L     R4,AREC                                                          
         AH    R4,MINMKLEN         OR IF THIS IS FF RECORD                      
         CLI   0(R4),X'FF'                                                      
         BNE   MNRESF02                                                         
*                                                                               
MNRESF08 BRAS  RE,DMWRT                                                         
         L     R4,AREC                                                          
         AH    R4,MINFCDSP                                                      
         NI    0(R4),X'7F'         ALSO RECORD                                  
         BRAS  RE,PUTREC                                                        
         B     MNRESF02                                                         
*                                                                               
MNRESF10 LTR   R8,R8               TEST ANY RECORDS FOUND                       
         BP    MNRESF12                                                         
         MVI   MINERR,MINESNF      SET NOT FOUND                                
         B     MNRESFX                                                          
*                                                                               
MNRESF12 LTR   R7,R7               TEST ANY NON-DELETES                         
         BZ    MNRESFX                                                          
         MVI   MINERR,MINENDEL     SET NOT DELETED                              
*                                  (BUT HAS BEEN FULLY RESTORED)                
MNRESFX  MVI   MINOPEN,C'N'        FORCE RE-OPEN NEXT TIME                      
         CLI   MINERR,0                                                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* MNCPYF - COPY A FILE                                                *         
***********************************************************************         
                                                                                
MNCPYF   NTR1  ,                                                                
         XC    COPYDET,COPYDET     CHECK FOR OVERRIDE ON 'TO' FILE              
         CLI   MINCTFIL,C' '                                                    
         BNH   MNCPYF02            - NONE                                       
         CLI   MINCTDIR,C' '                                                    
         BH    *+6                                                              
         DC    H'0'                - DIRECTORY MUST ALSO BE PROVIDED            
         OI    CFLAG,CFCTOVR       COPY 'TO' FILE OVERRIDDEN, SO SAVE           
         MVC   CMINFIL,MINFIL      ORIGINAL 'TO' FILE SETTINGS                  
         MVC   CMINDIR,MINDIR                                                   
         MVC   CMINAGYC,MINAGYC                                                 
         MVC   CMINAGYD,MINAGYD                                                 
         MVC   MINEKEY,MINMKEY     KEY IS UNCHANGED ACROSS FILES                
*                                                                               
MNCPYF02 MVC   AREC,MINBUFF        USE BUFFER 1                                 
         MVC   KEY,MINEKEY         NEW MASTER KEY IS IN MINEKEY                 
         BRAS  RE,SETTOFIL         OVERRIDE THE 'TO' FILE IF REQUIRED           
         BRAS  RE,HIGH                                                          
         BRAS  RE,KEYCHK                                                        
         BNE   MNCPYF04                                                         
         MVI   MINERR,MINEDUP      NEW CODE ALREADY EXISTS                      
         B     EXITL                                                            
*                                                                               
MNCPYF04 XC    KEY,KEY                                                          
         LH    RE,MINMKLEN         MASTER KEY LENGTH                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   KEY(0),MINMKEY      SET MASTER KEY                               
         SR    R8,R8                                                            
         BRAS  RE,SETFRFIL         RESET THE 'FROM' FILE IF NECESSARY           
         BRAS  RE,HIGH                                                          
         B     MNCPYF08                                                         
*                                                                               
MNCPYF06 BRAS  RE,SEQ                                                           
MNCPYF08 BRAS  RE,KEYCHK                                                        
         BNE   MNCPYF14                                                         
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP         POINT TO CTL BYTES                           
         TM    0(RF),X'80'         TEST DELETED                                 
         BNZ   MNCPYF06            YES, SKIP                                    
*                                                                               
         BRAS  RE,GETREC           GET RECORD                                   
         L     R4,AREC                                                          
         AH    R4,MINELDSP         SEE IF ANY ELEMENTS                          
         CLI   0(R4),0                                                          
         BNE   MNCPYF10            YES, COPY                                    
         L     R4,AREC             ELSE, SKIP UNLESS                            
         AH    R4,MINMKLEN         THIS IS FF RECORD                            
         CLI   0(R4),X'FF'                                                      
         BNE   MNCPYF06                                                         
*                                                                               
MNCPYF10 AHI   R8,1                BUMP RECORD COUNT                            
         L     R4,AREC                                                          
         LH    RE,MINMKLEN                                                      
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),MINEKEY     SET NEW MASTER KEY                           
         AH    R4,MINFCDSP                                                      
         NI    0(R4),X'7F'         MAKE SURE NOT DELETED                        
*                                                                               
MNCPYF12 MVC   WORK,KEY            SAVE KEY                                     
         BRAS  RE,SETTOFIL         OVERRIDE THE 'TO' FILE IF REQUIRED           
         BRAS  RE,DMADD                                                         
         MVC   KEY,WORK            RESTORE KEY                                  
*                                                                               
         BRAS  RE,SETFRFIL         RESET THE 'FROM' FILE IF NECESSARY           
         BRAS  RE,HIGH             READ NEXT 'FROM' RECORD                      
         B     MNCPYF06                                                         
*                                                                               
MNCPYF14 LTR   R8,R8               TEST ANY RECORDS FOUND                       
         BP    *+8                                                              
         MVI   MINERR,MINESNF      SET NOT FOUND                                
         BRAS  RE,SETFRFIL         ENSURE FILE CORRECT PRIOR TO EXIT            
         MVI   MINOPEN,C'N'        FORCE RE-OPEN NEXT TIME                      
         CLI   MINERR,0                                                         
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* COPY SUBROUTINES                                                    *         
***********************************************************************         
                                                                                
SETTOFIL TM    CFLAG,CFCTOVR       SET OVERRIDE 'TO' FILE DETAILS IF            
         BNOR  RE                  REQUIRED                                     
         MVC   MINFIL,MINCTFIL                                                  
         MVC   MINDIR,MINCTDIR                                                  
         MVC   MINAGYC,MINCTAGC                                                 
         MVC   MINAGYD,MINCTAGD                                                 
         B     SETTFAGY            - SET AGENCY CODE DETAILS                    
*                                                                               
SETFRFIL TM    CFLAG,CFCTOVR       RESTORE 'FROM' FILE DETAILS IF REQRD         
         BNOR  RE                                                               
         MVC   MINFIL,CMINFIL                                                   
         MVC   MINDIR,CMINDIR                                                   
         MVC   MINAGYC,CMINAGYC                                                 
         MVC   MINAGYD,CMINAGYD                                                 
*                                                                               
SETTFAGY SR    RF,RF               SET/RESET AGY CODE IN KEY AS REQRD           
         ICM   RF,1,MINAGYD        SKIP IF DISPLACEMENT NOT GIVEN               
         BZR   RE                                                               
         OC    MINAGYC,MINAGYC     OR IF CODE NOT GIVEN                         
         BZR   RE                                                               
         LA    RF,KEY(RF)                                                       
         MVC   0(2,RF),MINAGYC     - SET/RESET AGENCY IN KEY                    
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* DATAMGR INTERFACES                                                  *         
***********************************************************************         
                                                                                
HIGH     LR    R0,RE                                                            
         MVC   KEYSAVE,KEY                                                      
         GOTOR DOIO,DMCB,(DMIBTS,=C'DMRDHI'),MINDIR,KEY,KEY                     
         B     DMSETDA                                                          
*                                                                               
DMREAD   LR    R0,RE                                                            
         MVC   KEYSAVE,KEY                                                      
         GOTOR DOIO,DMCB,(DMIBTS,=C'DMREAD'),MINDIR,KEY,KEY                     
         B     DMSETDA                                                          
*                                                                               
SEQ      LR    R0,RE                                                            
         GOTOR DOIO,DMCB,(DMIBTS,=C'DMRSEQ'),MINDIR,KEY,KEY                     
         B     DMSETDA                                                          
*                                                                               
DMWRT    LR    R0,RE                                                            
         CLI   MINWRITE,C'N'                                                    
         BE    DMCK4                                                            
         LA    RF,KEY                                                           
         AH    RF,MINDADSP                                                      
         OC    0(4,RF),0(RF)                                                    
         BNZ   *+6                                                              
         DC    H'0'                NO DISK ADDRESS                              
         GOTOR DOIO,DMCB,(DMIBTS,=C'DMWRT'),MINDIR,KEY,KEY                      
         B     DMCK                                                             
*                                                                               
GETREC   LR    R0,RE                                                            
         GOTOR DOIO,DMCB,(DMIBTS,=C'GETREC'),MINFIL,DMDA,AREC,         X        
               DMWORK                                                           
         L     RF,AREC                                                          
         AH    RF,MINFCDSP                                                      
         MVC   DMSTAT,0(RF)        SAVE STATUS BYTES                            
         B     DMCK                                                             
*                                                                               
PUTREC   LR    R0,RE                                                            
         BRAS  RE,SETAGY           SET AGENCY CODE                              
         CLI   MINWRITE,C'N'                                                    
         BE    DMCK4                                                            
         GOTOR DOIO,DMCB,(DMIBTS,=C'PUTREC'),MINFIL,DMDA,AREC,         X        
               DMWORK                                                           
         OI    MINUPDT,MINUPFQ+MINUPFCQ  SET HAVE DONE FILE UPD                 
         B     DMCK                                                             
*                                                                               
DMADD    LR    R0,RE                                                            
         BRAS  RE,SETAGY           SET AGENCY CODE                              
         CLI   MINWRITE,C'N'                                                    
         BE    DMCK4                                                            
         GOTOR DOIO,DMCB,(DMIBTS,=C'ADDREC'),MINFIL,KEY,AREC,          X        
               DMWORK                                                           
         OI    MINUPDT,MINUPFQ+MINUPFCQ  SET HAVE DONE FILE UPD                 
         B     DMCK                                                             
*                                                                               
DMSETDA  LA    RF,KEY              SAVE DISK ADDRESS FROM KEY JUST READ         
         AH    RF,MINDADSP                                                      
         MVC   DMDA,0(RF)                                                       
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP                                                      
         MVC   DMSTAT,0(RF)        SAVE STATUS BYTES                            
*                                                                               
DMCK     MVC   BYTE,DMOBTS                                                      
         NC    BYTE,DMCB+8                                                      
         BZ    DMCK4                                                            
*                                                                               
         DC    H'0'                DATAMGR ERROR - FOR NOW JUST ABEND           
         LTR   RE,R0               RETURN CC NOT = IF ERROR                     
         BR    RE                                                               
*                                                                               
DMCK4    LR    RE,R0                                                            
         CR    RE,RE               RETURN CC = IF NO ERROR                      
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* SET 2-BYTE AGENCY CODE IN RECORD                                    *         
***********************************************************************         
                                                                                
SETAGY   SR    RF,RF                                                            
         ICM   RF,1,MINAGYD        SKIP IF DISPLACEMENT NOT GIVEN               
         BZR   RE                                                               
         OC    MINAGYC,MINAGYC     OR IF CODE NOT GIVEN                         
         BZR   RE                                                               
         A     RF,AREC                                                          
         MVC   0(2,RF),MINAGYC                                                  
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* KEYCHK - CHECK MASTER KEY                                           *         
***********************************************************************         
                                                                                
KEYCHK   LH    R1,MINMKLEN         MASTER KEY LENGTH                            
         BCTR  R1,0                                                             
         EX    R1,KCCLC            TEST RECORD FROM MINIO SET                   
         BR    RE                  NO- RETURN CC NOT =                          
                                                                                
***********************************************************************         
* KEYCOMP - TEST FULL KEY                                             *         
***********************************************************************         
                                                                                
KEYCOMP  SR    RF,RF                                                            
         IC    RF,MINFKLEN                                                      
         BCTR  RF,0                                                             
         EX    RF,KCCLC                                                         
         BR    RE                                                               
*                                                                               
KCCLC    CLC   KEY(0),KEYSAVE                                                   
         EJECT                                                                  
***********************************************************************         
* SRCHTAB - SEARCH RECORD TABLE                                       *         
***********************************************************************         
                                                                                
SRCHTAB  NTR1  ,                                                                
         CLI   MINEKEY,0                                                        
         BE    SRCH02                                                           
         CLC   MINEKEY(1),MINNKLO  TEST WITHIN NON-KEY RANGE                    
         BL    SRCH02                                                           
         CLC   MINEKEY(1),MINNKHI                                               
         BH    SRCH02                                                           
         MVI   MINERR,MINERNF      IF NON-KEY ELEM, SET RNF                     
         B     EXITL                                                            
*                                                                               
SRCH02   L     R2,AKEYMTRX                                                      
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
*                                                                               
         OC    KEYMKEY,KEYMKEY     FIRST ENTRY IS ZERO?                         
         BNZ   *+12                NO                                           
         MVI   MINERR,MINERNF      SET RECORD NOT FOUND IF NO RECORDS           
         B     EXITL                                                            
*                                                                               
         XR    RF,RF                                                            
         IC    RF,MINEKLEN         KEY LENGTH                                   
         BCTR  RF,0                                                             
*                                                                               
SRCH04   OC    KEYMKEY,KEYMKEY     ENTRY IS ZERO?                               
         BNZ   *+6                 NO                                           
         DC    H'0'                CANNOT HAPPEN IF FF ENTRY THERE              
         EX    RF,SRCHCLC                                                       
         BNH   SRCH06                                                           
         AHI   R2,KEYMTRXL         TRY NEXT RECORD                              
         B     SRCH04                                                           
*                                                                               
SRCH06   BRAS  RE,FNDREC           SET AREC WITH A(BUFFER)                      
         B     EXITOK                                                           
*                                                                               
SRCHCLC  CLC   MINEKEY(0),KEYMKEY  TEST VS HIGH KEY IN REC                      
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SEARCH RECORD FOR REQUIRED KEY                                      *         
* NTRY: AREC    = A(RECORD TO SEARCH)                                 *         
***********************************************************************         
                                                                                
SRCHREC  NTR1  ,                                                                
         MVI   RMODE,0                                                          
         L     R4,AREC                                                          
         AH    R4,MINELDSP         POINT TO FIRST ELEM                          
*                                                                               
SRCR02   CLI   0(R4),0             EOR                                          
         BE    SRCR08                                                           
         CLC   0(1,R4),MINNKLO     SKIP ELEMS WITHIN NON-KEY RANGE              
         BL    SRCR06              NO                                           
         CLC   0(1,R4),MINNKHI                                                  
         BH    SRCR06                                                           
*                                                                               
SRCR04   XR    RF,RF               NEXT ELEM                                    
         IC    RF,1(R4)                                                         
         BXH   R4,RF,SRCR02                                                     
*                                                                               
SRCR06   ST    R4,LASTKELM         SAVE A(LAST KEY ELEM)                        
         CLC   MINEKEY(1),0(R4)    FIRST TEST ELEM CODE                         
         BH    SRCR04              IF ARG IS HIGH GET ANOTHER ELEM              
         BL    SRCR08              ARG IS LOW                                   
*                                  HAVE RIGHT ELEM CODE, TEST KEY               
         XC    WORK,WORK                                                        
         SR    RF,RF                                                            
         IC    RF,1(R4)            ELEM LENGTH                                  
         SHI   RF,3                                                             
         SR    RE,RE                                                            
         IC    RE,MINEKLEN         KEY LENGTH                                   
         SHI   RE,2                (-1 FOR ELEM CODE)                           
         CR    RF,RE               USE SHORTER LENGTH FOR MOVE                  
         BNH   *+6                                                              
         LR    RF,RE                                                            
         EX    RF,SRMVC            MOVE TO WORK                                 
         EX    RE,SRCLC            COMPARE FOR MINEKLEN-1                       
         BH    SRCR04              IF ARG IS HIGH GET ANOTHER ELEM              
         BE    SRCR10              FOUND EXACT MATCH                            
*                                                                               
SRCR08   CLI   SMODE,C'E'          ARG IS LOW, TEST SEARCH MODE                 
         BNE   *+12                IF NEED EXACT MATCH, SET RNF                 
         MVI   MINERR,MINERNF      RECORD NOT FOUND                             
         B     EXITL                                                            
*                                                                               
         MVI   RMODE,C'U'          ELSE ACCEPT, BUT SET UNEQUAL FIND            
*                                                                               
SRCR10   BRAS  RE,SETMIN           SET CLUSTER IN MINELEM                       
         B     EXIT                                                             
*                                                                               
SRMVC    MVC   WORK(0),2(R4)                                                    
SRCLC    CLC   MINEKEY+1(0),WORK                                                
         EJECT                                                                  
***********************************************************************         
* FNDREC - COPY RECORD FROM XA TO LOCAL BUFFER                        *         
* NTRY: R2     = A(TABLE ENTRY)                                       *         
***********************************************************************         
                                                                                
FNDREC   NTR1  ,                                                                
         ST    R2,CURTAB           SET A(CURRENT RECORD)                        
         USING KEYMTRXD,R2                                                      
         L     R0,AREC                                                          
         L     R1,BUFFLEN                                                       
         ICM   RE,15,KEYMADR       COPY RECORD INTO LOCAL BUFFER                
         L     RF,BUFFLEN                                                       
         MVCL  R0,RE                                                            
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* PUTBACK - SET LOCAL BUFFER BACK INTO XA BUFFER                      *         
* NTRY: R2     = A(TABLE ENTRY)                                       *         
***********************************************************************         
                                                                                
         USING KEYMTRXD,R2                                                      
PUTBACK  NTR1  ,                                                                
         ICM   R0,15,KEYMADR       COPY RECORD FROM LOCAL BUFFER                
         L     R1,BUFFLEN                                                       
         L     RE,AREC                                                          
         L     RF,BUFFLEN                                                       
         MVCL  R0,RE                                                            
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* SETMIN - SET ELEM CLUSTER IN MINELEM                                *         
* NTRY: R4     = A(FIRST ELEMENT IN CLUSTER)                          *         
***********************************************************************         
                                                                                
SETMIN   NTR1  ,                                                                
         ST    R4,CURMIN           SET ADDRESS OF CURRENT ELEM                  
         ST    R4,MINELAD          AND SET FOR CALLER                           
         CLI   SMODE,C'A'          FOR ADDS, CHANGES                            
         BE    SMIN02              DONT CLOBBER MINELEM AREA                    
*                                                                               
         L     R0,MINELEM          CLEAR MINELEM AREA                           
         LH    R1,MINMAXEL                                                      
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
SMIN02   XR    R3,R3               R3 = CUMULATIVE LENGTH                       
         CLI   0(R4),0             IF AT EOR                                    
         BE    SMIN08              DONE                                         
         L     R5,MINELEM                                                       
*                                                                               
SMIN04   XR    RF,RF                                                            
         IC    RF,1(R4)                                                         
         AR    R3,RF               CUMULATIVE LENGTH                            
         CH    R3,MINMAXEL         TEST CLUSTER TOO LONG                        
         BNH   *+12                                                             
         MVI   MINERR,MINEROVF                                                  
         B     EXITL                                                            
*                                                                               
         CLI   SMODE,C'A'          FOR ADDS, CHANGES                            
         BE    SMIN06              DONT MOVE OVER MINELEM AREA                  
         BCTR  RF,0                                                             
         EX    RF,SMMVC                                                         
         LA    R5,1(R5,RF)         SPOT FOR NEXT ELEM                           
*                                                                               
SMIN06   SR    RF,RF               LOOK FOR SUB (NON-KEY) ELEMS                 
         IC    RF,1(R4)                                                         
         AR    R4,RF                                                            
         CLI   0(R4),0             DONE IF AT EOR                               
         BE    SMIN08                                                           
         CLC   0(1,R4),MINNKLO     FIRST KEY ELEM ENDS CLUSTER                  
         BL    SMIN08                                                           
         CLC   0(1,R4),MINNKHI                                                  
         BNH   SMIN04                                                           
*                                                                               
SMIN08   STH   R3,MINELEML         SET CLUSTER LENGTH                           
         ST    R4,CURNEXT          CURRENT NEXT RECORD                          
         B     EXITOK                                                           
*                                                                               
SMMVC    MVC   0(0,R5),0(R4)                                                    
         EJECT                                                                  
***********************************************************************         
* DELELS - DELETE ELEMENT CLUSTER                                     *         
* NTRY: AREC   = A(LOCAL COPY OF RECORD)                              *         
*       R4     = A(FIRST ELEMENT IN CLUSTER)                          *         
***********************************************************************         
                                                                                
DELELS   NTR1  ,                                                                
*                                                                               
DEL02    GOTO1 RECUP,DMCB,(RCUMODE,AREC),(R4),(C'R',0),RCUFILD,TO24=Y           
         SAM31 ,                                                                
         CLI   0(R4),0             DONE IF AT EOR                               
         BE    DEL04                                                            
         CLC   0(1,R4),MINNKLO     OR WITH NEW 'KEY' ELEM                       
         BL    DEL04                                                            
         CLC   0(1,R4),MINNKHI                                                  
         BNH   DEL02                                                            
*                                                                               
DEL04    L     R2,CURTAB           CURRENT TABLE ENTRY                          
         USING KEYMTRXD,R2                                                      
         BRAS  RE,PUTBACK                                                       
         OI    KEYMSTA,KEYMSCHG    SET WRITE PENDING                            
         OI    MINUPDT,MINUPCQ+MINUPCCQ                                         
*                                                                               
         XR    RF,RF                                                            
         IC    RF,MINFKLEN         NOW SEE IF RECORD HAS ANY ELEMENTS           
         A     RF,AREC                                                          
         CLC   0(2,RF),MINELDSP                                                 
         BH    *+8                                                              
         OI    KEYMSTA,KEYMSDEL    FLAG EMPTY RECORD (TO BE DELETED)            
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* TSTSET - TEST WHETHER SET EXISTS (ALSO CALLED AT OPEN)              *         
***********************************************************************         
                                                                                
TSTSET   CLI   MINNEW,C'N'                                                      
         BER   RE                                                               
         MVI   MINERR,MINESNF      SET NOT FOUND                                
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CKCLUST - TEST VALID CLUSTER                                        *         
***********************************************************************         
                                                                                
CKCLUST  NTR1  ,                                                                
         L     R2,MINELEM                                                       
         CLI   0(R2),X'FF'         MUST NOT START WITH FF                       
         BNE   *+12                                                             
         MVI   MINERR,MINEELM      BAD ELEMENT                                  
         B     EXITL                                                            
*                                                                               
         SR    R3,R3               CALC LENGTH OF NEW CLUSTER                   
*                                                                               
CKCL02   CLI   0(R2),0             EOR                                          
         BE    CKCL08                                                           
         CLI   1(R2),0                                                          
         BNE   *+12                                                             
         MVI   MINERR,MINEELM      ZERO LENGTH ELEM                             
         B     EXITL                                                            
*                                                                               
         CLC   0(1,R2),MINNKLO     TEST A KEY ELEM                              
         BL    CKCL04              YES                                          
         CLC   0(1,R2),MINNKHI                                                  
         BH    CKCL04              YES                                          
*                                                                               
         LTR   R3,R3                                                            
         BNZ   CKCL06                                                           
         MVI   MINERR,MINEELM      FIRST ELEMENT NON-KEY IS AN ERROR            
         B     EXITL                                                            
*                                                                               
CKCL04   LTR   R3,R3                                                            
         BZ    CKCL06                                                           
         MVI   MINERR,MINEELM      KEY ELEMENT NOT FIRST IS AN ERROR            
         B     EXITL                                                            
*                                                                               
CKCL06   SR    RF,RF               NEXT ELEM                                    
         IC    RF,1(R2)                                                         
         AR    R3,RF               CUMULATIVE LENGTH                            
         BXH   R2,RF,CKCL02                                                     
*                                                                               
CKCL08   LTR   R3,R3               NO ELEMS                                     
         BP    *+12                                                             
         MVI   MINERR,MINEELM      BAD ELEMENT                                  
         B     EXITL                                                            
*                                                                               
         STH   R3,MRLEN            SAVE LENGTH                                  
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* TSTFILT- TEST FILTER ON ELEM CODE                                   *         
***********************************************************************         
                                                                                
TSTFILT  CLI   MINFILTL,0          IF NOT FILTERING                             
         BER   RE                  RETURN OK                                    
         CLC   0(1,R4),MINEKEY     TEST RIGHT ELEM CODE                         
         BNER  RE                  NOTE- CC SET ON RETURN                       
         CLI   MINFILTL,1          IF ONLY ELEM CODE                            
         BER   RE                  RETURN OK                                    
         SR    RF,RF                                                            
         IC    RF,MINFILTL         LENGTH (INCLUDING ELEM CODE)                 
         SHI   RF,2                                                             
         EX    RF,TFCLC                                                         
         BR    RE                                                               
*                                                                               
TFCLC    CLC   2(0,R4),MINEKEY+1                                                
         EJECT                                                                  
***********************************************************************         
* WRTREC - PUT OR ADD A RECORD TO FILE                                *         
* NTRY: R2     = A(KEY MATRIX TABLE ENTRY)                            *         
***********************************************************************         
                                                                                
         USING KEYMTRXD,R2                                                      
WRTREC   NTR1  ,                                                                
         OC    KEYMDA,KEYMDA       IF HAVE DA                                   
         BZ    WREC08                                                           
*                                  DO PUT                                       
         MVC   DMDA,KEYMDA                                                      
         OI    DMIBTS,X'80'        SET RFU                                      
         BRAS  RE,GETREC                                                        
         BRAS  RE,FNDREC           COPY BUFFER FROM XA TO LOCAL                 
*                                                                               
         L     R6,AREC                                                          
         AH    R6,MINELDSP         POINT TO FIRST ELEM                          
         CLI   0(R6),0             TEST ANY ELEMS                               
         BNE   WREC02              YES - OK                                     
         CLI   KEYMKEY,X'FF'       ELSE UNLESS FF REC                           
         BNE   WREC06              MUST MARK DELETED                            
*                                                                               
WREC02   L     R6,AREC                                                          
         AH    R6,MINFCDSP                                                      
         TM    0(R6),X'80'         TEST RECORD IS NOT DELETED                   
         BNZ   *+12                                                             
         BRAS  RE,PUTREC           WRITE BACK RECORD AND EXIT                   
         B     EXIT                                                             
*                                                                               
WREC04   NI    0(R6),255-X'80'     UNDELETE                                     
         BRAS  RE,PUTREC           AND WRITE BACK                               
*                                                                               
         L     RF,AREC                                                          
         MVC   KEY,0(RF)           UNDELETE DIRECTORY AS WELL                   
         BRAS  RE,HIGH                                                          
         BRAS  RE,KEYCOMP          FULL KEY CHECK                               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP                                                      
         NI    0(RF),255-X'80'                                                  
         BRAS  RE,DMWRT                                                         
         B     EXIT                                                             
*                                                                               
WREC06   L     R6,AREC             NO ELEMS - MARK RECORD DELETED               
         AH    R6,MINFCDSP         POINT TO CTL BYTES                           
         OI    0(R6),X'80'                                                      
         BRAS  RE,PUTREC                                                        
*                                                                               
         L     R6,AREC                                                          
         MVC   KEY,0(R6)           READ POINTER TO MARK DELETED                 
         BRAS  RE,HIGH                                                          
         BRAS  RE,KEYCOMP          FULL KEY CHECK                               
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP                                                      
         OI    0(RF),X'80'                                                      
         BRAS  RE,DMWRT                                                         
         B     EXIT                                                             
*                              *** ADD OF NEW RECORD                            
WREC08   MVC   AREC,MINBUFF        GET RECORD INTO MINBUFF                      
         BRAS  RE,FNDREC                                                        
*                                                                               
         L     R6,AREC             ADD OF NEW RECORD                            
         AH    R6,MINELDSP         POINT TO FIRST ELEM                          
         CLI   0(R6),0             TEST ANY ELEMENTS                            
         BE    EXITOK                                                           
*                                                                               
         L     R6,AREC             SEE IF A USABLE RECORD ALREADY               
         MVC   KEY,0(R6)           ON FILE (DELETED)                            
         OI    DMIBTS,X'80'        SET RFU                                      
         BRAS  RE,HIGH                                                          
         BRAS  RE,KEYCOMP          FULL KEY CHECK                               
         BNE   WREC10                                                           
*                                                                               
         MVC   KEYMDA,DMDA         SET DISK ADDRESS                             
         LA    RF,KEY                                                           
         AH    RF,MINDCDSP         POINT TO CTL BYTES                           
         TM    0(RF),X'80'         SHOULD BE MARKED DELETED                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         NI    0(RF),255-X'80'     UNDELETE                                     
         BRAS  RE,DMWRT                                                         
*                                                                               
         OI    DMIBTS,X'80'        SET RFU                                      
         BRAS  RE,GETREC                                                        
         BRAS  RE,FNDREC           NOW GET/PUT RECORD                           
         L     R6,AREC                                                          
         AH    R6,MINFCDSP                                                      
         NI    0(R6),255-X'80'                                                  
         BRAS  RE,PUTREC                                                        
         B     EXITOK                                                           
*                                                                               
WREC10   BRAS  RE,DMADD            ADD RECORD                                   
         MVC   KEYMDA,KEY          SET DISK ADDRESS                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ADDMIN - ADD MINELEM                                                *         
* NTRY: MNLEN  = L'MINELEM CLUSTER                                    *         
***********************************************************************         
                                                                                
ADDMIN   NTR1  ,                                                                
         L     R2,CURTAB           CURRENT TABLE ENTRY                          
         USING KEYMTRXD,R2                                                      
*                                                                               
         L     R4,MINELEM          SET KEY FROM MINELEM                         
         XC    MINEKEY,MINEKEY                                                  
         MVC   MINEKEY(1),0(R4)    ELEM CODE                                    
         SR    RF,RF                                                            
         IC    RF,1(R4)                                                         
         SHI   RF,2                                                             
         SR    RE,RE                                                            
         IC    RE,MINEKLEN         TEST VS KEY LENGTH                           
         BCTR  RE,0                                                             
         CR    RF,RE                                                            
         BNH   *+6                 USE SHORTER                                  
         LR    RF,RE                                                            
         LTR   RF,RF                                                            
         BNP   ADDM02                                                           
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   MINEKEY+1(0),2(R4)                                               
*                                                                               
ADDM02   XC    CURVALS,CURVALS     CLEAR VALUES FOR CURRENT REC                 
         BRAS  RE,SRCHTAB          SEARCH TABLE FOR RECORD                      
         BE    *+6                                                              
         DC    H'0'                ATTEMPT TO ADD NON-KEY ELEM                  
*                                                                               
         MVI   SMODE,C'A'          SET SEARCH MODE FOR 'ADD'                    
         BRAS  RE,SRCHREC          SEARCH RECORD FOR ELEM                       
         CLI   RMODE,C'U'          TEST NO EQUAL FIND                           
         BE    *+12                                                             
         MVI   MINERR,MINEDUP      FOR NOW NO DUPLICATE KEYS                    
         B     EXITL                                                            
*                                                                               
         OI    MINUPDT,MINUPCQ+MINUPCCQ  SET REC CHANGE                         
*                                                                               
         L     R2,CURTAB                                                        
         OI    KEYMSTA,KEYMSCHG    SET WRITE PENDING AND UNDELETE               
         NI    KEYMSTA,255-KEYMSDEL                                             
         XR    R4,R4                                                            
         IC    R4,MINFKLEN         GET CURRENT RECORD LENGTH                    
         A     R4,AREC             POINT TO LENGTH                              
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,3,0(R4)          CURRENT REC LENGTH                           
         CLI   MINMODE,C'B'        IF BUFFER MODE                               
         BNE   *+8                                                              
         ICM   RE,7,0(R4)          USE 3-BYTE LENGTH                            
*                                                                               
         AH    RE,MRLEN            PLUS CLUSTER LENGTH                          
         AHI   RE,3                PLUS 3 FOR DMGR ADDED NULLS                  
         CLM   RE,15,BUFFLEN       TEST WILL FIT                                
         BNL   ADDM04                                                           
*                                                                               
         L     R4,CURMIN           INSERTION ADDRESS                            
         BRAS  RE,MVCLUST          MOVE CLUSTER INTO RECORD                     
         BRAS  RE,PUTBACK          MOVE RECORD BACK TO XA                       
         B     EXITOK              FINITO                                       
*                                                                               
* SPLIT RECORD INTO 2 BUFFERS                                                   
*                                                                               
ADDM04   CLI   MINMODE,C'B'        IF BUFFER MODE                               
         BNE   *+12                                                             
         MVI   MINERR,MINEBOVF     SET BUFFER OVERLFOW                          
         B     EXITL                                                            
*                                                                               
         L     R0,BUFFLEN                                                       
         MHI   R0,KEYMAX                                                        
         A     R0,ARECMTRX                                                      
         C     R0,BUFFNXT                                                       
         BH    *+12                                                             
         MVI   MINERR,MINETOVF     SET TABLE OVERLFOW                           
         B     EXITL                                                            
*                                                                               
         L     R2,AKEYMTRX         FIND FIRST EMPTY SLOT                        
         AHI   R2,KEYHDRL                                                       
         USING KEYMTRXD,R2                                                      
         OC    KEYMKEY,KEYMKEY                                                  
         BZ    *+12                                                             
         AHI   R2,KEYMTRXL                                                      
         B     *-14                                                             
*                                                                               
         ST    R2,NEWTAB           SET AND CLEAR A(NEW BUFFER ENTRY)            
         XC    0(KEYMTRXL,R2),0(R2)                                             
*                                                                               
         L     R0,BUFFNXT                                                       
         STCM  R0,15,KEYMADR       SET A(RECORD BUFFER IN IT)                   
         A     R0,BUFFLEN                                                       
         ST    R0,BUFFNXT          UPDATE A(NEXT)                               
*                                                                               
         L     RF,4(RD)            ACQUIRE ANOTHER LOCAL BUFFER AREA            
         ST    RD,WBUF                                                          
         A     RD,BUFFLEN                                                       
         AH    RD,MINMAXEL         ALLOW ROOM FOR BEFORE SPLITS                 
         AHI   RD,7                REALIGN RD                                   
         SRL   RD,3                                                             
         SLL   RD,3                                                             
         ST    RD,8(RF)            SET NEW FORWARD LINK                         
         ST    RF,4(RD)            AND NEW BACK LINK                            
*                                                                               
         L     R0,WBUF             COPY LOCAL RECORD TO NEW AREA                
         L     R1,BUFFLEN                                                       
         AH    R1,MINMAXEL         CLEAR THE EXTRA                              
         L     RE,AREC                                                          
         L     RF,BUFFLEN                                                       
         MVCL  R0,RE                                                            
*                                                                               
         L     R4,CURMIN           GET INSERTION SPOT IN AREC                   
         S     R4,AREC                                                          
         A     R4,WBUF             LOCATE SAME IN WBUF                          
         MVC   AREC,WBUF                                                        
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,RCUFILD+4      ADJUST RECUP MAX LENGTH                      
         AH    RF,MINMAXEL                                                      
         STCM  RF,3,RCUFILD+4                                                   
         BRAS  RE,MVCLUST          MOVE CLUSTER INTO WBUF COPY                  
         SH    RF,MINMAXEL                                                      
         STCM  RF,3,RCUFILD+4      RESET RECUP CONTROL                          
*                                                                               
         L     R1,BUFFLEN          SET SPLIT DISPLACEMENT                       
         SH    R1,MINELDSP         AS A PERCENTAGE OF ELEM SPACE                
         SR    R0,R0                                                            
         ICM   R0,3,MINSPCT                                                     
         BP    *+8                                                              
         LHI   R0,50               DEFAULT TO 50                                
         MR    R0,R0                                                            
         D     R0,=F'100'                                                       
         STH   R1,SPLIM            SET SPLIT DISPLACEMENT                       
*                                                                               
         L     R4,WBUF             DETERMINE WHERE TO SPLIT                     
         AH    R4,MINELDSP         R4 = A(1ST ELEMENT)                          
         SR    RF,RF                                                            
         SR    R8,R8               FOR ELEM COUNT                               
         XC    SPLAST,SPLAST                                                    
*                                                                               
ADDM06   CLI   0(R4),0             EOR                                          
         BE    ADDM12                                                           
         SR    R0,R0                                                            
         IC    R0,1(R4)                                                         
         CLC   0(1,R4),MINNKLO     TEST WITHIN NON-KEY RANGE                    
         BL    ADDM08              NO                                           
         CLC   0(1,R4),MINNKHI                                                  
         BNH   ADDM10              NON-KEY, BUMP LENGTH AND GET NEXT            
*                                                                               
ADDM08   CH    RF,SPLIM            TEST HAVE REACHED SPLIT LIMIT                
         BNL   ADDM12                                                           
         ST    R4,SPLAST           NO, SAVE ELEM THAT MAY BE LAST               
*                                                                               
ADDM10   AR    RF,R0               CUMULATIVE LENGTH                            
         AR    R4,R0               NEXT ELEM                                    
         AHI   R8,1                BUMP ELEM COUNT                              
         B     ADDM06                                                           
*                                                                               
ADDM12   LTR   R8,R8               TEST ANY ELEMS TO MOVE                       
         BP    *+6                                                              
         DC    H'0'                NO- SOMETHING MUST BE WRONG                  
         STH   R8,SPELEMS          SAVE COUNT OF ELEMS TO MOVE                  
*                                                                               
         ICM   RF,15,SPLAST        SET NEW KEY FROM WHAT WILL BE HIGH           
         BNZ   *+6                                                              
         DC    H'0'                SOMETHING'S WRONG                            
*                                                                               
         L     R2,NEWTAB                                                        
         BRAS  RE,AMSETEL          SET ELEM KEY IN WORK                         
         MVC   KEYMKEY,WORK                                                     
         MVC   AREC,MINBUFF        SET I/O AREA BACK TO LOCAL AREA              
*                                                                               
         L     R2,CURTAB           BACK TO LOW RECORD                           
         MVC   CURBUFA,MINBUFF                                                  
         BRAS  RE,INIBUFF          INITIALIZE LOW BUFFER                        
*                                                                               
         L     R4,AREC             COPY ELEMENTS BACK INTO LOW BUFFER           
         AH    R4,MINELDSP                                                      
         L     R5,WBUF                                                          
         AH    R5,MINELDSP                                                      
         XR    R8,R8                                                            
*                                                                               
ADDM14   CH    R8,SPELEMS          TEST HAVE SKIPPED ENOUGH                     
         BL    ADDM16                                                           
         CLI   0(R5),0             EOR                                          
         BE    ADDM20                                                           
*                                                                               
         GOTO1 RECUP,DMCB,(RCUMODE,CURBUFA),(R5),(R4),RCUFILD,TO24=Y            
         GOTO1 (RF),(R1),(RCUMODE,WBUF),(R5),0,RCUFILD,TO24=Y                   
         SAM31 ,                                                                
         B     ADDM18              SKIP R5 BUMP AFTER DELETE                    
*                                                                               
ADDM16   XR    R0,R0               BUMP FROM ADDRESS                            
         IC    R0,1(R5)                                                         
         AR    R5,R0                                                            
         AHI   R8,1                BUMP ELEM COUNTER                            
*                                                                               
ADDM18   SR    R0,R0               BUMP TO ADDRESS                              
         IC    R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     ADDM14                                                           
*                                                                               
ADDM20   L     R2,CURTAB           COPY BACK ORIGINAL RECORD                    
         MVC   AREC,MINBUFF                                                     
         BRAS  RE,PUTBACK                                                       
*                                                                               
         OI    KEYMSTA,KEYMSCHG    SET WRITE PENDING                            
         NI    KEYMSTA,255-KEYMSDEL                                             
         OI    MINUPDT,MINUPCQ+MINUPCCQ                                         
*                                                                               
         XR    RF,RF                                                            
         IC    RF,MINFKLEN         NOW SEE IF RECORD HAS ANY ELEMENTS           
         A     RF,AREC                                                          
         CLC   0(2,RF),MINELDSP                                                 
         BH    *+8                                                              
         OI    KEYMSTA,KEYMSDEL    FLAG EMPTY RECORD (TO BE DELETED)            
*                                                                               
         L     R2,NEWTAB           NOW COPY BACK NEW RECORD                     
         MVC   AREC,WBUF                                                        
         L     R3,AREC                                                          
         AH    R3,MINMKLEN                                                      
         XR    RF,RF                                                            
         IC    RF,MINEKLEN         MOVE IN ELEMENT KEY TO NEW RECORD            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),KEYMKEY                                                  
*                                                                               
         BRAS  RE,PUTBACK                                                       
         OI    KEYMSTA,KEYMSCHG    SET WRITE PENDING                            
         OI    MINUPDT,MINUPCQ+MINUPCCQ                                         
*                                                                               
         LH    RF,MINNRECS                                                      
         AHI   RF,1                                                             
         STH   RF,MINNRECS                                                      
         CHI   RF,KEYMAX                                                        
         BNH   *+12                                                             
         MVI   MINERR,MINETOVF     TABLE OVERFLOW                               
         B     EXITL                                                            
*                                                                               
         XR    RF,RF                                                            
         IC    RF,MINFKLEN         NOW SEE IF RECORD HAS ANY ELEMENTS           
         A     RF,AREC                                                          
         CLC   0(2,RF),MINELDSP                                                 
         BH    *+8                                                              
         OI    KEYMSTA,KEYMSDEL    FLAG EMPTY RECORD (TO BE DELETED)            
*                                                                               
         BRAS  RE,MTRXSRT                                                       
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* AMSETEL - SET ELEMENT KEY IN WORK FROM AN ELEMENT                   *         
* NTRY: RF     = A(ELEMENT)                                           *         
***********************************************************************         
                                                                                
AMSETEL  XC    WORK,WORK                                                        
         MVC   WORK(1),0(RF)       ELEMENT CODE                                 
         XR    R1,R1                                                            
         IC    R1,1(RF)                                                         
         AHI   R1,-2               R1 = ELEMENT LENGTH - 2                      
*                                                                               
         XR    R0,R0                                                            
         IC    R0,MINEKLEN         R0 = KEY LENGTH                              
         AHI   R0,-1               (ALREADY MOVED 1 BYTE)                       
*                                                                               
         CR    R1,R0                                                            
         BL    *+6                 USE SHORTER                                  
         LR    R1,R0                                                            
*                                                                               
         BCTR  R1,0                                                             
         EX    R1,AMSMVC                                                        
         BR    RE                                                               
*                                                                               
AMSMVC   MVC   WORK+1(0),2(RF)  REST OF KEY                                     
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SORT KEY MATRIX INTO ASCENDING KEY SEQ ORDER             *         
***********************************************************************         
                                                                                
MTRXSRT  NTR1  ,                                                                
         L     R2,AKEYMTRX                                                      
         AHI   R2,KEYHDRL                                                       
THIS     USING KEYMTRXD,R2                                                      
         XR    RF,RF               RF = COUNT OF RECORDS IN SET                 
*                                                                               
MXS02    OC    THIS.KEYMKEY,THIS.KEYMKEY                                        
         BZ    MXS10               ALL KEYS SORTED                              
*                                                                               
         LA    R3,KEYMTRXL(R2)                                                  
NEXT     USING KEYMTRXD,R3                                                      
*                                                                               
MXS04    OC    NEXT.KEYMKEY,NEXT.KEYMKEY                                        
         BZ    MXS08               DONE FOR THIS SLOT                           
         CLC   THIS.KEYMKEY,NEXT.KEYMKEY                                        
         BL    MXS06                                                            
*                                  YES I KNOW I CAN XC THEM...                  
         MVC   WORK(KEYMTRXL),THIS.KEYMTRXD                                     
         MVC   THIS.KEYMTRXD(KEYMTRXL),NEXT.KEYMTRXD                            
         MVC   NEXT.KEYMTRXD(KEYMTRXL),WORK                                     
*                                                                               
MXS06    AHI   R3,KEYMTRXL         NEXT UNSORTED RECORD                         
         B     MXS04                                                            
*                                                                               
MXS08    AHI   R2,KEYMTRXL         NEXT SLOT                                    
         AHI   RF,1                INCREMENT COUNT OF RECORDS                   
         B     MXS02                                                            
*                                                                               
MXS10    STH   RF,MINNRECS         RESET # OF RECORDS                           
         CHI   RF,KEYMAX                                                        
         BNH   *+12                                                             
         MVI   MINERR,MINETOVF     TABLE OVERFLOW                               
         B     EXITL                                                            
*                                                                               
         B     EXITOK                                                           
         DROP  THIS,NEXT                                                        
         EJECT                                                                  
***********************************************************************         
* MVCLUST - MOVE CLUSTER INTO RECORD                                  *         
* NTRY: AREC   = A(RECORD TO MOVE CLUSTER INTO)                       *         
*       R4     = INSERTION ADDRESS IN AREC                            *         
***********************************************************************         
                                                                                
MVCLUST  NTR1  ,                                                                
         L     R5,MINELEM                                                       
*                                                                               
MCL02    GOTO1 RECUP,DMCB,(RCUMODE,AREC),(R5),(R4),RCUFILD,TO24=Y               
         SAM31 ,                                                                
         SR    R0,R0                                                            
         IC    R0,1(R4)            BUMP TO ADDRESS                              
         AR    R4,R0                                                            
         IC    R0,1(R5)            BUMP FROM ADDRESS                            
         AR    R5,R0                                                            
         CLI   0(R5),0             END OF CLUSTER                               
         BNE   MCL02                                                            
         B     EXITR4                                                           
         EJECT                                                                  
***********************************************************************         
* I/O INTERFACE ROUTINE                                               *         
* Should turn protection on but not so easy to do                               
***********************************************************************         
DOIO     NTR1  ,                                                                
         OC    MINHOOK,MINHOOK     TEST USER I/O HOOK PROVIDED                  
         BZ    DOIOHK02                                                         
         LA    RE,DOIOHKX          RETURN POINT FOR NTR1 BELOW                  
*                                                                               
         NTR1  ,                                                                
         L     RE,MINREGD                                                       
         ICM   RF,15,MINHOOK       A(HOOK ROUTINE)                              
         LM    R2,RC,28(RE)        SET CALLERS REGS                             
         BASR  RE,RF                                                            
         XIT1                      EXIT TO DIOIOHKX                             
                                                                                
DOIOHKX  CLI   8(R1),0             SEE IF CALLER SET AN ERROR                   
         B     EXIT                                                             
                                                                                
DOIOHK02 GOTO1 DATAMGR,(R1),TO24=Y                                              
         SAM31 ,                                                                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* INITIALIZE RECORD BUFFER                                            *         
* NTRY: R2       = A(MATRIX TABLE ENTRY)                              *         
*       CURBUFA  = A(BUFFER)                                          *         
***********************************************************************         
                                                                                
         USING KEYMTRXD,R2                                                      
INIBUFF  NTR1  ,                                                                
         L     R0,CURBUFA          CLEAR OUT BUFFER                             
         L     R1,BUFFLEN                                                       
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R3,CURBUFA          MOVE IN MASTER KEY                           
         MVC   0(L'MINMKEY,R3),MINMKEY                                          
         AH    R3,MINMKLEN                                                      
         SR    RF,RF                                                            
         IC    RF,MINEKLEN         MOVE IN ELEMENT KEY                          
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),KEYMKEY                                                  
*                                                                               
         LH    RE,MINDCDSP         MOVE IN STATUS BYTES                         
         LA    RE,MINMKEY(RE)                                                   
         LH    RF,MINFCDSP                                                      
         A     RF,CURBUFA                                                       
         SR    R1,R1                                                            
         IC    R1,MINNCTL                                                       
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(RE)                                                    
         LA    RF,1(R1,RF)                                                      
         XC    0(4,RF),0(RF)       CLEAR LINKAGE AREA                           
*                                                                               
         SR    RF,RF                                                            
         IC    RF,MINFKLEN                                                      
         A     RF,CURBUFA                                                       
         LH    R0,MINELDSP                                                      
         STH   R0,0(RF)            SET INITIAL RECORD LENGTH                    
*                                                                               
         CLI   MINMODE,C'B'        IF BUFFER MODE                               
         BNE   *+8                                                              
         STCM  R0,7,0(RF)          THEN WE USE A 3 BYTE LENGTH                  
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* COMMONLY ADDRESSIBLE STORAGE                                        *         
***********************************************************************         
COMMON   DS    0D                                                               
*                                                                               
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
EXITR4   XIT1  REGS=(R4)                                                        
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
                                                                                
*        INTERNAL COMMAND CODES                                                 
*        X'40'= UPDATIVE, X'80'= DO NOT BUILD TABLE OR 'OPEN'                   
*                                                                               
CSEQ     EQU   X'01'                                                            
CHIGH    EQU   X'02'                                                            
READ     EQU   X'03'                                                            
BSEQ     EQU   X'04'                                                            
OPEN     EQU   X'05'                                                            
DEL      EQU   X'41'                                                            
WRT      EQU   X'42'                                                            
ADD      EQU   X'43'                                                            
CLOSE    EQU   X'C1'                                                            
DELF     EQU   X'C2'                                                            
RESF     EQU   X'C3'                                                            
CPYF     EQU   X'C4'                                                            
*                                                                               
         LTORG                                                                  
XABUFF   DC    A(0)                                                             
XABUFFL  DC    A(0)                                                             
*                                                                               
NULLS    DC    XL10'00'                                                         
LKEYS    DC    A((KEYMAX*KEYMTRXL)+KEYHDRL)                                     
SIZECAP  DC    A(512*1024*4)                                                    
*                                                                               
         DS    0D                                                               
         DC    CL8'XABUFFER'                                                    
EFFS     DC    32X'FF'                                                          
SSBAD    DC    CL8'SSBAD   '                                                    
*                                                                               
CMNDLST  DS    0XL3                INTERNAL COMMAND TABLE                       
         DC    AL1(READ),AL2(MNREAD-MINIO)                                      
         DC    AL1(CHIGH),AL2(MNHIGH-MINIO)                                     
         DC    AL1(CSEQ),AL2(MNSEQ-MINIO)                                       
         DC    AL1(BSEQ),AL2(MNBSEQ-MINIO)                                      
         DC    AL1(ADD),AL2(MNADD-MINIO)                                        
         DC    AL1(WRT),AL2(MNWRT-MINIO)                                        
         DC    AL1(DEL),AL2(MNDEL-MINIO)                                        
         DC    AL1(CPYF),AL2(MNCPYF-MINIO)                                      
         DC    AL1(DELF),AL2(MNDELF-MINIO)                                      
         DC    AL1(RESF),AL2(MNRESF-MINIO)                                      
         DC    AL1(CLOSE),AL2(MNCLSE-MINIO)                                     
         DC    AL1(OPEN),AL2(TSTSET-MINIO)                                      
CMNDLSTN EQU   (*-CMNDLST)/L'CMNDLST                                            
         EJECT                                                                  
                                                                                
***********************************************************************         
* ROUTINE TO GETMAIN STORAGE                                          *         
***********************************************************************         
MINIOGM  CSECT                                                                  
GETMAIN  NTR1  BASE=*,LABEL=*                                                   
         L     R0,LKEYS            L'KEY PORTION OF BLOCK                       
*                                                                               
         L     RF,BUFFLEN          CALC LENGTH FOR RECORD BUFFERS               
         MH    RF,BUFFCNT                                                       
         AR    R0,RF               R0 = TOTAL LENGTH                            
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO NEXT HIGHEST 4K                     
*                                                                               
         SLL   R0,1                DOUBLE IT FOR 2 BUFFERS                      
*                                                                               
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE                             
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RE,=A(XABUFFL)                                                   
         ST    R0,0(,RE)           SAVE STORAGE OBTAINED                        
         L     RE,=A(XABUFF)                                                    
         ST    R1,0(,RE)                                                        
         XIT1                                                                   
                                                                                
         LTORG                                                                  
                                                                                
*                                                                               
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
                                                                                
WORKD    DSECT                                                                  
DMCB     DS    6F                                                               
PARAMS   DS    0F                                                               
PCOMMAND DS    X                                                                
PABLOCK  DS    AL3                                                              
PARAMSL  EQU   *-PARAMS                                                         
*                                                                               
AKEYMTRX DS    A                   A(KEY MATRIX BLOCK)                          
ARECMTRX DS    A                   A(RECORD MATRIX BLOCK)                       
*                                                                               
SVRF     DS    A                   SAVE RF                                      
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
MYSIN    DS    F                                                                
AREC     DS    A                                                                
WBUF     DS    A                                                                
LASTKELM DS    A                                                                
VPROTON  DS    A                                                                
VPROTOFF DS    A                                                                
DATAMGR  DS    A                                                                
RECUP    DS    A                                                                
*                                                                               
         DS    0F                                                               
WORK     DS    XL64                                                             
DMDA     DS    XL4                                                              
DMSTAT   DS    XL20                                                             
KEY      DS    XL64                                                             
KEYSAVE  DS    XL64                                                             
DMWORK   DS    XL96                                                             
SPLAST   DS    F                                                                
NEWTAB   DS    A                                                                
SPELEMS  DS    H                                                                
SPLIM    DS    H                                                                
MRLEN    DS    H                                                                
*                                                                               
HALF     DS    H                                                                
BYTE     DS    X                                                                
DMIBTS   DS    X                                                                
DMOBTS   DS    X                                                                
COMMAND  DS    X                                                                
SMODE    DS    C                                                                
RMODE    DS    C                                                                
HAVFF    DS    C                                                                
HAVDEL   DS    C                                                                
OFFLINE  DS    C                                                                
*                                                                               
COPYDET  DS    0XL(CPYDLQ)         DETAILS FOR COPY                             
CFLAG    DS    XL1                 COPY FLAG                                    
CFCTOVR  EQU   X'80'               COPYING TO ANOTHER FILE                      
CMINFIL  DS    CL(L'MINFIL)        ORIGINAL 'FROM' FILE SETTINGS IF             
CMINDIR  DS    CL(L'MINDIR)        'TO' FILE OVERRIDDEN                         
CMINAGYC DS    CL(L'MINAGYC)                                                    
CMINAGYD DS    CL(L'MINAGYD)                                                    
CPYDLQ   EQU   *-CMINFIL                                                        
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* KEY BLOCK HEADER DSECT                                              *         
***********************************************************************         
                                                                                
KEYHDRD  DSECT                                                                  
KEYEYE   DS    CL32                                                             
KEYHSIN  DS    XL4                                                              
KEYHKEY  DS    CL64                                                             
KEYHDRL  EQU   256                 L'HEADER FOR KEY MATRIX                      
                                                                                
***********************************************************************         
* KEY BLOCK KEY DETAIL DSECT                                          *         
***********************************************************************         
                                                                                
KEYMTRXD DSECT                                                                  
KEYMKEY  DS    XL32                    MINIO ELEMENT KEY                        
KEYMDA   DS    XL4                     MINIO RECORD D/A                         
KEYMADR  DS    XL4                     A(BUFFER)                                
KEYMSTA  DS    X                       MINIO RECORD STATUS                      
KEYMSDEL EQU   X'80'                   RECORD DELETED                           
KEYMSCHG EQU   X'40'                   RECORD CHANGED AND NOT WRITTEN           
         DS    XL7                                                              
KEYMTRXL EQU   *-KEYMTRXD                                                       
*                                                                               
KEYMAX   EQU   1024                    MAX # OF RECORDS SUPPORTED               
         EJECT                                                                  
***********************************************************************         
* MINIO EXTERNAL PARAMETER LIST                                       *         
***********************************************************************         
                                                                                
       ++INCLUDE DDMINBLK                                                       
         EJECT                                                                  
***********************************************************************         
* SAVED STORAGE (FROM MINWORK)                                        *         
***********************************************************************         
                                                                                
MINBLKD  DSECT                                                                  
         ORG   MINWORK             **MINIO INTERNAL SAVED FIELDS**              
MINELDSP DS    H                   DISP TO 1ST ELEMENT                          
MINDCDSP DS    H                   DISP TO CONTROL BYTES IN DIRECTORY           
MINFCDSP DS    H                   DISP TO CONTROL BYTES IN RECORD              
MINMKLEN DS    H                   MASTER KEY LENGTH (=MINEKDSP)                
MINDADSP DS    H                   DISP TO DISK ADDRESS                         
MINMAXRC DS    H                   MAXIMUM RECORD COUNT                         
MINNEW   DS    C                   Y=NEW MINIO SET                              
*                                                                               
         DS    0F                                                               
CURVALS  DS    0XL17               VALUES FOR CURRENT MINELEM                   
CURBUFA  DS    A                   BUFFER ADDRESS                               
CURMIN   DS    A                   ELEM ADDRESS                                 
CURNEXT  DS    A                   NEXT MINELEM                                 
CURTAB   DS    A                   RECORD TABLE ENTRY                           
CURBUFN  DS    X                   BUFFER NUMBER                                
*                                                                               
BUFFCNT  DS    H                                                                
BUFFNXT  DS    A                                                                
BUFFLEN  DS    F                                                                
*                                                                               
RCUFILD  DS    XL6                 RECUP FILE DEFINITION PARM                   
RCUMODE  DS    X                   RECUP MODE (FE=2BYTE, FD=3BYTE)              
*                                                                               
MNFFSTAT DS    XL20                FF RECORD STATUS BYTES                       
MINWKREM EQU   MINWORK+L'MINWORK-*                                              
*                                                                               
MINBUFFL EQU   MINFRCLM            BUFFER LENGTH (OK AS HALF                    
*                                  EVEN IN 3-BYTE MODE)                         
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
                                                                                
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
*FATCB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'060DDMINIO   01/07/11'                                      
         END                                                                    
