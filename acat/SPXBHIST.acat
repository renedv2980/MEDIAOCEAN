*          DATA SET SPXBHIST   AT LEVEL 007 AS OF 06/20/01                      
*CATALP SPXBHIST                                                                
*INCLUDE DYNALLOC                                                               
         TITLE 'XBHIST - AUDIT TRAIL REPORTING'                                 
         PRINT NOGEN                                                            
XBHIST   CSECT                                                                  
         NMOD1 WORKDLNQ,XBHIST,RR=R5                                            
*                                                                               
         USING WORKD,RC                                                         
         ST    R5,RELO             SAVE RELOCATION VALUE                        
*                                                                               
         L     R1,0(R1)                                                         
         ST    R1,AXBBLK           A(ORIGINAL PARAMETER BLOCK)                  
         MVC   XBBLK,0(R1)         LOCAL COPY OF INPUT PARAMETERS               
         USING XBHISTD,XBBLK                                                    
         L     R9,XBACOMF                                                       
         USING COMFACSD,R9                                                      
*                                                                               
*        XC    XBFLAGS,XBFLAGS    OUTPUT FLAGS MUST BE CLEAR ON INPUT           
         NI    XBFLAGS,XBTEXTQ     CLEAR ALL FLAGS EXCEPT FOR TEXT              
         L     R1,XBAOUT1                                                       
         XC    0(79,R1),0(R1)     CLEAR OUPTUT LINE                             
*                                                                               
         L     R6,XBAHIST          POINT R6 TO VERY FIRST ELEMENT               
         MVI   ELCOUNT,0                                                        
         AHI   R6,42               DISPLACEMENT OF FIRST ELEMENT                
*                                                                               
         ZIC   R1,XBELEM           NUMBER OF ELEMENTS ALREADY DISPLAYED         
         CHI   R1,0                FIRST TIME WE ARE CALLED?                    
         BE    PB10                BRANCH IF YES                                
*                                                                               
PB05     DS    0H                  ADVANCE BY NUMBER OF ELEMENTS                
         BAS   RE,ADVANCE                        SPECIFIED BY XBELEM            
         BCT   R1,PB05                                                          
*                                                                               
PB10     DS    0H                                                               
         CLI   0(R6),HISTELQ       HISTORY ELEMENT?                             
         BE    PB30                IF YES, DISPLAY IT                           
*        CLI   0(R6),HTXTELQ       TEXT ELEMENT?                                
*        BE    PB50                IF YES, DISPLAY IT                           
         BAS   RE,ADVANCE          NEXT ELEMENT                                 
         BNE   PBENDXIT            END OF RECORD? EXIT WITH END FLAG            
         B     PB10                                                             
*                                                                               
PB30     DS    0H                  DISPLAY DATA FROM HISTORY ELEMENT            
         CLI   XBCOUNT,0           ARE WE IN MIDDLE OF SCHED CHANGE?            
         BNE   PB35                IF YES, SKIP COMMON INFO                     
         BAS   RE,DISPCOMM                                                      
         TM    XBFLAGS,XBTEXTQ                                                  
         BO    PBXIT                                                            
*                                                                               
PB35     DS    0H                                                               
         BAS   RE,DISPSPEC         DISPLAY ELEMENT-SPECIFIC INFO                
         CLI   XBCOUNT,0           ARE WE IN MIDDLE OF SCHED CHANGE?            
         BNE   PBXIT               YES, SO SKIP TEXT ELEMENT SEARCH             
                                                                                
         BAS   RE,ADVANCE                                                       
         BNE   PBENDXIT                                                         
         B     PBXIT                                                            
*                                                                               
* DISPLAY TEXT ELEMENT                                                          
*                                                                               
*B50     DS    0H                                                               
*        BAS   RE,DISPTEXT                                                      
*        OI    XBFLAGS,XBTEXTQ     INDICATE TEXT OUTPUT                         
*                                                                               
*        BAS   RE,ADVANCE          ELEMENT AFTER TEXT                           
*        BNE   PBENDXIT                                                         
*        B     PBXIT                                                            
*                                                                               
PBENDXIT DS    0H                                                               
         OI    XBFLAGS,XBENDQ                                                   
PBXIT    DS    0H                                                               
         MVC   XBELEM,ELCOUNT                                                   
*        ST    R6,XBAELEM         ELEMENT TO BE WORKED ON NEXT                  
         L     R1,AXBBLK                                                        
         MVC   0(XBHSTDLQ,R1),XBBLK                                             
*                                                                               
EQXIT    CR    RB,RB                                                            
         B     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*                                                                               
*                                                                               
ADVANCE  DS    0H                                                               
* R6 IS EXPECTED TO ADDRESS AN ELEMENT                                          
* ADVANCES R6 TO NEXT HIST ELEMENT.  UNEQUAL CONDITION AND ZERO ELCOUNT         
* IF END OF RECORD IS REACHED                                                   
* ELCOUNT KEEPS TRACK AT WHAT HIST ELEMENT R6 IS NOW                            
ADV10    DS    0H                                                               
         ZIC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         CLI   0(R6),0                                                          
         BE    ANEQXIT                                                          
         CLI   0(R6),HISTELQ                                                    
         BNE   ADV10                                                            
*                                                                               
AEQXIT   ZIC   R0,ELCOUNT                                                       
         AHI   R0,1                                                             
         STC   R0,ELCOUNT                                                       
         CR    RB,RB                                                            
         BR    RE                                                               
ANEQXIT  MVI   ELCOUNT,0                                                        
         LTR   RB,RB                                                            
         BR    RE                                                               
*                                                                               
*                                                                               
*                                                                               
DISPCOMM NTR1                                                                   
* DISPLAY INFORMATION COMMON TO ALL HISTORY ELEMENTS, SUCH AS                   
* REASON CODE, DATE, TIME, ETC.                                                 
* R6 IS EXPECTED TO ADDRESS A HISTORY ELEMENT                                   
*                                                                               
* CHECK FOR TEXT ELEMENT FOLLOWS                                                
         TM    XBFLAGS,XBTEXTQ                                                  
         BNO   DCOM10                                                           
         NI    XBFLAGS,X'FF'-XBTEXTQ                                            
         B     EQXIT                                                            
*                                                                               
DCOM10   DS    0H                                                               
         ZIC   R1,1(R6)            ELEMENT LENGTH                               
         AR    R1,R6               POINT R1 TO NEXT ELEMENT                     
         CLI   0(R1),HTXTELQ                                                    
         BNE   DCOM20                                                           
         BAS   RE,DISPTEXT                                                      
         OI    XBFLAGS,XBTEXTQ                                                  
*                                                                               
DCOM20   DS    0H                                                               
         L     R2,XBAOUT1                                                       
         USING PLINED,R2                                                        
         USING HISTEL,R6                                                        
*                                                                               
* REASON CODE *                                                                 
         MVC   PREASON,HISTRSN                                                  
*                                                                               
* PID *                                                                         
*        EDIT  (2,HISTPID),PPID,ALIGN=RIGHT                                     
         LA    R5,HISTPID                                                       
         BAS   RE,LDAUTH                                                        
*                                                                               
* TIME *                                                                        
* CONVERT DATE TO YYMMDD FORMAT, IN CASE ADJUSTMENTS ARE NEEDED                 
         GOTO1 CDATCON,DMCB,(2,HISTDATE),(0,TEMPDAT2)                           
         MVC   TEMPDATE,TEMPDAT2                                                
*                                                                               
         ZAP   DUB,=P'0'                                                        
         MVO   DUB,HISTTIME(2)                                                  
*                                                                               
* ADJUST FOR 6-HOUR DIFFERENCE                                                  
         AP    DUB,=PL4'600'                                                    
         CP    DUB,=PL4'2400'                                                   
         BL    DCOM30                                                           
         SP    DUB,=PL4'2400'                                                   
         GOTO1 CADDAY,DMCB,(C'D',TEMPDAT2),TEMPDATE,1                           
DCOM30   DS    0H                                                               
*                                                                               
         CVB   RE,DUB                                                           
         XC    FULL,FULL                                                        
         STCM  RE,3,FULL                                                        
         GOTO1 CCALLOV,DMCB,0,X'D9000A11'  GET UNTIME ADDRESS                   
         L     RF,0(R1)                                                         
         GOTO1 (RF),(R1),FULL,PTIME                                             
*                                                                               
* DATE *                                                                        
         GOTO1 CDATCON,DMCB,(0,TEMPDATE),(11,PDATE)                             
*                                                                               
         B     EQXIT                                                            
         DROP  R2                                                               
*                                                                               
*                                                                               
DISPSPEC NTR1                                                                   
* DISPLAY INFORMATION SPECIFIC TO THAT PARTICULAR CHANGE, SUCH AS               
* COST, PERIOD, ETC                                                             
* R6 IS EXPECTED TO ADDRESS A HISTORY ELEMENT                                   
         USING HISTEL,R6                                                        
         L     R4,XBAOUT1                                                       
         USING PLINED,R4                                                        
*                                                                               
         CLI   HISTTYPE,X'10'      PERIOD                                       
         BE    DIS100                                                           
         CLI   HISTTYPE,X'11'      TIME                                         
         BE    DIS200                                                           
         CLI   HISTTYPE,X'12'      SPOT LENGTH                                  
         BE    DIS300                                                           
         CLI   HISTTYPE,X'13'      COST                                         
         BE    DIS400                                                           
         CLI   HISTTYPE,X'14'      NUMBER OF SPOTS PER WEEK                     
         BE    DIS500                                                           
         CLI   HISTTYPE,X'15'      SCHEDULE                                     
         BE    DIS600                                                           
         B     PBXIT                                                            
*                                                                               
DIS100   DS    0H                  PERIOD                                       
         MVC   PDESC,=CL3'PER'                                                  
         GOTO1 CDATCON,DMCB,(X'13',HISTOLD),(11,POLD)                           
         GOTO1 CDATCON,DMCB,(X'13',HISTOLD+8),(11,PNEW)                         
*                                                                               
*        GOTO1 CCALLOV,DMCB,0,X'D9000A0F'  GET DAYUNPK ADDRESS                  
*        L     RF,0(R1)                                                         
*        GOTO1 (RF),(R1),HISTOLD+7,(X'07',POLD+18)                              
*        GOTO1 (RF),(R1),HISTOLD+15,(X'07',PNEW+18)                             
         B     EQXIT                                                            
*                                                                               
DIS200   DS    0H                  TIME                                         
         MVC   PDESC,=CL3'TIM'                                                  
*                                                                               
         GOTO1 CCALLOV,DMCB,0,X'D9000A11'  GET UNTIME ADDRESS                   
         L     RF,0(R1)                                                         
         GOTO1 (RF),(R1),HISTOLD,POLD                                           
         GOTO1 (RF),(R1),HISTOLD+4,PNEW                                         
         B     EQXIT                                                            
*                                                                               
DIS300   DS    0H                  SPOT LENGTH                                  
         MVC   PDESC,=CL3'SLN'                                                  
         LA    R2,HISTOLD                                                       
         EDIT  (1,0(R2)),(8,POLD),ALIGN=RIGHT                                   
         AHI   R2,1                                                             
         EDIT  (1,0(R2)),(8,PNEW),ALIGN=RIGHT                                   
         B     EQXIT                                                            
*                                                                               
DIS400   DS    0H                  COST                                         
         MVC   PDESC,=CL3'COS'                                                  
         LA    R2,HISTOLD                                                       
         LA    R3,POLD                                                          
         BAS   RE,DISPCOST                                                      
*                                                                               
         ZIC   R0,HISTDLEN         LENGTH OF DATA                               
         AR    R2,R0               ADVANCE TO NEW DATA                          
         LA    R3,PNEW                                                          
         BAS   RE,DISPCOST                                                      
         B     EQXIT                                                            
*                                                                               
DIS500   DS    0H                  SPOTS PER WEEK                               
         MVC   PDESC,=CL3'NPW'                                                  
         ZIC   R2,HISTOLD                                                       
         EDIT  (R2),(8,POLD),ALIGN=RIGHT                                        
         ZIC   R2,HISTOLD+1                                                     
         EDIT  (R2),(8,PNEW),ALIGN=RIGHT                                        
         B     EQXIT                                                            
*                                                                               
******   **    **                  ********                                     
DIS600   DS    0H                  SCHEDULE                                     
******   **    **                  ********                                     
*                                                                               
         ZIC   R2,HISTDLEN         #OF WEEKS TO DISPLAY                         
         GOTO1 CDATCON,DMCB,(2,HISTWEEK),(0,TEMPDATE) START DATE                
         LA    R5,HWOLDCNT         A(FIRST WEEK DATA)                           
*                                                                               
         CLI   XBCOUNT,0          FIRST CALL TO DISPLAY SCHED. CHANGE?          
         BE    DIS610              IF YES, SKIP COMPUTATIONS                    
*                                                                               
         ZIC   R3,XBCOUNT         WEEKS ALREADY DISPLAYED                       
         SR    R2,R3                                                            
         MHI   R3,7                #OF DAYS TO ADD TO START DATE                
         GOTO1 CADDAY,DMCB,(C'D',TEMPDATE),TEMPDAT2,(R3)                        
         MVC   TEMPDATE,TEMPDAT2                                                
         ZIC   R3,XBCOUNT                                                       
         MHI   R3,2                                                             
         AR    R5,R3               A(FIRST WEEK DATA)                           
*                                                                               
         CLC   0(1,R5),1(R5)  NUMBER OF SPOTS CHANGED FOR THAT WEEK?            
         BE    DIS615              IF NOT, DON'T PRINT THAT WEEK                
*                                                                               
DIS610   DS    0H                  PRINT THE DATA                               
*                                                                               
         MVC   PDESC,=CL3'SKD'                                                  
         MVC   POLD(3),=CL3'W/O'                                                
         GOTO1 CDATCON,DMCB,(0,TEMPDATE),(12,POLD+4)                            
         MVC   POLD+13(3),=CL3'OLD'                                             
         EDIT  (1,0(R5)),(2,POLD+17),ZERO=NOBLANK                               
*        GOTO1 CDATCON,DMCB,(0,TEMPDATE),(11,PNEW)                              
         MVC   POLD+20(3),=CL3'NEW'                                             
         EDIT  (1,1(R5)),(2,POLD+25),ZERO=NOBLANK                               
         B     DIS616                                                           
*                                                                               
DIS615   DS    0H                                                               
         OI    XBFLAGS,XBNOPRTQ                                                 
*                                                                               
DIS616   DS    0H                                                               
         ZIC   R2,HISTDLEN         #OF WEEKS TO DISPLAY                         
         ZIC   R3,XBCOUNT                                                       
         AHI   R3,1                                                             
         CR    R2,R3               COUNT ? #OF WEEKS                            
         BNE   DIS620                                                           
         MVI   XBCOUNT,0                                                        
         B     EQXIT                                                            
*                                                                               
DIS620   DS    0H                                                               
         STC   R3,XBCOUNT         STORE WEEK COUNTER                            
         B     EQXIT                                                            
*                                                                               
         DROP  R4                                                               
         DROP  R6                                                               
*                                                                               
*                                                                               
DISPTEXT NTR1                                                                   
* DISPLAYS TEXT IN TEXT ELEMENT                                                 
* R1 IS EXPECTED TO ADDRESS TEXT ELEMENT                                        
         L     R2,XBAOUT1                                                       
         USING HTXTEL,R1                                                        
         USING PLINED,R2                                                        
*                                                                               
         ZIC   R4,HTXTELLN                                                      
         SHI   R4,2                   ADJUST FOR ELEMENT NUM, LENGTH            
         CLI   HTXTELLN,40                                                      
         BNH   *+8                                                              
         LHI   R4,40                                                            
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   POLD(0),HTXT                                                     
         DROP  R1                                                               
         DROP  R2                                                               
         B     EQXIT                                                            
*                                                                               
*                                                                               
DISPCOST NTR1                                                                   
* DISPLAYS COST FROM HISTORY ELEMENT                                            
* R2 IS EXPECTED TO ADDRESS COST DATA                                           
* R3 IS EXPECTED TO ADDRESS OUTPUT AREA                                         
         SR    R0,R0                                                            
         ICM   R0,7,0(R2)          LOAD COST                                    
*                                                                               
         TM    4(R2),X'20'         TEST CANADA (BDCIND2)                        
         BNO   DCOS10                                                           
         TM    4(R2),X'01'         TEST IF RATE IN PENNIES                      
         BO    DCOS50              YES - DISPLAY COST                           
         B     DCOS30              NO - FIRST CONVERT THEN DISPLAY              
*                                                                               
DCOS10   DS    0H                  US HERE                                      
         TM    4(R2),X'10'         TEST IF RATE IN DOLLARS                      
         BZ    DCOS50              NO, DISPLAY COST                             
*                                                                               
DCOS30   DS    0H                                                               
         MHI   R0,100              CONVERT TO PENNIES                           
DCOS50   DS    0H                                                               
         EDIT  (R0),(8,(R3)),2,ALIGN=RIGHT                                      
         B     EQXIT                                                            
*                                                                               
*                                                                               
LDAUTH   NTR1                                                                   
* R5 IS EXPECTED TO ADDRESS HITSPID                                             
*                                                                               
*                                                                               
         L     R7,XBAHIST                                                       
         USING HISTRECD,R7                                                      
         L     R2,XBAOUT1                                                       
         USING PLINED,R2                                                        
*                                                                               
         LA    R0,IO                                                            
         ST    R0,AREC                                                          
         OC    0(3,R5),0(R5)                                                    
         BZ    EXIT                                                             
         XC    WORK(25),WORK                                                    
         MVI   WORK,C'0'                                                        
         MVC   WORK+1(2),XBSECAGY  SET SECURITY AGENCY                          
         CLC   XBSECAGY,=CL2'  '                                                
         BH    *+10                                                             
         MVC   WORK+1(2),HISTAGYA                                               
         MVC   WORK+23(2),0(R5)                                                 
*                                                                               
LDAUTH1  GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',WORK,AREC                    
         L     R8,AREC                                                          
         CLC   WORK(25),0(R8)                                                   
         BNE   LDAUTH4                                                          
         LA    RE,27(R8)                                                        
*                                                                               
LDAUTH2  CLC   =X'0318',0(RE)                                                   
         BE    LDAUTH6                                                          
         CLC   =X'C30A',0(RE)       - NEW SECURITY - PERSON ELEMENT             
         BE    LDAUTH3                                                          
         CLC   =X'030C',0(RE)                                                   
         BE    LDAUTH2A                                                         
         ZIC   R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         BNE   LDAUTH2                                                          
         B     LDAUTH4                                                          
*                                                                               
LDAUTH2A XC    WORK(25),WORK                                                    
         MVI   WORK,C'0'                                                        
         MVC   WORK+1(2),XBSECAGY  SET SECURITY AGENCY                          
         CLC   XBSECAGY,=CL2'  '                                                
         BH    *+10                                                             
         MVC   WORK+1(2),HISTAGYA                                               
         MVC   WORK+15(10),2(RE)                                                
         B     LDAUTH1                                                          
*                                                                               
LDAUTH3  MVC   PPID(8),2(RE)    SIGN ON ID                                      
         XC    WORK(25),WORK                                                    
         MVI   WORK,C'F'           GET PERSON RECORD                            
         MVI   WORK+1,X'04'                                                     
         MVC   WORK+13(2),XBSECAGY SET SECURITY AGENCY                          
         CLC   XBSECAGY,=CL2'  '                                                
         BH    *+10                                                             
         MVC   WORK+13(2),HISTAGYA                                              
         MVC   WORK+15(8),2(RE)                                                 
         GOTO1 CDATAMGR,DMCB,=C'DMRDHI',=C'CTFILE',WORK,AREC                    
         L     R8,AREC                                                          
         CLC   WORK(23),0(R8)                                                   
         BNE   LDAUTH8                                                          
         LA    RE,27(R8)                                                        
*                                                                               
LDAUTH3A CLI   0(RE),X'C6'                                                      
         BE    LDAUTH3B                                                         
         ZIC   R0,1(RE)                                                         
         AR    RE,R0                                                            
         CLI   0(RE),0                                                          
         BNE   LDAUTH3A                                                         
         B     LDAUTH8                                                          
*                                                                               
LDAUTH3B DS    0H                                                               
*        MVC   PPID(2),2(RE)    OFFICE                                          
         B     LDAUTH8                                                          
*                                                                               
LDAUTH4  DS    0H                                                               
*        MVC   PPID(10),=CL10'*UNKNOWN*'                                        
         B     LDAUTH8                                                          
*                                                                               
LDAUTH6  DS    0H                                                               
*        MVC   ELEM+20(2),2(RE)    OFFICE                                       
*        MVC   PPID(10),4(RE)      LAST NAME                                    
*        MVC   ELEM+23(18),4(RE)   LAST NAME                                    
*        MVC   ELEM+42(1),22(RE)   FIRST INIT                                   
*        MVC   ELEM+44(1),23(RE)                                                
*                                                                               
LDAUTH8  DS    0H                                                               
*        GOTO1 VDATCON,DMCB,(3,2(R5)),(5,ELEM+10)                               
* GET SQUASHER ADDRESS                                                          
*        MVC   DMCB+4(4),=X'D9000A0D'                                           
*        GOTO1 VCALLOV,DMCB,0                                                   
*        L     RF,DMCB                                                          
*        GOTO1 (RF),(R1),ELEM+10,35                                             
         B     EXIT                                                             
         DROP  R7                                                               
         DROP  R2                                                               
         EJECT                                                                  
*                                                                               
*                                                                               
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
RELO     DS    A                                                                
AREC     DS    A                                                                
XBBLK    DS    XL(XBHSTDLQ)                                                     
AXBBLK   DS    A                                                                
ELCOUNT  DS    X                                                                
HALF     DS    H                                                                
TEMPDATE DS    XL6                                                              
TEMPDAT2 DS    XL6                                                              
IO       DS    XL4096                                                           
WORKDLNQ EQU   *-WORKD                                                          
*                                                                               
       ++INCLUDE SPGENHIST                                                      
         EJECT                                                                  
       ++INCLUDE SPXBHISTD                                                      
       ++INCLUDE DDCOMFACS                                                      
         PRINT  ON                                                              
         SPACE 2                                                                
*                                                                               
PLINED   DSECT                                                                  
PREASON  DS    CL6                                                              
         DS    CL1                                                              
PDESC    DS    CL3                                                              
         DS    CL1                                                              
POLD     DS    XL20                                                             
         DS    CL1                                                              
PNEW     DS    XL20                                                             
         DS    CL1                                                              
PPID     DS    CL10                                                             
         DS    CL1                                                              
PDATE    DS    CL8                                                              
         DS    CL1                                                              
PTIME    DS    CL5                                                              
PLINEDLQ EQU   *-PLINED                                                         
*                                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007SPXBHIST  06/20/01'                                      
         END                                                                    
