*          DATA SET MXPRNT     AT LEVEL 079 AS OF 03/28/13                      
*CATALP MXPRNT                                                                  
         TITLE 'MXPRNT- PRINTING MODULE FOR SPREPMX02/PPREPMX02'                
         SPACE 2                                                                
*********************************************************************           
*        PARM 1  BYTE 0     NOT USED                                *           
*                BYTE 1-3   A(CONTROL DSECT) SEE MXPRTD             *           
*********************************************************************           
         SPACE 2                                                                
MXPRNT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,MXPRT,R9,R8,R5                                            
*                                                                               
         USING WORKD,RC                                                         
         L     R7,0(R1)            A(CONTROL DSECT)                             
         USING MXPRNTD,R7                                                       
*                                                                               
         RELOC RELO                                                             
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         L     R1,=A(BUFFALOC)    SET BUFFALO ADDRESS                           
         A     R1,RELO                                                          
         ST    R1,BUFFBUFF                                                      
*                                                                               
         L     RE,PHEAD                                                         
         L     R1,=A(MYHOOK)      SET HEADHOOK ADDRESS                          
         A     R1,RELO                                                          
         ST    R1,0(RE)                                                         
         EJECT                                                                  
*                                                                               
         CLI   PMODE,PMINIT        INITIALIZATION MODE                          
         BE    PINT                                                             
         CLI   PMODE,PMBILL        PRINT A BILL?                                
         BE    PRTBILL                                                          
         CLI   PMODE,PMMSG         PRINT A MESSAGE?                             
         BE    PRTMSG                                                           
         CLI   PMODE,PMTOTAL       PRINT A TOTAL?                               
         BE    PRTTOT                                                           
         CLI   PMODE,PMBILERR      PRINT BILLS IN ERROR?                        
         BE    PBILERR                                                          
         CLI   PMODE,PMRECAP       PRINT RECAPS?                                
         BE    PRECAP                                                           
         B     EXIT                                                             
         SPACE 1                                                                
YES      XR    RC,RC                                                            
NO       LTR   RC,RC                                                            
EXIT     XIT1                                                                   
         EJECT                                                                  
*--------------------------------------------*                                  
* PINT   - INITIALIZATION MODE               *                                  
*          INITIAL CALL TO SORTER AND OPEN   *                                  
*          ERRFIL                            *                                  
*--------------------------------------------*                                  
*                                                                               
PINT     L     RE,PRCSUBP                                                       
         MVI   0(RE),30           US VERSION OF DETAIL REPORT                   
         CLI   PCOUNTRY,C'C'                                                    
         BNE   *+8                                                              
         MVI   0(RE),20           CANADIAN VERSION OF DETAIL REPORT             
*                                                                               
         OPEN  (ERRFIL,OUTPUT)    OPEN FILE FOR BILLS IN ERROR                  
         LTR   RF,RF              IF ERROR RETURNED                             
         BZ    PINT5                                                            
         STC   RF,BYTE                                                          
         CLI   BYTE,8             AND IT'S NOT FILE ALREADY OPENED              
         BE    *+6                                                              
         DC    H'0'               DIE                                           
         CLOSE (ERRFIL,)          ELSE, CLOSE THE FILE                          
         OPEN  (ERRFIL,OUTPUT)    AND TRY TO REOPEN AGAIN                       
         LTR   RF,RF                                                            
         BZ    PINT5                                                            
         DC    H'0'               STILL DOESN'T WORK - DIE DIE DIE              
*                                                                               
PINT5    XC    MYBUFFIO,MYBUFFIO  SET BUFFALO                                   
         GOTO1 PBUFFALO,DMCB,=C'SET',BUFFBUFF                                   
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------------------*         
*  PRTBILL - PRINTS OUT A LINE OF BILL INFO                           *         
*            IF BILL NOT IN ERROR - ADDS TO TOTAL                     *         
*            IF BILL IN ERROR - PRINTS POSTINGS & ADDS BILL TO ERRFIL *         
*---------------------------------------------------------------------*         
*                                                                               
PRTBILL  MVI   REPTYPE,C'D'       DETAIL REPORT                                 
         BAS   RE,PRTBLINE        PRINT BILL LINE INFO                          
         CLI   PREV,C'Y'                                                        
         BNE   PRTBIL3                                                          
         CLI   PREVERR,0                                                        
         BE    PRTBIL10           REVERSAL OKAY - ADD TO TOTAL                  
         B     PRTBILLX           BAD REVERSAL - GET OUT                        
*                                                                               
PRTBIL3  CLI   PPOSTERR,0         ANY ERRORS IN POSTINGS FOR BILL               
         BNE   PRTBIL5                                                          
         CLI   PPOSTER2,0                                                       
         BNE   PRTBIL5                                                          
         CLI   PPOSTERC,0                                                       
         BNE   PRTBIL5            NOPE                                          
         CLI   PPOSTER3,0                                                       
         BNE   PRTBIL5                                                          
         CLI   PAORMSG,C'Y'                                                     
         BE    PRTBIL10                                                         
*                                                                               
PRTBIL5  BAS   RE,PRTPOST         PRINT OUT POSTINGS                            
         BAS   RE,BILLERR         ADD BILL TO ERROR FILE                        
         B     PRTBILLX           AND EXIT                                      
*                                                                               
PRTBIL10 BAS   RE,BILLTOT         ADD NO ERROR BILL TO TOTAL                    
*                                                                               
PRTBILLX B     EXIT                                                             
         EJECT                                                                  
*--------------------------------------------*                                  
* PRTBLINE- PRINT ONE LINE OF BILL DETAILS   *                                  
*--------------------------------------------*                                  
PRTBLINE NTR1                                                                   
         L     R2,PP1                                                           
         USING LIND,R2                                                          
         BAS   RE,PSPACES         PRE-CLEAR 3 PRINT LINES TO SPACES             
         BAS   RE,SETTYPE         SET BILL TYPE                                 
         CLI   PSYS,C'P'          PRINT BILL TYPE                               
         BNE   *+12                                                             
         BAS   RE,SETPRT          SET PRINT BILL REC INFO                       
         B     PRTBL30                                                          
         BAS   RE,SETSAN          SET SPOT/NET BILL REC INFO                    
*                                                                               
PRTBL30  MVC   LPRD,PRD           PRODUCT                                       
         MVC   LEST,EST           MOVE TO PRINT LINE                            
         MVC   LINV,PINVOICE      INVOICE NUMBER                                
         MVC   LINVDT,INVDT       MOVE INVOICE DATE TO PRINT                    
         MVC   LINVTYP,INVTYPE    AOR TYPE IF APPLICABLE                        
         MVC   LDUEDT,DUEDT       MOVE DUE DATE TO PRINT                        
         MVC   LMOS,MOS           PRINT OUT MONTH OF SERVICE MMMYY              
         MVC   LMOA,MOA           PRINT OUT MOA MMMYY                           
         BAS   RE,SETAMTS         SET ALL AMOUNTS                               
         EDIT  RCVAMT,(12,LRCVBL),2,FLOAT=-                                     
         EDIT  GRSAMT,(12,LGROSS),2,FLOAT=-                                     
PRTBL40  EDIT  NETAMT,(12,LNET),2,FLOAT=-                                       
         CLI   PSYS,C'P'                                                        
         BE    PRTBL80                                                          
         EDIT  AGYCOM,(11,LAGYCOM),2,FLOAT=-                                    
         CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTBL70                                                          
         MVC   TEMPAMT,GSTAMT                                                   
         BAS   RE,CHKGST                                                        
         BNE   PRTBL60            NO AMOUNT                                     
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LGST,=8C'*'                                                      
         B     PRTBL60                                                          
         EDIT  GSTAMT,(8,LGST),2,FLOAT=-                                        
PRTBL60  MVC   LACCOFF,PACCOFF    ACC OFFICE                                    
         OC    PSTAMT,PSTAMT                                                    
         BZ    PRTBL65                                                          
         EDIT  PSTAMT,(8,LPST),2,FLOAT=-                                        
PRTBL65  CLI   PREV,C'Y'                                                        
         BNE   PRTBL90                                                          
         MVC   LREV,=C'REV'                                                     
         CLI   PREVERR,0                                                        
         BE    PRTBL90                                                          
         MVC   LREV,=C'ERR'                                                     
         B     PRTBL90                                                          
*                                                                               
PRTBL70  MVC   LACCOFF2,PACCOFF   ACC OFFICE                                    
         CLI   PREV,C'Y'                                                        
         BNE   PRTBL90                                                          
         MVC   LREV2(3),=C'REV'                                                 
         CLI   PREVERR,0                                                        
         BE    PRTBL90                                                          
         MVC   LREV2,=C'ERR'                                                    
         B     PRTBL90                                                          
         EJECT                                                                  
PRTBL80  OC    CDAMT,CDAMT                                                      
         BZ    PRTBL81                                                          
         EDIT  CDAMT,(7,LPCD),2,FLOAT=-                                         
PRTBL81  EDIT  AGYCOM,(11,LPAGYCM),2,FLOAT=-                                    
         CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTBL85                                                          
         MVC   TEMPAMT,GSTAMT                                                   
         BAS   RE,CHKGST                                                        
         BNE   PRTBL82                                                          
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LPGST,=8C'*'                                                     
         B     PRTBL82                                                          
         EDIT  GSTAMT,(8,LPGST),2,FLOAT=-                                       
PRTBL82  OC    PSTAMT,PSTAMT                                                    
         BZ    PRTBL84                                                          
         EDIT  PSTAMT,(8,LPPST),2,FLOAT=-                                       
PRTBL84  CLI   PSYS,C'P'                                                        
         BNE   *+10                                                             
         MVC   LPROD,PJBCD                                                      
         MVC   LPAOFF,PACCOFF    ACC OFFICE                                     
         CLI   PREV,C'Y'                                                        
         BNE   PRTBL90                                                          
         MVC   LPREV,=C'REV'                                                    
         CLI   PREVERR,0                                                        
         BE    PRTBL90                                                          
         MVC   LPREV,=C'ERR'                                                    
         B     PRTBL90                                                          
*                                                                               
PRTBL85  CLI   PSYS,C'P'                                                        
         BNE   *+10                                                             
         MVC   LPROD2,PJBCD                                                     
         MVC   LPAOFF2,PACCOFF   ACC OFFICE                                     
         CLI   PREV,C'Y'                                                        
         BNE   PRTBL90                                                          
         MVC   LPREV2,=C'REV'                                                   
         CLI   PREVERR,0                                                        
         BE    PRTBL90                                                          
         MVC   LPREV2,=C'ERR'                                                   
*                                                                               
PRTBL90  GOTO1 PREPORT            PRINT LINE                                    
         OC    IORAMT,IORAMT      IF IOR AMOUNT                                 
         BZ    *+8                                                              
         BAS   RE,PRTIOR          PRINT IOR FOR SPOT,NET AND PRINT              
         CLC   PAOR,=C'NNNN'                                                    
         BE    PRTBLX                                                           
         CLI   PSYS,C'P'          PRINT SYS?                                    
         BE    *+12                                                             
         BAS   RE,SPTTAOR         SPOT/NET SYS - TRUE AOR LINE                  
         B     EXIT                                                             
         BAS   RE,PRTTAOR         PRINT SYS - TRUE AOR LINE                     
PRTBLX   B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*--------------------------*                                                    
* SETTYPE - SETS BILL TYPE *                                                    
*--------------------------*                                                    
*                                                                               
SETTYPE  NTR1                                                                   
         XC    SUBTYPE,SUBTYPE                                                  
         L     RE,PBILL            RE=A(BILL RECORD)                            
         USING BILLRECD,RE                                                      
         CLI   PSYS,C'P'                                                        
         BNE   SETTYP10                                                         
         MVC   TYPEBIL,=C'AOR'    AOR                                           
         TM    PBILCMSW,X'20'                                                   
         BO    SETTYPX                                                          
         TM    PBILCMSW,X'10'                                                   
         BNO   SETTYP1                                                          
         TM    PBILCMSW,X'01'                                                   
         BNO   *+14                                                             
         MVC   SUBTYPE,=C'UAC'                                                  
         B     SETTYPX                                                          
         TM    PBILCMSW,X'08'                                                   
         BNO   SETTYPX                                                          
         MVC   SUBTYPE,=C'UAN'                                                  
         B     SETTYPX                                                          
*                                                                               
SETTYP1  MVC   TYPEBIL,=C'REG'    REGULAR                                       
         TM    PBILCMSW,X'01'                                                   
         BNO   *+14                                                             
         MVC   SUBTYPE,=C'UC '                                                  
         B     SETTYP1A                                                         
         TM    PBILCMSW,X'08'                                                   
         BNO   SETTYP1A                                                         
         MVC   SUBTYPE,=C'UN '                                                  
*                                                                               
SETTYP1A LA    R1,PBILLEL                                                       
         USING PBILOTH,R1                                                       
         SR    R0,R0                                                            
SETTYP2  CLI   0(R1),0                                                          
         BE    SETTYPX                                                          
         CLI   0(R1),X'09'                                                      
         BE    SETTYP4                                                          
         ICM   R0,1,1(R1)                                                       
         AR    R1,R0                                                            
         B     SETTYP2                                                          
SETTYP4  TM    PBILLIND,X'01'                                                   
         BNO   SETTYPX                                                          
         MVC   TYPEBIL,=C'FIN'    FINANCIAL                                     
         B     SETTYPX                                                          
         DROP  R1                                                               
         EJECT                                                                  
SETTYP10 MVC   TYPEBIL,=C'BPC'                                                  
         TM    BILSTAT3,BSTREGQ+BSTLMGQ   LMG/REGNL BILLING?                    
         BNZ   SETTYPX                                                          
         MVC   TYPEBIL,=C'AOR'                                                  
         TM    BILSTAT,X'20'                                                    
         BO    SETTYPX                                                          
         TM    BILSTAT,X'10'                                                    
         BNO   SETTYP20                                                         
         TM    BILSTAT,BSTSCOMQ                                                 
         BNO   *+14                                                             
         MVC   SUBTYPE,=C'UAC'                                                  
         B     SETTYPX                                                          
         TM    BILSTAT,BSTSNETQ                                                 
         BNO   SETTYPX                                                          
         MVC   SUBTYPE,=C'UAN'                                                  
         B     SETTYPX                                                          
*                                                                               
SETTYP20 MVC   TYPEBIL,=C'REG'                                                  
         TM    BILSTAT,BSTSCOMQ                                                 
         BNO   *+14                                                             
         MVC   SUBTYPE,=C'UC '                                                  
         B     SETTYPX                                                          
         TM    BILSTAT,BSTSNETQ                                                 
         BNO   *+14                                                             
         MVC   SUBTYPE,=C'UN '                                                  
         B     SETTYPX                                                          
         TM    BILSTAT2,BSTAMAQ                                                 
         BNO   SETTYPX                                                          
         MVC   SUBTYPE,=C'D  '                                                  
*                                                                               
SETTYPX  B     EXIT                                                             
         DROP  RE                                                               
         EJECT                                                                  
*----------------------------------------------------------------*              
* SETPRT  - GRABS NECESSARY INFO FOR LINE FROM PRINT BILL RECORD *              
*----------------------------------------------------------------*              
*                                                                               
SETPRT   NTR1                                                                   
         L     R6,PBILL           A(BILL RECORD)                                
         USING BILLRECD,R6                                                      
         MVC   PRD,PBILKPRD       PRODUCT                                       
         SR    R0,R0              ESTIMATE NUMBER                               
         ICM   R0,3,PBILKEST                                                    
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EST,DUB+6(2)                                                     
*                                                                               
         MVI   INVTYPE,C' '                                                     
         TM    PBILCMSW,X'10'                                                   
         BNO   SETPRT10                                                         
         MVI   INVTYPE,C'M'       MAIN AOR                                      
         TM    PBILCMSW,X'01'                                                   
         BNO   *+12                                                             
         MVI   INVTYPE,C'U'                                                     
         B     SETPRT10                                                         
         TM    PBILCMSW,X'08'                                                   
         BNO   *+12                                                             
         MVI   INVTYPE,C'N'                                                     
         B     SETPRT10                                                         
         CLI   PBILSEP,C'C'                                                     
         BNE   SETPRT5                                                          
         MVI   INVTYPE,C'C'       CASH DISC AOR                                 
         B     SETPRT10                                                         
SETPRT5  CLI   PBILSEP,C'A'                                                     
         BNE   SETPRT10                                                         
         MVI   INVTYPE,C'J'       ADJ AOR                                       
*                                                                               
SETPRT10 GOTO1 PDTCN,DMCB,(3,PBILINVD),(4,INVDT)                                
         GOTO1 PDTCN,DMCB,(3,PBILDUED),(8,DUEDT)                                
         XC    WORK,WORK                                                        
         MVC   WORK(2),PBILKMOS                                                 
         MVI   WORK+2,X'01'       FUDGE DAY                                     
         GOTO1 PDTCN,DMCB,(3,WORK),(6,WORK+6)                                   
         MVC   MOS(3),WORK+6                                                    
         CLI   PBILKMOS+1,X'0D'   IF SPECIAL 13TH MONTH                         
         BNE   *+10                                                             
         MVC   MOS(3),=C'13/'     USE 13/                                       
         MVC   MOS+3(2),WORK+10                                                 
         XC    MOA,MOA                                                          
         OC    PMOA,PMOA                                                        
         BZ    SETPRTX                                                          
         GOTO1 PDTCN,DMCB,(0,PMOA),(6,WORK+6)                                   
         MVC   MOA(3),WORK+6                                                    
         MVC   MOA+3(2),WORK+10                                                 
*                                                                               
SETPRTX  B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
*----------------------------------------------------------------*              
* SETSAN  - GRABS NECESSARY INFO FOR LINE FROM SPOT/NET BILL REC *              
*----------------------------------------------------------------*              
*                                                                               
SETSAN   NTR1                                                                   
         L     R6,PBILL           A(BILL RECORD)                                
         USING BILLRECD,R6                                                      
         MVC   PRD,BKEYPRD        PRODUCT                                       
         ZIC   R0,BKEYEST                                                       
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  EST,DUB            ESTIMATE NUMBER                               
*                                                                               
         MVI   INVTYPE,C' '                                                     
         TM    BILSTAT,X'10'                                                    
         BNO   SETSAN10                                                         
         MVI   INVTYPE,C'M'       MAIN AOR                                      
         TM    BILSTAT,BSTSADJQ                                                 
         BNO   *+12                                                             
         MVI   INVTYPE,C'J'                                                     
         B     SETSAN10                                                         
         TM    BILSTAT,BSTSCOMQ                                                 
         BNO   *+12                                                             
         MVI   INVTYPE,C'U'                                                     
         B     SETSAN10                                                         
         TM    BILSTAT,BSTSNETQ                                                 
         BNO   *+12                                                             
         MVI   INVTYPE,C'N'                                                     
         B     SETSAN10                                                         
*                                                                               
SETSAN10 GOTO1 PDTCN,DMCB,(0,BQDATE),(4,INVDT)                                  
         GOTO1 PDTCN,DMCB,(3,BDUEDATE),(8,DUEDT)                                
         XC    WORK,WORK                                                        
         MVC   WORK(2),BKEYYSRV                                                 
         MVI   WORK+2,X'01'       FUDGE DAY                                     
         GOTO1 PDTCN,DMCB,(3,WORK),(6,WORK+6)                                   
         MVC   MOS(3),WORK+6                                                    
         CLI   BKEYYSRV+1,X'0D'   IF SPECIAL 13TH MONTH                         
         BNE   *+10                                                             
         MVC   MOS(3),=C'13/'     USE 13/                                       
         MVC   MOS+3(2),WORK+10                                                 
         XC    MOA,MOA                                                          
         OC    PMOA,PMOA                                                        
         BZ    SETSANX                                                          
         GOTO1 PDTCN,DMCB,(0,PMOA),(6,WORK+6)                                   
         MVC   MOA(3),WORK+6                                                    
         MVC   MOA+3(2),WORK+10                                                 
SETSANX  B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
*----------------------------------------*                                      
* SPTTAOR - PRINT SPOT/NET TRUE AOR BILL *                                      
*----------------------------------------*                                      
*                                                                               
SPTTAOR  NTR1                                                                   
         L     R2,PP1                                                           
         USING LIND,R2                                                          
         MVC   LPRD,PRD           PRODUCT                                       
         MVC   LEST,EST           MOVE TO PRINT LINE                            
         MVC   LINV,PTINV         INVOICE NUMBER                                
         MVC   LINVDT,INVDT       TRUE AOR INVOICE NUMBER                       
         MVI   LINVTYP,C'A'       INDICATE AOR INVOICE                          
         MVC   LDUEDT,DUEDT       MOVE DUE DATE TO PRINT                        
         MVC   LMOS,MOS           PRINT OUT MONTH OF SERVICE MMMYY              
         MVC   LMOA,MOA           PRINT OUT MOA MMMYY                           
         OC    AORAGYCM,AORAGYCM                                                
         BZ    SPTTA10                                                          
         ZAP   DUB,AORAGYCM                                                     
         CLI   PCOUNTRY,C'C'      IF U.S. 'SR' ACCOUNT                          
         BE    SPTTA05                                                          
         CLI   USSRACC,C'Y'                                                     
         BE    *+10                                                             
SPTTA05  MP    DUB,=P'-1'         ALWAYS SUBTRACT OUT AOR AGY COM               
         EDIT  (P8,DUB),(11,LAGYCOM),2,FLOAT=-                                  
*                                                                               
SPTTA10  CLI   PCOUNTRY,C'C'                                                    
         BNE   SPTTA30                                                          
         OC    GSTIAMT,GSTIAMT    ****  CANADIAN VERSION OF REPORT              
         BZ    SPTTA13                                                          
         ZAP   DUB,GSTIAMT                                                      
         MP    DUB,=P'-1'                                                       
         MVC   TEMPAMT,DUB                                                      
         BAS   RE,CHKGST                                                        
         BNE   SPTTA13                                                          
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LGST,=8C'*'                                                      
         B     SPTTA13                                                          
         EDIT  (P8,DUB),(8,LGST),2,FLOAT=-                                      
*                                                                               
SPTTA13  OC    PSTIAMT,PSTIAMT                                                  
         BZ    SPTTA15                                                          
         ZAP   DUB,PSTIAMT                                                      
         MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(8,LPST),2,FLOAT=-                                      
*                                                                               
SPTTA15  OC    AORAMT,AORAMT                                                    
         BZ    SPTTA20                                                          
         ZAP   DUB,AORAMT                                                       
         CLI   FLIPAOR,C'Y'                                                     
         BNE   *+10                                                             
         MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(10,LAORPAY),2,FLOAT=-                                  
*                                                                               
SPTTA20  MVC   LACCOFF,PACCOFF    ACC OFFICE                                    
         CLI   PREV,C'Y'                                                        
         BNE   SPTTA25                                                          
         MVC   LREV,=C'REV'                                                     
         CLI   PREVERR,0                                                        
         BE    *+10                                                             
         MVC   LREV,=C'ERR'                                                     
SPTTA25  GOTO1 PREPORT                                                          
         B     EXIT                                                             
*                                                                               
SPTTA30  OC    AORAMT,AORAMT      *****  U.S. VERSION OF REPORT                 
         BZ    SPTTA40                                                          
         ZAP   DUB,AORAMT                                                       
         CLI   FLIPAOR,C'Y'                                                     
         BNE   *+10                                                             
         MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(10,LAORPAY2),2,FLOAT=-                                 
*                                                                               
SPTTA40  MVC   LACCOFF2,PACCOFF    ACC OFFICE                                   
         CLI   PREV,C'Y'                                                        
         BNE   SPTTA45                                                          
         MVC   LREV2,=C'REV'                                                    
         CLI   PREVERR,0                                                        
         BE    *+10                                                             
         MVC   LREV2,=C'ERR'                                                    
SPTTA45  GOTO1 PREPORT                                                          
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*-----------------------------------------*                                     
* PRTTAOR - PRINT PRINT SYS TRUE AOR BILL *                                     
*-----------------------------------------*                                     
*                                                                               
PRTTAOR  NTR1                                                                   
         L     R2,PP1                                                           
         USING LIND,R2                                                          
         MVC   LPRD,PRD           PRODUCT                                       
         MVC   LEST,EST           MOVE TO PRINT LINE                            
         MVC   LINV,PTINV        INVOICE NUMBER                                 
         MVC   LINVDT,INVDT       TRUE AOR INVOICE NUMBER                       
         MVI   LINVTYP,C'A'       INDICATE AOR INVOICE                          
         MVC   LDUEDT,DUEDT       MOVE DUE DATE TO PRINT                        
         MVC   LMOS,MOS           PRINT OUT MONTH OF SERVICE MMMYY              
         MVC   LMOA,MOA           PRINT OUT MOA MMMYY                           
         OC    AORAGYCM,AORAGYCM                                                
         BZ    PRTTA10                                                          
         ZAP   DUB,AORAGYCM                                                     
         CLI   PCOUNTRY,C'C'      IF U.S. 'SR' ACC - DON'T X-1                  
         BE    PRTTA05                                                          
         CLI   USSRACC,C'Y'                                                     
         BE    *+10                                                             
PRTTA05  MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(11,LPAGYCM),2,FLOAT=-                                  
*                                                                               
PRTTA10  CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTTA30                                                          
         OC    GSTIAMT,GSTIAMT    CANADIAN                                      
         BZ    PRTTA13                                                          
         ZAP   DUB,GSTIAMT                                                      
         MP    DUB,=P'-1'                                                       
         MVC   TEMPAMT,DUB                                                      
         BAS   RE,CHKGST                                                        
         BNE   PRTTA13                                                          
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LGST,=8C'*'                                                      
         B     PRTTA13                                                          
         EDIT  (P8,DUB),(8,LPGST),2,FLOAT=-                                     
*                                                                               
PRTTA13  OC    PSTIAMT,PSTIAMT                                                  
         BZ    PRTTA15                                                          
         ZAP   DUB,PSTIAMT                                                      
         MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(8,LPPST),2,FLOAT=-                                     
*                                                                               
PRTTA15  OC    AORAMT,AORAMT      CANADIAN                                      
         BZ    PRTTA20                                                          
         ZAP   DUB,AORAMT                                                       
         CLI   FLIPAOR,C'Y'                                                     
         BNE   *+10                                                             
         MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(10,LPAOR),2,FLOAT=-                                    
*                                                                               
PRTTA20  MVC   LPAOFF,PACCOFF    ACC OFFICE                                     
         CLI   PREV,C'Y'                                                        
         BNE   PRTTA25                                                          
         MVC   LPREV,=C'REV'                                                    
         CLI   PREVERR,0                                                        
         BE    *+10                                                             
         MVC   LPREV,=C'ERR'                                                    
PRTTA25  GOTO1 PREPORT                                                          
         B     EXIT                                                             
*                                                                               
PRTTA30  OC    AORAMT,AORAMT      U.S.                                          
         BZ    PRTTA40                                                          
         ZAP   DUB,AORAMT                                                       
         CLI   FLIPAOR,C'Y'                                                     
         BNE   *+10                                                             
         MP    DUB,=P'-1'                                                       
         EDIT  (P8,DUB),(10,LPAOR2),2,FLOAT=-                                   
PRTTA40  MVC   LPAOFF2,PACCOFF    ACC OFFICE                                    
         CLI   PREV,C'Y'                                                        
         BNE   PRTTA45                                                          
         MVC   LPREV2,=C'REV'                                                   
         CLI   PREVERR,0                                                        
         BE    *+10                                                             
         MVC   LPREV2,=C'ERR'                                                   
PRTTA45  GOTO1 PREPORT                                                          
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO PRINT IOR LINE                                        
*                                                                               
PRTIOR   NTR1                                                                   
         L     R2,PP1                                                           
         USING LINIORD,R2                                                       
         MVC   LIPRD,PRD           PRODUCT                                      
         MVC   LIEST,EST           ESTIMATE NUMBER                              
         MVC   LIINV,PINVOICE      INVOICE NUMBER                               
         MVI   LIINVTYP,C'I'       INDICATE IOR                                 
         MVC   LIINVDT,INVDT       INVOICE DATE                                 
         MVC   LIDUEDT,DUEDT       DUE DATE                                     
         MVC   LIMOS,MOS           MONTH OF SERVICE                             
         MVC   LIMOA,MOA           MONTH OF ACTIVITY                            
*                                                                               
         ZAP   DUB,IORAMT          SET IORAMT NEGATIVE IN DUB                   
         MP    DUB,=P'-1'                                                       
*                                                                               
         CLI   PSYS,C'P'                                                        
         BE    PRTIOR20                                                         
         CLI   PCOUNTRY,C'C'                                                    
         BE    PRTIOR10                                                         
         EDIT  (P8,DUB),(11,LIORSUN),2,FLOAT=-  SPOT/NET US VERSION             
         EDIT  IORAMT,(10,LIORSUP),2,FLOAT=-                                    
         MVC   LIORSUOF,PACCOF2                                                 
         B     PRTIORX                                                          
*                                                                               
PRTIOR10 EDIT  (P8,DUB),(11,LIORSCN),2,FLOAT=-  SPOT/NET CAN VERSION            
         EDIT  IORAMT,(10,LIORSCP),2,FLOAT=-                                    
         MVC   LIORSCOF,PACCOF2                                                 
         B     PRTIORX                                                          
*                                                                               
PRTIOR20 CLI   PCOUNTRY,C'C'                                                    
         BE    PRTIOR30                                                         
         EDIT  (P8,DUB),(11,LIORPUN),2,FLOAT=-  PRINT US VERSION                
         EDIT  IORAMT,(10,LIORPUP),2,FLOAT=-                                    
         MVC   LIORPUOF,PACCOF2                                                 
         B     PRTIORX                                                          
*                                                                               
PRTIOR30 EDIT  (P8,DUB),(11,LIORPCN),2,FLOAT=-  PRINT CAN VERSION               
         EDIT  IORAMT,(10,LIORPCP),2,FLOAT=-                                    
         MVC   LIORPCOF,PACCOF2                                                 
*                                                                               
PRTIORX  GOTO1 PREPORT             PRINT THE LINE                               
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------------------------*              
* SETAMTS - SET ALL AMOUNTS FOR PRINTING                         *              
*----------------------------------------------------------------*              
*                                                                               
SETAMTS  NTR1                                                                   
         ZAP   RCVAMT,=P'0'                                                     
         ZAP   GRSAMT,=P'0'                                                     
         ZAP   NETAMT,=P'0'                                                     
         ZAP   AGYCOM,=P'0'                                                     
         XC    CDAMT,CDAMT                                                      
         XC    GSTAMT,GSTAMT                                                    
         XC    GSTIAMT,GSTIAMT                                                  
         XC    IORAMT,IORAMT                                                    
         XC    AORAMT,AORAMT                                                    
         XC    AORAGYCM,AORAGYCM                                                
         XC    PSTAMT,PSTAMT                                                    
         XC    PSTIAMT,PSTIAMT                                                  
         CLI   PREV,C'Y'                                                        
         BNE   *+12                                                             
         BAS   RE,SETRAMT         SET REVERSAL AMOUNTS                          
         B     SETAMTX                                                          
*                                                                               
         L     R2,PPOST           POSTING                                       
         USING ACPRTND,R2                                                       
         ZIC   R1,PPOSTNUM        # OF POSTINGS                                 
SETAMT5  CLI   0(R2),MBTTRCV      RECEIVABLE ROW                                
         BNE   SETAMT10                                                         
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         ZAP   RCVAMT,ACPAMT2     SET RECEIVABLE AMOUNT                         
         CLI   PSYS,C'P'                                                        
         BNE   SETAMT50                                                         
         OC    ACPMEMO2,ACPMEMO2                                                
         BZ    SETAMT50                                                         
         ZAP   CDAMT,ACPMEMO2     SET CD AMOUNT                                 
         B     SETAMT50                                                         
*                                                                               
SETAMT10 CLI   0(R2),MBTTNET      NET ROW                                       
         BNE   SETAMT20                                                         
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         ZAP   NETAMT,ACPAMT2     SET NET AMOUNT                                
         B     SETAMT50                                                         
*                                                                               
SETAMT20 CLI   0(R2),MBTTAPR      AOR PAY/RCVBL ROW                             
         BNE   SETAMT25                                                         
         MVI   FLIPAOR,C'N'       FLIP ALL BUT AOR PAYABLE AMOUNT               
         MVI   USSRACC,C'N'       NOT US 'SR' ACCOUNT                           
         CLC   =C'SR',ACPACC                                                    
         BNE   *+8                                                              
         MVI   FLIPAOR,C'Y'       FLIP ALL AMOUNTS FOR SR(AOR LINE)             
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         ZAP   AORAMT,ACPAMT2     SET AOR PAY/RCVBL AMT                         
         CLI   PCOUNTRY,C'C'                                                    
         BE    SETAMT50                                                         
         ZAP   AORAGYCM,ACPAMT2   US ONLY- AGY COMMISSION = AOR AMT             
         CLC   =C'SR',ACPACC                                                    
         BNE   SETAMT50                                                         
         MVI   USSRACC,C'Y'       US 'SR' ACCOUNT (DON'T FLIP THIS)             
         B     SETAMT50                                                         
*                                                                               
SETAMT25 CLI   0(R2),MBTTINTI     INTERNAL INCOME POSTING                       
         BNE   SETAMT30                                                         
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         ZAP   IORAMT,ACPAMT2      SET IOR AMT                                  
         B     SETAMT50                                                         
*                                                                               
SETAMT30 CLI   0(R2),MBTTGST      OUTPUT GST                                    
         BNE   SETAMT35                                                         
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         ZAP   GSTAMT,ACPAMT2     SET GST AMT                                   
         B     SETAMT50                                                         
*                                                                               
SETAMT35 CLI   0(R2),MBTTPQO      OUTPUT PST FOR PQ                             
         BE    SETAMT37                                                         
         CLI   0(R2),MBTTNBO      OR OUTPUT PST FOR NB                          
         BE    SETAMT37                                                         
         CLI   0(R2),MBTTNSO      OR OUTPUT PST FOR NS                          
         BE    SETAMT37                                                         
         CLI   0(R2),MBTTNFO      OR OUTPUT PST FOR NF                          
         BE    SETAMT37                                                         
         CLI   0(R2),MBTTONO      OR OUTPUT PST FOR ON                          
         BE    SETAMT37                                                         
         CLI   0(R2),MBTTPEO      OR OUTPUT PST FOR PE                          
         BE    SETAMT37                                                         
         CLI   0(R2),MBTTBCO      OR OUTPUT PST FOR BC                          
         BNE   SETAMT40                                                         
SETAMT37 OC    ACPAMT2,ACPAMT2    AND AMOUNT                                    
         BZ    SETAMT50                                                         
         OC    PSTAMT,PSTAMT      SET OR ADD PST AMOUNT                         
         BNZ   *+14                                                             
         ZAP   PSTAMT,ACPAMT2                                                   
         B     *+10                                                             
         AP    PSTAMT,ACPAMT2                                                   
         B     SETAMT50                                                         
*                                                                               
SETAMT40 CLI   0(R2),MBTTGSTI     INPUT GST                                     
         BNE   SETAMT48                                                         
         OC    ACPMEMO2,ACPMEMO2  AGENCY COMMISSION                             
         BZ    SETAMT45                                                         
         ZAP   AORAGYCM,ACPMEMO2  FOR CAN AGYS IS MEMO TO INPUT GST             
SETAMT45 OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         ZAP   GSTIAMT,ACPAMT2    SET INPUT GST AMOUNT FOR AOR LINE             
*                                                                               
SETAMT48 CLI   0(R2),MBTTPQI      INPUT PST FOR PQ                              
         BE    SETAMT49                                                         
         CLI   0(R2),MBTTNBI      OR INPUT PST FOR NB                           
         BE    SETAMT49                                                         
         CLI   0(R2),MBTTNSI      OR INPUT PST FOR NS                           
         BE    SETAMT49                                                         
         CLI   0(R2),MBTTNFI      OR INPUT PST FOR NF                           
         BE    SETAMT49                                                         
         CLI   0(R2),MBTTBCI      OR INPUT PST FOR BC                           
         BE    SETAMT49                                                         
         CLI   0(R2),MBTTPEI      OR INPUT PST FOR PE                           
         BE    SETAMT49                                                         
         CLI   0(R2),MBTTONI      OR INPUT PST FOR ON                           
         BNE   SETAMT50                                                         
SETAMT49 OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETAMT50                                                         
         OC    PSTIAMT,PSTIAMT   SET OR ADD TO INPUT PST AMOUNT                 
         BNZ   *+14                                                             
         ZAP   PSTIAMT,ACPAMT2                                                  
         B     *+10                                                             
         AP    PSTIAMT,ACPAMT2                                                  
*                                                                               
SETAMT50 LA    R2,ACPRTNL2(R2)                                                  
         BCT   R1,SETAMT5                                                       
*                                                                               
         BAS   RE,SETSI           READ MEDIA INC OR AOR INC                     
         ZAP   GRSAMT,MEMO        SET GROSS AMOUNT                              
         ZAP   AGYCOM,AMOUNT      SET AGENCY COMMISSION AMOUNT                  
SETAMTX  B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*----------------------------------------------------*                          
* SETSI   - GETS MEMO FROM INCOME OR MEDIA INCOME    *                          
*----------------------------------------------------*                          
*                                                                               
SETSI    NTR1                                                                   
         ZAP   AMOUNT,=P'0'                                                     
         ZAP   MEMO,=P'0'                                                       
         L     RE,PPOST           POSTING                                       
         USING ACPRTND,RE                                                       
         ZIC   R1,PPOSTNUM        # OF POSTINGS                                 
SETSI5   CLI   0(RE),MBTTINC      INCOME ROW                                    
         BNE   SETSI15                                                          
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETSI10                                                          
         ZAP   AMOUNT,ACPAMT2      SAVE AMOUNT                                  
SETSI10  OC    ACPMEMO2,ACPMEMO2                                                
         BZ    SETSIX                                                           
         ZAP   MEMO,ACPMEMO2      SAVE MEMO                                     
         B     SETSIX                                                           
SETSI15  LA    RE,ACPRTNL2(RE)                                                  
         BCT   R1,SETSI5                                                        
*                                 INCOME ACCOUNT NOT FOUND                      
         L     RE,PPOST           POSTING                                       
         ZIC   R1,PPOSTNUM        # OF POSTINGS                                 
SETSI20  CLI   0(RE),MBTTARI      AOR INCOME ROW                                
         BNE   SETSI30                                                          
         OC    ACPAMT2,ACPAMT2                                                  
         BZ    SETSI25                                                          
         ZAP   AMOUNT,ACPAMT2     SAVE AMOUNT                                   
SETSI25  OC    ACPMEMO2,ACPMEMO2                                                
         BZ    SETSIX                                                           
         ZAP   MEMO,ACPMEMO2      SAVE MEMO                                     
         B     SETSIX                                                           
SETSI30  LA    RE,ACPRTNL2(RE)                                                  
         BCT   R1,SETSI20                                                       
*                                                                               
SETSIX   B     EXIT                                                             
         DROP  RE                                                               
         EJECT                                                                  
*----------------------------------------------------*                          
* SETRAMT - SET REVERSAL AMOUNTS                     *                          
*----------------------------------------------------*                          
*                                                                               
SETRAMT  NTR1                                                                   
         LA    R0,NAMNTS            R0=NUMBER OF AMOUNTS                        
         LA    RE,AMNTS             RE=A(REVERSAL AMTS TO SET)                  
         LA    R1,PRRCV             R1=A(AMOUNTS)                               
SETRAMT5 OC    0(L'PRRCV,R1),0(R1)  IF AMOUNT                                   
         BZ    SETRAMT8                                                         
         ZAP   0(L'AMNTS,RE),0(L'PRRCV,R1)                                      
         MP    0(L'AMNTS,RE),=P'-1' SET AMOUNT AS REVERSAL                      
SETRAMT8 LA    RE,L'AMNTS(RE)                                                   
         LA    R1,L'PRRCV(R1)                                                   
         BCT   R0,SETRAMT5                                                      
*                                                                               
         MVC   FLIPAOR,PRFLPAOR                                                 
         MVC   USSRACC,PRMULTCM                                                 
         B     EXIT                                                             
         EJECT                                                                  
*----------------------------------------------------*                          
* PRTPOST - BILL IN ERROR - PRINT OUT THE POSTINGS   *                          
*----------------------------------------------------*                          
*                                                                               
PRTPOST  NTR1                                                                   
         L     R1,PP1                                                           
         L     R2,PP2                                                           
         MVC   0(18,R1),=CL18'ACCOUNT       TYPE'                               
         MVC   0(18,R2),=CL18'-------       ----'                               
         MVC   25(19,R1),=CL19'AMOUNT         MEMO'                             
         MVC   25(19,R2),=CL19'------         ----'                             
         MVC   48(5,R1),=CL5'ERROR'                                             
         MVC   48(5,R2),=CL5'-----'                                             
         GOTO1 PREPORT                                                          
         L     R3,PP1                                                           
         USING LPOSTD,R3                                                        
         ZIC   R4,PPOSTNUM        # POSSIBLE POSTINGS                           
         L     R2,PPOST           POSTINGS                                      
         USING ACPRTND,R2                                                       
*                                                                               
PRTP5    OC    0(ACPRTNL2,R2),0(R2)   SKIP BLANK POSTING ROW                    
         BZ    PRTP50                                                           
         MVC   LPACC,ACPACC           ACCOUNT                                   
         TM    ACPSTAT,ACPCR                                                    
         BNO   *+14                                                             
         MVC   LPTYPE,=C'CR'                                                    
         B     PRTP10                                                           
         TM    ACPSTAT,ACPDEB                                                   
         BNO   *+10                                                             
         MVC   LPTYPE,=C'DR'                                                    
PRTP10   OC    ACPAMT2,ACPAMT2                                                  
         BZ    PRTP20                                                           
         EDIT  ACPAMT2,(12,LPAMT),2,FLOAT=-                                     
PRTP20   OC    ACPMEMO2,ACPMEMO2                                                
         BZ    PRTP30                                                           
         EDIT  ACPMEMO2,(12,LPMEMO),2,FLOAT=-                                   
PRTP30   BAS   RE,PRTACERR        PRINT OUT ACCOUNT ERRRORS                     
         GOTO1 PREPORT                                                          
PRTP50   LA    R2,ACPRTNL2(R2)    NEXT POSTING                                  
         BCT   R4,PRTP5                                                         
         BAS   RE,PRTERROR        PRINT OUT MSG FOR BILL                        
         GOTO1 PREPORT                                                          
         B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------------*                     
* PRTACERR - PRINT OUT ACCOUNT ERRORS NEXT TO ACCOUNT ROW *                     
*---------------------------------------------------------*                     
*                                                                               
PRTACERR NTR1                                                                   
         LA    R4,LPERR                                                         
         CLI   ACPERR2,0          SPECIFIC ACCOUNT ERROR?                       
         BE    PRTAC70            NO                                            
         TM    ACPERR2,ACPEINV                                                  
         BNO   *+18                                                             
         MVC   0(16,R4),=CL16'RECORD NOT FOUND'                                 
         LA    R4,16(R4)                                                        
         B     PRTAC70                                                          
         TM    ACPERR2,ACPEINP                                                  
         BNO   *+18                                                             
         MVC   0(27,R4),=CL27'INVALID ACCOUNT FOR POSTING'                      
         LA    R4,27(R4)                                                        
         B     PRTAC70                                                          
         TM    ACPERR2,ACPEUNT3                                                 
         BNO   *+18                                                             
         MVC   0(20,R4),=CL20'UNIT 3 ACCOUNT ERROR'                             
         LA    R4,20(R4)                                                        
         B     PRTAC70                                                          
         TM    ACPERR2,ACPE3ROC                                                 
         BNO   *+18                                                             
         MVC   0(32,R4),=CL32'POINTER ON UNIT 3 PARTIC MISSING'                 
         LA    R4,32(R4)                                                        
         B     PRTAC70                                                          
         TM    ACPERR2,ACPEPAR                                                  
         BNO   *+18                                                             
         MVC   0(17,R4),=CL17'PARTICIPANT ERROR'                                
         LA    R4,17(R4)                                                        
         B     PRTAC70                                                          
         TM    ACPERR2,ACPESFX                                                  
         BNO   *+18                                                             
         MVC   0(12,R4),=CL12'SUFFIX ERROR'                                     
         LA    R4,12(R4)                                                        
         B     PRTAC70                                                          
         TM    ACPERR2,ACPEADCD                                                 
         BO    *+6                                                              
         DC    H'0'                                                             
         MVC   0(15,R4),=CL15'NO ADCODE FOUND'                                  
         LA    R4,15(R4)                                                        
*                                                                               
PRTAC70  CLI   ACPBCHG,C'E'       ERROR FOUND WITH CONTRA-ACCOUNT?              
         BNE   PRTACX                                                           
         MVC   0(20,R4),=CL20'CONTRA ACCOUNT ERROR'                             
PRTACX   B     EXIT                                                             
         DROP  R3,R2                                                            
         EJECT                                                                  
*---------------------------------------------------------*                     
* PRTERROR-PRINT OUT GENERAL POSTING MSGS AFTER POSTINGS  *                     
*---------------------------------------------------------*                     
*                                                                               
PRTERROR NTR1                                                                   
         L     R3,PP1                                                           
         TM    PPOSTERR,ACPNOREC                                                
         BO    *+12                                                             
         TM    PPOSTERC,ACPNOBPR                                                
         BNO   PRTE10                                                           
         MVC   0(2,R3),=CL2'NO'                                                 
         LA    R3,3(R3)                                                         
         CLC   =C'REG',TYPEBIL                                                  
         BNE   PRTE02                                                           
         OC    SUBTYPE,SUBTYPE                                                  
         BZ    PRTE08              (POST)                                       
         MVC   0(2,R3),SUBTYPE                                                  
         LA    R3,1(R3)                                                         
         CLI   0(R3),C' '                                                       
         BE    *+8                                                              
         LA    R3,1(R3)            (UCPOST/UNPOST/DPOST)                        
         B     PRTE08                                                           
PRTE02   CLC   =C'AOR',TYPEBIL                                                  
         BNE   PRTE04                                                           
         OC    SUBTYPE,SUBTYPE                                                  
         BZ    PRTE02X                                                          
         MVC   0(L'SUBTYPE,R3),SUBTYPE                                          
         LA    R3,L'SUBTYPE(R3)                                                 
         B     PRTE08              (UANPOST/UACPOST)                            
PRTE02X  MVI   0(R3),C'A'                                                       
         LA    R3,1(R3)            (APOST)                                      
         B     PRTE08                                                           
PRTE04   CLC   =C'FIN',TYPEBIL                                                  
         BNE   PRTE06                                                           
         MVI   0(R3),C'S'                                                       
         LA    R3,1(R3)                                                         
PRTE06   CLC   =C'BPC',TYPEBIL                                                  
         BNE   PRTE08                                                           
         MVI   0(R3),C'B'                                                       
         LA    R3,1(R3)                                                         
PRTE08   MVC   0(25,R3),=CL25'POST/MAINT RECORDS FOUND'                         
         GOTO1 PREPORT                                                          
         B     PRTEX                                                            
*                                                                               
PRTE10   TM    PPOSTERR,ACPMISSA                                                
         BNO   PRTE20                                                           
         CLC   =C'REG',TYPEBIL                                                  
         BNE   PRTE12                                                           
         OC    SUBTYPE,SUBTYPE                                                  
         BZ    PRTE18                                                           
         MVC   0(2,R3),SUBTYPE     (UCPOST/UNPOST/DPOST)                        
         LA    R3,1(R3)                                                         
         CLI   0(R3),C' '                                                       
         BE    *+8                                                              
         LA    R3,1(R3)                                                         
         B     PRTE18                                                           
PRTE12   CLC   =C'AOR',TYPEBIL                                                  
         BNE   PRTE14                                                           
         OC    SUBTYPE,SUBTYPE                                                  
         BZ    PRTE12X                                                          
         MVC   0(L'SUBTYPE,R3),SUBTYPE  (UACPOST/UANPOST)                       
         LA    R3,L'SUBTYPE(R3)                                                 
         B     PRTE18                                                           
PRTE12X  MVI   0(R3),C'A'                                                       
         LA    R3,1(R3)                                                         
         B     PRTE18              (APOST)                                      
PRTE14   CLC   =C'FIN',TYPEBIL                                                  
         BNE   PRTE16                                                           
         MVI   0(R3),C'S'                                                       
         LA    R3,1(R3)                                                         
PRTE16   CLC   =C'BPC',TYPEBIL                                                  
         BNE   PRTE18                                                           
         MVI   0(R3),C'B'                                                       
         LA    R3,1(R3)                                                         
PRTE18   MVC   0(30,R3),=CL30'POST/MAINT AMOUNTS MISSING'                       
         GOTO1 PREPORT                                                          
         B     PRTEX                                                            
*                                                                               
PRTE20   CLI   PAORMSG,C'Y'                                                     
         BE    PRTE30                                                           
         MVC   0(22,R3),=C'ERROR WITH BILL IN SET'                              
         GOTO1 PREPORT                                                          
*                                                                               
PRTE30   TM    PPOSTERC,ACPNOWK                                                 
         BNO   PRTE35                                                           
         MVC   0(18,R3),=C'NO WORK CODE FOUND'                                  
         GOTO1 PREPORT                                                          
PRTE35   TM    PPOSTERR,ACPNOPRD                                                
         BNO   PRTE40                                                           
         MVC   0(2,R3),=C'NO'                                                   
         MVC   3(2,R3),PSVPROD                                                  
         MVC   6(20,R3),=C'PRODUCT RECORD FOUND'                                
         GOTO1 PREPORT                                                          
PRTE40   TM    PPOSTERR,ACPNOP1C                                                
         BNO   PRTE45                                                           
         MVC   0(30,R3),=CL30'NO PROD LVL 1C FOUND'                             
         GOTO1 PREPORT                                                          
PRTE45   TM    PPOSTERR,ACPSJERR                                                
         BNO   PRTE50                                                           
         MVC   0(2,R3),=C'NO'                                                   
         MVC   3(2,R3),PSVPROD                                                  
         MVC   6(19,R3),=C'CLIENT RECORD FOUND'                                 
         GOTO1 PREPORT                                                          
PRTE50   TM    PPOSTERR,ACPNOMI                                                 
         BNO   PRTE55                                                           
         MVC   0(30,R3),=CL30'NO MI RECORD FOUND'                               
         GOTO1 PREPORT                                                          
PRTE55   TM    PPOSTERC,ACPINT                                                  
         BNO   PRTE58                                                           
         MVC   0(30,R3),=CL30'INCORRECT INTERNAL PCT'                           
         GOTO1 PREPORT                                                          
PRTE58   TM    PPOSTERC,ACPIRET                                                 
         BNO   PRTE60                                                           
         MVC   0(30,R3),=CL30'INCORRECT RETAIL PROFILES'                        
         GOTO1 PREPORT                                                          
PRTE60   TM    PPOSTERR,ACPNOTEQ                                                
         BNO   PRTE65                                                           
         MVC   0(30,R3),=CL30'DEBITS DO NOT EQUAL CREDITS'                      
         GOTO1 PREPORT                                                          
PRTE65   TM    PPOSTER3,PPNMPRD                                                 
         BNO   PRTE70                                                           
         MVC   0(40,R3),=CL40'NO PRODUCT REC FOUND FOR MASTER PRODUCT '         
         MVC   41(L'PSVMPRD,R3),PSVMPRD                                         
         GOTO1 PREPORT                                                          
PRTE70   TM    PPOSTER3,PPNMPRDE                                                
         BNO   PRTE75                                                           
         MVC   0(36,R3),=CL36'NO EST REC FOUND FOR MASTER PRODUCT '             
         MVC   37(L'PSVMPRD,R3),PSVMPRD                                         
         GOTO1 PREPORT                                                          
PRTE75   TM    PPOSTER3,PPNPCLT                                                 
         BNO   PRTEX                                                            
         MVC   0(28,R3),=CL28'NO PRODUCT FOUND FOR CLIENT '                     
         MVC   28(L'PCLT,R3),PCLT                                               
         GOTO1 PREPORT                                                          
PRTEX    B     EXIT                                                             
         EJECT                                                                  
*---------------------------------------------------*                           
* BILLERR - ADDS BILL IN ERROR TO ERRFIL            *                           
*---------------------------------------------------*                           
*                                                                               
BILLERR  NTR1                                                                   
         LA    R2,EREC                                                          
         USING ERECD,R2                                                         
         BAS   RE,SETEREC          SET UP BASIC EREC                            
         MVC   ERECINV,PINVOICE   INVOICE NUMBER                                
         MVC   ERECTYPE,INVTYPE   TYPE FOR CLIENT AOR BILL                      
         ZAP   ERECRCV,RCVAMT     RCVBL AMOUNT                                  
         ZAP   ERECGRS,GRSAMT     GROSS AMOUNT                                  
         ZAP   ERECNET,NETAMT     NET                                           
         OC    CDAMT,CDAMT                                                      
         BZ    *+10                                                             
         ZAP   ERECCD,CDAMT       CD AMOUNT                                     
         ZAP   ERECCOM,AGYCOM     AGY COMM                                      
         OC    GSTAMT,GSTAMT                                                    
         BZ    *+10                                                             
         ZAP   ERECGST,GSTAMT     GST                                           
         OC    PSTAMT,PSTAMT                                                    
         BZ    *+10                                                             
         ZAP   ERECPST,PSTAMT     PST                                           
         MVC   ERECAOFF,PACCOFF   ACC OFFICE                                    
         CLI   PSYS,C'P'                                                        
         BNE   *+10                                                             
         MVC   ERECPJB,PJBCD      PRODUCTION JOB CODE                           
*                                                                               
BILLERR9 PUT   ERRFIL,EREC        PUT BILL INFO TO FILE                         
         OC    IORAMT,IORAMT       IF IOR AMOUNT                                
         BZ    *+8                                                              
         BAS   RE,IBILLERR         ADD IOR AMOUNTS TO ERROR ALSO                
         CLC   PAOR,=C'NNNN'                                                    
         BE    *+8                                                              
         BAS   RE,ABILLERR        ADD TRUE AOR BILL ALSO                        
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*              ROUTINE TO SET UP BASIC EREC RECORD                              
*                                                                               
         USING ERECD,R2            R2=A(EREC)                                   
SETEREC  NTR1                                                                   
         XC    EREC,EREC                                                        
         MVC   ERECMED,PMED                                                     
         MVC   ERECOFF(1),PMEDOFF                                               
         MVC   ERECCLT,PCLT                                                     
         MVC   ERECPRD,PRD                                                      
         MVC   ERECEST,EST                                                      
         MVC   ERECIDT,INVDT      INVOICE DATE                                  
         MVC   ERECDDT,DUEDT      DUE DATE                                      
         MVC   ERECMOS,MOS        MONTH OF SERVICE                              
         MVC   ERECMOA,MOA        MOA                                           
         L     RE,PPAGE           A(PAGE NUMBER)                                
         ICM   R1,3,0(RE)                                                       
         BCTR  R1,0                                                             
         STCM  R1,3,ERECPG                                                      
         B     EXIT                                                             
         DROP  R2                                                               
         SPACE 2                                                                
*---------------------------------------------------*                           
* IBILLERR- ADDS IOR VERSION BILL TO ERRFIL         *                           
*---------------------------------------------------*                           
*                                                                               
IBILLERR NTR1                                                                   
         LA    R2,EREC                                                          
         USING ERECD,R2                                                         
         BAS   RE,SETEREC          SET UP BASIC ERROR RECORD                    
         MVC   ERECINV,PINVOICE    SET MAIN INVOICE NUMBER                      
         MVI   ERECTYPE,C'I'       IOR TYPE                                     
         MVC   ERECAOFF,PACCOF2    OFFICE OVERRIDE FOR IOR                      
         ZAP   ERECIORN,IORAMT                                                  
         MP    ERECIORN,=P'-1'                                                  
         ZAP   ERECIORP,IORAMT                                                  
         PUT   ERRFIL,EREC         PUT INFO TO FILE                             
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* ABILLERR- ADDS AOR VERSION BILL TO ERRFIL         *                           
*---------------------------------------------------*                           
*                                                                               
ABILLERR NTR1                                                                   
         LA    R2,EREC                                                          
         USING ERECD,R2                                                         
         BAS   RE,SETEREC         SET UP BASIC ERROR RECORD                     
         MVC   ERECINV,PTINV      INVOICE NUMBER                                
         MVI   ERECTYPE,C'A'      AOR INVOICE                                   
         ZAP   ERECRCV,=P'0'      RCVBL AMOUNT                                  
         ZAP   ERECGRS,=P'0'      GROSS AMOUNT                                  
         ZAP   ERECNET,=P'0'      NET                                           
         OC    AORAGYCM,AORAGYCM                                                
         BZ    ABILLER3                                                         
         ZAP   ERECCOM,AORAGYCM   AGY COMM                                      
         CLI   PCOUNTRY,C'C'      IF U.S. 'SR' ACCT - DONT X -1                 
         BE    ABILLER1                                                         
         CLI   USSRACC,C'Y'                                                     
         BE    *+10                                                             
ABILLER1 MP    ERECCOM,=P'-1'                                                   
*                                                                               
ABILLER3 OC    GSTIAMT,GSTIAMT                                                  
         BZ    ABILLER5                                                         
         ZAP   ERECGST,GSTIAMT                                                  
         MP    ERECGST,=P'-1'                                                   
ABILLER5 OC    PSTIAMT,PSTIAMT                                                  
         BZ    ABILLER6                                                         
         ZAP   ERECPST,PSTIAMT                                                  
         MP    ERECPST,=P'-1'                                                   
*                                                                               
ABILLER6 OC    AORAMT,AORAMT                                                    
         BZ    ABILLER8                                                         
         ZAP   ERECAOR,AORAMT     AOR PAY                                       
         CLI   FLIPAOR,C'Y'                                                     
         BNE   *+10                                                             
         MP    ERECAOR,=P'-1'                                                   
*                                                                               
ABILLER8 MVC   ERECAOFF,PACCOFF   ACC OFFICE                                    
         PUT   ERRFIL,EREC        PUT BILL INFO TO FILE                         
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* BILLTOT - ADDS LINE TOTALS TO PRODUCT ACCUMULATORS*                           
*---------------------------------------------------*                           
*                                                                               
BILLTOT  NTR1                                                                   
         AP    PTOTRCV,RCVAMT     ADD RCVBL TO RCVBL AMOUNT TOTAL               
         OC    GRSAMT,GRSAMT                                                    
         BZ    *+10                                                             
         AP    PTOTGRS,GRSAMT     ADD MEMO TO GROSS AMOUNT TOTAL                
         AP    PTOTNET,NETAMT     ADD NET TO NET TOTAL                          
         OC    CDAMT,CDAMT                                                      
         BZ    *+10                                                             
         AP    PTOTCD,CDAMT       ADD CD TO CD TOTAL                            
*                                                                               
         AP    PTOTCOM,AGYCOM     ADD AGY COMM TO TOTAL AGENCY COMM             
*                                                                               
         OC    IORAMT,IORAMT      IF IOR AMOUNT                                 
         BZ    BILLT02                                                          
         SP    PTOTCOM,IORAMT     SUBTRACT IOR FROM TOTAL AGY COM               
         AP    PTOTAOR,IORAMT     ADD IOR TO TOTAL AOR/IOR AMOUNT               
         AP    PTOTIOR,IORAMT     ADD IOR TO TOTAL IOR AMOUNT                   
*                                                                               
BILLT02  OC    AORAGYCM,AORAGYCM                                                
         BZ    BILLT10                                                          
         ZAP   DUB,AORAGYCM                                                     
         CLI   PCOUNTRY,C'C'      IF U.S. 'SR' ACCT - DON'T X -1                
         BE    *+12                                                             
         CLI   USSRACC,C'Y'                                                     
         BE    *+10                                                             
         MP    DUB,=P'-1'                                                       
         AP    PTOTCOM,DUB                                                      
*                                                                               
BILLT10  OC    AORAMT,AORAMT                                                    
         BZ    BILLT20                                                          
         ZAP   DUB,AORAMT                                                       
         CLI   FLIPAOR,C'Y'                                                     
         BNE   *+10                                                             
         MP    DUB,=P'-1'                                                       
         AP    PTOTAOR,DUB        ADD (+/-AORAMT) TO AOR PAY TOTAL              
*                                                                               
BILLT20  OC    GSTAMT,GSTAMT                                                    
         BZ    *+10                                                             
         AP    PTOTGST,GSTAMT     ADD OUTPUT GST TO GST TOTAL                   
         OC    PSTAMT,PSTAMT                                                    
         BZ    *+10                                                             
         AP    PTOTPST,PSTAMT     ADD OUTPUT PST TO PST TOTAL                   
         OC    GSTIAMT,GSTIAMT                                                  
         BZ    BILLT30                                                          
         ZAP   DUB,GSTIAMT                                                      
         MP    DUB,=P'-1'                                                       
         AP    PTOTGST,DUB        ADD INPUT GST TO GST TOTAL                    
BILLT30  OC    PSTIAMT,PSTIAMT                                                  
         BZ    BILLTX                                                           
         ZAP   DUB,PSTIAMT                                                      
         MP    DUB,=P'-1'                                                       
         AP    PTOTPST,DUB        ADD INPUT PST TO PST TOTAL                    
BILLTX   B     EXIT                                                             
         EJECT                                                                  
*--------------------------------------------------*                            
* PRTMSG- PRINT OUT A MESSAGE                      *                            
*--------------------------------------------------*                            
*                                                                               
PRTMSG   MVI   REPTYPE,C'D'                                                     
         L     R3,PP1                                                           
         TM    PMSG,PMNOREC       ANY POSTINGS RECORDS                          
         BNO   PRTMSG10                                                         
         MVC   0(32,R3),=CL32'NO POSTING RECORDS FOUND'                         
         GOTO1 PREPORT                                                          
         B     PRTMSGX                                                          
*                                                                               
PRTMSG10 TM    PMSG,PMNOPOST                                                    
         BNO   PRTMSGX                                                          
         MVC   0(36,R3),=CL36'POSTINGS SET TO NO ON PROF/MAINT FOR'             
         CLI   PMSGTYPE,C'M'                                                    
         BNE   PRTMSG15                                                         
         MVC   37(5,R3),=C'MEDIA'                                               
         MVC   44(1,R3),PMSGTYPE+1                                              
         B     PRTMSG30                                                         
PRTMSG15 CLI   PMSGTYPE,C'C'                                                    
         BNE   PRTMSG20                                                         
         MVC   37(6,R3),=C'CLIENT'                                              
         MVC   44(3,R3),PMSGTYPE+1                                              
         B     PRTMSG30                                                         
PRTMSG20 CLI   PMSGTYPE,C'P'                                                    
         BNE   PRTMSGX                                                          
         MVC   37(7,R3),=C'PRODUCT'                                             
         MVC   45(3,R3),PMSGTYPE+1                                              
PRTMSG30 GOTO1 PREPORT                                                          
PRTMSGX  B     EXIT                                                             
         EJECT                                                                  
* PRTTOT- PRINT OUT PROD TOTAL THEN ADD TO BUFFALO                              
*                                                                               
PRTTOT   L     R3,PP1                                                           
         USING LIND,R3                                                          
         LA    R2,MYBUFFIO                                                      
         USING BRECD,R2                                                         
         XC    MYBUFFIO,MYBUFFIO                                                
         MVC   BRECKEY,=10X'FF'                                                 
         MVI   BRECTYP,0          RECORD TYPE                                   
         MVC   BRECMED,PMED       READ FOR MEDIA TOTAL RECORD                   
         CLI   PTOTTYPE,PTOTMED   MEDIA TOTAL                                   
         BNE   PRTTOT3                                                          
         BAS   RE,HIGHBUFF        READ HIGH FOR KEY                             
         BO    PRTTOTX                                                          
         CLC   SVBKEY,BRECKEY     IF CORRECT KEY                                
         BNE   PRTTOTX                                                          
         MVI   REPTYPE,0          CLEAR HEADLINES                               
         MVC   0(17,R3),=C'** MEDIA TOTAL **'                                   
         BAS   RE,PRTBUFF                                                       
         B     PRTTOTX                                                          
*                                                                               
PRTTOT3  CLI   PTOTTYPE,PTOTMOF   MEDIA OFFICE TOTAL                            
         BNE   PRTTOT5                                                          
         MVI   BRECOFF,C' '       MEDIA OFFICE                                  
         MVC   BRECOFF+1(1),PMEDOFF                                             
         BAS   RE,HIGHBUFF        READ HIGH FOR KEY                             
         BO    PRTTOTX                                                          
         CLC   SVBKEY,BRECKEY     IF CORRECT KEY                                
         BNE   PRTTOTX                                                          
         MVC   0(25,R3),=C'** MEDIA OFFICE TOTALS **'                           
         BAS   RE,PRTBUFF                                                       
         B     PRTTOTX                                                          
*                                                                               
PRTTOT5  CLI   PTOTTYPE,PTOTCLT   CLIENT TOTAL                                  
         BNE   PRTTOT10                                                         
         MVI   BRECOFF,C' '       MEDIA OFFICE                                  
         MVC   BRECOFF+1(1),PMEDOFF                                             
         MVC   BRECCLT,PCLT       CLIENT                                        
         BAS   RE,HIGHBUFF        READ FOR CLIENT TOTALS                        
         BO    PRTTOTX                                                          
         CLC   SVBKEY,BRECKEY     IF CORRECT KEY                                
         BNE   PRTTOTX                                                          
         MVC   0(18,R3),=C'** CLIENT TOTAL **'                                  
         BAS   RE,PRTBUFF                                                       
         B     PRTTOTX                                                          
*                                                                               
PRTTOT10 CLI   PTOTTYPE,PTOTPRD   PRODUCT TOTAL                                 
         BE    *+6                                                              
         DC    H'0'                                                             
         BAS   RE,PRTPTOT         PRINT PRODUCT TOTAL                           
         BAS   RE,ADDBUFF         ADD BUFFALO RECORDS                           
         BAS   RE,CLRPTOTS         CLEAR ACCUMULATORS                           
PRTTOTX  B     EXIT                                                             
         EJECT                                                                  
PRTPTOT  NTR1                                                                   
         BAS   RE,CHKZERO                                                       
         BE    PRTPTX             IF ZERO TOTALS DON'T PRINT                    
         MVC   0(19,R3),=C'** PRODUCT TOTAL **'                                 
         EDIT  PTOTRCV,(12,LRCVBL),2,FLOAT=-                                    
         EDIT  PTOTGRS,(12,LGROSS),2,FLOAT=-                                    
         EDIT  PTOTNET,(12,LNET),2,FLOAT=-                                      
*                                                                               
         CLI   PSYS,C'P'                                                        
         BE    PRTPT30                                                          
         EDIT  PTOTCOM,(11,LAGYCOM),2,FLOAT=-                                   
         CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTPT20                                                          
         MVC   TEMPAMT,PTOTGST    SPOT/NET -- CANADIAN VERSION                  
         BAS   RE,CHKGST                                                        
         BNE   PRTPT10                                                          
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LGST,=8C'*'                                                      
         B     PRTPT10                                                          
         EDIT  PTOTGST,(8,LGST),2,FLOAT=-                                       
PRTPT10  EDIT  PTOTPST,(8,LPST),2,FLOAT=-                                       
PRTPT15  OC    PTOTAOR,PTOTAOR                                                  
         BZ    PRTPT60                                                          
         EDIT  PTOTAOR,(10,LAORPAY),2,FLOAT=-                                   
         B     PRTPT60                                                          
*                                 SPOT/NET -- US VERSION                        
PRTPT20  EDIT  PTOTAOR,(10,LAORPAY2),2,FLOAT=-                                  
         B     PRTPT60                                                          
*                               --  PRINT ---                                   
PRTPT30  EDIT  PTOTCD,(7,LPCD),2,FLOAT=-                                        
         EDIT  PTOTCOM,(11,LPAGYCM),2,FLOAT=-                                   
         CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTPT50                                                          
         MVC   TEMPAMT,PTOTGST                                                  
         BAS   RE,CHKGST                                                        
         BNE   PRTPT40                                                          
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LPGST,=8C'*'                                                     
         B     PRTPT40                                                          
         EDIT  PTOTGST,(8,LPGST),2,FLOAT=- PRINT -- CANADIAN                    
PRTPT40  EDIT  PTOTPST,(8,LPPST),2,FLOAT=-                                      
PRTPT45  EDIT  PTOTAOR,(10,LPAOR),2,FLOAT=-                                     
         B     PRTPT60                                                          
*                                                                               
PRTPT50  EDIT  PTOTAOR,(10,LPAOR2),2,FLOAT=- PRINT -- US VERSION                
*                                                                               
PRTPT60  GOTO1 PREPORT                                                          
         GOTO1 PREPORT            PRINT BLANK LINE AFTER TOTAL                  
PRTPTX   B     EXIT                                                             
         DROP  R3,R2                                                            
         EJECT                                                                  
*---------------------------------------------------*                           
* CLRPTOTS- CLEAR PRODUCT LEVEL ACCUMULATORS        *                           
*---------------------------------------------------*                           
*                                                                               
CLRPTOTS NTR1                                                                   
         LA    RE,PNUMTOTS        CLEAR ACCUMULATORS                            
         LA    R1,PMXSAVE                                                       
CLRPTOT5 ZAP   0(L'PMXSAVE,R1),=P'0'                                            
         LA    R1,L'PMXSAVE(R1)                                                 
         BCT   RE,CLRPTOT5                                                      
         B     EXIT                                                             
         SPACE 2                                                                
*---------------------------------------------------*                           
* CHKZERO - CHECK IF ACCUMULATORS ALL ZERO          *                           
*---------------------------------------------------*                           
*                                                                               
CHKZERO  NTR1                                                                   
         LA    RE,PNUMTOTS        CLEAR ACCUMULATORS                            
         LA    R1,PMXSAVE                                                       
CHKZERO5 CP    0(L'PMXSAVE,R1),=P'0'                                            
         BNE   NO                                                               
         LA    R1,L'PMXSAVE(R1)                                                 
         BCT   RE,CHKZERO5                                                      
         B     YES                                                              
         EJECT                                                                  
*---------------------------------------------------*                           
* ADDBUFF - ADD BUFFALO RECORDS                     *                           
*           ONCE WITH MEDIA OFF & ONCE WITH ACC OFF *                           
*---------------------------------------------------*                           
*                                                                               
ADDBUFF  NTR1                                                                   
         LA    R3,MYBUFFIO        SET BUFFALO RECORD                            
         USING BRECD,R3                                                         
         XC    MYBUFFIO,MYBUFFIO                                                
         MVI   BRECTYP,0          RECORD TYPE                                   
         MVC   BRECMED,PMED       MEDIA                                         
         MVI   BRECOFF,C' '       MEDIA OFFICE                                  
         MVC   BRECOFF+1(1),PMEDOFF                                             
         MVC   BRECCLT,PCLT       CLIENT                                        
         MVC   BRECPRD,PPRD       PRODUCT                                       
         MVC   BRECAMTS(BRECALNQ),PMXSAVE  RESET AMOUNTS                        
         BAS   RE,PUTBUFF         PUT DETAIL RECORD TO BUFFALO                  
         MVC   BRECPRD,=3X'FF'    ADD FOR CLIENT TOTALS                         
         BAS   RE,PUTBUFF         PUT RECORD TO BUFFALO                         
         MVC   BRECCLT,=3X'FF'    ADD FOR MEDIA OFFICE TOTALS                   
         BAS   RE,PUTBUFF         PUT RECORD TO BUFFALO                         
         MVC   BRECOFF,=2X'FF'    ADD FOR MEDIA TOTALS                          
         BAS   RE,PUTBUFF         PUT RECORD TO BUFFALO                         
*                                                                               
         XR    R2,R2              PROCESS PRIMARY ACC OFFICE FIRST              
ADDBUFF2 XC    MYBUFFIO,MYBUFFIO  NOW ADD RECORDS BY ACC OFF                    
         MVI   BRECTYP,1          RECORD TYPE                                   
         MVC   BRECMED,PMED       MEDIA                                         
         MVC   BRECOFF,PACCOFF    ACC OFFICE                                    
         MVC   BRECCLT,PCLT       CLIENT                                        
         MVC   BRECPRD,PPRD       PRODUCT                                       
         MVC   BRECAMTS(BRECALNQ),PMXSAVE   RESET AMOUNTS                       
         LTR   R2,R2              IF PROCESSING PRIMARY ACC OFFICE              
         BNZ   ADDBUFF5                                                         
         CLC   PACCOF2,SPACES      AND THERE IS A SECONDARY OFFICE              
         BE    *+10                                                             
         SP    BRECAOR,BRECIOR     DON'T SHOW IOR PORTION                       
         B     ADDBUFF8                                                         
*                                                                               
ADDBUFF5 CLC   PACCOF2,SPACES      PROCESS SECONDARY OFFICE                     
         BE    ADDBUFFX            ONLY IF THERE IS ONE                         
         MVC   BRECOFF,PACCOF2     SET OFFICE                                   
         BAS   RE,CLRBRAMT         CLEAR BUFFALO AMOUNTS                        
         MVC   BRECAOR,PTOTIOR     AND SHOW ONLY IOR AMOUNT                     
*                                                                               
ADDBUFF8 BAS   RE,PUTBUFF         PUT DETAIL RECORD TO BUFFALO                  
         MVC   BRECPRD,=3X'FF'    ADD FOR CLIENT TOTALS                         
         BAS   RE,PUTBUFF         PUT RECORD TO BUFFALO                         
         MVC   BRECCLT,=3X'FF'    ADD FOR MEDIA OFFICE TOTALS                   
         BAS   RE,PUTBUFF         PUT RECORD TO BUFFALO                         
         MVC   BRECOFF,=2X'FF'    ADD FOR MEDIA TOTALS                          
         BAS   RE,PUTBUFF         PUT RECORD TO BUFFALO                         
         LA    R2,1(R2)           PROCESS SECONDARY ACC OFFICE                  
         CH    R2,=H'1'                                                         
         BNH   ADDBUFF2                                                         
*                                                                               
ADDBUFFX B     EXIT                                                             
         DROP  R3                                                               
         SPACE 2                                                                
*              ROUTINE TO CLEAR BUFFALO AMOUNTS                                 
*                                                                               
         USING BRECD,R3            R3=A(BUFFALO RECORD)                         
CLRBRAMT NTR1                                                                   
         LA    R1,BRECAMTS                                                      
         LA    R0,BRECALNQ/L'BRECAMTS                                           
CLRBRA5  ZAP   0(L'BRECAMTS,R1),=P'0'                                           
         LA    R1,L'BRECAMTS(R1)                                                
         BCT   R0,CLRBRA5                                                       
         B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
*---------------------------------------------------*                           
* PRTBUFF - PRINT BUFFALO RECORD TOTAL              *                           
*---------------------------------------------------*                           
*                                                                               
PRTBUFF  NTR1                                                                   
         L     R3,PP1                                                           
         USING LIND,R3                                                          
         LA    R2,MYBUFFIO        SET BUFFALO RECORD                            
         USING BRECD,R2                                                         
         EDIT  BRECRCV,(12,LRCVBL),2,FLOAT=-                                    
         EDIT  BRECGRS,(12,LGROSS),2,FLOAT=-                                    
         EDIT  BRECNET,(12,LNET),2,FLOAT=-                                      
         CLI   PSYS,C'P'                                                        
         BE    PRTB30                                                           
*                                 -- SPOT/NET  --                               
         EDIT  BRECAGY,(11,LAGYCOM),2,FLOAT=-                                   
         CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTB20                                                           
         MVC   TEMPAMT,BRECGST                                                  
         BAS   RE,CHKGST                                                        
         BNE   PRTB15                                                           
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LGST,=8C'*'                                                      
         B     PRTB15                                                           
         EDIT  BRECGST,(8,LGST),2,FLOAT=-                                       
PRTB15   EDIT  BRECPST,(8,LPST),2,FLOAT=-                                       
PRTB18   EDIT  BRECAOR,(10,LAORPAY),2,FLOAT=-                                   
         B     PRTB50                                                           
PRTB20   EDIT  BRECAOR,(10,LAORPAY2),2,FLOAT=-                                  
         B     PRTB50                                                           
*                                 -- PRINT --                                   
PRTB30   EDIT  BRECCD,(7,LPCD),2,FLOAT=-                                        
         EDIT  BRECAGY,(11,LPAGYCM),2,FLOAT=-                                   
         CLI   PCOUNTRY,C'C'                                                    
         BNE   PRTB40                                                           
         MVC   TEMPAMT,BRECGST                                                  
         BAS   RE,CHKGST                                                        
         BNE   PRTB35                                                           
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LPGST,=8C'*'                                                     
         B     PRTB35                                                           
         EDIT  BRECGST,(8,LPGST),2,FLOAT=-                                      
PRTB35   EDIT  BRECPST,(8,LPPST),2,FLOAT=-                                      
PRTB37   EDIT  BRECAOR,(10,LPAOR),2,FLOAT=-                                     
         B     PRTB50                                                           
PRTB40   EDIT  BRECAOR,(10,LPAOR2),2,FLOAT=-                                    
*                                                                               
PRTB50   GOTO1 PREPORT                                                          
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
*---------------------------------------------------*                           
* PBILERR - PRINT OUT BILLS THAT WERE IN ERROR      *                           
*---------------------------------------------------*                           
*                                                                               
PBILERR  MVI   REPTYPE,C'E'       ERROR SECTION                                 
         L     RE,PRCSUBP                                                       
         MVI   0(RE),50           US VERSION OF BILL ERROR REPORT               
         CLI   PCOUNTRY,C'C'                                                    
         BNE   *+8                                                              
         MVI   0(RE),40           CANADIAN VER OF BILL ERROR REPORT             
         L     RE,PFORCEH                                                       
         MVI   0(RE),C'Y'         START ON A NEW PAGE                           
*                                                                               
         CLOSE (ERRFIL,)                                                        
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OPEN  (ERRFIL,INPUT)                                                   
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    MEDOFF,MEDOFF      CLEAR MEDIA OFFICE                            
         XC    CLT,CLT            CLEAR CLIENT                                  
PBILER10 GET   ERRFIL,EREC                                                      
         LA    R2,EREC                                                          
         USING ERECD,R2                                                         
         L     R3,PP1                                                           
         USING LIND,R3                                                          
*                                                                               
         CLC   MEDOFF(1),ERECOFF   CHANGE IN MEDIA OFFICE                       
         BE    PBILER12                                                         
         MVC   MEDOFF(1),ERECOFF   SET NEW MEDIA OFFICE                         
         MVC   PMEDOFF,ERECOFF                                                  
         B     PBILER15                                                         
PBILER12 CLC   CLT,ERECCLT                                                      
         BE    PBILER20                                                         
PBILER15 L     RE,PFORCEH                                                       
         MVI   0(RE),C'Y'                                                       
         MVC   CLT,ERECCLT                                                      
*                                                                               
PBILER20 MVC   LPRD,ERECPRD       PRODUCT                                       
         MVC   LEST,ERECEST       ESTIMATE                                      
         MVC   LINV,ERECINV       INVOICE NUMBER                                
         MVC   LINVDT,ERECIDT     MOVE INVOICE DATE TO PRINT                    
         MVC   LINVTYP,ERECTYPE   AOR RECORD TYPE (IF APPLICABLE)               
         MVC   LDUEDT,ERECDDT     MOVE DUE DATE TO PRINT                        
         MVC   LMOS,ERECMOS       PRINT OUT MONTH OF SERVICE MMMYY              
         MVC   LMOA,ERECMOA       PRINT OUT MOA MMMYY                           
         CLI   LINVTYP,C'I'        IF IOR LINE                                  
         BNE   *+12                                                             
         BAS   RE,PBILERRI         GO SET IT UP                                 
         B     PBILER70                                                         
*                                                                               
         EDIT  ERECRCV,(12,LRCVBL),2,FLOAT=-                                    
         EDIT  ERECGRS,(12,LGROSS),2,FLOAT=-                                    
         EDIT  ERECNET,(12,LNET),2,FLOAT=-                                      
         CLI   PSYS,C'P'                                                        
         BE    PBILER50                                                         
         OC    ERECCOM,ERECCOM                                                  
         BZ    PBILER22                                                         
         EDIT  ERECCOM,(11,LAGYCOM),2,FLOAT=-                                   
PBILER22 CLI   PCOUNTRY,C'C'                                                    
         BNE   PBILER30                                                         
         MVC   TEMPAMT,ERECGST                                                  
         BAS   RE,CHKGST                                                        
         BNE   PBILER25                                                         
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LGST,=8C'*'                                                      
         B     PBILER25                                                         
         EDIT  ERECGST,(8,LGST),2,FLOAT=-                                       
PBILER25 OC    ERECPST,ERECPST                                                  
         BZ    PBILER27                                                         
         EDIT  ERECPST,(8,LPST),2,FLOAT=-                                       
PBILER27 OC    ERECAOR,ERECAOR                                                  
         BZ    PBILER28                                                         
         EDIT  ERECAOR,(10,LAORPAY),2,FLOAT=-                                   
PBILER28 CLI   PSYS,C'P'                                                        
         BNE   *+10                                                             
         MVC   LPROD,ERECPJB      PRODUCTION JOB CODE                           
         MVC   LACCOFF,ERECAOFF   ACC OFFICE                                    
         EDIT  ERECPG,(4,LPAGE)                                                 
         B     PBILER70                                                         
*                                                                               
PBILER30 OC    ERECAOR,ERECAOR    SPOT/NET -US VERSION                          
         BZ    PBILER32                                                         
         EDIT  ERECAOR,(10,LAORPAY2),2,FLOAT=-                                  
PBILER32 CLI   PSYS,C'P'                                                        
         BNE   *+10                                                             
         MVC   LPROD2,ERECPJB     PRODUCTION JOB CODE                           
         MVC   LACCOFF2,ERECAOFF  ACC OFFICE                                    
         EDIT  ERECPG,(4,LPAGE2)                                                
         B     PBILER70                                                         
*                                                                               
PBILER50 OC    ERECCD,ERECCD                                                    
         BZ    PBILER52                                                         
         EDIT  ERECCD,(7,LPCD),2,FLOAT=-                                        
PBILER52 OC    ERECCOM,ERECCOM                                                  
         BZ    PBILER54                                                         
         EDIT  ERECCOM,(11,LPAGYCM),2,FLOAT=-                                   
PBILER54 CLI   PCOUNTRY,C'C'                                                    
         BNE   PBILER60                                                         
         MVC   TEMPAMT,ERECGST                                                  
         BAS   RE,CHKGST                                                        
         BNE   PBILER55                                                         
         CLI   TEMPAMT,C'*'                                                     
         BNE   *+14                                                             
         MVC   LPGST,=8C'*'                                                     
         B     PBILER55                                                         
         EDIT  ERECGST,(8,LPGST),2,FLOAT=-                                      
PBILER55 OC    ERECPST,ERECPST                                                  
         BZ    PBILER56                                                         
         EDIT  ERECPST,(8,LPPST),2,FLOAT=-                                      
PBILER56 OC    ERECAOR,ERECAOR                                                  
         BZ    PBILER58                                                         
         EDIT  ERECAOR,(10,LPAOR),2,FLOAT=-                                     
PBILER58 MVC   LPAOFF,ERECAOFF   ACC OFFICE                                     
         EDIT  ERECPG,(4,LPPAGE)                                                
         B     PBILER70                                                         
*                                                                               
PBILER60 OC    ERECAOR,ERECAOR    PRINT -- US VERSION                           
         BZ    PBILER62                                                         
         EDIT  ERECAOR,(10,LPAOR2),2,FLOAT=-                                    
PBILER62 MVC   LPAOFF2,ERECAOFF  ACC OFFICE                                     
         EDIT  ERECPG,(4,LPPAGE2)                                               
*                                                                               
PBILER70 GOTO1 PREPORT                                                          
         B     PBILER10                                                         
         DROP  R2,R3                                                            
         SPACE 2                                                                
         USING ERECD,R2            R2=A(ERROR RECORD)                           
         USING LINIORD,R3          R3=A(PRINT LINE)                             
PBILERRI NTR1                                                                   
         CLI   PSYS,C'P'                                                        
         BE    PBILEI20                                                         
         CLI   PCOUNTRY,C'C'                                                    
         BE    PBILEI10                                                         
         EDIT  ERECIORN,(11,LIORSUN),2,FLOAT=-  SPOT/NET US VERSION             
         EDIT  ERECIORP,(11,LIORSUP),2,FLOAT=-                                  
         MVC   BYTE,ERECOFF       CONVERT OFFICE INTO PRINTABLE FORMAT          
         BRAS  RE,CNVOFF          (FOR 2 CHAR OFFICES)                          
         MVC   LIORSUOF,HALF                                                    
         B     PBILEIX                                                          
*                                                                               
PBILEI10 EDIT  ERECIORN,(11,LIORSCN),2,FLOAT=-  SPOT/NET CAN VERSION            
         EDIT  ERECIORP,(11,LIORSCP),2,FLOAT=-                                  
         MVC   BYTE,ERECOFF       CONVERT OFFICE INTO PRINTABLE FORMAT          
         BRAS  RE,CNVOFF          (FOR 2 CHAR OFFICES)                          
         MVC   LIORSCOF,HALF                                                    
         B     PBILEIX                                                          
*                                                                               
PBILEI20 CLI   PCOUNTRY,C'C'                                                    
         BE    PBILEI30                                                         
         EDIT  ERECIORN,(11,LIORPUN),2,FLOAT=-  PRINT US VERSION                
         EDIT  ERECIORP,(11,LIORPUP),2,FLOAT=-                                  
         MVC   BYTE,ERECOFF       CONVERT OFFICE INTO PRINTABLE FORMAT          
         BRAS  RE,CNVOFF          (FOR 2 CHAR OFFICES)                          
         MVC   LIORPUOF,HALF                                                    
         B     PBILEIX                                                          
*                                                                               
PBILEI30 EDIT  ERECIORN,(11,LIORPCN),2,FLOAT=-  PRINT CAN VERSION               
         EDIT  ERECIORP,(11,LIORPCP),2,FLOAT=-                                  
         MVC   BYTE,ERECOFF       CONVERT OFFICE INTO PRINTABLE FORMAT          
         BRAS  RE,CNVOFF          (FOR 2 CHAR OFFICES)                          
         MVC   LIORPCOF,HALF                                                    
PBILEIX  B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
*---------------------------------------------------*                           
* PRECAP  - PROCESS RECAPS                          *                           
*           FIRST ALL OF THE MEDIA OFFICE ONES      *                           
*           THEN THE ACC OFFICE ONES                *                           
*---------------------------------------------------*                           
*                                                                               
PRECAP   DS    0H                                                               
         MVI   RECTYPE,0          BUFFALO RECORD TYPE                           
         MVI   REPTYPE,C'M'       MEDIA OFFICE RECAP                            
*                                                                               
PRECAP5  L     RE,PSUMPROF        A(MX PROFILE)                                 
         CLI   REPTYPE,C'M'       IF DOING MEDIA SUMMARIES                      
         BNE   *+14                                                             
         MVC   SUMOPT,2(RE)       SET PRD/CLI/OFF MEDIA SUMMARY OPTS            
         B     *+10                                                             
         MVC   SUMOPT,5(RE)       ELSE, SET PRD/CLI/OFF ACCTG SUM OPTS          
*                                                                               
         BAS   RE,PRDRCP          RECAP FOR PRODUCT TOTALS                      
         BAS   RE,CLTRCP          RECAP FOR CLIENT TOTALS                       
         BAS   RE,OFFRCP          RECAP FOR OFFICE TOTALS                       
         CLI   RECTYPE,0                                                        
         BNE   EXIT                                                             
*                                                                               
         MVI   REPTYPE,C'A'       ACCOUNT OFFICE RECAP                          
         MVI   RECTYPE,1          PROCESS ACC OFFICE REPORTS                    
         B     PRECAP5                                                          
         EJECT                                                                  
* PRDRCP  - PRODUCT TOTALS                                                      
*                                                                               
PRDRCP   NTR1                                                                   
         CLI   SUMPOPT,C'N'       IF WANT PRODUCT SUMMARY                       
         BE    PRDRCPX                                                          
*                                                                               
         MVI   RCPTYPE,C'P'                                                     
         L     RE,PFORCEH         START ON A NEW PAGE                           
         MVI   0(RE),C'Y'                                                       
         L     RE,PRCSUBP                                                       
         MVI   0(RE),60           US PRODUCT SUMMARY HEADLINES                  
         CLI   PCOUNTRY,C'C'                                                    
         BNE   *+8                                                              
         MVI   0(RE),65           CANADIAN VERSION OF DETAIL REPORT             
*                                                                               
         LA    R2,MYBUFFIO                                                      
         USING BRECD,R2                                                         
         L     R3,PP1                                                           
         USING LIND,R3                                                          
         XC    MYBUFFIO,MYBUFFIO                                                
         BAS   RE,HIGHBUFF        READ HIGH FOR FIRST RECORD                    
         B     *+8                                                              
PRDRCP5  BAS   RE,SEQBUFF         GET NEXT RECORD                               
         BO    PRDRCPX                                                          
*                                                                               
         CLC   BRECTYP,RECTYPE    RIGHT RECORD TYPE                             
         BNE   PRDRCP5                                                          
         CLC   BRECPRD,=3X'FF'    PRODUCT LVL TOTAL?                            
         BE    PRDRCP20           NO - HIGHER LEVEL TOTAL                       
*                                                                               
         MVC   PACCOFF,BRECOFF    SET OFFICE FIELDS FOR HEADLINE                
         MVC   PMEDOFF,BRECOFF+1                                                
         MVC   CLT,BRECCLT                                                      
*                                 MOVE PRODUCT TOTALS OUT                       
         MVC   33(L'BRECPRD,R3),BRECPRD                                         
         BAS   RE,PRTBUFF         PRINT BUFF RECORD AMTS                        
         B     PRDRCP5            GET NEXT RECORD                               
*                                                                               
PRDRCP20 CLC   BRECCLT,=3X'FF'    CLIENT LVL TOTALS?                            
         BE    PRDRCP5            NO - INTERESTED IN CLT TOTAL ONLY             
         MVC   0(18,R3),=C'** CLIENT TOTAL **'                                  
         BAS   RE,PRTBUFF                                                       
         L     RE,PFORCEH                                                       
         MVI   0(RE),C'Y'         FORCE NEW PAGE                                
         B     PRDRCP5                                                          
*                                                                               
PRDRCPX  B     EXIT                                                             
         EJECT                                                                  
* CLTRCP  - CLIENT TOTALS                                                       
*                                                                               
CLTRCP   NTR1                                                                   
         CLI   SUMCOPT,C'N'       IF WANT CLIENT SUMMARY                        
         BE    CLTRCPX                                                          
*                                                                               
         MVI   RCPTYPE,C'C'                                                     
         L     RE,PFORCEH         START ON A NEW PAGE                           
         MVI   0(RE),C'Y'                                                       
         L     RE,PRCSUBP                                                       
         MVI   0(RE),70           CLIENT LVL SUMMARY HEADLINES                  
         CLI   PCOUNTRY,C'C'                                                    
         BNE   *+8                                                              
         MVI   0(RE),75           CANADIAN VERSION OF DETAIL REPORT             
*                                                                               
         LA    R2,MYBUFFIO                                                      
         USING BRECD,R2                                                         
         L     R3,PP1                                                           
         USING LIND,R3                                                          
*                                                                               
         XC    MYBUFFIO,MYBUFFIO                                                
         BAS   RE,HIGHBUFF        READ HIGH FOR FIRST RECORD                    
         B     *+8                                                              
CLTRCP5  BAS   RE,SEQBUFF         GET NEXT RECORD                               
         BO    CLTRCPX                                                          
*                                                                               
         CLC   BRECTYP,RECTYPE    RIGHT RECORD TYPE                             
         BNE   CLTRCP5                                                          
         CLC   BRECPRD,=3X'FF'    CLIENT TOTAL?                                 
         BNE   CLTRCP5            NO PRODUCT DETAIL LINE- SKIP IT               
         CLC   BRECCLT,=3X'FF'    CLIENT DETAIL LINE?                           
         BE    CLTRCP20           NO - HIGHER TOTAL                             
*                                                                               
         MVC   PACCOFF,BRECOFF    SET OFFICE FIELDS FOR HEADLINE                
         MVC   PMEDOFF,BRECOFF+1                                                
*                                 PRINT DETAIL CLIENT LINE                      
         MVC   34(L'BRECCLT,R3),BRECCLT                                         
         BAS   RE,PRTBUFF         PRINT BUFF RECORD AMTS                        
         B     CLTRCP5            GET NEXT RECORD                               
*                                                                               
CLTRCP20 CLC   BRECOFF,=2X'FF'    MEDIA OFF TOTAL?                              
         BE    CLTRCP5            NO - ONLY INTERESTED IN MEDIA'S?              
         MVC   0(24,R3),=C'** MEDIA OFFICE TOTAL **'                            
         CLI   REPTYPE,C'A'                                                     
         BNE   *+10                                                             
         MVC   0(26,R3),=C'** ACCOUNT OFFICE TOTAL **'                          
         BAS   RE,PRTBUFF                                                       
         L     RE,PFORCEH                                                       
         MVI   0(RE),C'Y'         FORCE NEW PAGE                                
         B     CLTRCP5                                                          
*                                                                               
CLTRCPX  B     EXIT                                                             
         EJECT                                                                  
* OFFRCP  - OFFICE TOTALS                                                       
*                                                                               
OFFRCP   NTR1                                                                   
         CLI   SUMOOPT,C'N'       IF WANT OFFICE SUMMARY                        
         BE    OFFRCPX                                                          
*                                                                               
         MVI   RCPTYPE,C'O'                                                     
         L     RE,PFORCEH         START ON A NEW PAGE                           
         MVI   0(RE),C'Y'                                                       
         L     RE,PRCSUBP                                                       
         MVI   0(RE),80           US VERSION - MEDIA OFFICE                     
         CLI   REPTYPE,C'A'                                                     
         BNE   *+8                                                              
         MVI   0(RE),90           US VERSION - ACC OFFICE                       
         CLI   PCOUNTRY,C'C'                                                    
         BNE   OFFRCP2                                                          
         MVI   0(RE),85           CANADIAN VERSION - MEDIA OFFICE               
         CLI   REPTYPE,C'A'                                                     
         BNE   *+8                                                              
         MVI   0(RE),95           CANADIAN VERSION - ACC OFFICE                 
*                                                                               
OFFRCP2  LA    R2,MYBUFFIO                                                      
         USING BRECD,R2                                                         
         L     R3,PP1                                                           
         USING LIND,R3                                                          
*                                                                               
         XC    MYBUFFIO,MYBUFFIO                                                
         BAS   RE,HIGHBUFF        READ HIGH FOR FIRST RECORD                    
         B     *+8                                                              
OFFRCP5  BAS   RE,SEQBUFF         GET NEXT RECORD                               
         BO    OFFRCPX                                                          
*                                                                               
         CLC   BRECTYP,RECTYPE    RIGHT RECORD TYPE                             
         BNE   OFFRCP5                                                          
         CLC   BRECCLT,=3X'FF'    MEDIA OFF TOTAL LINE?                         
         BNE   OFFRCP5            NO - GET NEXT                                 
         CLC   BRECOFF,=2X'FF'    MEDIA TOTAL LINE?                             
         BE    OFFRCP20                                                         
*                                                                               
         MVC   32(L'BRECOFF,R3),BRECOFF                                         
         CLI   REPTYPE,C'M'       PROCESSING MEDIA OFFICE TOTALS?               
         BNE   OFFRCP10           NO                                            
         MVC   BYTE,BRECOFF+1     CONVERT OFFICE INTO PRINTABLE FORMAT          
         BRAS  RE,CNVOFF          (FOR 2 CHAR OFFICES)                          
         MVC   32(2,R3),HALF                                                    
OFFRCP10 BAS   RE,PRTBUFF         PRINT BUFF RECORD AMTS                        
         B     OFFRCP5            GET NEXT RECORD                               
*                                                                               
OFFRCP20 MVC   0(17,R3),=C'** MEDIA TOTAL **'                                   
         BAS   RE,PRTBUFF                                                       
         L     RE,PFORCEH                                                       
         MVI   0(RE),C'Y'         FORCE NEW PAGE                                
         B     OFFRCP5                                                          
*                                                                               
OFFRCPX  B     EXIT                                                             
         EJECT                                                                  
* CHKGST - CHECKS IF GST AMOUNT WILL FIT INTO COLUMN                            
*                                                                               
CHKGST   NTR1                                                                   
         OC    TEMPAMT,TEMPAMT                                                  
         BZ    NO                                                               
         CP    TEMPAMT,=P'9999999' IF GST AMOUNT CAN'T FIT PRINT *'S            
         BNH   CHKGST5                                                          
         MVI   TEMPAMT,C'*'                                                     
         B     YES                                                              
*                                                                               
CHKGST5  CP    TEMPAMT,=P'-999999'                                              
         BNL   YES                                                              
         MVI   TEMPAMT,C'*'                                                     
         B     YES                                                              
         EJECT                                                                  
*                                                                               
* - BUFFALO COMMANDS PUT,HIGH, AND SEQ                                          
*                                                                               
PUTBUFF  LA    R1,=C'PUT'                                                       
         B     BUFFCALL                                                         
*                                                                               
HIGHBUFF LA    R1,=C'HIGH'                                                      
         B     BUFFCALL                                                         
*                                                                               
SEQBUFF  LA    R1,=C'SEQ'                                                       
         B     BUFFCALL                                                         
*                                                                               
BUFFCALL NTR1                                                                   
         ST    R1,DMCB            SET COMMAND                                   
         LA    R3,MYBUFFIO                                                      
         MVC   SVBKEY,0(R3)       SAVE KEY                                      
         GOTO1 PBUFFALO,DMCB,,BUFFBUFF,(R3),1                                   
         TM    DMCB+8,X'80'       SET CC CODE                                   
         B     EXIT                                                             
         SPACE 2                                                                
*                                                                               
* -- END OF DATA ADDRESS - NO MORE RECORDS IN ERRFIL                            
*                                                                               
NOMORE   CLOSE (ERRFIL,)                                                        
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         SPACE 2                                                                
*                                                                               
* PSPACES - PRE CLEAR 3 LINES TO SPACES                                         
*                                                                               
PSPACES  LR    R1,R2                                                            
         MVC   0(L'SPACES,R1),SPACES      P1                                    
         LA    R1,L'SPACES(R1)                                                  
         MVC   0(L'SPACES,R1),SPACES      P2                                    
         LA    R1,L'SPACES(R1)                                                  
         MVC   0(L'SPACES,R1),SPACES      P3                                    
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
* HEAD HOOK ROUTINE                                                             
*                                                                               
MYHOOK   NTR1                                                                   
         CLI   PSYS,C'N'          IF NETWORK                                    
         BNE   MYHK10                                                           
         L     R2,PH1                                                           
         MVC   7(12,R2),SPACES                                                  
         CLI   PMED,C'C'                                                        
         BNE   *+14                                                             
         MVC   7(6,R2),=C'/CABLE'                                               
         B     MYHK10                                                           
         CLI   PMED,C'S'                                                        
         BNE   MYHK10                                                           
         MVC   7(12,R2),=C'/SYNDICATION'                                        
*                                                                               
MYHK10   L     R2,PH2             CLEAR OUT & SET SECOND HEADLINE               
         XC    0(20,R2),0(R2)                                                   
         CLI   REPTYPE,C'D'       DETAIL REPORT?                                
         BNE   MYHK20                                                           
         BAS   RE,SETMOFF         SET MEDIA OFFICE                              
         B     MYHKX                                                            
*                                                                               
MYHK20   L     R1,PH3                                                           
         XC    0(35,R1),0(R1)                                                   
         CLI   REPTYPE,C'M'                                                     
         BNE   MYHK30                                                           
         CLI   RCPTYPE,C'O'       DOING MEDIA OFFICE SUM?                       
         BE    MYHKX              YES -SKIP MEDIA OFFICE HEADLINE               
         BAS   RE,SETMOFF         NO - SET MEDIA OFFICE                         
         CLI   RCPTYPE,C'P'       DOING PRODUCT SUM?                            
         BNE   MYHKX                                                            
         BAS   RE,SETCLT          SET CLIENT IN HEADLINES                       
         B     MYHKX                                                            
*                                                                               
MYHK30   CLI   REPTYPE,C'A'                                                     
         BNE   MYHK40                                                           
         CLI   RCPTYPE,C'O'       DOING ACC OFFICE SUM?                         
         BE    MYHKX              YES -SKIP ACC A OFFICE HEADLINE               
         BAS   RE,SETAOFF         SET ACCOUNTING OFFICE                         
         CLI   RCPTYPE,C'P'       DOING PRODUCT SUM?                            
         BNE   MYHKX                                                            
         BAS   RE,SETCLT          SET CLIENT IN HEADLINES                       
         B     MYHKX                                                            
*                                                                               
MYHK40   CLI   REPTYPE,C'E'                                                     
         BNE   MYHKX                                                            
         BAS   RE,SETMOFF         SET MEDIA OFFICE                              
         BAS   RE,SETCLT          SET CLIENT                                    
*                                                                               
MYHKX    B     EXIT                                                             
         EJECT                                                                  
* SET HEADLINE INFO                                                             
*                                                                               
SETMOFF  MVC   0(7,R2),=C'MED OFF'                                              
         MVC   BYTE,PMEDOFF        CONVERT MEDIA OFFICE INTO PRINTABLE          
         ST    RE,FULL             SAVE RE SO WE CAN RETURN                     
         BRAS  RE,CNVOFF           FORMAT-FOR 2 CHAR OFFICE CODE FILES          
         MVC   9(2,R2),HALF                                                     
         L     RE,FULL             RESTORE RE                                   
SETMOFFX BR    RE                                                               
         SPACE                                                                  
SETAOFF  MVC   0(7,R2),=C'ACC OFF'                                              
         MVC   9(L'PACCOFF,R2),PACCOFF                                          
SETAOFFX BR    RE                                                               
         SPACE                                                                  
SETCLT   NTR1                                                                   
         L     R1,PH3                                                           
         MVC   0(6,R1),=C'CLIENT'                                               
         MVC   9(L'CLT,R1),CLT                                                  
         BAS   RE,GETCNM                                                        
         MVC   13(L'CLTNM,R1),CLTNM                                             
SETCLTX  XIT1                                                                   
         EJECT                                                                  
* -----------------------------------*                                          
* GETCNM - GET CLIENT NAME GIVEN CLT *                                          
* -----------------------------------*                                          
GETCNM   NTR1                                                                   
         MVC   CLTNM,SPACES                                                     
         XC    KEY,KEY                                                          
         CLI   PSYS,C'P'                                                        
         BNE   GETCNM50                                                         
         LA    R2,KEY                                                           
         USING PCLTREC,R2                                                       
         MVC   PCLTKAGY,PAGY                                                    
         MVC   PCLTKMED,PMED                                                    
         MVI   PCLTKRCD,X'02'                                                   
         MVC   PCLTKCLT,CLT                                                     
         MVC   SAVEKEY,KEY                                                      
         GOTO1 PDMGR,DMCB,=C'DMRDHI',=C'PRTDIR',KEY,KEY                         
         CLC   KEY(25),SAVEKEY                                                  
         BNE   GETCNMX                                                          
         GOTO1 PDMGR,DMCB,=C'GETREC',=C'PRTFILE',KEY+27,IOAREA,DMWORK           
         LA    R2,IOAREA                                                        
         MVC   CLTNM,PCLTNAME                                                   
         B     GETCNMX                                                          
         DROP  R2                                                               
*                                                                               
GETCNM50 LA    R2,KEY                                                           
         USING CLTHDRD,R2                                                       
         MVC   CKEYAM,PAGYMD                                                    
         GOTO1 PCLPK,DMCB,CLT,WORK                                              
         CLI   0(R1),0                                                          
         BNE   GETCNMX                                                          
         MVC   CKEYCLT,WORK                                                     
         MVC   SAVEKEY,KEY                                                      
         GOTO1 PDMGR,DMCB,=C'DMRDHI',=C'SPTDIR',KEY,KEY                         
         CLC   KEY(13),SAVEKEY                                                  
         BNE   GETCNMX                                                          
         GOTO1 PDMGR,DMCB,=C'GETREC',=C'SPTFILE',KEY+14,IOAREA,DMWORK           
         LA    R2,IOAREA                                                        
         MVC   CLTNM,CNAME                                                      
GETCNMX  XIT1                                                                   
         DROP  R2                                                               
         EJECT                                                                  
* --------------------------------------------------------------------*         
* CNVOFF - CALL OFFICER TO CONVERT THE ONE BYTE INTERNAL MEDIA OFFICE           
*          TO A TWO BYTE PRINTABLE FORMAT.  FOR ONE BYTE OFFICES THE            
*          OFFICE WILL JUST BE PASSED BACK IN THE SAME FORMAT.                  
* ENTRY  - BYTE CONTAINS THE ONE BYTE OFFICE                                    
* EXIT   - HALF CONTAINS THE PRINTABLE OFFICE                                   
* --------------------------------------------------------------------*         
CNVOFF   NTR1                                                                   
         XC    WORK,WORK          CALL OFFICER TO CONVERT THE OFFICE            
         LA    R4,WORK            TO A PRINTABLE FORMAT FOR 2 CHAR              
         USING OFFICED,R4         FILES.                                        
         MVI   OFCSYS,C'P'                                                      
         CLI   PSYS,C'P'          PRINT SYSTEM?                                 
         BE    CNVOF10                                                          
         MVI   OFCSYS,C'S'                                                      
         CLI   PSYS,C'S'          SPOT SYSTEM?                                  
         BE    CNVOF10                                                          
         MVI   OFCSYS,C'N'        THAN MUST BE NET                              
CNVOF10  MVC   OFCOFC,BYTE        PASS INTERNAL CLIENT OFFICE                   
         MVC   OFCAGY,PALPHA                                                    
         GOTO1 POFFICER,DMCB,(C'2',WORK),(0,PCOMFACS)                           
         LA    R4,WORK                                                          
         MVC   HALF,OFCOFC2       NO SO MOVE IN 2 BYTE OFFICE                   
         XIT1                                                                   
         DROP  R4                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*--------------------*                                                          
* LITERAL POOL       *                                                          
*--------------------*                                                          
*                                                                               
ERRFIL   DCB   DDNAME=ERRFIL,DSORG=PS,RECFM=FB,LRECL=150,MACRF=(GM,PM),X        
               BLKSIZE=15000,EODAD=NOMORE                                       
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
DUB2     DS    D                                                                
DMCB     DS    6F                                                               
RELO     DS    F                                                                
FULL     DS    F                                                                
BUFFBUFF DS    A                                                                
*                                                                               
AMNTS    DS    0PL8                AMOUNTS                                      
RCVAMT   DS    PL8                                                              
GRSAMT   DS    PL8                                                              
NETAMT   DS    PL8                                                              
CDAMT    DS    PL8                                                              
AGYCOM   DS    PL8                                                              
GSTAMT   DS    PL8                                                              
AORAMT   DS    PL8                                                              
GSTIAMT  DS    PL8                                                              
AORAGYCM DS    PL8                                                              
PSTAMT   DS    PL8                                                              
PSTIAMT  DS    PL8                                                              
IORAMT   DS    PL8                                                              
NAMNTS   EQU   (*-AMNTS)/L'AMNTS                                                
*                                                                               
MEMO     DS    PL8                                                              
AMOUNT   DS    PL8                                                              
TEMPAMT  DS    PL8                                                              
HALF     DS    H                                                                
BYTE     DS    X                                                                
TBYTE    DS    X                                                                
FLAG     DS    C                                                                
ERRRECS  DS    C                                                                
BUFFSW   DS    C                                                                
FLIPAOR  DS    C                                                                
USSRACC  DS    C                  U.S SR ACCOUNT                                
INVTYPE  DS    C                  AOR BILL TYPE                                 
TYPEBIL  DS    CL3                                                              
SUBTYPE  DS    CL3                                                              
POSTNUM  DS    XL1                                                              
RECTYPE  DS    XL1                                                              
REPTYPE  DS    XL1                D= DETAIL RPT,M=MED OFF RCP,A=ACC OFF         
RCPTYPE  DS    XL1                IF REPTYPE=(A/M) THEN O,C,P                   
*                                                                               
MEDOFF   DS    CL2                                                              
CLT      DS    CL3                USED FOR BREAKING BY CLIENT                   
PRD      DS    CL3                CURRENT PRODUCT                               
EST      DS    CL3                CURRENT ESTIMATE                              
INVDT    DS    CL5                INVOICE DATE                                  
DUEDT    DS    CL8                DUE DATE                                      
MOS      DS    CL5                MONTH OF SERVICE                              
MOA      DS    CL5                MOA                                           
*                                                                               
SUMOPT   DS    0CL3                NO SUMMARY OPTION                            
SUMPOPT  DS    CL1                 NO PRODUCT MEDIA OR ACCTG OFFICE             
SUMCOPT  DS    CL1                 NO CLIENT MEDIA OR ACCTG OFFICE              
SUMOOPT  DS    CL1                 NO OFFICE MEDIA OR ACCTG OFFICE              
*                                                                               
KEY      DS    CL44                                                             
KEYSAVE  DS    CL44                                                             
SAVEKEY  DS    CL44                                                             
*                                                                               
CLTNM    DS    CL20                                                             
SVBKEY   DS    CL(L'BRECKEY)      SAVED BUFFALO KEY                             
MYBUFFIO DS    CL(BRECLNQ)                                                      
EREC     DS    CL150                                                            
SPACES   DS    CL132                                                            
WORK     DS    XL100                                                            
ELEM     DS    XL100                                                            
*                                                                               
         DS    0D                                                               
IOAREA   DS    2000C                                                            
*                                                                               
WORKDL   EQU   *-WORKD                                                          
         SPACE 2                                                                
         EJECT                                                                  
*                                                                               
* - BILL LINE                                                                   
*                                                                               
LIND     DSECT                                                                  
LPRD     DS    CL3                PRODUCT                                       
         DS    CL1                                                              
LEST     DS    CL3                ESTIMATE                                      
         DS    CL1                                                              
LINV     DS    CL10               INVOICE NUMBER                                
LINVTYP  DS    CL1                                                              
LINVDT   DS    CL5                INVOICE DATE                                  
         DS    CL1                                                              
LDUEDT   DS    CL8                DUE DATE                                      
         DS    CL1                                                              
LMOS     DS    CL5                MONTH OF SERVICE                              
         DS    CL1                                                              
LMOA     DS    CL5                MOA                                           
         DS    CL1                                                              
LRCVBL   DS    CL12               RECEIVABLE AMOUNT                             
         DS    CL1                                                              
LGROSS   DS    CL12               GROSS AMOUNT                                  
         DS    CL1                                                              
LNET     DS    CL12               NET AMOUNT                                    
         DS    CL1                                                              
*                                                                               
*                                 SPOT/NET -- CANADIAN VERSION                  
LAGYCOM  DS    CL11               AGENCY COMMISSION                             
         DS    CL1                                                              
LGST     DS    CL8                GST AMOUNT                                    
         DS    CL1                                                              
LAORPAY  DS    CL10               AOR PAYABLE/RCVBL AMOUNT                      
         DS    CL1                                                              
LACCOFF  DS    CL2                ACC OFFICE                                    
         DS    CL1                                                              
LPAGE    DS    CL4                PAGE NUMBER                                   
         DS    CL11               SPARE                                         
         ORG   LPAGE                                                            
LREV     DS    CL3                REVERSAL NOTE                                 
         DS    CL12               SPARE                                         
         ORG   LGST+132                                                         
LPST     DS    CL8                                                              
         ORG                                                                    
*                                                                               
*                                 SPOT/NET -- U.S. VERSION                      
         ORG   LGST                                                             
LAORPAY2 DS    CL10               AOR PAYABLE/RCVBL AMOUNT                      
         DS    CL1                                                              
LACCOFF2 DS    CL2                ACC OFFICE                                    
         DS    CL1                                                              
LPAGE2   DS    CL4                PAGE NUMBER                                   
         DS    CL20               SPARE                                         
         ORG   LPAGE2                                                           
LREV2    DS    CL3                REVERSAL NOTE                                 
         DS    CL21               SPARE                                         
*                                                                               
*                                 PRINT/NET -- CANADIAN VERSION                 
         ORG   LAGYCOM                                                          
LPCD     DS    CL7                CD  AMOUNT                                    
         DS    CL1                                                              
LPAGYCM  DS    CL11               AGENCY COMMISSION                             
         DS    CL1                                                              
LPGST    DS    CL8                GST AMOUNT                                    
         DS    CL1                                                              
LPAOR    DS    CL10               AOR PAYABLE/RCVBL AMOUNT                      
         DS    CL1                                                              
LPROD    DS    CL6                PROD JOB                                      
         DS    CL1                                                              
LPAOFF   DS    CL2                ACC OFFICE                                    
         DS    CL1                                                              
LPPAGE   DS    CL4                PAGE NUMBER                                   
*                                 NO SPARE HERE                                 
         ORG   LPPAGE                                                           
LPREV    DS    CL3                REVERSAL NOTE                                 
         DS    CL1                                                              
         ORG   LPGST+132                                                        
LPPST    DS    CL8                 PST AMOUNT                                   
         ORG                                                                    
*                                                                               
*                                 PRINT -- U.S. VERSION                         
         ORG   LPGST                                                            
LPAOR2   DS    CL10               AOR PAYABLE/RCVBL AMOUNT                      
         DS    CL1                                                              
LPROD2   DS    CL6                PROD JOB                                      
         DS    CL1                                                              
LPAOFF2  DS    CL2                ACC OFFICE                                    
         DS    CL1                                                              
LPPAGE2  DS    CL4                PAGE NUMBER                                   
         DS    CL6                SPARE                                         
         ORG   LPPAGE2                                                          
LPREV2   DS    CL3                REVERSAL NOTE                                 
         DS    CL7                SPARE                                         
*                                                                               
         EJECT                                                                  
*              DSECT TO COVER IOR LINE (2ND LINE)                               
*                                                                               
LINIORD  DSECT                                                                  
LIPRD    DS    CL3                PRODUCT                                       
         DS    CL1                                                              
LIEST    DS    CL3                ESTIMATE                                      
         DS    CL1                                                              
LIINV    DS    CL6                INVOICE NUMBER                                
LIINVTYP DS    CL1                                                              
LIINVDT  DS    CL5                INVOICE DATE                                  
         DS    CL1                                                              
LIDUEDT  DS    CL8                DUE DATE                                      
         DS    CL1                                                              
LIMOS    DS    CL5                MONTH OF SERVICE                              
         DS    CL1                                                              
LIMOA    DS    CL5                MOA                                           
         DS    CL1                                                              
         DS    CL36                                                             
LITAG    EQU   *                                                                
*                                                                               
         ORG   LITAG              SPOT/NET -- CANADIAN VERSION                  
LIORSCN  DS    CL11               IOR SPOT CANADIAN NEGATIVE                    
         DS    CL10                                                             
LIORSCP  DS    CL10               IOR SPOT CANADIAN POSITIVE                    
         DS    CL1                                                              
LIORSCOF DS    CL2                IOR SPOT CANADIAN OFFICE                      
         ORG                                                                    
*                                                                               
         ORG   LITAG               SPOT/NET -- US VERSION                       
LIORSUN  DS    CL11                IOR SPOT US NEGATIVE                         
         DS    CL1                                                              
LIORSUP  DS    CL10                IOR SPOT US POSITIVE                         
         DS    CL1                                                              
LIORSUOF DS    CL2                 IOR SPOT US OFFICE                           
         ORG                                                                    
*                                                                               
         ORG   LITAG               PRINT -- CANADIAN VERSION                    
         DS    CL8                                                              
LIORPCN  DS    CL11                IOR PRINT CANADIAN NEGATIVE                  
         DS    CL10                                                             
LIORPCP  DS    CL10                IOR PRINT CANADIAN POSITIVE                  
         DS    CL8                                                              
LIORPCOF DS    CL2                 IOR PRINT CANADIAN OFFICE                    
         ORG                                                                    
*                                                                               
         ORG   LITAG               PRINT -- US VERSION                          
         DS    CL8                                                              
LIORPUN  DS    CL11                IOR PRINT US NEGATIVE                        
         DS    CL1                                                              
LIORPUP  DS    CL10                IOR PRINT US POSITIVE                        
         DS    CL8                                                              
LIORPUOF DS    CL2                 IOR PRINT US OFFICE                          
         ORG                                                                    
         EJECT                                                                  
*                                                                               
* - POSTINGS                                                                    
*                                                                               
LPOSTD   DSECT                                                                  
LPACC    DS    CL14               ACCOUNT                                       
         DS    CL1                                                              
LPTYPE   DS    CL2                DEBIT OR CREDIT                               
         DS    CL1                                                              
LPAMT    DS    CL13               POSTING AMOUNT                                
         DS    CL1                                                              
LPMEMO   DS    CL13               MEMO AMOUNT                                   
         DS    CL4                                                              
LPERR    DS    CL60                                                             
         SPACE                                                                  
*                                                                               
* - BUFFALO RECORD                                                              
BRECD    DSECT                                                                  
BRECKEY  DS    0CL10                                                            
BRECTYP  DS    CL1                KEY PORTION                                   
BRECMED  DS    CL1                                                              
BRECOFF  DS    CL2                                                              
BRECCLT  DS    CL3                                                              
BRECPRD  DS    CL3                                                              
*                                 DATA PORTION                                  
BRECAMTS DS    0PL8                                                             
BRECRCV  DS    PL8                                                              
BRECGRS  DS    PL8                                                              
BRECNET  DS    PL8                FOR PRINT NET - CD                            
BRECCD   DS    PL8                FOR PRINT                                     
BRECGST  DS    PL8                                                              
BRECPST  DS    PL8                                                              
BRECAGY  DS    PL8                                                              
BRECAOR  DS    PL8                                                              
BRECIOR  DS    PL8                                                              
BRECALNQ EQU   (*-BRECAMTS)                                                     
BRECLNQ  EQU   (*-BRECD)                                                        
*                                                                               
* - ERECD  RECORD                                                               
ERECD    DSECT                                                                  
ERECMED  DS    CL1                                                              
ERECOFF  DS    CL2                                                              
ERECCLT  DS    CL3                                                              
ERECPRD  DS    CL3                PRODUCT                                       
ERECEST  DS    CL3                ESTIMATE                                      
ERECINV  DS    CL10               INVOICE NUMBER                                
ERECIDT  DS    CL5                INVOICE DATE                                  
ERECDDT  DS    CL8                DUE DATE                                      
ERECMOS  DS    CL5                MONTH OF SERVICE                              
ERECMOA  DS    CL5                MOA                                           
ERECRCV  DS    PL8                RECEIVABLE AMOUNT                             
ERECGRS  DS    PL8                GROSS AMOUNT                                  
ERECNET  DS    PL8                NET AMOUNT (NET-CD FOR PRINT)                 
ERECCD   DS    PL8                CD AMOUNT                                     
ERECCOM  DS    PL8                AGENCY COMMISSION                             
         ORG   ERECCOM                                                          
ERECIORN DS    PL8                NEGATIVE IOR AMOUNT                           
ERECGST  DS    PL8                GST AMOUNT (CANADIAN VERSION)                 
ERECPST  DS    PL8                PST AMOUNT (CANADIAN VERSION)                 
ERECAOR  DS    PL8                AOR PAYABLE/RCVBL AMOUNT                      
         ORG   ERECAOR                                                          
ERECIORP DS    PL8                IOR AMOUNT                                    
ERECPJB  DS    CL6                PRINT PROD JOB                                
ERECAOFF DS    CL2                ACC OFFICE                                    
ERECPG   DS    XL2                PAGE NUMBER                                   
ERECTYPE DS    XL1                'A'=AOR LINE,'I'=IOR LINE                     
         EJECT                                                                  
*===========================*                                                   
* OTHER VARIOUS DSECTS      *                                                   
*===========================*                                                   
*                                                                               
* BUFFALO                                                                       
* ACPOSTD                                                                       
* ACPOSTD                                                                       
* DDBUFFALOD                                                                    
*                                                                               
         PRINT GEN                                                              
         BUFF  LINES=800,ROWS=1,COLUMNS=9,FLAVOR=PACKED,KEYLIST=(10,A)          
         SPACE                                                                  
         PRINT NOGEN                                                            
       ++INCLUDE MXPRNTD                                                        
         EJECT                                                                  
       ++INCLUDE ACPOSTD                                                        
         EJECT                                                                  
       ++INCLUDE DDBUFFALOD                                                     
         EJECT                                                                  
* DDCOMFACSD                                                                    
* DDOFFICED                                                                     
* SPGENBILL                                                                     
* PBILLREC                                                                      
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE DDOFFICED                                                      
BILLRECD DSECT                                                                  
       ++INCLUDE SPGENBILL                                                      
         ORG BILLRECD                                                           
       ++INCLUDE PBILLREC                                                       
*                                                                               
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
*                                                                               
       ++INCLUDE PCLTREC                                                        
*                                                                               
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'079MXPRNT    03/28/13'                                      
         END                                                                    
