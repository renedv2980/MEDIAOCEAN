*          DATA SET FASTART    AT LEVEL 017 AS OF 01/29/19                      
*CATALP FASTART                                                                 
         TITLE 'FASTART - INITIALISE AND START ONLINE SYSTEM'                   
***********************************************************************         
* UPSI 00000001 AUTO BUILD PRINTER QUEUES                             *         
*                                                                     *         
* ON ENTRY R1=A(PARM LIST)=A(ADCON LIST),A(FACPAK VARIABLES)          *         
***********************************************************************         
         PRINT NOGEN                                                            
FALOAD   START                                                                  
         NBASE 0,**LOAD**,WORK=ASWRK,SAVE=YES                                   
         ENTRY AMSOON                                                           
         ENTRY XPEDITER                                                         
         EJECT                                                                  
***********************************************************************         
* CALL FASTART LOGIC TO INITIALISE FACPAK                             *         
***********************************************************************         
         L     RA,ASYSFAC                                                       
         USING SYSFACD,RA                                                       
         GOTO1 VSTART,(R1)         GO TO START UP PROGRAM (FASTART)             
         OC    0(4,R1),0(R1)                                                    
         BZ    LOADEOJ                                                          
                                                                                
***********************************************************************         
* BUILD LOCAL (2K) TBUFFS AND SMALL UTL ENTRIES OVER FASTART          *         
***********************************************************************         
         L     R2,VTCB             BUILD 24-BIT (2K) TBUFF AREAS                
         LH    R4,0(R2)            OVER TOP OF FASTART                          
         L     R5,2(R2)                                                         
         LA    R2,6(R2)                                                         
         USING TCBD,R2                                                          
         L     RF,VSSB                                                          
         TM    SSBSTAT4-SSBD(RF),SSBSAOR                                        
         BZ    *+6                 IF THIS IS AN AOR THAN WE HAVE AN            
         AR    R5,R4               EXTRA TASK FOR SYSTEM STUFF                  
*                                                                               
         L     R7,VSTART                                                        
         LA    R7,15(R7)                                                        
         SRL   R7,4                                                             
         SLL   R7,4                                                             
         MVC   0(L'I2KTB,R7),I2KTB                                              
         LA    R7,16(R7)                                                        
*                                                                               
FXTCB02  MVC   0(L'TCBID,R7),TCBID                                              
         MVC   0(5,R7),=CL5'**UTL' SET '**UTL N*' AS HEADER                     
         AHI   R7,L'TCBID                                                       
         ST    R7,TCBLUTL          SET A(LOCAL UTL)                             
         LR    R0,R7                                                            
         LHI   R1,TUTLXALN                                                      
         XR    RF,RF                                                            
         MVCL  R0,RE               CLEAR UTL AREA                               
         AHI   R7,TUTLXALN                                                      
*                                                                               
         MVC   0(L'TCBID,R7),TCBID                                              
         MVC   0(5,R7),=CL5'TBUFF' SET 'TBUFF N*' AS HEADER                     
         AHI   R7,L'TCBID                                                       
*                                                                               
         LR    R0,R7                                                            
         LHI   R1,32+TBUFFL                                                     
         XR    RF,RF                                                            
         MVCL  R0,RE               CLEAR TBUFF AREA                             
*                                                                               
         LA    R0,32(R7)           SET A(TBUFF)                                 
         STCM  R0,15,TCBTBUFF                                                   
         AHI   R7,32+TBUFFL        NEXT TBUFF AREA                              
         BXLE  R2,R4,FXTCB02                                                    
         DROP  R2                                                               
                                                                                
***********************************************************************         
* BEGIN EXECUTION OF FACPAK                                           *         
***********************************************************************         
         L     RF,AADFIRST         GO TO TASK CONTROLLER                        
         XR    R1,R1                                                            
         BASR  RE,RF                                                            
***********************************************************************         
* END EXECUTION                                                       *         
***********************************************************************         
LOADEOJ  ICM   RF,15,VSSB          TEST STORAGE PROTECTION ENABLED              
         TM    SSBPROT-SSBD(RF),SSBPONQ                                         
         BO    LOADEOJ2                                                         
LOADEOJ1 XBASE                                                                  
LOADEOJ2 PR                                                                     
         EJECT                                                                  
***********************************************************************         
* THESE ENTRY POINTS ARE NOT USED ONLINE OR ANY MORE BUT NEED TO BE   *         
* AROUND TO CLEAN UP THE UNRESOLVED REFERENCES IN THE LINK            *         
***********************************************************************         
         DS    0L                                                               
         DC    CL9'AMSOON  ='                                                   
AMSOON   DC    C'N',XL6'00'                                                     
         DC    CL9'XPEDITER='                                                   
XPEDITER DC    C'N',XL6'00'                                                     
*                                                                               
VSTART   DC    V(FASTART)                                                       
AADFIRST DC    V(ADFIRST)                                                       
ASYSFAC  DC    V(SYSFAC)                                                        
I2KTB    DC    CL16'*TCBTBUFF*TCBTBUFF'                                         
         LTORG                                                                  
*                                                                               
ASWRK    DC    20D'0'                                                           
         EJECT                                                                  
***********************************************************************         
* FASTART CODE - GETS OVERWRITTEN BY LOAD ABOVE                       *         
***********************************************************************         
FASTART  CSECT                                                                  
         NBASE WORKL,**STRT**,WORK=AWORK,SAVE=YES                               
         USING WORKD,RC                                                         
         L     R9,ALITS                                                         
         USING LITERALS,R9         R9=A(LITERALS/CONSTANTS)                     
         L     RA,VSYSFAC                                                       
         USING SYSFACD,RA          RA=A(SYSTEM FACILITIES)                      
*                                                                               
         ST    RD,SAVERD           SAVE RD FOR EXIT                             
         ST    R1,APARM                                                         
         MVC   LOADPARM(LOADPARX-LOADPARM),0(R1)                                
         DATE  LOADDATE                                                         
*                                                                               
         LA    R0,FAPARMS          LOAD FACPAK PARM LIST                        
         GOTO1 VLOADER,DMCB,FAPARM,(R0),(C'A',(R0))                             
         OC    4(4,R1),4(R1)                                                    
         BNZ   STRT02                                                           
*                                                                               
         LA    R1,89               FAPARMS NOT FOUND                            
         BRAS  RE,PUTMESS                                                       
         B     STRTEOJ             EXIT BACK TO CALLER                          
*                                                                               
AWORK    DC    A(STRTWORK)                                                      
ALITS    DC    A(LITERALS)                                                      
         EJECT                                                                  
***********************************************************************         
* CONTINUATION OF FASTART CODE                                        *         
***********************************************************************         
STRT02   XR    RF,RF                                                            
         ICM   RF,1,FATASKS        SET UP TASK COUNT OVERRIDE IF SET            
         BZ    *+8                                                              
         ST    RF,TSKNUM                                                        
*                                                                               
         L     R2,LOADHIC          GET INITIAL HIGH CORE                        
         AHI   R2,7                                                             
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         ST    R2,HICORE           SET START OF HIGH CORE                       
         MVC   HICOREX,LOADHICX    SET END OF HIGH CORE                         
*                                                                               
         L     RF,HICOREX          NEED A BUFFER ADDRESS                        
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         S     RF,TRKLEN                                                        
         ST    RF,ASTRTBUF         SET READ BUFFER ADDRESS                      
*                                                                               
         L     R5,VSSB                                                          
         USING SSBD,R5                                                          
         L     R6,VTCB             POINT TO FIRST TASK                          
         LA    R6,6(R6)                                                         
         ST    R6,SVTSKADR         SAVE A(1ST TASK) AND SET IN SSB              
         ST    R6,SSBTKADR                                                      
         LR    R5,R6               SET A(DUMMY UTL ENTRY IN 1ST TASK)           
         USING TCBD,R5                                                          
         L     R6,VUTLZERO                                                      
         USING UTLD,R6                                                          
         MVC   TSVCREQ,=XL2'0101'                                               
         ST    R6,TCBUTL                                                        
         DROP  R6                                                               
*                                                                               
         MVC   UPSI,LOADUPSI                                                    
         MVC   VLCM,=V(LCM)                                                     
*                                                                               
         BRAS  RE,SETSSB           SET UP INITAL SSB VALUES                     
         BRAS  RE,GETSPC           GET BOTH DATASPACES                          
         BRAS  RE,GETTOR           GET A(TOR SLOT) IN DMGR DATASPACE            
         BRAS  RE,SETPRG           SET PROGRAMS TO OP/NOP                       
         EJECT                                                                  
***********************************************************************         
* GET SYSTEM LOAD OPTION                                              *         
***********************************************************************         
STRT04   MVI   REPLY,C'N'          READ-ONLY DEFAULT=N                          
         LA    R1,1                                                             
         BRAS  RE,PUTMESS                                                       
         MVC   OPTRONLY,REPLY                                                   
         CLI   OPTRONLY,C'Y'       TEST READ-ONLY                               
         BE    *+12                                                             
         CLI   OPTRONLY,C'N'                                                    
         BNE   STRT04              ASK AGAIN                                    
*                                                                               
         CLI   OPTRSTRT,C'I'                                                    
         BNE   STRT12                                                           
         CLI   FATOR,C'Y'                                                       
         BE    STRT10                                                           
         MVI   OPTRSTRT,C'N'       NO RESTART FOR AOR                           
         B     STRT12                                                           
*                                                                               
STRT10   MVI   REPLY,C'Y'          RESTART DEFAULT=Y                            
         LA    R1,2                                                             
         BRAS  RE,PUTMESS                                                       
         MVC   OPTRSTRT,REPLY                                                   
*                                                                               
STRT12   CLI   OPTRSTRT,C'Y'       TEST RESTART                                 
         BE    STRT16                                                           
         CLI   OPTRSTRT,C'N'                                                    
         BNE   STRT10              ASK AGAIN                                    
*                                                                               
         CLI   OPTVALDT,C'Y'       NO RESTART CHECK DAY IS VALID                
         BNE   *+8                                                              
         BRAS  RE,VALDATE                                                       
*                                                                               
STRT16   CLI   OPTRSTRT,C'Y'       IF NO RESTART AND TOR SET UP TEMPEST         
         BE    STRT18                                                           
         CLI   FATOR,C'Y'          TEST TOR                                     
         BNE   STRT18                                                           
         BRAS  RE,SETTMSA                                                       
         EJECT                                                                  
***********************************************************************         
* GET A(SYSFLES LIST) FOR SE 1.                                       *         
* A(LIST) IS RETURNED IN DMCB+12.                                     *         
***********************************************************************         
STRT18   BRAS  RE,RNMFIL           PERFORM FILE RENAMES                         
         BRAS  RE,SETRBUF          ALLOCATE BUFFERS FOR RECOVERY                
         BRAS  RE,SETINFO          SET PARAMS FROM BOOTLIST INFO                
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMREAD,SYSFLES,1                                   
*                                                                               
         CLI   OPTLOAD,C'Y'        IF LOADOPT=Y, SET NO EOF FOR PRGMS           
         BNE   *+14                                                             
         MVC   DUB(4),VPRGMS                                                    
         BRAS  RE,SETNOEOF                                                      
*                                                                               
         XC    P1(24),P1           OPEN SYSTEM FACILITIES FILES                 
         MVC   P1,VOPENSYS                                                      
         MVC   P2,HICORE           SET I/O AREA ADDRESS                         
         MVI   P2,1                SET SYSTEM=1                                 
         LA    R2,DMCB                                                          
         GOTO1 VDMOD000,P1                                                      
*                                                                               
         BRAS  RE,GETXA                                                         
         BRAS  RE,GETXA9                                                        
*                                                                               
         MVC   P1,VDARPT           GET TRACK CAPACITY OF PRGMS FILE             
         MVC   P3+2(2),=X'FFFF'                                                 
         MVC   P4,VPRGMS                                                        
         GOTO1 VDADDS,P1                                                        
         SR    RE,RE                                                            
         ICM   RE,3,P3+2           3390 HAS MAX OF 56,664 IE 55.34K             
         STH   RE,PHLMAX                                                        
*                                                                               
         BRAS  RE,GETAREAS         BUILD UTL/PRQ/PQE BUFFERS                    
         BRAS  RE,GETDUTL          BUILD DUMMY UTL ENTRIES                      
         BRAS  RE,GETBUFF          BUILD XA VTAM BUFFERS                        
         DROP  R5                                                               
*                                                                               
         CLI   OPTLOAD,C'Y'                                                     
         BNE   STRT20                                                           
         BRAS  RE,CHKPT            CREATE NEW CHECKPOINTS                       
*                                                                               
STRT20   L     R8,ASTRTBUF                                                      
         L     RE,SVTSKADR         GET ACCESS TO SE1 FILES                      
         USING TCBD,RE                                                          
         L     RF,ASTRTBUF         NEED A BUFFER ADDRESS IN TCBFILES            
         S     RF,OLDPHLEN         ALLOW 200K                                   
         ST    RF,TCBFILES                                                      
         L     RF,VSELIST                                                       
         LA    RF,6(RF)                                                         
         MVC   TCBDTFS(8),SEFILES-SELISTD(RF)                                   
         ST    RE,P1                                                            
         MVI   P1,C'I'                                                          
         GOTO1 VDTFIOA,P1          INITIALIZE DTFS                              
         DROP  RE                                                               
*                                                                               
         CLI   OPTRSTRT,C'Y'       IS THIS A RESTART                            
         BE    STRT22                                                           
         CLI   FACUTAB,C'N'        SUPPRESS REBUILD VERSION TABLE               
         BE    STRT22                                                           
         BRAS  RE,BLDVRSN          NOW GO BUILD VERSION TABLE                   
*                                                                               
STRT22   BRAS  RE,BLDPHLS          BUILD PHASE LIST                             
         BRAS  RE,SETOPS           SET UP OPERATOR COMMUNICATIONS               
         BRAS  RE,VERSE            VERIFY SE SETUP                              
*                                                                               
         CLI   OPTRSTRT,C'N'       IS THIS A RESTART?                           
         BE    *+12                                                             
         BRAS  RE,RESTART          YES-RESTART                                  
         B     LOADTABS                                                         
*                                                                               
         BRAS  RE,START            NO-START                                     
         B     LOADTABS                                                         
         EJECT                                                                  
***********************************************************************         
* LOAD OTHER SYSTEM TABLES                                            *         
***********************************************************************         
LOADTABS XC    P1(24),P1           READ TSTTAB FROM TSTRCVR FILE                
         MVC   P1,VRDID                                                         
         MVC   P2,VTSTTAB                                                       
         MVC   P4,VTSTRCVR                                                      
         MVC   P6,=X'00010100'                                                  
         LA    RE,P6                                                            
         ST    RE,P5                                                            
         L     RE,VTSTTAB                                                       
         MVC   SAVE,2(RE)          SAVE A(END OF TSTTAB)                        
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    LTABS02                                                          
         BRAS  RE,IOERROR                                                       
         ABEND 301,DUMP                                                         
*                                                                               
LTABS02  L     R1,VTSTTAB                                                       
         MVC   2(4,R1),SAVE        RESTORE A(END OF TSTTAB)                     
         EJECT                                                                  
***********************************************************************         
* ALLOCATE STORAGE FOR TRACE BUFFERS TO TSTTAB ENTRIES                *         
***********************************************************************         
         L     R5,VTSTTAB                                                       
         USING TSTTABD,R5          PASS TSTTAB TO CLEAR A(TRCTAB)               
         BRAS  RE,SETBXLE                                                       
         XC    TSTTRC,TSTTRC       FIRST ENTRY IS SPECIAL                       
         XC    TSTSCT,TSTSCT                                                    
*                                                                               
         SAM31                                                                  
         L     R1,XAATSTT          R1=A(TEST BUFFER)                            
         A     R1,XALOCORE                                                      
         MVC   00(16,R1),=CL16'XA TSTTAB BUFFER'                                
         MVC   16(16,R1),=CL16'XA TSTTAB BUFFER'                                
         AHI   R1,32                                                            
         B     ALTR06                                                           
*                                                                               
ALTR02   XC    TSTTRC,TSTTRC                                                    
         ICM   R0,15,FACTRACE      TEST NEED TRACE BUFFER                       
         BZ    ALTR04                                                           
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         ST    R1,TSTTRC           SET BUFFER ADDRESS                           
         MVC   00(16,R1),=CL16'*TRCBUFF*TRCBUFF'                                
         MVC   16(16,R1),=CL16'*TRCBUFF*TRCBUFF'                                
*                                                                               
         AR    R0,R1                                                            
         BCTR  R0,0                                                             
         ST    R0,40(R1)           SET END ADDRESS AND EYECATCHER               
         AHI   R0,1                                                             
         LR    R1,R0               GO PAST TRACE BUFFER                         
*                                                                               
ALTR04   XC    TSTSCT,TSTSCT       PROCESS SCRIPT TRACE TABLE                   
         ICM   R0,15,FACSCT        TEST NEED SCRIPT TRACE BUFFER                
         BZ    ALTR06                                                           
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         STCM  R1,15,TSTSCT         SET BUFFER ADDRESS                          
         MVC   00(8,R1),=CL16'*SCTBUF**SCTBUF*'                                 
         MVC   16(8,R1),=CL16'*SCTBUF**SCTBUF*'                                 
*                                                                               
         AR    R0,R1                                                            
         BCTR  R0,0                                                             
         STCM  R0,15,SCTEND-SCTTABD(R1)  SET END ADDRESS                        
         AHI   R0,1                                                             
         LR    R1,R0               GO PAST SCRIPT BUFFER                        
*                                                                               
ALTR06   BXLE  R5,R6,ALTR02                                                     
         SAM24                     GET OUT OF 31-BIT MODE                       
*                                                                               
*&&DO                                                                           
DEQPOWQ  ICM   RE,15,VPOWWOWS      DEQUEUE POWER XECBS                          
         BZ    DEQPOWQX                                                         
         GOTO1 VPOWWOW,P1,=C'GET',=C'INIT'                                      
DEQPOWQX DS    0H                                                               
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* FIX C/R BUFFER ADDR & LEN IN SSB                                    *         
***********************************************************************         
FIXCORE  OC    FACORELN,FACORELN   TEST BUFFER LENGTH PASSED                    
         BZ    FIXUP                                                            
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         MVC   SSBCRADR,HICORE                                                  
         MVC   SSBCRLEN,FACORELN                                                
         L     RF,HICORE           UPDATE HICORE ADDRESS                        
         XC    0(PSKEYL,RF),0(RF)  CLEAR START                                  
         A     RF,FACORELN                                                      
         ST    RF,HICORE                                                        
         CLI   FATOR,C'Y'          TEST TOR                                     
         BNE   *+8                                                              
         BRAS  RE,RSTCORE                                                       
         DROP  RE                                                               
*                                                                               
FIXUP    L     RF,=V(XAHOLE)                                                    
         SAM31                                                                  
         L     R1,XAACRCB          SET A(CORE BUFFER COPY AREA)                 
         A     R1,XALOCORE                                                      
         MVC   00(16,R1),=CL16'*XA CORE BUFFER*'                                
         MVC   16(16,R1),=CL16'*XA CORE BUFFER*'                                
         AHI   R1,32                                                            
         ST    R1,0(RF)                                                         
*                                                                               
         L     R1,VCRAPBLK                                                      
         USING CRAPPERD,R1                                                      
         L     R0,XAACRAP          FIX CRAPPER BLOCKS                           
         A     R0,XALOCORE                                                      
         ST    R0,CRCORE                                                        
         MVC   CRCOREL,=A(CRLENMX)                                              
         A     R0,CRCOREL                                                       
         ST    R0,CRAWRK                                                        
         MVC   CRLWRK,TSKWRK                                                    
         A     R0,CRLWRK                                                        
         ST    R0,CRATIA                                                        
         MVC   CRLTIA,TSKTIA                                                    
         A     R0,CRLTIA                                                        
         ST    R0,CRATWA                                                        
         MVC   CRLTWA,TSKTWA                                                    
         A     R0,CRLTWA                                                        
         ST    R0,CRAMAP                                                        
         MVC   CRLMAP,TSKMAP                                                    
         A     R0,CRLMAP                                                        
         ST    R0,CRAPGM                                                        
         MVC   CRLPGM,TSKPGM                                                    
         A     R0,CRLPGM                                                        
         ST    R0,CRAUTL                                                        
         L     RF,VUTL                                                          
         LH    RF,0(RF)                                                         
         ST    RF,CRLUTL                                                        
*                                                                               
         L     R1,VCHANBLK                                                      
         USING CHAIND,R1                                                        
         L     R0,XAACHAIN         FIX CHAIN BLOCKS                             
         A     R0,XALOCORE                                                      
         ST    R0,CHAWRK                                                        
         MVC   CHLWRK,TSKWRK                                                    
         A     R0,CHLWRK                                                        
         ST    R0,CHATIA                                                        
         MVC   CHLTIA,TSKTIA                                                    
         A     R0,CHLTIA                                                        
         ST    R0,CHATWA                                                        
         MVC   CHLTWA,TSKTWA                                                    
         A     R0,CHLTWA                                                        
         ST    R0,CHAMAP                                                        
         MVC   CHLMAP,TSKMAP                                                    
         A     R0,CHLMAP                                                        
         ST    R0,CHAPGM                                                        
         MVC   CHLPGM,TSKPGM                                                    
         A     R0,CHLPGM                                                        
         ST    R0,CHAUTL                                                        
         L     RF,VUTL                                                          
         LH    RF,0(RF)                                                         
         ST    RF,CHLUTL                                                        
         SAM24                                                                  
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* FIX ALL CORE ADDRESSES FOR TASK PARTITIONS                          *         
* THIS OVERWRITES THE ADDRESS OF ASTRTBUF SET ABOVE                   *         
* TCB 1 IS RESERVED FOR ALL KINDS OF I/O, SO GET TCBWRK IN TCB 2      *         
* AND SET THIS AS ASTRTBUF FOR REMAINDER OF FASTART                   *         
***********************************************************************         
         BRAS  RE,CORESET          FIX CORE ADDRESSES                           
*                                                                               
         L     R1,VTCB             SET NEW READ BUFFER IN TASK 2                
         LH    RE,0(R1)                                                         
         LA    R1,6(RE,R1)                                                      
         MVC   ASTRTBUF,TCBWRKA-TCBD(R1)                                        
         EJECT                                                                  
***********************************************************************         
* WRITE A VALID CHECKPOINT RECORD                                     *         
***********************************************************************         
WRTCKP   CLI   FATOR,C'Y'          AORS DO NOT WRITE CHECKPOINT RECORDS         
         BNE   WRTCKPX                                                          
         LA    R1,73                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         XC    P1(24),P1                                                        
         MVC   P1,VWTCKD                                                        
         L     RE,VCHKPT1                                                       
         ST    RE,P2                                                            
         L     RF,VCHKPT1X                                                      
         SR    RF,RE                                                            
         ST    RF,P3                                                            
         L     RE,VPRGMS                                                        
         ST    RE,P4                                                            
         USING DTFPHD,RE                                                        
         MVC   FULL,DNEXT                                                       
         XC    DNEXT,DNEXT         SET TRK 1/BLK 0 +OFFSET                      
         LLC   RF,FACSYSID                                                      
         LA    RF,1(RF,RF)                                                      
         STH   RF,DNEXT                                                         
         LA    RE,P6                                                            
         ST    RE,P5                                                            
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    WRTCKP1                                                          
         BRAS  RE,IOERROR                                                       
         ABEND 304,DUMP                                                         
*                                                                               
WRTCKP1  L     RE,VPRGMS                                                        
         XC    DNEXT,DNEXT         SET TRK 2/BLK 0 +OFFSET                      
         LLC   RF,FACSYSID                                                      
         LA    RF,2(RF,RF)                                                      
         STH   RF,DNEXT                                                         
*                                                                               
         ICM   RE,15,VCHKPT2       WRITE SECOND CHECK POINT                     
         BZ    WRTCKP2                                                          
         ST    RE,P2                                                            
         L     RF,VCHKPT2X                                                      
         SR    RF,RE                                                            
         ST    RF,P3                                                            
         GOTO1 VDADDS,(R1)                                                      
         OC    P3(2),P3                                                         
         BZ    WRTCKP2                                                          
         BRAS  RE,IOERROR                                                       
         ABEND 305,DUMP                                                         
*                                                                               
WRTCKP2  L     RE,VPRGMS            RESTORE DNEXT                               
         MVC   DNEXT,FULL                                                       
         MVI   DNEXT+2,0            BUMP TO NEXT TRACK AS WE                    
         LH    R1,DNEXT             HAVE DESTROYED CAPACITY COUNTER             
         LA    R1,1(R1)                                                         
         STH   R1,DNEXT                                                         
         DROP  RE                                                               
*                                                                               
         LA    R1,74                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
WRTCKPX  GOTO1 VCALLOV,DMCB,0,(C'C',0),0                                        
         GOTO1 VCALLOV,DMCB,0,(C'D',0),0 FIX ADDRESSES OF CORE PHASES           
         EJECT                                                                  
***********************************************************************         
* START FACPAK SYSTEMS                                                *         
***********************************************************************         
APSTART  L     RD,SVWRKADR         POINT RD TO TASK 1 WORK AREA                 
*                                                                               
         CLI   OPTSYS,C'I'                                                      
         BE    APST06                                                           
*                                  SE VALUES PASSED FROM BOOTVALS               
         LA    R8,SELIST           R8=A(SE OP/NO-OP LIST)                       
         MVC   REPLY,=CL8'ALL'                                                  
         MVI   FLAG,0                                                           
         CLI   SEOPNOP,C'-'                                                     
         BNE   *+12                                                             
         MVI   FLAG,255                                                         
         BRAS  RE,SETSE            NO-OP ALL SYSTEMS                            
         XI    FLAG,255                                                         
*                                                                               
APST02   CLI   0(R8),C' '          END OF OP/NO-OP LIST                         
         BE    APST04                                                           
         MVC   REPLY,0(R8)                                                      
         BRAS  RE,SETSE            OP/NO-OP SE                                  
         BZ    APST10                                                           
         LA    R8,L'SELIST(R8)                                                  
         B     APST02                                                           
*                                  ASK OPERATOR IF ANY SYS OVERRIDES            
APST04   MVI   REPLY,C'N'          DEFAULT IS NO OVERRIDES                      
         LA    R1,23                                                            
         BRAS  RE,PUTMESS                                                       
         CLI   REPLY,C'N'                                                       
         BE    APST12                                                           
         CLI   REPLY,C'Y'                                                       
         BNE   APST04                                                           
*                                  ASK OPERATOR FOR SYSTEM NO-OP'S              
APST06   MVI   FLAG,255                                                         
         MVC   REPLY,=CL8'NO'      DEFAULT IS NO NO-OP'S                        
         LA    R1,7                                                             
         BRAS  RE,PUTMESS                                                       
         CLC   REPLY,=CL8'NO'                                                   
         BNE   *+16                                                             
         CLI   OPTSYS,C'I'                                                      
         BE    APST12                                                           
         B     APST08                                                           
*                                                                               
         BRAS  RE,SETSE            SET SE TO NO-OP                              
         BZ    APST10                                                           
         CLC   REPLY(3),=CL8'ALL'                                               
         BNE   APST06              IF 'ALL' SYSTEMS NO-OPED                     
*                                  ASK OPERATOR FOR SYSTEM OP'S                 
APST08   MVI   FLAG,0                                                           
         MVC   REPLY,=CL8'NO'      DEFAULT IS NO OP'S                           
         LA    R1,8                                                             
         BRAS  RE,PUTMESS                                                       
         CLC   REPLY,=CL8'NO'                                                   
         BE    APST12                                                           
         BRAS  RE,SETSE            SET SE TO OP                                 
         BZ    APST10                                                           
         CLC   REPLY(3),=CL8'ALL'                                               
         BNE   APST08                                                           
         B     APST12                                                           
*                                  OUTPUT 'INVALID SENAME' MESSAGE              
APST10   MVC   XTRAMESS,REPLY                                                   
         LA    R1,9                                                             
         BRAS  RE,PUTMESS                                                       
         CLI   FLAG,255                                                         
         BE    APST06                                                           
         B     APST08                                                           
         EJECT                                                                  
***********************************************************************         
* DISCONNECT ALL TERMINALS CONNECTED TO NO-OP SYSTEMS                 *         
***********************************************************************         
APST12   L     R1,VSELIST                                                       
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)            R1=A(SELIST)                                 
*                                                                               
APST14   TM    SEIND-SELISTD(R1),SEINOP                                         
         BZ    APST18                                                           
*                                                                               
         SAM31                                                                  
         L     R5,VUTL                                                          
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING UTLD,R5             R5=A(UTL)                                    
APST16   CLC   TSYS,SESYS-SELISTD(R1)                                           
         BNE   *+10                                                             
         XC    TCTDATA,TCTDATA                                                  
         BXLE  R5,R6,APST16                                                     
*                                                                               
APST18   BXLE  R1,RE,APST14                                                     
         SAM24                                                                  
         B     APST20                                                           
         EJECT                                                                  
***********************************************************************         
* LOAD IN RECOVERY RESTORE PROGRAM                                    *         
***********************************************************************         
APST20   L     R3,VSSB             MOVE A SAVED TASK ENTRY                      
         USING SSBD,R3                                                          
         MVC   SSBTASKS,TSKNUM+3   SET N'TASKS FOR RECOVER/RESTORE              
         MVC   P1,SVPGMADR         MOVE A SAVED PROGRAM AREA ADDRESS            
         MVI   P2,C'R'                                                          
         MVC   P2+1(3),RCVRRSTR                                                 
         GOTO1 VCALLOV,P1                                                       
         CLI   4(R1),255                                                        
         BNE   APST21                                                           
         LA    R1,76                                                            
         BRAS  RE,PUTMESS                                                       
         ABEND 306,DUMP                                                         
*                                                                               
APST21   L     R3,VSELIST          NOW START ALL SYSTEMS                        
         LH    R4,0(R3)                                                         
         L     R5,2(R3)                                                         
         LA    R3,6(R3)                                                         
         USING SELISTD,R3                                                       
APST22   TM    SEIND,SEISTRT       TEST ALREADY UP (SE1)                        
         BO    APST40                                                           
         TM    SEIND,SEINOP        TEST NOP                                     
         BO    *+18                                                             
         OC    SEFILES,SEFILES     TEST NO FILES                                
         BZ    APST38                                                           
         B     APST24                                                           
*                                                                               
         TM    SEIND,SEIRCVP                                                    
         BZ    APST40              SYSTEM IS NOP AND NO RECOVERY REQ            
*                                                                               
         MVC   XTRAMESS,SPACES     ASK IF RECOVERY REQD FOR NO-OP SYS           
         MVC   XTRAMESS(L'SENAME),SENAME                                        
         MVC   REPLY,=CL8'NO'      DEFAULT IS NO RECOVERY                       
         LA    R1,10                                                            
         BRAS  RE,PUTMESS                                                       
         CLI   REPLY,C'Y'                                                       
         BNE   APST40                                                           
*                                                                               
APST24   LA    R1,32               SET 'ABOUT TO START SYSTEM' MESSAGE          
         MVC   XTRAMESS,SPACES                                                  
         MVC   XTRAMESS(L'SENAME),SENAME                                        
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     RE,SVTSKADR         INITIALISE DTFS FOR SYSTEM                   
         USING TCBD,RE                                                          
         TM    SEIND,SEIRONLY      TEST SYSTEM READ-ONLY                        
         BO    APST30                                                           
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    APST30                                                           
*                                                                               
         L     RF,VSYSFLES                                                      
         USING SYSFLSTD,RF                                                      
         XR    R1,R1                                                            
APST26   CLC   SESYS,0(RF)                                                      
         BNE   APST28                                                           
         TM    SYSFSTAT,SYSFQDP    TEST ENQ PROTECTED                           
         BO    APST30                                                           
         LA    RF,SYSFLIST                                                      
         ICM   RF,15,SYSFADTF-1    A(FIRST DTF)                                 
         BZ    APST40                                                           
         USING DTFPHD,RF                                                        
                                                                                
         TM    DTFFLAG,DTFGLOB     TEST GLOBAL FILE                             
         BO    APST30                                                           
         B     APST40              NO OTHERS CAN BE STARTED                     
*                                                                               
         USING SYSFLSTD,RF                                                      
APST28   LLC   R1,SYSF#FLS+1                                                    
         MHI   R1,SYSFLNQ                                                       
         LA    RF,SYSFLIST(R1)     NEXT SYSTEM                                  
         CLI   0(RF),X'FF'                                                      
         BNE   APST26                                                           
         ABEND 307,DUMP                                                         
         DROP  RF                                                               
*                                                                               
APST30   MVC   TCBDTFS(8),SEFILES  SET A(DTFS) IN SAVED TASK ENTRY              
         ST    RE,P1                                                            
         MVI   P1,C'I'                                                          
         GOTO1 VDTFIOA,P1                                                       
         XC    P1(24),P1                                                        
         MVC   P1,VOPENSYS                                                      
         MVC   P2,ASTRTBUF         SET IO AREA                                  
         MVC   P2(1),SESYS         SET SYSTEM NUMBER                            
         LA    R2,DMCB                                                          
         GOTO1 VDMOD000,P1                                                      
         OC    P3(2),P3            TEST FOR ERRORS                              
         BZ    APST34                                                           
         MVC   XTRAMESS,SPACES                                                  
         MVC   XTRAMESS(L'SENAME),SENAME                                        
         LA    R1,11                                                            
         TM    P3+1,X'80'          INVALID CPU ID                               
         BO    APST32                                                           
         LA    R1,12                                                            
         TM    P3+1,X'40'          MISSING CPU ID                               
         BO    APST32                                                           
         LA    R1,13                                                            
         TM    P3+1,X'20'          CONCURRENT FILE UPDATES                      
         BO    APST32                                                           
         LA    R1,34                                                            
         TM    P3+1,X'10'          DATASPACE MISMATCH                           
         BO    APST32                                                           
         LA    R1,77               UNKNOWN ERROR                                
*                                                                               
APST32   BRAS  RE,PUTMESS                                                       
         ABEND 308,DUMP                                                         
*                                                                               
APST34   GOTO1 SVPGMADR,P1,0,(SESYS,(RA))                                       
         NI    SEIND,255-SEIRCVP   TURN OFF RECOVERY PENDING FLAG               
         TM    SEIND,SEINOP                                                     
         BZ    APST36                                                           
*                                                                               
         MVC   P1,VCLSESYS         CLOSE SYSTEM IF NOP                          
         MVC   P2,ASTRTBUF                                                      
         MVC   P2(1),SESYS                                                      
         LA    R2,DMCB                                                          
         GOTO1 VDMOD000,P1                                                      
*                                                                               
         LA    R1,33               SET 'SYSTEM NOT STARTED' MESSAGE             
         MVC   XTRAMESS,SPACES                                                  
         MVC   XTRAMESS(L'SENAME),SENAME                                        
         BRAS  RE,PUTMESS                                                       
         B     APST40                                                           
*                                                                               
APST36   BRAS  RE,RBLDRL           GO BUILD REQ ADDR LIST ALWAYS                
*                                                                               
APST38   LA    R1,14               SEND SYSTEM STARTED MESSAGE                  
         TM    SEIND,SEIRONLY                                                   
         BZ    *+8                                                              
         LA    R1,31               SET SYSTEM STARTED READ-ONLY MESSAGE         
         TM    SEIND,SEISETRO                                                   
         BZ    *+8                                                              
         LA    R1,96               SET SYSTEM FORCED READ-ONLY MESSAGE          
         MVC   XTRAMESS,SPACES                                                  
         MVC   XTRAMESS(L'SENAME),SENAME                                        
         BRAS  RE,PUTMESS                                                       
         OI    SEIND,SEISTRT       SET SYSTEM STARTED                           
APST40   BXLE  R3,R4,APST22                                                     
         DROP  R3                                                               
*                                                                               
APST42   L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         TM    SSBSTAT1,SSBSCHK2   TEST REQ ADDR LIST REBUILT                   
         BZ    APST46                                                           
         NI    SSBSTAT1,255-SSBSCHK2                                            
         DROP  RE                                                               
*                                                                               
APST44   CLI   FATOR,C'Y'          TEST TOR                                     
         BNE   APST46                                                           
*                                                                               
         XC    P1(24),P1           WRITE SECOND CHECK POINT RECORD              
         MVC   P1,VWTID                                                         
         ICM   RE,15,VCHKPT2                                                    
         BZ    APST46                                                           
         ST    RE,P2                                                            
         L     RF,VCHKPT2X                                                      
         SR    RF,RE                                                            
         ST    RF,P3                                                            
         MVC   P4,VPRGMS                                                        
         XC    P6,P6               SET TRK 2/BLK 0 +OFFSET                      
         LLC   RE,FACSYSID                                                      
         LA    RE,2(RE,RE)                                                      
         STH   RE,P6                                                            
         MVC   P6+2(2),=X'0100'                                                 
         LA    RE,P6                                                            
         ST    RE,P5                                                            
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    APST46                                                           
         BRAS  RE,IOERROR                                                       
         ABEND 309,DUMP                                                         
*                                                                               
APST46   L     RE,SVTSKADR         SET TASK#1 FOR SE#1 TO READ CTFILE           
         L     RF,VSELIST                                                       
         LA    RF,6(RF)                                                         
         MVC   TCBDTFS-TCBD(8,RE),SEFILES-SELISTD(RF)                           
         ST    RE,P1                                                            
         MVI   P1,C'I'                                                          
         GOTO1 VDTFIOA,P1          INITIALIZE DTFS                              
*                                                                               
         L     RE,VSSB                                                          
         MVI   SSBCTLST-SSBD(RE),C'U'  SET PROFILES FROM DSPACE                 
*                                                                               
APST48   BRAS  RE,REPROVE          READ CTFILE PROVER RECORDS                   
         BRAS  RE,VTAMST           OPEN VTAM ACB AND INIT PRINTERS              
         B     RST                                                              
         EJECT                                                                  
***********************************************************************         
* BUILD REST OF SSB INITIALISE PRINT QUEUES AND EXIT BACK TO LOAD     *         
***********************************************************************         
RST      L     R2,VDMPFILE         SET RECSTRK/TRKSCYL FOR DMPFILE              
         USING DTFPHD,R2                                                        
         LHI   R0,DMPBLKQ                                                       
         GOTO1 VDADDS,P1,VDARPT,0,(R0),(R2)                                     
         XR    R0,R0                                                            
         ICM   R0,3,P3+2           R0=RECS/TRK                                  
         BNZ   RST02                                                            
         LA    R1,77                                                            
         BRAS  RE,PUTMESS                                                       
         ABEND 310,DUMP            DIE IF DEVICE NOT OK FOR DMPFILE             
*                                                                               
RST02    L     R1,P2                                                            
         LH    R1,2(R1)            R1=TRKS/CYL                                  
         ST    R0,DMPTRKS          R0=RECS/TRK                                  
         ST    R1,DMPCYLS                                                       
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         NI    SSBSTAT1,255-SSBSRSRT                                            
         CLI   STRTMODE,C'R'                                                    
         BNE   *+8                                                              
         OI    SSBSTAT1,SSBSRSRT   SET THIS IS A RESTART                        
         OI    SSBSTAT1,SSBUII     SET VTAM NOT READY FOR USER INPUT            
         NI    SSBSTAT2,255-SSBSTRAC                                            
         CLI   FACORES,C'Y'                                                     
         BE    *+8                                                              
         OI    SSBSTAT2,SSBSNCRP   SET CORES NOT ACTIVE IN SSB                  
*NOP*    CLI   FACAUTOQ,C'Y'                                                    
*NOP*    BNE   *+8                                                              
*NOP*    OI    SSBSTAT2,SSBSAPQM   SET AUTOPQ ACTIVE IN SSB                     
         MVC   SSBTASKS,TSKNUM+3   SET NUMBER OF ACTIVE TASKS                   
         MVC   SSBPHMAX,PHLMAX     SET MAXIMUM PHASE LENGTH                     
         MVC   SSBMAXIO,FACMAXIO   SET MAXIMUM TRANSACTION I/O COUNT            
         TM    SSBSYSFL,X'80'                                                   
         BZ    *+12                                                             
         OI    SSBSTAT3,SSBDUPDP   SET TEST SYS WANTS DUPLICATE DUMPS           
         NI    SSBSTAT1,255-SSBUII SET TEST SYS READY FOR USER INPUT            
         MVC   SSBPGMUP,FACPGMUP                                                
         MVC   SSBPOPLN,FACPOPLN   SET SYSTEM TIMER VALUES                      
         MVC   SSBPOPMX,FACPOPMX                                                
         MVC   SSBPRIO,FACPRIO     SET SYSTEM PRIORITY VALUES                   
         MVC   SSBPRCPU,FACPRCPU                                                
         MVC   SSBPRMIN,FACPRMIN                                                
         MVC   SSBJESIO,FACJESIO   SET JES SUBMIT FACILITY NAME                 
         MVC   SSBMQIO,FACMQIO                                                  
         MVC   SSBSMTP,FACSMTP                                                  
         DROP  RE                                                               
*                                                                               
         XC    DMCB,DMCB           DUMMY CALL TO INITIALISE TEMPEST             
         GOTO1 VDATAMGR,DMCB,DMREAD,TEMPEST,X'0000FFFF',TEMP                    
*                                                                               
*&&DO*&& BRAS  RE,BLDDDS           ALL THIS STUFF FROM SRTIM                    
*&&US*&& BRAS  RE,BLDNTFY          BUILD NOTIFY TABLE                           
         BRAS  RE,BLDDICTS                                                      
         BRAS  RE,GETFLD                                                        
         BRAS  RE,LOCKET                                                        
         EJECT                                                                  
***********************************************************************         
* INITIALIZE ADDRESSES FOR DUMP REGIONS                               *         
* SSBXA1 IS 24-BIT STORAGE                                            *         
* SSBXA2 IS 31-BIT STORAGE FOR IO AND TSAR BUFFERS                    *         
* SSBXA3 IS NOT YET DEFINED                                           *         
***********************************************************************         
AP62     L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         L     R1,HICORE           SET HIGH CORE MODULO 2K                      
         LA    R1,2047(R1)                                                      
         SRL   R1,11                                                            
         SLL   R1,11                                                            
         BCTR  R1,0                                                             
         ST    R1,HICORE                                                        
         ST    R1,SSBHIADR         SET ACTUAL HIGH CORE ADDR                    
         L     R0,VBOOT                                                         
         ST    R0,SSBLOADR         SET ACTUAL LOW CORE ADDR                     
         LA    R1,1(R1)            ADD BACK FOR ARITHMETIC                      
         SR    R1,R0                                                            
         SRL   R1,11               R1=NUM OF 2K BLOCKS IN REGION                
         STH   R1,SSBDMP2K                                                      
         LA    R1,3(R1)            ROUND                                        
         SRL   R1,2                DIV BY 4 TO GET 8K BLOCKS                    
         SR    R0,R0                                                            
         D     R0,DMPTRKS          DIVIDE BY RECS PER TRACK                     
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
         LA    R1,1(R1)            R1=NUM OF TRACKS XA1 DUMP                    
         STH   R1,SSBDXA1                                                       
***********************************************************************         
* INITIALIZE FOR 31-BIT XA2 REGION                                    *         
***********************************************************************         
         L     R0,SSBXALO                                                       
         L     R1,XAHICORE                                                      
         AHI   R1,2047             NB - DO NOT DO LA                            
         SRL   R1,11                                                            
         SLL   R1,11                                                            
         BCTR  R1,0                                                             
         ST    R1,SSBXAHI                                                       
*                                                                               
         AHI   R1,1                ADD BACK FOR ARITHMETIC                      
         SR    R1,R0                                                            
         SRL   R1,11                                                            
         C     R1,=X'00007FFF'                                                  
         BL    *+8                                                              
         ICM   R1,15,=X'00007FFF'                                               
         STH   R1,SSBDXA2K         # OF 2K BLOCKS IN XA                         
*                                                                               
         XR    R0,R0                                                            
         LHI   R1,DMPCYLQ          NUMBER OF CYLIDERS PER DUMP                  
         M     R0,DMPCYLS          *(TRACKS PER CYLINDER)                       
         L     R0,DMPTRKS          *(RECS PER TRACK)                            
         MR    R0,R0                                                            
         SLL   R1,2                *(2K BLOCKS)   (8K/2K)=4                     
*                                                                               
         SH    R1,SSBDMP2K         R1=TOTAL 2K BLOCKS PER DUMP                  
         CH    R1,SSBDXA2K         WILL XA FIT ON REMAINING DISK?               
         BH    *+8                 YES                                          
         STH   R1,SSBDXA2K         NO, SO USE SMALLER SIZE                      
*                                                                               
         AHI   R1,3                ROUND                                        
         SRL   R1,2                DIVIDE BY 4 FOR NUMBER 8K RECS               
         SR    R0,R0                                                            
         D     R0,DMPTRKS          DIVIDE BY RECS PER TRACK                     
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
         LA    R1,1(R1)                                                         
         STH   R1,SSBDXA2          GIVES TRACKS FOR XA STORAGE DUMP             
*                                                                               
AP63     LH    R0,SSBDXA1                                                       
         AH    R0,SSBDXA2                                                       
         AH    R0,SSBDXA3                                                       
         STH   R0,SSBDXASZ         GET TOTAL TRACKS REQUIRED                    
*                                                                               
         SRDL  R0,32                                                            
         D     R0,DMPCYLS          DIVIDE BY TRACKS PER CYLINDER                
         LTR   R0,R0                                                            
         BZ    *+8                                                              
         LA    R1,1(R1)                                                         
*                                                                               
         LHI   R1,DMPCYLQ          24 CYLS UK, 36 FOR US                        
         STC   R1,SSBDMPCY         R1=NUM OF CYLS FOR PARTITION DUMP            
*                                                                               
         SAM31                                                                  
         LA    RF,DMTX                                                          
         TM    DIND,DINDXAM        TEST HIGH CORE EXTENT MATRIX                 
         BZ    *+8                                                              
         ICM   RF,15,DMTX                                                       
         USING EXTENTD,RF                                                       
         MVC   SSBDMPSC,EXTCYLLO   SET START CYLINDER IN DMPFILE                
         LH    R3,EXTCYLHI                                                      
         SH    R3,EXTCYLLO                                                      
         LA    R3,1(R3)            R3=TOTAL NUM OF CYLINDERS                    
         DROP  RF                                                               
                                                                                
         SAM24                                                                  
         SR    R2,R2                                                            
         DR    R2,R1                                                            
         STC   R3,SSBDMPMX                                                      
         CLI   SSBDMPMX,31         MAX DUMPS SUPPORTED PER SYSTEM               
         BNH   *+8                                                              
         MVI   SSBDMPMX,31                                                      
         EJECT                                                                  
***********************************************************************         
* RELEASE UNUSED STORAGE                                              *         
***********************************************************************         
         CLI   FAPROT,C'Y'         TEST FACPAK STORAGE PROTECTION ON            
         BNE   AP64                                                             
         L     R1,HICORE           RETURN UNUSED CORE                           
         LA    R1,1(R1)                                                         
         ST    R1,HICORE                                                        
         L     R1,HICOREX                                                       
         S     R1,HICORE                                                        
         BNP   AP65                                                             
         ST    R1,HICORELN                                                      
         LR    R3,R1                                                            
*                                                                               
         STORAGE RELEASE,ADDR=HICORE,LENGTH=(R3),SP=132,COND=YES,KEY=8          
         B     AP65                                                             
*                                                                               
AP64     L     R3,HICOREX          RETURN UNUSED CORE                           
         S     R3,HICORE                                                        
         BNP   AP65                                                             
         L     R2,HICORE           HICORE POINTS TO A(2K - 1) BLOCK             
         AHI   R2,1                (SEE AP62)                                   
         ST    R2,HICORE                                                        
         ST    R3,HICORELN                                                      
         LA    R2,HICORE                                                        
         FREEMAIN VC,A=(R2),SP=132                                              
         DROP  R2                                                               
*                                                                               
* IT IS WORTH NOTING THE FREEMAIN MACRO REQUIRES A(2F) WHERE THE 1ST F          
* IS A(AREA TO BE RELEASED) AND THE 2ND IS L'AREA. THERE ARE REGISTER           
* CONSTRAINTS ALSO.                                                             
*                                                                               
AP65     BRAS  RE,DMPSET           SET HIGHEST DUMP NUMBER FOR FACPAK           
*                                                                               
AP70     GOTO1 VPQOPEN,P1          INITIALIZE PRTQUE AND QUEUES                 
         BE    AP72                                                             
         LA    R1,27               SEND ERROR ON PRTQUE MESSAGE                 
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
*                                                                               
AP72     LA    R1,28               SEND PRTQUE INITIALISED MESSAGE              
         BRAS  RE,PUTMESS                                                       
*                                                                               
AP74     LA    R1,18               SEND SYSTEM INIT COMPLETE MESSAGE            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         MVI   SSBMTIND,C'M'       ENABLE MULTI-TASKING WAITS                   
         DROP  RE                                                               
         J     STRTEXIT                                                         
         EJECT                                                                  
***********************************************************************         
* SET SELIST ENTRY(S) TO OP OR NO-OP.                                 *         
* REPLY CONTAINS SENAME OR C'ALL'                                     *         
* FLAG=X'00' TO OP OR FLAG=X'FF' TO NOP                               *         
* SET CC=EQ IF INVALID SENAME                                         *         
***********************************************************************         
SETSE    L     R5,VSELIST                                                       
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING SELISTD,R5          R5=A(SELIST)                                 
         XR    RF,RF               RF=NUMBER OF SELIST CHANGES                  
*                                                                               
SETSE2   CLC   REPLY(3),=C'ALL'                                                 
         JE    *+14                                                             
         CLC   SENAME,REPLY                                                     
         JNE   SETSE4                                                           
*                                                                               
         TM    SEIND,SEISTRT       IGNORE OPEN SE'S (SYS1)                      
         JO    SETSE4                                                           
         LA    RF,1(RF)            BUMP CHANGE COUNT                            
         NI    SEIND,255-SEINOP    SET SE TO OP                                 
         CLI   FLAG,255                                                         
         JNE   *+8                                                              
         OI    SEIND,SEINOP        SET SE TO NO-OP                              
*                                                                               
SETSE4   BRXLE R5,R6,SETSE2                                                     
         LTR   RF,RF               SET CONDITION CODE                           
         BR    RE                                                               
         DROP  R5                                                               
                                                                                
         TITLE 'FASTART - SELF ADDRESSING ROUTINES'                             
***********************************************************************         
* VERIFY SE ENTRIES ARE SET PROPERLY                                            
***********************************************************************         
VERSE    NTR1  BASE=*,LABEL=*                                                   
         L     R5,VSELIST                                                       
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING SELISTD,R5          R5=A(SELIST)                                 
*                                                                               
VERSE2   ICM   RF,15,SERCVDTF      RECOVERY FILE DTF                            
         BZ    VERSE4              RECOVERY DTF NOT IN SELIST                   
         USING DTFPHD,RF                                                        
         TM    DIND,DINDVRB        VERMONT RECOVERY                             
         BO    VERSE5                                                           
         DROP  RF                                                               
         LA    R1,109                                                           
         L     RF,=A(MES109)                                                    
         MVC   42(L'SENAME,RF),SENAME                                           
         BRAS  RE,PUTMESS                                                       
         ABEND 354,DUMP            SE RECOVERY NOT VERMONT OR GLOBAL            
*                                                                               
VERSE4   LA    R1,110              RECOVERY DTF NOT IN SELIST                   
         L     RF,=A(MES110)                                                    
         MVC   33(L'SENAME,RF),SENAME                                           
         BRAS  RE,PUTMESS                                                       
*                                                                               
VERSE5   BRXLE R5,R6,VERSE2                                                     
         J     EXIT                                                             
         DROP  R5                                                               
                                                                                
***********************************************************************         
* ROUTINE TO GET CONTIGUOUS XA AREA FOR LATER                         *         
***********************************************************************         
GETXA    NTR1  BASE=*,LABEL=*                                                   
         XC    FULL,FULL                                                        
*                                                                               
         L     R0,=A(CRLENMX)                                                   
         A     R0,TSKWRK           W/S                                          
         A     R0,TSKTIA           TIA                                          
         A     R0,TSKTWA           TWA                                          
         A     R0,TSKMAP           MAP                                          
         A     R0,TSKPGM           PGMS                                         
         L     RF,VUTL                                                          
         AH    R0,0(RF)            UTL                                          
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
         ST    R0,XALCRAP                                                       
         MVC   XAACRAP,FULL                                                     
         A     R0,FULL                                                          
         ST    R0,FULL                                                          
*                                                                               
         L     R0,TSKWRK           W/S                                          
         A     R0,TSKTIA           TIA                                          
         A     R0,TSKTWA           TWA                                          
         A     R0,TSKMAP           MAP                                          
         A     R0,TSKPGM           PGMS                                         
         L     RF,VUTL                                                          
         AH    R0,0(RF)            UTL                                          
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
         ST    R0,XALCHAIN                                                      
         MVC   XAACHAIN,FULL                                                    
         A     R0,FULL                                                          
         ST    R0,FULL                                                          
*                                                                               
         L     RE,VSYSFLES         CALCULATE MAX I/O SIZE FOR SYSTEMS           
         USING SYSFLSTD,RE                                                      
GTXA02   LLH   R0,SYSF#FLS         R3=NUM OF FILES IN SYSTEM                    
         LA    R2,8                R2=I/O AREA COUNTER                          
         LA    RE,SYSFLIST         RE=A(SYSFLES ENTRY)                          
*                                                                               
GTXA04   LA    R2,48(R2)           48 BYTES PER FILE                            
         L     RF,SYSFADTF-1       RF=A(DTF)                                    
*                                                                               
         USING DTFPHD,RF                                                        
         TM    SYSFIND1,SFISF      TEST IS FILE                                 
         BO    GTXA06                                                           
         TM    SYSFIND1,SFRCV      DONT ADD FOR RECOVERY                        
         BO    GTXA08                                                           
         TM    DBLKSZ,X'80'                                                     
         BO    GTXA08                                                           
*                                                                               
         AH    R2,DBLKSZ           DA FILE BLOCK SIZE                           
         TM    DTFTYPE,DTFTKCL+DTFTKCR  KEY COMPRESSED?                         
         BZ    *+8                                                              
         AHI   R2,64               ADD SPACE FOR SAVE AREA                      
         TM    DIND,DINDNDX        D/A INDEXED                                  
         BZ    *+8                                                              
         AHI   R2,4                ADD SPACE FOR SAVE AREA                      
*                                                                               
         TM    DIND,DIND2BU        TEST IF TWO BUFFERS ASSIGNED                 
         BZ    GTXA08                                                           
         AH    R2,DBLKSZ           YES-DO SAME AGAIN                            
         TM    DTFTYPE,DTFTKCL+DTFTKCR  KEY COMPRESSED?                         
         BZ    *+8                                                              
         AHI   R2,64               ADD SPACE FOR SAVE AREA                      
         TM    DIND,DINDNDX        D/A INDEXED                                  
         BZ    *+8                                                              
         AHI   R2,4                ADD SPACE FOR SAVE AREA                      
         B     GTXA08                                                           
*                                                                               
         USING ISDTF,RF                                                         
GTXA06   LH    R1,ISKEYLN                                                       
         MHI   R1,3                                                             
         AR    R2,R1               IS FILE KEYLEN*3                             
         LH    R1,ISPDLN                                                        
         TM    ISCMPRSW,X'40'      TEST IF I/S FILE NEEDS TWO PD BUFFS          
         BO    *+8                                                              
         AH    R1,ISPDLN                                                        
         AR    R2,R1               IS FILE PD BLOCK (ONE OR TWO)                
         TM    ISFTYPE,ISFTBIGF                                                 
         BZ    *+8                                                              
         AH    R2,ISPDLN           ADD EXTRA FOR TI IF PDIXOV FILE              
         DROP  RF                                                               
*                                                                               
GTXA08   AHI   RE,SYSFLNQ          NEXT FILE                                    
         BCT   R0,GTXA04                                                        
         DROP  RE                                                               
*                                                                               
         C     R2,LNIOA            IS TOTAL FOR THIS SYS THE LARGEST            
         BNH   *+8                                                              
         ST    R2,LNIOA            YES-SET IN LNIOA                             
*                                                                               
         CLI   0(RE),255           TEST LAST SYSTEM                             
         BNE   GTXA02                                                           
         L     R2,LNIOA            MOD MAX SIZE TO DOUBLE WORDS                 
         LA    R2,7(R2)                                                         
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         ST    R2,LNIOA                                                         
*                                  TASK BASED STORAGE                           
         L     R1,LNIOA            IO AREA                                      
         AHI   R1,4095+32          ADD ROOM FOR EYECATCHER + ROUND              
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         LR    R0,R1                                                            
*                                                                               
         CLI   FAPROT,C'Y'         TEST STORAGE PROTECTION REQUIRED             
         BNE   GTXA10                                                           
*                                                                               
         L     R1,PIOLEN           PIO AREA                                     
         AHI   R1,32               ADD ROOM FOR EYECATCHER                      
         AHI   R1,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*                                                                               
GTXA10   XR    R1,R1                                                            
         ICM   R1,7,FACTSAR+1      GET SIZE OF TSAR BUFFER                      
         BZ    GTXA12              NO TSAR BUFFER                               
         AHI   R1,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         TM    FACTSAR,X'80'       TEST TWO BUFFERS/TASK                        
         BZ    *+8                                                              
         SLL   R1,1                                                             
         AR    R0,R1                                                            
*                                                                               
GTXA12   XR    R1,R1                                                            
         ICM   R1,3,FACSCRXA       ALLOCATE SCRIPT BUFFER?                      
         BZ    GTXA14              NO                                           
         SLL   R1,10               X 1024                                       
         AHI   R1,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         AR    R0,R1                                                            
*                                                                               
GTXA14   LHI   R1,TWAMAX           ALLOCATE FALINK BUFFER                       
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*                                                                               
         ICM   R1,15,MINAREA       MINIO BUFFER 1                               
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         AR    R0,R1                                                            
*                                                                               
         ICM   R1,15,MINAREA       MINIO BUFFER 2                               
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         AR    R0,R1                                                            
*                                                                               
         L     R1,=A(ZIPBLENM)     ALLOCATE ZIP BUFFER                          
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*                                                                               
         L     R1,=A(TCBMQBFQ)     MQ BUFFER LENGTH                             
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*                                                                               
         L     R1,=A(RCVBKSZQ)     MAX RECOVERY 13682                           
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*&&US                                                                           
         L     R1,=A(WLMLNQ)       WORKLOAD MANANGER AREA                       
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*&&                                                                             
         XR    R1,R1                                                            
         ICM   R1,3,FACXALEN       TEST IF NEED XA W/S BUFFER                   
         BZ    GTXA16                                                           
*                                                                               
         CHI   R1,8096             IF LESS THAN 8K THEN IT IS IN K              
         BH    *+14                                                             
         MHI   R1,1024                                                          
         AR    R0,R1                                                            
         B     GTXA16                                                           
*                                                                               
         AHI   R1,4095+32                                                       
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         AR    R0,R1                                                            
*                                                                               
GTXA16   SRDL  R0,32                                                            
         L     RF,TSKNUM                                                        
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    *+8                                                              
         AHI   RF,1                EXTRA TSK REQUIRED IN AOR FOR SYSTEM         
         MR    R0,RF                                                            
*                                                                               
         MVC   XAATSK,FULL         SAVE DISPLACEMENT TO TASK STORAGE            
         ST    R1,XALTSK           SAVE LENGTH OF TASK STORAGE                  
         A     R1,FULL                                                          
         ST    R1,FULL             UPDATE XA STORAGE LENGTH                     
*                                                                               
         LAM   AR0,ARF,ARZERO      *** PHASE LIST ENTRIES                       
         L     RE,VSSB                                                          
         LAM   AR2,AR2,SSBPGMTA-SSBD(RE)                                        
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,PGMSPGM-FAPGMSD(R2)                                        
         ICM   R0,15,PGNCORE-PGAREAD(R2)                                        
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
*                                                                               
         AHI   R0,128              EXTRA FOR THE DAY                            
         MHI   R0,PROGSPCL         * LENGTH OF ONE ENTRY                        
         AHI   R0,256              + OVERHEAD FOR PARMS/ARREDIT                 
         AHI   R0,4095             ROUND UP TO A PAGE                           
         SRL   R0,12                                                            
         SLL   R0,12                                                            
*                                                                               
         MVC   XAAPLIST,FULL                                                    
         ST    R0,XALPLIST                                                      
         A     R0,FULL             UPDATE LENGTH                                
         ST    R0,FULL                                                          
*                                                                               
         L     R1,DDICTLEN         DATA DICTIONARY BUFFER                       
         C     R1,DDMINSZ                                                       
         BH    *+8                                                              
         L     R1,DDMINSZ                                                       
         MVC   XAADDIC,FULL                                                     
         ST    R1,XALDDIC                                                       
         A     R1,FULL                                                          
         ST    R1,FULL             UPDATE XA STORAGE LENGTH                     
*                                                                               
         LA    R1,XAISGSIZ                                                      
         MHI   R1,1024                                                          
         AHI   R1,4096                                                          
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
         ST    R1,XALISG                                                        
         MVC   XAAISG,FULL                                                      
         A     R1,FULL                                                          
         ST    R1,FULL             UPDATE XA STORAGE LENGTH                     
*                                                                               
         LHI   RF,GFMAX            FIELD RECORDS IN XA                          
         MHI   RF,HXFDRL           LENGTH OF POINTER ARRAY                      
         LHI   R0,GFMAX                                                         
         MHI   R0,HIFDRL           LENGTH OF 1 ELEMENT IN ARRAY                 
         AHI   R0,4                HEADER DETAIL                                
         AR    R0,RF                                                            
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         MVC   XAAGFRC,FULL                                                     
         ST    R0,XALGFRC                                                       
         A     R0,FULL             UPDATE LENGTH                                
         ST    R0,FULL                                                          
*                                                                               
         ICM   RF,15,VTSTTAB       TRACE TABLE ENTRIES                          
         ICM   R0,15,2(RF)                                                      
         AHI   R0,1                R0=A(END)                                    
         LA    R1,6(RF)            R1=A(START)                                  
         SR    R0,R1                                                            
         SRDL  R0,32               R0/1=TOTAL LENGTH OF BLOCK                   
*                                                                               
         LH    RE,0(RF)            RE=LENGTH OF SINGLE ENTRY                    
         DR    R0,RE                                                            
*                                                                               
         ICM   RE,15,FACTRACE      TEST NEED TRACE BUFFER                       
         BZ    GTXA18                                                           
         AHI   RE,4095                                                          
         SRL   RE,12                                                            
         SLL   RE,12               ROUND TO 4K MULTIPLE                         
*                                                                               
GTXA18   ICM   RF,15,FACSCT        TEST NEED SCRIPT TRACE BUFFER                
         BZ    GTXA20                                                           
         AHI   RF,4095                                                          
         SRL   RF,12                                                            
         SLL   RF,12               ROUND TO 4K MULTIPLE                         
*                                                                               
GTXA20   AR    RE,RF                                                            
         SRDL  RE,32                                                            
         MR    RE,R1                                                            
         AHI   RF,32                                                            
         AHI   RF,4095                                                          
         SRL   RF,12                                                            
         SLL   RF,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         MVC   XAATSTT,FULL                                                     
         ST    RF,XALTSTT                                                       
         A     RF,FULL             UPDATE LENGTH                                
         ST    RF,FULL                                                          
*                                                                               
         ICM   R0,15,FACORELN      XA CORE BUFFER COPY AREA                     
         BZ    GTXA22                                                           
         AHI   R0,4095+32          32 BYTES FOR HEADER                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
*                                                                               
         MVC   XAACRCB,FULL                                                     
         ST    RF,XALCRCB                                                       
         A     R0,FULL             UPDATE LENGTH                                
         ST    R0,FULL                                                          
*                                                                               
GTXA22   CLI   FATOR,C'Y'          XA UTL AREAS                                 
         BE    *+16                                                             
         L     RE,TSKNUM           AORS HAVE 1 UTL PER TASK ONLY                
         STH   RE,VTAMUTL                                                       
         B     GTXA24                                                           
*                                                                               
         XC    DMCB,DMCB           DUMMY CALL TO INITIALISE TEMPSTR             
         GOTO1 VDATAMGR,DMCB,DMREAD,TEMPSTR,X'0000FFFF',TEMP                    
         OC    TEMP+6(2),TEMP+6                                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         CLC   VTAMUTL,TEMP+6                                                   
         BL    *+10                                                             
         MVC   VTAMUTL,TEMP+6      SET NUMBER OF UTL ENTRIES WE SUPPORT         
*                                                                               
GTXA24   CLI   FATOR,C'Y'          AORS DO NOT HAVE XA UTLS                     
         BNE   GTXA26                                                           
         CLI   FACXAUTL,C'Y'       TEST UTL IN XA                               
         BNE   GTXA26                                                           
         LHI   R0,TUTLXALN         SIZE OF UTL ENTRY                            
         MH    R0,VTAMUTL                                                       
         AHI   R0,6+128            FOR UTL BXLE HEADERS + IDENTIFIERS           
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         MVC   XAAPUTL,FULL                                                     
         ST    R0,XALPUTL                                                       
         A     R0,FULL                                                          
         ST    R0,FULL                                                          
*                                                                               
GTXA26   L     R0,=A(XAUTLLN)                                                   
         MH    R0,VTAMUTL                                                       
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         MVC   XAAUTL,FULL                                                      
         ST    R0,XALUTL                                                        
         A     R0,FULL                                                          
         ST    R0,FULL                                                          
*                                                                               
         XR    R0,R0               XA TBUFF STORAGE                             
         ICM   R0,3,VTAMBUF                                                     
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    *+8                 NO-AORS DIFFERENT ENTRY COUNT                
         ICM   R0,15,TSKNUM                                                     
         STCM  R0,3,VTAMBUF                                                     
*                                                                               
         L     R1,=A(4+32+TBUFFXL) SIZE OF EACH BUFFER+ADCON                    
         MR    R0,R0               R1=TOTAL LENGTH REQUIRED                     
         AHI   R1,128              PLUS EXTRA FOR HEADERS                       
         AHI   R1,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         MVC   XAATBUFF,FULL                                                    
         ST    R1,XALTBUFF                                                      
         A     R1,FULL             UPDATE LENGTH                                
         ST    R1,FULL                                                          
*                                                                               
         CLI   FATOR,C'Y'          AORS HAVE NO DUMMY UTLS                      
         BNE   GTXA28                                                           
*                                                                               
         XR    R0,R0                                                            
         ICM   R0,3,FACDUTL                                                     
         C     R0,TSKNUM           MINIMUM IS NUMBER OF TASKS                   
         BNL   *+8                                                              
         L     R0,TSKNUM           DUMMY UTL TBUFF AREAS                        
         L     R1,=A(32+TBUFFL)    SIZE OF EACH BUFFER                          
         MR    R0,R0               R1=TOTAL LENGTH REQUIRED                     
         AHI   R1,32               PLUS EXTRA FOR HEADERS                       
         AHI   R1,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         MVC   XAADBUFF,FULL                                                    
         ST    R1,XALDBUFF                                                      
         A     R1,FULL             UPDATE LENGTH                                
         ST    R1,FULL                                                          
*                                                                               
GTXA28   MVC   TEMP(36),=C'xxAbout to getmain for      Mb of XA '               
         MVC   TEMP(2),=H'35'                                                   
         L     R1,FULL                                                          
         SRL   R1,20                                                            
         EDIT  (R1),(5,TEMP+23),FILL=0                                          
         LA    R4,TEMP                                                          
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
*                                                                               
         L     R0,FULL                                                          
         GETMAIN RU,LV=(0),LOC=(ANY,ANY),BNDRY=PAGE,SP=132                      
         LTR   RF,RF                                                            
         BZ    GTXA30                                                           
         LA    R1,75                                                            
         BRAS  RE,PUTMESS                                                       
         ABEND 302,DUMP                                                         
*                                                                               
GTXA30   L     RE,VSSB                                                          
         ST    R1,SSBXALO-SSBD(RE)                                              
         ST    R1,XALOCORE                                                      
         AR    R1,R0                                                            
         ST    R1,XAHICORE                                                      
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* GET XA AREAS IN KEY9 FOR APPLICATION USE                            *         
***********************************************************************         
GETXA9   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,VTCB                                                          
         LH    R6,0(R3)                                                         
         L     R7,2(R3)                                                         
         LA    R3,6(R3)            R3=A(TCB LIST ENTRY FOR TASK)                
         USING TCBD,R3                                                          
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    *+6                                                              
         AR    R7,R6               EXTRA TSK REQUIRED IN AOR FOR SYSTEM         
*                                                                               
         SR    R1,R1               TEST IF ANY XA9 STORAGE                      
         ICM   R1,1,FACXA9MB                                                    
         JZ    EXIT                                                             
         L     RF,VSSB                                                          
         STC   R1,SSBXA9MB-SSBD(RF) SAVE LEN IN MB                              
*                                                                               
         MVC   TEMP(43),=C'XXAbout to getmain   Mb of key9 XA Per task'         
         MVC   TEMP(2),=H'41'                                                   
         LLC   R1,FACXA9MB                                                      
         EDIT  (R1),(2,TEMP+19),FILL=0                                          
         LA    R4,TEMP                                                          
         WTO   TEXT=(R4),MCSFLAG=HRDCPY                                         
*                                                                               
         SAM31                     GET INTO 31-BIT MODE                         
GETXA910 LLC   R2,FACXA9MB                                                      
         SLL   R2,20               SET R2 TO MB                                 
         AHI   R2,4096                                                          
         STORAGE OBTAIN,LENGTH=(R2),BNDRY=PAGE,SP=132,KEY=9,           X        
               LOC=(ANY,ANY)                                                    
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
         ST    R1,TCBAXA9                                                       
*                                                                               
         BRXLE R3,R6,GETXA910      ROUND AND ROUND WE GO                        
         SAM24                     GET BACK TO 24-BIT MODE                      
*                                                                               
         L     R3,VTCB                                                          
         LA    R3,6(R3)            R3=A(TCB LIST ENTRY FOR TASK)                
         USING TCBD,R3                                                          
*                                                                               
         SAM31                     GET INTO 31-BIT MODE                         
GETXA950 L     R2,TCBAXA9                                                       
         LLC   R1,FACXA9MB                                                      
         SLL   R1,20                                                            
         AR    R2,R1                                                            
         STORAGE RELEASE,ADDR=(R2),LENGTH=4096,SP=132,COND=YES,KEY=9            
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         BRXLE R3,R6,GETXA950      ROUND AND ROUND WE GO                        
         J     EXIT                                                             
*                                                                               
         SAM24                     GET BACK TO 24-BIT MODE                      
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CLEAR TEMPEST COUNTER FOR THIS FACPAK                    *         
***********************************************************************         
SETTMSA  NTR1  BASE=*,LABEL=*                                                   
         LAM   AR0,ARF,ARZERO                                                   
         LA    R1,53                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     R3,VSSB             R3=A(SSB)                                    
         USING SSBD,R3                                                          
         XR    R0,R0                                                            
         ICM   R0,1,SSBSYSID       MUST HAVE SYSTEM ID                          
         N     R0,=X'0000000F'                                                  
         BNZ   SETTM02                                                          
         ABEND 311,DUMP            MUST HAVE RANGE 1-15                         
*                                                                               
SETTM02  LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,SSBTBLET                                                 
         XR    R2,R2                                                            
         USING FATABSD,R2                                                       
         SAC   512                                                              
         ICM   R2,15,TABSTMS       GET A(TEMPEST BLOCK)                         
         USING TTMSHDRD,R2                                                      
         ICM   RE,15,TTMSNTRY      GET MAX DSPACE ALLOCATIONS ALLOWED           
         BNZ   SETTM04                                                          
         ABEND 312,DUMP            CHECK TABS DATASPACE                         
*                                                                               
SETTM04  MHI   RE,L'TLUID                                                       
         AHI   RE,TTSSHDRL                                                      
         SRDL  RE,32                                                            
         MR    RE,R0               INDEX INTO BLOCK BY FACPAK ID                
*                                                                               
         AHI   R2,TTMSHDRL         GO TO START OF HEADERS                       
         AR    R2,RF               ADD INDEX VALUE                              
         USING TTSSHDRD,R2                                                      
         XC    TTSFAC,TTSFAC       THIS WILL CAUSE DMGR TO REINITIALISE         
*                                                                               
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         LA    R1,54                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CLEAR PROGRAMS DATASPACE CORE AREA FOR THIS TOR SET      *         
***********************************************************************         
RSTCORE  NTR1  BASE=*,LABEL=*                                                   
         LAM   AR0,ARF,ARZERO                                                   
         LA    R1,97                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     R4,VSSB             REQUIRE SSB                                  
         USING SSBD,R4                                                          
         XR    R0,R0                                                            
         ICM   R0,1,SSBSYSID       MUST HAVE SYSTEM ID                          
         N     R0,=X'0000000F'                                                  
         BNZ   RSTCR02                                                          
         ABEND 371,DUMP            MUST HAVE RANGE 1-15                         
*                                                                               
RSTCR02  LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,SSBPGMTA                                                 
         XR    R2,R2                                                            
         USING FAPGMSD,R2                                                       
         SAC   512                                                              
         ICM   R2,15,PGMSCORE      GET A(CORE BLOCK)                            
*                                                                               
         BCTR  R0,0                                                             
         MHI   R0,((SBFACMAX*4)+4)                                              
         AR    R2,R0               INDEX INTO BLOCK                             
*                                                                               
         LHI   R3,(SBFACMAX*4)+4   CLEAR BLOCK                                  
         XR    RF,RF                                                            
         LR    R0,R2               SAVE A(START)                                
         MVCL  R2,RE                                                            
         LR    R2,R0               RESTORE A(START)                             
*                                                                               
         MVC   0(4,R2),SSBSYSN4    SET FACPAK NAME                              
         MVC   4(4,R2),FACORELN    SET INITIAL CORE LENGTH FOR TOR              
*                                                                               
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         LA    R1,98                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         DROP  R2,R4                                                            
         EJECT                                                                  
***********************************************************************         
* INITIAL SSB VALUE SETUP                                             *         
***********************************************************************         
SETSSB   NTR1  BASE=*,LABEL=*                                                   
         L     R5,VSSB             INITIALISE SOME SSB FIELDS                   
         USING SSBD,R5                                                          
         MVC   SSBSYSID,FACSYSID                                                
         MVC   SSBDSPAC,FACSPACE+3 BASED ON DMGR DSPACE                         
*&&US*&& CLI   FAGLOBAL,C'Y'       ALLOCATE FILES BASED ON DSPACE               
*&&US*&& BNE   *+8                                                              
*&&US*&& OI    SSBSTAT2,SSBSGALO                                                
         CLI   FABULKUP,C'Y'       TEST BULK UP LOAD IN FALINK                  
         BNE   *+8                                                              
         OI    SSBSTAT5,SSB5BULK                                                
*&&US*&& CLI   FAPOLING,C'Y'       TEST DDLINK POLLING TURNAROUND               
*&&US*&& BNE   *+8                                                              
*&&US*&& OI    SSBSTAT6,SSB6POLL                                                
         CLI   FAALLC,C'Y' '       TEST ALL CHRS > X'40' ARE VALID              
         BNE   *+8                                                              
         OI    SSBSTAT6,SSB6ALLC                                                
         CLI   FAMOFA,C'Y'         TEST FAC-MO TRANSFER FOR REP                 
         BNE   *+8                                                              
         OI    SSBSTAT5,SSB5FAMO                                                
         CLI   SMFRECS,C'Y'        TEST SMF 249 ADRFILE RECORDS                 
         BNE   *+8                                                              
         OI    SSBSTAT5,SSBADRSM                                                
         CLI   FACTMPST,C'Y'       TEST NEW 'BIG' TEMPEST                       
         BNE   *+8                                                              
         OI    SSBSTAT4,SSBTMPST                                                
         CLI   FACTAPRG,C'Y'       TEST SOFT TAPRG                              
         BNE   *+8                                                              
         OI    SSBSTAT4,SSBPGDSP   SET TAPRG IS DISPLACEMENT                    
         CLI   FAC31BIT,C'Y'                                                    
         BNE   *+8                                                              
         OI    SSBMODE,SSBMODIO    SET IO ADDRESSING MODE                       
         MVC   FASMODE,SSBMODE     AND SAVE A USEFUL BYTE                       
         CLI   FACPROTO,C'Y'                                                    
         BNE   *+8                                                              
         OI    SSBPROT,SSBPROTQ    SET DUMP ON STORAGE PROTECTION CHECK         
         OI    SSBPROT,SSBSTRTQ                                                 
         CLI   FAPROT,C'Y'         TEST STORAGE PROTECTION REQUIRED             
         BNE   *+8                                                              
         OI    SSBPROT,SSBPONQ                                                  
*&&US*&& CLI   FAWLM,C'Y'          TEST WLM ENABLED                             
*&&US*&& BNE   *+8                                                              
*&&US*&& OI    SSBSTAT5,SSB5WLM                                                 
*                                                                               
         MVC   SSBSHMTB,=V(SHMTAB) SAVE A(SHMTAB)                               
*                                                                               
         OC    FACMQION,FACMQION   SET UP MQ SERIES PARAMETERS                  
         BZ    STSSB02                                                          
         MVC   SSBMQION,FACMQION                                                
         MVC   SSBMQMAX,FACMQMAX                                                
         ICM   RF,15,AFACMQM                                                    
         BZ    STSSB02                                                          
         ICM   RE,15,SSBAMQM                                                    
         BZ    STSSB02                                                          
         MVC   0(48,RE),0(RF)                                                   
         ICM   RF,15,AFACMQIN                                                   
         BZ    STSSB02                                                          
         ICM   RE,15,SSBAMQIN                                                   
         BZ    STSSB02                                                          
         MVC   0(48,RE),0(RF)                                                   
         ICM   RF,15,AFACMQOU                                                   
         BZ    STSSB02                                                          
         ICM   RE,15,SSBAMQOU                                                   
         BZ    STSSB02                                                          
         MVC   0(48,RE),0(RF)                                                   
         ICM   RF,15,AFACMQWK                                                   
         BZ    STSSB02                                                          
         ICM   RE,15,SSBAMQWK                                                   
         BZ    STSSB02                                                          
         MVC   0(48,RE),0(RF)                                                   
         ICM   RF,15,AFACMQCT                                                   
         BZ    STSSB02                                                          
         ICM   RE,15,SSBAMQCT                                                   
         BZ    STSSB02                                                          
         MVC   0(48,RE),0(RF)                                                   
*&&US*&& CLI   FATOR,C'Y'          SET FLAGS FOR TOR                            
*&&US*&& BNE   STSSB02                                                          
*&&US*&& OI    SSBSTAT6,SSB6MQWI   SET MQIO SYSOPEN CALL                        
*                                                                               
STSSB02  CLI   FATOR,C'Y'          SET FLAGS FOR AOR                            
         BE    STSSB03                                                          
         OI    SSBSTAT4,SSBSAOR                                                 
         MVI   FACPGMUP,C'N'                                                    
         MVI   OPTLOAD,C'N'                                                     
*                                                                               
STSSB03  CLI   FALP2,C'Y'          SET LP2 FLAG                                 
         BNE   STSSB04                                                          
         OI    SSBSTAT5,SSB5LP2                                                 
*                                                                               
STSSB04  CLI   FACUDATE,C'Y'       SET SSB DATE INFO                            
         BNE   *+8                                                              
         OI    SSBSTAT4,SSBUPTDT                                                
         GOTO1 VDATCON,DMCB,(0,LOADDATE),(10,SSBDATE)  C'DD/MM/YY'              
         GOTO1 (RF),(R1),,(3,SSBDATEB)                                          
         GOTO1 (RF),(R1),(3,SSBDATEB),(0,WORK)                                  
         GOTO1 VGETDAY,DMCB,WORK,WORK+10                                        
         MVC   SSBDAYNO,DMCB       SET DAY NUMBER IN SSB                        
*                                                                               
         TIME  BIN                                                              
         ST    R1,NOWDATE                                                       
         ST    R0,NOWTIME                                                       
         MVC   STRDATE(8),NOWDATE  DATE/TIME FORMAT USED IN CHKPT RECS          
         TIME  DEC                                                              
         SRL   R0,8                                                             
         SLL   R0,4                                                             
         ST    R0,SSBSTRTM         SET TIME P'0HHMMSS+'                         
*                                                                               
         MVC   SSBSDATE(8),NOWDATE SET DATE/TIME FACPAK STARTED                 
         MVC   SSBTWAS,FACTWAS     SET NUMBER OF TWA'S/TERMINAL                 
         MVC   SSBTWAM,FACTWAM     SET MODULUS FOR TWA'S                        
         MVC   SSBTWAL,FACTWAL     SET TEMPSTR RECORD LENGTH                    
         MVC   SSBTMSL,FACTMSL     SET TEMPEST RECORD LENGTH                    
         MVC   SSBSSMAX,FACSSMAX   SET LOGICAL SESSIONS PER TERMINAL            
         MVC   SSBSSPGS,FACSSPGS   SET TEMPSTR PAGES PER SESSION                
         MVC   SSBSSMXP,FACSSMXP   SET PHYSICAL SESSIONS PER TERMINAL           
         CLC   SSBSSMXP,SSBSSMAX                                                
         BH    *+10                                                             
         MVC   SSBSSMXP,SSBSSMAX   SET PHYSICAL=LOGICAL IF NOT SET              
         MVC   SSBJMAX,FACJOBMX    SET MAX SCHEDULER JOBS                       
         MVC   SSBVTID,VTAMAPL     SET VTAM APPLICATION ID IN SSB               
*                                                                               
         CLI   FACDAROK,C'Y'                                                    
         BNE   *+8                                                              
         OI    SSBDARFL,SSBDRUPD   THIS FACPAK USES $DARE                       
         CLI   FACRECRD,C'N'                                                    
         BNE   *+18                                                             
         OI    SSBSTAT3,SSBNOADR   SET NO DATA RECORDING                        
         L     RF,VLOGGER                                                       
         MVC   0(2,RF),=X'07FE'    DISABLE RECORDING                            
         CLI   FACRECRD,C'X'                                                    
         BNE   *+8                                                              
         OI    SSBSTAT3,SSBXTADR   SET EXTENDED DATA RECORDING                  
*                                                                               
         MVC   LENTWA,SSBTWAL      SET TWA SIZE INFO                            
         CLC   LENTWA,=H'18432'    18K TEMPSTR DISPLACEMENTS                    
         BNE   STSSB06                                                          
         MVC   STRCHK,=Y(CHKPTDSP)                                              
         MVC   LENCHK,=Y(CHKPTLEN)                                              
         B     STSSB10                                                          
*                                                                               
STSSB06  CLC   LENTWA,=H'14336'    14K TEMPSTR DISPLACEMENTS                    
         BNE   STSSB08                                                          
         MVC   STRCHK,=H'12800'    SET CHKPNT START AT 12.5K                    
         MVC   LENCHK,=H'1536'     SET CHKPNT LENGTH AT 1.5K                    
         B     STSSB10                                                          
*                                                                               
STSSB08  ABEND 313,DUMP            WRONG LENGTH DEFN FOR TEMPSTR                
*                                                                               
STSSB10  L     R1,=V(FACIDTAB)     A(FACIDTAB) FROM FATAB BOOK                  
         LHY   RE,-2(,R1)          FACIDMAX STORED IN FATABUS                   
         CLC   0(4,R1),=C'????'    IS THIS THE OLD TABLE?                       
         BE    STSSB10E            YES- THIS IS THE FACIDTAB ITSELF             
*                                  NO - THIS MUST BE NEW TABLE INDEX            
STSSB10C CLC   SSBDSPAC,0(R1)      MATCH ON THE DSPACE                          
         BE    STSSB10D            YES- SET NEW A(FACIDTAB)                     
         AHI   R1,FACIDLNQ         NO - TRY THE NEXT ENTRY                      
         CLI   0(R1),X'FF'                                                      
         BNE   STSSB10C                                                         
         DC    H'0'                UNKNOWN DSPACE VALUE                         
*                                                                               
STSSB10D L     R1,FACAID-FACIDD(R1)    LOAD DSPACE RELATED FACIDTAB             
*                                                                               
STSSB10E ST    R1,SSBAFID          SET A(FACIDTAB) IN SSB                       
         LLC   RF,FACSYSID         GET FACPAK SYSTEM ID                         
         CR    RF,RE               RF > FACIDMAX?                               
         BNH   STSSB10F            NO                                           
         SR    RF,RF               SET UNKNOWN SYSTEM                           
         STC   RF,FACSYSID                                                      
*                                                                               
STSSB10F MHI   RF,L'FACITAB                                                     
         AR    R1,RF                                                            
         ST    R1,ADRSYSID         SET ADR OF SYSTEM ID TABLE ENTRY             
         MVC   FAC3CHR,0(R1)       SET THREE CHR FACPAK NAME                    
         CLI   3(R1),C' '                                                       
         BE    *+10                                                             
         MVC   FAC3CHR+2(1),3(R1)                                               
         MVC   SSBSYSNA,FAC3CHR    SET FACPAK SYSTEM ID NAME (3 CHR)            
*                                                                               
         MVC   SSBSYSN4,0(R1)      SET FACPAK SYSTEM ID NAME (4 CHR)            
         MVC   SSBSYSFL,5(R1)      SET FACPAK SYSTEM ID FLAGS                   
         MVC   SSBSYSCH,6(R1)      SET FACPAK SYSTEM ID CHR FOR MSGS            
         MVC   SSBSYSN1,7(R1)      SET FACPAK SYSTEM ID NAME (1 CHR)            
         MVC   SSBWSSVR,FACXALEN   SET WSSVR LENGTH IN K                        
*                                                                               
         MVC   SSBSYSIX,FACSYSID   SET SSBSYSIX FOR TOR                         
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    STSSB12                                                          
         OI    SSBSTAT4,SSBSAOR    SET FACPAK AS AOR                            
*                                                                               
         LLC   R1,FAAORID          AOR ID=A,B,C,D                               
         SLL   R1,28                                                            
         SRL   R1,24                                                            
         STC   R1,SSBSYSIX         SET SSBSYSIX                                 
         OC    SSBSYSIX,FACSYSID                                                
*                                                                               
STSSB12  MVC   FACMSGID,FAC3CHR                                                 
         MVI   FACMSGID+3,C'+'                                                  
         TM    SSBSTAT4,SSBSAOR    TEST IF I AM AN AOR                          
         BZ    STSSB14                                                          
         LLC   R1,SSBSYSIX         SET AOR CHR IN MSG                           
         SRL   R1,4                                                             
         LA    R1,X'C0'(R1)                                                     
         STC   R1,FACMSGID+3                                                    
*                                                                               
STSSB14  XR    R1,R1                                                            
         ICM   R1,3,SSBJMAX                                                     
         L     RE,SSBAJOB          RE=A(JOB TABLE)                              
         LH    R0,0(RE)            R0=L'JOB TABLE ENTRY                         
         MR    R0,R0               R1=TOTAL L'JOB TABLE                         
         LA    R1,6-1(RE,R1)       R1=A(END OF JOB TABLE)                       
         C     R1,2(RE)                                                         
         BNL   *+8                                                              
         ST    R1,2(RE)            SET A(END OF JOB TABLE)                      
*                                                                               
SETSSBX  J     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* SET UP INITIAL LARGE STORAGE AREAS                                  *         
***********************************************************************         
GETAREAS NTR1  BASE=*,LABEL=*                                                   
         LA    R1,90                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         SAM31                                                                  
         L     R2,HICORE                                                        
         CLI   FACXAUTL,C'Y'       TEST UTL LIST IN XA                          
         BNE   *+12                                                             
         L     R2,XAAPUTL                                                       
         A     R2,XALOCORE                                                      
         AHI   R2,7                                                             
         SRL   R2,3                                                             
         SLL   R2,3                MAKE MODULO 8                                
         MVC   0(L'IUTL,R2),IUTL                                                
         XC    12(4,R2),12(R2)     ADR OF END OF UTL AT VUTL-10(4)              
*                                                                               
         AHI   R2,16                                                            
         XC    0(4,R2),0(R2)       ACTUAL NUM OF TRMS AT VUTL-6(4)              
         MVC   4(2,R2),VTAMUTL     MAXIMUM NUM OF TRMS AT VUTL-2(2)             
*                                                                               
         AHI   R2,6                                                             
         ST    R2,VUTL                                                          
         LHI   RF,TUTLXALN         GET UTL ENTRY LENGTH                         
         STH   RF,0(R2)            ENTRY LENGTH AT VUTL+0(2)                    
         LA    R0,5(R2)                                                         
         ST    R0,2(R2)            ADR OF LAST-1 AT VUTL+2(4)                   
*                                                                               
         MH    RF,VTAMUTL          RF=L'ALL ENTRIES                             
         LA    RE,6(R2)            RE=A(FIRST ENTRY)                            
         LA    R1,0(RF,RE)                                                      
*                                                                               
         CLI   FACXAUTL,C'Y'       TEST UTL LIST IN XA                          
         BE    *+8                                                              
         ST    R1,HICORE           R1=A(END OF UTL LIST)                        
*                                                                               
         AHI   R2,-10                                                           
         ST    R1,0(R2)            ADR OF END OF UTL AT VUTL-10(4)              
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR UTL ENTRIES                            
*                                                                               
         L     R2,VUTL             FILL IN XA UTL BLOCK ADDRESSES               
         USING UTLD,R2                                                          
         AHI   R2,-10                                                           
         ICM   RF,15,0(R2)         ADR OF END OF UTL AT VUTL-10(4)              
         BCTR  RF,0                                                             
         LH    RE,10(R2)                                                        
         AHI   R2,16                                                            
*                                                                               
         L     R3,XAAUTL                                                        
         A     R3,XALOCORE                                                      
SUTL02   ST    R3,TUTLXADR         SET A(XA UTL BLOCK)                          
         USING XAUTLD,R3                                                        
         MVC   XALEN,=AL4(XAUTLLN) SET LENGTH OF BLOCK                          
         MVC   XALUID,SPACES       SET LUID UNDEFINED                           
         MVC   XASWTABI,=CL8'XASWTAB*'                                          
         MVC   XAPASSRI,=CL8'XAPASSR*'                                          
*                                                                               
         LHI   R0,XASWTAB-XAUTLD                                                
         AR    R0,R3                                                            
         ST    R0,XAASWTAB         SET A(SWITCH TABLE)                          
         LHI   R0,XAPASSR-XAUTLD                                                
         AR    R0,R3                                                            
         ST    R0,XAAPASSR         SET A(WSSVR TABLE)                           
*                                                                               
         A     R3,=AL4(XAUTLLN)                                                 
         BXLE  R2,RE,SUTL02                                                     
         SAM24                                                                  
         DROP  R2,R3                                                            
         EJECT                                                                  
***********************************************************************         
* SET UP PRQ ENTRIES                                                  *         
***********************************************************************         
SETPRQ   CLI   FATOR,C'Y'          AORS HAVE NO PRQ ENTRIES                     
         BNE   SETPRQE                                                          
         XR    RE,RE               GET NUMBER OF PRQ ENTRIES REQUIRED           
         ICM   RE,3,VTAMPRQ                                                     
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R0,PRQDL            GET DEFAULT ENTRY WIDTH                      
         LA    RF,PRQDL            GET AVERAGE ENTRY WIDTH                      
         L     R2,HICORE                                                        
         LA    R2,7(R2)                                                         
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         MVC   0(L'IPRQ,R2),IPRQ                                                
         XC    12(4,R2),12(R2)     ADR OF END OF PRQ AT VPRQ-10(4)              
         LA    R2,16(R2)                                                        
         XC    0(4,R2),0(R2)       ACTUAL NUM OF PRNS AT VPRQ-6(4)              
         STH   RE,4(R2)            MAXIMUM NUM OF PRNS AT VPRQ-2(2)             
         LA    R2,6(R2)                                                         
         ST    R2,VPRQ                                                          
         STH   R0,0(R2)            ENTRY LENGTH AT VPRQ+0(2) (DEFAULT)          
         LA    R1,5(R2)                                                         
         ST    R1,2(R2)            ADR OF LAST-1 AT VPRQ+2(4)                   
         MR    RE,RE                                                            
         LA    RE,6(R2)            RE=A(FIRST ENTRY),RF=TOTAL LENGTH            
         LA    R1,0(RE,RF)         POINT TO END OF PRQ                          
         ST    R1,HICORE                                                        
         AHI   R2,-10                                                           
         ST    R1,0(R2)            ADR OF END OF PRQ AT VPRQ-10(4)              
*                                                                               
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR PRQ ENTRIES                            
         EJECT                                                                  
***********************************************************************         
* SET UP PRQE ENTRIES                                                 *         
***********************************************************************         
SETPRQE  CLI   FATOR,C'Y'          AORS HAVE NO PRQE ENTRIES                    
         BNE   GETAREAX                                                         
         XR    RE,RE               GET NUMBER OF PRQE ENTRIES PER PRQ           
         ICM   RE,3,VTAMPRQE                                                    
         BNZ   *+8                                                              
         LA    RE,16               USE 16 IF NOT DEFINED                        
*                                                                               
         XR    RF,RF                                                            
         ICM   RF,3,VTAMPRQ        MULTIPLY BY NUM OF PRQ ENTRIES               
         MR    RE,RE                                                            
         LR    RE,RF                                                            
         LA    RF,L'PNTRY          GET ENTRY WIDTH                              
         L     R2,HICORE                                                        
         LA    R2,7(R2)                                                         
         SRL   R2,3                                                             
         SLL   R2,3                                                             
         MVC   0(L'IPQE,R2),IPQE                                                
         XC    12(4,R2),12(R2)     ADR OF END OF PRQE AT VPRQE-10(4)            
         LA    R2,16(R2)                                                        
         XC    0(4,R2),0(R2)       ACTUAL NUM OF PQES AT VPRQE-6(4)             
         STH   RE,4(R2)            MAXIMUM NUM OF PQES AT VPRQE-2(2)            
         LA    R2,6(R2)                                                         
         ST    R2,VPRQENTS                                                      
         STH   RF,0(R2)            ENTRY LENGTH AT VPRQE+0(2)                   
         MR    RE,RE                                                            
         LA    RE,6(R2)            RE=A(FIRST ENTRY),RF=TOTAL LENGTH            
         LA    R1,0(RE,RF)         POINT TO END OF PRQE                         
         ST    R1,HICORE                                                        
         BCTR  R1,0                                                             
         ST    R1,2(R2)            ADR OF LAST-1 AT VPRQ+2(4)                   
         AHI   R2,-10                                                           
         LA    R1,1(R1)                                                         
         ST    R1,0(R2)            ADR OF END OF PRQE AT VPRQE-10(4)            
*                                                                               
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR PQE ENTRIES                            
*                                                                               
GETAREAX LA    R1,91                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* SET UP OPERATOR COMMUNICATIONS                                      *         
***********************************************************************         
SETOPS   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,59                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     R5,VSSB             TEST CHKPNT DATE SAME AS TODAY               
         USING SSBD,R5                                                          
         LA    R2,ACOMM                                                         
         EXTRACT (R2),FIELDS=COMM                                               
*                                                                               
         L     RF,ACOMM                                                         
         ST    RF,SSBACOMM                                                      
         USING COMLIST,RF                                                       
         MVC   SSBOPECB+1(3),COMECBPT+1                                         
         L     R2,COMCIBPT         GET A(CIB)                                   
         USING CIBNEXT,R2                                                       
         LA    R3,COMCIBPT         SET A(A(CIB))                                
         DROP  RF                                                               
*                                                                               
         CLI   CIBVERB,CIBSTART    TEST FOR 'S JOBNAME' (IE NOT BATCH)          
         BNE   SETOP2                                                           
         DROP  R2,R5                                                            
         QEDIT ORIGIN=(R3),BLOCK=(R2)        RELEASE THE CIB                    
*                                                                               
SETOP2   QEDIT ORIGIN=(R3),CIBCTR=1          ACCEPT MODIFY COMMANDS             
*                                                                               
         LA    R1,60                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ACCESS DATASPACES                                                   *         
***********************************************************************         
GETSPC   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,45                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     R5,VSSB             INITIALISE SOME SSB FIELDS                   
         USING SSBD,R5                                                          
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,MESSAGE                                                       
         XC    MESSAGE(28),MESSAGE                                              
         MVC   MESSAGE+0(4),=C'GETA'                                            
         MVC   MESSAGE+4(12),FACTBDSP                                           
         SVC   247                                                              
         LTR   RF,RF                                                            
         BZ    GETSPC02                                                         
         ABEND 314,DUMP                                                         
*                                                                               
GETSPC02 OC    MESSAGE+24(4),MESSAGE+24                                         
         BNZ   GETSPC04                                                         
         LA    R1,48                                                            
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
*                                                                               
GETSPC04 MVC   SSBTBLET,MESSAGE+24                                              
         L     RF,=A(DTVRSN)       SET NEW ADDRESS OF VERSION TABLE             
         SLL   RF,17               TURN OFF X'8000'                             
         SRL   RF,17-6             MULTIPLY BY 64                               
         STCM  RF,15,VVRSNTAB                                                   
*                                                                               
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,MESSAGE                                                       
         XC    MESSAGE(28),MESSAGE                                              
         MVC   MESSAGE+0(4),=C'GETA'                                            
         MVC   MESSAGE+4(12),FACSPACE                                           
         SVC   247                                                              
         LTR   RF,RF                                                            
         BZ    GETSPC06                                                         
         ABEND 315,DUMP                                                         
*                                                                               
GETSPC06 OC    MESSAGE+24(4),MESSAGE+24                                         
         BNZ   GETSPC08                                                         
         LA    R1,47                                                            
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
*                                                                               
GETSPC08 MVC   SSBALET,MESSAGE+24                                               
*&&UK                                                                           
         CLC   UKMEDDSP,SPACES     TEST IF UK MEDIA DATASPACE DEFINED           
         BNH   GETSPC16                                                         
         MVC   SSBMEDTN,UKMEDDSP   SAVE TABLE ID (4CHR ID)                      
*                                                                               
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,MESSAGE                                                       
         XC    MESSAGE(28),MESSAGE                                              
         MVC   MESSAGE+0(4),=C'GETA'                                            
         MVC   MESSAGE+4(12),UKMEDDSP                                           
         SVC   247                                                              
         LTR   RF,RF                                                            
         BNZ   GETSPC10                                                         
         OC    MESSAGE+24(4),MESSAGE+24                                         
         BNZ   GETSPC14                                                         
*                                                                               
GETSPC10 LA    RF,MEDDSTAB         LOOKUP TABLE ID FOR JOB NAME                 
         LA    R0,MEDDSNQ                                                       
         CLC   SSBMEDTN,0(RF)                                                   
         BE    *+12                                                             
         LA    RF,7(RF)                                                         
         BCT   R0,*-14                                                          
*                                                                               
         MVC   XTRAMESS(5),=C'MEDIA'                                            
         MVC   XTRAMESS+5(3),4(RF)                                              
         L     RF,=A(MES101)       PUT TABLE ID IN MESSAGE                      
         MVC   43(4,RF),SSBMEDTN                                                
         LA    R1,101              MEDIA DATASPACE NOT FOUND (101&102)          
         MVI   REPLY,C'R'          DEFAULT IS RETRY                             
         BRAS  RE,PUTMESS                                                       
*                                                                               
GETSPC12 CLI   REPLY,C'R'          RETRY                                        
         BE    GETSPC10                                                         
         CLI   REPLY,C'I'          IGNORE                                       
         BE    GETSPC16                                                         
         LA    R1,102              START MEDIAXXX                               
         MVI   REPLY,C'R'          DEFAULT IS RETRY                             
         BRAS  RE,PUTMESS                                                       
         B     GETSPC12                                                         
*                                                                               
GETSPC14 MVC   SSBMEDTB,MESSAGE+20 SAVE ORIGIN + ALET                           
*&&                                                                             
GETSPC16 CLC   PGMSDSP,SPACES      SEE IF PROGRAMS DATASPACE DEFINED            
         BNH   GETSPC18                                                         
*                                                                               
         LA    R0,21                                                            
         LNR   R0,R0                                                            
         LA    R1,MESSAGE                                                       
         XC    MESSAGE(28),MESSAGE                                              
         MVC   MESSAGE+0(4),=C'GETA'                                            
         MVC   MESSAGE+4(12),PGMSDSP                                            
         SVC   247                                                              
         LTR   RF,RF                                                            
         BNZ   GETSPC18                                                         
         OC    MESSAGE+24(4),MESSAGE+24                                         
         BNZ   GETSPC18                                                         
         LA    R1,95               PROGRAMS FILE DATASPACE NOT FOUND            
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
*                                                                               
GETSPC18 MVC   SSBPGMTB,MESSAGE+20 SAVE ORIGIN + ALET                           
*                                                                               
         LA    R2,FULL             GET JOBNAME INTO DUB                         
         EXTRACT (2),FIELDS=TIOT                                                
         L     R2,FULL                                                          
         MVC   DUB,0(R2)                                                        
*                                                                               
         XC    DUB,DUB                                                          
         MVI   DUB+1,3                                                          
         GOTO1 VLOCKSPC,DUB        CALL LOCKSPC TO ENTER A FACPAK               
         TM    4(R1),X'80'                                                      
         JO    STRTEOJ                                                          
*                                                                               
         LA    R1,46                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD DUMMY UTLS                                                    *         
***********************************************************************         
GETDUTL  NTR1  BASE=*,LABEL=*                                                   
         CLI   FATOR,C'Y'          AORS HAVE NO DUMMY UTLS                      
         BNE   GETDUTLX                                                         
         LA    R1,51                                                            
         BRAS  RE,PUTMESS                                                       
         SAM31                                                                  
*                                                                               
         XR    R6,R6               TEST IF DUMMY UTL'S REQUIRED                 
         ICM   R6,3,FACDUTL                                                     
         BZ    GETDUTL4                                                         
         C     R6,TSKNUM           MINIMUM IS NUMBER OF TASKS                   
         BNL   *+8                                                              
         L     R6,TSKNUM                                                        
         CH    R6,VTAMUTL          R6=NUMBER OF DUMMY TERMINALS                 
         BL    GETDUTL1                                                         
         ABEND 316,DUMP                                                         
*                                                                               
         USING UTLD,R5                                                          
GETDUTL1 ICM   R3,15,XAADBUFF      GET A(DUMMY TBUFF)                           
         BZ    *+12                                                             
         A     R3,XALOCORE                                                      
         AHI   R3,32               GO PAST HEADER INFO                          
*                                                                               
GETDUTL2 GOTO1 VLCM,DMCB,VTGETUTL  THIS SETS R1=A(NEW UTL ENTRY)                
         LTR   R5,R1                                                            
         BNZ   GETDUTL3                                                         
         ABEND 317,DUMP                                                         
*                                                                               
GETDUTL3 OI    TSTAT1,TSTATDDS     SET DDS/3270/DUMMY TERMINAL                  
         OI    TTYPE,TTYPE327                                                   
         OI    TTYPE2,TTYPEDUM                                                  
         OI    TFLAG,TFLAGIRB      SET INHIBIT RECEIVING BROADCASTS             
*                                                                               
         STCM  R3,15,TBUFF         SET A(DUMMY TBUFF)                           
         LTR   R3,R3                                                            
         BZ    *+8                                                              
         AHI   R3,32+TBUFFL        NEXT DUMMY TBUFF                             
*                                                                               
         LH    R0,TNUM                                                          
         CVD   R0,DUB              SET TEMINAL NUMBER IN LUID                   
         OI    DUB+7,X'0F'                                                      
         MVC   TSYM,=C'DUMMY00T'                                                
         UNPK  TSYM+5(2),DUB+6(2)                                               
         CR    R6,R0                                                            
         BH    GETDUTL2            BACK TO CREATE NEXT DUMMY TERMINAL           
*                                                                               
GETDUTL4 L     R5,VSSB                                                          
         USING SSBD,R5                                                          
         STH   R6,SSBDTRMS                                                      
         STH   R6,SSBTRMS                                                       
         SAM24                                                                  
*                                                                               
         LHI   R1,52                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
GETDUTLX J     EXIT                                                             
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* BUILD TBUFFS IN XA STORAGE                                          *         
***********************************************************************         
GETBUFF  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,43                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         SAM31                     GET INTO XA MODE                             
*                                                                               
         L     R2,XAATBUFF                                                      
         A     R2,XALOCORE                                                      
         L     R0,XALTBUFF                                                      
         MVC   0(16,R2),=CL16'*VTAM BUFF ADRS*'                                 
         AHI   R2,16                                                            
         ST    R2,VLCBUFFS         SET START OF LIST OF ADCONS                  
*                                                                               
         XR    R1,R1               NUMBER OF BUFFERS                            
         ICM   R1,3,VTAMBUF                                                     
         SLL   R1,2                EACH ENTRY 4 BYTES LONG                      
         AR    R1,R2                                                            
         BCTR  R1,0                                                             
         ST    R1,VLCBUFFX         SET END LIST-1 OF ADCONSS                    
*                                                                               
         LA    R1,16+1(R1)         R1=A(FIRST BUFFER)                           
         L     RF,=A(32+TBUFFXL)   SIZE OF EACH BUFFER                          
*                                                                               
GETBUF2  LA    R0,32(R1)           SET A(BUFFER+32) IN LIST                     
         ST    R0,0(R2)                                                         
         AR    R1,RF               BUMP TO NEXT BUFFER                          
         LA    R2,4(R2)            BUMP TO NEXT ADCON IN LIST                   
         C     R2,VLCBUFFX                                                      
         BL    GETBUF2                                                          
*                                                                               
         L     R2,VLCBUFFX         INITIALISE BUFFERS                           
         LA    R2,1(R2)                                                         
         MVC   0(16,R2),=CL16'**VTAM BUFFERS**'                                 
         LA    R2,16(R2)                                                        
         ST    R2,VBUFFERS         SET START OF BUFFERS                         
*                                                                               
         XR    RE,RE                                                            
         ICM   RE,3,VTAMBUF                                                     
         L     RF,=A(32+TBUFFXL)                                                
         MR    RE,RE                                                            
         LR    RE,R2               RE=A(START),RF=LENGTH                        
         AR    R2,RF                                                            
         BCTR  R2,0                                                             
         ST    R2,VBUFFERX         SET END OF BUFFERS -1                        
*                                                                               
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  RE,R0               CLEAR BUFFERS TO BINARY ZERO                 
*                                                                               
GETBUFFX SAM24                                                                  
         LHI   R1,44                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* FIND OUT OUR TOR ADDRESS IN DMGR DATASPACE                          *         
***********************************************************************         
GETTOR   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,49                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     RE,VSSB                                                          
         L     RF,SSBAFID-SSBD(RE)                                              
         USING FACITABD,RF                                                      
         XR    R0,R0               R0=COUNT OF TEST SYSTEMS                     
         XR    R1,R1               R1=COUNT OF LIVE SYSTEMS                     
GETTOR2  CLI   0(RF),255                                                        
         BNE   GETTOR3                                                          
         ABEND 310,DUMP            UNKNOWN SYSTEM                               
*                                                                               
GETTOR3  CLC   FACSYSID,FACIID     MATCH ON SYSTEM ID                           
         BE    GETTOR4                                                          
         TM    FACIFL,FACITST      IS THIS A TEST SYSTEM?                       
         BZ    *+12                                                             
         AHI   R0,1                INCREMENT TEST SYSTEM COUNT                  
         B     *+8                                                              
         AHI   R1,1                INCREMENT LIVE SYSTEM COUNT                  
         LA    RF,L'FACITAB(RF)                                                 
         B     GETTOR2                                                          
*                                                                               
GETTOR4  TM    FACIFL,FACITST      IS SYSTEM A TEST SYSTEM?                     
         BO    *+6                                                              
         LR    R0,R1               R0=COUNT OF SYSTEMS BEFORE THIS ONE          
*                                                                               
         LA    R1,SBFACMAX         MAX NUMBER OF AOR FACPAKS                    
         MHI   R1,SBEXCHLQ         LENGTH OF A ROW ON THE GRID                  
         AHI   R1,6+TORFACLQ       TOR INFO REGION                              
*                                                                               
         MR    R0,R0               R1=DISPLACEMENT TO TOR REGION                
*                                                                               
         L     RE,VSSB                                                          
         LAM   AR2,AR2,SSBALET-SSBD(RE)                                         
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,DHATOR-DMDSHDR(R2)                                         
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
         AR    R1,R2                                                            
         ST    R1,SSBATOR-SSBD(RE) SET A(TOR) FOR THIS FACPAK NAME              
*                                                                               
GETTORX  LA    R1,50                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* NEW CHKPT1 TO FACSYSID*TRK#1 (SSB/JOBTAB/BCTAB)                     *         
* NEW CHKPT2 TO FACSYSID*TRK#2 (TEMPEST BUFFER)                       *         
***********************************************************************         
CHKPT    NTR1  BASE=*,LABEL=*                                                   
         CLI   FATOR,C'Y'          ONLY TOR WRITES CHECKPOINT                   
         JNE   EXIT                                                             
         LA    R1,55                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         XC    P1(24),P1           WRITE CHKPNT 1 TO 1ST TRK OF PRGMS           
         MVC   P1,VWTCKD                                                        
         L     RE,VCHKPT1                                                       
         ST    RE,P2                                                            
         L     RF,VCHKPT1X                                                      
         SR    RF,RE               SUBTRACT TO GET LENGTH                       
         ST    RF,P3               SET LNE                                      
         L     RE,VPRGMS                                                        
         ST    RE,P4                                                            
         USING DTFPHD,RE                                                        
         MVC   DNEXT,=X'00010000'  SET TRK 1/BLK 0                              
         LA    R0,P6                                                            
         ST    R0,P5               SET A(DA) ON RETURN                          
         LA    R1,P1                                                            
         LA    R8,PHLSTRK-1        WRITE UP TO PHLIST TRACK                     
*                                                                               
CHKPT02  L     RF,VDADDS                                                        
         BASR  RE,RF                                                            
         OC    P3(2),P3                                                         
         BZ    CHKPT04                                                          
         BRAS  RE,IOERROR                                                       
         ABEND 311,DUMP                                                         
*                                                                               
CHKPT04  L     RE,VPRGMS                                                        
         LH    RF,DNEXT            WRITE FIRST 32 TRACKS                        
         USING DTFPHD,RE                                                        
         LA    RF,1(RF)                                                         
         SLL   RF,16                                                            
         ST    RF,DNEXT                                                         
         BRCT  R8,CHKPT02                                                       
*                                  WRITE CHKPNT 2 TO PRGMS                      
         XC    DNEXT,DNEXT         SET TRK 2/BLK 0 +OFFSET                      
         LLC   R1,FACSYSID                                                      
         LA    R1,2(R1,R1)                                                      
         STH   R1,DNEXT                                                         
*                                                                               
         ICM   RE,15,VCHKPT2       WRITE CHECKPOINT 2 TO PGMS                   
         BZ    CKPTX                                                            
         ST    RE,P2                                                            
         L     RF,VCHKPT2X                                                      
         SR    RF,RE                                                            
         ST    RF,P3                                                            
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    CKPTX                                                            
         BRAS  RE,IOERROR                                                       
         ABEND 312,DUMP                                                         
*                                                                               
CKPTX    LA    R1,56                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DISPLAY SYSTEM DATE ON OPERATOR CONSOLE                             *         
* ASK OPERATOR TO ENTER THE DAY OF WEEK                               *         
* NO SECOND CHANCE IS GIVEN IF HE GETS IT WRONG                       *         
***********************************************************************         
VALDATE  NTR1  BASE=*,LABEL=*                                                   
         L     R5,VSSB                                                          
         USING SSBD,R5                                                          
         GOTO1 VDATCON,DMCB,(3,SSBDATEB),(0,DM5)                                
         MVC   XTRAMESS,SPACES                                                  
         GOTO1 (RF),(R1),,(8,XTRAMESS)                                          
         LA    R1,21                                                            
         BRAS  RE,PUTMESS                                                       
         GOTO1 VGETDAY,DMCB,DM5,REPLY+3                                         
         CLC   REPLY(3),REPLY+3                                                 
         BNE   VALDATNG                                                         
         MVC   SSBDAYNO,DMCB       SET DAY NUMBER IN SSB                        
         J     EXIT                                                             
*                                                                               
VALDATNG LA    R1,22               INCORRECT RESPONSE/TERMINATE                 
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
         DROP  R5,RE                                                            
         EJECT                                                                  
***********************************************************************         
* BUILD A CORE RESIDENT PHASE LIST FROM PGMS DATASPACE ENTRIES        *         
***********************************************************************         
BLDPHLS  NTR1  BASE=*,LABEL=*                                                   
         L     RF,XAAPLIST                                                      
         A     RF,XALOCORE                                                      
         ST    RF,VPHLIST          SAVE V(PHLIST)                               
         GOTO1 =V(PROGBLD),DMCB,HICORE,0,VPHLIST,XALPLIST,PHLIST                
         MVC   HICORE,4(R1)                                                     
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* BUILD PROGRAM VERSION TABLE FROM PHASE RECORDS                      *         
***********************************************************************         
BLDVRSN  NTR1  BASE=*,LABEL=*                                                   
         LAM   AR0,ARF,ARZERO                                                   
         L     R5,VSSB                                                          
         USING SSBD,R5                                                          
         LA    R0,SSBDATEB         GET JULIAN DATE                              
         GOTO1 VDATCON,DMCB,(3,(R0)),(19,RLDATE)                                
*                                                                               
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTVRSN)                                              
         GOTO1 VLOCKSPC,DUB        LOCK VERSION TABLE                           
         ICM   R6,15,4(R1)                                                      
         BNZ   BLDV01                                                           
         ABEND 327,DUMP                                                         
*                                                                               
         USING DMSPACED,R6                                                      
BLDV01   LAM   AR0,ARF,ARZERO                                                   
         LAM   AR2,AR2,SSBTBLET                                                 
         ICM   R2,15,DSPTFRST      GET TABLE ADDRESS                            
         ICM   R3,15,DSPTEND       GET ADDRESS OF END-1                         
         AHI   R3,1                                                             
         SR    R3,R2                                                            
         XR    RF,RF                                                            
         SAC   512                                                              
         MVCL  R2,RE               CLEAR TABLE                                  
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         DROP  R5                                                               
*                                                                               
         ICM   R7,15,DSPTEND       SAVE ADDRESS OF END                          
         ICM   R6,15,DSPTFRST      GET TABLE ADDRESS                            
         L     RE,VSSB                                                          
         LAM   AR6,AR6,SSBTBLET-SSBD(RE)                                        
         SAC   512                                                              
         USING VRSNTABD,R6                                                      
*                                                                               
         LA    R8,RLKEY                                                         
         USING CTPHRECD,R8                                                      
         MVI   CTPHID,CTPHIDQ                                                   
         MVI   CTPHSUBI,CTPHSUBQ                                                
*                                                                               
         XC    P1(24),P1                                                        
         MVC   P1,ISREAD                                                        
         L     R8,ASTRTBUF         SET IOAREA ADDRESS                           
         ST    R8,P2                                                            
         MVC   P4,VCTFILE                                                       
         LA    RE,RLKEY                                                         
         ST    RE,P5               SET A(KEYARG)                                
         LA    RE,2400(R8)         SET FULL TRACK BUFFER ADDRESS                
         ST    RE,P6                                                            
         MVI   P6,255                                                           
         GOTO1 VISDDS,P1                                                        
         SAFE  CLEAR=ARZERO                                                     
*                                                                               
         CLI   CTPHID,CTPHIDQ      MAKE SURE FOUND A PHASE RECORD               
         BE    BLDV04                                                           
         ABEND 328,DUMP                                                         
*                                                                               
BLDV02   MVC   P1,ISRDSEQ                                                       
         GOTO1 VISDDS,P1           READ NEXT RECORD                             
         SAFE  CLEAR=ARZERO                                                     
         OC    P3(2),P3                                                         
         BZ    BLDV04                                                           
         BRAS  RE,IOERROR                                                       
         ABEND 329,DUMP                                                         
*                                                                               
BLDV04   CLC   CTPHID(2),=AL1(CTPHIDQ,CTPHSUBQ)                                 
         BNE   BLDV26                                                           
         TM    27(R8),X'80'        TEST DELETED                                 
         BO    BLDV02                                                           
*NOP*    CLI   CTPHHEXN+2,0        TEST A BASE PROGRAM                          
*NOP*    BNE   BLDV02              NO-SKIP                                      
*                                                                               
         LA    R1,CTPHFRST         POINT TO FIRST ELEMENT                       
         USING CTPHVRSD,R1                                                      
         XR    R0,R0                                                            
BLDV06   CLI   0(R1),0                                                          
         BE    BLDV02                                                           
         CLI   0(R1),CTPHSCEQ      SYSTEM ELEMENT?                              
         BNE   BLDV08                                                           
         MVC   BYTE,CTPHSFL1-CTPHSYSD(R1)                                       
         TM    BYTE,CTPHSOFQ       SKIP IF OFFLINE PHASE                        
         BO    BLDV02                                                           
*                                                                               
BLDV08   CLI   CTPHVCDE,CTPHVCEQ                                                
         BE    BLDV12                                                           
         ICM   R0,1,1(R1)                                                       
         BNZ   BLDV10                                                           
         ABEND 330,DUMP                                                         
*                                                                               
BLDV10   AR    R1,R0                                                            
         B     BLDV06                                                           
*                                                                               
BLDV12   MVC   RLSDATE,CTPHVDTE    SAVE NOTIFY DATE                             
         LLC   RE,CTPHVLEN         GET LENGTH                                   
         AHI   RE,-(CTPHVOVQ)                                                   
         SRDL  RE,32                                                            
         D     RE,=F'5'                                                         
         LTR   RE,RE                                                            
         BZ    BLDV14                                                           
         ABEND 331,DUMP                                                         
*                                                                               
BLDV14   LR    R0,RF               SAVE ENTRY COUNT                             
         LA    R1,CTPHVOVQ(R1)     POINT TO FIRST ENTRY                         
         USING CTPHVFAC,R1                                                      
*                                                                               
BLDV16   CLI   CTPHHEXN+2,0        TEST A BASE PROGRAM                          
         BE    BLDV18                                                           
         MVC   VRSNPHS(3),CTPHHEXN MOVE PHASE NUMBER                            
         OI    VRSNPHS,X'80'       PHASE VERSIONS HAVE X'80'                    
         MVC   WORK,VRSNPGM                                                     
         B     *+14                                                             
*                                                                               
BLDV18   MVC   VRSNPGM,CTPHHEXN    MOVE PROGRAM NUMBER                          
         NI    VRSNPGM,X'0F'                                                    
         MVC   VRSNADV(5),CTPHVFAC MOVE VERSION DATA                            
         LA    RE,X'01'            CHANGE LETTER TO BIT CONFIG                  
         CLI   VRSNABC,C'A'                                                     
         BE    BLDV20                                                           
         LA    RE,X'02'                                                         
         CLI   VRSNABC,C'B'                                                     
         BE    BLDV20                                                           
         LA    RE,X'03'                                                         
         CLI   VRSNABC,C'C'                                                     
         BE    BLDV20                                                           
         ABEND 332,DUMP            BAD RECORD                                   
*                                                                               
BLDV20   STC   RE,VRSNABC          STORE VALUE OVER LETTER                      
         TM    VRSNPHS,X'80'                                                    
         BO    BLDV22                                                           
         CLC   RLSDATE,RLDATE      TEST NOTIFY DATE                             
         BH    *+8                                                              
         OI    VRSNFLAG,VRSNDATE   SET DATE HAS PASSED                          
*                                                                               
BLDV22   TM    BYTE,(CTPHSCRQ+CTPHSCSQ+CTPHSDMQ)                                
         BZ    BLDV24                                                           
         L     RE,VSSB                                                          
         OI    SSBSTAT4-SSBD(RE),SSBVCNTL                                       
*                                                                               
BLDV24   LA    R6,VRSNNEXT         NEXT TABLE ENTRY                             
         CR    R6,R7               TEST PAST END                                
         BH    BLDVERR             NEED LARGER TABLE                            
*                                                                               
         LA    R1,CTPHVNXT         NEXT ENTRY                                   
         BCT   R0,BLDV16                                                        
         B     BLDV02                                                           
         DROP  R1                                                               
*                                                                               
BLDV26   SAC   0                   RESET EVERYTHING BEFORE LOCKSPC CALL         
         LAM   AR0,ARF,ARZERO                                                   
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTVRSN)                                              
         MVI   DUB,X'10'                                                        
         GOTO1 VLOCKSPC,DUB        UNLOCK VERSION TABLE                         
         LAM   AR0,ARF,ARZERO                                                   
         J     EXIT                                                             
         DROP  R6                                                               
*                                                                               
BLDVERR  SAC   0                   RESET EVERYTHING BEFORE LOCKSPC CALL         
         LAM   AR0,ARF,ARZERO                                                   
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTVRSN)                                              
         MVI   DUB,X'10'                                                        
         GOTO1 VLOCKSPC,DUB        UNLOCK VERSION TABLE                         
         LAM   AR0,ARF,ARZERO                                                   
         LHI   R1,100              OUTPUT ERROR MESSAGE                         
         BRAS  RE,PUTMESS                                                       
         ABEND 333,DUMP            YOU HAVE TO RECYCLE THE TABS DSPACE          
         EJECT                                                                  
***********************************************************************         
* READ CTFILE PROGRAM VERSION CONTROL (PROVER) RECORDS AND UPDATE     *         
* PGMLST ENTRIES.                                                     *         
***********************************************************************         
REPROVE  NTR1  BASE=*,LABEL=*                                                   
         L     RE,VSSB             GET COMPRESSED DATE NOW                      
         LA    R0,SSBDATEB-SSBD(RE)                                             
         GOTO1 VDATCON,DMCB,(3,(R0)),(2,PRDATE)                                 
*                                                                               
         XC    PRKEY,PRKEY                                                      
         MVI   PRKEY,CTPRKTYQ                                                   
*                                                                               
         XC    P1(24),P1                                                        
         MVC   P1,ISREAD                                                        
         L     R8,ASTRTIOA         SET IOAREA ADDRESS                           
         ST    R8,P2                                                            
         MVC   P4,VCTFILE                                                       
         LA    RE,PRKEY                                                         
         ST    RE,P5               SET A(KEYARG)                                
         GOTO1 VISDDS,P1                                                        
*                                                                               
         USING CTPRREC,R8                                                       
         CLI   CTPRKTYP,CTPRKTYQ   MAKE SURE FOUND PROVER RECORD                
         BE    REPR04                                                           
         ABEND 334,DUMP                                                         
*                                                                               
REPR02   MVC   P1,ISRDSEQ                                                       
         GOTO1 VISDDS,P1           READ NEXT RECORD                             
         OC    P3(2),P3                                                         
         BZ    REPR04                                                           
         BRAS  RE,IOERROR                                                       
         ABEND 335,DUMP                                                         
*                                                                               
REPR04   CLI   CTPRKTYP,CTPRKTYQ                                                
         BNE   REPRX                                                            
         TM    CTPRSTAT,X'80'      TEST DELETED                                 
         BO    REPR02                                                           
         CLI   CTPRKPTY,CTPRKPFQ   TEST FACPAK PROGRAM TYPE                     
         BNE   REPR02                                                           
*                                                                               
         L     R1,VSELIST                                                       
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING SELISTD,R1                                                       
*                                                                               
REPR06   CLC   SEOVSYS,CTPRKSYS    MATCH ON OVERLAY SYSTEM NUMBER               
         BE    REPR08                                                           
         BXLE  R1,RE,REPR06                                                     
         B     REPR02                                                           
*                                                                               
REPR08   L     R5,SEPGMS                                                        
         LH    RE,0(R5)                                                         
         L     RF,2(R5)                                                         
         LA    R5,6(R5)                                                         
         USING PGMLSTD,R5          R5=A(PGMLST FOR SYSTEM)                      
*                                                                               
REPR10   CLC   PGMNUM,CTPRKPRG     MATCH ON PROGRAM NUMBER                      
         BE    REPR14                                                           
*                                                                               
REPR12   BXLE  R5,RE,REPR10                                                     
         B     REPR02                                                           
*                                                                               
REPR14   XC    PGMVINFO,PGMVINFO                                                
         LA    R6,CTPRDATA         POINT TO FIRST ELEMENT                       
*                                                                               
REPR16   CLI   0(R6),0                                                          
         BE    REPR12                                                           
         CLI   0(R6),CTPRVELQ                                                   
         BE    REPR20                                                           
         CLI   0(R6),GMSGELC                                                    
         BE    REPR22                                                           
         B     REPR18                                                           
*                                                                               
REPR18   LLC   R0,1(R6)                                                         
         AR    R6,R0                                                            
         B     REPR16                                                           
*                                                                               
         USING CTPRVD,R6                                                        
REPR20   MVC   PGMVVER,CTPRVVER                                                 
         MVC   PGMVLVL,CTPRVLEV                                                 
         MVC   PGMVPQ1,CTPRVPQ1                                                 
         MVC   PGMVPQ2,CTPRVPQ2                                                 
         CLC   PRDATE,CTPRVDFR                                                  
         BL    *+18                                                             
         CLC   PRDATE,CTPRVDTO                                                  
         BH    *+8                                                              
         OI    PGMVFLG,PGMVDATE                                                 
         B     REPR18                                                           
*                                                                               
         USING GMSGEL,R6                                                        
REPR22   OI    PGMVFLG,PGMVHEAD                                                 
         B     REPR18                                                           
*                                                                               
REPRX    J     EXIT                                                             
         DROP  R1,R5,R6,R8                                                      
         EJECT                                                                  
***********************************************************************         
* PUT A MESSAGE TO THE OPERATOR CONSOLE AND OPTIONALLY GET A REPLY.   *         
* ON ENTRY R1=MESSAGE NUMBER AND XTRAMESS CAN OPTIONALLY CONTAIN 8    *         
* CHARACTERS TO BE INSERTED INTO MESSAGE.                             *         
***********************************************************************         
PUTMESS  NTR1  BASE=*,LABEL=*                                                   
         AHI   R1,-1                                                            
         BM    PUTMESS1            R1=ZERO MEANS R8=A(MESSAGE)                  
*                                                                               
         LA    RF,L'MESSTAB                                                     
         MR    RE,R1                                                            
         L     R8,=A(MESSTAB)                                                   
         AR    R8,RF               R8=A(MESSTAB ENTRY)                          
*                                                                               
PUTMESS1 GOTO1 ,PM1,VWCTYPE,MESSAGE,60                                          
*                                                                               
PUTMESS2 MVC   MESSAGE,SPACES      BUILD MESSAGE                                
         MVC   MESSAGE(8),=C'*FACPAK*'                                          
         MVC   MESSAGE+4(3),FAC3CHR                                             
         MVC   MESSAGE+9(L'MESSTAB-1),1(R8)                                     
         LA    R1,MESSAGE+9                                                     
         LA    R0,L'MESSTAB-8                                                   
         CLC   0(8,R1),=8C'X'      REPLACE SPECIAL CHARACTERS                   
         BE    *+16                                                             
         LA    R1,1(R1)                                                         
         BCT   R0,*-14                                                          
         B     *+10                                                             
         MVC   0(8,R1),XTRAMESS                                                 
         LA    R2,DMCB                                                          
*                                                                               
         GOTO1 VDMOD000,PM1                                                     
         CLI   0(R8),C'X'                                                       
         BE    PUTMESSX                                                         
         TM    0(R8),X'F0'                                                      
         BO    *+12                                                             
         LA    R8,L'MESSTAB(R8)    BUMP MSG PTR FOR MULTIPLE MESSAGES           
         B     PUTMESS2                                                         
*                                                                               
         MVC   PM1(1),0(R8)        REQUEST A REPLY FROM THE OPERATOR            
         NI    PM1,X'0F'                                                        
         LLC   R0,PM1                                                           
         GOTO1 (RF),(R1),VRCTYPE,REPLY,(R0)                                     
*                                                                               
         CLC   REPLY(4),=C'DUMP'   CHECK FOR DUMP REPLY                         
         BNE   PUTMESS3                                                         
         ABEND 336,DUMP                                                         
*                                                                               
PUTMESS3 CLC   REPLY(3),=C'EOJ'    CHECK FOR EOJ REPLY                          
         BNE   PUTMESSX                                                         
         BRAS  RE,STRTEOJ                                                       
*                                                                               
PUTMESSX J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* REBUILD TEMPEST COUNTER FROM CHECKPOINT AREA                        *         
***********************************************************************         
RESTMPS  NTR1  BASE=*,LABEL=*                                                   
         LAM   AR0,ARF,ARZERO                                                   
*                                                                               
         L     R2,VSSB             REQUIRE SSB                                  
         USING SSBD,R2                                                          
         XR    R0,R0                                                            
         ICM   R0,1,SSBSYSID       MUST HAVE SYSTEM ID                          
         N     R0,=X'0000000F'                                                  
*                                                                               
         LAM   AR2,AR2,SSBTBLET                                                 
         XR    R2,R2                                                            
         USING FATABSD,R2                                                       
         SAC   512                                                              
         ICM   R2,15,TABSTMS       GET A(TEMPEST BLOCK)                         
         USING TTMSHDRD,R2                                                      
         ICM   RE,15,TTMSNTRY      GET MAX DSPACE ALLOCATIONS ALLOWED           
         SRL   RE,5                ROUND DOWN BY 32 MULTIPLE (FULLWORD)         
         SLL   RE,5                                                             
         SRL   RE,3                NUMBER OF BYTES REQUIRED                     
         AHI   RE,TTSSHDRL                                                      
         ST    RE,FULL             SAVE LENGTH OF ENTRY                         
         SRDL  RE,32                                                            
         MR    RE,R0               INDEX INTO BLOCK BY FACPAK ID                
*                                                                               
         AHI   R2,TTMSHDRL         GO TO START OF HEADERS                       
         AR    R2,RF               ADD INDEX VALUE                              
         USING TTSSHDRD,R2                                                      
         L     RE,VCHKPT2                                                       
         AHI   RE,8                                                             
         L     R3,FULL                                                          
         L     RF,FULL                                                          
         MVCL  R2,RE                                                            
*                                                                               
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         J     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* REBUILD REQUEST ADDRESS LIST FROM A SYSTEM REQUEST FILE.            *         
* R3=A(SELIST ENTRY)                                                  *         
***********************************************************************         
         USING SELISTD,R3          R3=A(SELIST ENTRY)                           
RBLDRL   NTR1  BASE=*,LABEL=*                                                   
         STAR  CLEAR=Y,ARS=OFF                                                  
*                                                                               
         L     RF,VSSB                                                          
         LAM   AR5,AR5,SSBALET-SSBD(RF)                                         
         LLC   R5,SESYS            INDEX INTO DATASPACE                         
         SLL   R5,6                                                             
         SAC   512                                                              
*                                                                               
         ICM   R5,15,DSPECB-DMSPACED(R5)                                        
         LA    R5,0(,R5)           CLEAR HOB                                    
         ICM   R5,15,DSYAREQT-DMSYSHDR(R5)                                      
         STCM  R5,7,RBTHIS+1                                                    
         ICM   R5,15,RBTHIS                                                     
         LA    R5,6(,R5)           00 ENTRY - USED ONLY IF ALL FULL             
*                                                                               
         MVC   P1,VFINDSYS                                                      
         MVC   P2(1),SESYS                                                      
         L     R2,DMCB                                                          
         GOTO1 VDMOD000,P1         FIND FILE LIST FOR SYSTEM                    
         SAFE  CLEAR=ARZERO                                                     
*                                                                               
RBLDRL2  L     R7,P2               SEARCH FILE LIST FOR REQUEST FILE            
         USING SYSFLSTD,R7                                                      
         LLH   RF,SYSF#FLS                                                      
         LA    R7,SYSFLIST                                                      
*                                                                               
         TM    SYSFIND1,SFREQ      TEST REQUEST FILE                            
         BO    RBLDRL4                                                          
         LA    R7,SYSFLNQ(,R7)                                                  
         BCT   RF,*-12                                                          
         B     RBLDRLX             EXIT IF NO REQUEST FILE                      
*                                                                               
RBLDRL4  TM    SYSFIND1,SFNOP      TEST FILE IS NO-OP                           
         BO    RBLDRLX                                                          
*                                                                               
         XR    RF,RF               CHECK IF FILE EMPTY                          
         ICM   RF,7,SYSFADTF                                                    
         USING DTFPHD,RF                                                        
*&&UK*&& TM    DTFOPEN,DTF_RO                                                   
*&&UK*&& BO    RBLDRLX             FILE IS READ-ONLY                            
         MVC   DISK1ST,=X'00010000'                                             
         TM    DTFTYPE,DTFTBIGF                                                 
         BZ    *+10                                                             
         MVC   DISK1ST,=X'00001000'                                             
         CLC   DISK1ST,DNEXT                                                    
         BNE   RBLDRL5             FILE NOT EMPTY                               
         DROP  RF                                                               
*                                                                               
         L     R5,RBTHIS           SEARCH REQ ADDR LIST FOR REQ ID              
         LH    R6,0(,R5)                                                        
         L     R7,2(,R5)                                                        
         AHI   R5,6                                                             
         XC    0(8,R5),0(R5)       CLEAR 00 ENTRY                               
         MVC   0(2,R5),=C'00'      AND REBUILD IT                               
         AHI   R5,8                                                             
*                                                                               
         XC    0(8,R5),0(R5)       CLEAR THIS ENTRY                             
         BXLE  R5,R6,*-6                                                        
         B     RBLDRLX                                                          
*                                                                               
RBLDRL5  L     R5,RBTHIS           FILE NOT EMPTY - REBUILD REQUEST LST         
*&&US                                                                           
         OC    6+8(8,R5),6+8(R5)   FIRST SLOT FILLED IN ON FIRST ADD            
         BNZ   RBLDRLX             REQUEST ADDRESS LIST IS INITIALISED          
*&&                                                                             
*&&UK                                                                           
         CLI   FATOR,C'Y'          ONLY REBUILD IF A TOR                        
         BNE   RBLDRLX             (NOT 100% SAFE IF OPEN ELSEWHERE)            
         L     RF,VSSB             BUT NOT IF UK ECHO SYSTEMS ADVA/B/C          
         USING SSBD,RF                                                          
         CLI   SSBSYSID,10                                                      
         BE    RBLDRLX                                                          
         CLI   SSBSYSID,12                                                      
         BE    RBLDRLX                                                          
         CLI   SSBSYSID,14                                                      
         BE    RBLDRLX                                                          
         DROP  RF                                                               
*                                                                               
         LH    RE,0(,R5)           START BY CLEARING TABLE                      
         L     RF,2(,R5)                                                        
         AHI   R5,6                                                             
         XC    0(8,R5),0(R5)       CLEAR 00 ENTRY                               
         MVC   0(2,R5),=C'00'      AND REBUILD IT                               
         AHI   R5,8                                                             
*                                                                               
         XC    0(8,R5),0(R5)       CLEAR REMAINING ENTRIES                      
         BXLE  R5,RE,*-6                                                        
*&&                                                                             
         XC    P1(24),P1           SET PLIST TO READ REQUEST FILE               
         MVC   P4+1(3),SYSFADTF    SET DTF ADDR                                 
         MVC   P1,VRDTRK                                                        
         L     RE,ASTRTBUF         USE START OF TRACK BUFF FOR REQ REC          
         ST    RE,P2                                                            
         LA    RE,2048(RE)         USE REST AS TRACK BUFF                       
         ST    RE,P6                                                            
         LA    RE,P7                                                            
         ST    RE,P5                                                            
         MVC   P7,DISK1ST                                                       
         DROP  R7                                                               
*&&US                                                                           
         L     R8,VSSB             SET DATE DUB=C'YYMMDD' FULL=P'YMD'           
         USING SSBD,R8                                                          
         GOTO1 VDATCON,DMCB,(3,SSBDATEB),(0,DUB)                                
         GOTO1 (RF),(R1),,(1,FULL)                                              
         GOTO1 (RF),(R1),,(19,FULL+3) JULIAN P'CYYDDD' AS WELL                  
         DROP  R8                                                               
*&&                                                                             
         XR    R0,R0               SET NOT FOUND SWITCH                         
         LA    R1,P1                                                            
                                                                                
RBLDRL6  LLC   RE,P7+2             BUMP RECORD NUMBER                           
         LA    RE,1(RE)                                                         
         STC   RE,P7+2                                                          
         L     RE,ASTRTBUF         USE START OF TRACK BUFF FOR REQ REC          
         ST    RE,P2                                                            
         LA    RE,2048(RE)         USE REST AS TRACK BUFF                       
         ST    RE,P6               DADDS CHANGES P6 IF PROTECTED MODE           
         GOTO1 VDADDS                                                           
         SAFE  CLEAR=ARZERO                                                     
         OC    P3(2),P3                                                         
         BNZ   RBLDRLA                                                          
         XR    R0,R0                                                            
*                                                                               
RBLDRL7  XR    RE,RE               TEST IF NEW OR OLD STYLE HEADER              
         LH    RF,P3+2                                                          
         D     RE,=F'80'                                                        
         LTR   RF,RE               RF=ZERO IF NEW STYLE HEADER                  
         BNZ   RBLDRL7B                                                         
*                                                                               
RBLDRL7A L     RE,P2               NEW STYLE 80 BYTE HEADER                     
*&&US*&& CLC   0(3,RE),FULL+3      JULIAN DATE FORMAT                           
*&&US*&& BNE   RBLDRL6                                                          
         LA    RE,54(RE)           POINT TO LAST 26 BYTES OF NEW HEADER         
         B     RBLDRL7C                                                         
*                                                                               
RBLDRL7B L     RE,P2               OLD STYLE 26 BYTE HEADER                     
*&&US                                                                           
         TM    20(RE),X'F0'        BYPASS WRONG DATED RECORDS                   
         BO    *+18                                                             
         CLC   20(3,RE),FULL       PACKED DATE FORMAT                           
         BNE   RBLDRL6                                                          
         B     *+14                                                             
         CLC   20(6,RE),DUB        EBCDIC DATE FORMAT                           
         BNE   RBLDRL6                                                          
*&&                                                                             
RBLDRL7C TM    15(RE),X'01'                                                     
         BZ    RBLDRL6             BYPASS UNLINKED RECS                         
         CLC   26(2,RE),=X'FFFF'                                                
         BE    RBLDRL6             BYPASS DUMMY RECS                            
         CLC   26(2,RE),=C'99'                                                  
         BE    RBLDRL6             BYPASS CANCELLED RECS                        
*                                                                               
         L     R5,RBTHIS           SEARCH REQ ADDR LIST FOR REQ ID              
         LH    R6,0(,R5)                                                        
         L     R7,2(,R5)                                                        
         LA    R5,6(,R5)                                                        
*                                                                               
RBLDRL7D OC    0(2,R5),0(R5)       TEST EMPTY SLOT                              
         BZ    RBLDRL8                                                          
         CLC   26(2,RE),0(R5)      TEST RECORD MATCHES ENTRY                    
         BE    RBLDRL8                                                          
         BXLE  R5,R6,RBLDRL7D                                                   
         B     RBLDRL6             BYPASS IF TABLE FULL                         
*                                                                               
RBLDRL8  MVC   0(2,R5),26(RE)      SET CODE - IN CASE ITS BLANK                 
         OC    2(3,R5),2(R5)                                                    
         BNZ   *+10                                                             
         MVC   2(3,R5),P7          SET FIRST                                    
         MVC   5(3,R5),P7          SET LAST                                     
         B     RBLDRL6                                                          
*                                                                               
RBLDRLA  TM    P3+1,X'08'          TEST FOR NOT FOUND                           
         BZ    RBLDRLX             NO-EXIT ON ERROR                             
         LTR   R0,R0                                                            
         BNZ   RBLDRLX             YES-EXIT IF SECOND NOT FOUND                 
         LA    R0,1                                                             
         LH    RE,P7               BUMP TO NEXT TRACK                           
         LA    RE,1(RE)                                                         
         SLL   RE,16                                                            
         ST    RE,P7                                                            
         B     RBLDRL6                                                          
*                                                                               
RBLDRLX  REAR  ARS=OFF                                                          
         J     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* FIND LATEST DUMP IN THIS FACPAK AND SET IN DATASPACE STRIPE         *         
***********************************************************************         
DMPSET   NTR1  BASE=*,LABEL=*                                                   
         STAR  CLEAR=Y,ARS=ON      SET LATEST DUMP FOR THIS FACPAK              
         L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         OC    SSBTBLET,SSBTBLET   TEST TABS DSPACE DEFINED                     
         BZ    DMPSET16                                                         
*                                                                               
         LAM   AR2,AR2,SSBTBLET                                                 
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,TABSDUMP-FATABSD(R2)                                       
         USING TORDUMPD,R2                                                      
         LA    R2,TDLENQ(,R2)      FIRST SLOT UNUSED                            
*                                                                               
DMPSET02 OC    TDSYSNA,TDSYSNA     TEST END OF ARRAY                            
         BZ    DMPSET04                                                         
         CLC   TDSYSNA,SSBSYSN4    MATCH OUR FACPAK                             
         BE    DMPSET06                                                         
         LA    R2,TDLENQ(,R2)                                                   
         B     DMPSET02                                                         
*                                                                               
DMPSET04 XR    R2,R2               USE FIRST ENTRY IF NOT DEFINED               
         SAC   512                                                              
         ICM   R2,15,TABSDUMP-FATABSD(R2)                                       
         USING TORDUMPD,R2                                                      
         LA    R2,TDLENQ(,R2)      FIRST SLOT UNUSED                            
*                                                                               
DMPSET06 ICM   RF,15,TDDUMPFS      FIRST DUMP NUMBER FOR THIS FACPAK            
         XC    TDPSW,TDPSW                                                      
         XC    TDREGB,TDREGB       RESET STUFF FOR DUPES                        
         XC    TDMODID,TDMODID                                                  
         STCM  RF,15,TDDUMPNO      SET CURRENT TO FIRST                         
         XC    DUB,DUB                                                          
*                                                                               
DMPSET08 ICM   R0,15,TDDUMPNO      GET CURRENT NUMBER                           
         BCTR  R0,0                                                             
         LLC   R1,SSBDMPCY         CALCULATE START ADDR                         
         MR    R0,R0                                                            
         M     R0,DMPCYLS                                                       
         LA    R1,1(R1)                                                         
         SLA   R1,16                                                            
         ST    R1,FULL             SAVE DA IN FULL                              
         MVI   FULL+2,1                                                         
         LA    R1,FULL                                                          
         ST    R1,P5                                                            
         L     R1,ASTRTBUF                                                      
         ST    R1,P2                                                            
         GOTO1 VDADDS,P1,VRDID     READ DUMP HEADER                             
         SAFE  CLEAR=Y                                                          
         OC    P3(2),P3                                                         
         BNZ   DMPSET12                                                         
*                                                                               
         L     RE,VSSB                                                          
         L     R1,P2                                                            
         CLC   16(4,R1),=C'+FAC'                                                
         BNE   DMPSET12                                                         
         CLC   04(4,R1),DUB+0      CHECK DATE TIME                              
         BL    DMPSET10                                                         
         MVC   DUB+4(4),TDDUMPNO   SAVE NUMBER / DATE TIME                      
         MVC   DUB+0(4),4(R1)                                                   
*                                                                               
DMPSET10 ICM   R1,15,TDDUMPNO      MOVE ON TO NEXT DUMP                         
         LA    R1,1(R1)                                                         
         STCM  R1,15,TDDUMPNO                                                   
         CLC   TDDUMPNO,TDDUMPMX                                                
         BNH   DMPSET08                                                         
         MVC   TDDUMPNO,DUB+4                                                   
         B     DMPSET14                                                         
*                                                                               
DMPSET12 ICM   R1,15,TDDUMPNO      DISK ERROR. BACK UP 1 DUMP                   
         BCTR  R1,0                                                             
         STCM  R1,15,TDDUMPNO                                                   
         CLC   TDDUMPNO,TDDUMPFS   SET TO MAX IF BACKED TOO FAR                 
         BNL   *+10                                                             
         MVC   TDDUMPNO,TDDUMPMX                                                
*                                                                               
DMPSET14 L     RE,VSSB                                                          
         USING SSBD,RE                                                          
         ICM   R0,15,TDDUMPNO                                                   
         STC   R0,SSBDMPNO                                                      
         ICM   R0,15,TDDUMPMX                                                   
         STC   R0,SSBDMPMX                                                      
*                                                                               
DMPSET16 REAR  ARS=OFF                                                          
         J     EXIT                                                             
         DROP  R2,RE                                                            
         EJECT                                                                  
***********************************************************************         
* PERFORM FILE RENAMES                                                *         
***********************************************************************         
RNMFIL   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,35                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         GOTO1 VDATAMGR,DMCB,DMREAD,SYSFLES,1                                   
         MVC   APHLIST,12(R1)      SAVE A(SYSFLES LISTS)                        
*                                                                               
         SR    R8,R8               R8=A(FILE RENAME LIST)                       
         ICM   R8,7,ADMFLST                                                     
         BZ    RNMFILX                                                          
         XC    DUB,DUB                                                          
                                                                                
***********************************************************************         
* FLAG ALL FILES AS READ-ONLY IN DTF                                  *         
***********************************************************************         
         L     R1,APHLIST                                                       
         USING SYSFLSTD,R1                                                      
RNFIL02  LLC   R0,SYSF#FLS+1       R0=NUM OF FILES                              
         LA    R1,SYSFLIST         R1=A(FILE ENTRY)                             
*                                                                               
RNFIL03  L     RF,SYSFADTF-1       RF=A(DTF)                                    
         USING ISDTF,RF                                                         
         TM    ISFOPEN,ISFORO      TEST DEFINED AS READ-ONLY                    
         BZ    *+8                                                              
         OI    SYSFIND2,SF_RO                                                   
         CLI   SEOPNOP,C'+'        IF STARTING ALL THEN DONT SET R/O            
         BE    *+8                                                              
         OI    ISFOPEN,ISFORO      SET READ-ONLY BIT                            
         AHI   R1,SYSFLNQ                                                       
         BCT   R0,RNFIL03          BUMP TO NEXT FILE                            
*                                                                               
         CLI   0(R1),X'FF'         TEST END OF SYSLIST                          
         BNE   RNFIL02                                                          
*                                                                               
RNFIL05  CLI   WDMFLST,20          TEST IF NEW 20 BYTE ENTRIES                  
         BE    RNFIL26                                                          
*&&US*&& DC    H'0'                                                             
                                                                                
***********************************************************************         
* CHECK FOR NOP OPTION AND/OR EMULATION                               *         
***********************************************************************         
RNFIL06  L     R1,APHLIST          SEARCH SYSFLES FOR FILE NAME                 
         XR    R0,R0                                                            
         MVI   DUB+1,0             SET FILE NOT FOUND                           
*                                                                               
RNFIL08  ST    R1,DUB+4            SAVE A(SYSFLES SYSTEM HEADER)                
         MVI   DUB+2,0             SET FLAGS FOR THIS SYSTEM                    
         LLC   R0,SYSF#FLS+1                                                    
         LA    R1,SYSFLIST                                                      
RNFIL10  L     RF,SYSFADTF-1       RF=A(DTF)                                    
         CLC   ISFFID,0(R8)        COMPARE FILE NAMES                           
         BNE   RNFIL18                                                          
         CLC   8(3,R8),=C'NOP'                                                  
         BNE   RNFIL12                                                          
         OI    ISFOPEN,ISFONOP     SET NOP BIT IN DTF                           
         OI    SYSFIND1,SFNOP      SET NOP BIT IN SYSFLES                       
         CLI   15(R8),C'E'         TEST EMULATION REQUIRED                      
         BNE   *+8                                                              
         OI    ISFTYPE,ISFTEMU+ISFTNOD                                          
         B     RNFIL16                                                          
                                                                                
***********************************************************************         
* TEST FOR READ-ONLY FILE ASTERISK                                    *         
***********************************************************************         
RNFIL12  MVC   ISFFID,8(R8)        CHANGE FILE NAME                             
         OI    ISFOPEN,ISFORO      INSURE READ-ONLY BIT IS ON IN DTF            
         CLI   15(R8),C'*'         TEST READ-ONLY FILE                          
         BE    RNFIL13                                                          
*&&UK*&& B     RNFIL12C                                                         
                                                                                
***********************************************************************         
* FIX FOR REP SYSTEM READ/WRITE ON SUNDAY                             *         
***********************************************************************         
*&&US                                                                           
         CLI   OPTRONLY,C'Y'       TEST FORCE READ-ONLY                         
         BNE   RNFIL12C                                                         
         LLC   RE,SYSFILE#         SERVICE FILES ARE EXEMPT                     
         SLL   RE,5                INDEX INTO 32-BYTE FILE TABLE                
         A     RE,=V(FILTABL)                                                   
         TM    DMFLSTYP-FILTABD(RE),DMFLSRV                                     
         BO    RNFIL12C                                                         
*                                                                               
RNFIL12A MVC   HALF(1),SYSFILE#    CONTROL FILES EXEMPT ALSO                    
         NI    HALF,X'F0'                                                       
         CLI   HALF,X'10'                                                       
         BE    RNFIL12C                                                         
         CLI   HALF,X'A0'                                                       
         BE    RNFIL12C                                                         
         B     RNFIL13                                                          
*&&                                                                             
RNFIL12C NI    ISFOPEN,255-ISFORO  TURN OFF READ-ONLY BIT IN DTF                
*                                                                               
RNFIL13  CLI   15(R8),C'E'         TEST EMULATION REQUIRED                      
         BNE   *+8                                                              
         OI    ISFTYPE,ISFTEMU                                                  
         CLI   15(R8),C'G'         TEST GLOBAL FILE                             
         BNE   RNFIL14                                                          
         TM    ISFFLAG,ISFGLOB                                                  
         BO    *+8                                                              
         OI    DUB+2,SYSFSGBL      SET LOCAL FILE SET TO GLOBAL                 
         OI    ISFFLAG,ISFGLOB     TURN ON GLOBAL FLAG                          
         NI    ISFOPEN,255-ISFOENQ TURN OFF ENQ FLAG                            
         TM    SYSFIND1,SFRCV                                                   
         BZ    *+8                                                              
         OI    DIND-DTFPHD(RF),DINDVRB SET VERMONT IF GLOBAL RCVR FILE          
*                                                                               
RNFIL14  CLI   15(R8),C'L'         TEST LOCAL FILE                              
         BNE   *+8                                                              
         NI    ISFFLAG,255-ISFGLOB                                              
         CLI   15(R8),C'V'         TEST VERMONT                                 
         BNE   *+8                                                              
         OI    DIND-DTFPHD(RF),DINDVRB                                          
         CLI   15(R8),C'U'         TEST UNVERMONT                               
         BNE   *+8                                                              
         NI    DIND-DTFPHD(RF),255-DINDVRB                                      
*                                                                               
RNFIL16  MVI   DUB+1,1             SET FILE FOUND                               
*                                                                               
RNFIL18  LA    R1,SYSFLNQ(R1)                                                   
         BCT   R0,RNFIL10                                                       
         CLI   0(R1),X'FF'         TEST END OF SYSFLES                          
         BE    RNFIL19                                                          
*                                                                               
         CLI   DUB+2,0             ANY FLAGS SET FOR THIS SYSTEM                
         BE    RNFIL08                                                          
*                                                                               
C        USING SYSFLSTD,RE         CURRENT SYSTEM HEADER                        
         L     RE,DUB+4            RE=A(SYSFLES HEADER FOR SYSTEM)              
         TM    DUB+2,SYSFSGBL                                                   
         BZ    *+8                                                              
         OI    C.SYSFSTAT,SYSFSGBL SET ONLINE LOCAL FILES SET GLOBAL            
         B     RNFIL08                                                          
         DROP  C                                                                
*                                                                               
RNFIL19  CLI   DUB+1,0                                                          
         BNE   RNFIL20                                                          
         ABEND 337,DUMP            FILE NOT FOUND                               
*                                                                               
RNFIL20  LA    R8,16(R8)           BUMP TO NEXT FILE LIST ENTRY                 
         CLI   0(R8),255           EXIT IF END OF LIST                          
         BNE   RNFIL06                                                          
         B     RNFIL50                                                          
*                                                                               
RNFIL26  L     R1,APHLIST          SEARCH SYSFLES FOR FILE NAME                 
         XR    R0,R0                                                            
         MVI   DUB+1,0             SET FILE NOT FOUND                           
*                                                                               
RNFIL28  ST    R1,DUB+4            SAVE A(SYSFLES SYSTEM HEADER)                
         MVI   DUB+2,0                                                          
         LLC   R0,SYSF#FLS+1       R0=NUM OF FILES                              
         LA    R1,SYSFLIST         R1=A(FILE ENTRY)                             
*                                                                               
RNFIL30  L     RF,SYSFADTF-1       RF=A(DTF)                                    
         USING ISDTF,RF                                                         
         CLC   ISFDD,0(R8)         MATCH ON FILE NAME                           
         BNE   RNFIL38                                                          
         CLC   10(3,R8),=C'NOP'    TEST NOP FILE                                
         BNE   RNFIL32                                                          
         OI    ISFOPEN,ISFONOP     SET NOP BIT IN DTF                           
         OI    SYSFIND1,SFNOP      SET NOP BIT IN SYSFLES                       
         CLI   19(R8),C'E'         TEST EMULATION REQUIRED                      
         BNE   *+8                                                              
         OI    ISFTYPE,ISFTEMU+ISFTNOD                                          
         B     RNFIL36                                                          
*                                                                               
RNFIL32  MVC   ISFDD,10(R8)        SET FILE NAME                                
         OI    ISFOPEN,ISFORO      SET READ-ONLY BIT ON IN DTF                  
         CLI   19(R8),C'*'         TEST READ-ONLY FILE                          
         BE    RNFIL33                                                          
         TM    SYSFIND2,SF_RO      READ-ONLY FILE BASED ON SYSLIST              
         BO    RNFIL33             REGARDLESS OF DMFLIST SET READ-ONLY          
*&&UK*&& B     RNFIL32C                                                         
                                                                                
***********************************************************************         
* FIX FOR REP SYSTEM READ/WRITE ON SUNDAY                             *         
***********************************************************************         
*&&US                                                                           
         CLI   OPTRONLY,C'Y'       TEST FORCE READ-ONLY                         
         BNE   RNFIL32C                                                         
         LLC   RE,SYSFILE#         SERVICE FILES ARE EXEMPT                     
         SLL   RE,5                INDEX INTO 32-BYTE FILE TABLE                
         A     RE,=V(FILTABL)                                                   
         TM    DMFLSTYP-FILTABD(RE),DMFLSRV                                     
         BO    RNFIL32C                                                         
*                                                                               
RNFIL32A MVC   HALF(1),SYSFILE#    CONTROL FILES EXEMPT ALSO                    
         NI    HALF,X'F0'                                                       
         CLI   HALF,X'10'                                                       
         BE    RNFIL32C                                                         
         CLI   HALF,X'A0'                                                       
         BE    RNFIL32C                                                         
         B     RNFIL33                                                          
*&&                                                                             
RNFIL32C NI    ISFOPEN,255-ISFORO  TURN OFF READ-ONLY BIT IN DTF                
*                                                                               
RNFIL33  CLI   19(R8),C'E'         TEST EMULATION REQUIRED                      
         BNE   *+8                                                              
         OI    ISFTYPE,ISFTEMU                                                  
         CLI   19(R8),C'G'         TEST GLOBAL FILE (DATA SPACE)                
         BNE   RNFIL34                                                          
         TM    ISFFLAG,ISFGLOB                                                  
         BO    *+8                                                              
         OI    DUB+2,SYSFSGBL      SET LOCAL FILE SET TO GLOBAL                 
         OI    ISFFLAG,ISFGLOB     TURN ON GLOBAL FLAG                          
         NI    ISFOPEN,255-ISFOENQ TURN OFF ENQ FLAG                            
                                                                                
         TM    SYSFIND1,SFRCV      TEST RECOVERY FILE                           
         BZ    *+8                                                              
         OI    DIND-DTFPHD(RF),DINDVRB SET VERMONT IF GLOBAL RCVR FILE          
*                                                                               
RNFIL34  CLI   19(R8),C'L'         TEST LOCAL FILE                              
         BNE   *+8                                                              
         NI    ISFFLAG,255-ISFGLOB                                              
         CLI   19(R8),C'V'         TEST VERMONT                                 
         BNE   *+8                                                              
         OI    DIND-DTFPHD(RF),DINDVRB                                          
         CLI   19(R8),C'U'         TEST UNVERMONT                               
         BNE   *+8                                                              
         NI    DIND-DTFPHD(RF),255-DINDVRB                                      
*                                                                               
RNFIL36  MVI   DUB+1,1             SET FILE FOUND                               
*                                                                               
RNFIL38  LA    R1,SYSFLNQ(R1)      NEXT FILE                                    
         BCT   R0,RNFIL30                                                       
*                                                                               
         CLI   0(R1),X'FF'         TEST END OF SYSFLES                          
         BE    RNFIL39                                                          
         CLI   DUB+2,0             TEST ANY FLAGS SET FOR THIS SYSTEM           
         BE    RNFIL28                                                          
*                                                                               
C        USING SYSFLSTD,RE         CURRENT SYSTEM HEADER                        
         L     RE,DUB+4            RE=A(SYSFLES HEADER FOR SYSTEM)              
         TM    DUB+2,SYSFSGBL                                                   
         BZ    *+8                                                              
         OI    C.SYSFSTAT,SYSFSGBL SET ONLINE LOCAL FILES SET GLOBAL            
         B     RNFIL28                                                          
         DROP  C                                                                
*                                                                               
RNFIL39  CLI   DUB+1,0                                                          
         BNE   RNFIL40                                                          
         ABEND 337,DUMP            FILE NOT FOUND                               
*                                                                               
RNFIL40  LA    R8,20(R8)           BUMP TO NEXT FILE LIST ENTRY                 
         CLI   0(R8),255           EXIT IF END OF LIST                          
         BNE   RNFIL26                                                          
*                                                                               
RNFIL50  L     R5,VSELIST          TEST FOR READ-ONLY SYSTEMS                   
         BRAS  RE,SETBXLE                                                       
         USING SELISTD,R5                                                       
*                                                                               
RNFIL52  OC    SEFILES,SEFILES     CHECK NO FILES FOR SYSTEM                    
         BZ    RNFIL64                                                          
         L     R1,APHLIST          SEARCH SYSFLES FOR SYSTEM                    
RNFIL54  LLC   RE,SYSF#FLS+1       RE=NUM OF FILES                              
         CLC   SESYS,SYSFSYS#      MATCH ON SE NUMBER                           
         BE    RNFIL56                                                          
         MHI   RE,SYSFLNQ                                                       
         LA    R1,SYSFLIST(RE)                                                  
         CLI   0(R1),X'FF'                                                      
         BNE   RNFIL54                                                          
         ABEND 338,DUMP            FILE NOT FOUND                               
RNFIL56  LA    R1,SYSFLIST         R1=A(FILE ENTRY)                             
*                                                                               
RNFIL58  CLI   SESYS,X'0A'         IGNORE CONTROL SYSTEM FILES                  
         BE    RNFIL60                                                          
         CLI   SYSFILE#,X'A0'                                                   
         BL    RNFIL60                                                          
         CLI   SYSFILE#,X'AF'                                                   
         BNH   RNFIL62                                                          
*                                                                               
RNFIL60  L     RF,SYSFADTF-1       RF=A(DTF)                                    
         TM    ISFOPEN,ISFORO      TEST READ-ONLY                               
         BZ    RNFIL64             NO-SO GET NEXT SYSTEM                        
*                                                                               
RNFIL62  LA    R1,SYSFLNQ(R1)      NEXT FILE                                    
         BCT   RE,RNFIL58                                                       
*                                                                               
         OI    SEIND,SEIRONLY      SET SYSTEM READ-ONLY IN SELIST               
*                                                                               
RNFIL64  DS    0H                                                               
*NOP*    CLI   SESYS,X'01'         EXCEPT FOR SERVICE AND CONTROL               
*NOP*    BE    RNFIL64A                                                         
*NOP*    CLI   SESYS,X'0A'                                                      
*NOP*    BE    RNFIL64A                                                         
*NOP*    L     RE,VSSB                                                          
*NOP*    CLI   SSBDAYNO-SSBD(RE),7 SUNDAYS ARE READ-ONLY DAYS                   
*NOP*    BNE   RNFIL64A                                                         
*NOP*    OI    SEIND,SEISETRO      SET SYSTEM READ-ONLY IN SELIST               
*                                                                               
*&&US*&& CLI   OPTRONLY,C'Y'                                                    
*&&US*&& BNE   *+8                                                              
*&&US*&& OI    SEIND,SEISETRO      SET SYSTEM READ-ONLY IN SELIST               
*                                                                               
RNFIL64A BXLE  R5,R6,RNFIL52       BUMP TO NEXT SYSTEM                          
         DROP  R1                                                               
*                                                                               
RNMFILX  LA    R1,36                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         DROP  R5,RF                                                            
         EJECT                                                                  
***********************************************************************         
* REBUILD PRQ ENTRY FROM PRINTER CHECKPOINT RECORD.                   *         
* R1 POINTS TO CHECKPOINT RECORD                                      *         
* R5 POINTS TO UTL ENTRY                                              *         
***********************************************************************         
         USING UTLD,R5             R5=A(UTL ENTRY)                              
PRQRSTR  NTR1  BASE=*,LABEL=*                                                   
         CLI   FATOR,C'Y'          TEST TOR                                     
         BNE   PRQRX                                                            
         TM    TTYPE,TTYPETWX      NO RESTART FOR TWX PRINTER TRMS              
         BO    PRQRX                                                            
         CLC   24(2,R1),TNUM       EXIT IF NOT VALID CHKPNT                     
         BNE   PRQRX                                                            
*                                                                               
         LA    R6,64(R1)           R6=A(PRQ ENTRY IN CHKPNT RECORD)             
         USING PRQD,R6                                                          
         LLC   R0,PRQNE                                                         
         LR    R3,R0               R3=NUMBER OF ENTRIES                         
         XC    PNLAST,PNLAST                                                    
         XC    PNNEXT,PNNEXT                                                    
         CHI   R3,1                                                             
         BH    PRQR1C                                                           
         BE    PRQR1B                                                           
*                                                                               
PRQR1A   XR    R3,R3               EMPTY QUEUE                                  
         XC    PNTRY,PNTRY                                                      
         B     PRQR4                                                            
*                                                                               
PRQR1B   OC    PNTRY(8),PNTRY      SINGLE ENTRY QUEUE                           
         BZ    PRQR1A                                                           
         B     PRQR4                                                            
*                                                                               
PRQR1C   OC    PNTRY(8),PNTRY      MULTIPLE ENTRY QUEUE                         
         BZ    PRQR1A                                                           
         LA    R3,1                                                             
         BCTR  R0,0                                                             
         LA    R7,PNTRY+L'PNTRY    R7=A(FIRST POOL ENTRY)                       
*                                                                               
PRQR2    OC    0(8,R7),0(R7)       TEST IF VALID ENTRY                          
         BZ    PRQR3                                                            
         MVC   FULL(2),PNLAST      SAVE PREVIOUS LAST ENTRY NUM                 
         XC    PNNEXT-PNTRY(2,R7),PNNEXT-PNTRY(R7)                              
         GOTO1 VLCM,DMCB,VTADDPRQ,(R7)                                          
         ICM   RF,15,8(R1)         RF=NEW ENTRY NUM                             
         BZ    PRQR3                                                            
         SR    RE,RE                                                            
         ICM   RE,3,FULL           GET PREVIOUS ENTRY                           
         BNZ   PRQR2A                                                           
         LR    RE,R7                                                            
         AHI   RE,-(L'PNTRY)       PREVIOUS ENTRY WAS FIRST                     
         B     PRQR2B                                                           
*                                                                               
PRQR2A   BCTR  RE,0                                                             
         MHI   RE,L'PNTRY                                                       
         LA    RE,6(RE)                                                         
         A     RE,VPRQENTS                                                      
*                                                                               
PRQR2B   STH   RF,PNNEXT-PNTRY(RE) CHAIN LAST ENTRY TO THIS ENTRY               
         STH   RF,PNLAST           SET NEW LAST ENTRY                           
         LA    R3,1(R3)            BUMP NUM OF ENTRIES                          
         LA    R7,L'PNTRY(R7)                                                   
         BCT   R0,PRQR2            BACK FOR NEXT POOL ENTRY                     
         B     PRQR4                                                            
*                                                                               
PRQR3    EQU   *                   INVALID ENTRY FOUND                          
*                                                                               
         DROP  R6                                                               
PRQR4    L     R7,TXPRNT           R7=A(PRQ ENTRY)                              
         USING PRQD,R7                                                          
         STC   R3,PRQNE            SET NUMBER OF ENTRIES                        
         MVC   PNLAST,PNLAST-PRHDR(R6)                                          
         MVC   PNTRY,PNTRY-PRHDR(R6)                                            
         MVC   PRHDRSV,PRHDRSV-PRHDR(R6)                                        
         MVC   PRQMODE,PRQMODE-PRHDR(R6)                                        
*                                                                               
PRQR5    CLI   PRSTAT-PRHDR(R6),0  TEST IF ACTIVE PRINTING A REPORT             
         BE    PRQRX                                                            
         OC    PRCIADDR-PRHDR(4,R6),PRCIADDR-PRHDR(R6)                          
         BZ    PRQRX                                                            
         MVI   PRSTAT1,PRS1ARS     SET AUTO RESTART POSSIBLE                    
         MVC   PR1CIFST,PR1CIFST-PRHDR(R6)                                      
         MVC   PR1KEY,PR1KEY-PRHDR(R6)                                          
         MVC   PRPRTQA,PRPRTQA-PRHDR(R6)                                        
*                                                                               
PRQRX    J     EXIT                                                             
         DROP  R5,R7                                                            
         EJECT                                                                  
***********************************************************************         
* READ TWA'S TO REBUILD UTL/SSB/SELIST/TCB AND PRINTER QUEUES         *         
* TWA'S WILL BE READ INTO HICORE - LAST CHECKPOINT TAKEN WILL BE      *         
* SAVED AT HICORE+LENTWA FOR LENGTH OF CHKPTLEN                       *         
* TWA'S DATE/TIME MUST BE GE STRDATE TO QUALIFY FOR RESTART           *         
***********************************************************************         
READTWA  NTR1  BASE=*,LABEL=*                                                   
         LH    RF,LENTWA           CLEAR HICORE FOR L'TWA+L'CHKPT               
         AH    RF,LENCHK                                                        
         L     RE,HICORE                                                        
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,HICORE           SET I/O AREA ADDRESSES                       
         AH    RE,STRCHK                                                        
         ST    RE,ACHKPT1          SAVE A(TWA CHECKPOINT)                       
         L     RE,HICORE                                                        
         AH    RE,LENTWA                                                        
         ST    RE,ACHKPT2          SAVE A(HIGHEST CHECKPOINT)                   
*                                                                               
         MVC   P6(2),=C'L='        INITIALISE DMCB FOR TEMPSTR READS            
         MVC   P6+2(2),LENTWA                                                   
         GOTO1 ,P1,DMREAD,TEMPSTR,0,HICORE                                      
*                                                                               
RTWA02   L     R5,VUTL             SET UTL TO READ TEMPSTR                      
         USING UTLD,R5                                                          
         SAM31                                                                  
*                                                                               
         L     RE,VSSB             GET NUM OF TRMS FROM SSB                     
         XR    RF,RF                                                            
         ICM   RF,3,SSBTRMS-SSBD(RE)                                            
         BZ    RTWANO              NO TERMINALS IN UTL                          
         STH   RF,HALF                                                          
         AHI   R5,-6                                                            
         ST    RF,0(R5)            SET VUTL-6(4) TO NUM TRMS                    
         LA    R5,6(R5)                                                         
         LH    RE,0(R5)                                                         
         STH   RE,UTLLEN                                                        
         MR    RE,RE               RF=NUM-ENTRYS*ENTRY-LEN                      
         LA    RF,5(R5,RF)                                                      
         ST    RF,2(R5)            SET VUTL+2(4) TO A(END-1)                    
         BRAS  RE,SETBXLE                                                       
*                                                                               
         LA    RE,1                                                             
RTWA04   STH   RE,TNUM             SET TNUM IN EACH ENTRY                       
         LA    RE,1(RE)                                                         
         BRXLE R5,R6,RTWA04                                                     
*                                                                               
         L     R5,VUTL             POINT TO FIRST UTL ENTRY                     
         BRAS  RE,SETBXLE                                                       
*                                                                               
RTWA06   LH    RF,P3+2             BUMP TO NEXT TERMINAL NUMBER                 
         LA    RF,1(RF)                                                         
         ST    RF,P3                                                            
         GOTO1 VDATAMGR,P1,TO24=Y  READ TWA#0 FOR TERMINAL                      
         SAM31                                                                  
         CLI   8(R1),0                                                          
         BNE   RTWANO              DISK ERROR READING TEMPSTR                   
         L     R2,ACHKPT1                                                       
         USING CHKPTD,R2           RE=A(TWA CHECKPOINT DATA)                    
*                                                                               
RTWA08   CLC   CHUTLNUM,TNUM       VTAM TERMINAL OR PRINTER RECORD              
         BNE   RTWA20              TERMINAL NUMBERS DONT MATCH                  
         CLC   CHTDDATE(8),STRDATE TEST AGE FOR VALIDITY                        
         BL    RTWA16                                                           
*                                                                               
RTWA10   MVC   TSTAT1,CHUTLSTA     RESTORE STATIC UTL DATA                      
         MVC   TOFFCODE,CHUTLOFF                                                
         MVC   TTYPE,CHUTLTYP                                                   
         MVC   TSYM,CHUTLSYM                                                    
         MVC   TCTRY,CHUTLCTR                                                   
         MVC   TCFN,CHUTLCFN                                                    
         L     R1,HICORE                                                        
         CLC   16(4,R1),=C'*PRTCHK*'                                            
         BE    RTWA14                                                           
         CLC   16(8,R1),=C'*TRMCHK*'                                            
         BE    RTWA16                                                           
*                                                                               
RTWA12   MVC   TSVDATA,CHUTLSV     RESTORE UTL SAVE DATA FOR TERMS              
         MVC   TSVDATA1,CHUTLSV1                                                
         MVC   TJOBINFO,CHUTLJOB                                                
         MVC   TAGYSEC,CHUTLASC                                                 
         MVC   TPERSON,CHUTLPER                                                 
         MVC   TSVDATA3,CHUTLSV3                                                
         MVC   TCLRMSK,CHUTLCLR                                                 
         MVC   TSTAT8,CHUTLST8                                                  
         MVC   TLVLDRTY,CHUTLLDY                                                
         MVC   TSTAT9,CHUTLST9                                                  
         MVC   TXPINFO,CHUTLXPI                                                 
         MVC   TPASSEXP,CHUTLPEX                                                
         MVC   TSTATB,CHUTLSTB                                                  
         MVC   TAGYPER,CHUTLAGP                                                 
         MVC   TUPDFAC,CHUTLUPF                                                 
         MVC   TTICKET,CHUTLTKT                                                 
*                                                                               
         L     R1,TUTLXADR                                                      
         USING XAUTLD,R1                                                        
         MVC   TTICKETN,CHUTLTKN   TICKETN TO XAUTL                             
         DROP  R1                                                               
*                                                                               
         TM    TJOBFLAG,TJOBFINQ   SET SSB FLAG IF JOBS IN QUEUE                
         BZ    *+12                                                             
         L     RF,VSSB                                                          
         OI    SSBJFLAG-SSBD(RF),SSBJFINQ                                       
         B     RTWA18                                                           
*                                                                               
RTWA14   MVC   TPRDATA,CHUTLSV     RESTORE STATIC PRINTER DATA                  
         XR    R0,R0                                                            
         ICM   R0,1,TPRNEMAX       SET NUMBER OF REQUIRED PRQ ENTRIES           
         GOTO1 VLCM,DMCB,VTGETPRQ,(R0)                                          
         L     R0,DMCB+4           GET NUMBER OF ASSIGNED PRQ ENTRIES           
         LTR   R1,R1                                                            
         BZ    RTWANO                                                           
         STC   R0,TPRNEMAX                                                      
         ST    R1,TXPRNT           SET A(PRQ) IN UTL AND A(UTL) IN PRQ          
         ST    R5,PRQAUTL-PRQD(R1)                                              
         STCM  R1,7,TPRNT          .....                                        
         MVC   PRQBUFFL-PRQD(5,R1),TPRBUFFL                                     
         MVC   PRQATTR-PRQD(1,R1),TPRATTR                                       
         MVC   PRQATT2-PRQD(1,R1),TPRATT2                                       
         MVC   PRQESCN-PRQD(1,R1),TPRESC                                        
         MVC   PRQNEMAX-PRQD(1,R1),TPRNEMAX                                     
         L     R1,HICORE           POINT TO PRINTER CHECKPOINT REC              
         BRAS  RE,PRQRSTR          RESTORE PRQ ENTRY                            
         B     RTWA20                                                           
*                                                                               
RTWA16   OI    TSTAT2,TSTATNIT     SET TRM NOT INITIALIZED                      
         LLC   R0,TCTRY            INDEX INTO COUNTRY TABLE                     
         L     RE,VSSB                                                          
         L     RE,SSBACTRY-SSBD(RE)                                             
         MH    R0,0(RE)                                                         
         LA    RE,6(RE)                                                         
         AR    RE,R0                                                            
         MVC   TLANG,CTRYLANG-CTRYTABD(RE) SET DEFAULT LANGUAGE                 
         XC    TJOBINFO,TJOBINFO   CLEAR JOB SUBMISSION INFO                    
         L     R1,HICORE                                                        
         CLC   16(8,R1),=C'*TRMCHK*'                                            
         BE    RTWA20              BYPASS STATIC TRM CHECK POINTS               
*                                                                               
RTWA18   L     R2,ACHKPT1          POINT TO CURRENT CHECKPOINT AREA             
         USING CHKPTD,R2                                                        
         L     RF,ACHKPT2          POINT TO HIGHEST CHECKPOINT AREA             
         CLC   CHTDDATE(8),CHTDDATE-CHKPTD(RF)                                  
         BL    RTWA20                                                           
         BH    *+14                                                             
         CLC   CHSSBSEQ,CHSSBSEQ-CHKPTD(RF)                                     
         BL    RTWA20                                                           
         LH    R1,LENCHK           SAVE LATEST CHECKPOINT INFO                  
         LR    R0,RF                                                            
         LR    R3,R1                                                            
         MVCL  R0,R2                                                            
*                                                                               
RTWA20   BRXLE R5,R6,RTWA06        BUMP TO NEXT UTL ENTRY                       
         J     EXIT                                                             
*                                                                               
RTWANO   J     EXIT                ERROR IN READING TEMPSTR RECORDS             
         DROP  R2                                                               
***********************************************************************         
* WRITE IO ERROR MESSAGE TO CONSOLE                                   *         
***********************************************************************         
IOERROR  NTR1  BASE=*,LABEL=*                                                   
         LA    R8,IOERRMSG                                                      
         L     R1,P4               A(DTF)                                       
         MVC   15(7,R8),22(R1)                                                  
         L     R0,P5               A(D/A) & ERROR BYTES                         
         GOTO1 VHEXOUT,IODMCB,(R0),26(R8),4,=C'TOG'                             
         GOTO1 (RF),(R1),P3,38(R8),2,=C'TOG'                                    
         MVI   46(R8),C'R'         READ/WRITE I/O                               
         CLC   P1+1(3),VRDID+1                                                  
         BE    IOERROR2                                                         
         CLC   P1+1(3),VRDTRK+1                                                 
         BE    IOERROR2                                                         
         MVI   46(R8),C'W'                                                      
*                                                                               
IOERROR2 XR    R1,R1                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
*                                                                               
IOERRMSG DC    CL50'XI/O ERROR DTF=XXXXXXX DA=XXXXXXXX EB=XXXX IO=X'            
         EJECT                                                                  
***********************************************************************         
* SET PROGRAMS TO OP OR NO-OP                                         *         
***********************************************************************         
SETPRG   NTR1  BASE=*,LABEL=*                                                   
         LA    R1,41                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         LA    R2,PGLIST           R2=A(PGM OP/NOP LIST)                        
         MVI   FLAG,0                                                           
         CLI   PGOPNOP,C'-'                                                     
         BNE   SETPRG2                                                          
         MVI   FLAG,255                                                         
         MVC   REPLY(3),=X'FF00FF'                                              
         BRAS  RE,SETPG            NO-OP ALL PROGRAMS                           
*                                                                               
SETPRG2  XI    FLAG,255                                                         
*                                                                               
SETPRG4  CLI   0(R2),0             END OF OP/NO-OP LIST                         
         BE    SETPRGX                                                          
         MVC   REPLY(3),0(R2)                                                   
         BRAS  RE,SETPG            OP/NO-OP PROGRAMS                            
         AHI   R2,L'PGLIST                                                      
         B     SETPRG4                                                          
*                                                                               
SETPRGX  LA    R1,42                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SET PGMLIST ENTRY(S) TO OP OR NO-OP.                                *         
* REPLY+0(1) CONTAINS SESYS (255=ALL SYSTEMS)                         *         
* REPLY+1(2) CONTAINS LOW AND HIGH PROGRAM NUMBERS                    *         
* FLAG=X'00' TO OP OR FLAG=X'FF' TO NOP                               *         
* IF + THEN NOP SYSTEMS IN LIST AND IF - THEN OP SYSTEMS IN LIST      *         
***********************************************************************         
SETPG    NTR1                                                                   
         L     R5,VSELIST                                                       
         BRAS  RE,SETBXLE                                                       
         USING SELISTD,R5          R5=A(SELIST)                                 
*                                                                               
SETPG2   CLI   REPLY,255           TEST IF 'ALL SYSTEM' CALL                    
         BE    *+14                                                             
         CLC   SEOVSYS,REPLY       ELSE MATCH ON OVERLAY SYSTEM NUMBER          
         BNE   SETPG8                                                           
         CLI   SEOVSYS,1           IGNORE SERVICE SYSTEM                        
         BE    SETPG8                                                           
*                                                                               
         L     R1,SEPGMS                                                        
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING PGMLSTD,R1          R1=A(PGMLST FOR SYSTEM)                      
*                                                                               
SETPG4   CLC   PGMNUM,REPLY+1      TEST LOW AND HIGH PROGRAM RANGE              
         BL    SETPG6                                                           
         CLC   PGMNUM,REPLY+2                                                   
         BH    SETPG6                                                           
         NI    PGMIND,255-X'80' SET PGM TO OP                                   
         CLI   FLAG,255                                                         
         BNE   *+8                                                              
         OI    PGMIND,X'80'        SET PGM TO NO-OP                             
*                                                                               
SETPG6   BXLE  R1,RE,SETPG4                                                     
         CLI   REPLY,255           255 FOR ALL SYSTEMS                          
         JNE   EXIT                SINGLE SYSTEM                                
*                                                                               
SETPG8   BXLE  R5,R6,SETPG2        NEXT SYSTEM                                  
         J     EXIT                                                             
         DROP  R1,R5                                                            
         EJECT                                                                  
***********************************************************************         
* SET PARAMETERS FROM BOOTLIST INFO                                   *         
***********************************************************************         
SETINFO  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,39                                                            
         BRAS  RE,PUTMESS                                                       
         TM    UPSI,X'01'          USE BOOTLIST INFO TO SET PARAMS              
         BZ    *+8                                                              
         MVI   FACAUTOQ,C'Y'       SET AUTOPQ MODE FROM UPSI                    
*                                                                               
SETI02   L     R5,VSSB             SET TIMER VALUES                             
         USING SSBD,R5                                                          
         MVC   SSBTIMES(TIMVALEN),TIMVALS                                       
*&&UK                                                                           
         CLI   SSBSYSID,10         IF UK ECHO SYSTEMS ADVA/B/C                  
         BE    SETI03                                                           
         CLI   SSBSYSID,12                                                      
         BE    SETI03                                                           
         CLI   SSBSYSID,14                                                      
         BNE   SETI04                                                           
SETI03   L     R1,=V(SELIST0)      SET MAX SCRIPTS TO 10                        
         MVI   SETASKMX-SELISTD(R1),10                                          
*&&                                                                             
SETI04   ICM   R1,15,TSKNUM        NUMBER OF TASKS                              
         BNZ   SETI05                                                           
         ABEND 345,DUMP            CANT BE ZERO                                 
*                                                                               
SETI05   L     RF,VTCB                                                          
         MH    R1,0(RF)                                                         
         LA    R1,5(R1,RF)                                                      
         C     R1,2(RF)                                                         
         BNH   SETI05A                                                          
         ABEND 346,DUMP            NUM TASKS MUST BE LE MAXIMUM                 
SETI05A  ST    R1,2(RF)                                                         
*                                                                               
SETI06   L     RF,VUPDID           DATAMGR UPDATE ID                            
         MVC   0(2,RF),DMID                                                     
*                                                                               
SETI08   OC    ADMFLIST,ADMFLIST   CHECK ALL SYS1 FILES CHANGED/NOP             
         BZ    SETINFOX                                                         
         L     R1,APHLIST                                                       
         USING SYSFLSTD,R1                                                      
SETI10   LLC   R0,SYSF#FLS+1                                                    
         LA    R1,SYSFLIST                                                      
*                                                                               
SETI12   TM    SYSFIND1,SFNOP      IGNORE NOP FILES                             
         BO    SETI20                                                           
         L     RF,SYSFADTF-1                                                    
         USING DTFPHD,RF                                                        
*                                                                               
SETI14   CLI   SYSFILE#,X'F2'      SET TEMPSTR RECORD LENGTH                    
         BNE   SETI16                                                           
         MVC   DBLKSZ,SSBTWAL                                                   
         OI    DBLKSZ,X'80'                                                     
         B     SETI18                                                           
*                                                                               
SETI16   CLI   SYSFILE#,X'FE'      SET TEMPEST RECORD LENGTH                    
         BNE   SETI18                                                           
         MVC   DBLKSZ,SSBTMSL                                                   
         OI    DBLKSZ,X'80'                                                     
*                                                                               
SETI18   TM    DTFOPEN,DTF_RO      TEST READ-ONLY                               
         BZ    SETI20                                                           
         CLI   SYSFILE#,X'A1'      IGNORE CONTROL FILE                          
         BE    SETI20                                                           
         CLI   SYSFILE#,X'AE'      IGNORE GENDIR FILE                           
         BE    SETI20                                                           
         CLI   SYSFILE#,X'AF'      IGNORE GENFIL FILE                           
         BE    SETI20                                                           
         ABEND 347,DUMP            READ-ONLY (HAS NOT BEEN RENAMED)             
         DROP  RF                                                               
*                                                                               
SETI20   LA    R1,SYSFLNQ(R1)      NEXT FILE                                    
         BCT   R0,SETI12                                                        
         DROP  R1                                                               
*                                                                               
SETINFOX LA    R1,40                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* OPEN VTAM ACB                                                       *         
* IF AUTOPQ ACTIVE READ PRINTER PASSIVES TO FIND AUTO MODE PRINTERS   *         
***********************************************************************         
VTAMST   NTR1  BASE=*,LABEL=*                                                   
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    VTAMST0                                                          
         GOTO1 VLCM,DMCB,VTOPEN    SET LCBUFFS ONLY                             
         B     VTAMSTX                                                          
*                                                                               
VTAMST0  GOTO1 VLCM,DMCB,VTOPEN    OPEN VTAM ACB                                
         BE    VTAMST1                                                          
         LA    R1,29               VTAM ACB OPEN FAILURE                        
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
*                                                                               
VTAMST1  CLI   FACAUTOQ,C'Y'       TEST AUTOPQ ACTIVE                           
         BNE   VTAMST5                                                          
         CLI   STRTMODE,C'R'       IF RESTART HAVE ALREADY BUILT PRQS           
         BE    VTAMST5                                                          
         L     RE,SVTSKADR         GET ACCESS TO SE1 FILES                      
         USING TCBD,RE                                                          
         L     RF,VSELIST                                                       
         LA    RF,6(RF)                                                         
         MVC   TCBDTFS(8),SEFILES-SELISTD(RF)                                   
         ST    RE,P1                                                            
         MVI   P1,C'I'                                                          
         GOTO1 VDTFIOA,P1          INITIALIZE DTFS                              
*                                                                               
VTAMST2  L     R8,ASTRTIOA         SET KEY OF PRINTER PASSIVE REC               
         XC    0(25,R8),0(R8)                                                   
         MVI   0(R8),C'T'                                                       
         MVI   6(R8),C'P'                                                       
         LA    RE,25(R8)           SET IOAREA OF PRINTER PASSIVE REC            
         XC    P1(24),P1                                                        
         MVC   P1,ISREAD           SET TO READ FIRST RECPORD                    
         ST    RE,P2                                                            
         MVC   P4,VCTFILE                                                       
         ST    R8,P5                                                            
         B     *+10                                                             
*                                                                               
VTAMST3  MVC   P1,ISRDSEQ          READ NEXT PRINTER PASSIVE                    
         GOTO1 VISDDS,P1                                                        
         OC    P3(2),P3            TEST FOR I/O ERRORS                          
         BNZ   VTAMST5                                                          
         LA    RE,25(R8)           RE=A(PRINTER TERMINAL RECORD)                
         CLC   0(7,R8),0(RE)       END OF PRINTER PASSIVES                      
         BNE   VTAMST5                                                          
         OC    15(10,RE),15(RE)    BYPASS ANY PRINTERQ PAGE RECORDS             
         BNZ   VTAMST3                                                          
         TM    27(RE),X'80'        BYPASS DELETED RECORDS                       
         BO    VTAMST3                                                          
         TM    27(RE),X'0C'        BYPASS PRINTERS NOT IN AUTO MODE             
         BNO   VTAMST3                                                          
*                                                                               
VTAMST4  XC    DMCB(20),DMCB       SET UP PARAM LIST FOR V(TERMBLD)             
         MVC   DUB,7(RE)                                                        
         ST    RE,DMCB+8           SET A(TERMINAL RECORD)                       
         LA    RE,DUB                                                           
         ST    RE,DMCB+0           SET A(SYMBOLIC ID)                           
         GOTO1 VTERMBLD,DMCB                                                    
         CLI   DMCB+0,0                                                         
         BNE   VTAMST3                                                          
*                                                                               
         SAM31                                                                  
         ICM   RE,15,DMCB+4          GET A(UTL ENTRY)                           
         BZ    VTAMST3                                                          
         ICM   RF,15,TXPRNT-UTLD(RE) GET A(PRQ ENTRY)                           
         BZ    VTAMST3                                                          
         MVI   PRQMODE-PRQD(RF),X'80'  SET PRINTER ON AUTO                      
         OI    TSTAT5-UTLD(RE),TST5TCP SET TERM CHECKPOINT PENDING              
         L     RE,VSSB                                                          
         OI    SSBVTFL1-SSBD(RE),SSBVTTCP                                       
         SAM24                                                                  
         B     VTAMST3                                                          
*                                                                               
VTAMST5  EQU   *                                                                
*                                                                               
VTAMSTX  J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* A 'START' IS PERFORMED AT THE BEGINNING OF EACH DAY, OR WHEN IT IS  *         
* NECESSARY TO RELINK THE SYSTEM DURING THE DAY                       *         
* ALL Q-FILES ARE RECREATED AND PREVIOUS DATA IS LOST                 *         
***********************************************************************         
START    NTR1  BASE=*,LABEL=*                                                   
         LA    R1,61                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         XC    P1(24),P1           WRITE TSTTAB TO TSTRCVR FILE                 
         MVC   P1,VWTCKD                                                        
         L     RE,VTSTTAB                                                       
         ST    RE,P2                                                            
         L     RF,2(RE)                                                         
         LA    RF,1(RF)                                                         
         SR    RF,RE                                                            
         ST    RF,P3                                                            
         L     RE,VTSTRCVR                                                      
         ST    RE,P4                                                            
         USING DTFPHD,RE                                                        
         MVC   DNEXT,=X'00010000'  TSTTAB AT TRK1,REC1                          
         LA    R0,P6                                                            
         ST    R0,P5                                                            
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    START02                                                          
         BRAS  RE,IOERROR                                                       
         ABEND 348,DUMP                                                         
*                                                                               
START02  MVC   P1,VWTERASE         ERASE REST OF TSTRCVR FILE                   
         XC    P2(8),P2                                                         
         L     RE,VTSTRCVR                                                      
         ST    RE,P4                                                            
         LA    R0,P6                                                            
         ST    R0,P5                                                            
         MVC   P6,DNEXT                                                         
         BASR  RE,RF                                                            
*                                                                               
STARTX   LA    R1,62                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* RESTARTS ARE PERFORMED TO PRESERVE DATA STORED IN THE CHECKPOINT    *         
* AREAS OF THE PRGMS FILE (TRKS 1&2). SINCE THE CHECKPOINT REGION     *         
* MAY VARY IN SIZE WHEN THE SYSTEM IS RELINKED, NO RESTART CAN BE     *         
* PERFORMED WHEN PRGMS FILE IS RELOADED (OPT1=YES).                   *         
*                                                                     *         
* CHECKPOINT AREA#1 - SSB/JOBTAB/BCTAB                                *         
* CHECKPOINT AREA#2 - REQUEST-ADDRESS-LISTS/TEMPTAB                   *         
***********************************************************************         
RESTART  NTR1  BASE=*,LABEL=*      SET RESTART MODE                             
         LA    R1,63                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         MVI   STRTMODE,C'R'       SET RESTART MODE                             
         LA    R1,65                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         XC    P1(24),P1           READ CHKPNT REC 1 INTO HICORE                
         MVC   P1,VRDID                                                         
         MVC   P2,HICORE                                                        
         MVC   P4,VPRGMS           CHKPNT 1 REC IS 1ST TRK OF PRGMS             
         XC    P6,P6               SET TRK 1/BLK 0 +OFFSET                      
         LLC   RE,FACSYSID                                                      
         LA    RE,1(RE,RE)                                                      
         STH   RE,P6                                                            
         MVI   P6+2,1                                                           
         LA    RE,P6                                                            
         ST    RE,P5                                                            
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    RDCK01                                                           
         BRAS  RE,IOERROR                                                       
         ABEND 349,DUMP                                                         
*                                                                               
RDCK01   L     R5,VSSB             TEST CHKPNT DATE SAME AS TODAY               
         USING SSBD,R5                                                          
         L     RF,HICORE                                                        
CHKPG    USING SSBD,RF                                                          
         CLC   SSBDATE,CHKPG.SSBDATE                                            
         BE    RDCK04                                                           
         TM    SSBSYSFL,X'40'      TEST IF RESTART ALLOWED                      
         BO    RDCK02                                                           
         LA    R1,3                NO-OUTPUT INVALID DATE MESSAGE               
         BRAS  RE,PUTMESS                                                       
         ABEND 350,DUMP                                                         
         BRAS  RE,STRTEOJ                                                       
*                                                                               
RDCK02   LA    R1,4                OUTPUT SPECIAL INVALID DATE MESSAGE          
         BRAS  RE,PUTMESS                                                       
         CLC   REPLY(8),=C'OVERRIDE'                                            
         BE    RDCK04                                                           
         LA    R1,5                OUTPUT SYSTEM TERMINATE MESSAGE              
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTEOJ                                                       
*                                                                               
RDCK04   L     RF,HICORE           RF=A(CHKPNT SSB - 1024 BYTES LONG)           
         MVC   SSBTRMS(2),CHKPG.SSBTRMS                                         
         MVC   SSBSDATE(8),CHKPG.SSBSDATE                                       
         MVC   SSBRDATE(8),NOWDATE                                              
         LLC   R1,CHKPG.SSBRCNT    BUMP NUMBER OF RESTARTS                      
         LA    R1,1(R1)                                                         
         STC   R1,SSBRCNT                                                       
         MVC   SSBDTIME(2),CHKPG.SSBDTIME                                       
         MVC   STRDATE(8),CHKPG.SSBSDATE                                        
         MVC   SSBTTRN#,CHKPG.SSBTTRN#                                          
*                                                                               
         XR    R1,R1               REBUILD JOBTAB FROM CHKPT1 AREA              
         ICM   R1,3,SSBJMAX                                                     
         XR    R0,R0                                                            
         ICM   R0,3,CHKPG.SSBJMAX                                               
         CR    R0,R1                                                            
         BNH   *+6                                                              
         LTR   R0,R1                                                            
         BZ    RDCK08                                                           
         DROP  CHKPG                                                            
*                                                                               
         AHI   RF,1024+8           RF=A(CHECKPOINTED JOBTAB)                    
         L     RE,SSBAJOB                                                       
         LH    R1,0(RE)                                                         
         BCTR  R1,0                R1=L'JOBTAB ENTRY-1                          
         LA    RE,6(RE)            RE=A(CURRENT JOBTAB)                         
*                                                                               
RDCK06   EX    R1,RDCKJMVE                                                      
         LA    RE,1(RE,R1)                                                      
         LA    RF,1(RF,R1)                                                      
         BCT   R0,RDCK06                                                        
         B     RDCK08                                                           
*                                                                               
RDCKJMVE MVC   0(0,RE),0(RF)       RESTORE CHECKPOINT ENTRY                     
*                                                                               
RDCK08   L     RE,HICORE           REBUILD BCTAB FROM CHKPT1 AREA               
         LA    R1,1024+2(RE)                                                    
         S     R1,SSBAJOB-SSBD(RE) R1=RELOCATION FACTOR                         
         L     RE,SSBABC-SSBD(RE)                                               
         AR    RE,R1                                                            
         L     RF,2(RE)                                                         
         AR    RF,R1                                                            
         LA    RE,6(RE)            RE=A(FIRST ENTRY IN CHKPNT BCTAB)            
         SR    RF,RE                                                            
         LA    RF,1(RF)            RF=LENGTH OF CHKPNT BCTAB                    
         L     R6,SSBABC                                                        
         L     R7,2(R6)                                                         
         LA    R6,6(R6)            R6=A(FIRST ENTRY IN BCTAB)                   
         SR    R7,R6                                                            
         LA    R7,1(R7)            R7=LENGTH OF BCTAB                           
         MVCL  R6,RE                                                            
*                                                                               
         L     RE,VSYSFAC          GET A(ACC LOCKTAB)                           
         ICM   R6,15,AACCLTB1-ACCFACSD(RE)                                      
         BZ    RDCK10                                                           
*                                                                               
         LR    RE,R6                                                            
         S     RE,VSSB             GIVES DSPL FROM START OF CHKPT               
         A     RE,HICORE           POINT TO OLD TABLE                           
         AHI   RE,-10              BACK UP TO CHECK IF IT IS THERE              
         CLC   0(8,RE),=C'ACCLTAB1'                                             
         BNE   RDCK10                                                           
         LA    RE,8(RE)            POINT TO OLD TABLE - 2                       
         AHI   R6,-2               POINT TO NEW TABLE - 2                       
         MVC   0(1,R6),0(RE)       MOVE LOCK BYTE                               
*                                                                               
         LA    R6,2(R6)            POINT TO NEW TABLE START                     
         L     R7,2(R6)            GET A(END-1)                                 
         LA    R6,6(R6)            GIVES 'TO' ADDR                              
         SR    R7,R6               GIVES 'TO' LENGTH                            
*                                                                               
         LA    RE,2(RE)            POINT TO OLD TABLE START                     
         L     RF,2(RE)                                                         
         LA    RE,6(RE)            GIVES 'FROM' ADDR                            
         SR    RF,RE               GIVES 'FROM' LENGTH                          
         MVCL  R6,RE                                                            
*                                                                               
RDCK10   ICM   RE,15,VCHKPT2       READ CHKPNT2 REC INTO CHKPNT2 AREA           
         BZ    RDCK18                                                           
         MVC   DUB,0(RE)           SAVE A(CHKPT2/CHKPT2X)                       
         ST    RE,P2                                                            
*                                  CHKPNT 2 REC IS 2ND TRK OF PRGMS             
         XC    P6,P6               SET TRK 2/BLK 0 +OFFSET                      
         LLC   RE,FACSYSID                                                      
         LA    RE,2(RE,RE)                                                      
         STH   RE,P6                                                            
         MVI   P6+2,1                                                           
         GOTO1 VDADDS,P1                                                        
         OC    P3(2),P3                                                         
         BZ    RDCK11                                                           
         BRAS  RE,IOERROR                                                       
         ABEND 351,DUMP                                                         
*                                                                               
RDCK11   LM    RE,RF,DUB                                                        
         SR    RF,RE                                                            
         L     R1,VCHKPT2                                                       
         LM    R0,R1,0(R1)                                                      
         SR    R1,R0                                                            
         CR    RF,R1               TEST L'CHKPT2 AREA NOT CHANGED               
         BE    RDCK18                                                           
*                                                                               
RDCK12   LA    R1,25               OUTPUT 'NO RESTART POSSIBLE' MESSAGE         
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,STRTCNCL                                                      
*                                                                               
RDCK18   BRAS  RE,READTWA          READ TWA ENTRIES AND RESTORE UTLS            
         LA    R1,66                                                            
         BRAS  RE,PUTMESS                                                       
         B     RSUTL                                                            
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
* ALL TERMINAL RECORDS READ AND UTL ENTRIES RESTORED                  *         
***********************************************************************         
RSUTL    L     R5,VUTL             RESET UTL STATUS BYTES                       
         LA    R1,67                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         SAM31                                                                  
         BRAS  RE,SETBXLE                                                       
         USING UTLD,R5                                                          
*                                                                               
RSUTL02  OI    TSTAT2,TSTATNIT     SET TRM NOT INITIALIZED                      
         XC    TSVCREQ,TSVCREQ                                                  
         XC    TASVC,TASVC                                                      
         CLI   TSYS,0              TEST CONNECTED                               
         BE    RSUTL06                                                          
*                                  RESET TASYS & TAPRG                          
         L     R1,VSELIST                                                       
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         CLC   TSYS,SESYS-SELISTD(R1)                                           
         BE    RSUTL03                                                          
         BXLE  R1,RE,*-10                                                       
         ABEND 352,DUMP                                                         
*                                                                               
RSUTL03  STCM  R1,7,TASYS                                                       
         STCM  R1,7,TARSYS                                                      
*                                                                               
         L     R1,SEPGMS-SELISTD(R1)                                            
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         CLC   TPRG,PGMNUM-PGMLSTD(R1)                                          
         BE    RSUTL04                                                          
         BXLE  R1,RE,*-10                                                       
         XC    TCTDATA,TCTDATA     DISCONNECT TERMINAL IF PGM NOT FOUND         
         B     RSUTL06                                                          
*                                                                               
RSUTL04  XR    RF,RF               SET DISPLACEMENT TO PROG TABLE ENTRY         
         ICM   RF,7,TARSYS                                                      
         ICM   RF,15,SEPGMS-SELISTD(RF)                                         
         SR    R1,RF                                                            
         STCM  R1,7,TAPRG                                                       
*                                                                               
RSUTL06  BXLE  R5,R6,RSUTL02                                                    
         SAM24                                                                  
         LHI   R1,68                                                            
         BRAS  RE,PUTMESS                                                       
         B     RSSE                                                             
         EJECT                                                                  
***********************************************************************         
* RESET SELIST QUEUE LINKS                                            *         
***********************************************************************         
RSSE     LA    R1,69                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     R5,VSELIST                                                       
         BRAS  RE,SETBXLE                                                       
         USING SELISTD,R5                                                       
         L     RE,ACHKPT2                                                       
         USING CHKPTD,RE                                                        
         LA    R1,CHSELTAB                                                      
         USING CHSELTAB,R1                                                      
RSSE02   CLC   CHSELSYS,SESYS      RESTORE SE SAVE DATA FROM MAX SEQ            
         BE    RSSE03                                                           
         ABEND 353,DUMP            SELIST CHANGED ON A RESTART                  
*                                                                               
RSSE03   MVC   SEIND,CHSELIND                                                   
         TM    SEIND,SEIACTV       TEST SYSTEM WAS ACTIVE                       
         BZ    *+8                                                              
         MVI   SESIN+3,1           SET NON-ZERO SIN FOR RECOVER/RESTORE         
         NI    SEIND,255-SEIACTV                                                
*                                                                               
*        REMOVING SERCVDA, ASSUMING FILES ARE VERMONT BUFFERED                  
*        MVC   SERCVDA(4),CHSELRDA                                              
*        L     RE,TSKNUM                                                        
*        BCTR  RE,0                RE=N'TASKS-1                                 
*        SLL   RE,2                                                             
*        BCTR  RE,0                                                             
*        EX    RE,*+8              REPLICATE LOW D/A FOR ALL TASKS              
*        B     *+10                                                             
*        MVC   SERCVDA+4(0),SERCVDA                                             
*                                                                               
         NI    SEIND,X'7E'         RESET STARTED/NOP BITS                       
         CLI   SESYS,1             SET SE1 AS STARTED                           
         BNE   *+8                                                              
         OI    SEIND,X'80'                                                      
         MVI   SETASK,0            RESET ACTIVE FLAG                            
         XC    SEFIRST,SEFIRST                                                  
         XC    SELAST,SELAST                                                    
         XC    SEQLEN,SEQLEN                                                    
         LA    R1,CHSELLEN(R1)                                                  
         BXLE  R5,R6,RSSE02                                                     
*                                                                               
         LA    R1,70                                                            
         BRAS  RE,PUTMESS                                                       
         B     RSSSB                                                            
         DROP  R1                                                               
         EJECT                                                                  
***********************************************************************         
* RESTORE SSB SAVE DATA                                               *         
***********************************************************************         
RSSSB    LA    R1,71                                                            
         BRAS  RE,PUTMESS                                                       
         BRAS  RE,RESTMPS          REBUILD TEMPEST COUNTER                      
*                                                                               
         L     RF,VSSB                                                          
         USING SSBD,RF                                                          
         L     RE,ACHKPT2                                                       
         USING CHKPTD,RE                                                        
         MVC   SSBSIN,CHSSBSIN                                                  
         MVC   SSBTTRN#,CHSSBTT#                                                
         MVC   SSBSEQ,CHSSBSEQ                                                  
         MVC   SSBDMPNO,CHSSBDMP                                                
         MVC   SSBDMPSV,CHSSBDSV                                                
         NI    SSBDMPSV,X'7F'      ENABLE DUMPS                                 
*                                                                               
RSSSB02  L     R0,NOWTIME          GET DATE/TIME OF LAST CHECKPOINT             
         CLC   CHTDDATE,NOWDATE                                                 
         BE    RSSSB04                                                          
         MVC   FULL,CHTDDATE                                                    
         AP    FULL,=P'1'          IF YESTERDAY ADD 24 HOURS TO TIME            
         CP    FULL,NOWDATE                                                     
         BNE   RSSSB06                                                          
         A     R0,HOURS24                                                       
*                                                                               
RSSSB04  MVC   FULL,CHTDTIME       COMPUTE TIME NOW - LAST CHECKPOINT           
         S     R0,FULL                                                          
         BM    RSSSB06                                                          
         SRDA  R0,32                                                            
         D     R0,=F'100'          R1=DOWN TIME (SECONDS)                       
         ICM   R0,3,SSBDTIME                                                    
         AR    R0,R1                                                            
         STCM  R0,3,SSBDTIME       BUMP DOWN TIME (SECONDS)                     
*                                                                               
RSSSB06  L     R5,VTCB             SET TASK COMPLETED SINS                      
         BRAS  RE,SETBXLE                                                       
         USING TCBD,R5                                                          
         L     RE,ACHKPT2                                                       
         LA    R1,CHTCBSIN                                                      
*                                                                               
RSSSB08  MVC   TCBSINL+1(3),0(R1)  LAST SIN COMPLETED BY TASK                   
         MVC   TCBSINR+1(3),0(R1)  LAST SIN RESTARTED FOR TASK                  
         LA    R1,3(R1)                                                         
         BXLE  R5,R6,RSSSB08                                                    
*                                                                               
         LA    R1,72                                                            
         BRAS  RE,PUTMESS                                                       
         LA    R1,64                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         DROP  R5,RE,RF                                                         
         EJECT                                                                  
***********************************************************************         
* ALLOCATE BUFFERS FOR BLOCKED RECOVERY FILES                         *         
***********************************************************************         
SETRBUF  NTR1  BASE=*,LABEL=*                                                   
         LA    R1,37                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     R2,HICORE                                                        
         L     R1,APHLIST                                                       
         USING SYSFLSTD,R1                                                      
SETRB2   LLC   R0,SYSF#FLS+1       R0=NUM OF FILES                              
         LA    R1,SYSFLIST         R1=A(FILE ENTRY)                             
*                                                                               
SETRB4   TM    SYSFIND1,SFRCV      TEST FOR RECOVERY FILE                       
         BNO   SETRB6                                                           
         L     RF,SYSFADTF-1       RF=A(DTF)                                    
         USING DTFPHD,RF                                                        
         TM    DTFOPEN,DTF_RO+DTF_NOP   TEST READ-ONLY OR NOP                   
         BNZ   SETRB6                                                           
         XR    R3,R3                                                            
         ICM   R3,3,DBLKSZ                                                      
         BZ    SETRB6              TEST IF BLOCKED                              
*                                                                               
         MVC   0(8,R2),=C'**RCBF**' ALLOCATE BUFFER                             
         LA    R2,8(R2)                                                         
         STCM  R2,15,DBLK                                                       
         AR    R2,R3                                                            
         SRL   R2,3                                                             
         LA    R2,1(R2)            MOD TO NEXT DOUBLE WORD                      
         SLL   R2,3                                                             
         DROP  RF                                                               
*                                                                               
SETRB6   LA    R1,SYSFLNQ(R1)      TRY NEXT FILE                                
         BCT   R0,SETRB4                                                        
         CLI   0(R1),X'FF'                                                      
         BNE   SETRB2                                                           
         DROP  R1                                                               
*                                                                               
         ST    R2,HICORE           UPDATE HICORE ADDRESS                        
         LA    R1,38                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* INITIALISE CORE TASK AREAS                                          *         
***********************************************************************         
CORESET  NTR1  BASE=*,LABEL=*                                                   
         ZAP   TASK,=P'1'          SET TASK NUMBER COUNTER                      
*                                                                               
         L     R0,HICORE           CLEAR ALL OF STORAGE BELOW LINE              
         L     R1,HICOREX                                                       
         SR    R1,R0                                                            
         XR    RE,RE                                                            
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R3,VTCB                                                          
         LH    R6,0(R3)                                                         
         L     R7,2(R3)                                                         
         LA    R3,6(R3)            R3=A(TCB LIST ENTRY FOR TASK)                
         USING TCBD,R3                                                          
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    *+6                                                              
         AR    R7,R6               EXTRA TSK REQUIRED IN AOR FOR SYSTEM         
*                                                                               
         SAM31                     GET INTO 31-BIT MODE                         
         L     R5,XAATSK                                                        
         A     R5,XALOCORE                                                      
*                                                                               
SETAD02  BRAS  R8,SETNAME          SET IO AREA SAVE ADDRESS                     
         DC    CL8'**IOA **'                                                    
         ST    R5,TCBFILES         ALLOCATE IO AREA                             
         A     R5,LNIOA                                                         
         ST    R5,TCBFILEX                                                      
*                                                                               
         CLI   FAPROT,C'Y'         TEST STORAGE PROTECTION REQUIRED             
         BNE   SETAD04                                                          
         BRAS  R8,SETNAME          SET PROTECTED IO AREA SAVE ADDRESS           
         DC    CL8'**PIO **'                                                    
         ST    R5,TCBPIO                                                        
         A     R5,PIOLEN                                                        
         ST    R5,TCBPIOX                                                       
*                                                                               
SETAD04  L     RE,VSSB             TSAR BUFFER(S)                               
         USING SSBD,RE                                                          
         XR    R1,R1                                                            
         ICM   R1,7,FACTSAR+1      GET SIZE OF TSAR BUFFER                      
         AHI   R1,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         ST    R1,SSBTSAR          SET TSAR BUFFER LENGTH                       
         OC    SSBTSAR(1),FACTSAR  SET FLAGS                                    
*                                                                               
         ST    R5,TCBTSAR          SET ADDRESS IN TCB                           
         MVC   00(16,R5),=CL16'**TSAR****TSAR**'                                
         MVC   16(16,R5),=CL16'**TSAR****TSAR**'                                
         AR    R5,R1                                                            
*                                                                               
         TM    SSBTSAR,X'80'       TEST TWO TSAR BUFFERS/TASK                   
         BZ    SETAD05                                                          
         MVC   00(16,R5),=CL16'**TSA2****TSA2**'                                
         MVC   16(16,R5),=CL16'**TSA2****TSA2**'                                
         AR    R5,R1                                                            
*                                                                               
SETAD05  ICM   R1,15,XAAISG        TEST ISGENQ AREA                             
         JZ    SETAD06                                                          
         A     R1,XALOCORE                                                      
         MVC   00(16,R1),=CL16'*ISGENQ**ISGENQ*'                                
         MVC   16(16,R1),=CL16'*ISGENQ**ISGENQ*'                                
         ST    R1,SSBXAISG                                                      
*                                                                               
SETAD06  XR    R1,R1               TEST ALLOCATE SCRIPT BUFFER                  
         ICM   R1,3,FACSCRXA                                                    
         BZ    SETAD08                                                          
         SLL   R1,10               X 1024                                       
         AHI   R0,4095                                                          
         SRL   R1,12                                                            
         SLL   R1,12                                                            
         ST    R1,SSBSCRXA         SET ACTUAL BUFFER LENGTH                     
*                                                                               
         ST    R5,TCBSCRXA         SET ADDRESS IN TCB                           
         MVC   00(16,R5),=CL16'*SCRIPT**SCRIPT*'                                
         MVC   16(16,R5),=CL16'*SCRIPT**SCRIPT*'                                
         A     R5,SSBSCRXA                                                      
         DROP  RE                                                               
*                                                                               
SETAD08  BRAS  R8,SETNAME          FALINK BUFFER                                
         DC    CL8'*FLNK **'                                                    
         STCM  R5,15,TCBLNK        SET ADDRESS IN TCB                           
         LHI   R0,TWAMAX                                                        
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*                                                                               
         BRAS  R8,SETNAME          MINIO BUFFER 1                               
         DC    CL8'*MIN1 **'                                                    
         STCM  R5,15,TCBMINIO                                                   
         ICM   R0,15,MINAREA                                                    
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*                                                                               
         BRAS  R8,SETNAME          MINIO BUFFER 2                               
         DC    CL8'*MIN2 **'                                                    
         STCM  R5,15,TCBMINI2                                                   
         ICM   R0,15,MINAREA                                                    
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*                                                                               
         BRAS  R8,SETNAME          ZIP BUFFER                                   
         DC    CL8'*ZIPB **'                                                    
         STCM  R5,15,TCBAZIP       SET ADDRESS IN TCB                           
         L     R0,=A(ZIPBLENM)                                                  
         STCM  R0,15,ZIPBLEN-ZIPHDRD(R5)                                        
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*                                                                               
         BRAS  R8,SETNAME          MQ BUFFER                                    
         DC    CL8'*MQIO **'                                                    
         STCM  R5,15,TCBMQBF       SET ADDRESS IN TCB                           
         L     R0,=A(TCBMQBFQ)                                                  
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*                                                                               
         BRAS  R8,SETNAME          UNIX RECOVERY Q BUFFERS                      
         DC    CL8'*USSQ **'                                                    
         STCM  R5,15,TCBUSQBF      SET ADDRESS IN TCB                           
         L     R0,=A(RCVBKSZQ)                                                  
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*&&US                                                                           
         BRAS  R8,SETNAME          WORK LOAD MANAGER BUFFER                     
         DC    CL8'*WLMD **'                                                    
         STCM  R5,15,TCBWLMBF      SET ADDRESS IN TCB                           
         L     R0,=A(WLMLNQ)                                                    
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*&&                                                                             
         OC    FACXALEN,FACXALEN                                                
         BZ    SETAD10                                                          
         BRAS  R8,SETNAME          XA W/S BUFFER                                
         DC    CL8'*WSSV **'                                                    
         STCM  R5,15,TCBAXAWS                                                   
         XR    R0,R0                                                            
         ICM   R0,3,FACXALEN                                                    
         CHI   R0,8096             IF LESS THAN 8K THEN IT IS IN K              
         BH    *+14                                                             
         MHI   R0,1024                                                          
         AR    R5,R0                                                            
         B     SETAD10                                                          
*                                                                               
         AHI   R0,4095                                                          
         SRL   R0,12                                                            
         SLL   R0,12                                                            
         AR    R5,R0                                                            
*                                                                               
SETAD10  BRXLE R3,R6,SETAD02       ROUND AND ROUND WE GO                        
         SAM24                     GET BACK TO 24-BIT MODE                      
         EJECT                                                                  
***********************************************************************         
* ALLOCATE TASK STORAGE FOR AREAS BELOW 16-MEG LINE                   *         
***********************************************************************         
         L     R3,HICORE           CONVERT TASK AREA TO STORAGE KEY 9           
         SRL   R3,5                                                             
         LA    R3,1(R3)                                                         
         SLL   R3,5                                                             
         ST    R3,HICORE                                                        
         L     R3,HICOREX          RETURN UNUSED CORE                           
         S     R3,HICORE                                                        
         BNP   SETADRE                                                          
         ST    R3,HICORELN                                                      
*                                                                               
         L     R1,TSKPGM           CALCULATE TASK SIZE                          
         A     R1,TSKWRK                                                        
         A     R1,TSKTIA                                                        
         A     R1,TSKTWA                                                        
         A     R1,TSKMAP                                                        
         SRL   R1,12                                                            
         LA    R1,1(R1)            ROUND UP TO 4K BOUNDARY                      
         SLL   R1,12                                                            
         LR    RE,R1                                                            
         SRL   RE,10               COMPUTE NUMBER OF K                          
         L     RF,=A(MES108)                                                    
         EDIT  (RE),(3,17(RF))                                                  
                                                                                
         SR    RE,RE                                                            
         LR    RF,R3                                                            
         DR    RE,R1               DIVIDE INTO AVAILABLE STORAGE                
         LR    R1,RF                                                            
         L     RF,=A(MESS99)       SHOW HOW MUCH STORAGE IS AVAILABLE           
*                                                                               
         EDIT  (R1),(2,28(RF))     NUMBER OF TASKS SUPPORTED                    
         SRL   RE,10                                                            
         EDIT  (RE),(4,37(RF))     REMAINING STORAGE IN K                       
*                                                                               
         LR    R1,R3               CALCULATE ACTUAL STORAGE AVAILABLE           
         SRL   R1,10                                                            
         EDIT  (R1),(4,11(RF))                                                  
         LA    R1,108                                                           
         BRAS  RE,PUTMESS                                                       
         LA    R1,99                                                            
         BRAS  RE,PUTMESS                                                       
*&&US                                                                           
         CLI   FAPROT,C'Y'         TEST STORAGE PROTECTION REQUIRED             
         BNE   SETAD18                                                          
*&&                                                                             
         LA    R2,HICORE                                                        
         FREEMAIN VC,A=(R2),SP=132                                              
*                                                                               
         STORAGE OBTAIN,LENGTH=(R3),BNDRY=PAGE,SP=132,KEY=9,COND=YES            
         LTR   RF,RF                                                            
         BNZ   SETADRE                                                          
*                                                                               
         ST    R1,HICORE           SAVE TASK START STORAGE ADDRESS              
         A     R1,HICORELN                                                      
         ST    R1,HICOREX          AND END STORAGE ADDRESS                      
*                                                                               
SETAD18  ZAP   TASK,=P'1'          SET TASK NUMBER COUNTER                      
         L     R5,HICORE           ENSURE DBLWD BOUNDARY                        
*                                                                               
         L     R3,VTCB                                                          
         LH    R6,0(R3)                                                         
         L     R7,2(R3)                                                         
         LA    R3,6(R3)            R3=A(TCB LIST ENTRY FOR TASK)                
         USING TCBD,R3                                                          
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    *+6                                                              
         AR    R7,R6               EXTRA TSK REQUIRED IN AOR FOR SYSTEM         
*                                                                               
SETAD20  AHI   R5,4095                                                          
         SRL   R5,12                                                            
         SLL   R5,12               ROUND TO 4K MULTIPLE                         
*                                                                               
         BRAS  R8,SETNAME          SET WORK ADDRESS                             
         DC    CL8'**WRK **'                                                    
         ST    R5,TCBWRKA                                                       
         OC    SVWRKADR,SVWRKADR                                                
         BNZ   *+8                                                              
         ST    R5,SVWRKADR                                                      
         A     R5,TSKWRK                                                        
*                                                                               
         ST    R5,TCBTIA           SET TIA                                      
         AHI   R5,-32              PUT END LABEL OF TSKWRK                      
         BRAS  R8,SETNAME                                                       
         DC    CL8'*ENDWRK*'                                                    
         AHI   R5,32               BUMP BASE CRUMPLE ZONE                       
         A     R5,TSKTIA           ADD SIZE OF TIA                              
*                                                                               
         ST    R5,TCBTWA                                                        
         A     R5,TSKTWA           SET TWA                                      
*                                                                               
         BRAS  R8,SETNAME          SET PHASE MAP ADDRESS                        
         DC    CL8'**MAP **'                                                    
         ST    R5,TCBMAP                                                        
         A     R5,TSKMAP                                                        
*                                                                               
         BRAS  R8,SETNAME          SET PGM ADDRESS                              
         DC    CL8'**PGM **'                                                    
         ST    R5,TCBPGMA                                                       
         C     R3,SVTSKADR                                                      
         BNE   *+8                                                              
         ST    R5,SVPGMADR         SAVE A(1ST TASK PROGRAM AREA)                
         A     R5,TSKPGM                                                        
         ST    R5,TCBPGMX          SAVE END ADDRESS                             
*                                                                               
         C     R5,HICOREX          TEST IF STILL ENOUGH CORE                    
         BH    SETADRE                                                          
*                                                                               
SETADR8  AP    TASK,=P'1'          SET FOR NEXT TASK NUMBER                     
         BXLE  R3,R6,SETAD20                                                    
         ST    R5,HICORE                                                        
*                                                                               
         CLI   FAPROT,C'Y'         TEST STORAGE PROTECTION REQUIRED             
         BNE   SETADRX                                                          
*                                                                               
         L     R5,HICORE           ENSURE PAGE BOUNDARY                         
         SRL   R5,12                                                            
         LA    R5,1(R5)                                                         
         SLL   R5,12                                                            
         ST    R5,HICORE                                                        
         L     R5,HICOREX          RETURN UNUSED CORE                           
         S     R5,HICORE                                                        
         BNP   SETADRE                                                          
         ST    R5,HICORELN                                                      
*                                                                               
         STORAGE RELEASE,ADDR=HICORE,LENGTH=(R5),SP=132,KEY=9,COND=YES          
         LTR   RF,RF                                                            
         BNZ   SETADRE                                                          
*                                                                               
         LA    R6,0                                                             
         STORAGE OBTAIN,LENGTH=((R5),(R6)),BNDRY=PAGE,SP=132,KEY=8,    *        
               COND=YES                                                         
         LTR   RF,RF                                                            
         BNZ   SETADRE                                                          
*&&DO                              NOT SURE WHERE THIS CODE CAME FROM           
         ST    R1,HICORE                                                        
         LR    R5,R1                                                            
         ZAP   TASK,=P'1'                                                       
         L     R3,VTCB                                                          
         LH    R6,0(R3)                                                         
         L     R7,2(R3)                                                         
         LA    R3,6(R3)            R3=A(TCB LIST ENTRY FOR TASK)                
         USING TCBD,R3                                                          
         CLI   FATOR,C'Y'          TEST TOR                                     
         BE    *+6                                                              
         AR    R7,R6               EXTRA TSK REQUIRED IN AOR FOR SYSTEM         
*                                                                               
SETADRC  BRAS  R8,SETNAME          SET PHASE MAP ADDRESS                        
         DC    CL8'**MAP **'                                                    
         ST    R5,TCBMAP           OVER-RIDE MAP AREA TO KEY 8 AREA             
         A     R5,TSKMAP                                                        
         AP    TASK,=P'1'                                                       
         BXLE  R3,R6,SETADRC                                                    
         LR    R1,R5                                                            
*&&                                                                             
         ST    R1,HICORE                                                        
         A     R0,HICORE                                                        
         ST    R0,HICOREX                                                       
         SR    R0,R1                                                            
         ST    R0,HICORELN                                                      
         B     SETADRX                                                          
*                                                                               
SETADRE  L     RE,=A(MESS6)        PUT NUMBER OF TASKS IN ERROR MSG             
         SP    TASK,=P'1'          HAD ENOUGH FOR ONE LESS THAN NOW             
         OI    TASK+1,X'0F'                                                     
         UNPK  22(2,RE),TASK                                                    
         L     R1,TSKNUM                                                        
         CVD   R1,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  43(2,RE),DUB+6(2)                                                
         LA    R1,6                                                             
         BRAS  RE,PUTMESS                                                       
*&&UK                                                                           
         L     RE,VSSB                                                          
         TM    SSBSYSFL-SSBD(RE),X'80'                                          
         BZ    SETADRF                                                          
         L     RE,=A(MES102)                                                    
         MVC   40(4,RE),=C'TEST'                                                
         LA    R1,103              SUGGEST NET=FACPRODL                         
SETADRF  BRAS  RE,PUTMESS                                                       
         LA    R1,104              NET=FACPRODL FOR AORS TOO                    
         BRAS  RE,PUTMESS                                                       
*&&                                                                             
         BRAS  RE,STRTEOJ                                                       
*                                                                               
SETADRX  L     RE,VSSB             STORAGE NOW ALLOCATED                        
         NI    SSBPROT-SSBD(RE),255-SSBSTRTQ                                    
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CHECK ENOUGH CORE IN PARTITION AND MOVE DC TO AREA                  *         
* R5=A(CORE PARTITION)                                                *         
* R8=A(8-BYTE LABEL) AND 8(R8)=RETURN POINT IN PROGRAM                *         
***********************************************************************         
SETNAME  LA    R5,31(R5)           SET START ADDR MODULO 32                     
         SRL   R5,5                                                             
         SLL   R5,5                                                             
         LA    RE,0(R5)                                                         
         N     RE,=X'7F000000'     TEST 31-BIT ADDRESS                          
         BNZ   SETNAM2             YES-NO TESTS ON HICORE                       
         LA    RE,32(R5)                                                        
         C     RE,HICOREX          TEST IF EXCEEDS MAXIMUM                      
         BH    SETADRE                                                          
*                                                                               
SETNAM2  MVC   0(8,R5),0(R8)       R8 POINTS TO EIGHT CHR NAME                  
         ZAP   DUB,TASK                                                         
         CVB   RE,DUB                                                           
         BCTR  RE,0                                                             
         LLC   RE,SETNAMA(RE)                                                   
         STC   RE,5(R5)                                                         
*                                                                               
SETNAM4  MVC   8(24,R5),0(R5)                                                   
         LA    R5,32(R5)           POSITION PAST LAST NAME                      
         B     8(R8)               RETURN                                       
         EJECT                                                                  
*&&US                                                                           
***********************************************************************         
* BUILD TABLE OF EMAIL NOTIFIES FOR DUMPS                             *         
* R2=CURRENT INDEX ENTRY                                              *         
* R4=NUM OF ENTRIES                                                   *         
* R6=POINTS TO END OF LAST ENTRY OF DATA                              *         
***********************************************************************         
         USING SYSFACD,RA          RA=A(SYSTEM FACILITIES)                      
BLDNTFY  NTR1  BASE=*,LABEL=*                                                   
         ICM   R2,15,VNOTIFY       R2=A(TABLE)                                  
         BZ    BNTFYXIT                                                         
         AHI   R2,4                START WILL HAVE NUM OF ENTRIES               
         LHI   R1,105              MESSAGE TO START TO BUILD NOTIFIES           
         BRAS  RE,PUTMESS                                                       
                                                                                
         L     RA,VSYSFAC                                                       
         SR    R4,R4               R4=COUNT                                     
         L     R6,VNOTIFY                                                       
         AHI   R6,NTFTABSZ         R6=A(END OF TABLE)                           
         MVC   COMMAND,RDHI        READ HIGH                                    
                                                                                
         L     R3,ASTRTIOA         R3=A(EMAIL NOTIFIES) ONLINE ONLY             
         USING CTDEREC,R3                                                       
         XC    CTDEKEY,CTDEKEY                                                  
         MVI   CTDEKTYP,CTDEKTYQ                                                
                                                                                
BNTFY030 GOTO1 VDATAMGR,DMCB,COMMAND,DMCTFILE,CTDEKEY,CTDEKEY                   
         CLI   8(R1),0                                                          
         BE    BNTFY040                                                         
         BRAS  RE,IOERROR                                                       
         ABEND 360,DUMP                                                         
         DC    H'0'                                                             
*                                                                               
         USING NTFYD,R2                                                         
BNTFY040 CLI   CTDEKTYP,CTDEKTYQ   X'09' - DUMP E-MAIL RECORDS                  
         BNE   BNTFY800                                                         
         CLI   CTDEKSUB,CTDEKSON   ON-LINE                                      
         BNE   BNTFY800                                                         
         XC    NTFYITEM,NTFYITEM                                                
         MVC   NTFYSP,CTDESYS      SYSTEM/PROGRAM                               
*                                                                               
BNTFY050 CLI   CTDEELM,CTDEELMQ    X'09' ELEMENT                                
         BNE   BNTFY110            SHOULD HAVE BEEN THIS ONE                    
         LLC   RF,CTDELN           FOUND IT                                     
         SHI   RF,CTDEEML-CTDEELM                                               
         LA    RE,CTDEEML(RF)      A(END OF ELEMENT)                            
                                                                                
BNTFY062 BCTR  RE,0                                                             
         CLI   0(RE),C' '          FIND PERTINENT DATA                          
         BH    BNTFY064                                                         
         BRCT  RF,BNTFY062                                                      
         B     BNTFY110            NEXT WITHOUT BUMPING TABLE                   
                                                                                
BNTFY064 SHI   RF,1                                                             
         BM    BNTFY110            NEXT  WITHOUT BUMPING TABLE                  
         STC   RF,NTFYXLEN         LENGTH OF DATA-1                             
         SR    R6,RF                                                            
         SHI   R6,1                                                             
         CR    R2,R6               SEE IF WE COLLIDE                            
         BNL   BNTFY700            YES-SO TERMINATE FOR NOW                     
         MVC   0(0,R6),CTDEEML     MOVE IN DATA                                 
         EX    RF,*-6                                                           
         LR    RF,R6                                                            
         S     RF,VNOTIFY          DISPLACEMENT TO ENTRY                        
         STCM  RF,3,NTFYDISP       SAVE DISPLACEMENT                            
         AHI   R2,NTFYLNQ          NEXT ITEM                                    
         AHI   R4,1                COUNT NUM OF ITEMS                           
                                                                                
BNTFY110 MVC   COMMAND,RSEQ        READ SEQUENTIAL                              
         B     BNTFY030            GET NEXT RECORD                              
                                                                                
BNTFY700 LHI   R1,107              SEND A MESSAGE OUT                           
         BRAS  RE,PUTMESS                                                       
                                                                                
BNTFY800 L     R2,VNOTIFY          R2=TABLE                                     
         ST    R4,0(,R2)           SAVE NUMBER OF ENTRIES FOR SEARCH            
*                                                                               
BNTFYXIT LHI   R1,106                                                           
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         DROP  R2,R3,RA                                                         
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD TABLE OF DDS AUTHORIZATION BITS                               *         
***********************************************************************         
*&&DO                                                                           
BLDDDS   NTR1  BASE=*,LABEL=*                                                   
         LHI   R1,78                                                            
         BRAS  RE,PUTMESS                                                       
         L     RA,VSYSFAC                                                       
         USING SYSFACD,RA          RA=A(SYSTEM FACILITIES)                      
*                                                                               
         L     R3,ASTRTIOA                                                      
         USING CT3REC,R3           R3=A(FACPAK RECORDS)                         
         XC    CT3KEY,CT3KEY                                                    
         MVI   CT3KTYP,CT3KTYPQ                                                 
         MVI   CT3DSUB,CT3DDSDL    DDS SECURITY                                 
         GOTO1 VDATAMGR,DMCB,DMREAD,DMCTFILE,CT3KEY,CT3KEY                      
         CLI   8(R1),0                                                          
         BE    BDDS02                                                           
         BRAS  RE,IOERROR                                                       
         ABEND 360,DUMP                                                         
         DC    H'0'                                                             
*                                                                               
BDDS02   ICM   R2,15,VDDSDLTB      R2=TABLE                                     
         BZ    BLDDDSX                                                          
         XC    FULL,FULL           INIT DEPT LEV                                
*                                                                               
         LA    R3,CT3DATA          POINT TO DATA AREA                           
         USING CTDDSSD,R3                                                       
BDDS04   CLI   CTDDSDL,CTDDSDLQ                                                 
         BNE   BDDS06                                                           
         CLC   CTDDSDLV,FULL+3     IS THIS THE RIGHT ELEMENT                    
         BNE   BDDS08                                                           
         MVC   0(8,R2),CTDDSBIT    MOVE IN BITS                                 
*                                                                               
BDDS06   XR    R0,R0               NEXT ELEMENT                                 
         ICM   R0,1,CTDDSLEN                                                    
         BZ    BLDDDSX                                                          
         AR    R3,R0                                                            
*                                                                               
BDDS08   LLC   R1,FULL+3           BUMP DEPT LEVEL                              
         AHI   R1,1                                                             
         ST    R1,FULL                                                          
         LA    R2,8(R2)                                                         
         CLI   FULL+3,0            UNTIL WE'VE GONE FULL CIRCLE                 
         BNE   BDDS04                                                           
*                                                                               
BLDDDSX  LHI   R1,79                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         DROP  R3,RA                                                            
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD DATA DICTIONARY IN HIGH CORE                                  *         
***********************************************************************         
BLDDICTS NTR1  BASE=*,LABEL=*                                                   
         LA    R1,80                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     RA,VSYSFAC                                                       
         USING SYSFACD,RA          RA=A(SYSTEM FACILITIES)                      
         L     R7,VSSB                                                          
         USING SSBD,R7             R7=V(SSB)                                    
*                                                                               
         L     R0,XALDDIC          DATA DICTIONARY BUFFER                       
         STCM  R0,15,SSBDICTG      SAVE AMOUNT OF SPACE IN SSB                  
         L     R1,XAADDIC                                                       
         A     R1,XALOCORE                                                      
         STCM  R1,15,SSBADICT      SAVE ADDRESS ALSO                            
*                                                                               
         SAM31                                                                  
         ICM   R1,15,SSBADICT      SET TAG                                      
         MVC   0(L'DATADICT,R1),DATADICT                                        
         AHI   R1,(DDTBLNQ+8+8)    ALLOW TAG,ADDRTAB,SPACE                      
         ST    R1,DDNXTDIC         A(TO STORE NEXT DICTIONARY)                  
         SAM24                                                                  
*                                                                               
         MVC   DDSYTBL,EFFS        INITIALIZE SYSTEMS-PROCESSED TABLE           
         MVI   DDBSYS,1            SET SERVICE SYSTEM                           
         MVI   DDQSYS,C'1'                                                      
         MVI   DDBLNG,0            SET ENGLISH LANGUAGE                         
         MVI   DDQLNG,C'0'                                                      
         MVC   DDSYTBL(L'DDBSYS),DDBSYS                                         
*                                                                               
BDIC04   XC    PLIST,PLIST                                                      
         LA    RF,PLIST            BUILD PARAMETER BLOCK FOR DDBLDICT           
         USING BLDICTD,RF                                                       
         MVC   P1IATAB,DDNXTDIC    P1=A(DICTIONARY BUILD AREA)                  
         MVC   P2IADTCN,VDATCON    P2=BYTES 1-3: A(DATCON)                      
         MVC   P2ILANG,DDBLNG      P2=BYTE   0 : LANGUAGE CODE                  
         MVC   P3IADMGR,VDATAMGR   P3=BYTES 1-3: A(DATAMGR)                     
         MVI   P3ITEST,C'A'        P3=BYTE   0 : TEST VERSION                   
*                                                                               
         L     R0,SSBDICTG                                                      
         A     R0,SSBADICT                                                      
         S     R0,DDNXTDIC                                                      
         ST    R0,P4ILTAB          P4=SPACE ALLOTTED FOR THIS DICTNRY           
         BP    BDIC06                                                           
*                                                                               
         LA    RE,DCTNOBLD                                                      
         ST    RE,P4OAERMS                                                      
         MVI   P4OERRCD,BDCTEHRD                                                
         XC    P3ORDCNT,P3ORDCNT                                                
         B     BDIC08                                                           
*                                                                               
BDIC06   MVC   P5ISYST,DDBSYS      P5=BYTE 0 IS SYSTEM NUM                      
         GOTO1 VBLDDICT,DMCB,PLIST                                              
         BNE   BDIC08                                                           
         MVC   XTRAMESS,=CL8'T00DXX  '                                          
         MVC   XTRAMESS+04(L'DDQSL),DDQSL                                       
         LHI   R1,85                                                            
*&&DO*&& BRAS  RE,PUTMESS          OUTPUT GOOD FOR DEBUGGING                    
         B     BDIC10                                                           
*                                                                               
BDIC08   LA    RF,PLIST                                                         
         USING BLDICTD,RF                                                       
         MVI   DDERTYP,C'S'         SET SORT OR HARD ERROR                      
         TM    P4OERRCD,BDCTESFT                                                
         BO    *+8                                                              
         MVI   DDERTYP,C'H'                                                     
*                                                                               
         L     RE,P4OAERMS          RE=A(40-CHAR MSG FROM BLDICT)               
         MVI   TEMP+00,C'X'         NO RESPONSE REQUIRED                        
         MVC   TEMP+01(40),0(RE)                                                
         MVC   TEMP+40(09),SPACES                                               
         XR    R1,R1                                                            
         LA    R8,TEMP                                                          
         BRAS  RE,PUTMESS                                                       
*                                                                               
         MVC   XTRAMESS,=CL8'* SOFT *'                                          
         TM    P4OERRCD,BDCTESFT                                                
         BO    *+10                                                             
         MVC   XTRAMESS,=CL8'* HARD *'                                          
         LHI   R1,82                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         MVC   XTRAMESS,=CL8'T00DXX  '                                          
         MVC   XTRAMESS+04(L'DDQSL),DDQSL                                       
         LHI   R1,83                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
BDIC10   LA    RF,PLIST                                                         
         USING BLDICTD,RF                                                       
         OC    P3ORDCNT,P3ORDCNT   EXIT IF NO ENTRIES IN DICT                   
         BZ    BDIC12                                                           
*                                                                               
         SAM31                                                                  
         LLC   R0,DDBLNG           INDEX TO LANGUAGE                            
         SLL   R0,2                                                             
         LLC   R1,DDBSYS           INDEX TO SYSTEM                              
         SLL   R1,5                                                             
         AR    R1,R0               R1=DISP INTO TABLE OF DICT ADDRS             
         A     R1,SSBADICT                                                      
         AHI   R1,8                BUMP PASS ID TAG                             
*                                                                               
         ICM   R0,15,DDNXTDIC                                                   
         ST    R0,0(R1)            ENTER ADDRESS OF DICT INTO TABLE             
         A     R0,P2ODCTLN         L'DICTIONARY                                 
         ST    R0,DDNXTDIC                                                      
*                                                                               
         SAM24                     SET TO RETURN FROM 31-BIT MODE               
         DROP  RF                                                               
*                                                                               
BDIC12   L     R6,SSBALANG         LANGUAGE IS INNER LOOP                       
         USING LANGTABD,R6                                                      
         LH    RE,0(R6)                                                         
         L     RF,2(R6)                                                         
         LA    R6,6(R6)                                                         
*                                                                               
BDIC14   CLC   LANGCODE,DDBLNG     FIND THE LANGUAGE JUST PROCESSED             
         BE    BDIC16                                                           
         BXLE  R6,RE,BDIC14                                                     
         LA    R1,84                                                            
         BRAS  RE,PUTMESS                                                       
         ABEND 362,DUMP                                                         
*                                                                               
BDIC16   AR    R6,RE               GET THE NEXT ONE IN LIST                     
         CLI   LANGOVL1,0          EXIT ON FIRST EMPTY ENTRY                    
         BE    BDIC18                                                           
         CR    R6,RF               EXIT IF END OF TABLE                         
         BL    BDIC20                                                           
*                                                                               
BDIC18   MVI   DDBLNG,0            RESET LANGUAGE CODE                          
         MVI   DDQLNG,C'0'                                                      
         B     BDIC22              CHECK SYSTEM LOOP                            
*                                                                               
BDIC20   MVC   DDBLNG,LANGCODE                                                  
         CLI   DDBLNG,2            DONT PROCESS OTHER ENGL LANG CODES           
         BNH   BDIC12                                                           
         CLI   LANGSUB,0           DONT PROCESS SUB LANGUAGE ENTRIES            
         BNE   BDIC12                                                           
         MVC   DDQLNG,DDBLNG       SET EBCDIC LANGUAGE CODE                     
         OI    DDQLNG,X'F0'                                                     
         B     BDIC04                                                           
*                                                                               
BDIC22   LA    R6,SYSLST           SYSTEM IS OUTER LOOP                         
         USING SYSLSTD,R6                                                       
         LH    RE,0(R6)                                                         
         LA    RF,EXSYSLX-1                                                     
         LA    R6,6(R6)                                                         
*                                                                               
BDIC24   CLC   SYSLNUM,DDBSYS      FIND SYSTEM JUST PROCESSED                   
         BE    BDIC26                                                           
         BXLE  R6,RE,BDIC24                                                     
         LA    R1,84                                                            
         BRAS  RE,PUTMESS                                                       
         ABEND 363,DUMP                                                         
*                                                                               
BDIC26   AR    R6,RE               GET NEXT SYSTEM IN LIST                      
         CLC   EXSYSLX,0(R6)                                                    
         BE    BDIC34              END OF TABLE                                 
*                                                                               
         MVC   DDBSYS,SYSLNUM                                                   
         LA    R1,DDSYTBL          CHECK IF SYSTEM ALREADY PROCESSED            
BDIC28   CLI   0(R1),X'FF'                                                      
         BE    BDIC30                                                           
         CLC   DDBSYS,0(R1)        MATCH FOUND, SYS ALREADY PROCESSED           
         BE    BDIC26              GET NEXT SYSTEM IN LIST                      
         AHI   R1,1                                                             
         B     BDIC28                                                           
*                                                                               
BDIC30   MVC   0(1,R1),DDBSYS      PUT SYS CODE IN PROCESSED TABLE              
         MVC   DDQSYS,DDBSYS       SET EBCDIC SYSTEM CODE                       
         CLI   DDQSYS,9                                                         
         BH    BDIC32                                                           
         OI    DDQSYS,X'F0'                                                     
         B     BDIC04                                                           
*                                                                               
BDIC32   LLC   R1,DDQSYS                                                        
         AHI   R1,-9                                                            
         STC   R1,DDQSYS                                                        
         OI    DDQSYS,X'C0'                                                     
         B     BDIC04                                                           
         DROP  R6                                                               
*                                                                               
BDIC34   L     R0,DDNXTDIC                                                      
         S     R0,SSBADICT         GET USED LENGTH                              
         L     R1,SSBADICT                                                      
         AHI   R1,DDTBLNQ+8                                                     
*                                                                               
         SAM31                                                                  
         ST    R0,0(R1)            STORE AMOUNT USED                            
         SAM24                                                                  
*                                                                               
         MVC   SSBADICX,DDNXTDIC                                                
         CLC   DDNXTDIC,SSBXAHI                                                 
         BL    *+10                                                             
         MVC   SSBXAHI,DDNXTDIC                                                 
*                                                                               
BLDDICTX LA    R1,81                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         DROP  R7,RA                                                            
*                                                                               
DCTNOBLD DC    CL40'DICTIONARY NOT BUILT'                                       
*                                                                               
       ++INCLUDE FASYSLST                                                       
         ORG   SYSLSTX                                                          
         DC    X'0F0F',C'GENERALGEN',C'  ',X'0000'                              
EXSYSLX  DC    X'FFFF'                                                          
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO SET UP GETFLD RECORDS ON FIRST TIME FOR SYSTEM           *         
***********************************************************************         
GFMAX    EQU   5000                MAXIMUM NUMBER OF RECORDS IN TABLE           
*                                                                               
GETFLD   NTR1  BASE=*,LABEL=*                                                   
         LHI   R1,86                                                            
         BRAS  RE,PUTMESS                                                       
*                                                                               
         L     RA,VSYSFAC                                                       
         USING SYSFACD,RA                                                       
         L     R7,VSSB                                                          
         USING SSBD,R7                                                          
         LHI   RF,GFMAX                                                         
         STH   RF,SSBGFREQ                                                      
*                                                                               
         SAM31                     SWITCH TO XA MODE                            
*                                                                               
         L     R1,XAAGFRC                                                       
         A     R1,XALOCORE                                                      
         ST    R1,SSBGFIND         SET UP A(POINTER ARRAY)                      
*                                                                               
         LHI   RF,GFMAX                                                         
         MHI   RF,HXFDRL           LENGTH OF POINTER ARRAY                      
         AR    R1,RF                                                            
*                                                                               
         ST    R1,SSBGFTAB         SET UP A(TABLE) AFTER INDEX                  
         LHI   RF,GFMAX                                                         
         MHI   RF,HIFDRL                                                        
         AR    R1,RF               A(END OF BLOCK)                              
*                                                                               
         L     RF,SSBGFTAB                                                      
         MVC   0(L'LOCK,RF),LOCK   SET I AM INITIALISING TABLE                  
         LA    R2,L'LOCK(RF)       R2=A(FORMATTED ELEMENTS)                     
*                                                                               
         ST    R2,FULL                                                          
         L     R3,SSBGFIND         R3=A(POINTER ARRAY)                          
         L     R4,ASTRTIOA         R4=A(FILE RECORD)                            
         USING HIFDRD,R2                                                        
         USING HXFDRD,R3                                                        
         USING FDRRECD,R4                                                       
*                                                                               
X        USING FDRRECD,FKEY                                                     
         XC    X.FDRKEY,X.FDRKEY                                                
         MVI   X.FDRKMIN,FDRKMINQ                                               
         MVI   X.FDRKTYP,FDRKTYPQ                                               
         LA    R1,PLIST                                                         
         LA    RE,RDHI                                                          
         B     *+8                                                              
*                                                                               
GFLD02   LA    RE,RSEQ                                                          
         ST    RE,0(R1)            P1=A(COMMAND)                                
         LA    RE,=CL8'GENDIR'                                                  
         ST    RE,4(R1)            P2=A(FILE)                                   
         LA    RE,FKEY                                                          
         ST    RE,8(R1)            P3=A(FKEY)                                   
         ST    RE,12(R1)           P4=A(IOAREA)-THE KEY AREA FOR DIR            
         L     RF,VDATAMGR                                                      
         BASSM RE,RF               SWITCH BACK INTO 24-BIT MODE                 
         BNE   GFLDNO                                                           
*                                                                               
GFLD04   CLC   X.FDRKMIN(L'FDRKMIN+L'FDRKTYP),=AL1(FDRKMINQ,FDRKTYPQ)           
         BE    GFLD06              STILL READING FIELD RECORDS                  
         SAM31                                                                  
         B     GFLD16                                                           
*                                                                               
GFLD06   GOTOX VDATAMGR,PLIST,GETR,=CL8'GENFILE',X.FDRKDA,             *        
               ASTRTIOA,DMWORK                                                  
         BNE   GFLDNO                                                           
         DROP  X                                                                
*                                                                               
         SAM31                     SWITCH BACK INTO 31-BIT MODE                 
         USING FDRRECD,R4                                                       
GFLD08   MVC   HXSYS,FDRKSYS       SYSTEM                                       
         MVC   HXPRG,FDRKPRG       PROGRAM                                      
         MVC   HXREC,FDRKREC       RECORD                                       
         MVC   HXNUM,FDRKNUM       FIELD NUMBER                                 
         MVC   HXCTRY,FDRKCTRY     COUNTRY                                      
         MVC   HXSUB,FDRKSUB       SUB-COUNTRY                                  
         MVC   HXTEST,FDRKTEST     TEST PHASE                                   
         LR    RF,R2                                                            
         S     RF,FULL                                                          
         STCM  RF,7,HXADDR         DISP TO THIS ENTRY FROM START                
*                                                                               
         MVC   HISYS,FDRKSYS       SYSTEM                                       
         MVC   HIPRG,FDRKPRG       PROGRAM                                      
         MVC   HIREC,FDRKREC       RECORD                                       
         MVC   HINUM,FDRKNUM       FIELD NUMBER                                 
         MVC   HICTRY,FDRKCTRY     COUNTRY                                      
         MVC   HISUB,FDRKSUB       SUB-COUNTRY                                  
         MVC   HITEST,FDRKTEST     TEST PHASE                                   
         MVC   HISTAT,FDRFSTAT     STATUS                                       
*                                                                               
         LA    R5,FDRRECD+FDRFIRST                                              
         USING FDRELD,R5                                                        
         XR    RF,RF                                                            
*                                                                               
GFLD10   CLI   FDREL,0             TEST END OF RECORD                           
         BE    GFLD12                                                           
         CLI   FDREL,FDRELQ        TEST FIELD ELEMENT                           
         BNE   *+10                                                             
         MVC   HIFDR(FDRLNQ),FDRELD                                             
         CLI   FDREL,FLTRLQ        TEST FILTER ELEMENT                          
         BNE   *+10                                                             
         MVC   HIFLTR(FLTRLNQ),FDRELD                                           
         LLC   RF,FDRLN                                                         
         LA    R5,0(RF,R5)         NEXT ELEMENT                                 
         B     GFLD10                                                           
*                                                                               
GFLD12   LH    RF,SSBGFUSD         INCREMENT COUNT OF NUMBER OF RECS            
         LA    RF,1(RF)                                                         
         STH   RF,SSBGFUSD                                                      
         CHI   RF,GFMAX                                                         
         BNH   GFLD14                                                           
         L     RF,SSBGFTAB                                                      
         MVC   0(L'FAIL,RF),FAIL   SET FAILED INIT IN TABLES                    
         SAM24                                                                  
         B     GETFLDX                                                          
*                                                                               
GFLD14   LA    R2,HIFDRL(R2)       NEXT TABLE ENTRY                             
         LA    R3,HXFDRL(R3)       NEXT INDEX ENTRY                             
         B     GFLD02                                                           
         DROP  R2,R3                                                            
*                                                                               
GFLD16   L     RF,SSBGFTAB         FLAG INITIALISATION COMPLETE                 
         MVC   0(L'DONE,RF),DONE                                                
         SAM24                                                                  
         B     GETFLDX                                                          
*                                                                               
GFLDNO   SAM31                     SWITCH BACK INTO 31-BIT MODE                 
         L     R5,SSBGFTAB                                                      
         MVC   0(L'FAIL,R5),FAIL   SET FAILED INIT IN TABLES                    
         SAM24                                                                  
         LHI   R1,88               OUTPUT INIT FAILED MESSAGE                   
         BRAS  RE,PUTMESS                                                       
*                                                                               
GETFLDX  LHI   R1,87                                                            
         BRAS  RE,PUTMESS                                                       
         J     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* PROCESS LOCKET SUB ACTIONS                                          *         
***********************************************************************         
LOCKET   NTR1  BASE=*,LABEL=*                                                   
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTLOCKET)                                            
         GOTO1 VLOCKSPC,DUB        LOCK TABLE                                   
         L     RF,DUB+4                                                         
         USING DMSPACED,RF                                                      
         MVC   AUPDTAB,DSPTFRST                                                 
         MVI   AUPDTAB,0           REMOVE POST INFORMATION                      
         MVC   AUPDLEN,DSPTWIDE                                                 
         MVC   AUPDTABX,DSPTEND                                                 
         DROP  RF                                                               
*                                                                               
         L     R7,VSSB                                                          
         USING SSBD,R7                                                          
         GOTO1 VDATCON,DMCB,(3,SSBDATEB),(19,FULL)                              
*                                                                               
LCA02    SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         L     R2,AUPDTAB          R2=FAC UPDATE TABLE                          
         LH    RE,AUPDLEN                                                       
         L     RF,AUPDTABX                                                      
         LAM   AR2,AR2,SSBTBLET                                                 
         SAC   512                                                              
         USING UPDTABD,R2                                                       
*                                                                               
LCA04    CLI   UPDTABT,UPDTLQ                                                   
         BNE   LCA06                                                            
         CLC   UPDLDATE,FULL       DELETE IF NOT TODAY                          
         BNE   LCA08                                                            
*                                                                               
LCA06    BXLE  R2,RE,LCA04                                                      
         B     LCA10                                                            
*                                                                               
LCA08    L     R3,AUPDTABX                                                      
         AHI   R3,1                                                             
         SR    R3,R2               R5=L'DEST                                    
         LR    RE,R2                                                            
         AH    RE,AUPDLEN          RE=SOURCE                                    
         LR    RF,R3                                                            
         SH    RF,AUPDLEN          RF=L'SOURCE                                  
         CPYA  ARE,AR2                                                          
         MVCL  R2,RE               MOVE & PAD LAST ENTRY WITH ZERO              
         B     LCA02                                                            
*                                                                               
LCA10    SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTLOCKET)                                            
         OI    DUB,X'10'                                                        
         GOTO1 VLOCKSPC,DUB        FREE TABLE                                   
         LAM   AR0,ARF,ARZERO                                                   
         B     EXITOK                                                           
         DROP  R2,R7                                                            
                                                                                
         TITLE 'FASTART - ROUTINES/CONSTANTS ADDRESSED BY R9'                   
LITERALS DS    0F                                                               
                                                                                
***********************************************************************         
* EXIT POINTS AND OTHER HANDY ROUTINES KEPT ADDRESSIBLE FROM R9       *         
***********************************************************************         
SETBXLE  LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
         BR    RE                                                               
*                                                                               
SETNOEOF L     R4,DMCB+12          GET A(SYSFLES LIST)                          
         LLC   R0,3(R4)                                                         
         LA    R4,4(R4)            POINT TO FIRST FILE                          
SETNO2   CLC   DUB+1(3),5(R4)      MATCH DTF ADDRESS                            
         JE    SETNO4                                                           
         LA    R4,8(R4)                                                         
         BRCT  R0,SETNO2                                                        
         DC    H'0'                                                             
SETNO4   OI    0(R4),X'04'         SET NO EOF SEARCH INDICATOR                  
         BR    RE                                                               
*                                                                               
STRTEOJ  LR    R0,RE               SET EOJ REQUIRED                             
         L     R1,APARM                                                         
         XC    0(4,R1),0(R1)                                                    
         J     STRTEXIT                                                         
*                                                                               
STRTEXIT L     RD,SAVERD                                                        
         XBASE                     EXIT BACK TO CALLER                          
*                                                                               
STRTCNCL LR    R0,RE               SET CANCEL REQUIRED                          
         ABEND 0,DUMP                                                           
*                                                                               
EXITOK   CR    RB,RB                                                            
         J     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         J     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS - ALL ADDRESSED FROM R9                      *         
***********************************************************************         
         DC    CL8'LITERALS'                                                    
EOR      EQU   0                                                                
CTMAXLEN EQU   1024                MAX LENGTH OF CTFILE 'U' RECORD              
PHLSTRK  EQU   36                  PHASE LIST TRACK NUMBER                      
PHLSPGM  EQU   40                  FIRST PROGRAM TRACK NUMBER                   
PHLSSPR  EQU   100                 NUMBER OF SPARE ENTRIES                      
RLMINPHS EQU   80                  MIN NUM ENTRIES BETWEEN                      
RCVBKSZQ EQU   13682               4 RECORDS PER TRACK, 6K RECORDS              
*                                                                               
DDMINSZ  DC    A(4*1024*1024)      MINIMUM DATA DICTIONARY BLOCK SIZE           
DDTBLNQ  EQU   16*8*4              L'TABLE=#SYS*#LANG*#BYTES                    
*                                                                               
VADFIRST DC    V(ADFIRST)                                                       
VBOOT    DC    V(FALOAD)                                                        
VCTFILE  DC    V(CTFIL1)                                                        
VDATCON  DC    V(DATCON)                                                        
VISDDS   DC    V(ISDDS)                                                         
VISREAD  DC    V(ISREAD)                                                        
VGETDAY  DC    V(GETDAY)                                                        
VLOADER  DC    V(LOADER)                                                        
VPOWWOWS DC    V(POWWOWS)                                                       
VPQOPEN  DC    V(FAPQOPEN)                                                      
VRDTRK   DC    V(RDTRK)                                                         
VSYSFAC  DC    V(SYSFAC)                                                        
VSYSFLES DC    V(SYSFLES)                                                       
VUPDID   DC    V(UPDID)                                                         
VWORKER  DC    V(WORKER)                                                        
VWTERASE DC    V(WTERASE)                                                       
VHEXOUT  DC    V(HEXOUT)                                                        
VDDSDLTB DC    V(DDSDLTAB)         V(DDSDLTAB)                                  
VNOTIFY  DC    V(NOTFYTAB)         V(NOTIFY TABLE)                              
VBLDDICT DC    V(DDBLDICT)         DICTIONARY BUILD ROUTINE                     
*                                                                               
ASTART   DC    A(START)            ROUTINE TO DO A 'START'                      
ARESTART DC    A(RESTART)          ROUTINE TO DO A 'RESTART'                    
*                                                                               
ASTRTIOA DC    A(STRTIOA)          IO AREA WITHIN FASTART                       
*                                                                               
ISREAD   DC    F'1'                                                             
ISRDSEQ  DC    F'2'                                                             
REPLY    DC    CL8' '              MSGS TO/FROM OPERATOR                        
XTRAMESS DC    CL8' '                                                           
MESSAGE  DC    CL60' '                                                          
ADRSYSID DC    A(0)                                                             
FAC3CHR  DC    CL3' '                                                           
FACMSGID DC    CL4' '                                                           
*                                                                               
RCVRRSTR DC    X'010100'           RECOVER/RESTORE PHASE NAME                   
UPSI     DC    X'00'                                                            
FLAG     DC    X'00'                                                            
STRTMODE DC    C'S'                S=START,R=RESTART                            
         DC    C' '                                                             
UTLLEN   DC    AL2(TUTLXALN)                                                    
VUTLZERO DC    V(UTLZERO)                                                       
TRKLEN   DC    A(60*1024)         LENGTH OF STRTBUF                             
PIOLEN   DC    A(80000)           LENGTH OF PROTECTED IO AREA                   
*                                                                               
COMMAND  DC    CL8' '                                                           
DMCTFILE DC    CL8'CTFILE'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
SYSFLES  DC    CL8'SYSFLES'                                                     
TEMPSTR  DC    CL8'TEMPSTR'                                                     
TEMPEST  DC    CL8'TEMPEST'                                                     
DATADICT DC    CL8'DATADICT'                                                    
*                                                                               
HOURS24  DC    A(24*60*100)        24 HOURS IN TIME BIN UNITS                   
*                                                                               
ACHKPT1  DC    A(0)                A(CURRENT TWA CHECKPOINT)                    
ACHKPT2  DC    A(0)                A(HIGHEST TWA CHECKPOINT)                    
*                                                                               
LENTWA   DC    H'14336'            LENGTH OF TWA RECORD                         
LENCHK   DC    Y(CHKPTLEN)         LENGTH OF CHECKPOINT                         
STRCHK   DC    Y(CHKPTDSP)         START OF CHECKPOINT AREA                     
*                                                                               
ARZERO   DC    16F'0'                                                           
*                                                                               
OLDPHLEN DC    F'200000'           AMOUNT OF STORAGE TO READ OLD FAPHLS         
*                                                                               
SPACES   DC    80C' '                                                           
EFFS     DC    32X'FF'                                                          
SETNAMA  DC    C'123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'                           
*                                                                               
RDHI     DC    CL8'DMRDHI'                                                      
RSEQ     DC    CL8'DMRSEQ'                                                      
GETR     DC    CL8'GETREC'                                                      
LOCK     DC    CL4'LOCK'                                                        
FAIL     DC    CL4'FAIL'                                                        
DONE     DC    CL4'DONE'                                                        
*                                                                               
IUTL     DC    CL12'*UTL*UTL*UTL'                                               
IPRQ     DC    CL12'*PRQ*PRQ*PRQ'                                               
IPQE     DC    CL12'*PQE*PQE*PQE'                                               
         EJECT                                                                  
***********************************************************************         
* PARAMETERS PASSED FROM FABOOT                                       *         
***********************************************************************         
         DS    0L                                                               
         DC    CL16'****BOOTPARMS***'                                           
LOADPARM DS    0F                  PARAMS PASSED FROM FABOOT                    
LOADCMRG DC    A(0)                ADDR OF DOS COMRG/MVS PARAM LIST             
LOADUPSI DC    F'0'                UPSI VALUE                                   
LOADLOCO DC    A(0)                LOW  CORE OF FACPAK PHASE                    
LOADHIC  DC    A(0)                HIGH CORE OF FACPAK PHASE                    
LOADHICX DC    A(0)                HIGH CORE OF DOS PARTN/MVS ADRSPC            
FACPAK   DC    CL8' '              MODULE NAME FACPAK                           
         DC    CL8' '              MODULE NAME FASTART                          
         DC    XL8'00'             N/D                                          
FAPARM   DC    CL8' '              MODULE NAME RUN PARAMETERS                   
FATASKS  DC    XL1'00'             OVERRIDE NUMBER OF TASKS                     
FAPROT   DC    CL1' '              FACPAK STORAGE PROTECTION FLAG               
FATOR    DC    CL1' '              TOR STATUS                                   
FAAORID  DC    CL1' '              AOR STATUS                                   
FAWLM    DC    CL1' '              WLM ENABLES                                  
FALP2    DC    CL1' '              LP2 STATUS                                   
         DC    XL2'00'             N/D                                          
MSGLIB   DC    CL8' '              ANSWER LOAD PROGRAM LIB                      
MSGRST   DC    CL8' '              ANSWER IS THIS A RESTART                     
MSGDAY   DC    CL8' '              ANSWER DAY VALUE                             
MSGSYSO  DC    CL8' '              ANSWER SYSTEM OVERRIDE                       
MSGLINO  DC    CL8' '              ANSWER LINE OVERRIDE                         
LOADPARX DS    0F                                                               
*                                                                               
LOADDATE DC    CL8' '                                                           
VBUFFERS DC    A(0)                                                             
VBUFFERX DC    A(0)                                                             
*&&UK                                                                           
MEDDSTAB DC    C'MED1DSP'          UK MEDIA SYSTEM DATASPACES                   
         DC    C'MED2FQA'                                                       
         DC    C'MED3NEW'                                                       
         DC    C'MED4TST'                                                       
         DC    C'MED5CSC'                                                       
MEDDSNQ  EQU   (*-MEDDSTAB)/L'MEDDSTAB                                          
         DC    C'???????'                                                       
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* LITERAL POOL                                                        *         
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* FAPARM CONSTANTS LOADED                                             *         
***********************************************************************         
         DS    0L                                                               
         DC    CL16'****FAPARMS*****'                                           
       ++INCLUDE FAPARMDEF                                                      
         EJECT                                                                  
***********************************************************************         
* MESSAGE TABLE ENTRIES                                               *         
***********************************************************************         
MESSTAB  DS    0CL50                                                            
MESS1    DC    CL50'1All files to be opened Read Only?'                         
MESS2    DC    CL50'1IS THIS A RESTART ?'                                       
MESS3    DC    CL50'XCHKPT DATE NOT SAME AS IPL. EXECUTION TERMINATED'          
MESS4    DC    CL50'8CHKPT DATE NOT SAME AS IPL. WHAT NOW ?'                    
MESS5    DC    CL50'XINVALID REPLY. EXECUTION TERMINATED'                       
MESS6    DC    CL50'XOnly had storage for NN tasks - Requested NN'              
MESS7    DC    CL50'7ENTER SYSTEMS NOT TO BE STARTED OR EOB'                    
MESS8    DC    CL50'7ENTER SYSTEMS TO BE STARTED OR EOB'                        
MESS9    DC    CL50'XXXXXXXXX SYSTEM NOT RECOGNIZED. PLEASE CORRECT'            
MESS10   DC    CL50'1RECOVERY REQUIRED FOR XXXXXXXX ?'                          
MESS11   DC    CL50'XINVALID CPU ID FOR XXXXXXXX'                               
MESS12   DC    CL50'XMISSING CPU ID FOR XXXXXXXX'                               
MESS13   DC    CL50'XConcurrent file updates for XXXXXXXX'                      
MESS14   DC    CL50'XCompleted starting system XXXXXXXX'                        
MESS15   DC    CL50'8ENTER LINE ID FOR NOP OR EOB'                              
MESS16   DC    CL50'8ENTER LINE ID/LIST FOR OP OR EOB'                          
MESS17   DC    CL50'XXXXXXXXX LINE ID OR LIST ID IS INVALID'                    
MESS18   DC    CL50'XSYSTEM INITIALIZATION COMPLETE'                            
MESS19   DC    CL50'XPhase XXXXXXXX not in CIL'                                 
MESS20   DC    CL50'XPHASE XXXXXXXX TOO BIG'                                    
MESS21   DC    CL50'3IPL DATE IS XXXXXXXX - ENTER DAY OF WEEK'                  
MESS22   DC    CL50'XINCORRECT RESPONSE. EXECUTION TERMINATED'                  
MESS23   DC    CL50'1ANY SYSTEM OVERRIDES ?'                                    
MESS24   DC    CL50'1ANY LINE OVERRIDES ?'                                      
MESS25   DC    CL50'XNo restart possible - execution terminated'                
MESS26   DC    CL50'XXXXXXXXX PT LIST RECORD NOT FOUND'                         
MESS27   DC    CL50'XPRTQUE I/O ERROR. EXECUTION TERMINATED'                    
MESS28   DC    CL50'XPRTQUE INITIALISED'                                        
MESS29   DC    CL50'XVTAM ACB OPEN FAILURE'                                     
MESS30   DC    CL50'XRELOAD XXXXXXXX'                                           
MESS31   DC    CL50'XCompleted starting system XXXXXXXX Read Only'              
MESS32   DC    CL50'XBeginning starting system XXXXXXXX'                        
MESS33   DC    CL50'XNo-Op system XXXXXXXX not started'                         
MESS34   DC    CL50'XDataspace mismatch on XXXXXXXX'                            
MESS35   DC    CL50'XBeginning file renames'                                    
MESS36   DC    CL50'XCompleted file renames'                                    
MESS37   DC    CL50'XBeginning recovery buffer allocation'                      
MESS38   DC    CL50'XCompleted recovery buffer allocation'                      
MESS39   DC    CL50'XBeginning bootlist parameter processing'                   
MESS40   DC    CL50'XCompleted bootlist parameter processing'                   
MESS41   DC    CL50'XBeginning setting OP/NOP program status'                   
MESS42   DC    CL50'XCompleted setting OP/NOP program status'                   
MESS43   DC    CL50'XBeginning fixing XA TBUFF storage'                         
MESS44   DC    CL50'XCompleted fixing XA TBUFF storage'                         
MESS45   DC    CL50'XBeginning acquiring dataspaces'                            
MESS46   DC    CL50'XCompleted acquiring dataspaces'                            
MESS47   DC    CL50'4DMGR dataspace not found'                                  
MESS48   DC    CL50'4TABS dataspace not found'                                  
MESS49   DC    CL50'XBeginning acquiring TOR slot in DMGR dataspace'            
MESS50   DC    CL50'XCompleted acquiring TOR slot in DMGR dataspace'            
MESS51   DC    CL50'XBeginning building dummy UTL entries'                      
MESS52   DC    CL50'XCompleted building dummy UTL entries'                      
MESS53   DC    CL50'XBeginning resetting TEMPEST buffer'                        
MESS54   DC    CL50'XCompleted resetting TEMPEST buffer'                        
MESS55   DC    CL50'XBeginning writing new checkpoints'                         
MESS56   DC    CL50'XCompleted writing new checkpoints'                         
MESS57   DC    CL50'XBeginning building phase lists in core'                    
MESS58   DC    CL50'XCompleted building phase lists in core'                    
MESS59   DC    CL50'XBeginning setting operator command interface'              
MESS60   DC    CL50'XCompleted setting operator command interface'              
MESS61   DC    CL50'XBeginning start specific logic'                            
MESS62   DC    CL50'XCompleted start specific logic'                            
MESS63   DC    CL50'XBeginning restart specific logic'                          
MESS64   DC    CL50'XCompleted restart specific logic'                          
MESS65   DC    CL50'XBeginning restoring from checkpoint areas'                 
MESS66   DC    CL50'XCompleted restoring from checkpoint areas'                 
MESS67   DC    CL50'XBeginning rebuilding UTL entries'                          
MESS68   DC    CL50'XCompleted rebuilding UTL entries'                          
MESS69   DC    CL50'XBeginning resetting SE queues'                             
MESS70   DC    CL50'XCompleted resetting SE queues'                             
MESS71   DC    CL50'XBeginning restoring SSB save data'                         
MESS72   DC    CL50'XCompleted restoring SSB save data'                         
MESS73   DC    CL50'XBeginning writing updated checkpoint data'                 
MESS74   DC    CL50'XCompleted writing updated checkpoint data'                 
MESS75   DC    CL50'XGETMAIN failure - FASTART abending'                        
MESS76   DC    CL50'XCALLOV failure - FASTART abending'                         
MESS77   DC    CL50'XUnknown error from DMGR'                                   
MESS78   DC    CL50'XBeginning building DDS Dept table'                         
MESS79   DC    CL50'XCompleted building DDS Dept table'                         
MESS80   DC    CL50'XBeginning building Data dictionaries'                      
MESS81   DC    CL50'XCompleted building Data dictionaries'                      
MESS82   DC    CL50'XXXXXXXXX error in Data dictionary build'                   
MESS83   DC    CL50'XDictionary phase XXXXXXXX in error'                        
MESS84   DC    CL50'XInvalid value in Data dictionary routine'                  
MESS85   DC    CL50'XData dictionary XXXXXXXX has been built'                   
MESS86   DC    CL50'XBeginning building GETFLD records'                         
MESS87   DC    CL50'XCompleted building GETFLD records'                         
MESS88   DC    CL50'XGETFLD buffer build failed - using DISK only'              
MESS89   DC    CL50'XFAPARMS phase not found - FASTART abending'                
MESS90   DC    CL50'XBeginning building UTL/PRQ/PQE tables'                     
MESS91   DC    CL50'XCompleted building UTL/PRQ/PQE tables'                     
MESS92   DC    CL50'XProblem with ARREDIT - program abending'                   
MESS93   DC    CL50'X*WARNING* PHLIST Entry XXXXXXXX not found'                 
MESS94   DC    CL50'XPHLIST Entry XXXXXXXX loaded from LOADLIB'                 
MESS95   DC    CL50'4PGMS DATASPACE NOT FOUND'                                  
MESS96   DC    CL50'XCompleted forcing system XXXXXXXX Read Only'               
MESS97   DC    CL50'XBeginning resetting hole value in dataspace'               
MESS98   DC    CL50'XCompleted resetting hole value in dataspace'               
MESS99   DC    CL50'XTask core nnnnk enough for nn tasks nnnnk spare'           
MES100   DC    CL50'4*ERROR* VERSION Table is full - change TABS parms'         
MES101   DC    CL50' Media dataspace job XXXXXXXX not running (MEDX)'           
MES102   DC    CL50'1Start XXXXXXXX and Retry or Ignore. R/I ?'                 
MES103   DC    CL50'XLow on core - Try starting with NET=FACPRODL'              
MES104   DC    CL50'XUse NET=FACPRODL on any AORs for this system too'          
MES105   DC    CL50'XBeginning building Email notify table'                     
MES106   DC    CL50'XCompleted building Email notify table'                     
MES107   DC    CL50'XNeed to increase area for notify table'                    
MES108   DC    CL50'XSize of task is nnnK'                                      
MES109   DC    CL50'XRESTART not possible - no Vermont Buffer xxxxxxx'          
MES110   DC    CL50'XRecovery file DTF not in SELIST xxxxxxx'                   
                                                                                
         DS    0D                                                               
         DC    CL8'IOAREA'                                                      
STRTIOA  DS    8192X                                                            
         DS    0D                                                               
         DC    CL8'STRTWORK'                                                    
STRTWORK DS    2500D               WORK AREA                                    
                                                                                
         TITLE 'FASTART - DSECTS'                                               
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
SAVERD   DS    A                                                                
FULL     DS    F                                                                
*                                                                               
DMCB     DS    0CL24                                                            
DM1      DS    F                                                                
DM2      DS    F                                                                
DM3      DS    F                                                                
DM4      DS    F                                                                
DM5      DS    F                                                                
DM6      DS    F                                                                
*                                                                               
PLIST    DS    0CL24                                                            
PL1      DS    F                                                                
PL2      DS    F                                                                
PL3      DS    F                                                                
PL4      DS    F                                                                
PL5      DS    F                                                                
PL6      DS    F                                                                
*                                                                               
IODMCB   DS    4F                  DMCB FOR IOERROR ROUTINE                     
PM1      DS    F                   PARAMETER BLOCK USED BY PUTMESS              
PM2      DS    F                                                                
PM3      DS    F                                                                
*                                                                               
P1       DS    F                                                                
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
P7       DS    F                                                                
P8       DS    F                                                                
*                                                                               
DMWORK   DS    12D                                                              
*                                                                               
XAACRAP  DS    A                   A(CRAPPER BLOCK)                             
XALCRAP  DS    A                   L'CRAPPER BLOCK                              
XAACHAIN DS    A                   A(CHAIN BLOCK)                               
XALCHAIN DS    A                   L'CHAIN BLOCK                                
XAATSK   DS    A                   A(TASK BASED STORAGE)                        
XALTSK   DS    A                   L'TASK BASED STORAGE                         
XAAPLIST DS    A                   A(PHASE LIST)                                
XALPLIST DS    A                   L'PHASE LIST                                 
XAADDIC  DS    A                   A(DATA DICTIONARY)                           
XALDDIC  DS    A                   L'DATA DICTIONARY                            
XAATSTT  DS    A                   A(TSTTAB)                                    
XALTSTT  DS    A                   L'(TSTTAB)                                   
XAACRCB  DS    A                   A(CORE BUFFER COPY AREA)                     
XALCRCB  DS    A                   L'CORE BUFFER COPY AREA                      
XAAGFRC  DS    A                   A(GETFLD RECORDS)                            
XALGFRC  DS    A                   L'GETFLD RECORDS                             
XAATBUFF DS    A                   A(TBUFFS)                                    
XALTBUFF DS    A                   L'TBUFFS                                     
XAADBUFF DS    A                   A(DUMMY TBUFFS)                              
XALDBUFF DS    A                   L'DUMMY TBUFFS                               
XAAUTL   DS    A                   A(UTL BUFFER)                                
XALUTL   DS    A                   L'UTL BUFFER                                 
XAAPUTL  DS    A                   A(PRIMARY UTL BUFFER)                        
XALPUTL  DS    A                   L'PRIMARY UTL BUFFER                         
XAAISG   DS    A                   A(ISGENQ BUFFER)                             
XALISG   DS    A                   L'ISGENQ BUFFER                              
*                                                                               
HALF     DS    H                                                                
FASMODE  DS    C                                                                
         DS    C                                                                
SAVE     DS    F                                                                
ACOMM    DS    A                   A(OPERATOR COMMS)                            
APARM    DS    A                                                                
ASTRTBUF DS    A                                                                
RELOFACT DS    A                                                                
SVPGMADR DS    A                                                                
SVWRKADR DS    A                                                                
SVTSKADR DS    A                                                                
HICORE   DS    A                                                                
HICORELN DS    A                                                                
HICOREX  DS    A                                                                
XALOCORE DS    A                                                                
XAHICORE DS    A                                                                
APHLIST  DS    A                                                                
TASK     DS    H                                                                
PHLMAX   DS    H                                                                
NOWDATE  DS    PL4                 DATE/TIME NOW                                
NOWTIME  DS    F                                                                
STRDATE  DS    PL4                 DATE/TIME FACPAK WAS FIRST STARTED           
STRTIME  DS    F                                                                
DMPTRKS  DS    F                                                                
DMPCYLS  DS    F                                                                
LNIOA    DS    A                   VALUE SET BY MAXIOA ROUTINE                  
*                                                                               
AUPDTAB  DS    F                                                                
AUPDTABX DS    F                                                                
AUPDLEN  DS    H                                                                
*                                                                               
PRDATE   DS    XL2                                                              
PRKEY    DS    XL27                                                             
WORK     DS    XL32                                                             
TEMP     DS    XL256                                                            
*                                                                               
DDNXTDIC DS    A                   A(TO BUILD & STORE NEXT DICTIONARY)          
DDSYTBL  DS    CL17                TABLE OF SYSTEMS PROCESSED                   
DDBSYS   DS    XL1                 SYSTEM NUM                                   
DDBLNG   DS    XL1                 LANGUAGE NUM                                 
DDQSL    DS    0CL2                EBCDIC SYSTEM/LANGUAGE                       
DDQSYS   DS    CL1                 SYSTEM NUM                                   
DDQLNG   DS    CL1                 LANGUAGE NUM                                 
DDERTYP  DS    CL1                 ERROR TYPE FROM DDBLDICT                     
OPTRONLY DS    XL1                 START SYSTEMS READ-ONLY                      
*                                                                               
RLSDATE  DS    XL3                                                              
RLASPARE DS    A                   START OF SPARE ADDRESS                       
RLAPHOLD DS    A                   CORE ADDRESS OF OLD PHASE LIST               
RLSVDNXT DS    F                                                                
RLPHLEN  DS    F                   PHASE LIST LEN (INCL LOOK-UP TAB)            
RLLIST   DS    XL256                                                            
RLKEY    DS    CL25                                                             
RLDATE   DS    XL3                                                              
RLPGM    DS    XL2                                                              
*                                                                               
RBTHIS   DS    A                   A(THIS REQUEST ADDRESS LIST)                 
DISK1ST  DS    F                                                                
BYTE     DS    X                                                                
*                                                                               
         DS    0D                                                               
PSREC    DS    XL(PROGSPCL)                                                     
         DS    0D                                                               
DPSREC   DS    XL(PROGSPCL)                                                     
*                                                                               
         DS    0D                                                               
LIBUFFR  DS    XL(LIBUFFL)                                                      
         DS    0D                                                               
LIBUFFC  DS    XL(LIBUFFL)                                                      
*                                                                               
FKEY     DS    XL(FDRKLEN)                                                      
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* DSECTS                                                              *         
***********************************************************************         
HXFDRD   DSECT                   FORMAT OF FDREL INDEX BLOCK                    
HXSYS    DS    XL(L'FDRKSYS)                                                    
HXPRG    DS    XL(L'FDRKPRG)                                                    
HXREC    DS    XL(L'FDRKREC)                                                    
HXNUM    DS    XL(L'FDRKNUM)                                                    
HXCTRY   DS    XL(L'FDRKCTRY)                                                   
HXSUB    DS    XL(L'FDRKSUB)                                                    
HXTEST   DS    XL(L'FDRKTEST)                                                   
HXKEY    EQU   *-HXFDRD                                                         
HXADDR   DS    XL3               DISP OF RECORD INTO HIFDRD                     
HXFDRL   EQU   *-HXFDRD                                                         
*                                                                               
HIFDRD   DSECT                   FORMAT OF FDREL IN XA STORAGE                  
HISYS    DS    XL(L'FDRKSYS)                                                    
HIPRG    DS    XL(L'FDRKPRG)                                                    
HIREC    DS    XL(L'FDRKREC)                                                    
HINUM    DS    XL(L'FDRKNUM)                                                    
HICTRY   DS    XL(L'FDRKCTRY)                                                   
HISUB    DS    XL(L'FDRKSUB)                                                    
HITEST   DS    XL(L'FDRKTEST)                                                   
HIKEY    EQU   *-HIFDRD                                                         
HISTAT   DS    XL(L'FDRKSTAT)                                                   
HIFDR    DS    XL(FDRLNQ)                                                       
HIFLTR   DS    XL(FLTRLNQ)                                                      
HIFDRL   EQU   *-HIFDRD                                                         
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*FASYSLSTD                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSLSTD                                                      
         PRINT ON                                                               
*FANTFYD                                                                        
         PRINT OFF                                                              
       ++INCLUDE FANTFYD                                                        
         PRINT ON                                                               
*DDTSARD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DDTSARD                                                        
         PRINT ON                                                               
         EJECT                                                                  
*FACHKPT                                                                        
         PRINT OFF                                                              
       ++INCLUDE FACHKPT                                                        
         PRINT ON                                                               
*DDCHAIND                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDCHAIND                                                       
         PRINT ON                                                               
*DDCRAPPERD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCRAPPERD                                                     
         PRINT ON                                                               
*FAPRQ                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAPRQ                                                          
         PRINT ON                                                               
*DMDTFPH                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDTFPH                                                        
         PRINT ON                                                               
*DMRCVRHDR                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMRCVRHDR                                                      
         PRINT ON                                                               
*DDACCFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDACCFACS                                                      
         PRINT ON                                                               
*CTGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
*FACIDTABD                                                                      
         PRINT OFF                                                              
       ++INCLUDE FACIDTABD                                                      
         PRINT ON                                                               
*GEGENMSG                                                                       
         PRINT OFF                                                              
       ++INCLUDE GEGENMSG                                                       
         PRINT ON                                                               
*DDARREDITD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDARREDITD                                                     
         PRINT ON                                                               
*DDBLDICTD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDBLDICTD                                                      
         PRINT ON                                                               
*DMDTFIS                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDTFIS                                                        
         PRINT ON                                                               
*FADSECTS                                                                       
         PRINT OFF                                                              
       ++INCLUDE FADSECTS                                                       
         PRINT ON                                                               
*GEGENSCR                                                                       
         PRINT OFF                                                              
       ++INCLUDE GEGENSCR                                                       
         PRINT ON                                                               
         EJECT                                                                  
***********************************************************************         
* THE FOLLOWING DSECTS COVER DATASPACE REGIONS                        *         
***********************************************************************         
*DMSPACED                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
*DMDSYSHDR                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMDSYSHDR                                                      
         PRINT ON                                                               
*DMDSHDR                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
*DMTMSHDR                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMTMSHDR                                                       
         PRINT ON                                                               
*DMXTNTD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMXTNTD                                                        
         PRINT ON                                                               
*DMSYSFD                                                                        
         PRINT OFF                                                              
       ++INCLUDE DMSYSFD                                                        
         PRINT ON                                                               
*DMFILTABD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMFILTABD                                                      
         PRINT ON                                                               
*FAPIGFACD                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAPIGFACD                                                      
         PRINT ON                                                               
*DDZIPBLK                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDZIPBLK                                                       
         PRINT ON                                                               
*FAWLMD                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAWLMD                                                         
         PRINT ON                                                               
*FATABSD                                                                        
         PRINT OFF                                                              
       ++INCLUDE FATABSD                                                        
         PRINT ON                                                               
*FAPGMSD                                                                        
         PRINT OFF                                                              
       ++INCLUDE FAPGMSD                                                        
         PRINT ON                                                               
*FATABSDEQU                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FATABSDEQU                                                     
         PRINT  ON                                                              
*FAPGMSDEQU                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FAPGMSDEQU                                                     
         PRINT  ON                                                              
*FAPROGSPCD                                                                     
         PRINT  OFF                                                             
       ++INCLUDE FAPROGSPCD                                                     
         PRINT  ON                                                              
*FATABSDMP                                                                      
         PRINT OFF                                                              
       ++INCLUDE FATABSDMP                                                      
         PRINT ON                                                               
*FATABSTMS                                                                      
         PRINT OFF                                                              
       ++INCLUDE FATABSTMS                                                      
         PRINT ON                                                               
*DMISGENQD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMISGENQD                                                      
         PRINT ON                                                               
                                                                                
***********************************************************************         
* IBM COMMUNICATIONS DSECTS (FOR OPERATOR COMMS)                      *         
***********************************************************************         
         DSECT                                                                  
         IEZCIB                                                                 
         IEZCOM                                                                 
         EJECT                                                                  
***********************************************************************         
* WE USE FASTART AFTER INITIALISATION TO PROVIDE STORAGE FOR THE LOCAL*         
* COPIES OF THE FIRST (2K) PORTIONS OF TBUFFS. IF YOU CHANGE THE ORG  *         
* THAT FOLLOWS THIS THEN YOU HAD BETTER HAVE A *REALLY* GOOD REASON   *         
***********************************************************************         
FASTART  CSECT                                                                  
         ORG   FASTART+((32+TBUFFL+L'TCBID*2+TUTLXALN)*SBMXTSK)                 
         ORG                                                                    
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'017FASTART   01/29/19'                                      
         END                                                                    
