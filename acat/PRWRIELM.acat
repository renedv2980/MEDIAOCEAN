*          DATA SET PRWRIELM   AT LEVEL 025 AS OF 03/09/20                      
*CATALP PRWRIELM                                                                
         TITLE 'PRWRIELM - CHANGE LOG'                                          
         TITLE 'PRWRIELM - EXTRACT GENERIC ELEMENT DATA - ENTRY POINTS'         
         ENTRY IELM                                                             
PRWRIELM CSECT                                                                  
         TITLE 'PRWRIELM - EXTRACT GENERIC ELEMENT DATA - IELM'                 
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT DATA FROM AN ELEMENT BASED ON DISPLACEMENT*         
*         AND LENGTH                                                  *         
*                                                                     *         
*NTRY                                                                 *         
*        R2==>  DRIVER INPUT AREA                                     *         
*        R8==>  SPOOL WORKAREA                                        *         
*        R9==>  PRINTWRITER WORKAREA                                  *         
*        RA==>  DRIVER GLOBAL AREA                                    *         
*        RC==>  GENCON WORKAREA                                       *         
*                                                                     *         
*        GLARGS   = RECORD ID                                         *         
*                   C'B' - BUY      RECORD                            *         
*                   C'C' - CONTRACT RECORD                            *         
*                   C'G' - JOB      RECORD PARM+0 - A(JOBREC)         *         
*                   C'P' - PUB      RECORD                            *         
*                   C'L' - BILLING  RECORD                            *         
*                   C'I' - INVOICE  RECORD                            *         
*                   C'A' - AOR      RECORD                            *         
*                   C'X' - INVOICE  RECORD                            *         
*        GLARGS+1 = ELEMENT ID                                        *         
*        GLARGS+2 = DATA DISPLACEMENT                                 *         
*        GLARGS+3 = DATA LENGTH                                       *         
*        GLARGS+4 = DEFAULT VALUE                                     *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-?    -  DATA                                                *         
*                                                                     *         
***********************************************************************         
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-39024  12/12/19 EXPAND UPID KEYWORD TO REPORT 15 CHARS    *         
***********************************************************************         
         PRINT NOGEN                                                            
IELM     DS    0D                                                               
         NMOD1 0,**#IELM                                                        
*                                                                               
         USING SPOOLD,R8           ESTABLISH SPOOL WORKAREA                     
         USING SYSD,R9             ESTABLISH PRINTWRITER WORKAREA               
         USING GLOBALD,RA          EWSTABLISH DRIVER WORKAREA                   
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
IELMREC  DS    0H                  DETERMINE RECORD                             
*                                                                               
         CLI   GLARGS,C'B'         BUY RECORD IS IN I/O1                        
         BNE   *+16                                                             
         L     R5,AIO1             POINT TO BUY RECORD                          
         LA    RF,PBUYKIDQ         BUY RECORD ID                                
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS,C'C'         CONTRACT RECORD IS IN I/O2                   
         BNE   *+16                                                             
         L     R5,AIO2             POINT TO CONTRACT RECORD                     
         LA    RF,PCONKIDQ         CONTRACT RECORD ID                           
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS,C'P'         PUB RECORD IS IN I/O3                        
         BNE   *+16                                                             
         L     R5,AIO3             POINT TO PUB RECORD                          
         LA    RF,PUBKIDQ          PUB RECORD ID                                
         B     IELMREC1                                                         
*                                                                               
         CLI   GLARGS,C'L'         BILLING RECORD IS IN I/O1                    
         BNE   *+16                                                             
         L     R5,AIO1             POINT TO BILLING RECORD                      
         LA    RF,PBILKIDQ         BILL RECORD ID                               
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS,C'A'         AOR RECORD IS IN ITS OWN PLACE               
         BNE   *+16                                                             
         L     R5,AORECORD         POINT TO AOR RECORD                          
         LA    RF,AORKIDQ          AOR RECORD ID                                
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS,C'G'         JOB RECORD IS IN ITS OWN PLACE               
         BNE   *+16                                                             
         L     R5,0(R1)            POINT TO JOB RECORD                          
         LA    RF,PJOBKIDQ         JOB RECORD ID                                
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS,C'H'         HEADER RECORDS ARE IN OWN SPOTS              
         BNE   IELMHDRN                                                         
*                                                                               
         CLI   GLARGS+1,C'1'       AGENCY HEADER                                
         BNE   *+16                                                             
         L     R5,PBAGYADR         POINT TO AGENCY RECORD                       
         LA    RF,PAGYKIDQ         AGENCY RECORD ID                             
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS+1,C'2'       CLIENT HEADER                                
         BNE   *+16                                                             
         L     R5,PBCLTADR         POINT TO CLIENT RECORD                       
         LA    RF,PCLTKIDQ         CLIENT RECORD ID                             
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS+1,C'3'       PRODUCT HEADER                               
         BNE   *+16                                                             
         L     R5,PBPRDADR         POINT TO PRODUCT RECORD                      
         LA    RF,PPRDKIDQ         PRODUCT RECOED ID                            
         B     IELMRECX                                                         
*                                                                               
         CLI   GLARGS+1,C'4'       ESTIMATE HEADER                              
         BNE   *+16                                                             
         L     R5,PBESTADR         POINT TO ESTIMATE RECORD                     
         LA    RF,PESTKIDQ         ESTIMATE RECORD ID                           
         B     IELMRECX                                                         
*                                                                               
IELMHDRN DS    0H                                                               
*                                                                               
*        INVOICE RECORD ELEMENTS HAVE THEIR OWN ADDRESSES                       
*                                                                               
IELMINV  DS    0H                                                               
                                                                                
         CLI   GLARGS,C'X'         SKIP IF NOT OLD INVOICE KEYWORD              
         BNE   IELMIVN                                                          
*                                                                               
         CLI   PBMODE,PBPROCIV     SKIP IF NOT PROCESSING OLD INVS              
         BNE   IELMX                                                            
*                                                                               
         LA    RF,PINVTYPQ         SET RECORD TYPE                              
*                                                                               
         CLI   GLARGS+1,0          IF DATA IN KEY                               
         BNE   *+16                                                             
         ICM   R6,15,PBIKYELA         POINT TO INVOICE KEY                      
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
         CLI   GLARGS+1,PIMHDREQ   IF DATA IN HEADER                            
         BNE   *+16                                                             
         ICM   R6,15,PBIHDELA         POINT TO HEADER ELEMENT                   
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
         CLI   GLARGS+1,PIMDTLEQ   IF DATA IN DETAIL ELEMENT                    
         BNE   *+16                                                             
         ICM   R6,15,PBIDTELA         POINT TO DETAIL ELEMENT                   
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
         CLI   GLARGS+1,PIMCOMEQ   IF DATA IN COMMENT ELEMENT                   
         BNE   *+16                                                             
         ICM   R6,15,PBICMELA         POINT TO COMMENT ELEMENT                  
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
IELMIVN  DS    0H                                                               
*                                                                               
         CLI   GLARGS,C'Y'         SKIP IF NOT NEW INVOICE KEYWORD              
         BNE   IELMNVN                                                          
*                                                                               
         CLI   PBMODE,PBPROCNV     SKIP IF NOT PROCESSING NEW INVS              
         BNE   IELMX                                                            
*                                                                               
         LA    RF,PNVKRCDQ         SET RECORD TYPE                              
*                                                                               
         CLI   GLARGS+1,0          IF DATA IN KEY                               
         BNE   *+16                                                             
         ICM   R6,15,PBIKYELA         POINT TO INVOICE KEY                      
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
         CLI   GLARGS+1,PNVHKIDQ   IF DATA IN HEADER                            
         BNE   *+16                                                             
         ICM   R6,15,PBIHDELA         POINT TO HEADER ELEMENT                   
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
         CLI   GLARGS+1,PNVDKIDQ   IF DATA IN DETAIL ELEMENT                    
         BNE   *+16                                                             
         ICM   R6,15,PBIDTELA         POINT TO DETAIL ELEMENT                   
         BZ    IELMX                  DATA NOT AVAILABLE                        
         B     IELMINVX                                                         
*                                                                               
IELMNVN  DS    0H                                                               
*                                                                               
         B     IELMX               SKIP - UNKNOWN ELEMENT ID                    
*                                                                               
IELMINVX DS    0H                                                               
*                                                                               
         LA    R7,GLARGS+1         POINT TO ELEMENT ID                          
*                                                                               
         B     IELMELMF            WE HAVE ELEMENT                              
*                                                                               
IELMINVN DS    0H                                                               
*                                                                               
         B     IELMX               UNKNOWN RECORD TYPE                          
*                                                                               
IELMREC1 DS    0H                  DETERMINE IF PUB RECORD                      
*                                                                               
         CLM   RF,1,PUBKCOD-PUBKEY(R5)   EXIT IF WRONG RECORD IN AREA           
         BE    IELMREC2            OKAY                                         
         B     IELMX               NO GOOD                                      
*                                                                               
IELMRECX DS    0H                  DETERMINE RECORD                             
*                                                                               
         CLM   RF,1,PBUYKRCD-PBUYKEY(R5)   EXIT IF WRONG RECORD IN AREA         
         BNE   IELMX                 USING BUYRECORD AS GENERIC RECORD          
*                                                                               
IELMREC2 DS    0H                                                               
*                                                                               
*        FIND NEEDED ELEMENT IN RECORD                                          
*                                                                               
         LR    R6,R5               DEFAULT TO START OF RECORD                   
*                                                                               
         LA    R7,GLARGS+1         POINT TO ELEMENT ID                          
*                                                                               
         CLI   GLARGS,C'H'         IF HEADER RECORD                             
         BNE   *+8                                                              
         LA    R7,1(R7)               BUMP PAST SUB-ID                          
*                                                                               
         CLI   0(R7),0             IF NO CODE THEN DATA IN KEY                  
         BE    IELMELMF                                                         
*                                                                               
         SR    RF,RF                                                            
         SR    RE,RE                                                            
*                                                                               
         IC    RF,1(R7)            DISPLACEMENT OF DATA                         
         IC    RE,2(R7)            LENGTH OF DATA                               
*                                                                               
         AR    RE,RF               ELEMENT MUST BE THIS LONG                    
*                                                                               
         LA    R6,PBDELEM-PBUYKEY(R6)   POINT TO FIRST ELEMENT                  
*                                                                               
IELMELML DS    0H                                                               
*                                                                               
         CLI   0(R6),0             EXIT IF ELEMENT NOT FOUND                    
         BE    IELMELMD                                                         
         CLC   0(1,R6),0(R7)       SKIP IF NOT ELEMENT DESIRED                  
         BNE   IELMELMC                                                         
         CLM   RE,1,1(R6)          SKIP IF ELEMENT NOT LONG ENOUGH              
         BH    IELMELMC                                                         
*                                                                               
         B     IELMELMF            ELEMENT FOUND                                
*                                                                               
IELMELMC DS    0H                                                               
*                                                                               
         IC    RF,1(R6)            BUMP TO NEXT ELEMENT IN RECORD               
         LA    R6,0(RF,R6)                                                      
         B     IELMELML                                                         
*                                                                               
IELMELMD DS    0H                                                               
*                                                                               
         CLI   0(R2),0             IF NO DATA                                   
         BNE   *+10                                                             
         MVC   0(1,R2),3(R7)          USE DEFAULT VALUE                         
*                                                                               
         B     IELMOUTX                                                         
*                                                                               
IELMELMF DS    0H                                                               
*                                                                               
         L     R1,GLADTENT         ESTABLISH DRIVETABLE ENTRY                   
         USING DRIND,R1                                                         
*                                                                               
         CLI   DRINTYPE,C'C'       IF CHARACTER FIELD                           
         BNE   IELMOUT1                                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,DRINFLEN            GET DRIVER INPUT LENGTH                   
         BCTR  RF,0                   DECREMENT FOR EXECUTE                     
         DROP  R1                                                               
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),SPACES         INIT INPUT TO SPACES                      
*                                                                               
IELMOUT1 DS    0H                                                               
*                                                                               
         SR    RE,RE                                                            
         IC    RE,1(R7)            DISPLACEMENT OF DATA                         
         LA    R1,0(RE,R6)         POINT TO DATA                                
*                                                                               
         SR    RF,RF               ELSE VARIABLE LENGTH FLD AT ELM END          
         ICM   RF,1,2(R7)          DATA LENGTH                                  
         JNZ   *+10                OKAY IF LENGTH PRESENT                       
         IC    RF,1(R6)            ELSE VARIABLE LENGTH FLD AT ELM END          
         SR    RF,RE               DATA LENGTH                                  
*                                                                               
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R1)       PASS DATA TO DRIVER                          
*                                                                               
         CLC   GLARGS(4),=X'C2905008' UPID (C'B'/X'90'/X'50'/X'08')?            
         BNE   IELMOUTX            NO                                           
         L     R1,GLADTENT         ESTABLISH DRIVETABLE ENTRY                   
         USING DRIND,R1                                                         
         CLI   DRINFLEN,15         DRIVER INPUT LENGTH 15?                      
         BNE   IELMOUTX            NO - ENTRY RECORD NOT CHANGED YET            
         USING PIUPEL,R6           INSERTION UPLOAD PBUYREC ELEMENT             
         CLI   PIUPELLN,PIUPELXQ   HAVE EXTENDED UNIQUE SEQ NUMBER?             
         BL    IELMOUTX            NO                                           
         MVC   8(7,R2),PIUPUQXT    EXTENDED UNIQUE SEQUENCE NUMBER              
         DROP  R1,R6               DROP INSERTION UPLOAD PBUYREC ELEM           
*                                                                               
IELMOUTX DS    0H                                                               
*                                                                               
*        IF KEYWORD IS BEING FILTERED - FIND MASTER TYPE FILTER ENTRY           
*        AND DATA FAILS FILTER                                                  
*            DROP DATA FROM SORT                                                
*                                                                               
         CLI   GLARGS+6,0          SKIP IF NO FILTER ASKED FOR                  
         BE    IELMFLTX                                                         
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQMTIC),0,0 SKIP IF NO MASTER                
         BNE   IELMFLTX                                                         
*                                                                               
         ICM   R1,15,8(1)          POINT TO RETURNED FILTER                     
         BZ    IELMFLTX            NONE FOUND                                   
*                                                                               
         MVC   HALF,FLTFVAL-FLTCTL(R1)  SAVE MASTER ICODE                       
*                                                                               
*        VALIDATE KEYWORD VALUE                                                 
*                                                                               
IELMIN   DS    0H                                                               
*                                                                               
*        MAKE INPUT TO DDVAL CHARACTER                                          
*                                                                               
         L     R1,GLADTENT         ESTABLISH DRIVETABLE ENTRY                   
         USING DRIND,R1                                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,2(R7)            GET LENGTH                                   
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
*                                                                               
         CLI   DRINTYPE,C'C'       IF CHARATACTER                               
         BE    *+8                                                              
         CLI   DRINTYPE,C'X'       OR HEX                                       
         BNE   IELMINCX                                                         
*                                                                               
         MVC   IELMLEN,2(R7)          USE DATA AS IS                            
         XC    IELMWORK,IELMWORK                                                
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   IELMWORK(0),0(R2)   MOVE INPUT TO WORKAREA                       
*                                                                               
         B     IELMINX                                                          
*                                                                               
IELMINCX DS    0H                                                               
*                                                                               
         CLI   DRINTYPE,C'P'       IF PACKED                                    
         BE    *+8                                                              
         CLI   DRINTYPE,C'M'       OR MASKED                                    
         BNE   IELMINPX                                                         
*                                     NEED TO AVOID MASK                        
         MVI   IELMLEN,7              MAXIMUM 7 DIGITS                          
*                                                                               
         EX    RF,*+8                                                           
         B     *+10                                                             
         UNPK  IELMWORK(7),1(0,R2)    MOVE INPUT TO WORKAREA                    
*                                                                               
         OI    IELMWORK+6,X'F0'       FORCE SIGN                                
*                                                                               
         B     IELMINX                                                          
*                                                                               
IELMINPX DS    0H                                                               
*                                                                               
*                                                                               
         CLI   DRINTYPE,C'B'       IF BINARY                                    
         BNE   IELMINBX                                                         
*                                                                               
         MVI   IELMLEN,7              MAXIMUM 7 DIGITS                          
*                                                                               
         LA    RE,4                   MAX 4 BINARY BYTES                        
         SR    RE,RF                  NUMBER OF POSITIONS TO SHIFT              
*                                                                               
         ICM   RF,15,0(R2)            BINARY INPUT                              
*                                                                               
         BCT   RE,*+8                                                           
         B     *+12                   SHIFTING DONE                             
         SRL   RF,8                   SHIFT 1 POSITION RIGHT                    
         B     *-12                                                             
*                                                                               
         CVD   RF,DUB                 CVD                                       
         OI    DUB+7,X'0F'            FORCE SIGN                                
         UNPK  IELMWORK(7),DUB        UNPACK                                    
*                                                                               
         B     IELMINX                                                          
*                                                                               
IELMINBX DS    0H                                                               
*                                                                               
         B     IELMFLTX            IGNORE FILTERING                             
*                                                                               
IELMINX  DS    0H                                                               
*                                                                               
         L     R1,AVLPARMS         POINT TO DDVAL PARMS                         
*                                                                               
         GOTO1 VDDVAL,(R1),('VLPVALQ',HALF),(IELMLEN,IELMWORK),        X        
               AVLTAB,0,0,0                                                     
*                                                                               
         CLI   VLPERR,0            DROP IF ERRORS                               
         BNE   IELMDROP                                                         
*                                                                               
         L     R5,AVLTAB           POINT TO RETURNED TABLE ENTRY                
         USING VLTABD,R5           ESTABLISH TABLE ENTRY                        
*                                                                               
         LA    R0,L'VLTICODE       USE INTERNAL CODE FOR SEARCH                 
         LA    RF,VLTICODE                                                      
*                                                                               
         TM    VLTIND,VLTNSLNQ     SKIP IF NO EXTRA DATA                        
         BNO   *+12                ELSE USE EXTRA DATA                          
         IC    R0,VLTEXTRA            LENGTH OF EXTRA DATA                      
         LA    RF,VLTEXTRA+1          EXTRA DATA                                
*                                                                               
         GOTO1 FNDFLTRA,DMCB,HALF,((R0),(RF)),0                                 
*                                                                               
         BNE   IELMDROP            PROBLEM WITH FILTERING                       
*                                                                               
IELMFLTX DS    0H                                                               
*                                                                               
         B     IELMX                                                            
*                                                                               
IELMDROP DS    0H                                                               
*                                                                               
         MVI   SORTSW,C'N'         DROP DATA FROM SORT                          
*                                                                               
IELMX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
IELMLEN  DS    X                   LENGTH FOR DDVAL                             
IELMWORK DS    XL16                INPUT FOR DDVAL                              
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE   'PRWRIELM DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT   OFF                                                            
       ++INCLUDE PRWRIWORKD                                                     
         PRINT   ON                                                             
* INCLUDE DRGLOBAL                                                              
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
* DRIVETABLE                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
* DRINTRECD2                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
* DDSPOOLD                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT   ON                                                             
* PRVALTABD                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRVALTABD                                                      
         PRINT   ON                                                             
PRWRIELM CSECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'025PRWRIELM  03/09/20'                                      
         END                                                                    
