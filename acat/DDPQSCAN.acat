*          DATA SET DDPQSCAN   AT LEVEL 004 AS OF 07/20/20                      
*CATALP PQSCAN                                                                  
                                                                                
***********************************************************************         
* SCAN PRINT QUEUES FOR SPECIFIED TYPE OF REPORTS AND RETURN          *         
* P1(4)  PQBLOCKD - PQ BLOCK FILTERS AND SETTINGS                     *         
* P2(4)  PQCOUNTD - PQ COUNTERS                                       *         
* P3(4)  BUFFER TO BUILD INFORMATION IN                               *         
* P4(4)  END OF BUFFER IF PQBIGBUF IS ON                              *         
***********************************************************************         
         TITLE 'DDPQSCAN - SCAN PRTQUE AND BUILD TABLE OF REPORTS'              
                                                                                
         PRINT NOGEN                                                            
PQSCAN   CSECT                                                                  
         NMOD1 WORKX-WORKD,**PQSC**,RA,RR=R4,CLEAR=YES                          
         USING WORKD,RC                                                         
*                                                                               
         ST    R1,APARM                                                         
         MVC   PARMS(24),0(R1)                                                  
         L     R4,P1                                                            
         USING PQBLOCKD,R4         R4=A(FILTERS)                                
         L     R7,P2                                                            
         USING PQCOUNTD,R7         R7=A(COUNTERS)                               
         ZAP   PQR,=P'0'                                                        
         MVC   PQA(PQCLNQ-L'PQR),PQR  ZAP COUNTERS TO ZERO                      
         MVI   PQFILES,1                                                        
*                                                                               
         L     R6,P3               R6=A(START OF BUFFER)                        
         USING PQEDATAD,R6                                                      
         ST    R6,ABUFFER                                                       
         MVC   PQEDATA,FFS         MARK LAST ENTRY                              
         TM    PQFLAGS,PQBIGBUF                                                 
         BZ    PQSCAN10                                                         
         L     RE,P4               A(END OF BUFFER)                             
         ST    RE,ABUFEND                                                       
         SR    RE,R6               RE = LENGTH OF BUFFER                        
         LA    R3,PQELNQ           LENGTH OF AN ENTRY IN BUFFER                 
         SRDL  RE,32               MOVE RE TO RF WHILE RE IS CLEARED            
         DR    RE,R3                                                            
         CVD   RF,DUB              SAVE NUMBER THAT WILL FIT                    
         TP    PQSMAX              VAILD PACKED PASSED?                         
         BNE   PQSCAN06            NO, SO SET IT                                
         CP    PQSMAX,DUB          IS CALCULATED VALUE LESS OR GREATER          
         BNH   PQSCAN10            LEAVE AS IS, HOPEFULLY OKAY                  
         DC    H'0'                                                             
                                                                                
PQSCAN06 ZAP   PQSMAX,DUB                                                       
*                                                                               
PQSCAN10 LA    RF,CXREC                                                         
         ST    RF,ACXREC                                                        
*                                                                               
         MVC   PRTQID,PRTQUE       SET PQFILE DATA                              
         LA    RF,L'PQINDEX                                                     
         STH   RF,CINDXLN                                                       
*                                                                               
         GOTO1 ADATAMGR,DMCB,SHMUSS,ATTACH,MEMORY,0,0                           
         ICM   R1,15,DMCB+12       A(SHARED MEMORY)                             
         JZ    *+2                                                              
         ST    R1,FIWSHA                                                        
*                                                                               
         TBIN  SECS                GET TIME & DATE                              
         SR    R0,R0                                                            
         D     R0,=F'600'                                                       
         ST    R1,TIMENOW          TIME NOW BIN 10MINS                          
*                                                                               
         GOTO1 ADATCON,DMCB,(5,0),(30,TODAYC)                                   
*                                                                               
         XC    APRTQLST,APRTQLST                                                
         GOTO1 ADATAMGR,PQDMCB,GLIST,PRTQID,NDX,SAVE,ACXREC                     
         ICM   RE,15,NXUSRINF                                                   
         CLI   0(RE),0             CHECK NUM OF PRTQ FILES                      
         BNE   *+6                                                              
         DC    H'0'                DIE IF NOT IN MY RANGE                       
         CLI   0(RE),16                                                         
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    R1,R1               R1=NUM OF FILES IN LIST                      
         IC    R1,0(RE)                                                         
         SR    R0,R0                                                            
         IC    R0,1(RE)                                                         
         AR    R1,R0                                                            
         LA    R1,2(R1)            ADD TWO FOR HDR AND TRL                      
         SLL   R1,3                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRTQLST(0),0(RE)    COPY PRTQ LIST TO MY OWN AREA                
         XC    PRTQLSTX,PRTQLSTX   SET END OF MAXIMUM LIST                      
         EJECT                                                                  
                                                                                
***********************************************************************         
* START TO SCAN PRINT QUEUES                                          *         
***********************************************************************         
SEARCH   XC    NDX,NDX             FIND WHICH PRTQ FILE TO SEARCH               
         CLI   PQFILE,X'FF'        TEST FOR SEARCH ALL QUEUES                   
         BE    SRCH0A                                                           
         CLI   PQFILE,0            SEE IF SPECIFIC QUEUE                        
         BE    *+14                                                             
         MVC   PRTQID+4(1),PQFILE SET SPECIFIC                                  
         B     SRCH0B                                                           
*                                                                               
         TM    PQUSERID,X'80'      IF GENERIC ID SEARCH ALL                     
         BO    SRCH0A                                                           
*                                                                               
         MVC   NXSRCID,PQUSERID    PRTQ ID FROM SPECIFIC USER ID                
         GOTO1 ADATAMGR,PQDMCB,GFILE,PRTQID,NDX,SAVE,ACXREC                     
         MVC   PRTQID,NXUSRINF                                                  
         B     SRCH0B                                                           
*                                                                               
SRCH0A   MVI   PQFILE,X'FF'        SET TO ALL QUEUES IF NOT ALREADY             
         LA    RE,PRTQLST+8        POINT TO FIRST PRTQ FILE                     
         ST    RE,APRTQLST                                                      
         MVC   PRTQID+4(1),1(RE)   SET PRTQ ID FROM LIST                        
*                                                                               
SRCH0B   MVI   FIND,1              SET SEARCHING PART1 INDEX                    
         GOTO1 ADATAMGR,PQDMCB,(0,=C'BUFFER'),PRTQID                            
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R1,ACXREC                                                        
         MVC   BUFFDATA,0(R1)      SAVE V(PQTAB)/V(PQFILE)                      
         MVC   CIDATA,12(R1)       SAVE FILE DATA FOR THIS PRTQ FILE            
*                                                                               
         LA    R5,FIWNDX                                                        
         USING PQRECD,R5           R5=A(PRTQUE INDEX ENTRY)                     
*                                                                               
         SAM31                                                                  
         MVC   FIWRES,PRTQID       SET RESOURCE                                 
         BRAS  RE,FIRSET           SET ADDRESSES FOR THIS RESOURCE              
         JNE   *+2                 INVALID PRTQ                                 
         MVC   FIWNDA,FIWP1A       A(START OF PART1 INDEXES)                    
         B     SRCH1A                                                           
*                                                                               
SRCH1    SAM31                                                                  
         CLI   FIND,2                                                           
         BE    SRCH1B                                                           
         BRAS  RE,FIRNSN                                                        
         BNE   SRCHXX                                                           
SRCH1A   L     R1,FIWNDA                                                        
         MVC   FIWNDX(L'PQINDEX),SI1NDX-SI1PARD(R1)                             
         B     SRCH1D                                                           
SRCH1B   BRAS  RE,FIRNSN2                                                       
         BNE   SRCHXX                                                           
SRCH1C   L     R1,FIWNDA                                                        
         MVC   FIWNDX(L'PQINDEX),SI2NDX-SI2PARD(R1)                             
SRCH1D   BRAS  RE,FIRNC            A(NODE) TO A(CI)                             
         SAM24                                                                  
*                                                                               
SRCH2    OC    PQKEY,PQKEY         IGNORE PURGED                                
         BZ    SRCHX                                                            
         CLI   PQAGERT,X'FF'       TEST IF TEMPORARY                            
         BE    SRCH2D              COUNT AS NOT ACTIVE & NOT AVAIL              
         TM    PQSTAT,PQSTAC+PQSTKE ACTIVE KEEPS DON'T TEST RETAIN              
         BO    SRCH2A                                                           
         CLC   PQAGERD,PERMDAT     PERMANENT, NEVER EXPIRES                     
         BH    SRCH2A                                                           
*                                                                               
         MVC   HALF,PQAGERD                                                     
         TM    PQTYP1,PQTYNCD      SET HALF TO NEW CMPRSD DATE                  
         BO    SRCH201                                                          
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
SRCH201  CLC   HALF,TODAYC         TEST RETAIN DATE WITH TODAY                  
         BL    SRCH2F                                                           
         BH    SRCH2A                                                           
         CLI   PQAGERT,X'FE'       TEST TIME NOT AVAILABLE                      
         BE    SRCH2A                                                           
         CLC   PQAGERT,TIMENOW+3   TEST RETAIN TIME VALUE                       
         BL    SRCH2F                                                           
*                                                                               
SRCH2A   TM    PQSTAT,PQSTKE       UNEXPIRED KEEPS ALWAYS COUNT ACTIVE          
         BO    *+12                                                             
         TM    PQSTAT,PQSTAC       TEST FOR ACTIVE CI                           
         BZ    SRCH2D                                                           
         CLI   FIND,1              TEST PART 1 OR 2                             
         BNE   *+14                                                             
         AP    PQC3,=P'1'          BUMP PART1 ACTIVE                            
         B     SRCH2D                                                           
         AP    PQC4,=P'1'          BUMP PART2 ACTIVE                            
*                                                                               
SRCH2D   CLI   FIND,1              TEST PART 1 OR 2                             
         BNE   SRCH2E                                                           
         AP    PQC1,=P'1'          BUMP PART1 NOTAVAIL                          
         B     SRCH2G                                                           
SRCH2E   AP    PQC2,=P'1'          BUMP PART2 NOTAVAIL                          
         B     SRCHX                                                            
*                                                                               
SRCH2F   TM    PQFLAGS,PQFEXPD     DO WE WANT EXPIRED                           
         BZ    SRCHX                                                            
*                                                                               
SRCH2G   EQU   *                                                                
SRCH3    OC    PQUSERID,PQUSERID   TEST USER ID DEFINED                         
         BZ    SRCH3G                                                           
         TM    PQSRCID,X'80'       TEST GENERIC USER ID                         
         BZ    SRCH3B                                                           
         CLI   AGENIDS,X'FF'       TEST VGENIDS PRESENT                         
         BE    SRCHX                                                            
         GOTO1 AGENIDS,DMCB,PQSRCID,ADATAMGR                                    
         BNE   SRCHX                                                            
         LM    RE,RF,0(R1)         RE=N'ENTRIES,RF=A(ENTRIES)                   
         CLC   PQSRCID,0(RF)                                                    
         BE    SRCH3G              MATCH FOUND                                  
         LA    RF,2(RF)                                                         
         BCT   RE,*-14                                                          
         B     SRCHX               NO MATCH ON GENERIC                          
*                                                                               
SRCH3B   CLC   PQUSERID,PQSRCID    ID WAS SPECIFIED                             
         BNE   SRCHX                                                            
         CLI   PQSTAT,PQSTPU       IGNORE PURGED                                
         BE    SRCHX                                                            
*                                                                               
SRCH3G   OC    PQCLASN,PQCLASN     FILTER ON CLASS                              
         BZ    SRCH3GA                                                          
         LA    R1,8                PQCLASN IS 8 CHRS                            
         LA    RF,PQCLASN-1(R1)                                                 
         CLC   PQCLASS,0(RF)       TEST FOR EXCLUDE                             
         BE    SRCHX                                                            
         BCT   R1,*-14                                                          
SRCH3GA  OC    PQCLAS,PQCLAS       ZERO MEANS INCLUDE ALL                       
         BZ    SRCH3H                                                           
         LA    R1,8                PQCLAS IS 8 CHRS                             
         LA    RF,PQCLAS-1(R1)                                                  
         CLC   PQCLASS,0(RF)       TEST FOR INCLUDE                             
         BE    SRCH3H                                                           
         BCT   R1,*-14                                                          
         B     SRCHX                                                            
*                                                                               
SRCH3H   OC    PQREPID(3),PQREPID  TEST ALL                                     
         BE    SRCH3I                                                           
         LA    R0,3                                                             
         LA    RF,PQREPID                                                       
         LA    RE,PQSUBID                                                       
SRCH3HA  CLI   0(RF),C'*'          TEST WILDCARD                                
         BE    SRCH3HB                                                          
         CLI   0(RF),X'00'         ZERO ALSO                                    
         BE    SRCH3HB                                                          
         CLC   0(1,RE),0(RF)       ELSE MUST MATCH                              
         BNE   SRCHX                                                            
SRCH3HB  LA    RF,1(RF)                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SRCH3HA          TEST NEXT CHAR                               
*                                                                               
SRCH3I   EQU   *                                                                
*                                                                               
SRCH3X   DS    0H                                                               
         AP    PQR,=P'1'           BUMP TOTAL REPORTS                           
         TM    PQSTAT,PQSTAC                                                    
         BZ    *+10                                                             
         AP    PQA,=P'1'           BUMP ACTIVE                                  
         TM    PQSTAT,PQSTHO                                                    
         BZ    *+10                                                             
         AP    PQH,=P'1'           BUMP HOLD                                    
         CLI   PQAGERT,X'FE'                                                    
         BNE   *+10                                                             
         AP    PQG,=P'1'           BUMP PRINTING                                
*NOP*    TM    PQSTAT,PQSTPR                                                    
*NOP*    BZ    *+10                                                             
*NOP*    AP    PQD,=P'1'           BUMP PRINTED                                 
         TM    PQSTAT,PQSTSE                                                    
         BZ    *+10                                                             
         AP    PQE,=P'1'           BUMP SENT                                    
         TM    PQSTAT,PQSTKE                                                    
         BZ    *+10                                                             
         AP    PQK,=P'1'           BUMP KEEP                                    
*                                                                               
PQSRCH4A SR    R1,R1               ANY ATTRIBS TO EXCLUDE                       
         IC    R1,PQATTBN                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQATTB,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,PQATT                                                       
         BZ    PQSRCH4B            ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQATTB,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
PQSRCH4B SR    R1,R1               ANY STATUS TO EXCLUDE                        
         IC    R1,PQSTATN                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQSTAT,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,PQSTA                                                       
         BZ    PQSRCH4C            ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQSTAT,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
PQSRCH4C SR    R1,R1               ANY TYPE TO EXCLUDE                          
         IC    R1,PQTYPEN                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQTYPE,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,PQTYP                                                       
         BZ    PQSRCH4D            ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQTYPE,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
PQSRCH4D SR    R1,R1               ANY TYP1 TO EXCLUDE                          
         IC    R1,PQTYP1N                                                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQTYP1,0            IF ANY BITS MATCH EXCLUDE IT                 
         BNZ   SRCHX                                                            
         ICM   R1,1,PQTYP1I                                                     
         BZ    PQSRCH5             ZERO MEANS INCLUDE ALL                       
         EX    R1,*+8                                                           
         B     *+8                                                              
         TM    PQTYP1,0            IF ANY BITS MATCH INCLUDE IT                 
         BZ    SRCHX                                                            
*                                                                               
PQSRCH5  CLI   PQAGESV,0           TEST FOR NO LIMIT                            
         BE    PQSRCH5X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,PQAGESF        GET BC FILTER IN R1                          
         BZ    PQSRCH5X                                                         
         CLC   PQAGES,PQAGESV      TEST AGE FILTER                              
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,PQSRCH5X                                                       
PQSRCH5X EQU   *                                                                
*                                                                               
PQSRCH6  OC    PQAGELV,PQAGELV     TEST LIVE DATE                               
         BZ    PQSRCH6X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,PQAGELF        GET BC FILTER IN R1                          
         BZ    PQSRCH6X                                                         
         MVC   HALF,PQAGELD                                                     
         CLC   HALF,PERMDAT        PERMANENT, NO NEED TO CONVERT                
         BH    PQSRCH6A                                                         
         TM    PQTYP1,PQTYNCD      SET HALF TO NEW CMPRSD DATE                  
         BO    PQSRCH6A                                                         
         ST    R1,FULL2                                                         
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
         L     R1,FULL2                                                         
PQSRCH6A CLC   HALF,PQAGELV        TEST CDATE FILTER                            
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,PQSRCH6X                                                       
PQSRCH6X EQU   *                                                                
*                                                                               
PQSRCH7  OC    PQAGEDV,PQAGEDV     TEST DEAD DATE                               
         BZ    PQSRCH7X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,PQAGEDF        GET BC FILTER IN R1                          
         BZ    PQSRCH7X                                                         
         MVC   HALF,PQDATED                                                     
         CLC   HALF,PERMDAT        PERMANENT, NO NEED TO CONVERT                
         BH    PQSRCH7A                                                         
         TM    PQTYP1,PQTYNCD      SET HALF TO NEW CMPRSD DATE                  
         BO    PQSRCH7A                                                         
         ST    R1,FULL2                                                         
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
         L     R1,FULL2                                                         
PQSRCH7A CLC   HALF,PQAGEDV        TEST DDATE FILTER                            
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,PQSRCH7X                                                       
PQSRCH7X EQU   *                                                                
*                                                                               
PQSRCH8  OC    PQAGERV,PQAGERV     TEST RETAIN DATE                             
         BZ    PQSRCH8X                                                         
         SR    R1,R1                                                            
         ICM   R1,1,PQAGERF        GET BC FILTER IN R1                          
         BZ    PQSRCH8X                                                         
         MVC   HALF,PQAGERD                                                     
         CLC   HALF,PERMDAT        PERMANENT, NO NEED TO CONVERT                
         BH    PQSRCH8A                                                         
         TM    PQTYP1,PQTYNCD      SET HALF TO NEW CMPRSD DATE                  
         BO    PQSRCH8A                                                         
         GOTO1 ADATCON,DMCB,(2,HALF),(30,HALF)                                  
PQSRCH8A CLC   HALF,PQAGERV        TEST RDATE FILTER                            
         EX    R1,*+8                                                           
         B     SRCHX                                                            
         BC    0,PQSRCH8X                                                       
PQSRCH8X EQU   *                                                                
*                                                                               
         TM    PQFLAGS,PQFTOTAL    TEST FOR TOTALS                              
         BZ    SRCHT                                                            
*                                                                               
TOTAL01  ST    R6,FULL             SAVE CURRENT R6                              
         L     R6,ABUFFER                                                       
TOTAL02  C     R6,FULL                                                          
         BE    SRCHT               NOT FOUND                                    
         CLC   PQEUSER,PQSRCID     SAME USERID                                  
         BE    TOTAL03                                                          
         LA    R6,L'PQEDATA(R6)    NEXT ENTRY                                   
         B     TOTAL02                                                          
*                                                                               
TOTAL03  ICM   R1,15,PQESORT       BUMP COUNT                                   
         LA    R1,1(R1)                                                         
         STCM  R1,15,PQESORT                                                    
         L     R6,FULL             RESTORE R6                                   
         B     SRCHX                                                            
*                                                                               
SRCHT    OC    PQSMAX,PQSMAX                                                    
         BZ    SRCHTA                                                           
         CP    PQT,PQSMAX          ROOM IN TABLE                                
         BL    SRCHU               YES                                          
         B     SRCHT1              NO                                           
                                                                                
SRCHTA   CP    PQT,QTMAX           ROOM IN TABLE                                
         BL    SRCHU               YES                                          
                                                                                
SRCHT1   AP    PQX,=P'1'           PQX=COUNT OF NO ROOM ENTRYS                  
         B     SRCHX                                                            
*                                                                               
SRCHU    XC    0(L'PQEDATA,R6),0(R6) SET UP TABLE ENTRY                         
         MVC   PQEUSER,PQSRCID                                                  
         MVC   PQESTAT,PQSTAT                                                   
         MVC   PQEATTB,PQATTB                                                   
         MVC   PQESUBID,PQSUBID                                                 
         MVC   PQEPRTQ,PRTQID+4                                                 
         MVC   PQEREPNO,PQREPNO                                                 
         TM    PQFLAGS,PQFREFNO    RETURN REPORT NUMBER IF REQUESTED            
         BO    *+10                                                             
         MVC   PQECIAD,FIWCIA      RETURN FULL CI DISK ADDR                     
*                                                                               
SORT     TM    PQFLAGS,PQFTOTAL    TEST FOR TOTALS                              
         BNO   SORT1                                                            
         MVC   PQESORT,=F'1'       SET TO 1                                     
         B     SORTX                                                            
*                                                                               
SORT1    CLI   PQSORT,1                                                         
         BNE   SORT2                                                            
         MVC   PQESORT+0(4),PQSUBID      ALPHA SORT                             
         B     SORTX                                                            
*                                                                               
SORT2    CLI   PQSORT,3                                                         
         BH    SORT4                                                            
         MVC   PQESORT+0(2),PQAGELD      CREATE TIME SORT                       
         CLC   PQAGELD,PERMDAT           PERMANENT, NO NEED TO CONVERT          
         BH    SORT2A                                                           
         TM    PQTYP1,PQTYNCD                                                   
         BO    SORT2A                                                           
         GOTO1 ADATCON,DMCB,(2,PQESORT),(30,PQESORT)   TO NEW CMPRSD            
SORT2A   MVC   PQESORT+2(2),PQAGELT                                             
         CLI   PQSORT,2                                                         
         BE    SORTX                                                            
         XC    PQESORT,FFS               -CREATE                                
         B     SORTX                                                            
*                                                                               
SORT4    CLI   PQSORT,5                                                         
         BH    SORT6                                                            
         MVC   PQESORT+2(2),PQDATED      SENT DATE SORT                         
         CLC   PQDATED,PERMDAT           PERMANENT, NO NEED TO CONVERT          
         BH    SORT4A                                                           
         TM    PQTYP1,PQTYNCD                                                   
         BO    SORT4A                                                           
         GOTO1 ADATCON,DMCB,(2,PQESORT+2),(30,PQESORT+2) TO NEW CMPRSD          
SORT4A   CLI   PQSORT,4                                                         
         BE    SORTX                                                            
         XC    PQESORT,FFS               -SENT                                  
         B     SORTX                                                            
*                                                                               
SORT6    CLI   PQSORT,7                                                         
         BH    SORT8                                                            
         MVC   PQESORT+0(2),PQAGERD      RETAIN TIME SORT                       
         CLC   PQAGERD,PERMDAT           PERMANENT, NO NEED TO CONVERT          
         BH    SORT6A                                                           
         TM    PQTYP1,PQTYNCD                                                   
         BO    SORT6A                                                           
         GOTO1 ADATCON,DMCB,(2,PQESORT),(30,PQESORT)   TO NEW CMPRSD            
SORT6A   MVC   PQESORT+2(1),PQAGERT                                             
         CLI   PQSORT,6                                                         
         BE    SORTX                                                            
         XC    PQESORT,FFS               -RETAIN                                
         B     SORTX                                                            
*                                                                               
SORT8    CLI   PQSORT,9                                                         
         BH    SORT10                                                           
         MVC   PQESORT+3(1),PQAGES       SIZE SORT                              
         CLI   PQSORT,8                                                         
         BE    SORTX                                                            
         XC    PQESORT,FFS               -RETAIN                                
         B     SORTX                                                            
*                                                                               
SORT10   DC    H'0'                INVALID SORT VALUE                           
*                                                                               
SORTX    LA    R6,PQELNQ(R6)       NEXT ENTRY                                   
         AP    PQT,=P'1'                                                        
*                                                                               
SRCHX    B     SRCH1               BUMP TO NEXT INDEX ENTRY                     
*                                                                               
SRCHXX   SAM24                                                                  
         CLI   FIND,2              ARE WE FINISHED SEARCHING PART2S?            
         BE    SRCHY               YES: THEN DONE                               
         TM    PQFLAGS,PQFPART2    TEST SEARCH PART 2S                          
         BZ    SRCHY               NO: THEN DONE                                
         MVI   FIND,2              SET TO SEARCH PART2 (SIZE ACTION)            
         MVC   FIWNDA,FIWP2A       START WITH FIRST PART2                       
         SAM31                                                                  
         B     SRCH1C                                                           
*                                                                               
SRCHY    ICM   RE,15,APRTQLST      TEST FOR MULTIPLE FILE SEARCH                
         BZ    SORT010                                                          
         LA    RE,8(RE)            BUMP TO NEXT FILE                            
         ST    RE,APRTQLST                                                      
         CLI   0(RE),0             TEST FOR LAST FILE                           
         BE    SORT010                                                          
         MVC   PRTQID+4(1),1(RE)                                                
         MVC   FULL(3),PQT         SAVE PQT                                     
         LA    R7,L'PQCOUNT(R7)    POINT TO NEXT COUNT AREA                     
         SR    R1,R1               BUMP NO OF FILES SEARCHED                    
         IC    R1,PQFILES                                                       
         LA    R1,1(R1)                                                         
         STC   R1,PQFILES                                                       
         ZAP   0(3,R7),=P'0'       ZAP ALL THE COUNTS                           
         MVC   3(45,R7),0(R7)                                                   
         MVC   PQT(3),FULL         RESTORE PQT                                  
         B     SRCH0B              START AGAIN                                  
*                                                                               
SRCHURR  L     R1,APARM                                                         
         MVI   8(R1),X'80'         FLAG ERROR                                   
         B     EXITX                                                            
                                                                                
SORT010  MVC   0(PQELNQ,R6),FFS    SET END OF TABLE AND SORT ON KEY             
         XC    FULL,FULL                                                        
         ZAP   DUB,PQT                                                          
         CVB   R6,DUB                                                           
         LA    R6,1(R6)                                                         
         CHI   R6,2                                                             
         BNH   SORT01X                                                          
*                                                                               
         L     R0,ABUFFER                                                       
         XC    DMCB(24),DMCB                                                    
         MVI   DMCB+15,6           LENGTH                                       
         MVI   DMCB+19,0           DISPLACEMENT                                 
         TM    PQFLAGS,PQFXUSER                                                 
         BNO   SORT011             EXCLUDE USERID                               
*                                                                               
         MVI   DMCB+15,4           ON XSORT                                     
         MVI   DMCB+19,2                                                        
*                                                                               
SORT011  GOTO1 AXSORT,DMCB,(R0),(R6),L'PQEDATA                                  
*                                                                               
SORT01X  EQU   *                                                                
*                                                                               
EXITX    XMOD1                                                                  
*                                                                               
       ++INCLUDE DDSHFIR           SHARED MEMORY INDEX ROUTINES                 
*                                                                               
QTMAX    DC    PL3'800'            MAX NUMBER OF REPORTS 800X16=12800           
*                                                                               
ADATAMGR DC    V(DATAMGR)                                                       
ADATCON  DC    V(DATCON)                                                        
AXSORT   DC    V(XSORT)                                                         
AGENIDS  DC    V(GENIDS)                                                        
FFS      DC    16X'FF'                                                          
PERMDAT  DC    XL2'FF9F'                                                        
PRTQUE   DC    CL8'PRTQU '                                                      
GLIST    DC    CL8'GLIST '                                                      
GFILE    DC    CL8'GFILE '                                                      
SHMUSS   DC    CL8'SHMUSS '                                                     
ATTACH   DC    CL8'ATTACH '                                                     
MEMORY   DC    CL8'PRTQ'                                                        
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
PARMS    DS    0CL24                                                            
P1       DS    A                                                                
P2       DS    A                                                                
P3       DS    A                                                                
P4       DS    A                                                                
P5       DS    A                                                                
P6       DS    A                                                                
*                                                                               
DUB      DS    D                                                                
FULL     DS    F                                                                
FULL2    DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
FIND     DS    X                                                                
DMCB     DS    6F                                                               
PQDMCB   DS    6F                                                               
FLAG     DS    X                                                                
         DS    X                                                                
TODAYC   DS    XL2                 TODAYS DATE CMPRSD NEW                       
*                                                                               
TIMENOW  DS    F                                                                
*                                                                               
ABUFFER  DS    A                   START OF BUFFER                              
ABUFEND  DS    A                   END OF BUFFER (OPTIONAL)                     
APARM    DS    A                                                                
ACXREC   DS    A                                                                
*                                                                               
WORK     DS    0CL80                FOR BOOKVAL ETC                             
WORK1    DS    CL20                                                             
WORK2    DS    CL20                                                             
WORK3    DS    CL20                                                             
WORK4    DS    CL20                                                             
*                                                                               
SAVE     DS    360C                SAVE AREA FOR DATAMGR CALLS                  
*                                                                               
APRTQLST DS    A                                                                
PRTQIDSV DS    CL8                 SAVED PRTQ NAME                              
PRTQID   DS    CL8                 NAME OF PRTQ FOR DMGR                        
PRTQLST  DS    0X                                                               
PRTQMAX  DS    X                   NUMBER OF PRTQ FILES                         
         DS    X                                                                
PRTQFLG  DS    X                                                                
         DS    XL5                                                              
PRTQNTRY DS    16XL8               MAXIMUM OF 16 PRTQ FILES                     
PRTQLSTX DS    XL8                                                              
*                                                                               
BUFFDATA DS    0XL12               PRTQIL DATA GIVEN BY BUFFER ACTION           
BUWKTAB  DS    V                                                                
BUWKFILE DS    V                                                                
BUSAVE   DS    XL4                                                              
*                                                                               
       ++INCLUDE DDSHFIW                                                        
       ++INCLUDE DMPRTQW                                                        
*                                                                               
         DS    0F                                                               
NDX      DS    0XL40               INDEX ENTRY                                  
NXSRCID  DS    XL2                                                              
NXSUB    DS    CL3                                                              
NXREP    DS    XL2                                                              
NXCLASS  DS    CL1                                                              
NXTYPE   DS    XL1                                                              
NXATTB   DS    XL1                                                              
NXSTAT   DS    XL1                                                              
NXSEQ    DS    XL1                                                              
NXAGES   DS    XL1                                                              
NXAGELD  DS    XL2                                                              
NXAGEDD  DS    XL2                                                              
NXAGERD  DS    XL2                                                              
NXAGERT  DS    XL1                                                              
NXAGELT  DS    XL2                                                              
*                                                                               
         DS    XL2                                                              
NXINFO   DS    XL2                                                              
NXFILNOX DS    XL2                                                              
NXCIADDR DS    XL2                                                              
NXFLAG   DS    XL1                                                              
         DS    XL1                                                              
NXUSRINF DS    XL8                                                              
         DS    CL24                                                             
*                                                                               
CXREC    DS    14336C                                                           
*                                                                               
WORKX    EQU   *                                                                
                                                                                
***********************************************************************         
* MORE DSECTS                                                         *         
***********************************************************************         
* DDPQSCAND                                                                     
       ++INCLUDE DDPQSCAND                                                      
* DMPRTQD                                                                       
       ++INCLUDE DMPRTQD                                                        
* DDSHFID                                                                       
       ++INCLUDE DDSHFID                                                        
* ASCB                                                                          
         IHAASCB LIST=YES                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDPQSCAN  07/20/20'                                      
         END                                                                    
