*          DATA SET TACKREP    AT LEVEL 206 AS OF 06/23/16                      
*CATALP TACKREP                                                                 
         TITLE 'TACKREP - TALENT CHECK WRITING APPLICATION'                     
TACKREP  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TACKREP,RA,R9,R8                                               
         L     RC,0(R1)            RC = A(GENCON WORKING STORAGE)               
         USING GEND,RC                                                          
         L     R7,ASUBSYSD         R7 = A(CONTROLLER WORKING STORAGE)           
         USING SUBSYSD,R7                                                       
         LA    R6,BUFF             R6 = A(LOCAL WORKING STORAGE)                
         LA    R6,8(R6)                                                         
         USING CHECKD,R6                                                        
         EJECT                                                                  
* MAIN DRIVER - THE WORK IS BROKEN UP INTO TWO PASSES.  THE FIRST PASS          
* READS AND PROCESSES ALL THE RECORDS FROM TALDIR/TALFIL THAT MUST BE           
* READ AND WRITES OUT SORTER RECORDS WITH THE INFORMATION IT EXTRACTED          
* FROM THE FILE.  ADDITIONALLY, IT CALLS ALLTAX TO GET THE TAX AMOUNTS          
* FOR EACH TAX UNIT, AND IT SAVES THOSE VALUES IN THE SORTER RECORD AS          
* WELL.  THE SECOND PASS READS BACK THE SORTER RECORDS AND FROM THE             
* INFORMATION THEY CONTAIN PRINTS CHECKS AND WRITES CHECK RECORDS.              
*                                                                               
MAIN     DS    0H                                                               
         GOTO1 INITIAL,DMCB,0      INITIALIZE SYSTEM WORKING STORAGE            
*                                                                               
         CLI   MODE,PRINTREP       DO ALL THROUGH MODE PRINTREP                 
         BNE   MX                                                               
         BAS   RE,INITADDR         INITIALIZE ADDRESSES                         
*                                                                               
         BRAS  RE,BLDACHT          BUILD ACH TABLE                              
*                                  READ SYSTEM CONTROL RECORD                   
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)                                    
         BNE   ERRSYS                                                           
         MVC   TGGSTRAT,=AL4(GSTRT)   DEFAULT 5.00% GST                         
         L     R4,AIO                                                           
         USING TAPCD,R4                                                         
         MVI   ELCODE,TAPCELQ         GET CANADIAN PROVINCE ELEMENT             
         BAS   RE,GETEL                                                         
         BNE   M30                                                              
         MVC   TGGSTRAT,TAPCGSTR      SET CANADIAN GST RATE                     
*                                                                               
M30      L     R4,AIO                                                           
         MVI   ELCODE,TASYELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRSYS                                                           
*                                                                               
         GOTO1 INITCTR             INITIALIZE CONTROL VARIABLES                 
*                                                                               
         GOTO1 =A(VINITABS)        INITIALIZE LCLTAB AND OFFTAB                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',CHECKD),SRTREC-CHECKD,=C'CHECKD'              
*                                                                               
         GOTO1 =V(ILBOSTP0)        INITIALIZE ALLTAX MODULE                     
*                                                                               
         LA    R2,SRTKEYL          INSERT SORT KEY LENGTH INTO SORTCARD         
         LA    R3,SORTCARD+15                                                   
         EDIT  (R2),(2,(R3))                                                    
*                                  SET L'SORT RECORD IN RECCARD                 
         LHI   R2,SRTRECL                                                       
         CVD   R2,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  RECCARD+21(4),DUB+5(3)                                           
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',SORTCARD),160,=C'SORTCARD'                    
*                                                                               
         GOTO1 VSORTER,DMCB,SORTCARD,RECCARD  OPEN SORTER FILE                  
*                                                                               
         XC    TGTHREE,TGTHREE     CLEAR START DATE                             
         MVC   END1,REQEND1        SET REQUESTED END DATE                       
         BAS   RE,CHKPASS1         CHECK RECORDS PASS 1 - PUT SORTRECS          
*                                                                               
         CLI   RERUN,C'Y'          IF RERUN INDICATOR IS Y(ES) OR               
         BE    M50                     R(ECALC) THEN SKIP                       
         CLI   RERUN,C'R'                                                       
         BE    M50                                                              
         OC    REQASOF,REQASOF     IF AS/OF DATE REQUESTED                      
         BZ    M50                                                              
         MVC   TGTHREE,REQASOF     SET TO START AT THAT DATE                    
         MVC   END1,XFFS           AND TO GO UNTIL END                          
         BAS   RE,CHKPASS1         CHECK RECORDS PASS 1 - PUT SORTRECS          
         MVC   END1,REQEND1        RESET REQ. END DATE                          
*                                                                               
M50      L     RF,NEXTERI          MARK END OF ERROR INVOICE TABLE              
         MVI   0(RF),X'FF'                                                      
*                                  CHECK RECORDS PASS 2 - GET SORTRECS          
         GOTO1 =V(TACKPRT),DMCB,(RC)                                            
*        BRAS  RE,COMMIT                                                        
*                                                                               
MX       B     XIT                                                              
         EJECT                                                                  
* INITIALIZE ADDRESSES OF ROUTINES, TABLES, AND IOAREAS.                        
*                                                                               
INITADDR NTR1                                                                   
         L     RF,ATWA             RF = A(TWA)                                  
         USING T703FFD,RF                                                       
*                                                                               
         MVC   AMASTD,TWAMASTC     SAVE A(MASTCON DSECT)                        
         MVC   ORIGID,TWAORIG           SIGN-ON ID IN HEX                       
         MVC   AGYALPH,TWAAGY           SIGN-ON ID IN ALPHANMERIC               
*                                                                               
         L     RE,TWADCONS         SAVE ADDRESS OF TWADCON ROUTINES             
         USING TWADCOND,RE                                                      
         MVC   BINSRCH,TBINSRCH                                                 
         MVC   ALOGOC,TLOGOC                                                    
         DROP  RE                                                               
*                                                                               
         LR    RE,RF               SAVE ADDRESSES OF SAVED PP BLOCK             
         A     RE,=A(6144)             AND ADD PP BLOCK - STARTING              
         ST    RE,ASPBLK               AT 6144 PAST BEGIN OF TWA AND            
         A     RE,=A(PPBLOCKL)         SPANNING A TOTAL OF 6144 BYTES           
         ST    RE,AAPBLK                                                        
         DROP  RF                                                               
*                                                                               
         L     RF,AMASTD           SAVE MASTD VARIABLES                         
         USING MASTD,RF                                                         
         MVC   RERUN,MCRERUN       MCRERUN (RERUN INDICATOR)                    
         MVC   ALOGO,MCVLOGO       SAVE A(LOGO)                                 
         MVC   AREMOT,MCVREMOT     SAVE A(REMOTE)                               
*                                                                               
         CLI   RECNUM,DC           IF RUNNING DMB&B CHECKS                      
         BNE   *+8                                                              
         MVI   MCPOSTNG,C'N'       SET TO NOT POST                              
*                                                                               
         L     RF,ASPOOLD          SAVE ADDRESS OF SPOOLD ROUTINES              
         USING SPOOLD,RF                                                        
         MVC   VSORTER,SORTER                                                   
         DROP  RF                                                               
*                                                                               
         L     RF,ACOMFACS         SAVE ADDRESS OF COMFACSD ROUTINES            
         USING COMFACSD,RF                                                      
         MVC   GETRET,CGETRET                                                   
         DROP  RF                                                               
*                                                                               
         TM    REQOPTS,REQALTXA    LOAD TASEGUEA                                
         BO    SEGTST                                                           
         LOAD  EP=TASEGUE,ERRET=LOADERR   SAVE A(LOADED PHASES)                 
         B     SEGTSTX                                                          
SEGTST   LOAD  EP=TASEGUEA,ERRET=LOADERR   SAVE A(LOADED PHASES)                
SEGTSTX  ST    R0,TASEGUE                                                       
         LOAD  EP=TACKERI,ERRET=LOADERR                                         
         ST    R0,AERITAB                                                       
         LOAD  EP=TACKWAR,ERRET=LOADERR                                         
         ST    R0,AWARTAB                                                       
         LOAD  EP=TACKDUE,ERRET=LOADERR                                         
         ST    R0,ADUETAB                                                       
         LOAD  EP=TACKLIN,ERRET=LOADERR                                         
         ST    R0,ALINTAB                                                       
         LOAD  EP=TACKTRS,ERRET=LOADERR                                         
         ST    R0,ATRSTAB                                                       
         LOAD  EP=TACKACC,ERRET=LOADERR                                         
         ST    R0,AACCTAB                                                       
         LOAD  EP=TACKLCL,ERRET=LOADERR                                         
         ST    R0,ALCLTAB                                                       
         LOAD  EP=TACKPYT,ERRET=LOADERR                                         
         ST    R0,APYTDTAB                                                      
         A     R0,=AL4(MAXPYTDS*PYTDLEN+BNPRMLNQ)                               
         ST    R0,APYTDINV                                                      
         LOAD  EP=TACKQTD,ERRET=LOADERR                                         
         ST    R0,AQTDTAB                                                       
         LOAD  EP=TACKEPI,ERRET=LOADERR                                         
         ST    R0,AEPITAB                                                       
         LOAD  EP=TACKAYF,ERRET=LOADERR                                         
         ST    R0,AAYFTAB                                                       
         LOAD  EP=TACKCLF,ERRET=LOADERR                                         
         ST    R0,ACLFTAB                                                       
         LOAD  EP=TACKACH,ERRET=LOADERR                                         
         ST    R0,AACHTAB                                                       
         B     *+6                                                              
LOADERR  DC    H'0'    COULDN'T LOAD - R0=A(PHASE NAME), RF=RETURN CODE         
*                                                                               
         L     R3,=AL4(MAXESUS*ESULEN)                                          
         STORAGE OBTAIN,LOC=31,COND=NO,LENGTH=(R3),ADDR=(R2),BNDRY=PAGE         
         ST    R2,AESUTAB                                                       
         SAM31                                                                  
         MVC   0(ESUBLNQ,R2),ESUBIN                                             
         SAM24                                                                  
*                                                                               
         L     R3,=AL4(MAXCAST*5*ESULEN)                                        
         STORAGE OBTAIN,LOC=31,COND=NO,LENGTH=(R3),ADDR=(R2),BNDRY=PAGE         
         ST    R2,AESUINV                                                       
         SAM31                                                                  
         MVC   0(ESUIBLNQ,R2),ESUIBIN                                           
         SAM24                                                                  
*                                                                               
         MVC   GETESU,=A(VGETESU)  SAVE A(COMMON ROUTINES)                      
         MVC   GETSUC,=A(VGETSUC)                                               
         MVC   DOTRACE,=A(VDOTRACE)                                             
         MVC   ADDPTRS,=A(VADDPTRS)                                             
         MVC   NOCALC,=A(VNOCALC)                                               
         MVC   NOPRNT,=A(VNOPRNT)                                               
         MVC   ADDBIN,=A(VADDBIN)                                               
         MVC   BLDSORT,=A(VBLDSORT)                                             
         MVC   UPDPYTD,=A(VUPDPYTD)                                             
         MVC   FLUSHI,=A(VFLUSHI)                                               
         MVC   GETPYTD,=A(VGETPYTD)                                             
         MVC   GETQTD,=A(VGETQTD)                                               
         MVC   INITCTR,=A(VINITCTR)                                             
         MVC   CALLTAPP,=A(VCALLTAP)                                            
*                                                                               
         ST    RB,SAVERB           SAVE BASE REGISTERS                          
         ST    RA,SAVERA                                                        
         ST    R9,SAVER9                                                        
         ST    R8,SAVER8                                                        
         ST    RC,SAVERC           SAVE RC                                      
*                                                                               
         LA    RF,SRTCHECK         SAVE A(CHECK RECORD IOAREA)                  
         ST    RF,ASRTCHK                                                       
         MVC   AINVIO,=A(INVIO)    SAVE A(INVOICE RECORD IOAREA)                
         MVC   AEMPIO,=A(EMPIO)         A(EMPLOYER RECORD IOAREA)               
         MVC   AW4IO,=A(W4IO)           A(W4 RECORD IOAREA)                     
         MVC   AEVTIO,=A(EVTIO)         A(EVTIME RECORD IOAREA)                 
         MVC   ASUITAB,=A(SUITAB)       A(SUI TABLE IOAREA)                     
         MVC   AESUCUR,=A(ESUCUR)       A(CURRENT ESU ENTRY)                    
         MVC   AESUICUR,=A(ESUICUR)     A(CURRENT ESU INVOICE ENTRY)            
*                                                                               
         LA    R5,BLANKACC         BUILD INITIALIZED ACCTAB LOOKUP              
         USING ACCTABD,R5              ENTRY                                    
         MVC   ACCENTRY,MYSPACES                                                
         XC    ACCAMNT,ACCAMNT                                                  
         B     XIT                                                              
         DROP  R5                                                               
*                                                                               
*        TABLE OF EMP/SSN/UNIT CHECK AMOUNTS                                    
*                                                                               
ESUBIN   DC    0H                                                               
         DC    F'0'                NUMBER IN TABLE                              
         DC    AL4(ESULEN)         RECORD LENGTH                                
         DC    AL4(ESULKEY)        DISP. TO KEY / KEY LENGTH                    
         DC    AL4(MAXESUS)        MAX. NUMBER OF RECORDS                       
         DC    AL1((ESULEN-ESUDSP)/4)   NUMBER OF BUCKETS                       
         DC    AL1(ESUDSP)         DISP TO BUCKETS                              
         DC    X'80'               BINARY                                       
         DC    AL1(0)                                                           
ESUBLNQ  EQU   *-ESUBIN                                                         
*                                                                               
*        TABLE OF EMP/SSN/UNIT CHECK AMOUNTS FOR AN INVOICE                     
*                                                                               
ESUIBIN  DC    0H                                                               
         DC    F'0'                NUMBER IN TABLE                              
         DC    AL4(ESULEN)         RECORD LENGTH                                
         DC    AL4(ESULKEY)        DISP. TO KEY / KEY LENGTH                    
         DC    AL4(MAXCAST*5)      MAX. NUMBER OF RECORDS                       
         DC    AL1((ESULEN-ESUDSP)/4)   NUMBER OF BUCKETS                       
         DC    AL1(ESUDSP)         DISP TO BUCKETS                              
         DC    X'80'               BINARY                                       
         DC    AL1(0)                                                           
ESUIBLNQ EQU   *-ESUIBIN                                                        
*                                                                               
         EJECT                                                                  
* CHECK PASS 1 - READ THROUGH THE INVOICE PASSIVE KEYS BY DUE DATE              
* AND PROCESS EACH INVOICE IN TURN.  LOWER LEVEL ROUTINES WILL EXTRACT          
* INFORMATION FROM THE INVOICE RECORDS,  LOOP THROUGH THE CHECK RECORDS         
* FOR EACH INVOICE, AND WRITE A SORT RECORD FOR EACH CHECK.                     
*                                                                               
CHKPASS1 NTR1                                                                   
         LA    R3,KEY              BUILD DUE DATE PASSIVE INVOICE KEY           
         USING TLINPD,R3                                                        
         XC    TLINPKEY,TLINPKEY                                                
         MVI   TLINPCD,TLINCCDQ                                                 
         MVC   TLINCDUE,TGTHREE    SET START DATE                               
*                                                                               
         CLI   RERUN,C'Y'          IF RERUN INDICATOR IS Y(ES) OR               
         BE    *+12                    R(ECALC)                                 
         CLI   RERUN,C'R'                                                       
         BNE   CPO5                                                             
         XC    TLINPKEY,TLINPKEY   SET TO READ VIA CHECK DATE PTRS              
         MVI   TLINPCD,TLINKCDQ                                                 
         MVC   TLINKDTE,CHKDATE1   SET CHECK DATE                               
         MVC   TLINKAGY,REQAGY     SET POSSIBLE AGENCY FILTER                   
         MVC   TLINKINV,REQINV                  INVOICE FILTER                  
*                                                                               
CPO5     GOTO1 HIGH                READ FIRST INVOICE                           
*                                                                               
CPO10    XC    TGEMP,TGEMP                                                      
         CLI   RERUN,C'Y'          IF RERUN INDICATOR IS Y(ES) OR               
         BE    CPO20                   R(ECALC) THEN SKIP                       
         CLI   RERUN,C'R'                                                       
         BE    CPO20                                                            
*                                                                               
         CLI   TLINPCD,TLINCCDQ    WHILE STILL DUE DATE POINTERS                
         BNE   CPO100                                                           
         CLC   TLINCDUE,END1       DUE DATE MUST BE <= END DATE                 
         BH    CPO100                                                           
*                                                                               
         BRAS  RE,EXTAGY           EXTRACT AGENCY STATUS                        
         BE    CPO15                                                            
CPO13    MVC   TLINCINV,=X'FFFFFFFFFFFF'      NEXT AGENCY                       
         GOTO1 HIGH                                                             
         B     CPO10                                                            
*                                                                               
CPO15    MVC   SRTAGY,TLINCAGY     SAVE AGENCY IN SORT RECORD                   
         MVC   SRTINV,TLINCINV          INVOICE                                 
         B     CPO30                                                            
*                                                                               
CPO20    CLI   TLINPCD,TLINKCDQ    WHILE STILL CHECK DATE POINTERS              
         BNE   CPO100                                                           
         CLC   TLINKDTE,CHKDATE1   CHECK DATE MUST BE THE SAME                  
         BNE   CPO100                                                           
*                                                                               
         MVC   SRTAGY,TLINKAGY     SAVE AGENCY IN SORT RECORD                   
         MVC   SRTINV,TLINKINV          INVOICE                                 
*                                                                               
CPO30    MVC   SVIKEY,TLINPKEY     SAVE INVOICE KEY                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'5',TLINPKEY),38,=C'INV KEY 1'                    
*                                                                               
         USING TLDRD,R3                                                         
         MVC   SRTINDA,TLDRDA      SAVE DISK ADDRESS IN SORT RECORD             
*                                                                               
         XC    SRTAAGY,SRTAAGY     CLEAR ADJUSTMENT AGY/INV                     
         XC    SRTAINV,SRTAINV                                                  
*                                                                               
         OC    REQAGY,REQAGY       IF AGENCY FILTER GIVEN                       
         BZ    *+14                                                             
         CLC   SRTAGY,REQAGY       THEN ONLY TAKE THAT AGENCY                   
         BNE   CPO90                                                            
*                                                                               
         OC    REQINV,REQINV       IF INVOICE FILTER GIVEN                      
         BZ    *+14                                                             
         CLC   SRTINV,REQINV       THEN ONLY TAKE THAT INVOICE                  
         BNE   CPO90                                                            
*                                                                               
         MVI   INVTERR,0           CLEAR INVOICE ERROR TYPE                     
*                                                                               
         MVC   AIO,AINVIO          READ RECORD INTO AINVIO                      
         GOTO1 GETREC                                                           
                                                                                
         BRAS  RE,EXTCLI           EXTRACT CLIENT STATUS                        
                                                                                
         BRAS  RE,CHKREG           CHECK REGRESSION STATUS                      
         BNE   CPO90                                                            
                                                                                
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,AINVIO           R4 = A(INVOICE RECORD)                       
         USING TLIND,R4                                                         
         MVC   SRTAGY,TLINAGY      SAVE AGENCY IN SORT RECORD                   
         MVC   SRTINV,TLININV           INVOICE                                 
         XC    SRTINV,XFFS                                                      
*                                                                               
         OC    REQAGY,REQAGY       IF AGY/INV FILTER REQUESTED                  
         BZ    *+14                                                             
         OC    REQINV,REQINV                                                    
         BNZ   CPO60               THEN ACCEPT INVOICE NO MATTER WHAT           
*                                                                               
         MVI   ELCODE,TAPDELQ      R4 = A(INVOICE PAYMENT ELEMENT)              
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R4                                                         
         CLI   RECNUM,PC           PRINT CHECKS                                 
         BE    CPO31                                                            
         CLI   RECNUM,PK           AND P+ CHECKS                                
         BNE   CPO39                                                            
*                                                                               
CPO31    TM    WHEN,X'20'          URGENT?                                      
         BZ    CPO35                 NO, DIFFERENT RULES                        
*                                  URGENT PC AND PK                             
*                                    RUNNING CALIFORNIA CHECKS                  
         TM    TAPDPST2,TAPDPCAL     HAS TO HAVE CA CAST IN INVOICE             
         BZ    CPO90                 NO, SKIP                                   
*                                                                               
         CLI   RECNUM,PK           P+ URGENT CHECKS?                            
         BNE   CPO39               NO, OK CONTINUE                              
*                                                                               
         TM    AYSTAT7,TAATSPLA    AGENCY HAS PRTLA STATUS?                     
         BZ    CPO90               NO, SKIP IT                                  
         B     CPO39               YES, CONTINUE                                
***                                                                             
***                                OVERNIGHT PC OR PK                           
CPO35    DS    0H                                                               
         TM    REQOPTS,REQOCAL     RUNNING TO PRINT AT LA?                      
         BZ    CPO37                                                            
         TM    TAPDPST2,TAPDPCAL   CALIFORNIA CAST IN INVOICE?                  
         BZ    CPO90               NO, SKIP IT                                  
         B     CPO39                                                            
*                                                                               
CPO37    TM    TAPDPST2,TAPDPCAL   CALIFORNIA CAST IN INVOICE?                  
         BO    CPO90               YES, SKIP IT                                 
***                                                                             
CPO39    L     R4,AINVIO           R4 = A(INVOICE RECORD)                       
         MVI   ELCODE,TAINELQ      R4 = A(INVOICE STATUS ELEMENT)               
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAIND,R4                                                         
*                                                                               
         NI    TGPRVST,X'FF'-TGPSNHST-TGPRE111                                  
         OC    TAINBDTE,TAINBDTE   BILLED?                                      
         BZ    CPO40                                                            
         CLC   TAINBDTE,=X'B00701'                                              
         BNL   *+8                                                              
         OI    TGPRVST,TGPSNHST    NO HST PRIOR TO 2010 JUL 01                  
         CLC   TAINBDTE,=X'B10101'                                              
         BNL   *+8                                                              
         OI    TGPRVST,TGPRE111    BILLED PRIOR TO 2011 JAN 01                  
*                                                                               
CPO40    TM    TAINSTAT,TAINSCHK   IF CHECKS WRITTEN BIT SET                    
         BZ    CPO50                                                            
         GOTOR TESTRR              THEN IF NOT RERUNABLE                        
         BNE   CPO90               THEN SKIP                                    
*                                                                               
CPO50    TM    TAINSTA2,TAINSADJ   IF NOT PAYROLL ADJUSTMENT INVOICE            
         BO    *+12                                                             
         TM    TAINSTAT,TAINSAPR   THEN SKIP IF NOT QC APPROVED                 
         BZ    CPO90                                                            
*                                  SKIP IF IN ERROR, HELD, OR CANCELLED         
         TM    TAINSTAT,TAINSERR+TAINSHLD+TAINSCAN                              
         BNZ   CPO90                                                            
*                                                                               
         TM    TAINSTA2,TAINSRTH   SKIP IF RETRO INVOICE ON HOLD                
         BNZ   CPO90                                                            
*                                                                               
CPO60    L     R4,AINVIO           R4 = A(DUE DATE ELEMENT)                     
         MVI   ELCODE,TADDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+16                                                             
         MVI   INVTERR,TAINENDD    ERROR NO DUE DATE ELEMENT                    
         BAS   RE,ADDERI                                                        
         B     CPO80                                                            
*                                                                               
         USING TADDD,R4                                                         
         MVC   DUEDATE,TADDDATE    SAVE DUE DATE                                
*                                                                               
         L     R4,AINVIO           R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+16                                                             
         MVI   INVTERR,TAINECPI    ERROR NO PAYMENT ELEMENT/INVOICE             
         BAS   RE,ADDERI                                                        
         B     CPO80                                                            
*                                                                               
         USING TAPDD,R4            GET USE TABLE ENTRY FOR THIS USE,TYP         
         OC    REQOFFC,REQOFFC                                                  
         BZ    CPO63                                                            
         CLC   TAPDOFF,REQOFFC     MATCH ON OFFICE                              
         BNE   CPO90                                                            
*                                                                               
CPO63    GOTO1 USEVAL,DMCB,TAPDUSE,TAPDTYPE                                     
*                                                                               
         TM    TAPDPST1,TAPDPBNP   SKIP BILL NO PAYROLL INVOICES                
         BNZ   CPO90                                                            
*                                                                               
         TM    TAPDSTAT,TAPDSCAN   IF THIS IS CAN$ INVOICE                      
         BZ    *+16                                                             
         CLI   RECNUM,CN           THEN REJECT IF NOT PRINTING THEM             
         BNE   CPO90                                                            
         B     CPO76               PRINT ALL EMPLOYERS ON CN IF CAN$            
         CLI   RECNUM,CN           ELSE REJECT IF PRINTING CAN$ CHKS            
         BE    CPO90                                                            
*                                                                               
         TM    TAPDPST2,TAPDPEUR   IF THIS IS EURO INVOICE                      
         BZ    *+16                                                             
         CLI   RECNUM,EC           THEN REJECT IF NOT PRINTING THEM             
         BNE   CPO90                                                            
         B     CPO76               PRINT ALL EMPLOYERS ON EC IF EURO            
         CLI   RECNUM,EC           ELSE REJECT IF PRINTING EURO CHKS            
         BE    CPO90                                                            
*                                                                               
         CLI   RECNUM,DC           IF DMB&B CHECK RUN                           
         BNE   *+14                                                             
         CLC   TAPDEMP,=C'DM '     REJECT IF NOT THEM                           
         BNE   CPO90                                                            
*                                                                               
         CLI   RECNUM,CK           IF REGULAR CHECK RUN                         
         BNE   *+14                                                             
         CLC   TAPDEMP,=C'DM '     REJECT IF DMB&B EMPLOYER                     
         BE    CPO90                                                            
*                                  IF TAX/REFUND OR TAX/SSN XFER                
         TM    TAPDADJS,TAPDADTR+TAPDADTT+TAPDADST                              
         BZ    CPO65                                                            
         MVC   TGEMP,TAPDEMP       SET EMPLOYER IF SSN TRANSFER                 
*                                  CHECK EMPLOYER B/C NO TACO ELEMENT           
         CLC   TAPDEMP,=C'PP '     IF EMPLOYER IS PRINT PAYROLL SVCS            
         BNE   *+16                                                             
         CLI   RECNUM,PC           THEN REJECT IF NOT PRINTING THEM             
         BNE   CPO90                                                            
         B     CPO76                                                            
         CLI   RECNUM,PC           ELSE REJECT IF PRINTING PP CHKS              
         BE    CPO90                                                            
         B     CPO76                                                            
*                                                                               
CPO65    L     R4,AINVIO                                                        
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+16                                                             
         MVI   INVTERR,TAINECCO    ERROR NO COMMERCIAL DETAILS ELEMENT          
         BAS   RE,ADDERI                                                        
         B     CPO80                                                            
*                                                                               
         USING TACOD,R4            R4=A(COMMERCIAL DETAILS ELEMENT)             
         CLI   RECNUM,PC           IF PRINT CHECK RUN                           
         BNE   *+16                                                             
         CLI   TACOMED,TACOMEDP    REJECT IF NOT MEDIA PRINT COMML              
         BNE   CPO90                                                            
         B     *+12                                                             
         CLI   TACOMED,TACOMEDP    ELSE REJECT IF NOT PRINTING THEM             
         BE    CPO90                                                            
*                                                                               
CPO76    CLI   RECNUM,PK           IF EVENT CHECK RUN                           
         BNE   CPO77                                                            
         CLC   =C'P+ ',TGEMP       EMPLOYER MUST BE P+                          
         BE    CPO78                                                            
         CLI   TACOMED,TACOMEDE    COMML MEDIA MUST BE EVENT                    
         BNE   CPO90                                                            
         B     CPO78                                                            
                                                                                
CPO77    CLC   =C'P+ ',TGEMP       REJECT P+ EMPLOYER CHECKS                    
         BE    CPO90                                                            
         CLI   TACOMED,TACOMEDE    IF NOT EVENT CHECK RUN,                      
         BE    CPO90               REJECT IF MEDIA = EVENT.                     
         TM    AYSTAT7,TAAYSPPL    PRODUCTIONS PLUS AGENCY?                     
         BO    CPO90               REJECT IF P+ AGENCY                          
                                                                                
CPO78    CLI   REQURG,C'U'         IF THIS IS URGENT CHECK RUN                  
         BNE   CPO79                                                            
         CLI   RECNUM,PC           PRINT CHECKS TESTED ALREADY                  
         BE    CPO79                                                            
         BRAS  RE,TESTURG          DETERMINE IF INVOICE IS ELIGIBLE             
         BNE   CPO90                                                            
*                                                                               
CPO79    GOTO1 DOTRACE,DMCB,(C'I',AINVIO),0,=C'INVOICE'                         
*                                                                               
         BAS   RE,PROCINVC         PROCESS ALL CHECKS FOR THIS INVOICE          
*                                                                               
CPO80    BAS   RE,UPDINV           UPDATE INVOICE RECORD IN THE FILE            
*                                                                               
         GOTO1 FLUSHI              FLUSH INVOICE TOTALS FROM TABLES             
*                                                                               
         MVC   KEY,SVIKEY          RESTORE INVOICE KEY READING SEQUENCE         
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(L'TLINKEY),SVIKEY  THIS WILL FAIL ONLY WHEN RUN              
         BNE   CPO10                  WITH A DATE= CARD < TODAY AND             
*                                     WRITE = Y.  NEVER IN REAL WORLD.          
*                                     01/01/15 (SCHT)                           
*                                                                               
CPO90    GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         B     CPO10                                                            
*                                                                               
CPO100   GOTO1 DOTRACE,DMCB,(C'5',KEY),38,=C'INV KEY 2'                         
*                                                                               
         B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
* PROCESS THE CURRENT INVOICE - EXTRACT INFORMATION FROM IT AND VARIOUS         
* SUPPORT RECORDS.  THEN READ ALL CHECKS FOR THE INVOICE AND PROCESS            
* EACH IN TURN.                                                                 
*                                                                               
PROCINVC NTR1                                                                   
         ST    RD,INVRD            SAVE RD AND USE TO ABORT INVOICE             
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         MVI   SRTWTYP,0           CLEAR WARNING FLAGS                          
         MVI   SRTWARN,0                                                        
*                                                                               
         L     R4,AINVIO           R4 = A(INVOICE STATUS ELEMENT)               
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAIND,R4                                                         
*                                                                               
         NI    SRTBITS2,X'FF'-SRTGSTO   TURN OF OLD GST STATUS                  
         OC    TAINHDTE,TAINHDTE     IS THERE A PUR HLD DATE?                   
         BZ    PI03                                                             
         CLC   TAINHDTE,=X'A60701'   IF PUR HLD DATE IS < JUL01/06              
         BNL   PI05                                                             
         B     PI04                                                             
PI03     OC    TAINBDTE,TAINBDTE     IS THERE A BILLED DATE?                    
         BZ    PI05                                                             
         CLC   TAINBDTE,=X'A60701'   IF BILLED DATE IS < JUL01/06               
         BNL   *+8                                                              
PI04     OI    SRTBITS2,SRTGSTO      USE OLD GST RATE                           
*                                                                               
PI05     MVC   SRTINVS,TAINSTAT    SAVE INVOICE STATUS IN SORT RECORD           
         MVC   SRTINVS2,TAINSTA2        INVOICE STATUS TWO                      
         MVC   SRTPDTE,TAINPDTE         PAYMENT DATE                            
         MVC   SRTCDTE,CHKDATE1         CHECK DATE                              
*                                                                               
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+10                                                             
         MVC   SRTCDTE,CHKDATEL    SAVE DIFFERENT CHECK DATE                    
*                                                                               
         MVC   SRTKYEAR,SRTCDTE    SET YEAR IN ESUTAB                           
*                                                                               
         BRAS  RE,EXTRIPAY         EXTRACT PAY INFO FROM INVOICE REC            
         BRAS  RE,EXTRIMSC                 MISC INFO                            
*                                                                               
*                                  IF TAX REFUND OR TRANSFER INVOICE            
         TM    SRTADJS,TAPDADTR+TAPDADTT+TAPDADST                               
         BZ    PI10                                                             
         BAS   RE,EXTREMP          EXTRACT EMP INFO FROM EMP RECORD             
         B     PI50                SKIP TO CHECKS                               
*                                                                               
PI10     CLI   SRTADJS,0           IF THIS IS AN ADJUSTMENT INVOICE             
         BE    PI20                                                             
*                                                                               
         MVC   SRTAAGY,SRTAGY      THEN SAVE ADJUSTMENT AGY/INV                 
         MVC   SRTAINV,SRTINV                                                   
*                                                                               
         L     R4,AINVIO           R4 = A(ORIGINAL AGY/INV ELEMENT)             
         MVI   ELCODE,TAOIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERREIOI             ERROR IF NOT FOUND                           
         USING TAOID,R4                                                         
*                                                                               
         MVC   SRTAGY,TAOIAGY      SAVE AGY/INV IN SORT RECORD                  
         MVC   SRTINV,TAOIINV                                                   
*                                                                               
PI20     BAS   RE,EXTRICOM         EXTRACT CMCL INFO FROM INVOICE REC           
         BRAS  RE,EXTRIUSE                 USE INFO FROM INVOICE REC            
         BAS   RE,EXTREMP                  EMP INFO FROM EMP RECORD             
         BRAS  RE,EXTRAGYN                 AGY NAME FROM AGY RECORD             
         BAS   RE,EXTRCLIN                 CLI NAME FROM CLI RECORD             
         BAS   RE,EXTRPRDN                 PRD NAME FROM PRD OR INV REC         
*                                                                               
         BRAS  RE,EXTRAEXP                 ACTRA EXP DATE FROM COMM REC         
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   PI30                                                             
         MVI   BYTE,C'S'           EXTRACT SERVICES FOR NAME                    
         BAS   RE,EXTPNAME                                                      
         MVI   BYTE,C'P'           EXTRACT PHOTOGRAPHER'S NAME                  
         BAS   RE,EXTPNAME                                                      
*                                                                               
PI30     XC    SRTLEAD,SRTLEAD     PRE-CLEAR AFM LEADER                         
*                                                                               
         MVC   TGAGY,SRTAGY        CALL RECVAL TO GET FIRST MUSICIAN            
         MVC   TGCOM,SRTCOM            IN THE CAST                              
         XC    TGCSORT,TGCSORT                                                  
         MVI   TGCSORT,X'80'                                                    
         XC    TGSSN,TGSSN                                                      
         GOTO1 RECVAL,DMCB,TLCACDQ,(X'80',0)                                    
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'6',KEY),38,=C'CAST KEY'                          
*                                                                               
*                                  IF SAME COMMERCIAL                           
         CLC   KEY(TLCASORT-TLCAD),KEYSAVE                                      
         BNE   PI50                                                             
*                                                                               
         LA    R3,KEY              AND THERE IS A MUSICIAN IN THE CAST          
         USING TLCAD,R3                                                         
         TM    TLCASORT,X'80'                                                   
         BZ    PI50                                                             
*                                                                               
         MVC   SRTSSN,TLCASSN      THEN SET SSN TO FIRST MUSICIAN               
*                                                                               
         BRAS  RE,EXTRW4           EXTRACT NAME FROM W4 RECORD                  
*                                                                               
         MVC   SRTLEAD,SRTNAME     SAVE NAME IN SORT RECORD                     
*                                                                               
PI50     BAS   RE,CHKLOOP          LOOP THROUGH AND PROCESS CHECK RECS          
*                                                                               
PIX      B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
* UPDATE THE INVOICE STATUS ELEMENT ONB THE INVOICE RECORD WITH THE             
* CHECK DATE, THE CHECKS WRITTEN BIT, AND THE URGENT ID AND WRITE               
* THE RECORD BACK TO THE FILE.  ALSO UPDATE THE STATUS BYTE IN THE              
* BILLING/STATUS PASSIVE POINTER WITH THE NEW INVOICE STATUS.                   
*                                                                               
UPDINV   NTR1                                                                   
         MVC   KEY+TLDRDA-TLDRD(4),SRTINDA  SET D/A OF INVOICE RECORD           
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                       GET INVOICE RECORD                  
*                                                                               
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS NOT IN ERROR                   
         BNE   UI10                                                             
         MVC   AIO,AINVIO          THEN USE INVOICE I/O AREA                    
         B     UI30                                                             
*                                                                               
         USING TANUD,R4                                                         
UI10     CLI   INVTERR,TAINEW4     IF NO W4 RECORD                              
         BE    UI20                                                             
         CLI   INVTERR,TAINEW4E    NO W4 ELEMENT                                
         BE    UI20                                                             
         CLI   INVTERR,TAINEW4A    NO W4 ADDRESS                                
         BE    UI20                                                             
         CLI   INVTERR,TAINECRP    NO W4 RECORD FOR INDIV CORPORATION           
         BE    UI20                                                             
         CLI   INVTERR,TAINECW4    NO W4 RECORD                                 
         BE    UI20                                                             
         CLI   INVTERR,TAINECFD    NO FEDERAL WITHHOLDING ELEM IN W4            
         BE    UI20                                                             
         CLI   INVTERR,TAINEMAT    OR W4 TYPE DOES NOT MATCH PAYMENT            
         BNE   UI30                                                             
UI20     LA    R4,ELEMENT                                                       
         XC    ELEMENT,ELEMENT                                                  
         MVI   TANUEL,TANUELQ      SAVE PID OF THE PROBLEM PERFORMER            
         MVI   TANULEN,9                                                        
         MVI   TANUTYPE,TANUEPID                                                
         GOTO1 SSNPACK,DMCB,TGSSN,TANUMBER                                      
         GOTO1 ADDELEM                                                          
         DROP  R4                                                               
*                                                                               
UI30     L     R4,AIO              R4 = A(INVOICE STATUS ELEMENT)               
         MVI   ELCODE,TAINELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAIND,R4                                                         
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    UI40                                                             
         OI    TAINSTAT,TAINSERR   THEN SET ERROR BIT IN STATUS                 
         MVC   TAINTERR,INVTERR    AND SAVE ERROR TYPE                          
         B     UI50                                                             
*                                                                               
UI40     OI    TAINSTAT,TAINSCHK   ELSE SET CHECKS WRITTEN BIT                  
*                                                                               
         MVC   TAINCDTE,SRTCDTE    SAVE CHECK DATE                              
         MVC   TAINCKID,REQURG          URGENT ID                               
         MVC   TAINCKRN,TGTODAY1        RUN DATE                                
*                                                                               
UI50     GOTO1 PUTREC              WRITE BACK UPDATED RECORD                    
*        BRAS  RE,COMMIT                                                        
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',AINVIO),0,=C'UPDATED INV'                     
*                                                                               
         GOTO1 ADDPTRS             ADD PASSIVE POINTERS TO FILE                 
*                                                                               
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
UIX      B     XIT                                                              
         EJECT                                                                  
* EXTRACT INFO FROM THE ELEMENTS ON THE INVOICE RECORD THAT COME FROM           
* THE COMMERCIAL RECORD AT THE TIME OF THE PAYMENT.                             
*                                                                               
EXTRICOM NTR1                                                                   
*                                  PRE-CLEAR INVOICE CMCL INFO VARS             
         XC    SRTICOMS(SRTICOML),SRTICOMS                                      
*                                                                               
         MVC   AIO,AINVIO          EXTRA COMMERCIAL TITLE                       
*                                                                               
         MVI   ELCODE,TAFNELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAFNTVTL))                                     
         BNE   EIC2                                                             
         GOTO1 CHAROUT,DMCB,TAFNELQ,0,TAFNTVTL                                  
         B     EIC4                                                             
*                                                                               
EIC2     GOTO1 CHAROUT,DMCB,TAFNELQ,0,TAFNTTTL                                  
EIC4     MVC   AIO,AIO1                                                         
*                                                                               
         BE    *+12                IF ERROR CODE RETURNED THEN SET              
         BAS   RE,WARECTI              WARNING IN SORT RECOD                    
         B     EIC5                                                             
*                                                                               
         MVC   SRTTITL,TGNAME      ELSE SAVE TITLE IN SORT RECORD               
*                                                                               
EIC5     L     R4,AINVIO           R4 = A(COMMERCIAL DETAILS ELEMENT)           
         MVI   ELCODE,TACOELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRECCO                                                          
         USING TACOD,R4                                                         
*                                                                               
         MVC   SRTCID,TACOCID      SAVE COMMERCIAL ID                           
         MVC   TGCID,TACOCID            IN TGCID ALSO                           
         MVC   SRTAIR,TACOAIR           FIRST AIR DATE                          
         MVC   SRTFCYC,TACOFCYC         FIRST FIXED CYCLE                       
         MVC   SRTEXP,TACOEXP           EXPIRATION DATE                         
         MVC   SRTDUB,TACODUB           DUB DATE                                
         MVC   SRTMED,TACOMED           MEDIA                                   
         MVC   SRTCTYP,TACOTYPE         COMMERCIAL TYPE                         
         MVC   SRTCOSTA,TACOSTAT        STATUS                                  
*                                                                               
         L     R4,AINVIO           R4 = A(LIFT DETAILS ELEMENT)                 
         MVI   ELCODE,TALFELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC10               IF NONE THEN DONE                            
         USING TALFD,R4                                                         
*                                                                               
         MVC   SRTEDIT,TALFLID     SAVE EDIT VERSION # IN SORT RECORD           
         B     EIC12               SKIP VERSIONS ELEMENT                        
*                                                                               
EIC10    L     R4,AINVIO           R4 = A(VERSIONS ELEMENT)                     
         MVI   ELCODE,TAVRELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC12               IF NONE THEN DONE                            
         USING TAVRD,R4                                                         
*                                                                               
         MVC   SRTVERS,TAVRVERS    SAVE VERSION CODE                            
         MVC   SRTEDIT,TAVRCID                                                  
*                                                                               
EIC12    L     R4,AINVIO           R4 = A(SOAP CABLE ELEMENT)                   
         XC    CID2,CID2                                                        
         MVI   ELCODE,TASBELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC15               IF NONE THEN DONE                            
         USING TASBD,R4                                                         
*                                                                               
         MVC   CID2,TASBCID2       SAVE 2ND CID                                 
*                                                                               
EIC15    MVC   SRTCON,MYSPACES       CLEAR CONTRACTS OUT                        
         MVC   SRTCON34,MYSPACES                                                
         L     R4,AINVIO           R4 = A(COMMERCIAL CONTRACT ELEMENT)          
         MVI   ELCODE,TACCELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC20               IF NONE THEN DONE                            
         USING TACCD,R4                                                         
*                                                                               
         CLI   TACCNCON,0          IF N'CONTRACTS = 0 THEN DONE                 
         BE    EIC20                                                            
*                                                                               
         LA    RF,11               MOVE UP TO TWO CONTRACTS TO SORTREC          
         CLI   TACCNCON,2                                                       
         BL    *+8                                                              
         LA    RF,23                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTCON(0),TACCCON                                                
*                                                                               
EIC18    LA    RF,11               MOVE THE OTHER 2 CONTRACTS                   
         CLI   TACCNCON,3                                                       
         BL    EIC20                                                            
         BE    *+8                                                              
         LA    RF,23                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTCON34(0),TACCCON+24                                           
*                                                                               
EIC20    L     R4,AINVIO           R4 = A(FIRST CMCL STUDIO ELEMENT)            
         MVI   ELCODE,TACSELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC70               SKIP IF NONE FOUND                           
         USING TACSD,R4                                                         
*                                                                               
         CLI   RECNUM,PC           IF PRINT CHECKS                              
         BNE   EIC30                                                            
         CLI   TACSTYPE,TACSTYPS   AND IF SHOOT ELEMENT                         
         BNE   EIC30                                                            
         MVC   SRTSHOOT,TACSDATE   THEN SAVE SHOOT DATE                         
         MVC   SRTSTUD,TACSSTUD    AND SHOOT STUDIO                             
         MVC   SRTCITY,TACSCITY    AND SHOOT CITY                               
         B     EIC70                                                            
*                                                                               
EIC30    CLI   TACSTYPE,TACSTYPF   IF FILM ELEMENT                              
         BNE   EIC40                                                            
         MVC   SRTFILM,TACSDATE    THEN SAVE FILM DATE                          
         B     EIC60                                                            
*                                                                               
EIC40    CLI   TACSTYPE,TACSTYPR   ELSE IF RECORD ELEMENT                       
         BNE   EIC50                                                            
         MVC   SRTRCRD,TACSDATE    THEN SAVE RECORD DATE                        
         B     EIC60                                                            
*                                                                               
EIC50    CLI   TACSTYPE,TACSTYPM   ELSE IF MUSIC ELEMENT                        
         BNE   EIC60                                                            
         MVC   SRTMUS,TACSDATE     THEN SAVE MUSIC DATE                         
*                                                                               
EIC60    BAS   RE,NEXTEL           GET NEXT CS ELEMENT                          
         BE    EIC30               REPEAT UNTIL NO MORE FOUND                   
*                                                                               
EIC70    L     R4,AINVIO           R4 = A(OLD AGENCY/CID ELEMENT)               
         MVI   ELCODE,TAOCELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EIC90               SKIP IF NONE FOUND                           
         USING TAOCD,R4                                                         
*                                                                               
EIC80    MVC   SRTORIG,TAOCCID     SAVE LAST ORIGINAL CID                       
*                                                                               
         BAS   RE,NEXTEL           BUMP TO NEXT OC ELEMENT                      
         BE    EIC80               REPEAT UNTIL NO MORE FOUND                   
*                                                                               
EIC90    DS    0H                                                               
*                                                                               
EICX     B     XIT                                                              
         EJECT                                                                  
* EXTRACT INFO FROM THE EMPLOYER RECORD FOR THIS INVOICE IF IT IS               
* DIFFERENT THAN THE EMPLOYER FOR THE PREVIOUS INVOICE.  * NOTE * THE           
* EMPLOYER RECORD IS READ INTO AEMPIO.  THE ROUTINE THAT EXTRACTS THE           
* CAST DETAIL INFORMATION WILL ADDITIONALLY LOOK IN THE EMPLOYER RECORD         
* FOR THE STATE U.C. # FOR THE STATE OF WORK.                                   
*                                                                               
EXTREMP  NTR1                                                                   
         CLC   SRTEMP,PREVEMP      IF EMPLOYER HAS CHANGED                      
         BE    EEMX                                                             
*                                                                               
         XC    SRTEMPN,SRTEMPN     PRE-CLEAR EMPLOYER NAME                      
         XC    DEFSTATE,DEFSTATE             DEFAULT TAXABLE STATE              
*                                                                               
         L     RF,AEMPIO           PRE-SET EMPLOYER RECORD AS NOT FOUND         
         MVI   0(RF),0                                                          
*                                                                               
         XC    PREVEMP,PREVEMP     PRE-CLEAR PREVIOUS EMPLOYER                  
*                                                                               
         XC    BLOCK(44),BLOCK     GET EMP REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         MVC   AIO,AEMPIO                                                       
         GOTO1 RECVAL,DMCB,TLEMCDQ,(X'88',SRTEMP),BLOCK                         
         MVC   AIO,AIO1                                                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECEM              IN SORT RECORD AND RETURN                
         B     EEMX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AEMPIO),0,=C'EMP'                             
*                                                                               
         MVC   PREVEMP,SRTEMP      SET PREVIOUS EMP TO THIS EMP                 
*                                                                               
         MVC   SRTEMPN,BLOCK+8     SAVE NAME IN SORT RECORD                     
*                                                                               
         L     R4,AEMPIO           R4 = A(EMPLOYER DETAILS ELEMENT)             
         MVI   ELCODE,TAEDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EEMX                                                             
         USING TAEDD,R4                                                         
         MVC   DEFSTATE,TAEDDST    SAVE DEFAULT TAXABLE STATE                   
*                                                                               
EEMX     B     XIT                                                              
         EJECT                                                                  
* EXTRACT THE CLIENT NAME FROM THE CLIENT RECORD FOR THIS INVOICE IF IT         
* IS DIFFERENT THAN THE CLIENT FOR THE PREVIOUS INVOICE.                        
*                                                                               
EXTRCLIN NTR1                                                                   
         CLC   SRTCLI,PREVCLI      IF CLIENT HAS CHANGED                        
         BE    ECLX                                                             
*                                                                               
         XC    SRTCLIN,SRTCLIN     PRE-CLEAR CLIENT NAME                        
         XC    CLICLG,CLICLG       AND CLIENT GROUP                             
*                                                                               
         XC    PREVCLI,PREVCLI     PRE-CLEAR PREVIOUS CLIENT                    
*                                                                               
         XC    BLOCK(44),BLOCK     GET CLI REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         GOTO1 RECVAL,DMCB,TLCLCDQ,(X'88',SRTCLI),BLOCK                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECCL              IN SORT RECORD                           
         B     ECLX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AIO),0,=C'CLI'                                
*                                                                               
         MVC   PREVCLI,SRTCLI      SET PREVIOUS CLI TO THIS CLI                 
*                                                                               
         MVC   SRTCLIN,BLOCK+8     SAVE NAME IN SORT RECORD                     
*                                                                               
         USING TACID,R4                                                         
         L     R4,AIO              SAVE CLIENT GROUP                            
         MVI   ELCODE,TACIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         MVC   CLICLG,TACICLG                                                   
         DROP  R4                                                               
*                                                                               
ECLX     B     XIT                                                              
         EJECT                                                                  
* EXTRACT THE PRODUCT NAME FROM THE PRODUCT RECORD IF THE PRODUCT IS            
* NON-ZERO.  OTHERWISE LOOK IN THE COMMERCIAL RECORD FOR IT.                    
*                                                                               
EXTRPRDN NTR1                                                                   
         XC    SRTPRDN,SRTPRDN     PRE-CLEAR PRODUCT NAME                       
*                                                                               
         OC    SRTPRD,SRTPRD       IF PRODUCT IS NOT ZEROS                      
         BZ    EPR10                                                            
*                                                                               
         XC    BLOCK(44),BLOCK     GET PRD REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         GOTO1 RECVAL,DMCB,TLPRCDQ,(X'88',SRTPRD),BLOCK                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECPR              IN SORT RECORD AND RETURN                
         B     EPRX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AIO),0,=C'PROD'                               
*                                                                               
         MVC   SRTPRDN,BLOCK+8     ELSE SAVE NAME IN SORT RECORD                
         B     EPRX                                                             
*                                                                               
EPR10    MVC   TGAGY,SRTAGY        ELSE GET COMMERCIAL RECORD                   
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A0',SRTCOM)                              
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WARECOM              IN SORT RECORD AND RETURN                
         B     EPRX                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'3',AIO),0,=C'COMML'                              
*                                                                               
         GOTO1 CHAROUT,DMCB,TAFNELQ,0,TAFNTPRD  EXTR PRODUCT NAME               
*                                                                               
         MVC   SRTPRDN,TGNAME      SAVE PRODUCT NAME IN SORT RECORD             
*                                                                               
EPRX     B     XIT                                                              
         EJECT                                                                  
* EXTRACTS SERVICES FOR OR PHOTOGRAPHER NAME.  FIRST TRIES TANUD                
* ELEMENT, IF NONE, WILL THEN TRY TAFND ELEMENT.                                
*                                  NTRY BYTE = NAME TO GET (S,P)                
*                                                                               
EXTPNAME NTR1                                                                   
         ZIC   R2,BYTE             NAME TYPE REQUESTED                          
         MVI   ELCODE,TANUELQ      LOOK FOR TANUD ELEMENT                       
         CLM   R2,1,=C'S'          IF READING FOR SVC FOR NAME                  
         BNE   EXTPNM5                                                          
         MVC   SRTSVCNM,MYSPACES   PRECLEAR THE NAME                            
         MVI   BYTE,TANUTSVC       SET ELEMENT TYPE                             
         B     EXPNM10                                                          
EXTPNM5  MVC   SRTPHONM,MYSPACES   ELSE READING FOR PHOTOGRAPHER NAME           
         MVI   BYTE,TANUTPHO       SET ELEMENT TYPE                             
*                                                                               
EXPNM10  MVC   AIO,AINVIO          SET INVOICE IOAREA                           
         GOTO1 GETL,DMCB,(1,BYTE)                                               
         BNE   EXPNM20                                                          
         L     R4,TGELEM           R4=A(TANUD ELEMENT)                          
         USING TANUD,R4                                                         
         XC    DMCB,DMCB                                                        
         MVI   DMCB+3,TLAYCDQ      SET AGENCY RECORD CODE                       
         CLM   R2,1,=C'S'          UNLESS LOOKING FOR PHOTOGRAPHER              
         BE    *+8                                                              
         MVI   DMCB+3,TLW4CDQ      IN WHICH CASE, SET W4 FOR PHOTO              
         MVC   AIO,AIO1                                                         
         GOTO1 RECVAL,DMCB,,(X'A8',TANUMBER),0                                  
         BE    EXPNM30                                                          
*                                  NOW TRY FOR TAFND (OVERRIDE NAME)            
EXPNM20  XC    DMCB+8(4),DMCB+8                                                 
         MVI   DMCB+11,TAFNTPHO    SET FOR PHOTOGRAPHER OVERRIDE NAME           
         CLM   R2,1,=C'S'          UNLESS LOOKING FOR SERVICES FOR              
         BNE   *+8                                                              
         MVI   DMCB+11,TAFNTSVC    IN WHICH CASE, SET SVC FOR OVERRIDE          
         GOTO1 CHAROUT,DMCB,TAFNELQ,0                                           
*                                                                               
EXPNM30  CLM   R2,1,=C'S'          IF READING FOR SERVICES FOR NAME             
         BNE   *+14                                                             
         MVC   SRTSVCNM,TGNAME     SET SERVICES FOR                             
         B     *+10                                                             
         MVC   SRTPHONM,TGNAME     ELSE, SET PHOTOGRAPHER                       
*                                                                               
         MVC   AIO,AIO1            RESET IOAREA                                 
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* LOOP THROUGH THE CHECK RECORDS FOR THIS INVOICE AND PROCESS EACH IN           
* TURN.                                                                         
*                                                                               
CHKLOOP  NTR1                                                                   
         LA    R3,KEY              BUILD FIRST CHECK KEY FOR CURRENT            
         USING TLCKD,R3                AGENCY/INVOICE                           
         XC    TLCKKEY,TLCKKEY                                                  
         MVI   TLCKCD,TLCKCDQ                                                   
         MVC   TLCKAGY,SRTAGY                                                   
         MVC   TLCKINV,SRTINV                                                   
*                                                                               
         CLI   SRTADJS,0           IF THIS IS AN ADJUSTMENT INVOICE             
         BE    CH5                 AND NOT TAX REFUND OR TRANSFER               
         TM    SRTADJS,TAPDADTR+TAPDADTT+TAPDADST                               
         BNZ   CH5                                                              
         MVC   TLCKAGY,SRTAAGY     THEN SET AGY/INV FOR ADJUSTMENT              
         MVC   TLCKINV,SRTAINV                                                  
*                                                                               
CH5      MVC   FILENAME,CHKDIR     READ FIRST KEY                               
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
*                                                                               
*                                  WHILE NOT END OF AGENCY/INVOICE              
CH10     CLC   TLCKKEY(TLCKSORT-TLCKD),KEYSAVE                                  
         BNE   CH100                                                            
*                                                                               
         MVC   SVCKEY,TLCKKEY      SAVE CHECK KEY                               
*                                                                               
         OC    REQSSN,REQSSN       IF WE HAVE PERFORMER FILTER                  
         BZ    *+14                                                             
         CLC   REQSSN,TLCKSSN      TEST WE HAVE A MATCH                         
         BNE   CH90                                                             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'6',KEY),38,=C'CHK KEY'                           
*                                                                               
         MVC   AIO,ASRTCHK         READ CHECK RECORD INTO SORT RECORD           
         MVC   FILENAME,CHKFIL                                                  
*        MVI   RDUPDATE,C'Y'                                                    
         GOTO1 GETREC                                                           
         XC    FILENAME,FILENAME                                                
         MVC   AIO,AIO1                                                         
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'C',ASRTCHK),0,=C'CHECK'                          
*                                                                               
         L     R4,ASRTCHK          IF CHECK DETAILS ELEMENT NOT FOUND           
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    CH20                                                             
*                                                                               
         LA    R4,ELEM             THEN BUILD NEW ONE                           
         USING TACDD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACDEL,TACDELQ                                                   
         MVI   TACDLEN,TACDLNQ                                                  
*                                                                               
         MVC   AIO,ASRTCHK         ADD CHECK DETAILS ELEMENT                    
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
CH20     OC    REQCHKN,REQCHKN     IF CHECK NUMBER FILTER OPTION                
         BZ    *+14                                                             
         CLC   TACDCHK,REQCHKN     THEN CHECK NUMBER MUST BE THE SAME           
         BNE   CH90                                                             
*                                                                               
         CLI   RERUN,C'Y'          IF PROGRAM NOT RUN FOR EXTRACTION            
         BE    CH50                    ONLY                                     
         BRAS  RE,DELLIEN          AND CHECK WAS PREVIOUSLY GENERATED           
         BE    CH90                    FOR LIEN PAYEE THEN SKIP                 
         BRAS  RE,DELTRUST         AND CHECK WAS PREVIOUSLY GENERATED           
         BE    CH90                    FOR W4 TRUSTEE THEN SKIP                 
*                                                                               
CH50     BAS   RE,PROCCHK          ELSE PROCESS CHECK RECORD                    
*                                                                               
         MVC   TMPCLA,SRTCLA       SAVE CLA DATES AROUND                        
*                                                                               
         CLI   RERUN,C'Y'          IF PROGRAM NOT RUN FOR EXTRACTION            
         BE    CH90                    ONLY                                     
         OC    SRTTRST,SRTTRST     IF W4 MINOR WITHHOLDINGS TO TRUSTEE          
         BZ    CH55                                                             
         BAS   RE,TRSTCHK          THEN GENERATE CHECK FOR TRUSTEE              
         B     CH80                                                             
*                                                                               
CH55     OC    SRTLIEN,SRTLIEN     IF CHECK HAS LIEN AMOUNT                     
         BNZ   CH58                                                             
         NI    SRTBITS2,X'FF'-SRTBEFTL                                          
         B     CH90                                                             
CH58     BAS   RE,LIENCHK          THEN GENERATE CHECK FOR LIEN PAYEE           
         B     CH80                                                             
*                                                                               
CH80     MVC   SRTCLA,TMPCLA       RESTORE SRTCLA, XC'D IN TRST & LIEN          
*                                                                               
         USING TLCKD,R3                                                         
CH90     MVC   TLCKKEY,SVCKEY      RESTORE CHECK KEY READING SEQUENCE           
         MVC   FILENAME,CHKDIR                                                  
         OI    DMINBTS,X'08'       READ FOR DELETED IN CASE DEL'D RECS          
         GOTO1 HIGH                                                             
         NI    DMINBTS,X'F7'                                                    
*                                                                               
         GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         XC    FILENAME,FILENAME                                                
         B     CH10                                                             
*                                                                               
CH100    GOTO1 DOTRACE,DMCB,(C'6',KEY),38,=C'CHK KEY'                           
*                                                                               
CHX      B     XIT                                                              
         DROP  R3                                                               
         EJECT                                                                  
* PROCESS THE CHECK RECORD THAT HAS BEEN READ INTO THE END OF THE SORT          
* RECORD.  THE ROUTINE WILL READ SEVERAL SUPPORT RECORDS AND BUILD THE          
* REST OF THE SORT RECORD.  IF NECESSARY IT CALLS ALLTAX TO CALCULATE           
* THE WITHHOLDING.  FINALLY, THE ROUTINE WILL WRITE THE SORT RECORD TO          
* THE SORT FILE.                                                                
*                                                                               
PROCCHK  NTR1                                                                   
         LA    RE,SRTCVARS         PRE-CLEAR CHECK VARIABLES                    
         L     RF,=A(SRTCVARL)                                                  
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         XC    SRTYMEDC(SRTCVRL2),SRTYMEDC                                      
*                                                                               
         MVI   SRTBITS,0           PRE-CLEAR DESCRIPTION BITS                   
         NI    SRTBITS2,SRTGSTO    CLEAR ALL BITS EXCEPT OLD GST                
***      MVI   SRTBITS2,0          PRE-CLEAR DESCRIPTION BITS                   
         MVI   SRTTRBTS,0          PRE-CLEAR TRUST BITS                         
*                                                                               
         CLI   SRTWTYP,C'C'        IF PREVIOUS WARNING WAS FOR A CHECK          
         BNE   *+12                                                             
         MVI   SRTWTYP,0           THEN CLEAR WARNING FLAGS                     
         MVI   SRTWARN,0                                                        
*                                                                               
         LA    R3,KEY              R3 = A(CHECK KEY)                            
         USING TLCKD,R3                                                         
*                                                                               
         MVC   SRTCST,TLCKSORT     SAVE CAST SORT KEY IN SORT RECORD            
         MVC   SRTSSN,TLCKSSN           SSN                                     
         MVC   SRTCAT,TLCKCAT           CATEGORY                                
*                                                                               
         USING TLDRD,R3            SAVE DISK ADDRESS IN SORT RECORD             
         MVC   SRTDSKA,TLDRDA                                                   
*                                                                               
         GOTO1 CATVAL,DMCB,SRTCAT  GET CATEGORY ENTRY FOR LATER                 
*                                                                               
         BRAS  RE,EXTRW4           EXTRACT W4 INFO FROM W4 RECORD               
*                                                                               
         BAS   RE,EXTRCPAY         EXTRACT PAY INFO FROM CHECK RECORD           
         BAS   RE,MISCAMT                  AOS PST AMOUNT                       
         BAS   RE,EXTRCUPG                 UPGRADE INFO                         
         BRAS  RE,EXTRSESS                 SESSION INFO                         
         BAS   RE,EXTRCAST                 CAST INFO                            
         BAS   RE,EXTRUH                   USAGE HISTORY INFO                   
*                                                                               
         BAS   RE,EXTRNCDE         EXTRACT AGENT INFO FROM AGENT RECORD         
*                                                                               
         BAS   RE,TAKEGUAR         TAKE GUAR CREDITS FROM CHECK                 
*                                                                               
*        GOTO1 GETESU,DMCB,=C'FD ' MAKE SURE PERF ESU ENTRIES ARE BUILT         
         LA    RF,=C'FD '                                                       
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         LA    RF,=C'EU '                                                       
         GOTO1 GETESU,DMCB,(RF)    MAKE SURE PERF ESU ENTRIES ARE BUILT         
*                                                                               
         GOTO1 GETQTD,DMCB         MAKE SURE PERF QTD ENTRIES ARE BUILT         
*                                                                               
         BRAS  RE,GETEVT           GET EVTIME RECORD                            
*                                                                               
         BRAS  RE,INITPK           INITIALIZE P+ CHECK                          
*                                                                               
         GOTO1 NOCALC              IF WE DON'T WANT TO CALCULATE THIS           
         BNE   PC20                    CHECK                                    
*                                                                               
         BAS   RE,EXTRAMNT         THEN EXTRACT CHECK AMOUNTS                   
         BRAS  RE,PKDUE            FOR P+, PROCESS DUE COMPANY                  
*                                                                               
         CLI   RECNUM,EC                                                        
         BNE   PC40                                                             
         TM    SRTADJS,TAPDADVD    VOID?                                        
         BO    *+8                                                              
         BRAS  RE,WIRED                                                         
         B     PC40                                                             
*                                                                               
PC20     CLI   REQFRCW4,C'Y'       ELSE IF REQ OPTION TO FORCE W4 TYPE          
         BNE   PC30                                                             
         MVC   SRTW4TY,SRTTYPE     SET CHK W4 TYPE AS W4 TYPE ON W4 REC         
*                                                                               
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
         MVC   TAPDW4TY,SRTW4TY    MOVE TO ELEMENT AS WELL                      
         DROP  R4                                                               
*                                                                               
PC30     CLC   SRTTYPE,SRTW4TY     ELSE IF W4 TYPE DOES NOT MATCH               
         BE    PC35                    PAYMENT W4 TYPE THEN ERROR               
         CLI   SRTW4TY,TAW4TYCO                                                 
         BNE   ERREMAT             UNLESS SWITCHING FROM CORPORATION            
         CLI   SRTTYPE,TAW4TYCA        TO CANADIAN                              
         BNE   ERREMAT                                                          
*                                                                               
PC35     CLI   RECNUM,PK                                                        
         BNE   PC37                                                             
         CLI   SRTW4TY,TAW4TYCA    CANADIAN?                                    
         BNE   PC37                                                             
         OC    SRTW4CP,SRTW4CP     CANADIAN PROVINCE DEFINED?                   
         BZ    ERREMCP                                                          
*                                                                               
         USING TACAD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   PC37                                                             
         MVC   TACAUNIT,SRTW4CP    SET CANADIAN PROVINCE                        
         MVI   TACAUNIT+2,C' '                                                  
*                                                                               
PC37     BAS   RE,CALCAMNT         CALCULATE CHECK AMOUNTS                      
*                                                                               
PC40     BAS   RE,CHKESU           UPDATE ESUTAB W/ THIS CHECK                  
         BRAS  RE,CHKQTD           UPDATE QTDTAB W/ THIS CHECK                  
*                                                                               
         CLI   RERUN,C'Y'          IF NOT RUNNING FOR EXTRACTION ONLY           
         BE    PC45                                                             
         GOTO1 UPDPYTD             UPDATE PYTDTAB & CHECK WITH YTD AMTS         
         BRAS  RE,UPDYTD           UPDATE CHECK AND SORT WITH YTD WITH.         
         BRAS  RE,UPDQTD           UPDATE CHECK AND SORT WITH QTD WITH.         
         BRAS  RE,UPDSUI           UPDATE SUI                                   
*                                                                               
PC45     L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         MVC   TACDRUN,TGTODAY1    SAVE RUN DATE IN ELEMENT                     
         MVC   TACDDTE,SRTCDTE     SAVE CHECK DATE                              
         TM    SRTBITS,SRTBWIRE    IF PERF'S CHKS ARE WIRE TRANSFERRED          
         BO    PC48                                                             
         CLI   SRTCTYP,CTYSOAP     FOR SOAP COMMERCIALS                         
         BNE   PC49                                                             
         TM    SRTBITS,SRTBDD      OR PERF'S CHKS ARE DIRECT DEPOSIT            
         BZ    PC49                                                             
PC48     MVC   TACDCSH,SRTCDTE     SET CASHED DATE AS CHECK DATE                
PC49     OC    TACDBNK,TACDBNK     ANY BANK ACCT ALREADY, VOIDS                 
         BNZ   *+10                                                             
         MVC   TACDBNK,BANKCODE    SAVE BANK ACCOUNT CODE                       
         MVC   TACDSEQ,PROCSEQ     SAVE PROCESSED SEQUENCE NUMBER               
*                                                                               
         MVC   SRTCID,TGCID        SET CID WITH TGCID                           
         TM    TACDSTAT,TACDS2ND   IF CHECK FROM 2ND COMM'L OF SOC USE          
         BZ    PC50                                                             
         OC    CID2,CID2           AND HAVE 2ND CID                             
         BZ    *+10                                                             
         MVC   SRTCID,CID2         USE 2ND CID INSTEAD                          
*                                                                               
PC50     TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         BRAS  RE,GETSEQ           GET SPECIAL SEQUENCE NUMBER FOR IT           
*                                                                               
         L     RF,PROCSEQ          INCREMENT PROCESSED SEQUENCE NUMBER          
         AHI   RF,1                                                             
         ST    RF,PROCSEQ                                                       
*                                                                               
         BRAS  RE,PULLTST          DETERMINE IF CHECK SHOULD BE PULLED          
*                                                                               
         GOTO1 BLDSORT             BUILD SORT KEY                               
*                                                                               
         BRAS  RE,TRACEESU         TRACE ESUTAB ENTRIES FOR THIS PERF           
*                                                                               
*                                  PUT SORT RECORD TO SORTER FILE               
         GOTO1 VSORTER,DMCB,=C'PUT',SRTREC                                      
         L     RF,NOSRTCKS         INCREMENT NO SORTED CHECKS COUNTER           
         AHI   RF,1                                                             
         ST    RF,NOSRTCKS                                                      
*                                                                               
         MVC   BLOCK(14),MYSPACES                                               
         MVC   BLOCK(8),=C'SORT PUT'                                            
         LA    R2,TACDSEQ                                                       
         EDIT  (3,1(R2)),(5,BLOCK+9),ALIGN=RIGHT,FILL=0                         
         GOTO1 DOTRACE,DMCB,(C'P',SRTREC),SRTCHECK-SRTREC,(14,BLOCK)            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'P',ASRTCHK),0,=C'SRTCHECK'                       
*                                                                               
PCX      B     XIT                                                              
         DROP  R3,R4                                                            
         EJECT                                                                  
* THIS ROUTINE USES THE CURRENT CHECK, WHICH HAS A LIEN AMOUNT IN IT,           
* TO BUILD A CHECK FOR THE LIEN PAYEE.  IT THEN PUTS THE CHECK TO THE           
* SORTER FILE.                                                                  
*                                                                               
LIENCHK  NTR1                                                                   
         XC    SRTAMNTS(SRTAMNTL),SRTAMNTS  PRE-CLEAR CHECK AMOUNTS             
         XC    SRTPNH,SRTPNH                                                    
*                                                                               
         XC    SRTFSO,SRTFSO       INSURE THERE'S NO FSO INFO SAVED             
         XC    SRTFSON,SRTFSON                                                  
         XC    SRTCLA,SRTCLA                                                    
*                                                                               
         MVI   SRTBITS,SRTBLIN     SET LIEN GENERATED CHECK BIT                 
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE FROM THE CHECK RECORD:                
         MVI   ELCODE,TACWELQ          1) CHECK WITHHOLDING ELEMENTS            
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TADWELQ          2) DUECOMP WITHHOLDING ELEMENTS          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAODELQ          3) OTHER DEDUCTION ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACYELQ          4) YTD WITHHOLDING ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAYEELQ          5) BILLING YTD EARNINGS ELEMENT          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TABYELQ          6) BILLING YTD ELEMENT                   
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TATIELQ          7) TAX ID ELEMENT                        
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TANUELQ          8) GST ID NUMBER ELEMENT                 
         GOTO1 DELL,DMCB,(1,=AL1(TANUTGST))                                     
*                                      9) LIEN PAID TO S/S NUMBER               
         GOTO1 DELL,DMCB,(1,=AL1(TANUTLIN))                                     
         MVI   ELCODE,TABKELQ         10) BREAKOUT ELEMENT                      
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TARPELQ         11) RETRO PNH ELEMENT                     
         GOTO1 REMELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK RECORD)                         
         USING TLCKD,R4                                                         
         MVC   SRTLSSN,TLCKSSN     SAVE SSN OF LIEN PAYER                       
         MVC   TLCKSSN,LIENSSN     MOVE LIEN PAYEE SSN TO KEY                   
         MVC   SRTSSN,LIENSSN      AND TO SORT RECORD                           
*                                                                               
         LA    R4,ELEM             BUILD LIEN PAYER ELEMENT                     
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTLIN   SET LIEN PAYER TYPE                          
         MVC   TANUMBER(9),SRTLSSN SET LIEN PAYER S/S NUMBER                    
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         L     R4,ASRTCHK          R4 = A(LIEN WITHHOLDING ELEMENT)             
         MVI   ELCODE,TALWELQ                                                   
         BAS   RE,GETEL                                                         
         USING TALWD,R4                                                         
*                                                                               
         ICM   RF,15,TALWREC       RF = LIEN AMOUNT RECOVERED                   
         ST    RF,SRTNET           SAVE AS CHECK AMOUNT IN SORT RECORD          
*                                                                               
         LNR   RF,RF               CONVERT TO NEGATIVE AND SAVE BACK IN         
         STCM  RF,15,TALWREC           ELEMENT                                  
*                                                                               
         ST    RF,SRTLIEN          SAVE LIEN AMOUNT IN SORT RECORD              
*                                                                               
         BRAS  RE,EXTRW4           EXTRACT INFO FROM W4 RECORD                  
         MVC   SRTW4TY,SRTTYPE     SET W4 TYPE                                  
*                                                                               
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACAD,R4                                                         
         XC    TACAGUA,TACAGUA     CLEAR GUARANTEE CODE                         
         XC    TACANCDE,TACANCDE   CLEAR AGENT CODE HERE                        
         XC    SRTNCDE,SRTNCDE     AND IN SORT RECORD                           
*                                                                               
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R4                                                         
         MVC   TAPDW4TY,SRTW4TY             SET W4 TYPE                         
         XC    TAPDAMTS(TAPDAMTL),TAPDAMTS  CLEAR ALL AMOUNTS                   
         XC    TAPDTXNW,TAPDTXNW                                                
         XC    TAPDNTNW,TAPDNTNW                                                
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   TACDNET,SRTNET      SAVE NET CHECK AMOUNT IN ELEMENT             
         MVC   TACDFREQ,SRTFREQ         FREQUENCY                               
*                                                                               
         XC    TACDEARN,TACDEARN   CLEAR GROSS TAXABLE EARNINGS                 
         XC    TACDNTAX,TACDNTAX         NON-TAXABLE EARNINGS                   
         XC    TACDYREX,TACDYREX         YTD REIMB. EXPENSES                    
         MVC   TACDSEQ,PROCSEQ           PROCESSED SEQUENCE NUMBER              
         TM    SRTBITS2,SRTBEFTL                                                
         BZ    *+8                                                              
         OI    TACDSTA2,TACDSELN   SET EFT LIEN IN CHECK DETAILS                
*                                                                               
         L     RF,PROCSEQ          INCREMENT PROCESSED SEQUENCE NUMBER          
         AHI   RF,1                                                             
         ST    RF,PROCSEQ                                                       
*                                                                               
         CLI   RECNUM,EC           FOR EURO CHECKS ONLY                         
         BNE   *+8                                                              
         BRAS  RE,WIRED            LIEN CHECKS ARE WIRED TOO                    
*                                                                               
         BRAS  RE,PULLTST          DETERMINE IF CHECK SHOULD BE PULLED          
*                                                                               
         GOTO1 BLDSORT             BUILD SORT KEY                               
*                                  PUT SORT RECORD TO SORTER FILE               
         GOTO1 VSORTER,DMCB,=C'PUT',SRTREC                                      
         L     RF,NOSRTCKS         INCREMENT NO SORTED CHECKS COUNTER           
         AHI   RF,1                                                             
         ST    RF,NOSRTCKS                                                      
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'P',SRTREC),SRTCHECK-SRTREC,=C'LIEN PUT'          
         GOTO1 DOTRACE,DMCB,(C'P',ASRTCHK),0,=C'SRTCHECK'                       
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE USES THE CURRENT CHECK, WHICH HAS A W4 TRUST AMOUNT              
* TO BUILD A CHECK FOR THE TRUSTEE.  IT THEN PUTS THE CHECK TO THE              
* SORTER FILE.                                                                  
*                                                                               
TRSTCHK  NTR1                                                                   
         OC    TRUSTSSN,TRUSTSSN   IF W4 TRUSTEE SSN SPECIFIED                  
         BZ    XIT                                                              
*                                                                               
         XC    SRTAMNTS(SRTAMNTL),SRTAMNTS  PRE-CLEAR CHECK AMOUNTS             
         XC    SRTPNH,SRTPNH                                                    
         XC    SRTFSO,SRTFSO       INSURE THERE'S NO FSO INFO SAVED             
         XC    SRTFSON,SRTFSON                                                  
         XC    SRTCLA,SRTCLA                                                    
*                                                                               
         OI    SRTTRBTS,SRTTTRST   SET W4 TRUSTEE GENERATED CHK BIT             
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE FROM THE CHECK RECORD:                
         MVI   ELCODE,TACWELQ          1) CHECK WITHHOLDING ELEMENTS            
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TADWELQ          2) DUECOMP WITHHOLDING ELEMENTS          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAODELQ          3) OTHER DEDUCTION ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACYELQ          4) YTD WITHHOLDING ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAYEELQ          5) BILLING YTD EARNINGS ELEMENT          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TABYELQ          6) BILLING YTD ELEMENT                   
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TATIELQ          7) TAX ID ELEMENT                        
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAKPELQ          8) CHECK STOP ELEMENT                    
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAKLELQ          9) CHECK PULL ELEMENT                    
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACNELQ          10) STOP/PULL CANCEL ELEMENT             
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TANUELQ          11) GST ID NUMBER ELEMENT                
         GOTO1 DELL,DMCB,(1,=AL1(TANUTGST))                                     
*                                      12) TRUSTEE S/S NUMBER                   
         GOTO1 DELL,DMCB,(1,=AL1(TANUTRST))                                     
         MVI   ELCODE,TABKELQ          13) BREAKOUT ELEMENT                     
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACXELQ          14) CHECK EXTRA ELEMENT                  
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TARPELQ          15) RETRO PNH ELEMENT                    
         GOTO1 REMELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK RECORD)                         
         USING TLCKD,R4                                                         
         MVC   SRTTSSN,TLCKSSN     SAVE SSN OF MINOR PAYER                      
         MVC   TLCKSSN,TRUSTSSN    MOVE TRUSTEE SSN TO KEY                      
         MVC   SRTSSN,TRUSTSSN     AND TO SORT RECORD                           
*                                                                               
         LA    R4,ELEM             BUILD MINOR PAYER ELEMENT                    
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTRST   SET W4 TRUSTEE TYPE                          
         MVC   TANUMBER(9),SRTTSSN SET MINOR PAYER S/S NUMBER                   
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         ICM   RF,15,TRUSTCOL      RF = AMOUNT IN TRUST COLLECTED               
         TM    SRTADJS,TAPDADST    IF SSN TRANSFER, KEEP SIGN                   
         BO    TRSTC50                                                          
         LPR   RF,RF               MAKE SURE ALWAYS POSTIVE                     
TRSTC50  ST    RF,SRTNET           SAVE AS CHECK AMOUNT IN SORT RECORD          
*                                                                               
         XC    ELEM,ELEM                                                        
         LA    R4,ELEM             BUILD MINOR PAYER ELEMENT                    
         USING TAODD,R4                                                         
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPT   SET W4 TRUSTEE TYPE                          
*        LNR   RF,RF               CONVERT TO NEG. & SAVE IN ELEMENT            
         LCR   RF,RF               COMPLEMENT REG  & SAVE IN ELEMENT            
         STCM  RF,15,TAODAMT                                                    
         ST    RF,SRTTRST                          & IN SORT RECORD             
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         BRAS  RE,EXTRW4           EXTRACT INFO FROM W4 RECORD                  
         MVC   SRTW4TY,SRTTYPE     SET W4 TYPE                                  
*                                                                               
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACAD,R4                                                         
         XC    TACAGUA,TACAGUA     CLEAR GUARANTEE CODE                         
         XC    TACANCDE,TACANCDE   CLEAR AGENT CODE HERE                        
         XC    SRTNCDE,SRTNCDE     AND IN SORT RECORD                           
*                                                                               
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R4                                                         
         MVC   TAPDW4TY,SRTW4TY             SET W4 TYPE                         
         XC    TAPDAMTS(TAPDAMTL),TAPDAMTS  CLEAR ALL AMOUNTS                   
*                                                                               
         CLI   TAPDLEN,TAPDLNQ     OLD STYLE TAPD?                              
         BL    *+16                                                             
         XC    TAPDTXNW,TAPDTXNW                                                
         XC    TAPDNTNW,TAPDNTNW                                                
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   TACDNET,SRTNET      SAVE NET CHECK AMOUNT IN ELEMENT             
         MVC   TACDFREQ,SRTFREQ         FREQUENCY                               
*                                                                               
         XC    TACDEARN,TACDEARN   CLEAR GROSS TAXABLE EARNINGS                 
         XC    TACDNTAX,TACDNTAX         NON-TAXABLE EARNINGS                   
         XC    TACDYREX,TACDYREX         YTD REIMB. EXPENSES                    
         MVC   TACDSEQ,PROCSEQ           PROCESSED SEQUENCE NUMBER              
*                                                                               
         L     RF,PROCSEQ          INCREMENT PROCESSED SEQUENCE NUMBER          
         AHI   RF,1                                                             
         ST    RF,PROCSEQ                                                       
*                                                                               
         CLI   RECNUM,EC           FOR EURO CHECKS ONLY                         
         BNE   *+8                                                              
         BRAS  RE,WIRED            TRUST CHECKS ARE WIRED TOO                   
*                                                                               
         BRAS  RE,PULLTST          DETERMINE IF CHECK SHOULD BE PULLED          
*                                                                               
         GOTO1 BLDSORT             BUILD SORT KEY                               
*                                  PUT SORT RECORD TO SORTER FILE               
         GOTO1 VSORTER,DMCB,=C'PUT',SRTREC                                      
         L     RF,NOSRTCKS         INCREMENT NO SORTED CHECKS COUNTER           
         AHI   RF,1                                                             
         ST    RF,NOSRTCKS                                                      
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'P',SRTREC),SRTCHECK-SRTREC,=C'TRST PUT'          
         GOTO1 DOTRACE,DMCB,(C'P',ASRTCHK),0,=C'SRTCHECK'                       
         B     XIT                                                              
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE PAYMENT DETAILS ELEMENT IN THE CHECK             
* RECORD INTO THE SORT RECORD.                                                  
*                                                                               
EXTRCPAY NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
*                                                                               
         MVC   SRTW4TY,TAPDW4TY    SAVE W4 TYPE IN SORT RECORD                  
         MVC   SRTOV1,TAPDOV1           OVERSCALE PERCENTAGE                    
         MVC   SRTACDE,TAPDACDE         APPLIED CODE                            
         MVC   SRTPNH,TAPDPNH           PENSION & HEALTH AMOUNT                 
*                                                                               
         L     RE,TAPDPAYI         PAYMENT AMOUNT WILL COME FROM EITHER         
         A     RE,TAPDPAYC             TAPDPAYI OR TAPDPAYC                     
         ST    RE,SRTPAY                                                        
*                                                                               
         ST    RE,SRTNET           SAVE PAYMENT AMOUNT IN CHECK ACCUM           
*                                                                               
         MVC   SRTAPPL,TAPDAPPL    SAVE APPLIED CREDITS IN SORT RECORD          
         MVC   SRTGUAR,TAPDGUAR         GUARANTEE CREDITS                       
         MVC   SRTREXP,TAPDREXP         REIMBURSED EXPENSES                     
         MVC   SRTMDED,TAPDMDED         MISCELLANEOUS DEDUCTIONS                
         MVC   SRTDUES,TAPDDUES         UNION DUES                              
         MVC   SRTINR,TAPDINR           I&R FRATERNAL CONTRIBUTION              
         MVC   SRTSTUS,TAPDSTUS         STARTING USE NUMBER                     
         MVC   SRTUSES,TAPDUSES         NUMBER OF USES                          
         MVC   SRTVMAJ,TAPDMAJ          MAJORS FOR VERSIONS                     
         MVC   SRTVUNT,TAPDUNIT         UNITS                                   
*                                                                               
         TM    TAPDOPT1,TAPDOCAN   IF CANADIAN TAXES ARE TO BE TAKEN            
         BZ    *+8                                                              
         OI    SRTBITS,SRTBCAN     THEN SET CANADIAN TAKES BIT IN SORT          
*                                                                               
         CLI   TAPDLEN,TAPDLNQ                                                  
         BL    EXTRCP10                                                         
         L     RE,SRTNET                                                        
         ICM   RF,15,TAPDTXNW                                                   
         AR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
EXTRCP10 CLI   SRTTYPE,TAW4TYCA                                                 
         BNE   EXTRCP30                                                         
         OC    TAPDTXNW,TAPDTXNW                                                
         JNZ   EXTRCP30                                                         
*                                                                               
         MVC   AIO,ASRTCHK         SO WE MUST GET TAXABLE REIMB.                
         MVI   ELCODE,TATUELQ      FROM FEDERAL OR CANADIAN TATU                
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BE    EXTRCP20                                                         
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   EXTRCP30                                                         
*                                                                               
EXTRCP20 L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         L     RE,SRTNET                                                        
         ICM   RF,15,TATUSTRE      TAXABLE REIMBURSEMENTS                       
         AR    RE,RF                                                            
         ST    RE,SRTNET                                                        
*                                                                               
EXTRCP30 XC    TGFULL,TGFULL                                                    
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   EXTRCP40                                                         
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         MVC   TGFULL,TATUSTRE     SET TAXABLE REIMBURSEMENTS                   
EXTRCP40 LARL  RF,SVTXRE                                                        
         MVC   0(4,RF),TGFULL                                                   
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT AOS AMOUNT IF IT EXISTS                                               
*                                                                               
         USING TAMAD,R4            R4 = A(CHECK WITHHOLDING ELEMENT)            
MISCAMT  NTR1                                                                   
         MVI   ELCODE,TAMAELQ      GET MISC AMOUNTS                             
         L     R4,ASRTCHK                                                       
         BAS   RE,GETEL                                                         
MISCA10  BNE   MISCAX                                                           
         CLI   TAMATYPE,TAMATYPA   MISC AMOUNTS FOR AOS                         
         BE    MISCA20                                                          
         BAS   RE,NEXTEL                                                        
         B     MISCA10                                                          
*                                                                               
MISCA20  CLC   SRTSSN,=C'000112843'   IF AOS CHECK, DON'T ADD PST               
         BE    MISCAX                                                           
         CLC   SRTSSN,=C'000117358'   IF AOS CHECK, DON'T ADD PST               
         BE    MISCAX                                                           
         MVC   SRTPST,TAMAASPA     PST AMOUNT                                   
         L     RF,SRTNET                                                        
         A     RF,SRTPST                                                        
         ST    RF,SRTNET                                                        
*                                                                               
MISCAX   B     XIT                                                              
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE UPGRADE DETAILS ELEMENT IN THE CHECK             
* RECORD INTO THE SORT RECORD.  ONLY PRESENT IF PAID A VERSION.                 
*                                                                               
EXTRCUPG NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAUPELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   XIT                 SKIP IF NONE FOUND                           
         USING TAUPD,R4                                                         
*                                                                               
         MVC   SRTVUMAJ,SRTVMAJ    SAVE MAJORS/UNITS FROM PD ELEM AS            
         MVC   SRTVUUNT,SRTVUNT      UPGRADE MAJORS/UNITS IN SORT REC           
*                                                                               
         CLI   TGUSEQU,UCBL        IF CBL USE                                   
         BNE   ECU5                                                             
         MVC   SRTVUNT,TAUPICBU      SAVE INITIAL UNITS                         
         B     XIT                                                              
ECU5     MVC   SRTVMAJ,TAUPIMAJ   ELSE SAVE INITIAL MAJORS/UNITS IN             
         MVC   SRTVUNT,TAUPIUNT       SORT RECORD                               
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE CAST DETAILS ELEMENT IN THE CHECK                
* RECORD INTO THE SORT RECORD.  IF INDIVIDUAL IS BEING PAID AS A CORP           
* THEN EXTRACT THE INDIVIDUAL CORP FROM THE TAX ID ELEMENT IN THE CHECK         
* RECORD.                                                                       
*                                                                               
EXTRCAST NTR1                                                                   
         USING TLCKD,R4                                                         
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVC   TGCSORT,TLCKSORT                                                 
         DROP  R4                                                               
*                                                                               
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ERRECCA                                                          
         USING TACAD,R4                                                         
*                                                                               
         OC    TACAFRST,TACAFRST   IF FIRST SERVICES DATE IS ZERO               
         BNZ   ECA50                                                            
*                                                                               
*        TM    TGCAUNI,AFM         THEN IF UNION IS AFM                         
         GOTO1 UNITEST,DMCB,TGCAUNIS,AFM,0,0,0                                  
         BZ    ECA10                                                            
         MVC   TACAFRST,SRTMUS     THEN SAVE MUSIC DATE IN ELEMENT              
         B     ECA50                                                            
*                                                                               
ECA10    CLC   TACAONOF,=C'ON '    ELSE IF ON CAMERA                            
         BNE   ECA20                                                            
         MVC   TACAFRST,SRTFILM    THEN SAVE FILM DATE IN ELEMENT               
         B     ECA50                                                            
*                                                                               
ECA20    MVC   TACAFRST,SRTRCRD    ELSE SAVE RECORD DATE IN ELEMENT             
*                                                                               
ECA50    MVC   SRTONOF,TACAONOF    SAVE CAMERA (ON/OFF) IN SORT RECORD          
         MVC   SRTUNIT,TACAUNIT         TAX UNIT                                
         MVC   TGCTRY,=C'CA'       CANADIAN                                     
         GOTO1 TAXVAL,DMCB,(X'FF',TACAUNIT) UNIT                                
         BNE   ECA55                                                            
         MVC   SRTUNIT,=C'CN '     FORCE CANADIAN PROVINCES TO CN               
*                                                                               
ECA55    MVC   SRTDBL,TACADBL           N'DOUBLES                               
         MVC   SRTFRST,TACAFRST         FIRST SERVICES DATE                     
         MVC   SRTUN,TACAUN             UNION                                   
         MVC   SRTLOCL,TACALOCL         LOCAL                                   
         MVC   SRTYEAR,TACAYEAR         CONTRACT YEAR                           
         MVC   SRTNCDE,TACANCDE         AGENT CODE                              
         MVC   SRTCFCY,TACAFCYC         OVERRIDE OF CMCL FIRST CYCLE            
         MVC   SRTCEXP,TACAEXP          OVERRIDE OF CMCL EXPIRATION             
         MVC   SRTOV2,TACAOV2           OVERSCALE PERCENTAGE 2                  
         MVC   SRTCSTAT,TACASTAT        STATUS BYTE 1                           
         MVC   SRTCSTA2,TACASTA2        STATUS BYTE 2                           
*                                                                               
         GOTOR ITN                 ADJUST ITN PAYMENTS                          
         GOTOR PAX                 ADJUST CLA PAYMENTS FOR PAX                  
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,SRTUNIT)  LOOK UP TAX UNIT                        
         BE    ECA60                                                            
         CLI   RECNUM,PK           INVALID OK FOR PAYROLL PLUS CHECKS           
         BE    ECA70                                                            
         CLI   SRTW4TY,TAW4TYCO    INVALID OK ONLY FOR CORPORATIONS             
         BE    ECA70                                                            
         CLI   SRTW4TY,TAW4TYCA    AND CANDIANS                                 
         BE    ECA70                                                            
         CLI   SRTW4TY,TAW4TYTR    AND TRUSTEES                                 
         BE    ECA70                                                            
         CLI   SRTADJS,0           OR IF CHECK IS AN ADJUSTMENT                 
         BNE   ECA70                                                            
         B     ERRETAX                                                          
*                                                                               
ECA60    CLI   SRTUNIT+2,C' '      IF TAX UNIT IS A STATE                       
         BNE   *+14                                                             
         MVC   SRTSOW,SRTUNIT      THEN SAVE STATE OF WORK                      
         B     *+16                                                             
         MVC   SRTSOW,TGTASTCY     ELSE SAVE STATE OF WORK FOR CITY             
         MVC   SRTCOW,SRTUNIT      AND SAVE CITY OF WORK                        
*                                                                               
         CLI   SRTW4TY,TAW4TYIN    IF THIS IS INDIVIDUAL                        
         BNE   ECA70                                                            
         CLC   SRTSOR,=C'CN '      AND CANADIAN RESIDENT                        
         BNE   ECA70                                                            
         CLC   SRTSOW,=C'CN '      AND WORKING IN CANADA                        
         BE    ERRECNI             THEN SET ERROR - SHOULD BE CORP              
*                                                                               
ECA70    L     R4,ASRTCHK          IF TAX ID ELEMENT EXISTS                     
         MVI   ELCODE,TATIELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ECA100                                                           
         USING TATID,R4                                                         
*                                                                               
         MVC   SRTFSO,SRTSSN       THEN SAVE INDIVIDUAL SSN AND NAME AS         
         MVC   SRTFSON,SRTNAME          FSO SSN AND NAME                        
         MVI   SRTFSON+L'SRTFSON-1,C' '                                         
         MVC   SRTW4SOR,SRTSOR           SAVE ST OF RESID. FROM IND.            
         MVC   COMMNAME(L'SRTW4T),SRTW4T SAVE W4 TRUSTEE FROM IND.              
*                                                                               
         MVC   SRTSSN,TATIID       SAVE CORPORATION ID AS SSN                   
*                                                                               
         BRAS  RE,EXTRW4           EXTRACT INFO FROM W4 RECORD                  
         MVC   SRTW4T,COMMNAME     RESET W4 TRUSTEE FROM INDIVID.               
         XC    COMMNAME,COMMNAME                                                
*                                                                               
ECA100   L     R2,ALCLTAB          R2 = A(AFT LCLS NOT GETTING CHECKS)          
*                                                                               
ECA110   CLI   0(R2),0             IF END OF TABLE THEN DONE                    
         BE    ECAX                                                             
*                                                                               
         CLC   SRTUN,0(R2)         IF UNION/LOCAL MATCH TABLE ENTRY             
         BNE   ECA120                                                           
         CLC   SRTLOCL,3(R2)                                                    
         BE    ECA130              THEN GO SET NO UNION COPY BIT                
*                                                                               
ECA120   LA    R2,6(R2)            ELSE BUMP TO NEXT LCLTAB ENTRY               
         B     ECA110              AND LOOP BACK                                
*                                                                               
ECA130   OI    SRTBITS,SRTBLCL     SET BIT FOR NO UNION COPIES                  
*                                                                               
ECAX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE USAGE HISTORY RECORD                             
*                                                                               
EXTRUH   NTR1                                                                   
         CLI   SRTVERS,0           IF PAYMENT WAS MADE TO A VERSION             
         BE    XIT                                                              
         TM    TGUSSTA3,USEMTAB    AND USE IS CABLE OR WILDSPOT ...             
         BZ    XIT                                                              
                                                                                
         USING TLUHD,R4                                                         
         LA    R4,KEY              READ CAST LEVEL USAGE HISTORY                
         XC    KEY,KEY             KEY FOR THIS INVOICE                         
         MVI   TLUHCD,TLUHCDQ                                                   
         MVC   TLUHCOM,SRTCOM                                                   
         MVC   TLUHCSEQ,TGCSORT+4                                               
         MVC   TLUHUSE,SRTUSE                                                   
         MVC   TLUHINV,SRTINV                                                   
         XC    TLUHINV,XFFS                                                     
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLUHKEY),KEYSAVE                                           
         BNE   XIT                                                              
         DROP  R4                                                               
                                                                                
         GOTO1 GETREC              READ USAGE HISTORY RECORD                    
                                                                                
         USING TAUHD,R4                                                         
         L     R4,AIO              GET USAGE HISTORY ELEMENT                    
         MVI   ELCODE,TAUHELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
                                                                                
         CLI   TGUSEQU,ULCB        IF LOCAL CABLE USE                           
         BNE   EUH10                                                            
         MVC   SRTUTYP,TAUHTYPE    SAVE TYPE                                    
         B     XIT                                                              
                                                                                
EUH10    TM    TGUSSTA3,CBLUSE    IF CABLE USE                                  
         BZ    EUH20                                                            
         MVC   SRTVUUNT,TAUHUSN   OVERRIDE UNITS                                
         B     XIT                                                              
                                                                                
EUH20    MVC   SRTVUUNT,TAUHUNT   ELSE, OVERRIDE UNITS                          
         MVC   SRTVUMAJ,TAUHMAJ   AND MAJORS                                    
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* EXTRACT THE NAME AND ADDRESS OF THE AGENT IF THE CAST MEMBER USED             
* ONE AND PUT THE INFORMATION OVER THE PAYEE NAME AND ADDRESS.                  
*                                                                               
EXTRNCDE NTR1                                                                   
         OC    SRTNCDE,SRTNCDE     IF CAST MEMBER USED AN AGENT                 
         BZ    ENCX                                                             
*                                                                               
         XC    BLOCK(44),BLOCK     READ AGENT RECORD AND EXTRACT NAME           
         MVI   BLOCK,8+36                                                       
         GOTO1 TRNSAGT,DMCB,(X'40',SRTNCDE),TGAGT                               
         GOTO1 RECVAL,DMCB,TLANCCDQ,(X'88',TGAGT),BLOCK                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WAREAN               IN SORT RECORD                           
         B     ENCX                                                             
*                                                                               
         USING TAAND,R4                                                         
         L     R4,AIO              R4 = A(AGENT ELEMENT)                        
         MVI   ELCODE,TAANELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ENC5                SKIP IF NOT FOUND                            
         TM    TAANSTAT,TAANSNCK   IF IGNORING THIS AGENT ON CHECKS             
         BZ    ENC5                                                             
         XC    SRTNCDE,SRTNCDE     CLEAR AGENT CODE FROM SORT RECORD            
*                                                                               
         L     R4,ASRTCHK          R4 = A(CAST DETAILS EL ON CHECK REC)         
         MVI   ELCODE,TACAELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   ENCX                                                             
         USING TACAD,R4                                                         
         XC    TACANCDE,TACANCDE   CLEAR AGENT CODE                             
         B     ENCX                                                             
*                                                                               
ENC5     MVC   SRTPNAM,BLOCK+8     SAVE PAYEE NAME IN SORT RECORD               
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'N',AIO),0,=C'AGENT'                              
*                                                                               
         XC    SRTADD,SRTADD       PRE-CLEAR ADDRESS                            
*                                                                               
         L     R4,AIO              R4 = A(ADDRESS ELEMENT)                      
         MVI   ELCODE,TAADELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAADD,R4                                                         
*                                                                               
         BE    *+12                IF ERROR RETURNED THEN SET WARNING           
         BAS   RE,WAREANA              IN SORT RECORD                           
         B     ENCX                                                             
*                                                                               
         ZIC   R2,TAADLEN          SAVE ADDRESS IN SORT RECORD                  
         SH    R2,=H'4'                                                         
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   SRTADD(0),TAADADD                                                
*                                                                               
         GOTO1 =A(MIXDCASE),DMCB,SRTADD,30    1ST LINE                          
*                                                                               
         GOTO1 =A(MIXDCASE),DMCB,(X'80',SRTADD2),L'SRTADD-L'SRTADD1             
ENCX     B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE GETS THE CHECK AMOUNTS (WITHHOLDINGS, ETC.) FROM THE             
* CHECK RECORD.  THE AMOUNTS ARE ALREADY THERE BECAUSE THE CHECK HAS            
* ALREADY BEEN WRITTEN BY A PREVIOUS CHECK RUN WHICH DID ALL THE                
* CALCULATIONS NECESSARY TO COMPUTE THE WITHHOLDINGS AND THE NET CHECK          
* AMOUNT.  THIS ROUTINE EXTRACTS THOSE AMOUNTS FROM THE CHECK WITHHOLD          
* ELEMENTS, THE YTD ELEMENTS, AND THE CHECK DETAILS ELEMENT.                    
*                                                                               
EXTRAMNT NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   SRTNET,TACDNET      SAVE NET CHECK AMOUNT IN SORT RECORD         
*                                                                               
EA2      MVC   SRTNET,TACDNET      SAVE NET CHECK AMOUNT IN SORT RECORD         
         MVC   SRTYREX,TACDYREX         YTD REIMBURSED EXPENSES                 
         MVC   SRTEARN,TACDEARN    SAVE TAXABLE EARNINGS IN SORT REC            
         MVC   SRTNTAX,TACDNTAX    NON-TAXABLE EARNINGS                         
*                                                                               
         MVC   SRTGRS,SRTEARN      SET GROSS EARNINGS=TAXABLE EARNINGS          
         CLI   RECNUM,CN           IF CHECK IS FOR CAN$                         
         BE    EA3                                                              
         CLI   RECNUM,EC           OR FOR EUROS                                 
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYCO    OR FOR CORPORATION                           
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYCA    OR FOR CANADIAN                              
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYTR    OR FOR TRUSTEE                               
         BE    EA3                                                              
         CLI   SRTW4TY,TAW4TYFO    OR FOR FOREIGNER                             
         BNE   *+16                                                             
EA3      L     RF,SRTNTAX          GROSS EARNINGS=NON-TAXABLE EARNINGS          
         S     RF,SRTREXP          LESS REIMBURSED EXPENSES                     
         ST    RF,SRTGRS                                                        
*                                                                               
         XC    SRTYMDED(36),SRTYMDED                                            
         L     R4,ASRTCHK          R4 = A(CHECK EXTRA DETAILS ELEMENT)          
         MVI   ELCODE,TACXELQ                                                   
         USING TACXD,R4                                                         
         BAS   RE,GETEL                                                         
         BNE   EA70                                                             
         L     RF,TACXGST          SAVE GOODS AND SERVICES TAX                  
         LCR   RF,RF               (COMPLEMENT INTERNALLY)                      
         ST    RF,SRTGST                                                        
         L     RF,TACXGSTY         SAVE GOODS AND SERVICES TAX                  
         ST    RF,SRTYGST                                                       
         L     RF,TACXPST          SAVE PROVINCIAL SERVICE TAX                  
         ST    RF,SRTPST                                                        
         L     RF,TACXPSTY         SAVE PROVINCIAL SERVICE TAX                  
         ST    RF,SRTYPST                                                       
         MVC   SRTYMDED,TACXMDDY                                                
         MVC   SRTYMPT,TACXMTRY                                                 
         MVC   SRTYDUE,TACXDTPY                                                 
         MVC   SRTYCHAR,TACXPRCY                                                
         MVC   SRTYLIEN,TACXLIEY                                                
         MVC   SRTYDUES,TACXUNDY                                                
         MVC   SRTYTRST,TACXTRSY                                                
         MVC   SRTYDIRT,TACXDRTY                                                
         MVC   SRTYWIRE,TACXWRDY                                                
         CLI   TACXLEN,TACXLNQ                                                  
         BL    EA70                                                             
         MVC   SRTYPPLA,TACXPPAC                                                
         MVC   SRTYDUET,TACXDTPT                                                
         MVC   SRTYDUEN,TACXDTPN                                                
*                                                                               
EA70     CLI   RECNUM,PK                                                        
         BNE   EA80                                                             
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         MVC   AIO,AIO1                                                         
         BNE   EA80                                                             
                                                                                
         L     R3,TGELEM                                                        
         USING TATUD,R3                                                         
         MVC   SRTTXRE,TATUSTRE    SET TAXABLE REIMBURSEMENTS                   
*                                                                               
EA80     MVC   AIO,ASRTCHK         GET YTD ELEMENT                              
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         MVC   AIO,AIO1                                                         
         BNE   EA90                                                             
         L     R3,TGELEM           R3=A(YTD EARNINGS ELEMENT)                   
         USING TAYED,R3                                                         
         MVC   SRTYEARN,TAYEEARN   SAVE YTD TAXABLE EARNINGS                    
         MVC   SRTYNTAX,TAYENTAX            NON-TAXABLE EARNINGS                
*                                                                               
EA90     L     R4,ASRTCHK          R4 = A(FIRST CHECK WITHHOLD ELEMENT)         
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,GETEL                                                         
         BE    EA100                                                            
         LA    R4,DMYCWEL          IF NONE, POINT TO DUMMY AND PROCESS          
         USING TACWD,R4                                                         
*                                                                               
EA100    MVC   AIO,ASRTCHK         GET YTD ELEMENT WITH SAME UNIT               
         MVI   ELCODE,TACYELQ                                                   
         GOTO1 GETL,DMCB,(3,TACWUNIT)                                           
         MVC   AIO,AIO1                                                         
         BE    EA150               IF ERROR RETURNED                            
*                                                                               
         C     R4,=A(DMYCWEL)      THEN IF NOT POINTING TO DUMMY EL.            
         BE    *+16                                                             
         CLI   RERUN,C'Y'          AND IF RUNNING FOR EXTRACTION ONLY           
         BNE   *+8                                                              
         BAS   RE,WARECKY          THEN SET WARNING IN SORT RECORD              
*                                                                               
         XC    ELEM,ELEM           ELSE R5 = A(CLEAR BLOCK)                     
         LA    R5,ELEM                                                          
         B     EA170                                                            
*                                                                               
EA150    L     R5,TGELEM           ELSE R5 = A(YTD WITHHOLD ELEMENT)            
         USING TACYD,R5                                                         
         CLI   TACYLEN,TACYLN3Q                                                 
         BL    *+10                                                             
         MVC   SRTYTXRE,TACYTXRE   SAVE TAXABLE REIMBURSEMENTS                  
*                                                                               
EA170    CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   EA190                                                            
         MVC   SRTFTAX,TACWTAX     THEN SAVE FEDERAL TAX IN SORT RECORD         
         MVC   SRTFICA,TACWFICA              FICA                               
         MVC   SRTYFED,TACYTAX               YTD FEDERAL TAX                    
         MVC   SRTYFIC,TACYFICA              YTD FICA                           
         B     EA500                                                            
*                                                                               
EA190    CLI   TACWUNIT+2,C' '     ELSE IF UNIT IS A CITY                       
         BNH   EA300                                                            
         TM    TACWSTAT,TACWSTAX   THEN IF CITY IS TAXABLE CITY                 
         BZ    EA200                                                            
         MVC   SRTLTAX,TACWTAX     SAVE LOCAL TAX IN SORT RECORD                
         MVC   SRTYLOC,TACYTAX          YTD LOCAL TAX                           
         MVC   SRTTXCY,TACWUNIT    SAVE TAXABLE CITY                            
*                                                                               
EA200    TM    TACWSTAT,TACWSRES   IF CITY IS RESIDENCE CITY                    
         BZ    *+10                                                             
         MVC   SRTCOR,TACWUNIT     THEN SAVE CITY OF RESIDENCE                  
*                                                                               
         TM    TACWSTAT,TACWSWRK   IF CITY IS WORK CITY                         
         BZ    *+10                                                             
         MVC   SRTCOW,TACWUNIT     THEN SAVE CITY OF WORK                       
         B     EA500                                                            
*                                                                               
EA300    CLC   TACWUNIT,=C'CN '    ELSE IF UNIT IS CANADA                       
         BNE   EA350                                                            
         MVC   SRTCTAX,TACWTAX     THEN SAVE CANADIAN TAX IN SORT REC           
         MVC   SRTGSTPD,TACWGST              GOODS & SERVICES TAX PAID          
         MVC   SRTYCAN,TACYTAX               YTD CANADIAN TAX                   
         MVC   SRTYGST,TACYGST               YTD GOODS & SERVICES TAX           
         TM    TACWSTAT,TACWSWRK   IF STATE IS WORK STATE                       
         BZ    *+10                                                             
         MVC   SRTSOW,TACWUNIT     SAVE STATE OF WORK IN SORT RECORD            
         B     EA500                                                            
*                                                                               
EA350    TM    TACWSTAT,TACWSTAX   ELSE IF STATE IS TAXABLE STATE               
         BZ    EA400                                                            
         MVC   SRTSDI,TACWSDI      THEN SAVE SDI IN SORT REC                    
         MVC   SRTSUI,TACWSUI                SUI                                
         CLI   TACWLEN,TACWLN2Q    ONLY NEW TACW HAS FLI AMOUNTS                
         BL    *+10                                                             
         MVC   SRTSFLI,TACWSFLI              FLI                                
         MVC   SRTYSTA,TACYTAX               YTD STATE TAX                      
*                                                                               
         MVC   SRTTXST,TACWUNIT    SAVE TAXABLE STATE IN SORT RECORD            
*                                                                               
         GOTO1 GETSUC,DMCB,SRTTXST GET STATE U.C. # INTO SORT RECORD            
*                                                                               
EA400    TM    TACWSTAT,TACWSWRK   IF STATE IS WORK STATE                       
         BZ    EA450                                                            
         MVC   SRTWTAX,TACWTAX     THEN SAVE WORK STATE TAX IN SORT REC         
         MVC   SRTSOW,TACWUNIT     SAVE STATE OF WORK IN SORT RECORD            
         B     EA500                                                            
*                                                                               
EA450    MVC   SRTRTAX,TACWTAX     ELSE SAVE RES STATE TAX IN SORT REC          
         MVC   SRTSOR,TACWUNIT     SAVE STATE OF RES IN SORT RECORD             
*                                                                               
EA500    MVI   ELCODE,TACWELQ      GET NEXT CHECK WITHHOLD ELEMENT              
         BAS   RE,NEXTEL                                                        
         BE    EA100               REPEAT UNTIL NO MORE ELEMENTS                
*                                                                               
         MVI   ELCODE,TAODELQ      LOOK FOR FEDERAL OTHER DEDUCT ELEM           
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPF))                                     
         BNE   EA510               SKIP IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(FEDERAL OTHER DEDUCTION ELEM)         
         USING TAODD,R4                                                         
         L     RF,SRTFTAX          ADD TO FEDERAL TAXES                         
         A     RF,TAODAMT                                                       
         ST    RF,SRTFTAX                                                       
*                                                                               
EA510    GOTO1 GETL,DMCB,(1,=AL1(TAODTYPM))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(MPTRF OTHER DEDUCTION ELEM)           
         MVC   SRTMPT,TAODAMT                                                   
*                                                                               
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPP))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(CHARITY OTHER DEDUCTION ELEM)         
         MVC   SRTCHAR,TAODAMT                                                  
*                                                                               
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPD))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(DIRCET OTHER DEDUCTION ELEM)          
         MVC   SRTDIRCT,TAODAMT                                                 
*                                                                               
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPW))                                     
         BNE   *+14                SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(WIRE OTHER DEDUCTION ELEM)            
         MVC   SRTWIRE,TAODAMT                                                  
*                                                                               
         BRAS  RE,PPLAAMT          GET PAYROLL+ COMMISSION                      
*                                                                               
         MVC   AIO,AIO1                                                         
         L     RF,SRTFTAX          TOTAL DEDUCTIONS = FEDERAL TAX               
         A     RF,SRTFICA              + FICA                                   
         A     RF,SRTRTAX              + RESIDENT STATE TAX                     
         A     RF,SRTWTAX              + WORK STATE TAX                         
         A     RF,SRTSDI               + STATE DISABILITY INSURANCE             
         A     RF,SRTSUI               + STATE UNEMPLOYMENT INSURANCE           
         A     RF,SRTSFLI              + STATE FAMILY LEAVE INSURANCE           
         A     RF,SRTLTAX              + LOCAL TAX                              
         A     RF,SRTCTAX              + CANADIAN TAX                           
**NO-OP**A     RF,SRTGSTPD             + GST (NOT A DEDUCTION 9/99)             
         A     RF,SRTMPT               + MPTRF                                  
         A     RF,SRTCHAR              + PERMANENT CHARITIES                    
         A     RF,SRTDIRCT             + DIRECT DEPOSIT                         
         A     RF,SRTWIRE              + WIRE TRANSFER                          
         A     RF,SRTPPLA              + PAYROLL+ COMMISSION                    
         A     RF,SRTMDED              + MISCELLANEOUS DEDUCTIONS               
         A     RF,SRTDUES              + UNION DUES                             
         ST    RF,SRTTDED          SAVE TOTAL DEDUCTIONS                        
*                                                                               
         BAS   RE,DUEWITH          EXTRACT DUE COMPANY AMOUNT                   
         BAS   RE,LIENWITH         EXTRACT LIEN AMOUNT                          
         BAS   RE,TRSTWITH         EXTRACT W4 TRUSTEE AMOUNT                    
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE CHECK RECORD FOR DUE COMPANY WITHHOLDING            
* ELEMENTS, AND IF IT FINDS ANY, IT CALCULATES THE DUE COMPANY AMOUNT           
* FROM THEM AND SAVES THE AMOUNT IN SRTDUE.  ADDITIONALLY, IF THE CHECK         
* IS A VOID OR ISSUE CHECK, THEN THE DUE COMPANY AMOUNTS WILL BE                
* DEDUCTED FROM THEIR CORRESPONDING DUE COMPANY TABLE ENTRIES.                  
*                                                                               
DUEWITH  NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(FIRST DUECOMP WITH ELEMENT)           
         MVI   ELCODE,TADWELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   DWX                 IF NONE FOUND THEN DONE                      
         USING TADWD,R4                                                         
*                                                                               
DW10     ICM   RF,15,TADWREC       ADD AMOUNT TO DUE COMPANY AMOUNT             
         A     RF,SRTDUE                                                        
         ST    RF,SRTDUE                                                        
*                                                                               
         BAS   RE,NEXTEL                                                        
         BNE   DW15                                                             
         B     DW10                                                             
*                                                                               
DW15     CLI   SRTADJS,0           IF CHECK IS A VOID OR ISSUE CHECK            
         BE    DW90                                                             
         TM    SRTADJS,TAPDADVD                                                 
         BO    DW20                                                             
         TM    SRTADJS,TAPDADIS                                                 
         BZ    DW90                                                             
*                                                                               
DW20     ST    R4,TGDUB            THEN SAVE A(DW ELEMENT) IN TGDUB             
         MVC   FULL,TADWREC        SAVE COLLECTED AMOUNT IN FULL                
*                                                                               
         LA    R3,KEY              BUILD KEY FOR DUE COMPANY RECORD             
         USING TLDUD,R3                                                         
         XC    TLDUKEY,TLDUKEY                                                  
         MVI   TLDUCD,TLDUCDQ                                                   
         MVC   TLDUSSN,SRTSSN                                                   
         MVC   TLDUDUC,TADWDUC                                                  
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND THEN ERROR               
         CLC   TLDUKEY,KEYSAVE                                                  
         BNE   ERREDU                                                           
*                                  GET DUETAB ENTRY FOR THIS CODE               
         GOTO1 GETDUE,DMCB,TADWDUC                                              
         BNE   ERREDU                                                           
*                                                                               
         L     R5,0(R1)            R5 = A(DUETAB ENTRY)                         
         USING DUETABD,R5                                                       
*                                                                               
         LA    R4,DUEELEM          R4 = A(DUE COMPANY DETAILS ELEMENT)          
         USING TADUD,R4                                                         
*                                                                               
         ICM   RF,15,TADUCOL       ADD COLLECTED AMOUNT TO ELEMENT              
         A     RF,FULL                                                          
         STCM  RF,15,TADUCOL                                                    
*                                                                               
         L     R4,TGDUB            SAVE BALANCE REMAINING IN DW ELEM            
         USING TADWD,R4                                                         
         STCM  RF,15,TADWBAL                                                    
*                                                                               
DW90     MVI   ELCODE,TADUELQ      GET NEXT WITHHOLDING ELEMENT                 
         BAS   RE,NEXTEL                                                        
         BE    DW10                REPEAT UNTIL NO MORE ELEMENTS                
*                                                                               
DWX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE CHECK RECORD FOR A LIEN WITHHOLDING                 
* ELEMENT, AND IF IT FINDS ONE, IT SAVES THE AMOUNT IN SRTLIEN.                 
* ADDITIONALLY, IF THE CHECK IS A VOID OR ISSUE CHECK, THEN THE LIEN            
* AMOUNT WILL BE DEDUCTED FROM ITS CORRESPONDING LIEN TABLE ENTRY.              
*                                                                               
LIENWITH NTR1                                                                   
         L     R4,ASRTCHK          R4 = A(LIEN WITHHOLDING ELEMENT)             
         MVI   ELCODE,TALWELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   LWX                 IF NOT FOUND THEN DONE                       
         USING TALWD,R4                                                         
*                                                                               
         ST    R4,TGDUB            SAVE A(LW ELEMENT) IN DUB                    
*                                                                               
         MVC   SRTLIEN,TALWREC     SAVE AMOUNT IN SORT RECORD                   
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSLIN   IF CHECK IS FOR LIEN PAYEE THEN              
         BZ    LW20                                                             
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTLIN  EXTRACT LIEN PAYER SSN          
         MVC   AIO,AIO1                                                         
         MVC   SRTLSSN,TGNAME                   SAVE IN SORT RECORD             
         B     LWX                              AND THEN GET OUT                
*                                                                               
LW20     LA    R3,KEY              ELSE BUILD KEY FOR LIEN                      
         USING TLLND,R3                                                         
         XC    TLLNKEY,TLLNKEY                                                  
         MVI   TLLNCD,TLLNCDQ                                                   
         MVC   TLLNSSN,SRTSSN                                                   
         L     R4,TGDUB                                                         
         USING TALWD,R4                                                         
         MVC   TLLNLIN,TALWLIN                                                  
*                                                                               
         GOTO1 HIGH                IF RECORD NOT FOUND THEN ERROR               
         CLC   TLLNKEY,KEYSAVE                                                  
         BNE   ERRELN                                                           
*                                  ELSE GET LIEN TABLE ENTRY                    
         GOTO1 GETLIEN,DMCB,TLLNLIN                                             
*                                                                               
         L     R5,0(R1)            R5 = A(LIEN TABLE ENTRY)                     
         USING LINTABD,R5                                                       
*                                                                               
         LA    R4,LINELEM          R4 = A(LIEN DETAILS ELEMENT)                 
         USING TALND,R4                                                         
*                                                                               
         MVC   LIENSSN,TALNPAYE    SAVE SSN FOR LIEN PAYEE CHECK                
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS A VOID OR ISSUE CHECK            
         BE    LWX                                                              
         TM    SRTADJS,TAPDADVD                                                 
         BO    *+12                                                             
         TM    SRTADJS,TAPDADIS                                                 
         BZ    LWX                                                              
*                                                                               
         ICM   RF,15,TALNCOL       ADD COLLECTED AMOUNT TO ELEMENT              
         A     RF,SRTLIEN                                                       
         STCM  RF,15,TALNCOL                                                    
*                                                                               
         L     R4,TGDUB            SAVE BALANCE REMAINING IN LW ELEM            
         USING TALWD,R4                                                         
         STCM  RF,15,TALWBAL                                                    
*                                                                               
LWX      B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE CHECK RECORD FOR A W4 TRUSTEE DEDUCTION             
* ELEMENT, AND IF IT FINDS ONE, IT SAVES THE AMOUNT IN SRTTRST.                 
* ADDITIONALLY, IF THE CHECK IS A VOID OR ISSUE CHECK, THEN THE AMOUNT          
* IN TRUST WILL BE DEDUCTED FROM ITS CORRESPONDING TABLE ENTRY.                 
*                                                                               
TRSTWITH NTR1                                                                   
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAODELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPT))                                     
         MVC   AIO,AIO1                                                         
         BNE   TRSTWX                                                           
         L     R4,TGELEM           R4 = A(W4 TRUSTEE DEDUCTION ELE)             
         ST    R4,TGDUB            SAVE ELEMENT ADDRESS IN DUB                  
         USING TAODD,R4                                                         
         MVC   SRTTRST,TAODAMT     SAVE AMOUNT IN SORT RECORD                   
         MVC   TRUSTCOL,TAODAMT    SAVE AMOUNT IN SORT RECORD                   
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTRST  EXTRACT SSN                     
         MVC   AIO,AIO1                                                         
         TM    TACDSTAT,TACDSTRS   IF CHECK IS FOR W4 TRUSTEE THEN              
         BZ    *+14                                                             
         MVC   SRTTSSN,TGNAME      THEN SAVE MINOR PAYER IN SORT REC            
         B     TRSTWX              AND EXIT                                     
*                                                                               
         XC    TRUSTSSN,TRUSTSSN   ELSE, IF TRUSTEE SSN FOUND                   
         CLC   TGNAME,MYSPACES                                                  
         BNH   *+10                                                             
         MVC   TRUSTSSN,TGNAME     SAVE SSN OF TRUSTEE GEN. CHK                 
         BRAS  RE,GETTRST          GET TABLE ENTRY                              
         L     R5,0(R1)            R5 = A(TABLE ENTRY)                          
         USING TRSTABD,R5                                                       
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS A VOID, ISSUE, REFUND            
         BE    TRSTWX                                                           
         TM    SRTADJS,TAPDADVD                                                 
         BO    TRSTW10                                                          
         TM    SRTADJS,TAPDADIS                                                 
         BO    TRSTW10                                                          
         TM    SRTADJS,TAPDADTR                                                 
         BZ    TRSTWX                                                           
*                                                                               
TRSTW10  ICM   RF,15,TRSTCOL       ADD COLLECTED AMT TO TABLE                   
         A     RF,SRTTRST                                                       
         STCM  RF,15,TRSTCOL                                                    
*                                                                               
         TM    SRTADJS,TAPDADTR    IF NOT TRUST REFUND CHECK                    
         BO    TRSTWX                                                           
         L     R4,TGDUB            SAVE BALANCE REMAINING IN ELEM               
         USING TAODD,R4                                                         
         STCM  RF,15,TAODAMT                                                    
*                                                                               
TRSTWX   B     XIT                                                              
         DROP  R5                                                               
         EJECT                                                                  
* THIS ROUTINE CALCULATES THE CHECK AMOUNTS AND DETERMINES THE FINAL            
* NET CHECK AMOUNT.  IT MAKE CALLS TO DUECOMP, TAXES, MISCDED, LIENS,           
* AND TRUSTEES TO COMPUTE THE W/HOLDINGS. THE FINAL NET CHECK AMT WILL          
* BE IN SRTNET.   LASTLY, THE ROUTINE WILL UPDATE THE CHECK DETAILS             
* ELEMENT WITH CHECK AMOUNTS AND OTHER INFORMATION.                             
*                                                                               
CALCAMNT NTR1                                                                   
         TM    AYSTAT7,TAAYSTSM    IF TAXING USING SEM-MONTHLY                  
         BZ    *+8                 FREQUENCY, SET STATUS                        
         OI    SRTBITS2,SRTTAXSM                                                
*                                                                               
         TM    AYSTAT7,TAAYSTWK    IF TAXING USING WEEKLY                       
         BZ    *+8                 FREQUENCY, SET STATUS                        
         OI    SRTBITS2,SRTTAXWK                                                
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE FROM THE CHECK RECORD:                
         MVI   ELCODE,TACWELQ          1) CHECK WITHHOLDING ELEMENTS            
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TADWELQ          2) DUECOMP WITHHOLDING ELEMENTS          
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TALWELQ          3) LIEN WITHHOLDING ELEMENTS             
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAODELQ          4) OTHER DEDUCTION ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TACYELQ          5) YTD WITHHOLDING ELEMENTS              
         GOTO1 REMELEM                                                          
         MVI   ELCODE,TAYEELQ          6) YTD EARNINGS ELEMENT                  
         GOTO1 DELL,DMCB,(1,=AL1(TAYETCHK))                                     
         MVI   ELCODE,TANUELQ          7) GST ID NUMBER ELEMENT                 
         GOTO1 DELL,DMCB,(1,=AL1(TANUTGST))                                     
         MVC   AIO,AIO1                                                         
*                                                                               
         BAS   RE,DUECOMP          APPLY DUE COMPANIES TO CHECK                 
         BRAS  RE,TUWITH           BUILDS CERTAIN TU ELEMENTS                   
         BRAS  RE,PKDUE            FOR P+, PROCESS DUE COMPANY                  
*                                                                               
         MVC   SRTGRS,SRTNET       SAVE GROSS EARNINGS IN SORT RECORD           
*                                                                               
         MVI   ELCODE,TACAELQ      GET CAST UNIT                                
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,0                                                      
         MVC   AIO,AIO1                                                         
         BNE   CA05                NOT FOUND - DON'T PASS EARNINGS              
         L     R3,TGELEM                                                        
         USING TACAD,R3                                                         
         GOTO1 =A(GETPSTR),DMCB,TACAUNIT   GET PST RATE AND HST INDICAT         
*                                                                               
CA05     GOTO1 =V(TACKTAX),DMCB,(RC)  TAKE TAXES FROM CHECK                     
*                                                                               
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   CA10                                                             
         L     RF,SRTNET           ADD REIMBURSED EXPENSES TO CHECK             
         A     RF,SRTREXP                                                       
         ST    RF,SRTNET                                                        
         LTR   RF,RF                                                            
         BNM   CA06                IF NEGATIVE, THEN ADVANCE RECONC.            
         OI    SRTBITS2,SRTADREC   NOT ALL TAKEN                                
         XC    SRTNET,SRTNET                                                    
         ST    RF,TGFULL           ADVANCE RECONCILIATION NOT TAKEN             
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTARE))                                     
         BNE   CA06                                                             
         L     R4,TGELEM                                                        
         USING TANUD,R4                                                         
         ICM   RF,15,TANUOVAM      TOTAL ADVANCE RECONCILIATION                 
         L     RE,TGFULL           AMOUNT NOT TAKEN                             
         SR    RF,RE                                                            
         STCM  RF,15,TANUOVAM      ACTUAL ADVANCE RECONC. TAKEN                 
*                                                                               
         USING TAPDD,R4            UPDATE TAPDREXP                              
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   CA06                                                             
         ICM   RF,15,TAPDREXP                                                   
         L     RE,TGFULL           ACTUAL ADVANCE RECONC. TAKEN                 
         SR    RF,RE                                                            
         STCM  RF,15,TAPDREXP                                                   
         ST    RF,SRTREXP                                                       
*                                                                               
CA06     BRAS  RE,PPLAAMT                                                       
                                                                                
         ICM   RE,15,SRTPPLA                                                    
         L     RF,SRTNET                                                        
         AR    RF,RE                                                            
         BNM   CA08                NET NOT NEGATIVE AFTER P+ AGY COMM           
         ICM   RF,15,SRTNET        NEGATIVE, MAKE P+ AGY COMM = -NET            
         LCR   RF,RF               REVERSE THE SIGN                             
         STCM  RF,15,SRTPPLA                                                    
         OI    SRTBITS2,SRTBPPLA   P+ AGY COMM CHANGED                          
                                                                                
         XR    RF,RF               AND MAKE NET=0.00                            
                                                                                
CA08     ST    RF,SRTNET                                                        
                                                                                
CA10     BRAS  RE,MISCDED          SUBTRACT OUT MISC DEDUCTIONS                 
*                                                                               
         BRAS  RE,TRST             APPLY W4 TRUSTEE TO CHECK                    
         OC    SRTTRST,SRTTRST     IF W4 TRUSTEE APPLIED                        
         BNZ   *+8                 SKIP LIENS                                   
         BAS   RE,LIENS            APPLY LIENS TO CHECK                         
*                                                                               
         L     RF,SRTNET           ADD REIMBURSED EXPENSES TO CHECK             
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BE    *+12                                                             
         A     RF,SRTREXP                                                       
         B     CA12                                                             
*                                                                               
         CLI   SRTW4TY,TAW4TYCA    SRTNET HAS SRTREXP FOR P+ ALREADY            
         BNE   CA12                                                             
         LARL  RE,SVTXRE           REXP INCLUDES TAX REIM FOR CANADIANS         
         MVC   FULL,0(RE)                                                       
         S     RF,FULL             SO WE MUST SUBTRACT THAT OFF                 
CA12     ST    RF,SRTNET                                                        
         LTR   RF,RF                                                            
         BNM   *+10                                                             
         XC    SRTNET,SRTNET       CAN'T BE NEGATIVE                            
*                                                                               
         USING TAPDD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   CA14                                                             
         CLI   TAPDLEN,TAPDLNQ                                                  
         BL    CA14                                                             
         L     RE,SRTNET                                                        
         ICM   RF,15,TAPDTXNW                                                   
         SR    RE,RF                                                            
         CLI   SRTW4TY,TAW4TYCA    NOT FOR CANADIAN                             
         BE    *+8                                                              
         A     RE,SRTDUETR         ADD BACK DUE COMP TAKEN OUT                  
         ST    RE,SRTNET                                                        
         L     RE,SRTGRS                                                        
         ICM   RF,15,TAPDTXNW                                                   
         SR    RE,RF                                                            
         CLI   SRTW4TY,TAW4TYCA    NOT FOR CANADIAN                             
         BE    *+8                                                              
         A     RE,SRTDUETR         ADD BACK DUE COMP TAKEN OUT                  
         ST    RE,SRTGRS                                                        
         DROP  R4                                                               
*                                                                               
CA14     BRAS  RE,DIRECTD          CHECK DIRECT DEPOSIT OF CHECK                
         BRAS  RE,WIRED            CHECK WIRE TRANSFER CHECK                    
*                                                                               
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         MVC   TACDNET,SRTNET      SAVE NET CHECK AMOUNT IN ELEMENT             
         MVC   TACDFREQ,SRTFREQ         FREQUENCY                               
*                                                                               
         CLI   RECNUM,CN           IF CHECK IS NOT FOR CAN$                     
         BE    CA20                                                             
         CLI   RECNUM,EC           AND NOT FOR EUROS                            
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYCO    AND NOT FOR CORPORATION                      
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYCA    AND NOT FOR CANADIAN                         
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYTR    AND NOT FOR TRUSTEE                          
         BE    CA20                                                             
         CLI   SRTW4TY,TAW4TYFO    AND NOT FOR FOREIGNER                        
         BE    CA20                                                             
         MVC   TACDEARN,SRTGRS     SAVE TAXABLE EARNINGS IN ELEMENT             
         MVC   TACDNTAX,SRTREXP         REIMBURSED EXPENSES                     
*                                                                               
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   CAX                                                              
         ICM   RF,15,TACDNTAX      REIMBURSEMENTS                               
*        L     RE,SRTTXRE                                                       
*        SR    RF,RE               - TAXABLE REIMBURSEMENTS                     
         LARL  RE,SVTXRE                                                        
         MVC   FULL,0(RE)                                                       
         S     RF,FULL             -TAXABLE REIMBURSEMENTS                      
         STCM  RF,15,TACDNTAX                                                   
         B     CAX                                                              
*                                                                               
CA20     L     RF,SRTGRS           ELSE NON-TAXABLE EARNINGS = GROSS            
         A     RF,SRTREXP              EARNINGS + REIMBURSED EXPENSES           
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BNE   CA22                                                             
         CLI   SRTW4TY,TAW4TYCA    AND NOT FOR CANADIAN                         
         BNE   CA22                                                             
         LARL  RE,SVTXRE           REXP INCLUDES TAX REIM FOR CANADIANS         
         MVC   FULL,0(RE)                                                       
         S     RF,FULL             -TAXABLE REIMBURSEMENTS                      
CA22     ST    RF,TACDNTAX         SAVE IN ELEMENT                              
*                                                                               
CAX      MVC   SRTEARN,TACDEARN    SAVE TAXABLE EARNINGS IN SORT RECORD         
         MVC   SRTNTAX,TACDNTAX         NON-TAXABLE EARNINGS                    
         B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE CALLS THE ROUTINE TAPPGUAR TO TAKE GUARANTEE CREDITS             
* FROM THIS CHECK IF THE PERFORMER IS ON A GUARANTEE.  ADDITIONALLY,            
* IT TAKES THE INVOICE COMMENT ELEMENT ON THE INVOICE RECORD AND ADDS           
* IT TO THE CHECK RECORD FOR USE BY TAPPGUAR.                                   
*                                                                               
TAKEGUAR NTR1                                                                   
         TM    SRTINVS,TAINSBIL    IF INVOICE BILLED THEN RETURN                
         BO    TGX                                                              
         TM    SRTINVS2,TAINSHLP                                                
         BO    TGX                                                              
         MVI   BYTE,TACMTYPG       SEARCH INVOICE RECORD FOR INVOICE            
         MVC   AIO,AINVIO              COMMENT ELEMENT                          
         MVI   ELCODE,TACMELQ                                                   
         GOTO1 GETL,DMCB,(1,BYTE)                                               
         MVC   AIO,AIO1                                                         
         BNE   TG10                                                             
*                                                                               
         L     RF,TGELEM           MOVE ELEMENT TO ELEM                         
         MVC   ELEM,0(RF)                                                       
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                  CALL TAPPGUAR TO TAKE GUAR CREDITS           
TG10     GOTO1 CALLTAPP,DMCB,(RC),(X'A0',ASRTCHK),(0,AINVIO),SYSCOMM            
         BE    TG20                                                             
         MVC   INVTERR,0(R1)       IF ERROR THEN SAVE INVOICE ERROR             
         TM    INVTERR,X'80'       TEST THIS IS ACTUALLY A WARNING              
         BZ    ERRABORT            NO - ABORT INVOICE                           
         BAS   RE,ADDERI           YES - ADD TO ERROR TABLE                     
         MVI   INVTERR,0           CLEAR ERROR NUMBER                           
         CLI   0(R1),TAINWGDU      IF THIS PARTICULAR WARNING                   
         BE    TG15                                                             
         CLI   0(R1),TAINWGCN                                                   
         BNE   TG16                                                             
TG15     MVI   0(R1),X'FE'         SIMULATE UPDATE RETURN CODE                  
         B     TG20                                                             
TG16     MVI   0(R1),X'FF'         ELSE SIMULATE NO CHANGES MADE                
*                                                                               
TG20     CLI   0(R1),X'FF'         IF NO CHANGES MADE THEN RETURN               
         BE    TGX                                                              
         L     R4,ASRTCHK          ELSE R4 = A(PAYMENT DETAILS ELEM)            
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         USING TAPDD,R4                                                         
*                                                                               
         MVC   SRTGUAR,TAPDGUAR    SAVE GUARANTEE CREDITS IN SORT REC           
         MVC   SRTACDE,TAPDACDE         APPLIED CODE                            
         MVC   SRTPNH,TAPDPNH           PENSION & HEALTH AMOUNT                 
*                                                                               
         L     RE,TAPDPAYI         PAYMENT AMOUNT WILL COME FROM EITHER         
         A     RE,TAPDPAYC             TAPDPAYI OR TAPDPAYC                     
         ST    RE,SRTPAY                                                        
*                                                                               
         ST    RE,SRTNET           SAVE PAYMENT AMOUNT IN CHECK ACCUM           
TGX      B     XIT                                                              
         EJECT                                                                  
* THIS ROUTINE COMPUTES THE DUE COMPANY AMOUNT FOR THE CHECK.  IT MAKES         
* CALLS TO GETDUE FOR EACH DUE COMPANY KEY ON FILE.  GETDUE WILL                
* EITHER FIND A TABLE ENTRY FOR THE KEY OR READ A RECORD AND BUILD A            
* NEW TABLE ENTRY.  CALCDUE WILL USE THE TABLE ENTRY TO CALCULATE               
* THE AMOUNT.                                                                   
*                                                                               
DUECOMP  NTR1                                                                   
         TM    SRTBITS,SRTBNDU     IF THIS PERF TAKES NO DUE COMP               
         BO    DCX                 THEN DON'T BOTHER                            
*                                                                               
         MVI   DUESTAT,0           CLEAR STATUS                                 
*                                                                               
         LA    R3,KEY              BUILD KEY FOR DUE COMPANY                    
         USING TLDUPD,R3                                                        
         XC    TLDUPKEY,TLDUPKEY                                                
         MVI   TLDUPCD,TLDUPCDQ                                                 
         MVC   TLDUPSSN,SRTSSN                                                  
*                                                                               
         GOTO1 HIGH                READ FOR KEY                                 
*                                                                               
DC10     CLC   TLDUPKEY(TLDUPTYP-TLDUPD),KEYSAVE  WHILE SAME EMPLOYEE           
         BNE   DCX                                                              
*                                                                               
         MVC   SVDUEKEY,TLDUPKEY    SAVE DUE COMPANY KEY                        
         L     RE,SRTNET                                                        
         TM    TGSYSTA2,TASYSNDC   NEW DUE COMPANY?                             
         BZ    DC20                                                             
*                                                                               
         LARL  R1,SVTXRE           MAKE SURE TO INCLUDE                         
         ICM   RF,15,0(R1)         NON TAX REIMBURSEMENTS AS WELL               
         L     R1,SRTREXP          TOTAL REIM - TAX REIM = NON TAX REIM         
         SR    R1,RF                                                            
         AR    RE,R1                                                            
*                                                                               
DC20     L     R1,SRTMDED                                                       
         A     R1,SRTDUES                                                       
         CR    RE,R1               AND CHECK AMOUNT > MISC DED + DUES           
         BNH   DCX                                                              
*                                                                               
         GOTO1 GETDUE,DMCB,TLDUPDUC GET DUE COMPANY TABLE ENTRY                 
         BNE   DC90                SKIP IF CAN'T FIND OR BUILD                  
         L     R5,0(R1)            R5 = A(DUE COMPANY TABLE ENTRY)              
         USING DUETABD,R5          RETURNED FROM GETDUE CALL                    
*                                                                               
         GOTO1 =A(CALCDUE),DMCB,(R5) CALCULATE DUE COMPANY AMOUNT               
         TM    DUESTAT,DUESERR     IF ERROR RETURNED                            
         BZ    *+16                                                             
         BAS   RE,WAREDUE2         SET WARNING IN SORT RECORD                   
         NI    DUESTAT,X'FF'-DUESERR  CAN'T FIND FLIST                          
         B     DC90                                                             
*                                                                               
         OC    DUECOL,DUECOL       IF NONE FROM THIS RECORD THEN TRY            
         BZ    DC90                    NEXT RECORD                              
***********************************************************************         
* APPLY DUE COMPANY COLLECTED THIS TIME IN THE FOLLOWING WAYS:        *         
*     1) ADD TO TOTAL COLLECTED AMOUNT IN TABLE ENTRY                 *         
*     2) COMPUTE REMAINING BALANCE                                    *         
*     3) SUBTRACT FROM CHECK AMOUNT IN SORT RECORD                    *         
*     4) ADD TO TOTAL DUE COMPANY COLLECTED IN SORT RECORD            *         
*     5) ADD DUE COMPANY WITHHOLDING ELEMENT TO CHECK RECORD          *         
*        AND PUT THE AMOUNT THERE                                     *         
***********************************************************************         
*                                                                               
         LA    R4,DUEELEM          R4 = A(DUE COMPANY DETAILS ELEMENT)          
         USING TADUD,R4                                                         
*                                                                               
         ICM   RF,15,TADUCOL       ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,DUECOL               IN THE DUE COMPANY DETAILS               
         STCM  RF,15,TADUCOL           ELEMENT                                  
*                                                                               
         ICM   RF,15,DUEINV        ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,DUECOL               FOR THIS INVOICE                         
         STCM  RF,15,DUEINV                                                     
*                                                                               
         ICM   RF,15,TADUDUE       BALANCE REMAINING = AMOUNT DUE -             
         ICM   RE,15,TADUCOL           COLLECTED AMOUNT                         
         SR    RF,RE                                                            
*                                                                               
         ST    RF,DUEBAL           SAVE BALANCE REMAINING IN DUEBAL             
*                                                                               
         TM    DUEFLAG,DUEFTR      TAXABLE REIMBURSEMENTS?                      
         BO    DC30                                                             
         TM    DUEFLAG,DUEFNR      NON TAXABLE REIMBURSEMENTS?                  
         BO    DC40                                                             
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,DUECOL               AMOUNT IN SORT RECORD                    
         ST    RF,SRTNET                                                        
         L     RF,SRTDUE           ADD AMOUNT TO THE DUE COMPANY                
         A     RF,DUECOL               AMOUNT IN SORT RECORD                    
         ST    RF,SRTDUE                                                        
         B     DC50                                                             
*                                                                               
DC30     L     RF,SRTDUETR         ADD AMOUNT TO THE DUE COMPANY                
         A     RF,DUECOL           TAXABLE REIMBURSEMENTS                       
         ST    RF,SRTDUETR                                                      
         L     RF,SRTREXP                                                       
         S     RF,DUECOL                                                        
         ST    RF,SRTREXP                                                       
         LARL  R1,SVTXRE                                                        
         ICM   RF,15,0(R1)                                                      
         S     RF,DUECOL                                                        
         STCM  RF,15,0(R1)                                                      
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,DUECOL               AMOUNT IN SORT RECORD                    
         ST    RF,SRTNET                                                        
         B     DC50                                                             
*                                                                               
DC40     L     RF,SRTDUENR         ADD AMOUNT TO THE DUE COMPANY                
         A     RF,DUECOL           NON TAX REIMBURSEMENTS                       
         ST    RF,SRTDUENR                                                      
         L     RF,SRTREXP                                                       
         S     RF,DUECOL                                                        
         ST    RF,SRTREXP                                                       
*                                                                               
DC50     LA    R4,ELEM             BUILD DUECOMP WITHHOLDING ELEMENT            
         USING TADWD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TADWEL,TADWELQ                                                   
         MVI   TADWLEN,TADWLNQ                                                  
         MVC   TADWDUC,TLDUPDUC                                                 
*                                                                               
         MVC   TADWREC,DUECOL      SAVE COLLECTED AMOUNT IN ELEMENT             
         MVC   TADWBAL,DUEBAL      SAVE REMAINING BALANCE IN ELEMENT            
*                                                                               
         TM    DUEFLAG,DUEFTR      TAXABLE REIMBURSEMENTS?                      
         BZ    *+8                                                              
         OI    TADWSTAT,TADWSTR                                                 
         TM    DUEFLAG,DUEFNR      NON TAXABLE REIMBURSEMENTS?                  
         BZ    *+8                                                              
         OI    TADWSTAT,TADWSNR                                                 
*                                                                               
         MVC   AIO,ASRTCHK         ADD DW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
DC90     TM    TGSYSTA2,TASYSNDC   NEW DUE COMPANY?                             
         BO    *+12                                                             
         TM    DUESTAT,DUESPCT     IF A PERCENTAGE WAS TAKEN                    
         BO    DCX                 THEN DONE                                    
         GOTO1 SEQ                 GET NEXT DUECOMP KEY AND LOOP BACK           
         B     DC10                                                             
*                                                                               
DCX      B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE DUE COMPANY TABLE FOR AN ENTRY THAT HAS THE         
* DUE COMPANY CODE POINTED TO BY PARAMETER 1.  IF IT DOESN'T FIND               
* ONE IT READS THE DUE COMPANY RECORD AND BUILDS A NEW ONE FROM THE             
* DETAILS ELEMENT.  IT RETURNS THE ADDRESS OF THE TABLE ENTRY IN                
* PARAMETER 1.  IF THE DUE COMPANY RECORD FOR SOME REASON HAS NO DUE            
* COMPANY DETAILS ELEMENT THEN THE ROUTINE RETURNS 'NO', OTHERWISE IT           
* RETURNS 'YES'.                                                                
*                                                                               
GETDUE   NTR1                                                                   
         L     R3,0(R1)            R3 = A(DUE COMPANY CODE)                     
*                                                                               
         LA    R5,BLOCK            R5 = A(DUETAB LOOKUP ENTRY)                  
         USING DUETABD,R5                                                       
*                                  BUILD LOOKUP ENTRY                           
         XC    BLOCK(DUELEN),BLOCK                                              
         MVC   DUESSN,SRTSSN                                                    
         MVC   DUEDUC,0(R3)                                                     
         MVC   DUEEMP,SRTEMP                                                    
*                                  LOOKUP ENTRY IN TABLE                        
         L     R2,ADUETAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),X'01'         IF ENTRY IS FOUND THEN USE IT                
         BNE   YES                                                              
         GOTO1 GETREC              ELSE READ RECORD AND BUILD NEW ENTRY         
*                                                                               
         L     R4,AIO              R4 = A(DUE COMPANY DETAILS ELEMENT)          
         MVI   ELCODE,TADUELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GD100                                                            
         USING TADUD,R4                                                         
*                                                                               
         TM    TADUSTAT,TADUSHLD   IGNORE RECORDS STILL ON HOLD                 
         BO    NO                                                               
         TM    TADUSTAT,TADUSLCK   IGNORE RECORDS LOCKED                        
         BO    NO                                                               
         CLC   TADUEMP,SRTEMP      MUST BE SAME EMPLOYER                        
         BNE   NO                                                               
         L     RF,TADUDUE          AMOUNT TO COLLECT = AMOUNT DUE -             
         S     RF,TADUCOL              AMOUNT ALREADY COLLECTED                 
         BNP   NO                  SKIP IF NOTHING LEFT TO TAKE                 
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'D',AIO),0,=C'DUE COMP'                           
*                                                                               
         LA    R5,BLOCK            BUILD NEW ENTRY FROM DETAILS ELEMENT         
         MVC   DUEELEM,TADUEL                                                   
*                                  ADD ENTRY TO TABLE                           
         GOTO1 ADDBIN,DMCB,ADUETAB,(R5)                                         
         B     YES                 AND RETURN SUCCESS                           
*                                                                               
GD100    BAS   RE,WAREDUE          SET WARNING IN SORT RECORD                   
         B     NO                  AND RETURN FAILURE                           
         EJECT                                                                  
* THIS ROUTINE COMPUTES THE LIEN AMOUNT FOR THE CHECK.  IT MAKES CALLS          
* TO GETLIEN AND CALCLIEN FOR EACH LIEN KEY UNTIL A USABLE LIEN AMOUNT          
* IS FOUND.                                                                     
*                                                                               
LIENS    NTR1                                                                   
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
         OC    SRTNET,SRTNET       IF CHECK AMOUNT ZERO THEN DONE               
         BZ    LNX                                                              
*                                                                               
         LA    R3,KEY              BUILD KEY FOR LIEN                           
         USING TLLNPD,R3                                                        
         XC    TLLNPKEY,TLLNPKEY                                                
         MVI   TLLNPCD,TLLNRCDQ                                                 
         MVC   TLLNRSSN,SRTSSN                                                  
*                                                                               
         GOTO1 HIGH                READ FOR KEY                                 
*                                                                               
*                                  WHILE SAME EMPLOYEE                          
LN10     CLC   TLLNPKEY(TLLNRRNK-TLLNPD),KEYSAVE                                
         BNE   LNX                                                              
*                                  GET LIEN TABLE ENTRY                         
         GOTO1 GETLIEN,DMCB,TLLNRLIN    ADDRESS RETURNED IN PARM 1              
*                                                                               
         BNE   LN40                SKIP IF CAN'T FIND OR BUILD                  
*                                                                               
         GOTO1 CALCLIEN,DMCB       CALCULATE LIEN AMOUNT PASSING PARM 1         
*                                      RETURNED FROM GETLIEN CALL               
*                                                                               
         OC    LIENCOL,LIENCOL     IF LIEN AMOUNT FOUND FOR THIS RECORD         
         BNZ   LN50                    THEN APPLY                               
*                                                                               
LN40     GOTO1 SEQ                 ELSE GET NEXT LIEN KEY AND LOOP BACK         
         B     LN10                                                             
***********************************************************************         
* APPLY LIEN COLLECTED THIS TIME IN THE FOLLOWING WAYS:               *         
*     1) ADD TO TOTAL COLLECTED AMOUNT IN TABLE ENTRY                 *         
*     2) COMPUTE REMAINING BALANCE                                    *         
*     3) SAVE SSN FOR LIEN PAYEE CHECK                                *         
*     4) SUBTRACT FROM CHECK AMOUNT IN SORT RECORD                    *         
*     5) ADD LIEN WITHHOLDING ELEMENT TO CHECK RECORD AND PUT         *         
*        AMOUNT THERE                                                 *         
***********************************************************************         
LN50     DS    0H                                                               
         L     R5,0(R1)            R5 = A(LIEN TABLE ENTRY)                     
         USING LINTABD,R5                                                       
*                                                                               
         LA    R4,LINELEM          R4 = A(LIEN DETAILS ELEMENT)                 
         USING TALND,R4                                                         
*                                                                               
         ICM   RF,15,TALNCOL       ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,LIENCOL              IN THE LIEN DETAILS ELEMENT              
         STCM  RF,15,TALNCOL                                                    
*                                                                               
         ICM   RF,15,LININV        ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,LIENCOL              FOR THIS INVOICE                         
         STCM  RF,15,LININV                                                     
*                                                                               
         MVC   LIENSSN,TALNPAYE    SAVE SSN FOR LIEN PAYEE'S CHECK              
*                                                                               
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,LIENCOL              AMOUNT IN SORT RECORD                    
         ST    RF,SRTNET                                                        
*                                                                               
         MVC   SRTLIEN,LIENCOL     SAVE AMOUNT IN SORT RECORD                   
*                                                                               
         LA    R4,ELEM             BUILD LIEN WITHHOLDING ELEMENT               
         USING TALWD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TALWEL,TALWELQ                                                   
         MVI   TALWLEN,TALWLNQ                                                  
         MVC   TALWLIN,TLLNRLIN                                                 
*                                                                               
         MVC   TALWREC,LIENCOL     SAVE COLLECTED AMOUNT IN ELEMENT             
         MVC   TALWBAL,LIENBAL     SAVE REMAINING BALANCE IN ELEMENT            
*                                                                               
         MVC   AIO,ASRTCHK         ADD LW ELEMENT TO CHECK RECORD               
         GOTO1 ADDELEM                                                          
*                                                                               
         CLC   LIENCASE,MYSPACES   BUILD LIEN CASE/ACCOUNT                      
         BNH   LN60                                                             
         LA    R4,ELEM                                                          
         USING TACMD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACMEL,TACMELQ                                                   
         LA    RF,TACMLNQ+L'LIENCASE                                            
         MVI   TACMTYPE,TACMTYPL                                                
         MVC   TACMCOMM,LIENCASE                                                
         LA    R1,L'LIENCASE-1                                                  
         LA    RE,LIENCASE+L'LIENCASE-1   LAST CHAR OF LIENCASE                 
LN55     CLI   0(RE),C' '          FIND LAST NON-SPACE CHAR                     
         BH    LN58                                                             
         AHI   RE,-1                                                            
         BCT   R1,LN55                                                          
LN58     AHI   R1,TACMLNQ+1                                                     
         STC   R1,TACMLEN                                                       
         GOTO1 ADDELEM                                                          
*                                                                               
LN60     CLC   LIENTYPE,MYSPACES   BUILD LIEN TYPE                              
         BNH   LN90                                                             
         LA    R4,ELEM                                                          
         USING TACMD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACMEL,TACMELQ                                                   
         LA    RF,TACMLNQ+L'LIENTYPE                                            
         STC   RF,TACMLEN                                                       
         MVI   TACMTYPE,TACMTYPT   LIEN TYPE                                    
         MVC   TACMCOMM,LIENTYPE                                                
         GOTO1 ADDELEM                                                          
*                                                                               
LN90     MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
         LA    R4,ELEM             BUILD ELEMENT LIEN PAID TO                   
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTLIN   SET TYPE                                     
         MVC   TANUMBER(9),LIENSSN SET S/S NUMBER FOR WHO LIEN PAID TO          
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
*                                                                               
LNX      B     XIT                                                              
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
* THIS ROUTINE LOOKS IN THE LIEN TABLE FOR AN ENTRY WITH THE LIEN               
* CODE POINTED TO BY PARAMETER 1.  IF IT DOESN'T FIND ONE THEN                  
* IT READS THE LIEN RECORD AND BUILDS A NEW ONE FROM THE DETAILS                
* ELEMENT.  IT RETURNS THE ADDRESS OF THE TABLE ENTRY IN PARAMETER 1.           
* IF THE LIEN RECORD FOR SOME REASON HAS NO LIEN DETAILS ELEMENT THEN           
* THE ROUTINE RETURNS 'NO', OTHERWISE IT RETURNS 'YES'.                         
*                                                                               
GETLIEN  NTR1                                                                   
         NI    SRTBITS2,X'FF'-SRTBEFTL                                          
         MVC   LIENTYPE,MYSPACES                                                
         L     R3,0(R1)            R3 = A(LIEN CODE)                            
*                                                                               
         LA    R5,BLOCK            R5 = A(LINTAB LOOKUP ENTRY)                  
         USING LINTABD,R5                                                       
*                                  BUILD LOOKUP ENTRY                           
         XC    BLOCK(LINLEN),BLOCK                                              
         MVC   LINSSN,SRTSSN                                                    
         MVC   LINLIN,0(R3)                                                     
*                                  LOOKUP ENTRY IN TABLE                        
         L     R2,ALINTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),X'01'         IF ENTRY IS FOUND THEN USE IT                
         BNE   YES                                                              
*                                                                               
         GOTO1 GETREC              ELSE READ RECORD AND BUILD NEW ENTRY         
*                                                                               
         L     R4,AIO              R4 = A(LIEN DETAILS ELEMENT)                 
         MVI   ELCODE,TALNELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   GL100                                                            
         USING TALND,R4                                                         
         TM    TALNSTAT,TALNSEFT                                                
         BZ    *+8                                                              
         OI    SRTBITS2,SRTBEFTL   SET LIEN IS EFT                              
         MVC   LIENTYPE,TALNTYPE                                                
*                                                                               
         GOTO1 NOCALC              IF WE DON'T WANT TO CALCULATE THIS           
         BE    GL50                    CHECK THEN SKIP BALANCE TEST             
*                                                                               
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS GAR. FOR TRUSTEE                
         BE    GL50                    SKIP BALANCE TEST                        
         ICM   RF,15,TALNDUE       BALANCE REMAINING = AMOUNT DUE -             
         ICM   RE,15,TALNCOL           COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         BNP   NO                  SKIP IF NOTHING LEFT TO TAKE                 
*                                                                               
GL50     GOTO1 DOTRACE,DMCB,(C'L',AIO),0,=C'LIEN'                               
*                                                                               
         LA    R5,BLOCK            BUILD NEW ENTRY FROM DETAILS ELEMENT         
         MVC   LINELEM,TALNEL                                                   
*                                  ADD ENTRY TO TABLE                           
         MVC   LIENCASE,MYSPACES                                                
         MVI   ELCODE,TACMELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TACMTYPL))                                     
         BNE   GL60                RETURN SUCCESS                               
         USING TACMD,R4                                                         
         L     R4,TGELEM                                                        
         SR    RF,RF                                                            
         IC    RF,TACMLEN                                                       
         AHI   RF,-4                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   LIENCASE(0),TACMCOMM                                             
*                                                                               
GL60     GOTO1 ADDBIN,DMCB,ALINTAB,(R5)                                         
         B     YES                 AND RETURN SUCCESS                           
*                                                                               
GL100    BAS   RE,WARELNE          SET WARNING IN SORT RECORD                   
         B     NO                  AND RETURN FAILURE                           
         DROP  R2,R5                                                            
         EJECT                                                                  
* THIS ROUTINE USES THE TABLE ENTRY, POINTED TO BY PARAMETER ONE, TO            
* CALCULATE THE LIEN AMOUNT TO COLLECT.  THE COLLECTED AMOUNT IS                
* STORED IN LIENCOL.  IF THE TABLE ENTRY IS FILTERED FOR SOME REASON,           
* LIENCOL WILL BE ZERO.                                                         
*                                                                               
CALCLIEN NTR1                                                                   
         XC    LIENCOL,LIENCOL     INITIALIZE COLLECTED AMOUNT TO ZERO          
*                                                                               
         L     R5,0(R1)            R5 = A(LIEN TABLE ENTRY)                     
         USING LINTABD,R5                                                       
*                                                                               
         LA    R4,LINELEM          R4 = A(LIEN DETAILS ELEMENT)                 
         USING TALND,R4                                                         
*                                                                               
         CLI   RECNUM,CN           ONLY PROCESS CAN$ LIENS ON CNCHECKS          
         BNE   *+16                 AND OTHERS ON US$ RUNS                      
         TM    TALNSTAT,TALNSCAN                                                
         BZ    CLX                                                              
         B     *+12                                                             
         TM    TALNSTAT,TALNSCAN                                                
         BO    CLX                                                              
*                                                                               
         CLI   RECNUM,EC           ONLY PROCESS EURO LIENS ON EUCHECKS          
         BNE   *+16                 AND OTHERS ON US$ RUNS                      
         TM    TALNSTAT,TALNSEUR                                                
         BZ    CLX                                                              
         B     *+12                                                             
         TM    TALNSTAT,TALNSEUR                                                
         BO    CLX                                                              
*                                                                               
         OC    TALNEMP,TALNEMP     SKIP RECS WHERE EMPLOYER DON'T MATCH         
         BZ    *+18                                                             
         CLC   TALNEMP,SRTEMP                                                   
         BNE   CLX                                                              
         B     *+14                                                             
         CLC   TGTPEMP,SRTEMP                                                   
         BNE   CLX                                                              
*                                                                               
         CLC   SRTCDTE,TALNEXP     SKIP RECORDS WHERE THE EXPIRATION            
         BNL   CLX                     DATE IS NOT AFTER TODAY                  
*                                                                               
         CLC   SRTNET,TALNXMPT     SKIP RECORDS WHERE THE CHECK AMOUNT          
         BNH   CLX                     IS NOT > THE EXEMPT AMOUNT               
*                                                                               
         OC    SRTTRST,SRTTRST     SKIP RECORDS WHERE THERE WAS ALREADY         
         BNZ   CLX                     AN AMT W/HELD FOR W4 TRUSTEE             
*                                                                               
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS GAR. FOR TRUSTEE                
         BE    CL10                AMOUNT COLLECTING UNLIMITED                  
         ICM   RF,15,TALNDUE       BALANCE REMAINING = AMOUNT DUE -             
         ICM   RE,15,TALNCOL           COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         LTR   RF,RF               SKIP RECS WITH NO REMAINING BALANCE          
         BZ    CLX                                                              
         ST    RF,LIENBAL          SAVE BALANCE REMAINING IN LIENBAL            
*                                                                               
CL10     L     RF,SRTNET           GREATEST POSSIBLE AMOUNT TO COLLECT          
         ICM   RE,15,TALNXMPT          = CHECK AMOUNT - EXEMPT AMOUNT           
         SR    RF,RE                                                            
         ST    RF,FULL                                                          
*                                                                               
         ICM   RF,15,TALNAMT       IF DEDUCTION AMOUNT NOT ZERO THEN            
         BNZ   CL30                    USE IT                                   
*                                                                               
         ICM   R0,3,TALNPCT        ELSE IF DEDUCTION PERCENTAGE NOT             
         BZ    CL20                    ZERO THEN USE IT                         
         L     RF,SRTNET                                                        
         CLC   TALNTYPE,=AL2(TALNTYGT) IF WAGE GARNISHMENT FROM GROSS           
         BE    *+14                                                             
         CLC   TALNTYPE,=AL2(TALNTYGG)                                          
         BNE   *+8                                                              
         L     RF,SRTGRS           THEN USE GROSS AMOUNT                        
         MR    RE,R0                                                            
         D     RE,=F'5000'         PERCENTAGE                                   
         LTR   RF,RF                                                            
         BM    *+8                 & ROUND IT                                   
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         B     CL30                                                             
*                                                                               
CL20     L     RF,FULL             ELSE USE GREATEST POSSIBLE AMOUNT            
*                                                                               
CL30     C     RF,FULL             IF AMOUNT TO COLLECT > GREATEST              
         BNH   *+8                                                              
         L     RF,FULL             THEN AMOUNT TO COLLECT = GREATEST            
*                                                                               
         CLM   RF,15,SRTNET        IF AMOUNT TO COLLECT > CHECK AMOUNT          
         BNH   *+8                                                              
         L     RF,SRTNET           THEN AMOUNT TO COLLECT = CHECK AMNT          
*                                                                               
         CLC   TALNTYPE,=AL2(TALNTYGT) IF GROSS GAR. FOR TRUSTEE                
         BNE   CL40                                                             
         ST    RF,LIENCOL          SAVE AMOUNT TO COLLECT IN LIENCOL            
         XC    LIENBAL,LIENBAL     AND DON'T KEEP TRACK OF BALANCE              
         B     CLX                 EXIT                                         
*                                                                               
CL40     CLM   RF,15,LIENBAL       IF AMOUNT TO COLLECT > REM BAL               
         BNH   *+8                                                              
         L     RF,LIENBAL          THEN AMOUNT TO COLLECT = REM BAL             
*                                                                               
         ST    RF,LIENCOL          SAVE AMOUNT TO COLLECT IN LIENCOL            
*                                                                               
         L     RF,LIENBAL          SUBTRACT COLLECTED AMOUNT FROM               
         S     RF,LIENCOL              REMAINING BALANCE                        
         ST    RF,LIENBAL                                                       
*                                                                               
CLX      B     XIT                                                              
         DROP  R4,R5                                                            
         EJECT                                                                  
* THIS ROUTINE ADDS ESUTAB ENTRIES FOR THE CHECK RECORD.                        
*                                                                               
CHKESU   NTR1                                                                   
         XC    TGFULL,TGFULL       CLEAR TEMP. AREA                             
*                                                                               
         MVI   ELCODE,TAODELQ      LOOK FOR FEDERAL OTHER DEDUCT ELEM           
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(1,=AL1(TAODTYPF))                                     
         MVC   AIO,AIO1                                                         
         BNE   CE20                SKIP IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(FEDERAL OTHER DEDUCTION ELEM)         
         USING TAODD,R4                                                         
         MVC   TGFULL,TAODAMT      SAVE FOREIGN FEDERAL TAXES                   
*                                                                               
CE20     TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BO    CE22                                                             
*                                                                               
         BRAS  RE,TSTPCAN          P+ CANADIAN TAXES?                           
         JNE   CE22                                                             
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BAS   RE,GETEL                                                         
         BE    CE100                                                            
*                                                                               
CE22     MVI   ELCODE,TACWELQ      LOOK FOR FEDERAL WITHHOLDING ELEM            
         MVC   AIO,ASRTCHK                                                      
         LA    RF,=C'FD '                                                       
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         LA    RF,=C'EU '                                                       
         GOTO1 GETL,DMCB,(3,(RF))                                               
         MVC   AIO,AIO1                                                         
         BE    CE26                SKIP IF FOUND                                
*                                                                               
         LA    R5,BLOCK            ELSE BUILD NEW ENTRY FOR FEDERAL             
         USING ESUTABD,R5                                                       
         XC    0(ESULEN,R5),0(R5)  (THIS MUST BE CORP.)                         
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,=C'FD '                                                  
         CLI   RECNUM,EC                                                        
         BNE   *+10                                                             
         MVC   ESUUNIT,=C'EU '                                                  
         MVC   ESUREXP,SRTREXP     ACCUMULATE REIMB. EXPENSES                   
         MVC   ESUEARN,SRTEARN                TAXABLE EARNINGS                  
         MVC   ESUTAX,TGFULL                  FOREIGN FEDERAL TAX               
         MVC   ESUMDD,SRTMDED                 MISC DEDUCTION                    
         MVC   ESUMTR,SRTMPT                  MPTRP                             
         MVC   ESUDTP,SRTDUE                  DUE TP                            
         MVC   ESUDTPTR,SRTDUETR              DUE TP TAX REIM                   
         MVC   ESUDTPNR,SRTDUENR              DUE TP NONTAX REIM                
         MVC   ESUPRC,SRTCHAR                 PERMANENT CHARITIES               
         MVC   ESULIE,SRTLIEN                 LIENS                             
         MVC   ESUUND,SRTDUES                 UNION DUES                        
         MVC   ESUTRS,SRTTRST                 TRUSTS                            
         MVC   ESUDRT,SRTDIRCT                DIRECT DEPOSITS                   
         MVC   ESUWRD,SRTWIRE                 WIRED                             
         MVC   ESUPPA,SRTPPLA                 PAYROLL+ COMMISSION               
         MVC   ESUFERN,SRTEARN                FED EARN AT PROV LEVEL            
*                                                                               
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5),1  ADD ESUTAB ENTRY                     
         GOTO1 ADDBIN,DMCB,AESUINV,(R5),1  ADD ESUINV ENTRY                     
*                                                                               
CE26     L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACWELQ      GET CHECK WITHHOLDING ELS.                   
         BAS   RE,GETEL                                                         
         BNE   CE95                                                             
         USING TACWD,R4            R4 = A(CHECK WITHHOLDING ELEMENT)            
*                                                                               
CE30     LA    R5,BLOCK            BUILD NEW ENTRY FOR UNIT                     
         XC    BLOCK(ESULEN),BLOCK                                              
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,TACWUNIT                                                 
         MVC   ESUFERN,SRTEARN                FED EARNINGS AT PROV LVL          
*                                                                               
         TM    TACWSTAT,TACWSTAX   IF THIS IS TAXABLE UNIT                      
         BZ    *+10                                                             
         MVC   ESUEARN,SRTEARN     ACCUMULATE EARNINGS                          
*                                                                               
         MVC   ESUTAX,TACWTAX      ACCUMULATE TAXES                             
*                                                                               
         CLC   TACWUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         BNE   CE60                                                             
         ICM   RF,15,ESUTAX                                                     
         A     RF,TGFULL           ADD FOREIGN FEDERAL TAX                      
         STCM  RF,15,ESUTAX                                                     
         MVC   ESUREXP,SRTREXP     ACCUMULATE REIMB. EXPENSES                   
         MVC   ESUFICA,TACWFICA               FICA                              
         MVC   ESUMDD,SRTMDED                 MISC DEDUCTION                    
         MVC   ESUMTR,SRTMPT                  MPTRP                             
         MVC   ESUDTP,SRTDUE                  DUE TP                            
         MVC   ESUDTPTR,SRTDUETR              DUE TP TAX REIM                   
         MVC   ESUDTPNR,SRTDUENR              DUE TP NONTAX REIM                
         MVC   ESUPRC,SRTCHAR                 PERMANENT CHARITIES               
         MVC   ESULIE,SRTLIEN                 LIENS                             
         MVC   ESUUND,SRTDUES                 UNION DUES                        
         MVC   ESUTRS,SRTTRST                 TRUSTS                            
         MVC   ESUDRT,SRTDIRCT                DIRECT DEPOSITS                   
         MVC   ESUWRD,SRTWIRE                 WIRED                             
         MVC   ESUWRD,SRTWIRE                 WIRED                             
         MVC   ESUPPA,SRTPPLA                 PAYROLL+ COMMISSION               
         B     CE80                                                             
*                                                                               
CE60     CLC   TACWUNIT,=C'CN '    IF THIS IS CANADA                            
         BNE   CE70                                                             
         MVC   ESUGST,TACWGST      SAVE GOODS AND SERVICES TAX                  
         MVC   ESUPST,SRTPST                                                    
         B     CE80                                                             
*                                                                               
CE70     CLI   TACWUNIT+2,C' '     IF THIS IS A STATE                           
         BH    CE80                                                             
         MVC   ESUSDI,TACWSDI      SAVE DISABILITY                              
         MVC   ESUSUI,TACWSUI           UNEMPLOYMENT                            
         CLI   TACWLEN,TACWLN2Q                                                 
         BL    *+10                                                             
         MVC   ESUSFLI,TACWSFLI         FAMILY LEAVE                            
*                                                                               
CE80     CLI   RECNUM,PK           PAYROLL PLUS?                                
         BE    *+14                                                             
         CLC   =C'P+',TGEMP        TGEMP FILLED IF SSN TRANSFER                 
         BNE   CE90                                                             
         MVI   ELCODE,TATUELQ      GET TAX UNIT                                 
         MVC   AIO,ASRTCHK                                                      
         LA    RF,TACWUNIT                                                      
         TM    SRTADJS,TAPDADIS+TAPDADTT  ISSUE/TAXTRANS NO TATU'S              
         BNZ   *+16                                                             
         XC    ESUEARN,ESUEARN                                                  
         XC    ESUFERN,ESUFERN                                                  
         GOTO1 GETL,DMCB,(3,(RF))                                               
         BNE   CE90                                                             
*                                                                               
         L     RF,TGELEM                                                        
         USING TATUD,RF                                                         
         MVC   ESUEARN,TATUWAAD    SET TAXABLE AMOUNT                           
         MVC   ESUTXRE,TATUTNAD    SET TAXABLE REIMBURSEMENTS                   
         MVC   ESUNTRE,TATUNNAD    SET NON-TAXABLE REIMBURSEMENTS               
         MVC   ESUFERN,ESUEARN                                                  
*                                                                               
         CLC   TACWUNIT,=C'FD '                                                 
         BNE   CE90                                                             
         ICM   R1,15,TATUTNAD      TAXABLE REIMBURSEMENTS                       
         ICM   RE,15,TATUNNAD      NON-TAXABLE REIMBURSEMENTS                   
         AR    R1,RE                                                            
         STCM  R1,15,ESUREXP       TOTAL REIMBURSEMENTS                         
*                                                                               
         TM    SRTBITS2,SRTADREC   NOT ALL TAKEN ADVANCE RECONC. TAKEN?         
         BZ    *+10                                                             
         MVC   ESUREXP,SRTREXP                                                  
         DROP  RF                                                               
*                                                                               
CE90     MVC   AIO,AIO1                                                         
*                                  UPDATE ESUTAB ENTRY FOR THIS UNIT            
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5),1                                       
*                                  UPDATE ESUINV ENTRY FOR THIS UNIT            
         GOTO1 ADDBIN,DMCB,AESUINV,(R5),1                                       
*                                                                               
         MVI   ELCODE,TACWELQ                                                   
         BAS   RE,NEXTEL                                                        
         BE    CE30                GO PROCESS CHECK WITHHOLDING EL.             
*                                                                               
CE95     TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    CEX                                                              
*                                                                               
CE100    L     R4,ASRTCHK          PROCESS CANADIAN P+ TAXES                    
         MVI   ELCODE,TAATELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   CEX                                                              
         USING TAATD,R4            R4 = A(CHECK WITHHOLDING ELEMENT)            
*                                                                               
CE130    LA    R5,BLOCK            BUILD NEW ENTRY FOR UNIT                     
         XC    BLOCK(ESULEN),BLOCK                                              
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,TAATUNIT                                                 
         MVC   ESUUTAX,TAATTAX      FD TAXES                                    
         MVC   ESUUPP,TAATPP       PENSION PLAN                                 
         MVC   ESUUEI,TAATEI       EMP INSURANCE PLAN                           
         MVC   ESUUPIP,TAATPIP     PARENTAL INSURANCE PLAN                      
*                                                                               
         CLI   TAATLEN,TAATLN2Q    OLD STYLE?                                   
         JL    CE140                                                            
         MVC   ESUCTAX,TAATCTAX    FD TAXES CAD                                 
         MVC   ESUCPP,TAATCPP      PENSION PLAN CAD                             
         MVC   ESUCEI,TAATCEI      EMP INSURANCE PLAN CAD                       
         MVC   ESUCPIP,TAATCPIP    PARENTAL INSURANCE PLAN CAD                  
*                                                                               
CE140    MVC   ESUMDD,SRTMDED                 MISC DEDUCTION                    
         MVC   ESUMTR,SRTMPT                  MPTRP                             
         MVC   ESUDTP,SRTDUE                  DUE TP                            
         MVC   ESUDTPTR,SRTDUETR              DUE TP TAX REIM                   
         MVC   ESUDTPNR,SRTDUENR              DUE TP NONTAX REIM                
         MVC   ESUPRC,SRTCHAR                 PERMANENT CHARITIES               
         MVC   ESULIE,SRTLIEN                 LIENS                             
         MVC   ESUUND,SRTDUES                 UNION DUES                        
         MVC   ESUTRS,SRTTRST                 TRUSTS                            
         MVC   ESUDRT,SRTDIRCT                DIRECT DEPOSITS                   
         MVC   ESUWRD,SRTWIRE                 WIRED                             
         MVC   ESUWRD,SRTWIRE                 WIRED                             
         MVC   ESUPPA,SRTPPLA                 PAYROLL+ COMMISSION               
         MVC   ESUPST,SRTPST                                                    
         TM    TGSYSTAT,TASYSFLT   NEW FLAT TAX RULES?                          
         BZ    *+14                                                             
         CLC   ESUUNIT,=C'CN '     ALREADY ADDED IT UP TOP                      
         BE    *+10                                                             
         MVC   ESUGST,SRTGST                                                    
*                                                                               
         MVI   ELCODE,TATUELQ      GET TAX UNIT                                 
         MVC   AIO,ASRTCHK                                                      
         TM    SRTADJS,TAPDADIS+TAPDADTT  ISSUE/TAXTRANS NO TATU'S              
         BNZ   *+16                                                             
         XC    ESUEARN,ESUEARN                                                  
         XC    ESUFERN,ESUFERN                                                  
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   CE190                                                            
*                                                                               
         USING TATUD,RF                                                         
         L     RF,TGELEM                                                        
         MVC   ESUEARN,TATUWAAD    SET TAXABLE AMOUNT                           
         MVC   ESUTXRE,TATUTNAD    SET TAXABLE REIMBURSEMENTS                   
         MVC   ESUFERN,ESUEARN                                                  
*                                                                               
         ICM   RE,15,TATUNNAD      TOTAL REIMB (CANADIANS ONLY)                 
         ICM   R1,15,TATUTNAD      TAXABLE REIMB                                
         SR    RE,R1                                                            
         STCM  RE,15,ESUNTRE       SET NON-TAXABLE REIMBURSEMENTS               
*                                                                               
         ICM   R1,15,TATUTNAD      TAXABLE REIMBURSEMENTS                       
         ICM   RE,15,TATUNNAD      NON-TAXABLE REIMBURSEMENTS                   
         AR    R1,RE                                                            
         STCM  R1,15,ESUREXP       TOTAL REIMBURSEMENTS                         
*                                                                               
         TM    SRTBITS2,SRTADREC   NOT ALL TAKEN ADVANCE RECONC. TAKEN?         
         BZ    *+10                                                             
         MVC   ESUREXP,SRTREXP                                                  
         DROP  RF                                                               
*                                                                               
         GOTOR CONVCAD,DMCB,ESUEARN,ESUCEARN  CONVERT TO CAD                    
         GOTOR CONVCAD,DMCB,ESUTXRE,ESUCTXRE  CONVERT TO CAD                    
         GOTOR CONVCAD,DMCB,ESUNTRE,ESUCNTRE  CONVERT TO CAD                    
*                                                                               
CE190    MVC   AIO,AIO1                                                         
*                                  UPDATE ESUTAB ENTRY FOR THIS UNIT            
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5),1                                       
*                                  UPDATE ESUINV ENTRY FOR THIS UNIT            
         GOTO1 ADDBIN,DMCB,AESUINV,(R5),1                                       
*                                                                               
         MVI   ELCODE,TAATELQ                                                   
         BAS   RE,NEXTEL                                                        
         BE    CE130               GO PROCESS CHECK WITHHOLDING EL.             
*                                                                               
CEX      B     XIT                                                              
         DS    0F                                                               
         EJECT                                                                  
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF AN ERROR OCCURS         
* THAT MAKES IT IMPOSSIBLE TO CONTINUE PROCESSING THE INVOICE.  THE             
* TYPE OF THE ERROR IS SAVED IN INVTERR AND THEN THE PROCESSING OF              
* THE INVOICE IS ABORTED BY LOADING RD WITH INVRD (WHICH WAS SET BY             
* PROCINVC) AND DOING AND XIT. CONTROL WILL BE RETURNED TO CHKPASS1             
* WHICH WILL CALL UPDINV TO WRITE BACK THE INVOICE RECORD WITH THE              
* ERROR STATUS INCLUDED.                                                        
*                                                                               
ERRECPI  MVI   INVTERR,TAINECPI    NO PAYMENT DETAILS ELEMENT/INVOICE           
         B     ERRABORT                                                         
*                                                                               
ERRECCO  MVI   INVTERR,TAINECCO    NO CMCL DETAILS ELEMENT/INVOICE              
         B     ERRABORT                                                         
*                                                                               
ERRECAG  MVI   INVTERR,TAINECAG    NO AGENCY RECORD                             
         B     ERRABORT                                                         
*                                                                               
ERREAYE  MVI   INVTERR,TAINEAYE    NO AGENCY DETAILS ELEMENT                    
         B     ERRABORT                                                         
*                                                                               
ERRECPC  MVI   INVTERR,TAINECPC    NO PAYMENT DETAILS ELEMENT/CHECK             
         B     ERRABORT                                                         
*                                                                               
ERRECW4  MVI   INVTERR,TAINECW4    NO W4 RECORD                                 
         B     ERRABORT                                                         
*                                                                               
ERREW4E  MVI   INVTERR,TAINEW4E    NO W4 DETAILS ELEMENT                        
         B     ERRABORT                                                         
*                                                                               
ERRECCA  MVI   INVTERR,TAINECCA    NO CAST ELEMENT                              
         B     ERRABORT                                                         
*                                                                               
ERRECRP  MVI   INVTERR,TAINECRP    NO W4 RECORD FOR INDIV CORPORATION           
         B     ERRABORT                                                         
*                                                                               
ERRETID  MVI   INVTERR,TAINETID    NO TAX ID ELEMENT IN EMP RECORD              
         B     ERRABORT                                                         
*                                                                               
ERRELN   MVI   INVTERR,TAINELN     NO LIEN RECORD                               
         B     ERRABORT                                                         
*                                                                               
ERRELNE  MVI   INVTERR,TAINELNE    NO LIEN DETAILS ELEMENT                      
         B     ERRABORT                                                         
*                                                                               
ERREDU   MVI   INVTERR,TAINEDU     NO DUE COMPANY RECORD                        
         B     ERRABORT                                                         
*                                                                               
ERREIOI  MVI   INVTERR,TAINEIOI    NO ORIGINAL AGY/INV ELEMENT                  
         B     ERRABORT                                                         
*                                                                               
ERREMAT  MVI   INVTERR,TAINEMAT    W4 TYPE DOES NOT MATCH PAYMENT               
         B     ERRABORT                                                         
*                                                                               
ERRETAX  MVI   INVTERR,TAINETAX    INVALID TAX UNIT IN CAST ELEMENT             
         B     ERRABORT                                                         
*                                                                               
ERRECNI  MVI   INVTERR,TAINECNI    CANADIAN SET UP AS INDIVIDUAL                
         B     ERRABORT                                                         
*                                                                               
ERREMCP  MVI   INVTERR,TAINECP     MISSING CANADIAN PROVINCE                    
         B     ERRABORT                                                         
*                                                                               
ERRABORT BAS   RE,ADDERI           ADD INVOICE ERROR TO ERITAB                  
         MVC   AIO,AIO1            RESTORE AIO JUST IN CASE                     
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',SRTREC),SRTCHECK-SRTREC,=C'ERROR'             
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'I',ASRTCHK),0,=C'SRTCHECK'                       
*                                                                               
         L     RD,INVRD            RETURN TO CALLER OF PROCINVC                 
         B     XIT                                                              
*                                                                               
* THIS ROUTINE ADDS AN ENTRY TO THE ERROR INVOICE TABLE AND PUTS THE            
* INVOICE NUMBER AND INVOICE ERROR NUMBER IN THE ENTRY.                         
*                                                                               
ADDERI   L     RF,AERITAB          DIE IF NO MORE ROOM IN ERITAB                
         A     RF,=A(MAXERIS*ERILEN)                                            
         C     RF,NEXTERI                                                       
         BH    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,NEXTERI          R5 = A(NEXT ERITAB ENTRY TO ADD)             
         USING ERITABD,R5                                                       
*                                                                               
         MVC   ERIAGY,SRTAGY       SAVE AGENCY NUMBER IN ENTRY                  
         MVC   ERIINV,SRTINV       SAVE INVOICE NUMBER IN ENTRY                 
         MVC   ERITERR,INVTERR     SAVE INVOICE ERROR NUMBER IN ENTRY           
*                                                                               
         OC    SRTAAGY,SRTAAGY     IF ADJUSTMENT AGY/INV                        
         BZ    *+16                                                             
         MVC   ERIAGY,SRTAAGY      THEN SAVE IN ENTRY                           
         MVC   ERIINV,SRTAINV                                                   
*                                                                               
         LA    R5,ERILEN(R5)       SAVE ADDRESS OF NEXT ENTRY                   
         ST    R5,NEXTERI                                                       
         BR    RE                                                               
         EJECT                                                                  
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF A CONDITION             
* OCCURS THAT MAKES IT NECESSARY TO ISSUE A WARNING BUT DOES NOT MAKE           
* IT IMPOSSIBLE TO CONTINUE PROCESSING THE INVOICE.  THE WARNING TYPE           
* (INVOICE/CHECK) AND NUMBER ARE SAVED IN THE SORT RECORD.  THE SECOND          
* PASS OF THE PROGRAM WHICH READS THE SORTER RECORDS WILL ADD ENTRIES           
* TO THE WARNING TABLE FOR EACH CHECK THAT HAS A WARNING BECAUSE IT             
* IS AT THAT TIME THAT THE PROGRAM WILL KNOW THE CHECK NUMBER.  WARNING         
* MESSAGES WILL APPEAR AS PART OF THE ERROR REPORT AT THE END.                  
*                                                                               
WARECTI  MVI   SRTWARN,TAINECTI    NO CMCL TITLE ELEMENT/INVOICE                
         B     WARITYP                                                          
*                                                                               
WARECOM  MVI   SRTWARN,TAINECOM    NO COMMERCIAL RECORD                         
         B     WARITYP                                                          
*                                                                               
WARECEM  MVI   SRTWARN,TAINECEM    NO EMPLOYER RECORD                           
         B     WARITYP                                                          
*                                                                               
WARECCL  MVI   SRTWARN,TAINECCL    NO CLIENT RECORD                             
         B     WARITYP                                                          
*                                                                               
WARECPR  MVI   SRTWARN,TAINECPR    NO PRODUCT RECORD                            
         B     WARITYP                                                          
*                                                                               
WAREW4A  MVI   BYTE,TAINEW4A       NO W4 ADDRESS ELEMENT                        
         B     WARCTYP                                                          
*                                                                               
WAREAN   MVI   BYTE,TAINEAN        NO AGENT RECORD                              
         B     WARCTYP                                                          
*                                                                               
WAREANA  MVI   BYTE,TAINEANA       NO AGENT ADDRESS ELEMENT                     
         B     WARCTYP                                                          
*                                                                               
WAREDUE  MVI   BYTE,TAINEDUE       NO DUE COMPANY DETAILS ELEMENT               
         B     WARCTYP                                                          
*                                                                               
WAREDUE2 MVI   BYTE,TAINEDU2       NO FLIST RECORD AS INDICATED ON              
         B     WARCTYP             DUE COMPANY RECORD                           
*                                                                               
WARELNE  MVI   BYTE,TAINELNE       NO LIEN DETAILS ELEMENT                      
         B     WARCTYP                                                          
*                                                                               
WARECKY  MVI   BYTE,TAINECKY       NO CHECK YTD ELEMENT                         
         B     WARCTYP                                                          
*                                                                               
WARITYP  MVI   SRTWTYP,C'I'        SET WARNING TYPE TO 'I' FOR INVOICE          
         BR    RE                      AND RETURN                               
*                                                                               
WARCTYP  CLI   SRTWTYP,C'I'        IF WARNING TYPE ALREADY 'I' FOR              
         BER   RE                      INVOICE THEN RETURN                      
*                                                                               
         MVI   SRTWTYP,C'C'        ELSE SET WARNING TYPE TO 'C' FOR             
         MVC   SRTWARN,BYTE            CHECK AND SET WARNING NUMBER             
         BR    RE                                                               
         EJECT                                                                  
* THE FOLLOWING ARE PLACES THAT THE PROGRAM COMES TO IF A CONDITION             
* OCCURS THAT MAKES IT IMPOSSIBLE FOR THE PROGRAM TO CONTINUE.  A               
* PROGRAM TERMINATION REASON WILL PRINT ON ITS OWN PAGE 20 TIMES, AND           
* THEN THE PROGRAM WILL DIE.                                                    
*                                                                               
ERRSYS   BRAS  RE,ERRSYSR                                                       
         B     ERRDIE                                                           
*                                                                               
ERRNRE   BRAS  RE,ERRNRE                                                        
         B     ERRDIE                                                           
*                                                                               
* THIS ROUTINE PRINTS THE ERROR MESSAGE IN BLOCK 20 TIMES ON ITS OWN            
* PAGE.  THEN IT TAKES AN INTENTIONAL HIT.                                      
*                                                                               
ERRDIE   L     R3,ASPOOLD          R3 = A(SPOOL DSECT)                          
         USING SPOOLD,R3                                                        
*                                                                               
         MVI   LINE,99             FORCE PAGE EJECT                             
         LA    R2,20               PRINT 20 TIMES                               
*                                                                               
ED10     MVC   P,BLOCK             MOVE BLOCK TO PRINT LINE                     
         GOTO1 SPOOL,DMCB,(R3)     PRINT IT                                     
         BCT   R2,ED10             REPEAT UNTIL 20 TIMES                        
*                                                                               
         DC    H'0'                CAUSE PROGRAM FAILURE                        
         DROP  R3                                                               
         EJECT                                                                  
         GETEL R4,DATADISP,ELCODE                                               
YES      SR    RC,RC                                                            
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
*                                                                               
XFFS     DC    6X'FF'                                                           
CHKDIR   DC    CL8'CHKDIR'                                                      
CHKFIL   DC    CL8'CHKFIL'                                                      
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,00,A),FORMAT=BI,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=XXXX'                                  
*                                                                               
DMYCWEL  DC    AL1(TACWELQ,TACWLN2Q)  DUMMY FEDERAL WITHHOLDING EL.             
         DC    C'FD '                                                           
         DC    (TACWLN2Q-(*-DMYCWEL))X'00'                                      
         DC    X'00'               SET EOR                                      
*                                                                               
MYSPACES DC    CL36' '                                                          
TMPCLA   DS    CL(L'SRTCLA)                                                     
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
         DROP  RA,R9,R8                                                         
         EJECT                                                                  
*                                                                               
* BUILD ACH TABLE FROM TLSYACHM SYSTEM KEY                                      
*                                                                               
BLDACHT  NTR1  BASE=*,LABEL=*                                                   
         USING TLSYD,R3                                                         
*                                                                               
         LA    R3,KEY                                                           
         XC    KEY,KEY                                                          
         MVI   TLSYCD,TLSYCDQ                                                   
         MVI   TLSYTYPE,TLSYACHM                                                
         MVC   TLSYSNDT,=C'TP '                                                 
         CLI   RECNUM,PK                                                        
         JNE   *+10                                                             
         MVC   TLSYSNDT,=C'P+ '                                                 
         CLI   RECNUM,PC                                                        
         JNE   *+10                                                             
         MVC   TLSYSNDT,=C'PP '                                                 
         CLI   RECNUM,EC                                                        
         JNE   *+10                                                             
         MVC   TLSYSNDT,=C'PP '                                                 
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLSYKEY),KEYSAVE                                           
         JNE   BLDACHTX                                                         
*                                                                               
         GOTO1 GETREC                                                           
*                                                                               
         USING ACHTABD,R3                                                       
         L     R3,AACHTAB                                                       
         MVI   0(R3),X'FF'                                                      
*                                                                               
         USING TATID,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TATIELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
BACHT10  BRAS  RE,NEXTEL                                                        
         JNE   BLDACHTX                                                         
*                                                                               
         MVC   ACHUNIT,TATIUNIT    UNIT                                         
         MVC   ACHTYPE,TATITYPE    TYPE                                         
         MVC   ACHACCT,TATIID      ACCOUNT                                      
         OC    ACHACCT,=C'              '                                       
         AHI   R3,ACHLEN                                                        
         MVI   0(R3),X'FF'                                                      
         J     BACHT10                                                          
*                                                                               
BLDACHTX J     XIT                                                              
         DROP  R3,R4                                                            
         LTORG                                                                  
* EXTRACT THE AGENCY NAME FROM THE AGENCY RECORD FOR THIS INVOICE IF IT         
* IS DIFFERENT THAN THE AGENCY FOR THE PREVIOUS INVOICE.                        
*                                                                               
EXTRAGYN NTR1  BASE=*,LABEL=*                                                   
         MVC   TGAGY,SRTAGY        SAVE AGENCY IN GLOBAL                        
*                                                                               
         CLC   SRTAGY,PREVAGY      IF AGENCY HAS CHANGED                        
         BE    EAYX                                                             
*                                                                               
         XC    SRTAGYN,SRTAGYN     PRE-CLEAR AGENCY NAME                        
*                                                                               
         XC    PREVAGY,PREVAGY     PRE-CLEAR PREVIOUS AGENCY                    
         XC    PREVCLI,PREVCLI     PRE-CLEAR PREVIOUS CLIENT                    
*                                                                               
         XC    BLOCK(44),BLOCK     GET AGY REC AND EXTR NAME TO BLOCK           
         MVI   BLOCK,36+8                                                       
         GOTO1 RECVAL,DMCB,TLAYCDQ,(X'88',SRTAGY),BLOCK                         
         JNE   ERRECAG                                                          
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'A',AIO),0,=C'AGENCY'                             
*                                                                               
         MVC   SRTAGYN,BLOCK+8     SAVE NAME IN SORT RECORD                     
*                                                                               
* NO-OP  L     R4,AIO              R4 = A(AGENCY DETAILS ELEMENT)               
* NO-OP  MVI   ELCODE,TAAYELQ                                                   
* NO-OP  BAS   RE,GETEL                                                         
* NO-OP  BNE   ERREAYE                                                          
* NO-OP  USING TAAYD,R4                                                         
* NO-OP                                                                         
* NO-OP  MVC   SRTTPOF,TAAYTPOF    SAVE TP OFFICE IN SORT RECORD                
*                                                                               
         MVC   PREVAGY,SRTAGY      SET PREVIOUS AGY TO THIS AGY                 
*                                                                               
EAYX     J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE USE ELEMENTS IN THE INVOICE RECORD INTO          
* THE SORT RECORD.                                                              
*                                                                               
EXTRIUSE NTR1  BASE=*,LABEL=*                                                   
         XC    SRTUDATA(SRTULEN),SRTUDATA  CLEAR USE ELEMENT DATA               
*                                                                               
         L     R4,AINVIO           R4 = A(CLASS A PROGRAM DETAILS ELEM)         
         MVI   ELCODE,TANPELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EIU50               SKIP IF NONE FOUND                           
         USING TANPD,R4                                                         
*                                                                               
         LA    R2,SRTCLA           R2 = A(FIRST CLASS A DATE)                   
         LA    R3,SRTCLA+L'SRTCLA  R3 = A(END OF CLASS A DATES)                 
*                                                                               
EIU10    MVC   0(3,R2),TANPDATE    MOVE CLASS A DATE TO SRTCLA                  
         MVC   3(1,R2),TANPLFT          LIFT CODE                               
*                                                                               
         CLI   TANPNWK,C'X'        IF PAX PROGRAM                               
         BNE   *+8                                                              
         OI    1(R2),X'80'         TURN ON MONTH'S X'80' BIT                    
*                                                                               
         LA    R2,4(R2)            BUMP R2 TO NEXT CLASS A DATE                 
         CR    R2,R3               IF REACHED END OF CLASS A DATES              
         BNL   EIU50               THEN DONE                                    
*                                                                               
         BRAS  RE,NEXTEL           BUMP TO NEXT CLASS A ELEMENT                 
         BE    EIU10               REPEAT UNTIL NO MORE ELEMENTS                
*                                                                               
EIU50    L     R4,AINVIO           R4 = A(UPGRADE DETAILS ELEMENT)              
         MVI   ELCODE,TAUPELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EIU100              SKIP IF NONE FOUND                           
         USING TAUPD,R4                                                         
*                                                                               
         MVC   SRTUMAJ,SRTMAJ      SAVE MAJORS/UNITS FROM PD ELEM AS            
         MVC   SRTUUNT,SRTUNT          UPGRADE MAJORS/UNITS IN SORT REC         
         MVC   SRTUINS,SRTINS      SAME GOES FOR INSERTS                        
*                                                                               
         LA    RF,UPGLIST                                                       
EIU53    CLI   0(RF),X'FF'         EOT                                          
         BE    EIU60                                                            
         CLC   SRTUSE,0(RF)        UPGRADABLE USE?                              
         BE    EIU55                                                            
         LA    RF,3(RF)                                                         
         B     EIU53               NO, SKIP UPGRADING MAJOR/UNITS               
EIU55    MVC   SRTMAJ,TAUPIMAJ     SAVE INITIAL MAJORS/UNITS IN                 
         MVC   SRTUNT,TAUPIUNT         SORT RECORD                              
*                                                                               
EIU60    TM    TGUSTYST,INSERTS                                                 
         BZ    *+10                                                             
         MVC   SRTINS,TAUPIINS     IF INSERTS REQUIRED, SAVE INSERTS            
*                                                                               
EIU100   DS    0H                                                               
*                                                                               
EIUX     J     XIT                                                              
UPGLIST  DC    C'CBLLCBSCBSWSWSCWSPSWSWSCADW'                                   
         DC    X'FF'                                                            
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
* INITIALIZE P+ CHECK                                                           
*                                                                               
INITPK   NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         JNE   INITPKX                                                          
*                                                                               
         USING TATUD,R4            INITIALIZE TATUWAAD'S TO TATUWAGE            
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
INITPK10 BRAS  RE,NEXTEL                                                        
         BNE   INITPKX                                                          
         MVC   TATUWAAD,TATUWAGE   INITIALIZE TAXABLE WAGES                     
         MVC   TATUTNAD,TATUSTRE   INITIALIZE TAXABLE REIMB                     
         MVC   TATUNNAD,TATUNNWA   INITIALIZE NON-TAXABLE REIMB                 
INITPK20 B     INITPK10                                                         
*                                                                               
INITPKX  J     XIT                                                              
         LTORG                                                                  
*                                                                               
* ADDS STATE TATU ELEMENT IF ONLY PAID AT CITY LEVEL                            
* ADDS PHL/NYC TATU ELEMENT IF RESIDENT AND NOT PAID IN PHL/NYC                 
* ADDS NON PHL/NYC CITY TAXES TO PHL/NYC IF RESIDENT                            
*                                                                               
TUWITH   NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,PK                                                        
         JNE   XIT                                                              
*                                                                               
         MVI   TUFLAG,0                                                         
         MVC   AIO,ASRTCHK                                                      
*                                                                               
TU10     L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                 DONE IF NO ELEMENTS                          
TU20     BRAS  RE,NEXTEL                                                        
         BNE   TU40                REPEAT UNTIL NO MORE                         
         USING TATUD,R4                                                         
         CLI   TATUUNIT+2,C' '                                                  
         BE    TU20                                                             
*                                                                               
         TM    TATUSTAT,TATUSPRO   ALREADY PROCESSED THIS?                      
         BO    TU20                                                             
         OI    TATUSTAT,TATUSPRO                                                
*                                                                               
         GOTO1 TAXVAL,DMCB,(3,TATUUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
         MVC   TGTWAGE,TATUWAGE    WAGES                                        
         MVC   TGTNWAGE,TATUTNWA   TAXABLE NON WAGES                            
         MVC   TGTSTRE,TATUSTRE    TAXABLE NON WAGES                            
         MVC   TGNTWAGE,TATUNNWA   NON TAXABLE NON WAGES                        
         MVC   TGTWAAD,TATUWAAD    WAGES                                        
         MVC   TGTTNAD,TATUTNAD    TAXABLE NON WAGES                            
         MVC   TGTNNAD,TATUNNAD    NON TAXABLE NON WAGES                        
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   TU30                                                             
         DROP  R4                                                               
*                                                                               
         L     R5,TGELEM                                                        
         USING TATUD,R5                                                         
*                                                                               
         ICM   RF,15,TATUWAGE      ACCUM WAGES                                  
         ICM   RE,15,TGTWAGE                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUWAGE                                                   
*                                                                               
         ICM   RF,15,TATUTNWA      ACCUM TAXABLE NON WAGES                      
         ICM   RE,15,TGTNWAGE                                                   
         AR    RE,RF                                                            
         STCM  RE,15,TATUTNWA                                                   
*                                                                               
         ICM   RF,15,TATUNNWA      ACCUM NON TAXABLE NON WAGES                  
         ICM   RE,15,TGNTWAGE                                                   
         AR    RE,RF                                                            
         STCM  RE,15,TATUNNWA                                                   
*                                                                               
         ICM   RF,15,TATUSTRE      ACCUM TAXABLE NON WAGES                      
         ICM   RE,15,TGTSTRE                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUSTRE                                                   
*                                                                               
         ICM   RF,15,TATUWAAD      ACCUM WAGES                                  
         ICM   RE,15,TGTWAAD                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUWAAD                                                   
*                                                                               
         ICM   RF,15,TATUTNAD      ACCUM WAGES                                  
         ICM   RE,15,TGTTNAD                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUTNAD                                                   
*                                                                               
         ICM   RF,15,TATUNNAD      ACCUM WAGES                                  
         ICM   RE,15,TGTNNAD                                                    
         AR    RE,RF                                                            
         STCM  RE,15,TATUNNAD                                                   
         B     TU20                                                             
*                                                                               
TU30     LA    R5,ELEM             BUILD STATE TU ELEMENT                       
         XC    ELEM,ELEM                                                        
         MVC   ELEM(TATULNQ),0(R4) COPY CITY ELEMENT                            
         MVC   TATUUNIT,TGTHREE    MOVE IN STATE                                
         GOTO1 ADDELEM                                                          
         B     TU10                                                             
         DROP  R5                                                               
*                                                                               
TU40     L     R4,ASRTCHK          CLEAR OUT PROCESSED MARKER                   
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
TU50     BRAS  RE,NEXTEL                                                        
         BNE   TU60                                                             
         USING TATUD,R4                                                         
         NI    TATUSTAT,X'FF'-TATUSPRO                                          
         B     TU50                                                             
         DROP  R4                                                               
*                                                                               
TU60     L     R4,AW4IO            IF RESIDES IN PHILADELPHIA THEN              
         MVI   ELCODE,TAWHELQ      ALWAYS TAKE PHILADELPHIA TAXES               
         BRAS  RE,GETEL                                                         
         BNE   TU80                                                             
         B     *+12                                                             
TU65     BRAS  RE,NEXTEL                                                        
         BNE   TU67                                                             
         USING TAWHD,R4                                                         
         CLC   TAWHUNIT,=C'PHL'    PHL RESIDENT?                                
         BNE   *+8                                                              
         OI    TUFLAG,TUPHLRES     PHL RESIDENT                                 
         CLC   TAWHUNIT,=C'NYC'    NYC RESIDENT?                                
         BNE   *+8                                                              
         OI    TUFLAG,TUNYCRES     NYC RESIDENT                                 
         B     TU65                                                             
*                                                                               
TU67     TM    TUFLAG,TUPHLRES+TUNYCRES                                         
         JZ    TU80                                                             
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
*                                                                               
         L     R5,TGELEM           GET TAXABLE AMOUNTS FROM FEDERAL             
         USING TATUD,R5                                                         
         MVC   TGTWAGE,TATUWAGE    WAGES                                        
         MVC   TGTNWAGE,TATUTNWA   TAXABLE NON WAGES                            
         MVC   TGTSTRE,TATUSTRE    TAXABLE NON WAGES                            
         MVC   TGTWAAD,TATUWAAD                                                 
         MVC   TGTTNAD,TATUTNAD                                                 
         MVC   TGTNNAD,TATUNNAD                                                 
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         MVC   MYTHREE,=C'PHL'                                                  
         TM    TUFLAG,TUNYCRES                                                  
         BZ    *+10                                                             
         MVC   MYTHREE,=C'NYC'                                                  
         GOTO1 GETL,DMCB,(3,MYTHREE)                                            
         BE    TU70                                                             
*                                                                               
         LA    R5,ELEM             NO - CREATE NEW PHLNYC TATU                  
         XC    ELEM,ELEM                                                        
         MVI   TATUEL,TATUELQ                                                   
         MVI   TATULEN,TATULNQ                                                  
         MVC   TATUUNIT,MYTHREE                                                 
         MVC   TATUWAGE,TGTWAGE    SET PHL/NYC WAGES TO FEDERAL                 
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         MVC   TATUWAAD,TGTWAAD                                                 
         MVC   TATUTNAD,TGTTNAD                                                 
         MVC   TATUNNAD,TGTNNAD                                                 
         OI    TATUSTAT,TATUSRES   RESIDENCE ONLY, NOT WORKING                  
         GOTO1 ADDELEM                                                          
         B     TU80                                                             
*                                                                               
TU70     L     R5,TGELEM                                                        
         MVC   TATUWAGE,TGTWAGE    SET PHL/NYC WAGES TO FEDERAL                 
         MVC   TATUWAAD,TATUWAGE                                                
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         MVC   TATUWAAD,TGTWAAD                                                 
         MVC   TATUTNAD,TGTTNAD                                                 
         MVC   TATUNNAD,TGTNNAD                                                 
         DROP  R5                                                               
*                                                                               
TU80     MVI   ELCODE,TATUELQ      WORKED IN CANADA?                            
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   TUX                                                              
*                                                                               
         L     R5,TGELEM           GET TAXABLE AMOUNTS                          
         USING TATUD,R5                                                         
         MVC   TGTWAGE,TATUWAGE    WAGES                                        
         MVC   TGTNWAGE,TATUTNWA   TAXABLE NON WAGES                            
         MVC   TGTSTRE,TATUSTRE    TAXABLE NON WAGES                            
         MVC   TGTWAAD,TATUWAAD                                                 
         MVC   TGTTNAD,TATUTNAD                                                 
         MVC   TGTNNAD,TATUNNAD                                                 
*                                                                               
         L     R4,AW4IO            IF WORKING IN CANADA                         
         MVI   ELCODE,TAWHELQ      MUST PAY US STATE AND CITY TAXES             
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
TU85     BRAS  RE,NEXTEL                                                        
         BNE   TUX                                                              
         USING TAWHD,R4                                                         
*                                                                               
         CLC   TAWHUNIT,=C'FD '                                                 
         BE    TU85                                                             
         CLC   TAWHUNIT,=C'CN '                                                 
         BE    TU85                                                             
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TAWHUNIT)                                           
         BE    TU90                                                             
*                                                                               
         LA    R5,ELEM             NO - CREATE NEW TATU                         
         XC    ELEM,ELEM                                                        
         MVI   TATUEL,TATUELQ                                                   
         MVI   TATULEN,TATULNQ                                                  
         MVC   TATUUNIT,TAWHUNIT                                                
         OI    TATUSTAT,TATUSRES   RESIDENCE ONLY, NOT WORKING                  
         MVC   TATUWAGE,TGTWAGE    SET WAGES TO CANADIAN                        
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         MVC   TATUWAAD,TGTWAAD                                                 
         MVC   TATUTNAD,TGTTNAD                                                 
         MVC   TATUNNAD,TGTNNAD                                                 
         GOTO1 ADDELEM                                                          
         MVI   ELCODE,TAWHELQ                                                   
         B     TU85                                                             
*                                                                               
TU90     L     R5,TGELEM                                                        
         MVC   TATUWAGE,TGTWAGE    SET PHL/NYC WAGES TO FEDERAL                 
         MVC   TATUWAAD,TATUWAGE                                                
         MVC   TATUTNWA,TGTNWAGE                                                
         MVC   TATUSTRE,TGTSTRE                                                 
         MVC   TATUWAAD,TGTWAAD                                                 
         MVC   TATUTNAD,TGTTNAD                                                 
         MVC   TATUNNAD,TGTNNAD                                                 
         B     TU85                                                             
         DROP  R4,R5                                                            
*                                                                               
TUX      MVC   AIO,AIO1                                                         
         J     XIT                                                              
*                                                                               
MYTHREE  DS    CL3                                                              
*                                                                               
TUFLAG   DS    XL1                 TATU FLAGS                                   
TUPHLRES EQU   X'80'               PHL RESIDENT                                 
TUNYCRES EQU   X'40'               NYC RESIDENT                                 
*                                                                               
TGTWAGE  DS    XL4                 TAXABLE WAGES                                
TGTNWAGE DS    XL4                 TAXABLE NON WAGES                            
TGNTWAGE DS    XL4                 NON TAXABLE WAGES/EARNINGS                   
TGTSTRE  DS    XL4                 TAXABLE NON WAGES                            
TGTWAAD  DS    XL4                 TAXABLE WAGES AFTER DUE COMPANY              
TGTTNAD  DS    XL4                 TAXABLE REIMB AFTER DUE COMPANY              
TGTNNAD  DS    XL4                 NON-TAXABLE WAGES AFTER DUE COMPANY          
         LTORG                                                                  
*                                                                               
* CONVERT TO CANADIAN DOLLARS                                                   
*        ON ENTRY, PARAM 1 = FIELD TO BE CONVERTED                              
*                  PARAM 2 = CONVERTED LOCATION                                 
*                                                                               
CONVCAD  NTR1  BASE=*,LABEL=*                                                   
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
*                                                                               
         ZAP   DUB,CCVTRATE                                                     
         CVB   R1,DUB                                                           
         ST    R1,FULL                                                          
*                                                                               
         ICM   R1,15,0(R2)                                                      
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LPR   R1,R1                                                            
*                                                                               
         XR    R0,R0                                                            
         M     R0,=F'10000'                                                     
         D     R0,FULL                                                          
*                                                                               
         L     RE,FULL                                                          
         AHI   RE,1                                                             
         SRA   RE,1                                                             
         CR    R0,RE                                                            
         BL    *+8                                                              
         AHI   R1,1                                                             
*                                                                               
         TM    0(R2),X'80'         NEGATIVE? (VOID/CREDIT/CANCEL)               
         BZ    *+6                                                              
         LNR   R1,R1                                                            
*                                                                               
         STCM  R1,15,0(R3)                                                      
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE COMPUTES THE W4 TRUSTEE AMT FOR THE CHECK.  SAVES IT             
* IN TRUSTEE TABLE, COMPUTES REMAINING BALANCE, SAVES SSN FOR TRUSTEE           
* CHECK, SUBTRACTS FROM CHK AMOUNT IN SORT RECORD, ADDS OTHER DEDUCTION         
* ELEMENT AND SAVES TRUSTEE SSN TO CHECK RECORD                                 
*                                                                               
TRST     NTR1  BASE=*,LABEL=*                                                   
         OC    SRTNET,SRTNET       IF CHECK AMOUNT ZERO THEN DONE               
         JZ    TRSTX                                                            
                                                                                
         OC    SRTW4DOB,SRTW4DOB   IF DATE OF BIRTH SPECIFIED                   
         JZ    TRST2                                                            
         GOTO1 DATCON,DMCB,(1,SRTW4DOB),(0,WORK)                                
         CLC   SRTUN,=C'ACT'       IF CANADIAN IS LOCAL VBC                     
         JNE   TRSTA                                                            
         CLC   SRTLOCL,=C'VBC'     TEST FOR UNDER 15 YRS OF AGE                 
         JNE   TRSTA                                                            
         GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK+8,15                                 
         J     TRSTB                                                            
*                                   OTHERWISE TEST UNDER 18 YRS                 
TRSTA    GOTO1 ADDAY,DMCB,(C'Y',WORK),WORK+8,18                                 
                                                                                
TRSTB    CLC   TGTODAY0,WORK+8     AND PERFORMER STILL A MINOR                  
         JNL   TRSTX                                                            
         J     *+14                                                             
TRST2    OC    SRTW4SSN,SRTW4SSN   OR IF TRUSTEE SPECIFIED                      
         JZ    TRSTX                                                            
                                                                                
         CLC   SRTUN,=C'ACT'       IF CANADIAN                                  
         JNE   TRST200                                                          
                                                                                
         BRAS  RE,ERN5000          CHECK >=$5000 LIFETIME EARNINGS              
         JNE   TRSTX                                                            
         OI    SRTTRBTS,SRTTCNTR   CANADIAN TRUST GENERATED                     
         MVC   SRTW4SSN,=C'000012175'   DEFAULT TRUSTEE FOR ACT                 
         CLC   SRTLOCL,=C'VBC'                                                  
         JNE   TRST900                                                          
         MVC   SRTW4SSN,=C'000012174'   DEFAULT TRUSTEE FOR ACT VBC             
         J     TRST900             CONTINUE TRUST CALCULATION                   
                                                                                
TRST200  CLC   SRTSOW,=C'CA '      IF LIVE OR WORK IN CALIFORNIA                
         JE    TRST230                                                          
         CLC   SRTW4SOR,=C'CA '    (STATE OF RESIDENCE IF INDIVIDUAL            
         JE    TRST230             PAID AS CORP)                                
         CLC   SRTSOR,=C'CA '                                                   
         JE    TRST230                                                          
         CLC   CLICLG,=CL6'DIS'    OR WORKING FOR A DISNEY CLIENT               
         JNE   TRST250                                                          
TRST230  TM    TGCATYPE,EXTRA      AND NOT AN EXTRA                             
         JO    TRSTX                                                            
         OI    SRTTRBTS,SRTTCATR   CALIFORNIA TRUST GENERATED                   
         J     TRST900                                                          
                                                                                
TRST250  CLC   SRTSOW,=C'NY '      IF LIVE OR WORK IN NEW YORK                  
         JE    TRST260                                                          
         CLC   SRTW4SOR,=C'NY '    (STATE OF RESIDENCE IF INDIVIDUAL            
         JE    TRST260             PAID AS CORP)                                
         CLC   SRTSOR,=C'NY '                                                   
         JNE   TRST300                                                          
TRST260  CLC   SRTUSE,=C'AUD'      AND EXCLUDE AUDITIONS                        
         JE    TRSTX                                                            
                                                                                
         CLI   RECNUM,PK           ALWAYS TAKE FOR P+                           
         JE    TRST270                                                          
                                                                                
         CLI   RECNUM,PC           ONLY TAKE FOR PP                             
         JNE   TRST265                                                          
         CLI   TGCAEQU,CTPM        IF CATEGORY IS PM                            
         JNE   TRSTX                                                            
         BRAS  RE,SETSHOOT                                                      
         CLC   WORK(3),=X'A40328'  AND SHOOT DATE >= 2004/03/28                 
         JNL   TRST270                                                          
         J     TRSTX                                                            
                                                                                
TRST265  BRAS  RE,NYTDATES         FOR TP                                       
         CLC   WORK(3),=X'A40328'  TEST FOR WORK DATE >= 2004/03/28             
         JL    TRSTX                                                            
                                                                                
TRST270  OI    SRTTRBTS,SRTTNYTR   NY TRUST GENERATED                           
         J     TRST900                                                          
                                                                                
TRST300  GOTO1 TRSTROUT,DMCB,SRTSOW      STATE OF WORK CHECK                    
         JNE   TRSTX                                                            
                                                                                
TRST900  TM    TGUSSTAT,SESSION    IF IT IS A RESIDUAL PAYMENT                  
         JO    TRST930                                                          
         MVC   WORK(3),SRTFRST     WORK DATE MUST BE 1/1/00 OR LATER            
         OC    SRTFRST,SRTFRST                                                  
         JNZ   *+10                                                             
         MVC   WORK(3),SRTFCYC                                                  
                                                                                
TRST930  L     RF,SRTGRS           USE GROSS                                    
         ICM   R0,15,SRTW4PCT      ELSE IF DEDUCTION PERCENTAGE NOT             
         JZ    TRST950                                                          
         CHI   R0,2500             IF < MINIMUM REQUIRED BY LAW,                
         JNL   TRST990             USE DEFAULTS                                 
         TM    SRTTRBTS,SRTTCNTR   CANADA? MUST BE AT LEAST 25%                 
         JO    TRST950                                                          
         CHI   R0,1500             OTHERWISE, MUST BE AT LEAST 15%              
         JNL   TRST990                                                          
                                                                                
TRST950  LHI   R0,1500             DEFAULT TO 15%                               
         TM    SRTTRBTS,SRTTCNTR   CANADA?                                      
         JZ    TRST990                                                          
         LHI   R0,2500             YES, DEFAULT TO 25%                          
                                                                                
TRST990  MR    RE,R0                                                            
         D     RE,=F'5000'         PERCENTAGE                                   
         LTR   RF,RF                                                            
         JM    *+8                 & ROUND IT                                   
         AHI   RF,1                                                             
         SRA   RF,1                                                             
         CLM   RF,15,SRTNET        IF AMOUNT TO COLLECT > CHECK AMOUNT          
         JNH   *+8                                                              
         L     RF,SRTNET           THEN AMOUNT TO COLLECT = CHECK AMNT          
         ST    RF,TRUSTCOL         SAVE AMOUNT TO COLLECT IN TRUSTCOL           
         MVC   TRUSTSSN,SRTW4SSN   SAVE SSN FOR GENERATED TRUSTEE CHK           
                                                                                
         BRAS  RE,GETTRST          GET TABLE ENTRY                              
         L     R5,0(R1)            R5 = A(TABLE ENTRY)                          
         USING TRSTABD,R5                                                       
                                                                                
         ICM   RF,15,TRSTCOL       ADD AMT TO COLLECTED AMOUNT                  
         A     RF,TRUSTCOL                                                      
         STCM  RF,15,TRSTCOL                                                    
                                                                                
         ICM   RF,15,TRSTINV       ADD AMOUNT TO THE COLLECTED AMOUNT           
         A     RF,TRUSTCOL         FOR THIS INVOICE                             
         STCM  RF,15,TRSTINV                                                    
         MVC   TRSTSSN,TRUSTSSN    SAVE SSN FOR TRUSTEE GENERATED CHK           
                                                                                
         L     RF,SRTNET           SUBTRACT AMOUNT FROM THE CHECK               
         S     RF,TRUSTCOL         AMOUNT IN SORT RECORD                        
         ST    RF,SRTNET                                                        
         MVC   SRTTRST,TRUSTCOL    SAVE COLLECTED AMOUNT IN SORT RECORD         
                                                                                
         LA    R4,ELEM             BUILD W4 TRUSTEE OTHER DEDUCT ELE            
         USING TAODD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPT   SET W4 TRUSTEE DEDUCTION                     
         MVC   TAODAMT,TRUSTCOL    SAVE COLLECTED AMOUNT IN ELEMENT             
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
                                                                                
         OC    TRSTSSN,TRSTSSN     IF NO TRUSTEE SSN                            
         JZ    TRSTX                                                            
         LA    R4,ELEM             BUILD ELEMENT LIEN PAID TO                   
         USING TANUD,R4                                                         
         XC    ELEM,ELEM                                                        
         MVI   TANUEL,TANUELQ                                                   
         MVI   TANULEN,TANULNQ+9                                                
         MVI   TANUTYPE,TANUTRST   SET TYPE                                     
         MVC   TANUMBER(9),TRSTSSN SET S/S NUMBER OF TRUSTEE                    
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1            RESTORE AIO TO AIO1                          
                                                                                
TRSTX    J     XIT                                                              
         EJECT                                                                  
*---------------------------------------------------------------------          
*        CHECK IF UNIT PASSED ARE ONE WITH TRUST WITHHOLDINGS                   
*              P1(1) = X'80' - USE RESIDENTS TABLE                              
*              SRTTRBTS OR'D WITH BIT ACCORDINGLY                               
*---------------------------------------------------------------------          
TRSTROUT NTR1                                                                   
         LA    RF,TRSTUNTT                                                      
         L     R3,0(R1)            ADDRESS OF UNIT TO TEST AGAINST              
TRSTR10  CLI   0(RF),X'FF'         EOT                                          
         JE    NO                                                               
         CLC   0(3,RF),0(R3)       COMPARE UNIT TO WHAT'S PASSED                
         JE    TRSTR50                                                          
         AHI   RF,4                BUMP TO NEXT ONE                             
         B     TRSTR10                                                          
*                                                                               
TRSTR50  OC    SRTTRBTS,3(RF)      MARK WHICH UNIT WITH BIT                     
         J     YES                                                              
                                                                                
TRSTUNTT DC    C'LA ',AL1(SRTTLATR)                                             
         DC    C'NM ',AL1(SRTTNMTR)                                             
         DC    X'FFFFFFFF'                                                      
         DROP  R4,R5                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS ROUTINE GETS THE PAYROLL+ COMMISSION IF THERE IS ONE                     
*----------------------------------------------------------------------         
PPLAAMT  NTR1  BASE=*,LABEL=*                                                   
         XC    SRTPPLA,SRTPPLA                                                  
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TANUELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TANUTPAC))                                     
         JNE   PPLAAMTX            SKIP IF NONE FOUND                           
         L     R4,TGELEM           R4 = A(PAYROLL+ COMMISSION)                  
         USING TANUD,R4                                                         
         MVC   SRTPPLA,TANUOVAM                                                 
PPLAAMTX MVC   AIO,AIO1                                                         
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*======================================================================         
* THIS ROUTINE TAKES THE FINAL NET AMOUNT AND SETS IT TO DIRECT                 
* DEPOSIT.  IT ADDS THE NEW DIRECT DEPOSIT AMOUNT TO THE TOTAL                  
* DEDUCTIONS AND SUBTRACTS IT FROM NET AMOUNT (CAUSING NET TO BE ZERO)          
*======================================================================         
DIRECTD  NTR1  BASE=*,LABEL=*                                                   
         XC    SRTDIRCT,SRTDIRCT   PRE CLEAR DIRECT AMOUNT                      
*                                                                               
*NO-OP*  CLC   SRTEMP,=C'PG '      FOR EMPLOYER PG                              
*NO-OP*  BNE   DIRECTDX                                                         
         CLI   RECNUM,PK           FOR PAYROLL PLUS CHECKS                      
         JE    DD10                                                             
         CLI   SRTCTYP,CTYSOAP     FOR SOAP COMMERCIALS                         
         JNE   DIRECTDX                                                         
DD10     TM    SRTBITS,SRTBDD      IF PERF'S CHKS ARE DIRECT DEPOSIT            
         JZ    DIRECTDX                                                         
         OC    SRTNET,SRTNET       AND IF CHECK AMOUNT                          
         JZ    DIRECTDX                                                         
*                                                                               
         MVC   SRTDIRCT,SRTNET     SET CHECK AMOUNT TO DIRECT                   
*                                                                               
         XC    ELEM,ELEM           BUILD DIR DEP OTHER WITHHOLDING ELE          
         LA    R4,ELEM                                                          
         USING TAODD,R4                                                         
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPD                                                
         MVC   TAODAMT,SRTDIRCT                                                 
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         L     RF,SRTTDED          ADD DIRECT TO TOTAL DEDUCTIONS               
         A     RF,SRTDIRCT                                                      
         ST    RF,SRTTDED                                                       
*                                                                               
         L     RF,SRTNET           SUBTRACT DIRECT FROM CHECK AMOUNT            
         S     RF,SRTDIRCT                                                      
         ST    RF,SRTNET                                                        
*                                                                               
DIRECTDX J     XIT                                                              
                                                                                
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*======================================================================         
* THIS ROUTINE TAKES THE FINAL NET AMOUNT AND SETS IT TO WIRE                   
* TRANSFER.  IT ADDS THE NEW WIRE TRANSFER AMOUNT TO THE TOTAL                  
* DEDUCTIONS AND SUBTRACTS IT FROM NET AMOUNT (CAUSING NET TO BE ZERO)          
*======================================================================         
WIRED    NTR1  BASE=*,LABEL=*                                                   
         XC    SRTWIRE,SRTWIRE     PRE CLEAR WIRE AMOUNT                        
*                                                                               
         TM    SRTBITS,SRTBWIRE    IF PERF'S CHKS ARE WIRED                     
         JZ    WIREDX                                                           
         OC    SRTNET,SRTNET       AND IF CHECK AMOUNT                          
         JZ    WIREDX                                                           
*                                                                               
WIRED10  MVC   SRTWIRE,SRTNET      SET CHECK AMOUNT TO WIRE AMOUNT              
*                                                                               
WIRED20  XC    ELEM,ELEM           BUILD OTHER WITHHOLDING EL 'W'               
         LA    R4,ELEM                                                          
         USING TAODD,R4                                                         
         MVI   TAODEL,TAODELQ                                                   
         MVI   TAODLEN,TAODLNQ                                                  
         MVI   TAODTYPE,TAODTYPW                                                
         MVC   TAODAMT,SRTWIRE                                                  
*                                                                               
         MVC   AIO,ASRTCHK         ADD ELEMENT TO CHECK RECORD                  
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         CLI   RECNUM,EC           EUROS AND ADJUSTMENTS                        
         JNE   WIRED30                                                          
         TM    SRTADJS,TAPDADTR+TAPDADTT                                        
         JNZ   WIRED40                                                          
*                                                                               
WIRED30  L     RF,SRTTDED          ADD IT TO TOTAL DEDUCTIONS                   
         A     RF,SRTWIRE                                                       
         ST    RF,SRTTDED                                                       
*                                                                               
WIRED40  L     RF,SRTNET           SUBTRACT IT FROM CHECK AMOUNT                
         S     RF,SRTWIRE                                                       
         ST    RF,SRTNET                                                        
*                                                                               
WIREDX   J     XIT                                                              
                                                                                
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*====================================================================           
* EXTRACT AGENCY STATUS                                                         
*====================================================================           
         USING TLINPD,R3                                                        
EXTAGY   NTR1  BASE=*,LABEL=*                                                   
         CLC   SRTAGY,TLINCAGY     IF AGENCY HASN'T CHANGED, SKIP               
         JE    YES                 YES, LEAVE                                   
                                                                                
         MVI   AYSTAT6,0                                                        
         MVI   AYSTAT7,0                                                        
         MVC   SVIKEY,KEY          SAVE THE KEY                                 
                                                                                
         USING TLAYD,R3                                                         
         XC    KEY,KEY                                                          
         MVI   TLAYCD,TLAYCDQ                                                   
         MVC   TLAYAGY,SVIKEY+TLINCAGY-TLINPD                                   
         GOTO1 HIGH                                                             
                                                                                
         CLC   TLAYKEY(TLAYLEN-TLAYKEY),KEYSAVE    CHECK AGENCY EQUAL           
         JNE   EXTAGYNO                                                         
         MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
                                                                                
         USING TAAYD,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TAAYELQ      GET AGENCY ELEMENT                           
         BRAS  RE,GETEL                                                         
         JNE   EXTAGYNO                                                         
         MVC   AYSTAT6,TAAYSTA6                                                 
         CLI   TAAYLEN,TAAYLNQ                                                  
         BL    EXTAGYES                                                         
         MVC   AYSTAT7,TAAYSTA7                                                 
         DROP  R4                                                               
                                                                                
EXTAGYES CR    RE,RE                                                            
         B     EXTAGYCC                                                         
                                                                                
EXTAGYNO LTR   RB,RB                                                            
                                                                                
EXTAGYCC MVC   AIO,AIO1                                                         
         MVC   KEY,SVIKEY          RESTORE KEY                                  
         JNE   NO                                                               
         GOTO1 HIGH                RESTORE READ SEQ                             
         J     YES                                                              
         EJECT                                                                  
*====================================================================           
* EXTRACT CLIENT STATUS                                                         
*====================================================================           
         USING TLCLPD,R3                                                        
EXTCLI   NTR1  BASE=*,LABEL=*                                                   
         MVI   PDOPT5,0                                                         
         MVI   CLSTAT2,0                                                        
         CLC   SVIKEY+TLINCAGY-TLINPD(6),=C'999999'                             
         JE    YES                                                              
                                                                                
         MVC   SVIKEY,KEY          SAVE THE KEY                                 
                                                                                
         USING TAPDD,R4                                                         
         L     R4,AINVIO                                                        
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   EXTCLYES                                                         
         MVC   SRTCLI,TAPDCLI      GET CLIENT                                   
                                                                                
         CLI   TAPDLEN,TAPDLNQ     IF WE'RE USING AN OLDER LENGTH               
         BL    EXTCLI30            NO, NEW LENGTH                               
         MVC   PDOPT5,TAPDOPT5                                                  
                                                                                
         USING TLCLD,R3                                                         
EXTCLI30 XC    KEY,KEY                                                          
         MVI   TLCLCD,TLCLCDQ                                                   
         MVC   TLCLAGY,SVIKEY+TLINCAGY-TLINPD                                   
         MVC   TLCLCLI,SRTCLI                                                   
         GOTO1 HIGH                                                             
                                                                                
         CLC   TLCLKEY(TLCLLEN-TLCLKEY),KEYSAVE    CHECK CLIENT EQUAL           
         JNE   EXTCLYES                                                         
         MVC   AIO,AIO2                                                         
         GOTO1 GETREC                                                           
                                                                                
         USING TACID,R4                                                         
         L     R4,AIO                                                           
         MVI   ELCODE,TACIELQ      GET CLIENT ELEMENT                           
         BRAS  RE,GETEL                                                         
         JNE   EXTCLYES                                                         
         MVC   CLSTAT2,TACISTA2                                                 
                                                                                
EXTCLYES CR    RE,RE                                                            
         B     EXTCLICC                                                         
                                                                                
EXTCLINO LTR   RB,RB                                                            
                                                                                
EXTCLICC MVC   AIO,AIO1                                                         
         MVC   KEY,SVIKEY          RESTORE KEY                                  
         JNE   NO                                                               
         GOTO1 HIGH                RESTORE READ SEQ                             
         J     YES                                                              
         EJECT                                                                  
*====================================================================           
* CHECK REGRESSION STATUS                                                       
*====================================================================           
CHKREG   NTR1  BASE=*,LABEL=*                                                   
         TM    REQOPTS,REQREG+REQREG2   REGRESSION REQUESTED                    
         JZ    CHKREG10                                                         
         J     CHKREG20                                                         
                                                                                
CHKREG10 TM    AYSTAT6,TAAYSREG     NO, AGENCY OR CLIENT CAN'T HAVE IT          
         JO    NO                                                               
         TM    CLSTAT2,TACISREG                                                 
         JO    NO                                                               
         J     YES                                                              
*                                  REGRESSION REQUESTED                         
CHKREG20 TM    REQOPTS,REQREG      YES, AGENCY AND CLIENT MUST HAVE IT          
         BZ    CHKREG40                                                         
         TM    PDOPT5,TAPDORG2     REG2 SET, SKIP IT                            
         JO    NO                                                               
         TM    AYSTAT6,TAAYSREG    IF AGENCY OR CLIENT HAS REG                  
         JO    YES                 YES, PROCESS IT                              
         TM    CLSTAT2,TACISREG                                                 
         JO    YES                                                              
         J     NO                                                               
*                                  REGRESSION 2 REQUESTED                       
CHKREG40 TM    PDOPT5,TAPDORG2     REG2 SET                                     
         JZ    NO                  NO, SKIP                                     
         J     YES                                                              
         EJECT                                                                  
*=================                                                              
* NEW WAY                                                                       
*=================                                                              
* THIS ROUTINE UPDATES THE CHECK RECORD WITH THE YTD AMOUNTS FOR THIS           
* CHECK.  IT GETS THE YTD AMOUNTS FROM ESUTAB AND SAVES THEM IN YTD             
* ELEMENTS, WHICH IT ADDS, AND THE CHECK DETAILS ELEMENT.                       
* ADDITIONALLY, IT SAVES THE YTD AMOUNTS IN THE SORT RECORD FOR                 
* PRINTING.                                                                     
*                                                                               
UPDYTD   NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,ASRTCHK         REMOVE EXISTING YTD WITHHOLDING ELS          
         MVI   ELCODE,TACYELQ                                                   
         GOTO1 REMELEM                                                          
         MVC   AIO,AIO1                                                         
         MVI   BYTE,0              CLEAR PROCESSED FD ESUTAB ENTRY FLAG         
*                                                                               
         LA    R5,ELEM             R5 = A(SKELETON YTD ELEMENT)                 
         USING TACYD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACYEL,TACYELQ                                                   
         MVI   TACYLEN,TACYLN3Q                                                 
*                                                                               
         SAM31                                                                  
         L     R3,AESUTAB                                                       
         USING BIND,R3                                                          
         L     R2,BININ            NUMBER OF ENTRIES IN ESUTAB                  
         LA    R3,BINTABLE         POINT TO FIRST ENTRY                         
         LTR   R2,R2               ANY ENTRIES?                                 
         JZ    UY95                                                             
*                                                                               
         ST    R3,AESUCUR                                                       
         USING ESUTABD,R3          R4 = A(ESUTAB ENTRY FOR THIS UNIT)           
UY10     SAM31                                                                  
         L     RF,AESUCUR          GET ESUTAB ENTRY INTO BLOCK                  
         LA    R3,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
*                                                                               
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BO    *+14                                                             
         CLC   SRTEMP,ESUEMP                                                    
         BNE   UY90                                                             
*                                                                               
         CLC   SRTSSN,ESUSSN       SAME SS#                                     
         JNE   UY90                                                             
         MVC   TACYUNIT,ESUUNIT    SAVE UNIT IN YTD ELEMENT                     
         MVC   TACYTAX,ESUTAX           TAX                                     
         MVC   TACYFICA,ESUFICA         FICA (OR SDI)                           
         MVC   TACYEARN,ESUEARN         EARNINGS                                
         MVC   TACYTXRE,ESUTXRE         TAXABLE REIMBURSEMENTS                  
         MVC   TACYNTRE,ESUNTRE         NON-TAXABLE REIMBURSEMENTS              
*                                                                               
         OC    ESUYSUI,ESUYSUI     ANY YTD SUI?                                 
         BZ    UY12                                                             
         MVI   TACYLEN,TACYLN4Q                                                 
         MVC   TACYYSUI,ESUYSUI                                                 
*                                                                               
UY12     CLC   ESUUNIT,=C'CN '                                                  
         JE    UY15                                                             
         MVC   TGCTRY,=C'CA'       CANADIAN                                     
         GOTO1 TAXVAL,DMCB,(X'FF',ESUUNIT) UNIT                                 
         JNE   UY15                                                             
         MVI   TACYLEN,TACYLN6Q                                                 
         MVC   TACYFERN,ESUFERN    FEDERAL EARNINGS AT PROV LEVEL               
*                                                                               
UY15     CLC   TACYUNIT,=C'FD '    IF UNIT IS FEDERAL                           
         JNE   UY20                                                             
         MVC   SRTYFED,TACYTAX     THEN SAVE YTD FEDERAL TAX IN SORTREC         
         MVC   SRTYFIC,TACYFICA              YTD FICA                           
         MVC   SRTYREX,ESUREXP               YTD REIMBURSED EXP.                
         MVI   BYTE,1              SET PROCESSED FD ESUTAB ENTRY                
         J     UY70                                                             
*                                                                               
UY20     CLC   TACYUNIT,=C'CN '    ELSE IF UNIT IS CANADA                       
         JNE   UY30                                                             
         MVC   SRTYCAN,TACYTAX     THEN SAVE YTD CAN TAX IN SORTREC             
         MVC   SRTYGST,ESUGST                YTD GST                            
         MVC   TACYGST,ESUGST      SET YTD GST IN YTD ELEMENT                   
         MVC   SRTYPST,ESUPST                YTD GST                            
         MVC   TACYPST,ESUPST      SET YTD GST IN YTD ELEMENT                   
         J     UY70                                                             
*                                                                               
UY30     MVC   TACYSUI,ESUSUI      SET YTD SUI IN YTD ELEMENT                   
         MVC   TACYSFLI,ESUSFLI    SET YTD FLI IN YTD ELEMENT                   
*                                                                               
         MVC   AIO,ASRTCHK         SEE IF WE HAVE WITHOLDING ELEMENT            
         MVI   ELCODE,TACWELQ                                                   
         GOTO1 GETL,DMCB,(1,TACYUNIT)                                           
         MVC   AIO,AIO1                                                         
         JNE   UY70                                                             
         L     R4,TGELEM                                                        
         USING TACWD,R4                                                         
*                                                                               
         CLI   TACYUNIT+2,C' '     ELSE IF UNIT IS A CITY                       
         JNH   UY40                                                             
         TM    TACWSTAT,TACWSTAX   AND IT'S MARKED TAXABLE                      
         JZ    *+16                                                             
         L     RF,TACYTAX          THEN ADD LOCAL TAX TO YTD LOCAL TAX          
         A     RF,SRTYLOC              IN SORT RECORD                           
         ST    RF,SRTYLOC                                                       
         J     UY70                                                             
*                                                                               
UY40     TM    TACWSTAT,TACWSTAX   ELSE IF THIS IS TAXABLE STATE                
         JZ    *+16                                                             
         L     RF,TACYTAX          THEN ADD STATE TAX TO YTD STATE TAX          
         A     RF,SRTYSTA              IN SORT RECORD                           
         ST    RF,SRTYSTA                                                       
*                                                                               
UY70     OC    TACYAMTS,TACYAMTS   ANY AMOUNTS                                  
         JNZ   UY75                                                             
         OC    TACYTXRE,TACYTXRE   ANY AMOUNTS                                  
         JNZ   UY75                                                             
         CLI   RECNUM,PK           IF EVENT CHECK RUN                           
         JNE   UY90                                                             
         TM    SRTEARN,X'80'       NEGATIVE? (VOID/CREDIT/CANCEL)               
         JZ    UY90                                                             
*                                                                               
UY75     CLI   RECNUM,PK           IF EVENT CHECK RUN                           
         JNE   UY85                                                             
         TM    SRTEARN,X'80'       NEGATIVE? (VOID/CREDIT/CANCEL)               
         JZ    UY80                                                             
         CLC   TACYUNIT,=C'FD '                                                 
         JE    UY80                                                             
         CLC   TACYUNIT,=C'CN '                                                 
         JE    UY80                                                             
         CLI   TACYUNIT+2,C' '                                                  
         JNE   UY80                NO TAXABLE SUI FOR CITIES                    
         OC    ESUYSUI,ESUYSUI                                                  
         JZ    UY80                                                             
         CLI   TACYLEN,TACYLN4Q                                                 
         BH    *+8                                                              
         MVI   TACYLEN,TACYLN4Q                                                 
*                                                                               
UY80     BRAS  RE,TSTPCAN          P+ CANADIAN TAXES?                           
         JNE   UY85                                                             
         CLC   ESUUNIT,=C'CN '                                                  
         JE    UY82                                                             
         MVC   TGCTRY,=C'CA'       CANADIAN                                     
         GOTO1 TAXVAL,DMCB,(X'FF',ESUUNIT) UNIT                                 
         JNE   UY85                                                             
UY82     CLI   TACYLEN,TACYLN5Q                                                 
         BH    *+8                                                              
         MVI   TACYLEN,TACYLN5Q                                                 
         MVC   TACYCTXR,ESUCTXRE                                                
         MVC   TACYCNTR,ESUCNTRE                                                
         MVC   TACYCERN,ESUCEARN                                                
         MVC   TACYUTAX,ESUUTAX                                                 
         MVC   TACYUPP,ESUUPP                                                   
         MVC   TACYUEI,ESUUEI                                                   
         MVC   TACYUPIP,ESUUPIP                                                 
         MVC   TACYCTAX,ESUCTAX                                                 
         MVC   TACYCPP,ESUCPP                                                   
         MVC   TACYCEI,ESUCEI                                                   
         MVC   TACYCPIP,ESUCPIP                                                 
*                                                                               
UY85     MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACYELQ                                                   
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         LA    R5,ELEM             R5 = A(SKELETON YTD ELEMENT)                 
         XC    ELEM,ELEM                                                        
         MVI   TACYEL,TACYELQ                                                   
         MVI   TACYLEN,TACYLN3Q                                                 
*                                                                               
*UY90     LA    R3,ESULEN(R3)       BUMP TO NEXT WITHHOLDING ELEMENT            
UY90     SAM31                                                                  
         L     RF,AESUCUR          BUMP TO NEXT ESUTAB ENTRY                    
         LA    RF,ESULEN(RF)                                                    
         ST    RF,AESUCUR                                                       
         SAM24                                                                  
         BCT   R2,UY10             REPEAT UNTIL NO MORE WITHHOLD ELEMS          
*                                                                               
UY95     CLI   BYTE,0              IF DIDN'T PROCESS FD ESUTAB ENTRY            
         JNZ   UY100               (OCCURS FOR CORPS WITH CAN TAX WHLD)         
         LA    RF,=C'FD '                                                       
         CLI   RECNUM,EC                                                        
         JNE   *+8                                                              
         LA    RF,=C'EU '                                                       
         GOTO1 GETESU,DMCB,(RF)    GET IT NOW                                   
*        L     R3,0(R1)                                                         
*                                                                               
         SAM31                                                                  
         L     RF,0(R1)            GET ESUTAB ENTRY INTO BLOCK                  
         LA    R3,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
*                                                                               
         MVC   SRTYREX,ESUREXP     AND EXTRACT YTD REIMBURSED EXP.              
*                                                                               
UY100    L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         JE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
         MVC   TACDYREX,SRTYREX    SAVE YTD REIMBURSED EXP IN CD ELEM           
*                                                                               
         LA    RF,=C'FD '                                                       
         CLI   RECNUM,EC                                                        
         JNE   *+8                                                              
         LA    RF,=C'EU '                                                       
         GOTO1 GETESU,DMCB,(RF)    GET IT NOW                                   
*        L     R3,0(R1)                                                         
*                                                                               
         SAM31                                                                  
         L     RF,0(R1)                                                         
         LA    R3,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE TACX FROM THE CHECK RECORD            
         MVI   ELCODE,TACXELQ                                                   
         GOTO1 REMELEM                                                          
*                                                                               
         LA    R5,ELEM             R5 = A(SKELETON TACX ELEMENT)                
         USING TACXD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACXEL,TACXELQ                                                   
         MVI   TACXLEN,TACXLNQ2                                                 
         L     RF,SRTGST                                                        
         LCR   RF,RF                                                            
         ST    RF,TACXGST                                                       
*        MVC   TACXGST,SRTGST      GST                                          
         L     RF,SRTYGST                                                       
         LCR   RF,RF                                                            
         ST    RF,TACXGSTY                                                      
*        MVC   TACXGSTY,SRTYGST    YTD GST                                      
         MVC   TACXMDDY,ESUMDD     YTD MISC DEDUCTIONS                          
         MVC   TACXMTRY,ESUMTR     YTD MPTRF                                    
         MVC   TACXDTPY,ESUDTP     YTD DUE EMPLOYER                             
         MVC   TACXPRCY,ESUPRC     YTD PERMANENT CHARITIES                      
         MVC   TACXLIEY,ESULIE     YTD LIENS                                    
         MVC   TACXUNDY,ESUUND     YTD UNION DUES                               
         MVC   TACXTRSY,ESUTRS     YTD TRUST                                    
         MVC   TACXDRTY,ESUDRT     YTD DIRECT DEPOSIT                           
         MVC   TACXWRDY,ESUWRD     YTD WIRED                                    
         MVC   TACXPPAC,ESUPPA     YTD PAYROLL+ COMMISSION                      
         MVC   TACXDTPT,ESUDTPTR   YTD DUE EMPLOYER TAX REIM                    
         MVC   TACXDTPN,ESUDTPNR   YTD DUE EMPLOYER NON TAX REIM                
*                                                                               
         MVC   SRTYMDED,ESUMDD     YTD MISC DEDUCTIONS                          
         MVC   SRTYMPT,ESUMTR      YTD MPTRF                                    
         MVC   SRTYDUE,ESUDTP      YTD DUE EMPLOYER                             
         MVC   SRTYDUET,ESUDTPTR   YTD DUE EMPLOYER TAX REIM                    
         MVC   SRTYDUEN,ESUDTPNR   YTD DUE EMPLOYER NON TAX REIM                
         MVC   SRTYCHAR,ESUPRC     YTD PERMANENT CHARITIES                      
         MVC   SRTYLIEN,ESULIE     YTD LIENS                                    
         MVC   SRTYDUES,ESUUND     YTD UNION DUES                               
         MVC   SRTYTRST,ESUTRS     YTD TRUST                                    
         MVC   SRTYDIRT,ESUDRT     YTD DIRECT DEPOSIT                           
         MVC   SRTYWIRE,ESUWRD     YTD WIRED                                    
         MVC   SRTYPPLA,ESUPPA     YTD PAYROLL+ COMMISSION                      
         MVC   SRTYTXRE,ESUTXRE    YTD TAXABLE REIMBURSEMENTS                   
*                                                                               
         OC    SRTPST,SRTPST       ANY AOS PST?                                 
         JNZ   *+14                                                             
         OC    SRTYPST,SRTYPST                                                  
         JZ    UY200                                                            
         GOTO1 GETESU,DMCB,=C'CN ' GET CANADIAN                                 
*        L     R3,0(R1)                                                         
*                                                                               
         SAM31                                                                  
         L     RF,0(R1)            GET ESUTAB ENTRY INTO BLOCK                  
         LA    R3,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
         SAM24                                                                  
*                                                                               
         MVC   TACXPST,SRTPST      PST                                          
         MVC   TACXPSTY,ESUPST     YTD PST                                      
         MVC   SRTYPST,ESUPST      YTD PST                                      
*                                                                               
UY200    OC    TACXGST(TACXLNQ2-4),TACXGST                                      
         JZ    XIT                 DON'T ADD IF ALL AMOUNT ARE ZERO             
         MVC   AIO,ASRTCHK         ADD TACW ELEMENT TO CHECK RECORD             
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         J     XIT                                                              
                                                                                
         LTORG                                                                  
         DROP  R3,R4,R5                                                         
         EJECT                                                                  
*---------------------------------------------------------------------          
* TEST IF P+ CANADIAN                                                           
*---------------------------------------------------------------------          
TSTPCAN  NTR1  BASE=*,LABEL=*                                                   
         CLC   =C'P+',SRTEMP                                                    
         JNE   NO                                                               
         CLI   SRTW4TY,TAW4TYCA    CANADIAN?                                    
         JNE   NO                                                               
*                                                                               
         USING TAATD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAATELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         CLI   TAATLEN,TAATLN2Q                                                 
         JL    NO                                                               
         J     YES                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE UPDATES THE CHECK RECORD WITH THE QTD AMOUNTS FOR THIS           
* CHECK.  IT GETS THE QTD AMOUNTS FROM QTDTAB AND SAVES THEM IN QTD             
* ELEMENTS, WHICH IT ADDS.                                                      
*---------------------------------------------------------------------          
UPDQTD   NTR1  BASE=*,LABEL=*                                                   
         LA    R4,BLOCK            R4=A(TALIM INTERFACE BLOCK)                  
         USING TMDD,R4                                                          
         XCEF  (R4),'TMLNQ'        CLEAR INTERFACE TO TALIM                     
         MVC   TMEMP,SRTEMP        EMPLOYER                                     
         MVI   TMCURR,C'U'         IF CURRENCY IS US THEN TMCURR = U            
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TMCURR,C'C'         ELSE TMCURR = C                              
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   TMCURR,C'E'         ELSE TMCURR = E                              
         MVC   TMSSN,SRTSSN        S/S NUMBER                                   
         MVC   TMW4TYPE,SRTW4TY    W4 TYPE                                      
         MVC   TMUNIT,=C'HI '      TAXABLE STATE                                
         MVI   TMSTAT,TMSCHK       REQUESTING PAYROLL DATA                      
         GOTO1 DATCON,DMCB,(9,CHKDATEA),(0,TMEFDTE0)                            
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    UQ5                                                              
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(0,TMEFDTE0)                            
UQ5      ST    RC,TMRC             A(GEND)                                      
         MVC   TMNTEARN,SRTGRS                                                  
                                                                                
         GOTO1 =V(TALIM),DMCB,TMD  OFF TO TALIM                                 
                                                                                
         MVC   AIO,ASRTCHK         REMOVE EXISTING QTD WITHHOLDING ELS          
         MVI   ELCODE,TACQELQ                                                   
         TM    SRTGRS,X'80'        NEGATIVE CHECK?                              
         JZ    UQ7                                                              
                                                                                
         USING TACQD,RF                                                         
         GOTO1 GETL,DMCB,(3,=C'HI ')                                            
         JNE   *+14                                                             
         L     RF,TGELEM                                                        
         MVC   SVTSDI,TACQTSDI                                                  
         DROP  RF                                                               
                                                                                
UQ7      MVC   AIO,ASRTCHK         REMOVE EXISTING QTD WITHHOLDING ELS          
         MVI   ELCODE,TACQELQ                                                   
         GOTO1 REMELEM                                                          
         MVC   AIO,AIO1                                                         
         MVI   BYTE,0              CLEAR PROCESSED FD QTDTAB ENTRY FLAG         
                                                                                
         LA    R5,ELEM             R5 = A(SKELETON YTD ELEMENT)                 
         USING TACQD,R5                                                         
         XC    ELEM,ELEM                                                        
         MVI   TACQEL,TACQELQ                                                   
         MVI   TACQLEN,TACQLN2Q                                                 
                                                                                
         L     R3,AQTDTAB          R4 = A(FIRST WITHHOLDING ELEMENT)            
         USING BIND,R3                                                          
         L     R2,BININ            NUMBER OF ENTRIES IN QTDTAB                  
         LA    R3,BINTABLE         POINT TO FIRST ENTRY                         
         LTR   R2,R2               ANY ENTRIES?                                 
         JZ    UQ95                                                             
                                                                                
         USING QTDTABD,R3          R4 = A(QTDTAB ENTRY FOR THIS UNIT)           
UQ10     CLC   SRTSSN,QTDSSN       SAME SS#                                     
         JNE   UQ90                                                             
         CLC   =C'HI ',QTDUNIT     SAME TAX UNIT                                
         JNE   UQ90                                                             
         MVC   TACQUNIT,QTDUNIT                                                 
         MVC   TACQSDI,QTDSDI      SDI                                          
         MVC   TACQEARN,QTDEARN    EARNINGS                                     
                                                                                
         MVC   TACQTSDI,SRTGRS     SET TAXABLE SDI TO CHECK GROSS               
                                                                                
         TM    SRTGRS,X'80'        NEGATIVE CHECK?                              
         JZ    UQ15                                                             
         L     RF,SVTSDI                                                        
         LNR   RF,RF                                                            
         STCM  RF,15,TACQTSDI                                                   
         J     UQ30                                                             
                                                                                
UQ15     L     RF,TMBSDI           SDI MAX LIMIT                                
         ICM   RE,15,QTDEARN                                                    
         SR    RF,RE                                                            
         LTR   RF,RF                                                            
         BP    UQ20                                                             
         L     RE,SRTGRS                                                        
         AR    RE,RF                                                            
         STCM  RE,15,TACQTSDI      ACTUAL TAXABLE SDI FOR THIS CHECK            
                                                                                
         LTR   RE,RE                                                            
         BP    UQ20                                                             
         XC    TACQTSDI,TACQTSDI                                                
                                                                                
UQ20     OC    TACQAMTS,TACQAMTS   ANY AMOUNTS                                  
         JZ    UQ90                                                             
                                                                                
         TM    TACQTSDI,X'80'      VOID?                                        
         JZ    UQ30                                                             
         ICM   RF,15,TACQTSDI      CAN ONLY VOID UP TO QTD SDI LIMIT            
         LPR   RF,RF                                                            
         C     RF,TMBSDI                                                        
         BL    UQ30                                                             
         L     RF,TMBSDI                                                        
         LNR   RF,RF                                                            
         STCM  RF,15,TACQTSDI                                                   
                                                                                
UQ30     MVC   AIO,ASRTCHK         ADD TACQ ELEMENT TO CHECK RECORD             
         GOTO1 ADDELEM                                                          
                                                                                
UQ90     LA    R3,QTDLEN(R3)       BUMP TO NEXT WITHHOLDING ELEMENT             
         BCT   R2,UQ10             REPEAT UNTIL NO MORE WITHHOLD ELEMS          
UQ95     J     XIT                                                              
                                                                                
         DS    0F                                                               
SVTSDI   DS    F                                                                
                                                                                
         LTORG                                                                  
         DROP  R3,R5                                                            
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE GETS THE CURRENT ENTRY FROM PQTDTAB FOR THIS PERFORMER           
* AND USES IT AS INPUT TO TALIM ALONG WITH THE VALUES FROM THIS CHECK           
* IN ORDER TO UPDATE PQTDTAB AND TO ADD A NEW QTD ELEMENT (TAYE) TO             
* THE CHECK.  IT ALSO SETS SEVERAL QTD SORT FIELDS.                             
*---------------------------------------------------------------------          
CHKQTD   NTR1  BASE=*,LABEL=*                                                   
         L     RC,SAVERC           RESTORE RC                                   
                                                                                
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACWELQ      GET CHECK WITHHOLDING ELS.                   
         BRAS  RE,GETEL                                                         
         J     CQTD20                                                           
CQTD10   BRAS  RE,NEXTEL                                                        
CQTD20   JNE   CQTDX                                                            
                                                                                
         USING TACWD,R4            R4 = A(CHECK WITHHOLDING ELEMENT)            
         CLC   =C'HI ',TACWUNIT    ONLY HAWAII DOES QTD                         
         JNE   CQTD10                                                           
                                                                                
         LA    R5,WORK             R5=A(PQTDTAB ENTRY)                          
         USING QTDTABD,R5                                                       
         XC    0(QTDLEN,R5),0(R5)  BUILD PQTDTAB ENTRY                          
         MVC   QTDEMP,SRTEMP       EMPLOYER                                     
         MVC   QTDSSN,SRTSSN       SOCIAL SECURITY NUMBER                       
         MVC   QTDUNIT,TACWUNIT    TAX UNIT                                     
                                                                                
         MVC   QTDEARN,SRTEARN     EARNINGS                                     
         MVC   QTDSDI,TACWSDI      SAVE DISABILITY                              
                                                                                
         GOTO1 ADDBIN,DMCB,AQTDTAB,(R5)  ADD TO QTDTAB ENTRY                    
CQTDX    J     XIT                                                              
                                                                                
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
* READ THE W4 RECORD FOR THE CURRENT PERFORMER AND EXTRACT INFORMATION          
* FROM IT INTO THE SORT RECORD.  * NOTE * THE RECORD IS READ INTO AW4IO         
* FOR USE BY OTHER ROUTINES.                                                    
*---------------------------------------------------------------------          
EXTRW4   NTR1  BASE=*,LABEL=*                                                   
         XC    SRTW4S(SRTW4L),SRTW4S  CLEAR W4 RECORD VARIABLES                 
         NI    SRTBITS,ALL-SRTBW4           CHECKS FIRST BIT                    
*                                                                               
         MVC   AIO,AW4IO           READ W4 RECORD INTO AW4IO                    
         GOTO1 RECVAL,DMCB,TLW4CDQ,(X'A0',SRTSSN)                               
         JNE   ERRECW4                                                          
         MVC   AIO,AIO1                                                         
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'W',AW4IO),0,=C'W4'                               
*                                                                               
         L     R4,AW4IO            R4 = A(W4 DETAILS ELEMENT)                   
         MVI   ELCODE,TAW4ELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   ERREW4E                                                          
         USING TAW4D,R4                                                         
*                                                                               
         TM    TAW4STAT,TAW4SCKF   IF THIS PERF'S CHECKS COME FIRST             
         BZ    *+8                                                              
         OI    SRTBITS,SRTBW4      THEN SET BIT IN STATUS                       
*                                                                               
*********TM    TAW4STA2,TAW4SNFI   IF THIS PERF TAKES NO FICA                   
*********BZ    *+8                                                              
*********OI    SRTBITS,SRTBNFI     THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA2,TAW4SNDU   IF THIS PERF TAKES NO DUE COMP               
         BZ    *+8                                                              
         OI    SRTBITS,SRTBNDU     THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA2,TAW4SDD    IF THIS PERF HAS DIRECT DEPOSIT              
         BZ    *+8                                                              
         OI    SRTBITS,SRTBDD      THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA2,TAW4SWIR   IF THIS PERF HAS WIRE TRANSFER               
         BZ    *+8                                                              
         OI    SRTBITS,SRTBWIRE    THEN SET BIT IN STATUS                       
*                                                                               
         CLI   RECNUM,EC           IF THIS IS EURO CHECKS                       
         BNE   *+8                                                              
         OI    SRTBITS,SRTBWIRE    THEN SET WIRE TRANSFER BIT IN STATUS         
*                                                                               
         TM    TAW4STAT,TAW4STNY   IF YTD AMOUNTS SHOULD NOT PRINT              
         BZ    *+8                                                              
         OI    SRTBITS,SRTBNYTD    THEN SET BIT IN STATUS                       
*                                                                               
         TM    TAW4STA3,TAW4SNTX   IF NON-TAXABLE FOREIGNER                     
         BZ    *+8                                                              
         OI    SRTBITS2,SRTFONTX   THEN SET BIT IN STATUS                       
*                                                                               
         MVC   RECIPST,TAW4RECP    SAVE RECIPROCAL STATE FOR TAX CALC           
         MVC   SRTTYPE,TAW4TYPE    SAVE PERFORMER TYPE IN SORT RECORD           
         MVC   SRTFREQ,TAW4FREQ         FREQUENCY                               
         MVC   SRTW4CP,TAW4CP      SAVE CANADIAN PROVINCE                       
*                                                                               
*                                  SAVE NAME FOR SORT KEY                       
         MVC   LASTNAME(32),TAW4NAM2                                            
*                                                                               
         L     RF,AMASTD           SAVE MASTD VARIABLES                         
         USING MASTD,RF                                                         
         MVC   MIDNAME,MCSPACES                                                 
         CLI   TAW4LEN,TAW4LN2Q    DO WE HAVE MIDDLE INITIAL?                   
         BNE   *+10                                                             
         MVC   MIDNAME,TAW4MIDN                                                 
         DROP  RF                                                               
*                                                                               
         CLI   SRTTYPE,TAW4TYCO    IF PERFORMER IS NOT CORPORATION              
         BE    EW10                                                             
         CLI   SRTTYPE,TAW4TYTR    OR TRUSTEE                                   
         BE    EW10                                                             
         CLI   SRTTYPE,TAW4TYES    OR ESTATE                                    
         BE    EW10                                                             
         BRAS  RE,FITNAME                                                       
*                                                                               
         MVC   SRTNAME,BLOCK       SAVE IN SORT RECORD                          
         B     EW20                                                             
*                                                                               
EW10     MVC   SRTNAME,LASTNAME    ELSE SAVE WHOLE NAME IN SORT RECORD          
*                                                                               
EW20     L     R4,AW4IO            R4 = A(ADDRESS ELEMENT)                      
         MVI   ELCODE,TAA2ELQ      LOOK FOR NEW STYLE ADDRESS                   
         BRAS  RE,GETEL                                                         
         BE    EW30                                                             
         L     R4,AW4IO                                                         
         MVI   ELCODE,TAADELQ      ELSE LOOK FOR OLD STYLE ADDRESS              
         BRAS  RE,GETEL                                                         
*                                                                               
         BE    EW25                IF ERROR RETURNED THEN SET WARNING           
         BRAS  RE,WAREW4A              IN SORT RECORD                           
* NO-OP  L     R0,=A(CHECKL)                                                    
* NO-OP  GOTO1 DOTRACE,DMCB,(C'R',CHECKD),(R0),=C'NO W4 ADDRESS'                
         B     EW35                                                             
*                                                                               
         USING TAADD,R4                                                         
EW25     ZIC   RF,TAADLEN          SAVE ADDRESS IN SORT RECORD                  
         SH    RF,=H'4'                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTADD(0),TAADADD                                                
         AHI   RF,1                                                             
         GOTO1 =A(MIXDCASE),DMCB,SRTADD,(RF)                                    
         B     EW35                                                             
*                                                                               
         USING TAA2D,R4                                                         
EW30     CLI   TAA2LEN,TAA2LNQ        FOR OLD STYLE ADDRESSES                   
         BL    *+14                                                             
         CLC   TAA2CTRY,=C'US'        AND NEW STYLE US ADDRESSES                
         BNE   EW30A                                                            
         MVC   SRTADD(3*30),TAA2ADDR  SAVE FIRST 3 ADDRESS LINES                
         GOTO1 =A(MIXDCASE),DMCB,SRTADD,90                                      
         MVC   WORK(25),TAA2CITY      CITY                                      
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   SRTADD+90(39),WORK     SAVE IT                                   
         B     EW35                                                             
                                                                                
EW30A    MVC   SRTADD(2*30),TAA2ADDR  ELSE SAVE FIRST 2 ADDRESS LINES           
         GOTO1 =A(MIXDCASE),DMCB,SRTADD,60                                      
         MVC   WORK(25),TAA2CITY      CITY                                      
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAA2ST      STATE                                     
         MVC   WORK+29(10),TAA2ZIP    ZIP                                       
         GOTO1 SQUASHER,DMCB,WORK,39  SQUASH IT                                 
         MVC   SRTADD+60(39),WORK     SAVE IT                                   
         GOTO1 VALCTRY,DMCB,(X'80',TAA2CTRY)                                    
         BNE   EW35                                                             
         USING CTRYTABD,R1                                                      
         L     R1,TGACTRY                                                       
         ZIC   RE,CTRYDSP                                                       
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         IC    RE,CTRYLEN                                                       
         SHI   RE,CTRYDESC-CTRYTABD+1                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SRTADD+90(0),CTRYDESC                                            
         DROP  R1                                                               
*                                                                               
EW35     CLC   TAA2CTRY,=C'CA'                                                  
         BNE   *+10                                                             
         MVC   SRTW4CP,TAA2ST      SET CANADIAN PROVINCE                        
*                                                                               
         L     R4,AW4IO            R4 = A(PAYEE ELEMENT) IF EXISTS              
         MVI   ELCODE,TAPEELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EW40                                                             
         USING TAPED,R4                                                         
         CLC   TAPEACT,CHKDATE1    INSURE ACTIVE DATE NOT AFTER                 
         BH    EW40                CHECK DATE                                   
         OC    TAPEEXP,TAPEEXP     IF EXPIRY DATE DEFINED                       
         BZ    *+14                                                             
         CLC   CHKDATE1,TAPEEXP    INSURE CHECK DATE NOT AFTER EXPIRY           
         BH    EW40                                                             
*                                                                               
         MVC   SRTPNAM,TAPENAME    SAVE PAYEE NAME                              
*                                                                               
         OC    TAPEADD1(4*30),TAPEADD1                                          
         BZ    EW40                IF PAYEE ADDRESS PRESENT                     
         XC    SRTADD,SRTADD       THEN SAVE PAYEE ADDR OVER W4 ADDR            
                                                                                
         CLI   TAPELEN,TAPELNQ     HANDLE OLD STYLE PAYEE ADDRESS               
         BNL   EW35A                                                            
         MVC   SRTADD(4*30),TAPEADD1                                            
         B     EW40                                                             
                                                                                
EW35A    CLC   TAPECTRY,=C'US'     HANDLE NEW STYLE PAYEE ADDRESS               
         BNE   EW35B               FOR US PAYEES                                
         MVC   SRTADD(3*30),TAPEADD1                                            
         GOTO1 =A(MIXDCASE),DMCB,SRTADD,90                                      
         MVC   WORK(25),TAPECITY                                                
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAPEST                                                
         MVC   WORK+29(10),TAPEZIP                                              
         GOTO1 SQUASHER,DMCB,WORK,39                                            
         MVC   SRTADD+90(39),WORK                                               
         B     EW40                                                             
                                                                                
EW35B    MVC   SRTADD(2*30),TAPEADD1        HANDLE NEW STYLE PAYEE              
         GOTO1 =A(MIXDCASE),DMCB,SRTADD,60  ADDRESS FOR FOREIGN                 
         MVC   WORK(25),TAPECITY            PAYEES                              
         GOTO1 =A(MIXDCASE),DMCB,WORK,25                                        
         MVC   WORK+26(2),TAPEST                                                
         MVC   WORK+29(10),TAPEZIP                                              
         GOTO1 SQUASHER,DMCB,WORK,39                                            
         MVC   SRTADD+60(39),WORK                                               
         GOTO1 VALCTRY,DMCB,(X'80',TAPECTRY)                                    
         BNE   EW40                                                             
         USING CTRYTABD,R1                                                      
         L     R1,TGACTRY                                                       
         ZIC   RE,CTRYDSP                                                       
         LTR   RE,RE                                                            
         BNZ   *+8                                                              
         IC    RE,CTRYLEN                                                       
         SHI   RE,CTRYDESC-CTRYTABD+1                                           
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   SRTADD+90(0),CTRYDESC                                            
         DROP  R1                                                               
*                                                                               
EW40     L     R4,AW4IO            R4=A(W4 RECORD)                              
         MVI   ELCODE,TAWXELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EW42                                                             
         USING TAWXD,R4            R4=A(EXTRA W4 DETAILS ELEMENT)               
         MVC   SRTW4DOB,TAWXDOB    SAVE MINOR DATE OF BIRTH                     
         MVC   SRTW4PCT,TAWXPCT    SAVE DEDUCT PERCENTAGE                       
         MVC   SRTW4SSN,TAWXTSSN   SAVE TRUSTEE SSN                             
*                                                                               
EW42     MVC   AIO,AW4IO                                                        
         GOTO1 CHAROUT,DMCB,TANUELQ,0,TANUTGST  EXTRACT GST#                    
         MVC   AIO,AIO1                                                         
         MVC   SRTGST#,TGNAME                   SAVE IN SORT RECORD             
*                                                                               
         L     R4,AW4IO            R4 = A(FIRST W4 WITHHOLDING ELEMENT)         
         MVI   ELCODE,TAWHELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
EW44     BRAS  RE,NEXTEL                                                        
         BNE   EWX                                                              
*                                                                               
         USING TAWHD,R4                                                         
         OC    TAWHEMP,TAWHEMP     IF NO EMPLOYER DEFINED                       
         BNZ   *+10                                                             
         MVC   TAWHEMP,TGTPEMP     SET TO DEFAULT EMPLOYER                      
*                                                                               
         CLC   TAWHEMP,TGTPEMP     ONLY USE TALENT PARTNERS FOR NOW             
         BNE   EW44                ** NOT SUPPORTING MULT. RULES YET **         
*                                                                               
         CLC   TAWHUNIT,=C'FD '    SKIP IF THIS IS FEDERAL                      
         BE    EW44                                                             
*                                                                               
         CLI   TAWHUNIT+2,C' '     IF THIS IS STATE                             
         BNE   *+14                                                             
         MVC   SRTSOR,TAWHUNIT     SAVE STATE OF RESIDENCE                      
         B     EW44                                                             
*                                                                               
         MVC   SRTCOR,TAWHUNIT     ELSE SAVE CITY OF RESIDENCE                  
         B     EW44                                                             
*                                                                               
EWX      XIT1                                                                   
*                                                                               
*        GETEL2 R4,DATADISP,ELCODE                                              
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE LOOKS UP THE FIRST TLCKY POINTER ON THE FILE FOR CHECKS          
* WHICH ARE GOING TO BE BACK DATED TO 12/31 OF LAST YEAR.  THIS NUMBER          
* IS ADDED TO THE CURRENT SEQUENCE NUMBER TO GUARANTEE THAT THESE               
* POINTERS SORT IN THE CORRECT ORDER.                                           
*---------------------------------------------------------------------          
         USING TACDD,R4            R4 = A(CHECK DETAILS ELEMENT)                
GETSEQ   NTR1  BASE=*,LABEL=*                                                   
         LA    R3,KEY              BUILD KEY FOR TLCKY POINTER                  
         USING TLCKPD,R3                                                        
         XC    TLCKPKEY,TLCKPKEY                                                
         MVI   TLCKPCD,TLCKYCDQ    RECORD CODE                                  
         MVC   TLCKYEAR,CHKDTEAL   LAST YEAR                                    
         MVC   TLCKYTXU,=C'FD '    TAX UNIT                                     
         MVI   TLCKYCUR,C'U'       IF CURRENCY IS US THEN TLCKYCUR = U          
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TLCKYCUR,C'C'       ELSE TLCKYCUR = C                            
         CLI   RECNUM,EC                                                        
         BNE   *+14                                                             
         MVI   TLCKYCUR,C'E'       ELSE TLCKYCUR = E                            
         MVC   TLCKYTXU,=C'EU '    TAX UNIT                                     
*                                                                               
         MVC   TLCKYEMP,SRTEMP     EMPLOYER                                     
         MVC   TLCKYSSN,SRTSSN     S/S NUMBER                                   
         MVC   TLCKYDTE,CHKDATEL   CHECK DATE                                   
         XC    TLCKYDTE,=X'FFFFFFFFFFFF'    COMPLEMENTED                        
*                                                                               
         MVC   FILENAME,=CL8'CHKDIR'   SET TO READ FROM CHKDIR                  
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
*                                                                               
         CLC   TLCKPKEY(TLCKYSEQ-TLCKPD),KEYSAVE  IF FOUND A GOOD KEY           
         BNE   GSEQX                                                            
         ICM   R1,15,TLCKYSEQ      ITS SEQUENCE NUMBER                          
         LCR   R1,R1               (IT'S COMPLEMENTED IN KEY)                   
         AH    R1,TACDSEQ+2        + CURRENT SEQUENCE NUMBER                    
         ST    R1,TACDSEQ2         = SPECIAL SEQUENCE NUMBER                    
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'6',TLCKPKEY),38,=C'TLCKY KEY'                    
*                                                                               
GSEQX    XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE DETERMINES IF THE CURRENT CHECK IS A LIEN PAYEE CHECK            
* GENERATED BY A PREVIOUS CHECK RUN.  IF IT IS, THEN THE RECORD IS              
* DELETED AND THE ROUTINE RETURNS 'YES'.  OTHERWISE THE ROUTINE RETURNS         
* 'NO'.                                                                         
*---------------------------------------------------------------------          
DELLIEN  NTR1  BASE=*,LABEL=*                                                   
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSLIN   IF CHECK WAS NOT GENERATED FOR LIEN          
         BZ    DELLIENN                PAYEE THEN RETURN 'NO'                   
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         L     R4,ASRTCHK          DELETE RECORD AND WRITE BACK                 
         USING TLCKD,R4                                                         
         OI    TLCKSTAT,X'80'                                                   
         MVC   FILENAME,=CL8'CHKFIL'                                            
         GOTO1 PUTREC                                                           
         MVC   AIO,AIO1                                                         
         GOTO1 DOTRACE,DMCB,(C'L',ASRTCHK),0,=C'DELETED LIEN CHK'               
*                                                                               
         LA    R3,KEY              DELETE KEY AND WRITE BACK                    
         USING TLDRD,R3                                                         
         MVC   TLDRREC,SVCKEY      INSURE WE HAVE CHECK RECORD KEY              
         OI    TLDRSTAT,X'80'                                                   
         MVC   FILENAME,=CL8'CHKDIR'                                            
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',KEY),38,=C'DELETED LIEN KEY'                  
         GOTO1 ADDPTRS             REMOVE OLD PASSIVE POINTERS                  
         XC    FILENAME,FILENAME   RESET FILE NAME                              
DELLIENY SR    RC,RC               RETURN 'YES'                                 
DELLIENN LTR   RC,RC               RETURN 'NO'                                  
         XIT1                                                                   
         DROP  R3,R4                                                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* THIS ROUTINE DETERMINES IF THE CURRENT CHECK IS A W4 TRUSTEE CHECK            
* GENERATED BY A PREVIOUS CHECK RUN.  IF IT IS, THEN THE RECORD IS              
* DELETED AND THE ROUTINE RETURNS 'YES'.  OTHERWISE THE ROUTINE RETURNS         
* 'NO'.                                                                         
*---------------------------------------------------------------------          
DELTRUST NTR1  BASE=*,LABEL=*                                                   
         L     R4,ASRTCHK          R4 = A(CHECK DETAILS ELEMENT)                
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACDD,R4                                                         
*                                                                               
         TM    TACDSTAT,TACDSTRS   IF CHECK WAS NOT GENERATED FOR W4            
         BZ    DELTRSTN            TRUSTEE THEN RETURN 'NO'                     
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         GOTO1 ASAVPTRS,DMCB,ASPBLK  SAVE PASSIVE POINTERS IN ASPBLK            
*                                                                               
         L     R4,ASRTCHK          DELETE RECORD AND WRITE BACK                 
         USING TLCKD,R4                                                         
         OI    TLCKSTAT,X'80'                                                   
         MVC   FILENAME,=CL8'CHKFIL'                                            
         GOTO1 PUTREC                                                           
         MVC   AIO,AIO1                                                         
         GOTO1 DOTRACE,DMCB,(C'L',ASRTCHK),0,=C'DELETED TRUSTEE CHK'            
*                                                                               
         LA    R3,KEY              DELETE KEY AND WRITE BACK                    
         USING TLDRD,R3                                                         
         MVC   TLDRREC,SVCKEY      INSURE WE HAVE CHECK RECORD KEY              
         OI    TLDRSTAT,X'80'                                                   
         MVC   FILENAME,=CL8'CHKDIR'                                            
         GOTO1 WRITE                                                            
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'L',KEY),38,=C'DELETED TRUSTEE KEY'               
         GOTO1 ADDPTRS             REMOVE OLD PASSIVE POINTERS                  
         XC    FILENAME,FILENAME   RESET FILE NAME                              
DELTRSTY SR    RC,RC               RETURN 'YES'                                 
DELTRSTN LTR   RC,RC               RETURN 'NO'                                  
         XIT1                                                                   
         DROP  R3,R4                                                            
         EJECT                                                                  
*---------------------------------------------------------------------          
*        MIXED CASE TEXT                                                        
*              P1 = ADDRESS OF DATA                                             
*              P2 = LENGTH                                                      
*---------------------------------------------------------------------          
MIXDCASE NTR1  BASE=*,LABEL=*                                                   
         L     RF,0(R1)            ADDRESS OF TEXT                              
         L     R2,4(R1)            LENGTH OF TEXT                               
         MVI   BYTE,C'U'           UPPERCASE                                    
*                                                                               
MCASE10  CLI   BYTE,C'U'           UPPERCASE?                                   
         BNE   MCASE20             NO                                           
         TM    0(R1),X'80'         DON'T LOWERCASE 2 CHAR WORDS                 
         BZ    MCASE15                                                          
         CH    R2,=H'2'            CHECK ONLY IF MORE THAN 2 CHARS LEFT         
         BNH   MCASE15                                                          
         CLI   2(RF),C'a'          SWITCH TO UPPER CASE IF NOT ALPHA            
         BL    MCASE30                                                          
         CLI   0(RF),C'Z'                                                       
         BH    MCASE30                                                          
*                                                                               
MCASE15  CLI   0(RF),C'a'          SWITCH TO UPPER CASE IF NOT ALPHA            
         BL    MCASE90                                                          
         CLI   0(RF),C'Z'                                                       
         BH    MCASE90                                                          
         MVI   BYTE,C'L'           SWITCH TO LOWERCASE MODE                     
         B     MCASE90             AND BUMP TO NEXT CHAR                        
*                                                                               
MCASE20  CLI   0(RF),C'a'          LOWER THAN LOWER A, SWTICH TO UPPER          
         BL    MCASE30                                                          
         CLI   0(RF),C'A'          HAS TO BE UPPERCASE A-Z                      
         BL    MCASE90                                                          
         CLI   0(RF),C'Z'          HIGHER THAN UPPER Z, SWITCH TO UPPER         
         BH    MCASE30                                                          
         NI    0(RF),X'BF'         REMOVE UPPERCASE BIT                         
         B     MCASE90             AND BUMP TO NEXT CHAR                        
*                                                                               
MCASE30  MVI   BYTE,C'U'           SWITCH TO UPPERCASE MODE                     
*                                                                               
MCASE90  LA    RF,1(RF)            BUMP TO NEXT CHAR                            
         BCT   R2,MCASE10          UNTIL ALL CHARS ARE DONE                     
*                                                                               
MCASEX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
*        COMMIT CALL TO DATAMGR                                                 
*---------------------------------------------------------------------          
COMMIT   NTR1  BASE=*,LABEL=*                                                   
         GOTO1 DATAMGR,DMCB,=C'COMMIT'  COMMIT                                  
COMMITX  XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS ROUTINE TRACES THE PERFORMER'S ESUTAB ENTRIES FOR EACH UNIT              
* THE PERFORMER USES FOR THE EMPLOYER.  IT NOW ALSO TRACES PYTDTAB              
* ENTRY FOR PERFORMER/EMPLOYER.                                                 
*----------------------------------------------------------------------         
TRACEESU NTR1  BASE=*,LABEL=*                                                   
         J     TEX                                                              
         GOTO1 GETPYTD             READ ENTRY FOR THIS EMPLOYER/SSN             
         L     R5,DMCB             R5 = A(RECORD)                               
*                                                                               
*                                  TRACE IF USER WANTS HISTORY TRACE            
         GOTO1 DOTRACE,DMCB,(C'H',(R5)),PYTDLEN,=C'PYTDTAB'                     
*                                                                               
         LA    R5,BLOCK            READ HIGH FOR FIRST UNIT FOR THIS            
         USING ESUTABD,R5              EMPLOYER/SSN                             
         XC    0(ESULEN,R5),0(R5)                                               
         MVC   ESUEMP,SRTEMP                                                    
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   TGTHREE,ESUEMP                                                   
         MVC   ESUSSN,SRTSSN                                                    
*                                                                               
         L     R2,AESUTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(2,(R5)),BINTABLE                                   
*                                                                               
         L     R5,0(R1)            R5 = A(FIRST UNIT)                           
*                                                                               
TE10     CLC   ESUEMP,TGTHREE      WHILE SAME EMPLOYER                          
         BNE   TE50                                                             
         CLC   ESUSSN,SRTSSN       AND SAME PERFORMER                           
         BNE   TE50                                                             
*                                                                               
         LA    R5,ESULEN(R5)       BUMP R2 TO NEXT ESUTAB ENTRY                 
         B     TE10                LOOP BACK                                    
*                                                                               
TE50     L     R3,0(R1)            R3 = A(FIRST UNIT)                           
         SR    R5,R3               R5 = LENGTH OF PERFORMER'S ENTRIES           
*                                                                               
*                                  TRACE IF USER WANTS HISTORY TRACE            
         GOTO1 DOTRACE,DMCB,(C'H',(R3)),(R5),=C'ESUTAB'                         
         SAM24                                                                  
*                                                                               
TEX      XIT1                                                                   
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS ROUTINE TRACES THE PERFORMER'S QTDTAB ENTRIES FOR EACH UNIT              
* THE PERFORMER USES FOR THE EMPLOYER.  IT NOW ALSO TRACES PYTDTAB              
* ENTRY FOR PERFORMER/EMPLOYER.                                                 
*----------------------------------------------------------------------         
TRACEQTD NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LA    R5,BLOCK            READ HIGH FOR FIRST UNIT FOR THIS            
         USING QTDTABD,R5              EMPLOYER/SSN                             
         XC    0(QTDLEN,R5),0(R5)                                               
         MVC   QTDEMP,SRTEMP                                                    
         MVC   TGTHREE,QTDEMP                                                   
         MVC   QTDSSN,SRTSSN                                                    
*                                                                               
         L     R2,AQTDTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(2,(R5)),BINTABLE                                   
*                                                                               
         L     R5,0(R1)            R5 = A(FIRST UNIT)                           
*                                                                               
TQ10     CLC   QTDEMP,TGTHREE      WHILE SAME EMPLOYER                          
         BNE   TQ50                                                             
         CLC   QTDSSN,SRTSSN       AND SAME PERFORMER                           
         BNE   TQ50                                                             
*                                                                               
         LA    R5,QTDLEN(R5)       BUMP R2 TO NEXT QTDTAB ENTRY                 
         B     TQ10                LOOP BACK                                    
*                                                                               
TQ50     L     R3,0(R1)            R3 = A(FIRST UNIT)                           
         SR    R5,R3               R5 = LENGTH OF PERFORMER'S ENTRIES           
*                                                                               
*                                  TRACE IF USER WANTS HISTORY TRACE            
         GOTO1 DOTRACE,DMCB,(C'H',(R3)),(R5),=C'QTDTAB'                         
*                                                                               
TQX      XIT1                                                                   
         EJECT                                                                  
*----------------------------------------------------------------------         
* EXTRACTS THE ACTRA EXPIRY DATE FROM THE COMMERCIAL RECORD                     
* CANADIAN CHECKS ONLY                                                          
*----------------------------------------------------------------------         
EXTRAEXP NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,CN           IF CANADIAN CHECKS                           
         BNE   EAEXPX                                                           
*                                                                               
         MVC   AIO,AIO1            GET COMMERCIAL RECORD                        
         GOTO1 RECVAL,DMCB,TLCOCCDQ,(X'A0',SRTCOM)                              
         BNE   EAEXPX                                                           
*                                                                               
         L     R4,AIO                                                           
         USING TACOD,R4                                                         
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EAEXPX                                                           
         CLI   TACOLEN,TACOLNQ2    OLD STYLE ELEMENT                            
         BL    EAEXPX              LEAVE                                        
*                                                                               
         OC    TACOAEXP,TACOAEXP        USE ACTRA EXPIRY DATE                   
         BZ    EAEXPX                   INSTEAD OF REGULAR EXP DATE             
         MVC   SRTEXP,TACOAEXP          IF IT EXISTS                            
         DROP  R4                                                               
*                                                                               
EAEXPX   XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
* EXTRACT MISCELLANEOUS INFORMATION FROM THE INVOICE RECORD INTO THE            
* SORT RECORD.                                                                  
*----------------------------------------------------------------------         
EXTRIMSC NTR1  BASE=*,LABEL=*                                                   
         ZAP   SRTCCVT,CCVTRATE    SET CURRENT CANADIAN $ CONV RATE             
*                                                                               
         CLI   RECNUM,CN           PROTECT AGAINST BAD DATA - ONLY              
         BE    EIM00               NEED FOR CANADIAN DOLLAR CHECKS              
         CLI   RECNUM,EC           AND EURO CHECKS                              
         BNE   EIM10                                                            
*                                                                               
EIM00    L     R4,AINVIO           R4 = A(BILLING DETAILS ELEMENT)              
         MVI   ELCODE,TABDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   EIM10                                                            
         USING TABDD,R4                                                         
         OC    TABDCCVT,TABDCCVT   IF DEFINED                                   
         BZ    EIM10                                                            
         ZAP   SRTCCVT,TABDCCVT    SET CANADIAN $ CONV RATE FROM BILL           
*                                                                               
EIM10    XC    SRTINVC,SRTINVC     PRE-CLEAR INVOICE COMMENTS                   
*                                                                               
         MVI   ELCODE,TACMELQ      SEARCH FOR COMMENT TYPE GENERAL              
         MVC   AIO,AINVIO                                                       
         GOTO1 GETL,DMCB,(1,=C'G')                                              
         MVC   AIO,AIO1                                                         
         BNE   EIMX                DONE IF NONE FOUND                           
*                                                                               
         L     R4,TGELEM           R4 = A(COMMENT ELEMENT)                      
         USING TACMD,R4                                                         
*                                                                               
         ZIC   RF,TACMLEN          MOVE INVOICE COMMENT TO SORT RECORD          
         SH    RF,=H'4'                                                         
         CH    RF,=AL2(L'SRTINVC-1) PROTECT AGAINST BIG CMTS IN OLD INV         
         BNH   *+8                                                              
         LH    RF,=AL2(L'SRTINVC-1)                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   SRTINVC(0),TACMCOMM                                              
*                                                                               
EIMX     XIT1                                                                   
         LTORG                                                                  
         DROP  R4                                                               
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS ROUTINE IS FOR NEW YORK TRUST                                            
*        DIFFERENT DATES BASED ON DIFFERENT CONDITIONS                          
*----------------------------------------------------------------------         
NYTDATES NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         CLC   SRTUN,=C'AFM'       AFM UNION                                    
         BNE   NYTD200                                                          
         MVC   WORK(3),SRTMUS      DEFAULT TO MUSIC DATE                        
         CLC   SRTMUS,=X'A40327'   BUT IF EARLIER THAN MAR28/2004               
         BH    NYTX                                                             
         CLC   SRTUSE,=C'MUS'                                                   
         BNE   NYTX                                                             
         OC    SRTDUB,SRTDUB       IF THERE IS A DUB DATE                       
         BZ    NYTX                                                             
         MVC   WORK(3),SRTDUB      USE DUB DATE                                 
         B     NYTX                                                             
*                                                                               
NYTD200  DS    0H                  NOT AFM                                      
         MVC   WORK(3),SRTFRST     USE CAST'S FIRST SERVICES DATE               
         CLC   SRTFRST,=X'A40327'  BUT IF EARLIER THAN MAR28/2004               
         BH    NYTX                                                             
*                                                                               
         CLC   SRTCFCY,=X'A40328'  BUT IF EARLIER THAN MAR28/2004               
         BL    NYTD250                                                          
         MVC   WORK(3),SRTCFCY     USE OVERRIDE OF COMML FIRST CYCLE            
         B     NYTX                                                             
*                                                                               
NYTD250  MVC   WORK(3),SRTFCYC     THEN USE COMML FIRST FIXED CYCLE             
*                                                                               
NYTX     XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*        ROUTINE SETS WORK WITH THE SHOOT DATE                        *         
*        ON ENTRY ... R8=A(WEB RESPONSE DETAILS AREA)                 *         
***********************************************************************         
                                                                                
SETSHOOT NTR1                                                                   
         USING TLCAD,R4                                                         
         L     R4,ASRTCHK                                                       
         LA    R4,TLCAELEM                                                      
         DROP  R4                                                               
                                                                                
         USING TACAD,R4                                                         
SSHOOT10 CLI   0(R4),0                                                          
         JE    SSHOOT30                                                         
         CLI   0(R4),TACAELQ                                                    
         JE    SSHOOT20                                                         
         ZIC   RE,1(R4)                                                         
         AR    R4,RE                                                            
         J     SSHOOT10                                                         
                                                                                
SSHOOT20 MVC   WORK(L'TACASDTE),TACASDTE                                        
         OC    TACASDTE,TACASDTE                                                
         JNZ   XIT                                                              
         DROP  R4                                                               
                                                                                
SSHOOT30 MVC   WORK(L'TACASDTE),SRTSHOOT                                        
         J     XIT                                                              
                                                                                
***********************************************************************         
*        CONSTANTS AND LITERALS                                       *         
***********************************************************************         
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
* THIS CHECKS IF PERFORMER HAS EARNED MORE THAN $5000 LIFETIME                  
* FOR MINORS ONLY (CANADIAN TRUST)                                              
*----------------------------------------------------------------------         
ERN5000  NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,AIO2            BUILD KEY TO READ W4                         
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING TLW4D,R4                                                         
         MVI   TLW4CD,TLW4CDQ                                                   
         MVC   TLW4SSN,SRTSSN                                                   
         GOTO1 HIGH                                                             
         GOTO1 GETREC                                                           
*                                                                               
         L     R4,AIO                                                           
         USING TAWXD,R4                                                         
         MVI   ELCODE,TAWXELQ        GET EXTRA DETAILS ELEMENT                  
         BRAS  RE,GETEL                                                         
         BNE   ERNXN                                                            
         CLC   TAWXMERN,=X'0007A120' CHECK EARNED AT LEAST $5000                
         BL    ERNXN                 NO, CC=NO                                  
*                                                                               
         MVC   AIO,AIO1                                                         
ERNXY    CR    RE,RE                                                            
         B     *+6                                                              
ERNXN    LTR   RB,RB                                                            
         XIT1                      AND RETURN SUCCESS                           
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO DETERMINE IF THE CHECK IS TO BE PULLED                
         SPACE 1                                                                
PULLTST  NTR1  BASE=*,LABEL=*                                                   
         MVI   PULLCHK,C'N'        INITIALIZE PULL INDICATOR TO NO              
         SPACE 1                                                                
         USING TACND,R4                                                         
         MVI   ELCODE,TACNELQ      IF CHECK/PULL CANCEL ELEMENT                 
         BRAS  RE,GETEL            EXISTS, THEN DON'T PULL IT                   
         BNE   PTST10                                                           
         CLI   TACNTYPE,TACNTYPP                                                
         BE    PTSTX                                                            
         DROP  R4                                                               
         SPACE 1                                                                
PTST10   L     R4,ASRTCHK                                                       
         MVI   ELCODE,TAKLELQ      ELSE, IF CHECK/PULL ELEMENT                  
         BRAS  RE,GETEL            EXISTS, THEN PULL IT                         
         BNE   PTSTX                                                            
         MVI   PULLCHK,C'Y'                                                     
         SPACE 1                                                                
PTSTX    XIT1                                                                   
         EJECT                                                                  
ERRSYSR  NTR1  BASE=*,LABEL=*                                                   
         MVI   BLOCK,C' '                                                       
         MVC   BLOCK+1(131),BLOCK                                               
         MVC   BLOCK(34),=C'** BAD OR MISSING SYSTEM RECORD **'                 
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
ERRNRER  NTR1  BASE=*,LABEL=*                                                   
         MVI   BLOCK,C' '                                                       
         MVC   BLOCK+1(131),BLOCK                                               
         MVC   BLOCK(40),=C'** RERUN TRIED WITHOUT RERUN=YES CARD **'           
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO ADJUST CLASS A PAYMENTS FOR PAX                       
         SPACE                                                                  
PAX      NTR1  BASE=*,LABEL=*                                                   
         CLC   SRTUSE,=C'CLA'      IF USE IS CLASS A                            
         BNE   PAXXIT                                                           
         CLI   SRTYEAR,C'8'        AND CAST YEAR IS 2000                        
         BE    PAXXIT              OR GREATER                                   
         CLI   SRTYEAR,C'9'                                                     
         BE    PAXXIT                                                           
         L     R4,ASRTCHK          AND USAGE HISTORY IS NOT OVERRIDDEN          
         MVI   ELCODE,TAPDELQ      ENTER THIS ROUTINE                           
         BRAS  RE,GETEL                                                         
         BNE   PAXXIT                                                           
         USING TAPDD,R4                                                         
         TM    TAPDOPT2,TAPDOPTU                                                
         BO    PAXXIT                                                           
*                                                                               
         MVI   CSTAT,0                                                          
         L     R4,ASRTCHK          R4 = A(CAST DETAILS ELEMENT)                 
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   PAXXIT                                                           
         USING TACAD,R4                                                         
         MVC   CSTAT,TACASTAT      SAVE CAST STATUS                             
*                                                                               
         MVC   AIO,AIO2            BUILD KEY TO READ INVOICE                    
         LA    R4,KEY              RECORD                                       
         XC    KEY,KEY                                                          
         USING TLIND,R4                                                         
         MVI   TLINCD,TLINCDQ                                                   
         MVC   TLINAGY,SRTAGY                                                   
         MVC   TLININV,SRTINV                                                   
         XC    TLININV,=6X'FF'                                                  
         GOTO1 HIGH                                                             
         CLC   TLINKEY(TLININV+L'TLININV-TLIND),KEYSAVE                         
         BNE   PAXXIT                                                           
         GOTO1 GETREC              GET INVOICE RECORD                           
*                                                                               
         SR    R1,R1               CLEAR PAX COUNTER                            
*                                                                               
         L     R4,AIO                                                           
         USING TANPD,R4                                                         
         MVI   ELCODE,TANPELQ      GET NETWORK/CLASS A PRO DET ELEMS            
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
PAX10    BRAS  RE,NEXTEL                                                        
         BNE   PAX30                                                            
         CLI   TANPNWK,C'X'        REJECT ALL NON-PAX PROGRAMS                  
         BNE   PAX10                                                            
*                                                                               
         CLI   TANPLFT,C'Y'        IF LIFT PROGRAM                              
         BNE   PAX20                                                            
         TM    CSTAT,TACASTLF      ONLY COUNT IF PERFORMER IS                   
         BZ    PAX10               ON LIFT                                      
         B     PAX25                                                            
*                                                                               
PAX20    TM    CSTAT,TACASTLO      IF NOT A LIFT PROGRAM                        
         BO    PAX10               DO NOT COUNT IF PERF ONLY ON LIFT            
*                                                                               
PAX25    AHI   R1,1                INCREMENT PAX COUNTER                        
         B     PAX10                                                            
*                                                                               
PAX30    LNR   R1,R1               SUBTRACT NUMBER OF PAX PROGRAMS              
         AH    R1,SRTUSES          FROM TOTAL NUMBER OF USES                    
         STH   R1,SRTUSES                                                       
*                                                                               
         XC    HALF,HALF           IF PAYMENT IS TO COMMERCIAL                  
         CLI   SRTVERS,0           WITH VERSIONS                                
         BE    PAX35                                                            
         LA    R4,KEY              BUILD PASSIVE KEY FOR CAST                   
         USING TLCAPD,R4                                                        
         XC    TLCAPKEY,TLCAPKEY                                                
         MVI   TLCAPCD,TLCACCDQ    RECORD CODE                                  
         MVC   TLCACSSN,SRTSSN     SOCIAL SECURITY NUMBER                       
         MVC   TLCACCOM,TGCOM      COMMERCIAL                                   
         MVC   TLCACCAT,SRTCAT     CATEGORY                                     
         GOTO1 HIGH                                                             
         CLC   KEY(TLCACCAT+L'TLCACCAT-TLCAPCD),KEYSAVE                         
         BNE   PAXXIT                                                           
         GOTO1 GETREC              GET THE CAST RECORD                          
         SPACE                                                                  
         L     R4,AIO                                                           
         USING TLCAD,R4                                                         
         MVC   HALF,TLCASEQ        SAVE THE SEQUENCE NUMBER                     
         SPACE                                                                  
PAX35    LA    R4,KEY              NOW GET USAGE HISTORY RECORD                 
         XC    KEY,KEY                                                          
         USING TLUHD,R4                                                         
         MVI   TLUHCD,TLUHCDQ      RECORD CODE                                  
         MVC   TLUHCOM,TGCOM       INTERNAL COMMERCIAL NUMBER                   
         MVC   TLUHCSEQ,HALF       CAST SEQUENCE NUMBER                         
         MVC   TLUHUSE,TGUSCDE     USE CODE                                     
         MVC   TLUHINV,SRTINV      COMPLIMENTED INVOICE NUMBER                  
         XC    TLUHINV,=6X'FF'                                                  
         GOTO1 HIGH                                                             
         CLC   TLUHKEY(TLUHINV+L'TLUHINV-TLUHD),KEYSAVE                         
         BNE   PAXXIT                                                           
         GOTO1 GETREC                                                           
         L     R4,AIO                                                           
         USING TAUHD,R4                                                         
         MVI   ELCODE,TAUHELQ      GET USAGE HISTORY ELEMENT                    
         BRAS  RE,GETEL                                                         
         BNE   PAXXIT                                                           
         LH    RE,TAUHUSN          DECREMENT LAST USE NUMBER                    
         SH    RE,TAUHUSNP         BY # OF PAX PAYMENTS EXLUD FROM CLA          
         SH    RE,SRTUSES          BY # OF USES ON THIS PAYMENT                 
*                                                                               
         CLI   SRTVERS,0           IF PAYING A VERSION                          
         BNE   PAX50               SKIP AHEAD                                   
*                                                                               
         TM    CSTAT,TACASTLO                                                   
         BO    PAX40               IF PERFORMER NOT ON LIFT                     
         TM    CSTAT,TACASTLF      SUBTRACT NUMBER OF LIFT USES                 
         BO    PAX50                                                            
         LH    RE,TAUHUSN                                                       
         SH    RE,TAUHUSNL                                                      
         SH    RE,TAUHUSNP                                                      
         AH    RE,TAUHLUSP                                                      
         SH    RE,SRTUSES                                                       
         B     PAX50                                                            
*                                  IF PERFOMER IS ONLY ON LIFT                  
PAX40    LH    RE,TAUHUSNL         DECREMENT LAST LIFT USE NUMBER               
         SH    RE,TAUHLUSP         BY # LIFT PAX PAYS EX FROM CLA               
         SH    RE,SRTUSES          BY # OF USES ON THIS PAYMENT                 
*                                                                               
PAX50    AHI   RE,1                ADJUST FOR STARTING USE NUMBER               
         STH   RE,SRTSTUS          AND STORE STARTING USE IN SRTSTUS            
PAXXIT   MVC   AIO,AIO1                                                         
         XIT1                                                                   
         SPACE                                                                  
CSTAT    DS    XL1                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*              ROUTINE TO CALCULATE NUMBER OF USES ON ITN USE                   
         SPACE                                                                  
ITN      NTR1  BASE=*,LABEL=*                                                   
         CLC   SRTUSE,=C'ITN'      IF USE IS CLASS A                            
         BNE   ITNX                                                             
         SPACE                                                                  
         MVC   AIO,AIO2                                                         
         SPACE                                                                  
         LA    R4,KEY                                                           
         XC    KEY,KEY             BUILD KEY TO READ INVOICE                    
         USING TLIND,R4                                                         
         MVI   TLINCD,TLINCDQ                                                   
         MVC   TLINAGY,SRTAGY                                                   
         MVC   TLININV,SRTINV                                                   
         XC    TLININV,=6X'FF'                                                  
         GOTO1 HIGH                                                             
         CLC   TLINKEY(TLININV+L'TLININV-TLIND),KEYSAVE                         
         BE    *+6                                                              
         DC    H'00'                                                            
         GOTO1 GETREC                                                           
         L     R4,AIO              GET INVOICE                                  
         MVI   ELCODE,TAUHELQ                                                   
         BRAS  RE,GETEL            IF USAGE HISTORY ELEMENT                     
         BNE   ITN01               IS PRESENT                                   
         USING TAUHD,R4                                                         
         MVC   SRTSTUS,TAUHIFUS    JUST USE THE MANUALLY INPUTTED               
         MVC   SRTUSES,TAUHINUS    START USE AND # OF USES                      
         B     ITN50                                                            
         SPACE                                                                  
ITN01    XC    VERS,VERS                                                        
         SPACE                                                                  
         LA    R4,KEY              BUILD PASSIVE KEY FOR CAST                   
         USING TLCAPD,R4                                                        
         XC    TLCAPKEY,TLCAPKEY                                                
         MVI   TLCAPCD,TLCACCDQ    RECORD CODE                                  
         MVC   TLCACSSN,SRTSSN     SOCIAL SECURITY NUMBER                       
         MVC   TLCACCOM,TGCOM      COMMERCIAL                                   
         MVC   TLCACCAT,SRTCAT     CATEGORY                                     
         GOTO1 HIGH                                                             
         CLC   KEY(TLCACCAT+L'TLCACCAT-TLCAPCD),KEYSAVE                         
         BNE   ITN50                                                            
         GOTO1 GETREC              GET THE CAST RECORD                          
         SPACE                                                                  
         MVI   ELCODE,TAFNELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAFNTVER))                                     
         BNE   ITN04                                                            
         USING TAFND,R4                                                         
         L     R4,TGELEM           SAVE WHICH VERSIONS THE CAST                 
         ZIC   RE,TAFNLEN          MEMBER IS ON                                 
         SHI   RE,4                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   VERS(0),TAFNNAME                                                 
         SPACE                                                                  
ITN04    L     R4,AIO                                                           
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'00'                                                            
         USING TACAD,R4                                                         
         MVC   CASTAT,TACASTAT                                                  
         SPACE                                                                  
ITN05    LA    R4,KEY              BUILD PASSIVE KEY FOR INVOICE                
         USING TLINPD,R4                                                        
         XC    TLINPKEY,TLINPKEY                                                
         MVI   TLINPCD,TLINHCDQ    RECORD CODE                                  
         MVC   TLINHCOM,TGCOM      INTERNAL COMMERCIAL NUMBER                   
         GOTO1 HIGH                GET DIRECTORY RECORD                         
         B     ITN20                                                            
ITN10    GOTO1 SEQ                                                              
ITN20    CLC   KEY(TLINHCOM+L'TLINHCOM-TLINPCD),KEYSAVE                         
         BNE   ITN40                                                            
         GOTO1 GETREC              READ INVOICE RECORD                          
         SPACE                                                                  
         L     R4,AIO              R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL            R4=A(PAYMENT DETAILS ELEMENT)                
         BNE   ITN10                                                            
         USING TAPDD,R4                                                         
         CLC   TAPDUSE,=C'CLA'     HAS TO BE FOR CLASS A USE                    
         BNE   ITN10                                                            
         SPACE                                                                  
         MVC   INSTAT,TAPDPST1     SAVE STATUS                                  
         SPACE                                                                  
         MVI   VERSTAT,0                                                        
         L     R4,AIO              R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TAVRELQ                                                   
         BRAS  RE,GETEL            IF INVOICE WAS PAID TO VERSION               
         BNE   ITN25                                                            
         USING TAVRD,R4                                                         
         MVC   VERSTAT,TAVRVERS    SAVE VERSION LETTER IN VERSTAT               
         SPACE                                                                  
         LA    RE,VERS                                                          
ITN21    CLI   0(RE),0             IF CAST MEMBER IS NOT ON THE                 
         BE    ITN10               INVOICE'S VERSION, SKIP THIS                 
         CLC   VERSTAT,0(RE)       INVOICE                                      
         BE    ITN25                                                            
         LA    RE,1(RE)                                                         
         B     ITN21                                                            
         SPACE                                                                  
ITN25    L     R4,AIO              R4=A(INVOICE RECORD)                         
         MVI   ELCODE,TANPELQ                                                   
         BRAS  RE,GETEL            READ ALL PROGRAM ELEMENTS                    
         B     *+8                                                              
ITN30    BRAS  RE,NEXTEL                                                        
         BNE   ITN10                                                            
         USING TANPD,R4                                                         
         CLC   TANPDATE,SRTCYCS    IF DATE DOES NOT FIT WITHIN                  
         BL    ITN30               CYCLE, REJECT IT                             
         CLC   TANPDATE,SRTCYCE                                                 
         BH    ITN30                                                            
         SPACE                                                                  
         CLI   VERSTAT,0           IF INVOICE NOT TO VERSION                    
         BNE   ITN33                                                            
         TM    CASTAT,TACASTLO     IF CAST MEMBER IS ONLY ON                    
         BZ    ITN32               LIFT                                         
         CLI   TANPLFT,C'Y'        THEN ONLY ACCEPT LIFT PROGRAMS               
         BNE   ITN30                                                            
         SPACE                                                                  
ITN32    TM    CASTAT,TACASTLF     IF CAST MEMBER IS NOT ON LIFT                
         BO    ITN33                                                            
         CLI   TANPLFT,C'Y'        THEN REJECT LIFT PROGRAMS                    
         BE    ITN30                                                            
         SPACE                                                                  
ITN33    CLI   SRTYEAR,C'8'        IF CAST YEAR >= 2000                         
         BE    ITN34                                                            
         CLI   SRTYEAR,C'9'                                                     
         BE    ITN34                                                            
         CLI   TANPNWK,C'X'        THEN SKIP PAX PROGRAMS                       
         BE    ITN30                                                            
         SPACE                                                                  
ITN34    LH    RE,SRTSTUS                                                       
         TM    INSTAT,TAPDPCRD     DECREMENT # OF CLA USES                      
         BZ    *+12                IF CREDIT INVOICE                            
         SHI   RE,1                                                             
         B     *+8                                                              
         AHI   RE,1                OTHERWISE                                    
         STH   RE,SRTSTUS          INCREMENT # OF CLA USES                      
         SPACE                                                                  
         CLI   TANPNWK,C'I'        IF PROGRAM IS ITN                            
         BNE   ITN30                                                            
         LH    RE,SRTUSES                                                       
         TM    INSTAT,TAPDPCRD     DECREMENT ITN COUNTER                        
         BZ    *+12                IF CREDIT INVOICE                            
         SHI   RE,1                                                             
         B     *+8                                                              
         AHI   RE,1                OTHERWISE                                    
         STH   RE,SRTUSES          INCREMENT ITN COUNTER                        
         B     ITN30                                                            
         SPACE                                                                  
ITN40    LH    RE,SRTSTUS          CONVERT TCUNITS FROM CLASS A                 
         SH    RE,SRTUSES          COUNTER TO STARTING USE #                    
         AHI   RE,1                                                             
         STH   RE,SRTSTUS                                                       
         SPACE                                                                  
         LH    RE,SRTSTUS                                                       
         CHI   RE,0                IF START USE IS NEGATIVE #                   
         BNL   *+10                CONVERT TO ZERO                              
         XC    SRTSTUS,SRTSTUS                                                  
         SPACE                                                                  
         LH    RE,SRTUSES                                                       
         CHI   RE,0                IF ITN USES IS NEGATIVE #                    
         BNL   ITN50               CONVERT TO ZERO                              
         XC    SRTUSES,SRTUSES                                                  
         SPACE                                                                  
ITN50    MVC   AIO,AIO1                                                         
ITNX     XIT1                                                                   
         SPACE                                                                  
INSTAT   DS    XL1                                                              
VERSTAT  DS    XL1                                                              
CASTAT   DS    XL1                                                              
VERS     DS    XL27                                                             
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE DETERMINES IF THE INVOICE SHOULD BE RERUN.  IT LOOKS AT          
* THE RERUN INDICATOR IN MASTD AND THE CHECK RUN DATE FROM THE INVOICE.         
* IT RETURNS 'YES' TO RERUN THE INVOICE AND 'NO' OTHERWISE.                     
*                                                                               
         USING TAIND,R4            R4=A(INVOICE STATUS ELEMENT)                 
TESTRR   NTR1  BASE=*,LABEL=*                                                   
         CLI   RERUN,C'Y'          RERUN INDICATOR MUST BE Y(ES),               
         BE    TR10                    A(LL), R(ECALC), OR D(IED)               
         CLI   RERUN,C'A'                                                       
         BE    TR10                                                             
         CLI   RERUN,C'R'                                                       
         BE    TR10                                                             
         CLI   RERUN,C'D'                                                       
         BNE   TRNO                                                             
*                                                                               
TR10     CLC   TAINCKID,REQURG     DON'T RERUN IF NOT SAME URGENT ID            
         BNE   TRNO                                                             
         CLC   TAINCKRN,TGTODAY1   MUST BE SAME DATE AS TODAY                   
         BNE   TRNO                                                             
         B     TRYES                                                            
*                                                                               
TRYES    SR    RC,RC                                                            
TRNO     LTR   RC,RC                                                            
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*---------------------------------------------------------------------          
* FITNAME - FITS FULL NAME INTO SRTNAME,                                        
*           IF MIDDLE NAME DOESN'T FIT, USE MIDDLE INITIAL                      
*---------------------------------------------------------------------          
FITNAME  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   BLOCK(50),BSPACES                                                
         MVC   BLOCK(16),FRSTNAME  THEN SQUASH FIRST NAME WITH LAST             
         MVC   BLOCK+17(16),MIDNAME                                             
         MVC   BLOCK+34(16),LASTNAME   NAME                                     
         GOTO1 SQUASHER,DMCB,BLOCK,50                                           
         CLC   MIDNAME,BSPACES                                                  
         BE    FITNAMEX                                                         
         CLI   DMCB+7,L'SRTNAME    DID FULL NAME FIT?                           
         BNH   FITNAMEX                                                         
*                                                                               
         MVC   BLOCK(50),BSPACES                                                
         MVC   BLOCK(16),FRSTNAME  NO, USE MIDDLE INITIAL                       
         MVC   BLOCK+17(1),MIDNAME                                              
         MVC   BLOCK+19(16),LASTNAME                                            
         GOTO1 SQUASHER,DMCB,BLOCK,50                                           
         B     FITNAMEX                                                         
*                                                                               
BSPACES  DC    CL52' '                                                          
*                                                                               
FITNAMEX XIT1                                                                   
*              ROUTINE TO INITIALIZE LCLTAB AND OFFTAB                          
         SPACE 1                                                                
         DS    0D                                                               
VINITABS NMOD1 0,INITAB                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         BAS   RE,INITLCL          INITIALIZE LCLTAB                            
*                                                                               
         BAS   RE,INITOFF          INITIALIZE OFFTAB                            
*                                                                               
INITABSX XIT1                                                                   
         EJECT                                                                  
* INITIALIZE TABLE OF AFTRA LOCALS THAT ARE NOT TO HAVE THEIR CHECKS            
* SENT DIRECTLY TO THEM.  THESE AFTRA LOCALS WILL SORT WITH THE SAG             
* CHECKS.                                                                       
*                                                                               
INITLCL  NTR1                                                                   
         L     R2,ALCLTAB          R2=A(FIRST LCLTAB ENTRY)                     
         LA    R0,MAXLCLS          R0=MAXIMUM NUMBER OF LCLTAB ENTRIES          
*                                                                               
         LA    R3,KEY              BUILD KEY OF UNION LOCAL RECORD              
         XC    KEY,KEY                 FOR UNION 'AFT'                          
         USING TLLOD,R3                                                         
         MVI   TLLOCD,TLLOCDQ                                                   
         MVC   TLLOUN,=C'AFT'                                                   
*                                                                               
         GOTO1 HIGH                GET FIRST UNION LOCAL KEY                    
*                                                                               
*                                  WHILE STILL UNION 'AFT'                      
IL10     CLC   TLLOKEY(TLLOLCL-TLLOKEY),KEYSAVE                                 
         BNE   INITABSX                                                         
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         L     R4,AIO              R4 = A(LOCAL ELEMENT)                        
         MVI   ELCODE,TALOELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   IL50                IF NONE FOUND THEN ADD TO TABLE              
         USING TALOD,R4                                                         
*                                                                               
         TM    TALOSTAT,TALOSUCP   IF UNION COPIES STATUS BIT NOT SET           
         BO    IL90                                                             
*                                                                               
IL50     LTR   R0,R0               TEST DIE IF NO MORE ROOM IN LCLTAB           
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   0(3,R2),TLLOUN      SAVE UNION/LOCAL IN LCLTAB                   
         MVC   3(3,R2),TLLOLCL                                                  
*                                                                               
         LA    R2,6(R2)            BUMP R2 TO NEXT LCLTAB ENTRY                 
         SH    R0,=H'1'            DECREMENT NUMBER OF ENTRIES LEFT             
*                                                                               
IL90     GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         B     IL10                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
* INITIALIZE TABLE OF OFFICES.  THIS IS DONE STRICTLY FOR EFFICIENCY            
* REASONS - PROGRAM USED TO READ OFFICE RECORD FOR EACH PRINTED CHECK.          
*                                                                               
INITOFF  NTR1                                                                   
         LA    R2,OFFTAB           R2=A(FIRST OFFTAB ENTRY)                     
         USING OFFD,R2                                                          
         LA    R0,MAXOFFS          R0=MAXIMUM NUMBER OF OFFTAB ENTRIES          
*                                                                               
         LA    R3,KEY              INITIALIZE KEY                               
         XC    KEY,KEY                                                          
         USING TLOFD,R3                                                         
         MVI   TLOFCD,TLOFCDQ                                                   
*                                                                               
         GOTO1 HIGH                GET FIRST OFFICE KEY                         
*                                                                               
IO10     CLI   TLOFKEY,TLOFCDQ     WHILE STILL OFFICE RECORD                    
         BNE   INITABSX                                                         
*                                                                               
         LTR   R0,R0               TEST DIE IF NO MORE ROOM IN OFFTAB           
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   OFFCODE,TLOFOFF     SAVE OFFICE CODE IN OFFTAB                   
*                                                                               
         GOTO1 GETREC              GET RECORD                                   
*                                                                               
         GOTO1 CHAROUT,DMCB,TANAELQ,0  GET NAME                                 
         MVC   OFFNAME,TGNAME                                                   
*                                                                               
         L     R4,AIO              R4 = A(OFFICE ELEMENT)                       
         MVI   ELCODE,TAOFELQ                                                   
         USING TAOFD,R4                                                         
         BRAS  RE,GETEL                                                         
         BNE   *+10                                                             
         MVC   OFFTEL,TAOFTEL      SAVE TELEPHONE NUMBER IN TABLE               
*                                                                               
         LA    R2,OFFNEXT          BUMP R2 TO NEXT OFFTAB ENTRY                 
         SH    R0,=H'1'            DECREMENT NUMBER OF ENTRIES LEFT             
*                                                                               
         GOTO1 SEQ                 GET NEXT KEY AND LOOP BACK                   
         B     IO10                                                             
         DROP  R2,R3,R4                                                         
         SPACE                                                                  
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE USES THE TABLE ENTRY, POINTED TO BY PARAMETER ONE, TO            
* CALCULATE THE DUE COMPANY AMOUNT TO COLLECT.  THE COLLECTED AMOUNT IS         
* STORED IN DUECOL.  IF THE TABLE ENTRY IS FILTERED FOR SOME REASON,            
* DUECOL WILL BE ZERO.                                                          
*                                  XIT DUESTAT SET                              
*                                                                               
         DS    0D                                                               
CALCDUE  NMOD1 0,CLCDUE                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         XC    DUECOL,DUECOL       INITIALIZE COLLECTED AMOUNT TO ZERO          
         MVI   DUEFLAG,0                                                        
         L     R5,0(R1)            R5 = A(DUE COMPANY ENTRY)                    
         USING DUETABD,R5                                                       
*                                                                               
         LA    R4,DUEELEM          R4 = A(DUE COMPANY DETAILS ELEMENT)          
         USING TADUD,R4                                                         
*                                                                               
         TM    TGSYSTA2,TASYSNDC   NEW DUE COMPANY?                             
         BO    CD05                                                             
         TM    TADUSTAT,TADUSNTR   NON TAXABLE RIEM?                            
         BO    CD90                                                             
         TM    TADUSTA2,TADUSTXR   TAXABLE REIM?                                
         BO    CD90                                                             
*                                                                               
CD05     TM    TADUSTAT,TADUSNTR   NON TAXABLE RIEM?                            
         BZ    *+8                                                              
         OI    DUEFLAG,DUEFNR                                                   
         TM    TADUSTA2,TADUSTXR   TAXABLE REIM?                                
         BZ    *+8                                                              
         OI    DUEFLAG,DUEFTR                                                   
*                                                                               
         OC    TADUUNI,TADUUNI     IF UNION IS BLANK - USE IT                   
         BZ    *+14                                                             
         CLC   TADUUNI,SRTUN       ELSE IT MUST BE SAME UNION                   
         BNE   CD90                                                             
*                                                                               
         TM    TADUSTAT,TADUSCAN   IF RECORD IS FOR CANADIAN $                  
         BZ    *+16                                                             
         CLI   RECNUM,CN           THEN ONLY PROCESS IF WE'RE CAN$ CKS          
         BNE   CD90                                                             
         B     *+12                                                             
         CLI   RECNUM,CN           ELSE ONLY PROCESS IF NOT CAN$ CKS            
         BE    CD90                                                             
*                                  IF BOTH COLLECT ONLY BITS SET                
         TM    TADUSTAT,TADUSEUR   IF RECORD IS FOR EUROS                       
         BZ    *+16                                                             
         CLI   RECNUM,EC           THEN ONLY PROCESS IF WE'RE EURO CKS          
         BNE   CD90                                                             
         B     *+12                                                             
         CLI   RECNUM,EC           ELSE ONLY PROCESS IF NOT EURO CKS            
         BE    CD90                                                             
*                                  IF BOTH COLLECT ONLY BITS SET                
         TM    TADUSTAT,TADUSAGY+TADUSCLI                                       
         BNO   CD10                                                             
         BAS   RE,CKDUEAGY         THEN MUST BE EITHER SAME AGENCY              
         BL    CD90                                                             
         BE    CD20                                                             
         BAS   RE,CKDUECLI         OR SAME CLIENT AS SORT RECORD                
         BE    CD20                                                             
         B     CD90                                                             
*                                                                               
CD10     TM    TADUSTAT,TADUSAGY   IF COLLECT ONLY FROM AGENCY BIT SET          
         BZ    *+12                                                             
         BAS   RE,CKDUEAGY         THEN MUST BE SAME AGENCY AS SORT REC         
         BNE   CD90                                                             
*                                                                               
         TM    TADUSTAT,TADUSCLI   IF COLLECT ONLY FROM CLIENT BIT SET          
         BZ    *+12                                                             
         BAS   RE,CKDUECLI         THEN MUST BE SAME CLIENT AS SORT REC         
         BNE   CD90                                                             
*                                                                               
CD20     L     RF,TADUDUE          AMOUNT TO COLLECT = AMOUNT DUE -             
         S     RF,TADUCOL              AMOUNT ALREADY COLLECTED                 
*                                                                               
         L     R3,SRTNET           AVAILABLE CHECK BALANCE = CHECK              
         CLI   RECNUM,PK                                                        
         BNE   CD20A                                                            
         CLI   SRTW4TY,TAW4TYCO    REIMBURSEMENTS NOT INCLUDED                  
         BE    CD20A                                                            
         LARL  R1,SVTXRE                                                        
         ICM   RE,15,0(R1)         P+ ONLY (SRTTXRE NOT SET YET)                
         SR    R3,RE                                                            
CD20A    S     R3,SRTMDED              AMOUNT - MISC DED                        
         S     R3,SRTDUES                     - UNION DUES                      
*                                                                               
         TM    DUEFLAG,DUEFTR      TAXABLE REIMBURSEMENTS?                      
         BZ    CD20B                                                            
         LARL  R1,SVTXRE                                                        
         ICM   R3,15,0(R1)         P+ ONLY (SRTTXRE NOT SET YET)                
CD20B    TM    DUEFLAG,DUEFNR      NON TAXABLE REIMBURSEMENTS?                  
         BZ    CD21                                                             
         L     R3,SRTREXP                                                       
         CLI   RECNUM,PK                                                        
         BNE   CD21                                                             
         LARL  R1,SVTXRE           (SRTTXRE NOT SET YET)                        
         ICM   R0,15,0(R1)         P+ ONLY (SRTTXRE NOT SET YET)                
         SR    R3,R0               ONLY GET NON TAXABLE REIM FOR P+             
*                                                                               
CD21     XR    R0,R0                                                            
         ICM   R0,3,TADUPCT        IF DEDUCTION PERCENTAGE NOT ZERO             
         BZ    CD30                                                             
         CLI   TADULEN,X'2C'       AND NEW LENGTH                               
         BNH   CD23                                                             
         OC    TADUAYFL,TADUAYFL   AND AGENCY FLIST                             
         BZ    CD22                                                             
         BAS   RE,CKDUEAGY         IS AGENCY IN FLIST?                          
         BNE   CD25                NO, TAKE %                                   
         BE    CD30                YES, TAKE AVAILABLE BALANCE                  
CD22     OC    TADUCLFL,TADUCLFL   OR CLIENT FLIST                              
         BZ    CD23                                                             
         BAS   RE,CKDUECLI         IS CLIENT IN FLIST?                          
         BNE   CD25                NO, TAKE %                                   
         BE    CD30                YES, TAKE AVAILABLE BALANCE                  
CD23     CLC   TADUAGY,SRTAGY      AND DIFFERENT AGENCY                         
         BNE   CD25                                                             
         CLC   TADUFCLI,=6C' '     & SPECIFIC,                                  
         BNH   CD30                                                             
         CLC   TADUFCLI,SRTCLI     DIFFERENT CLIENT                             
         BE    CD30                                                             
*                                                                               
CD25     OI    DUESTAT,DUESPCT     PERCENTAGE TAKEN                             
         MR    R2,R0               USE THE PECENTAGE                            
         D     R2,=F'5000'                                                      
         LTR   R3,R3                                                            
         BM    *+8                 & ROUND IT                                   
         AHI   R3,1                                                             
         SRA   R3,1                                                             
*                                                                               
CD30     CR    RF,R3               IF AMOUNT TO COLLECT > AVAIL BAL             
         BNH   *+6                                                              
         LR    RF,R3               THEN AMOUNT TO COLLECT = AVAIL BAL           
         ST    RF,DUECOL           SAVE AMOUNT TO COLLECT IN DUECOL             
*                                                                               
CD90     TM    DUESTAT,DUESRERD    IF NEED TO RE-ESTABLISH READ                 
         BZ    CDX                                                              
         NI    DUESTAT,X'FF'-DUESRERD                                           
         MVC   KEY,SVDUEKEY        USE SAVED DUE COMPANY KEY                    
         GOTO1 HIGH                                                             
         B     CDX                                                              
         SPACE 2                                                                
CDLOW    LNR   RC,RC                                                            
         B     CDNO                                                             
CDYES    SR    RC,RC                                                            
CDNO     LTR   RC,RC                                                            
CDX      XIT1                                                                   
         DROP  R5                                                               
*                                                                               
SVTXRE   DS    F                   TAXABLE REIMBURSEMNTS FOR CHECK              
*                                                                               
         EJECT                                                                  
*              ROUTINE CHECKS DUE COMPANY AGENCY MATCHES                        
*              AGENCY ON CHECK                                                  
*                                                                               
CKDUEAGY NTR1                                                                   
         XR    R0,R0               SET INDICATOR PROCESSING AGENCY              
         CLI   TADULEN,X'2C'       IF NEW ELEMENT LENGTH                        
         BNH   CKDUEAY8                                                         
         OC    TADUAYFL,TADUAYFL   AND AGENCY FLIST SPECIFIED                   
         BZ    CKDUEAY8                                                         
         BAS   RE,PROCFLST         CHECK AGAINST AGENCY FLIST                   
         B     *+10                                                             
CKDUEAY8 CLC   TADUAGY,SRTAGY      ELSE, CHECK AGAINST AGENCY CODE              
         B     CDX                 CC LOW/YES/NO SET                            
         SPACE 2                                                                
*              ROUTINE CHECKS DUE COMPANY CLIENT MATCHES                        
*              CLIENT ON CHECK                                                  
*                                                                               
CKDUECLI NTR1                                                                   
         LA    R0,1                SET INDICATOR PROCESSING CLIENT              
         CLI   TADULEN,X'2C'       IF NEW ELEMENT LENGTH                        
         BNH   CKDUECL8                                                         
         OC    TADUCLFL,TADUCLFL   AND CLIENT FLIST SPECIFIED                   
         BZ    CKDUECL8                                                         
         BAS   RE,PROCFLST         CHECK AGAINST CLIENT FLIST                   
         B     *+10                                                             
CKDUECL8 CLC   TADUFCLI,SRTCLI     ELSE, CHECK AGAINST CLIENT CODE              
         B     CDX                 CC LOW/YES/NO SET                            
         EJECT                                                                  
*              ROUTINE CHECKS AGENCY OR CLIENT ON CHECK IS IN                   
*              FLIST INDICATED ON THE DUE COMPANY RECORD                        
*                                  NTRY - R0=TABLE INDICATOR                    
*                                  XIT    CC SET LOW/YES/NO                     
*                                                                               
PROCFLST NTR1                                                                   
         MVC   TGLST,TADUCLFL      SET CLIENT FLIST RECORD CODE                 
         L     R2,ACLFTAB          R2=A(CLIENT FLIST TABLE)                     
         LTR   R0,R0               IF PROCESSING CLIENT                         
         BNZ   *+14                                                             
         MVC   TGLST,TADUAYFL      ELSE, SET AGENCY FLIST RECORD CODE           
         L     R2,AAYFTAB          R2=A(AGENCY FLIST TABLE)                     
*                                                                               
         USING FLSTTABD,R2         R2=A(TABLE TO CHECK)                         
PFLIST2  CLI   0(R2),X'FF'         IF END OF TABLE                              
         BNE   PFLIST3                                                          
         L     R2,ACLFTAB          START OVER, CLEARING TABLE                   
         LTR   R0,R0                                                            
         BNZ   *+8                                                              
         L     R2,AAYFTAB                                                       
         BAS   RE,CLRFTAB          CLEAR FLIST TABLE                            
*                                                                               
PFLIST3  CLI   FLSTCODE,0          IF END OF TABLE                              
         BNE   *+12                                                             
         BAS   RE,ADDFLST          ADD FLIST TO TABLE                           
         BNE   CDLOW               FLIST RECORD NOT FOUND(EXIT LOW)             
*                                                                               
         CLC   FLSTCODE,TGLST      ELSE CHECK FLIST IN TABLE                    
         BNE   *+12                                                             
         BAS   RE,CKFLST           CHECK CODE IN FLIST                          
         B     CDX                 CC YES/NO SET                                
*                                                                               
         ZIC   R1,FLSTNUM          BUMP TO NEXT FLIST ENTRY                     
         MH    R1,=AL2(L'FLSTDATA)                                              
         LA    R1,FLSTLEN(R1)                                                   
         AR    R2,R1                                                            
         B     PFLIST2                                                          
         SPACE 2                                                                
*              ROUTINE TO CLEAR FLIST TABLE                                     
*                                  R2=A(TABLE)                                  
CLRFTAB  NTR1                                                                   
         LR    RE,R2                                                            
         L     RF,=A(FLISTABL)                                                  
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
         B     CDX                                                              
         EJECT                                                                  
*              ROUTINE CHECKS CODE IN FLIST                                     
*                                                                               
CKFLST   NTR1                                                                   
         ZIC   R1,FLSTNUM          R1=N'CODES IN FLIST                          
         LA    R3,FLSTDATA         R3=A(LIST OF CODES IN FLIST)                 
*                                                                               
CKFLST5  LTR   R0,R0                                                            
         BZ    *+14                                                             
         CLC   SRTCLI,0(R3)        CHECK CLIENT IN FLIST                        
         B     *+10                                                             
         CLC   SRTAGY,0(R3)        CHECK AGENCY IN FLIST                        
         BE    CDYES                                                            
         LA    R3,L'FLSTDATA(R3)   BUMP TO NEXT CODE IN THIS FLIST              
         BCT   R1,CKFLST5                                                       
         B     CDNO                                                             
         SPACE 2                                                                
*              ROUTINE READS FLIST RECORD AND ADDS TO TABLE                     
*                                                                               
ADDFLST  NTR1                                                                   
         MVI   TGLTYP,TLGLTYPF                                                  
         MVC   AIO,AIO2                                                         
         OI    DUESTAT,DUESRERD    SET TO REREAD DUE COMPANY                    
         GOTO1 RECVAL,DMCB,TLGLCDQ,(X'A0',0)                                    
         MVC   AIO,AIO1                                                         
         BE    *+12                                                             
ADDFLST2 OI    DUESTAT,DUESERR     SET ERROR READING FLIST RECORD               
         B     CDNO                                                             
*                                                                               
         XR    R1,R1               R1=NUMBER OF CODES                           
         LA    R3,FLSTDATA         R3=A(CODES)                                  
         L     R4,AIO2                                                          
         MVI   ELCODE,TAGLELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+8                                                              
ADDFLST5 BRAS  RE,NEXTEL                                                        
         BNE   ADDFLST7                                                         
*                                                                               
         USING TAGLD,R4                                                         
         ZIC   RF,TAGLLEN                                                       
         SH    RF,=Y(TAGLLNQ)                                                   
         CH    RF,=AL2(L'FLSTDATA)                                              
         BH    ADDFLST2            BAD FLIST DATA - PUT IN ERROR                
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),TAGLDATA                                                 
         OC    0(L'FLSTDATA,R3),=6C' '                                          
         LA    R3,L'FLSTDATA(R3)                                                
         LA    R1,1(R1)            INCREMENT NUMBER OF CODES                    
         B     ADDFLST5                                                         
*                                                                               
ADDFLST7 STC   R1,FLSTNUM                                                       
         MVC   FLSTCODE,TGLST      SET FLIST CODE IN TABLE                      
         B     CDYES                                                            
         DROP  R2,R4                                                            
         EJECT                                                                  
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*                                                                               
* THIS ROUTINE RETURNS THE ESUTAB ENTRY FOR THE EMPLOYER/PERFORMER              
* SPECIFIED BY THE SORT RECORD AND THE UNIT SPECIFIED BY PARAMETER 1.           
* THE ROUTINE WILL LOOK FOR THE REQUESTED ENTRY AND BUILD A NEW ONE             
* IF IT DOESN'T FIND IT.  IF THE NEW ENTRY IS FOR FD, THE ROUTINE WILL          
* BUILD NEW ENTRIES FOR ALL UNITS THAT PERFORMER HAS BEEN PAID ON.              
* THE ASSUMPTION IS THAT THIS ROUTINE IS ALWAYS CALLED FIRST FOR FD.            
* THE ADDRESS OF THE ENTRY IN ALL CASES WILL BE RETURNED IN DMCB.               
*                                                                               
         DS    0D                                                               
VGETESU  NMOD1 0,GETESU                                                         
         SAM24                                                                  
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         L     R2,0(R1)            R2 = A(UNIT FOR LOOKUP)                      
*                                                                               
         LA    R5,BLOCK            R5 = A(ESUTAB LOOKUP ENTRY)                  
         USING ESUTABD,R5                                                       
*                                                                               
GE10     XC    BLOCK(ESULEN),BLOCK  BUILD LOOKUP ENTRY FOR UNIT                 
         MVC   ESUEMP,SRTEMP              REQUESTED BY CALLER                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,0(R2)                                                    
*                                                                               
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5),1  LOOKUP/ADD ENTRY TO TABLE            
*                                                                               
*        CLI   0(R1),1             IF ENTRY WAS FOUND THEN RETURN IT            
*        BNE   GEX                     (DMCB HAS ADDRESS)                       
*                                                                               
         TM    0(R1),X'80'         IF ENTRY WAS FOUND THEN RETURN IT            
         BZ    GEX                     (DMCB HAS ADDRESS)                       
*                                                                               
         CLC   ESUUNIT,=C'FD '     ELSE IF THIS UNIT IS FEDERAL                 
         BE    GE20                                                             
         CLC   ESUUNIT,=C'EU '     OR IF THIS UNIT IS EURO                      
         BNE   GEX                                                              
GE20     BAS   RE,BLDESU           BUILD ALL ESUTAB ENTRIES FOR PERF.           
         B     GE10                GO BACK AND FIND FD ENTRY JUST ADDED         
*                                                                               
GEX      XIT1                                                                   
         EJECT                                                                  
* THIS ROUTINE WILL BUILD NEW ESUTAB ENTRIES FOR A PERFORMER.  THE              
* NEW ENTRIES WILL BE BUILT BY GOING TO TAYTD, WHICH BUILDS A TABLE             
* WITH EACH TAX UNIT THE PERFORMER HAS BEEN INVOLVED WITH.  EACH ENTRY          
* IN THE TABLE RETURNED BY TAYTD CORRESPONDS TO AN ENTRY IN ESUTAB.             
*                                                                               
BLDESU   NTR1                                                                   
         XC    YTDBLK,YTDBLK       CLEAR TAYTD PARAMETER BLOCK                  
         LA    R3,YTDBLK                                                        
         USING TYD,R3                                                           
         MVC   TYASYSIO,TASYSIO    A(SYSIO)                                     
         MVC   TYTRACE,YTDTRACE    TRACE SWITCH                                 
         L     R4,=A(YTDTAB)                                                    
         ST    R4,TYATAB           A(YTD TABLE)                                 
         USING YTDD,R4                                                          
*                                                                               
         CLC   TGTODAY1(1),CHKDATE1 IF CHECK DATE IN THIS YEAR                  
         BNE   *+14                                                             
         MVC   TYPEND,TGTODAY1     THEN SET END DATE AS TODAY                   
         B     *+10                                                             
         MVC   TYPEND,CHKDATE1     ELSE, SET END DATE AS CHECK DATE             
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+10                                                             
         MVC   TYPEND,CHKDATEL     USE THAT DATE                                
         MVC   TYEMP,SRTEMP        EMPLOYER                                     
         MVC   TYSSN,SRTSSN        SOCIAL SECURITY NUMBER                       
*                                                                               
         MVI   TYCUR,C'U'          IF CURRENCY IS US THEN TYCUR = U             
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TYCUR,C'C'          ELSE TYCUR = C                               
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   TYCUR,C'E'          ELSE TYCUR = E                               
*                                                                               
         GOTO1 TGTAYTD,DMCB,(RC),SYSCOMM,(R3)  BUILD YTD TABLE                  
*                                                                               
         LA    R5,BLOCK            R5=A(PYTDTAB ENTRY)                          
         USING PYTDTABD,R5                                                      
         XC    0(PYTDLEN,R5),0(R5) BUILD PYTDTAB ENTRY                          
         MVC   PYTDEMP,SRTEMP      EMPLOYER                                     
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN      SOCIAL SECURITY NUMBER                       
         MVC   PYTDAMTS,TYCYTD     YTD TOTALS                                   
         MVC   PYTDCAMT,TYCCYTD    YTD TOTALS                                   
         GOTO1 ADDBIN,DMCB,APYTDTAB,(R5)  ADD PYTDTAB ENTRY                     
*                                                                               
BE10     CLI   0(R4),0             END OF YTD TABLE BY UNIT                     
         BE    BEX                                                              
         USING ESUTABD,R5          R5=A(ESUTAB ENTRY)                           
         XC    0(ESULEN,R5),0(R5)  BUILD ESUTAB ENTRY                           
         MVC   ESUEMP,SRTEMP       EMPLOYER                                     
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN       SOCIAL SECURITY NUMBER                       
         MVC   ESUUNIT,YTDUNIT     TAX UNIT                                     
         MVC   ESUEARN,YTDEARN     EARNINGS                                     
         MVC   ESUTAX,YTDTAX       TAX                                          
         MVC   ESUFERN,YTDFERN     FEDERAL EARNINGS AT PROV LEVEL               
*                                                                               
         CLC   ESUUNIT,=C'FD '     IF THIS IS FEDERAL                           
         BE    BE15                                                             
         CLC   ESUUNIT,=C'EU '     OR THIS IS EURO                              
         BNE   BE20                                                             
BE15     MVC   ESUREXP,YTDREXP     REIMB. EXPENSES                              
         MVC   ESUFICA,YTDFICA     FICA                                         
*                                                                               
         MVC   ESUMDD,YTDMDD       MISC DEDUCTION                               
         MVC   ESUMTR,YTDMTR       MPTRP                                        
         MVC   ESUDTP,YTDDTP       DUE TP                                       
         MVC   ESUDTPTR,YTDDTPTR   DUE TP TAX REIMBURSEMENT                     
         MVC   ESUDTPNR,YTDDTPNR   DUE TP NON TAX REIMBURSEMENT                 
         MVC   ESUPRC,YTDPRC       PERMANENT CHARITIES                          
         MVC   ESULIE,YTDLIE       LIENS                                        
         MVC   ESUUND,YTDUND       UNION DUES                                   
         MVC   ESUTRS,YTDTRS       TRUSTS                                       
         MVC   ESUDRT,YTDDRT       DIRECT DEPOSITS                              
         MVC   ESUWRD,YTDWRD       WIRED                                        
         MVC   ESUPPA,YTDPPA       PAYROLL+ COMMISSION                          
         B     BE30                                                             
*                                                                               
BE20     CLC   ESUUNIT,=C'CN '     IF THIS IS CANADA                            
         BNE   BE25                                                             
         MVC   ESUGST,YTDGST       GOODS AND SERVICES TAX                       
         MVC   ESUPST,YTDPST       PST                                          
         B     BE30                                                             
*                                                                               
BE25     CLI   ESUUNIT+2,C' '      IF THIS IS STATE                             
         BNE   BE30                                                             
         MVC   ESUSUI,YTDSUI       SUI                                          
         MVC   ESUSDI,YTDSDI       SDI                                          
         MVC   ESUSFLI,YTDSFLI     FLI                                          
*                                                                               
BE30     MVC   TGFULL,AIO          SAVE AIO                                     
         CLI   RECNUM,PK           PAYROLL PLUS?                                
         BE    *+14                                                             
         CLC   =C'P+',TGEMP        TGEMP FILLED IF SSN TRANSFER                 
         BNE   BE40                                                             
         MVC   ESUTXRE,YTDTXRE     SET TAXABLE REIMBURSEMENTS                   
         MVC   ESUNTRE,YTDNTRE     SET NON-TAXABLE REIMBURSEMENTS               
         MVC   ESUTSUI,YTDTSUI     TAXABLE SUI FOR THIS UNIT                    
         MVC   ESUYSUI,YTDYSUI     YTD TAXABLE SUI FOR CHECK                    
         MVC   ESUCTXRE,YTDCTXRE   YTD TAXABLE REIMBURSEMENTS CAD               
         MVC   ESUCNTRE,YTDCNTRE   YTD NON TAXABLE REIMBURSEMENTS CAD           
         MVC   ESUCEARN,YTDCEARN   YTD EARNINGS CAD                             
         MVC   ESUUTAX,YTDUTAX     FD TAXES                                     
         MVC   ESUUPP,YTDUPP       PENSION PLAN                                 
         MVC   ESUUEI,YTDUEI       EMP INSURANCE PLAN                           
         MVC   ESUUPIP,YTDUPIP     PARENTAL INSURANCE PLAN                      
         MVC   ESUCTAX,YTDCTAX     FD TAXES CAD                                 
         MVC   ESUCPP,YTDCPP       PENSION PLAN CAD                             
         MVC   ESUCEI,YTDCEI       EMP INSURANCE PLAN CAD                       
         MVC   ESUCPIP,YTDCPIP     PARENTAL INSURANCE PLAN CAD                  
*                                                                               
BE40     GOTO1 ADDBIN,DMCB,AESUTAB,(R5),1  ADD ESUTAB ENTRY                     
*                                                                               
         MVC   AIO,TGFULL          RESTORE AIO                                  
         LA    R4,YTDNEXT          BUMP TO NEXT TABLE ENTRY                     
         B     BE10                                                             
*                                                                               
BEX      B     GEX                                                              
         EJECT                                                                  
* CONSTANTS, ETC. FOR GETESU, BLDESU ROUTINES                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE ADDS THE BINSRCH RECORD PASSED AS PARAMETER 2 TO THE             
* BINSRCH TABLE PASSED AS PARAMTER 1.                                           
* IF THE ENTRY IS ALREADY IN THE TABLE IT ADDS THE AMOUNTS IN THE               
* NEW RECORD TO THOSE IN THE TABLE ALREADY.  THE ADDRESS OF THE ENTRY           
* IS ALWAYS RETURNED IN DMCB.                                                   
*                                                                               
         DS    0D                                                               
VADDBIN  NMOD1 0,ADDBIN                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         CLI   11(R1),1            31 BIT MODE?                                 
         JE    AB31                                                             
*                                                                               
         LM    R2,R3,0(R1)         R3=A(NEW RECORD)                             
         USING BIND,R2             R2=A(BINSRCH PARAMETERS/TABLE)               
*                                                                               
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         GOTO1 BINSRCH,DMCB,(1,(R3)),BINTABLE                                   
*                                                                               
         OC    1(3,R1),1(R1)       DIE IF NO MORE ROOM IN TABLE                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   BININ,8(R1)         UPDATE COUNT                                 
*                                                                               
         CLI   0(R1),1             IF NEW RECORD ADDED                          
         BE    ABX                 THEN DONE                                    
*                                                                               
         L     R4,0(R1)            R4=A(RECORD FOUND)                           
         ZIC   R1,BINFRST          DISP. TO FIRST BUCKET                        
         LA    RE,0(R1,R4)         RE=A(1ST BUCKET IN RECORD FOUND)             
         LA    RF,0(R1,R3)         RF=A(1ST BUCKET IN NEW RECORD)               
         ICM   R1,1,BINNUMB        R1=NUMBER OF BUCKETS                         
         BZ    ABX                                                              
         SPACE 1                                                                
         TM    BINSTAT,X'80'       ACCUMS ARE BINARY                            
         BO    AB2                                                              
         AP    0(8,RE),0(8,RF)     ADD NEW TO OLD                               
         LA    RE,8(RE)                                                         
         LA    RF,8(RF)                                                         
         BCT   R1,*-14                                                          
         B     ABX                                                              
*                                                                               
AB2      L     R0,0(RE)            ADD NEW TO OLD                               
         A     R0,0(RF)                                                         
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R1,AB2                                                           
         J     ABX                                                              
*                                                                               
AB31     SAM31                                                                  
         LM    R2,R3,0(R1)         R3=A(NEW RECORD)                             
         USING BIND,R2             R2=A(BINSRCH PARAMETERS/TABLE)               
*                                                                               
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         MVI   DMCB+12,1                                                        
         GOTO1 =V(BINSRCH),DMCB,(R3),BINTABLE                                   
*                                                                               
         OC    1(3,R1),1(R1)       DIE IF NO MORE ROOM IN TABLE                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   BININ,8(R1)         UPDATE COUNT                                 
*                                                                               
         TM    0(R1),X'80'         IF NEW RECORD ADDED                          
         BO    ABX                 THEN DONE                                    
*                                                                               
         L     R4,0(R1)            R4=A(RECORD FOUND)                           
         ZIC   R1,BINFRST          DISP. TO FIRST BUCKET                        
         LA    RE,0(R1,R4)         RE=A(1ST BUCKET IN RECORD FOUND)             
         LA    RF,0(R1,R3)         RF=A(1ST BUCKET IN NEW RECORD)               
         ICM   R1,1,BINNUMB        R1=NUMBER OF BUCKETS                         
         BZ    ABX                                                              
         SPACE 1                                                                
         TM    BINSTAT,X'80'       ACCUMS ARE BINARY                            
         BO    AB312                                                            
         AP    0(8,RE),0(8,RF)     ADD NEW TO OLD                               
         LA    RE,8(RE)                                                         
         LA    RF,8(RF)                                                         
         BCT   R1,*-14                                                          
         B     ABX                                                              
*                                                                               
AB312    L     R0,0(RE)            ADD NEW TO OLD                               
         A     R0,0(RF)                                                         
         ST    R0,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R1,AB312                                                         
*                                                                               
ABX      SAM24                                                                  
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE BUILDS THE SORT KEY PART OF THE SORT RECORD FROM THE             
* DATA CONTAINED BOTH IN THE GENERAL STORAGE ARE AND THE SORT RECORD            
* ITSELF.                                                                       
*                                                                               
         DS    0D                                                               
VBLDSORT NMOD1 0,BLDSRT                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                  PRE-CLEAR SORT KEY                           
         XC    SRTKEY(SRTKEYL),SRTKEY                                           
*                                                                               
         MVI   SRTKSTA,SRTKFRST    DEFAULT TO FIRST                             
*                                                                               
         CLI   PULLCHK,C'Y'        FIRST COME PULLED CHECKS                     
         BNE   *+12                                                             
         MVI   SRTKSTA,SRTKPULL                                                 
         B     BS200                                                            
*                                                                               
         TM    SRTBITS,SRTBW4      THEN COME PERFORMERS CHECKS WHO              
         BZ    BS10                    NEED THEM FIRST (SORTED BY LAST          
         MVI   SRTKSTA,SRTKSW4         NAME)                                    
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS10     OC    SRTDUE,SRTDUE       THEN COME CHECKS WITH DUE COMPANY            
         BZ    BS20                    AMOUNTS IN THEM (SORTED BY LAST          
         MVI   SRTKSTA,SRTKSDU         NAME)                                    
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS20     OC    SRTLIEN,SRTLIEN     THEN COME CHECKS WITH LIEN AMOUNTS           
         BZ    BS25                    IN THEM (SORTED BY LAST NAME)            
         MVI   SRTKSTA,SRTKSLN                                                  
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS25     OC    SRTTRST,SRTTRST     THEN COME CHECKS TO TRUSTEE'S                
         BZ    BS30                    (SORTED BY LAST NAME)                    
         CLI   SRTW4TY,TAW4TYTR                                                 
         BNE   BS30                                                             
         MVI   SRTKSTA,SRTKSW4T                                                 
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS30     CLC   SRTNET,=F'1000000'  THEN COME CHECKS WITH NET AMOUNTS OF         
         BL    BS40                   $10000.00 OR MORE (SORTED BY LAST         
         MVI   SRTKSTA,SRTKS50         NAME)                                    
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS40     CLI   RECNUM,PC           IF NOT PRINT CHECKS                          
         BE    BS50                                                             
         CLC   SRTNCDE,=H'1999'    THEN COME CHECKS FOR AGENT 1999 AND          
         BNE   *+12                    ASIAN CHECKS AND SPANISH CHECKS          
         MVI   SRTKSTA,SRTKS199                                                 
         B     BS50                                                             
         CLI   SRTCTYP,CTYASIAN                                                 
         BNE   BS45                                                             
         CLC   SRTUN,=C'SAG'       SPECIAL FOR ASIAN ONLY IF SAG                
         BNE   BS60                                                             
         MVI   SRTKSTA,SRTKASN                                                  
         B     BS50                                                             
BS45     CLI   SRTCTYP,CTYSPAN                                                  
         BNE   BS60                                                             
         CLC   SRTUN,=C'SAG'       SPECIAL FOR SPANISH ONLY IF SAG              
         BNE   BS60                                                             
         MVI   SRTKSTA,SRTKSSP                                                  
*                                                                               
BS50     MVC   SRTKAGY,SRTAGY      SORTED BY AGENCY, INVOICE, LAST NAME         
         MVC   SRTKINV,SRTINV                                                   
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS60     CLC   SRTUN,=C'AFT'       THEN COME CHECKS FOR AFTRA LOCALS            
         B     BS70                NO MORE AFTRA SORTING (SEP 2013)             
*                                  TREAT IT LIKE SAG                            
*                                                                               
*        BNE   BS70                    THAT GET UNION COPIES                    
         TM    SRTBITS,SRTBLCL                                                  
         BO    BS70                                                             
*                                                                               
         MVI   SRTKSTA,SRTKSAF     SORTED BY LOCAL, AGENCY, INVOICE,            
         MVC   SRTKLOCL,SRTLOCL        CAST SORT KEY, SSN, AND                  
         MVC   SRTKAGY,SRTAGY          CATEGORY                                 
         MVC   SRTKINV,SRTINV                                                   
         MVC   SRTKCST,SRTCST                                                   
         MVC   SRTKSSN,SRTSSN                                                   
         MVC   SRTKCAT,SRTCAT                                                   
         B     BS200                                                            
*                                                                               
BS70     CLC   SRTUN,=C'SAG'       THEN COME SAG CHECKS AND AFTRA               
         BE    BS80                    CHECKS FOR LOCALS THAT DON'T             
         CLC   SRTUN,=C'AFT'           GET UNION COPIES                         
         BE    BS80                                                             
         CLC   SRTUN,=C'NON'           NON-UNION WITH AGENTS ALSO               
         BNE   BS100                                                            
         CLC   SRTLOCL,=C'NYC'         DURING STRIKE ONLY                       
         BNE   BS100                                                            
*                                                                               
BS80     OC    SRTNCDE,SRTNCDE     WITHOUT AGENTS (SORTED BY LAST NAME)         
         BNZ   BS90                                                             
         MVI   SRTKSTA,SRTKSNA                                                  
         MVC   SRTKLST(32),LASTNAME                                             
         B     BS200                                                            
*                                                                               
BS90     MVI   SRTKSTA,SRTKSWA     THEN WITH AGENTS (SORTED BY AGENT            
         MVC   SRTKNCDE,SRTNCDE        CODE)                                    
         B     BS200                                                            
*                                                                               
BS100    CLC   SRTUN,=C'AFM'       THEN COME AFM CHECKS                         
         BE    BS120                                                            
         CLC   SRTUN,=C'ACT'                                                    
         BE    BS120                                                            
         CLC   SRTUN,=C'UDA'                                                    
         BE    BS120                                                            
         CLI   RECNUM,CN           OR CANADIAN DOLLAR CHECKS                    
         BNE   BS190                                                            
BS120    MVI   SRTKSTA,SRTKSAM     SORTED BY UNION,LOCAL,AGENCY,INV,            
         MVC   SRTKUN,SRTUN                                                     
         MVC   SRTKLOCL,SRTLOCL        CAST SORT KEY, SSN, AND                  
         MVC   SRTKAGY,SRTAGY          CATEGORY                                 
         MVC   SRTKINV,SRTINV                                                   
         MVC   SRTKCST,SRTCST                                                   
         MVC   SRTKSSN,SRTSSN                                                   
         MVC   SRTKCAT,SRTCAT                                                   
         B     BS200                                                            
*                                                                               
BS190    MVI   SRTKSTA,SRTKSOT     THEN COME ALL OTHER CHECKS (SORTED           
         MVC   SRTKUN,SRTUN            BY UNION/LOCAL/LAST NAME)                
         MVC   SRTKLOCL,SRTLOCL                                                 
         MVC   SRTKLST(32),LASTNAME                                             
*                                                                               
BS200    MVC   SRTKSEQ,PROCSEQ+1   ALL CHECKS SORTED LOWEST BY PROCSEQ          
*                                                                               
         CLC   SRTAGY,=C'2801  '                                                
         BE    BS210                                                            
         CLC   SRTAGY,=C'8000  '                                                
         BE    BS210                                                            
         CLC   SRTAGY,=C'8013  '                                                
         BE    BS210                                                            
         CLC   SRTAGY,=C'8014  '                                                
         BE    BS210                                                            
         CLC   SRTAGY,=C'8015  '                                                
         BE    BS210                                                            
         CLC   SRTAGY,=C'8097  '                                                
         BNE   BS220               SORT AGENCIES 2801, 8000, 8013,              
BS210    MVI   SRTKSTA,SRTKAGY1    8014, 8015 AND 8097 FIRST                    
*                                                                               
BS220    OC    REQASOF,REQASOF     IF AS/OF DATE REQUESTED                      
         BZ    BS230                                                            
         CLC   DUEDATE,REQASOF     AND DUE DATE ON/AFTER AS/OF DATE             
         BL    BS230                                                            
         OI    SRTKSTA,SRTKASOF    THEN SET TO SORT SEPARATELY                  
*                                                                               
BS230    CLC   SRTUSE,=C'HLD'      IF USE IS HOLDING FEE                        
         BE    BS240                                                            
         CLC   SRTUSE,=C'ADH'      OR ADDENDUM HOLDING FEE                      
         BNE   BSX                                                              
BS240    MVI   SRTKSTA,SRTKHLD     SORT CHECK FIRST                             
BSX      XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE SEARCHES THE EMPLOYER RECORD FOR THE STATE U.C. # FOR            
* THE STATE PASSED IN PARAMETER 1 AND SAVES IT IN THE SORT RECORD.              
*                                                                               
*                                  P1=A(STATE FOR LOOKUP)                       
         DS    0D                                                               
VGETSUC  NMOD1 0,GETSUC                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         L     R2,0(R1)            R2=A(STATE CODE)                             
*                                                                               
         XC    SRTSUC,SRTSUC       PRE-CLEAR STATE U.C. #                       
*                                                                               
         L     RF,AEMPIO           IF EMPLOYER RECORD NOT FOUND                 
         CLI   0(RF),0                                                          
         BE    GSX                 THEN RETURN                                  
*                                                                               
         MVC   AIO,AEMPIO          SEARCH EMPLOYER RECORD FOR TAX ID            
         MVI   ELCODE,TATIELQ          ELEMENT FOR STATE OF WORK                
         MVI   FULL,TATITYUN                                                    
         MVC   FULL+1(3),0(R2)                                                  
         GOTO1 GETL,DMCB,(4,FULL)  FIRST TRY TO FIND UNEMPLOYMENT ID            
         BE    GS10                                                             
*                                                                               
         MVI   FULL,TATITYTX       THEN TRY TO FIND TAX ID                      
         GOTO1 GETL,DMCB,(4,FULL)                                               
         BNE   GSX                 RETURN IF NOT FOUND                          
*                                                                               
GS10     L     R4,TGELEM           SAVE STATE U.C. # IN SORT RECORD             
         USING TATID,R4                                                         
         MVC   SRTSUC,TATIID                                                    
*                                                                               
GSX      MVC   AIO,AIO1                                                         
         XIT1                                                                   
         DROP  R4                                                               
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE WILL PASS THE FIRST 4 PARAMETERS ALONG TO 'TRACE' IF             
* THE HOB OF PARM 1 IS ONE OF THE REQUESTED TRACE BYTES.                        
*                                                                               
         DS    0D                                                               
VDOTRACE NMOD1 0,DOTRAC                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         ZIC   RF,8(R1)            EXTRACT L'LITERAL                            
         ST    RF,12(R1)           AND MOVE TO P4                               
*                                                                               
         LA    R0,60               TEST 60 TRACE BYTES AGAINST ARGUMENT         
         LA    RE,REQTRACE                                                      
*                                                                               
DT10     CLI   0(RE),C' '          IF END OF TRACE BYTES THEN DONE              
         BNH   DTX                                                              
         CLC   0(1,RE),0(R1)       IF MATCH THEN DO TRACE                       
         BE    DT20                                                             
         LA    RE,1(RE)            ELSE BUMP TO NEXT TRACE BYTE                 
         BCT   R0,DT10                                                          
*                                                                               
         B     DTX                 NO MATCH - RETURN                            
*                                                                               
DT20     L     R4,AMASTD           SAVE AND CLEAR PRTQUE REMOTE KEY             
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
         MVC   SVREMOT,REMOTKEY                                                 
         XC    REMOTKEY,REMOTKEY                                                
*                                                                               
         GOTO1 TRACE               DO TRACE                                     
*                                                                               
         MVC   REMOTKEY,SVREMOT    RESTORE PRTQUE REMOTE KEY                    
         DROP  R4                                                               
*                                                                               
DTX      XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE INTERFACES INTO THE THE COMMON ROUTINE TO MAINTAIN               
* PASSIVE POINTERS ON THE FILE.                                                 
*                                                                               
         DS    0D                                                               
VADDPTRS NMOD1 0,ADDPTR                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                  INITIALIZE PARAMETER BYTE                    
         MVI   BYTE,X'08'+X'04'    SET PASSING A(NEW PTR BLOCK AREA)            
*                                  AND OK TO DELETE TLINBCDQ POINTER            
*                                                                               
         L     R3,AIO              IF NEW RECORD IS DELETED THEN SET            
         USING TLRCD,R3                FORCE DELETE PARAMETER BIT               
         TM    TLRCSTAT,X'80'                                                   
         BZ    *+8                                                              
         OI    BYTE,X'40'                                                       
*                                                                               
         CLI   RERUN,C' '          IF RERUNNING THEN PTRS MAY BE OUT            
         BNH   *+8                                                              
         OI    BYTE,X'02'          OF SYNC, SO SET TO ALLOW ALL CHANGES         
*                                                                               
         CLI   PTRTRACE,C'Y'       IF TRACING ADDPTR CALLS                      
         BNE   *+8                                                              
         OI    BYTE,X'10'          THEN SET APPROPRIATE PARAMETER BIT           
*                                                                               
         GOTO1 AADDPTRS,DMCB,(BYTE,ASPBLK),AAPBLK  MAINTAIN POINTERS            
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE RETURNS 'YES' IF THE CURRENT CHECK IS NOT TO UNDERGO             
* CALCULATIONS, THAT IS TO SAY, IT'S AMOUNTS ARE TO BE EXTRACTED.               
* OTHERWISE IT RETURN 'NO'.                                                     
*                                                                               
         DS    0D                                                               
VNOCALC  NMOD1 0,NOCALC                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         CLI   RERUN,C'Y'          IF PROGRAM REQUESTED FOR EXTRACTION          
         BE    NCYES                   ONLY THEN RETURN 'YES'                   
*                                                                               
         CLI   SRTADJS,0           ELSE IF CHECK IS A PAY ADJUSTMENT            
         BNE   NCYES               THEN RETURN 'YES'                            
*                                                                               
         TM    SRTPST1,TAPDPCRD    ELSE IF CHECK IS A CREDIT PAYMENT            
         BO    NCYES               THEN RETURN 'YES'                            
*                                                                               
         CLI   RERUN,C'D'          ELSE IF RERUN = DIED                         
         BNE   NCNO                                                             
*                                                                               
         L     R2,AIO              R2=A(CURRENT AIO)                            
         MVC   AIO,ASRTCHK         AND CHECK WAS ALREADY CALCULATED             
         MVI   ELCODE,TACDELQ          DURING RUN THAT DIED                     
         GOTO1 GETL,DMCB,0                                                      
         ST    R2,AIO              RESET A(CURRENT AIO)                         
         BE    *+6                                                              
         DC    H'0'                                                             
         L     R4,TGELEM                                                        
         USING TACDD,R4                                                         
         OC    TACDRUN,TACDRUN                                                  
         BNZ   NCYES               THEN RETURN 'YES'                            
         B     NCNO                                                             
*                                                                               
NCYES    SR    RC,RC               RETURN 'YES'                                 
NCNO     LTR   RC,RC               RETURN 'NO'                                  
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE RETURNS 'YES' IF THE CHECK IS NOT TO BE PRINTED.                 
* OTHERWISE IT RETURNS 'NO'.                                                    
*                                                                               
         DS    0D                                                               
VNOPRNT  NMOD1 0,NOPRNT                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         CLI   SRTADJS,0           IF CHECK IS PAYROLL ADJUSTMENT               
         BE    NP10                                                             
         TM    SRTADJS,TAPDADVD    AND ADJUSTMENT TYPE IS VOID                  
         BO    NPYES                                                            
         TM    SRTADJS,TAPDADIS    OR ISSUE                                     
         BO    NPYES                                                            
         TM    SRTADJS,TAPDADTT    OR TAX TRANSFER                              
         BO    NPYES                                                            
         TM    SRTADJS,TAPDADST    OR SSN TRANSFER                              
         BO    NPYES               THEN RETURN 'YES'                            
         TM    SRTADJS,TAPDADTR    IF TAX REFUND CHECK                          
         BZ    NP10                                                             
         OC    SRTTRST,SRTTRST     SKIP MINOR CHECK                             
         BZ    NP10                                                             
         CLI   RECNUM,EC           PRINT FOR EURO CHECKS                        
         BE    NPNO                                                             
*                                                                               
         OC    SRTNET,SRTNET       IN THE CASE WERE WE ARE ISSUING              
         BZ    NPYES               TRUSTEE CHECK                                
         B     NPNO                                                             
*                                                                               
NP10     OC    SRTPAY,SRTPAY       ELSE IF PAYMENT AMOUNT IS ZERO               
         BNZ   NPNO                                                             
         OC    SRTREXP,SRTREXP     AND REIMBURSED EXPENSES IS ZERO              
         BNZ   NPNO                                                             
         OC    SRTMDED,SRTMDED     AND MISCELLANEOUS DEDUCTIONS IS ZERO         
         BNZ   NPNO                                                             
         OC    SRTGST,SRTGST       AND GST IS ZERO                              
         BNZ   NPNO                                                             
         OC    SRTDUES,SRTDUES     AND UNION DUES IS ZERO                       
         BNZ   NPNO                                                             
         OC    SRTLIEN,SRTLIEN     AND LIEN AMOUNT IS ZERO                      
         BNZ   NPNO                                                             
         OC    SRTTRST,SRTTRST     AND W4 TRUSTEE AMOUNT IS ZERO                
         BNZ   NPNO                                                             
         TM    SRTADJS,TAPDADTR    AND NOT TAX REFUND CHECK                     
         BO    NPNO                                                             
*                                                                               
         TM    TGUSSTA2,HLDTYPE    IF THIS IS A HOLDING FEE PAYMENT             
         BZ    NPYES                                                            
         CLC   SRTONOF,=C'OFF'     AND AN OFF-CAMERA PERF.                      
         BNE   NPYES                                                            
         CLC   SRTCFCY,SRTCYCS     AND CAST FFC EQUAL TO CYCLE START            
         BNE   NPYES                                                            
         B     NPNO                                                             
*                                                                               
NPYES    SR    RC,RC               RETURN 'YES'                                 
NPNO     LTR   RC,RC               RETURN 'NO'                                  
         XIT1                                                                   
         SPACE                                                                  
         LTORG                                                                  
         EJECT                                                                  
* INITIALIZE TABLES AND OTHER PROGRAM CONTROL VARIABLES.                        
*                                                                               
         USING TASYD,R4            R4=A(SYSTEMS DETAILS ELEMENT)                
         DS    0D                                                               
VINITCTR NMOD1 0,INICTR                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'R',AIO),0,=C'SYSTEM'                             
         ZAP   CCVTRATE,TASYCCVT   SAVE CURRENT CANADIAN $ CONV RATE            
         CLI   RECNUM,EC                                                        
         BNE   *+10                                                             
         ZAP   CCVTRATE,TASYECVT   SAVE CURRENT EUROPEAN CONV RATE              
         MVC   BANKCODE,TASYUSBK   SAVE BANK ACCOUNT CODE                       
*                                                                               
         LA    R1,TASYUSST         R1=A(STARTING CHECK NUMBER)                  
         CLI   RECNUM,CN           CAN$ CHECKS USE SEPARATE STOCK               
         BNE   *+14                                                             
         MVC   BANKCODE,TASYCNBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYCNST                                                      
         CLI   RECNUM,EC           EURO CHECKS USE SEPARATE STOCK               
         BNE   *+14                                                             
         MVC   BANKCODE,TASYEUBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYEUST                                                      
         CLI   RECNUM,DC           DMB&B CHECKS USE SEPARATE STOCK              
         BNE   *+14                                                             
         MVC   BANKCODE,TASYDMBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYDMST                                                      
         CLI   RECNUM,PC           PRINT CHECKS USE SEPARATE STOCK              
         BNE   *+14                                                             
         MVC   BANKCODE,TASYPCBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYPCST                                                      
         CLI   RECNUM,PK           PAYROLL PLUS USE SEPARATE STOCK              
         BNE   *+14                                                             
         MVC   BANKCODE,TASYLCBK   AND DIFFERENT BANK ACCOUNT                   
         LA    R1,TASYLCST                                                      
*                                                                               
         MVC   CHKNBIN,=F'1'       DEFAULT BINARY CHECK NUMBER TO 1             
         OC    0(8,R1),0(R1)       IF CHECK NUMBER IS DEFINED                   
         BZ    IC5                                                              
         PACK  DUB,0(8,R1)         CONVERT CHECK NUMBER TO BINARY               
         CVB   RF,DUB                                                           
         ST    RF,CHKNBIN                                                       
IC5      ICM   RF,15,REQSCHK       IF OVERRIDE STARTING CHECK NUMBER            
         BZ    *+8                                                              
         ST    RF,CHKNBIN                                                       
*                                                                               
         L     RE,AERITAB          CLEAR ERROR INVOICE TABLE                    
         L     RF,=A(MAXERIS*ERILEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,AWARTAB          CLEAR WARNING CHECK TABLE                    
         L     RF,=A(MAXWARS*WARLEN+1)                                          
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         L     RE,ALCLTAB          CLEAR UNION LOCAL SORT TABLE                 
         L     RF,=A(MAXLCLS*6+1)                                               
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
         MVC   NEXTERI,AERITAB     SET NEXT ERITAB ENTRY TO FIRST               
         MVC   NEXTWAR,AWARTAB     SET NEXT WARTAB ENTRY TO FIRST               
*                                                                               
         MVC   CHKDATE1,TGNXTBUS   CONVERT CHECK DATE TO 6-BYTE CHAR            
         TM    REQOPTS,REQOCDTE    IF FORCING CHECK DATE                        
         BZ    IC10                                                             
         MVC   CHKDATE1,REQEND1     SET SAME AS THRU DTE                        
         OC    REQFORC,REQFORC     IS THERE A FORCE DATE?                       
         BZ    IC10                                                             
         MVC   CHKDATE1,REQFORC    IF YES, USE FORCE DATE                       
         TM    PRGSTAT,TESTSYS+CSCSYS+FQASYS                                    
         BNZ   IC20                                                             
IC10     CLI   REQURG,C'U'                                                      
         BNE   *+10                                                             
         MVC   CHKDATE1,TGTODAY1                                                
IC20     GOTO1 DATCON,DMCB,(1,CHKDATE1),(20,CHKDATEA)                           
*                                                                               
         GOTO1 DATCON,DMCB,(1,CHKDATE1),(0,DUB)                                 
         GOTO1 ADDAY,DMCB,(C'Y',DUB),DUB,-1                                     
         MVC   DUB+2(4),=C'1231'                                                
         GOTO1 DATCON,DMCB,(0,DUB),(20,CHKDTEAL)                                
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(1,CHKDATEL)  SAVE PWOS TOO             
*                                                                               
         XC    PREVEMP,PREVEMP     CLEAR PREVIOUS EMPLOYER OF RECORD            
         XC    PREVAGY,PREVAGY           PREVIOUS AGENCY CODE                   
         XC    PREVCLI,PREVCLI           PREVIOUS CLIENT CODE                   
*                                                                               
         MVC   PROCSEQ,=F'1'       INITIALIZE PROCESSED SEQ NUMBER TO 1         
*                                                                               
         TIME  DEC                 PUT BINARY HOURS/MINS IN HIGH ORDER          
         STCM  R0,12,HALF              TWO BYTES OF PROCSEQ                     
         ZAP   DUB,=P'0'                                                        
         MVO   DUB,HALF                                                         
         CVB   R1,DUB                                                           
         STH   R1,PROCSEQ                                                       
*                                                                               
         CLI   REQURG,C'U'         IF URGENT CHECK RUN THEN SET                 
         BNE   *+8                     URGENT BIT IN HIGH ORDER                 
         OI    PROCSEQ,TACDSQUR        BYTE OF PROCSEQ                          
*                                                                               
         MVI   TAPPTRAC,C'N'       INITIALIZE SPECIAL TRACE SWITCHES            
         MVI   YTDTRACE,C'N'                                                    
         MVI   PTRTRACE,C'N'                                                    
*                                                                               
         LA    RF,REQTRACE         LOOP THROUGH REQTRACE LOOKING FOR            
         LA    RE,L'REQTRACE           TAPPGUAR TRACE INDICATOR                 
*                                                                               
IC60     CLI   0(RF),C'T'          IF TRACE TAPPGUAR INDICATOR FOUND            
         BNE   *+8                                                              
         MVI   TAPPTRAC,C'Y'       SET FLAG TO TRACE TAPPGUAR CALLS             
*                                                                               
         CLI   0(RF),C'H'          IF TRACE YTD INDICATOR FOUND                 
         BNE   *+8                                                              
         MVI   YTDTRACE,C'Y'       SET FLAG TO TRACE TAYTD CALLS                
*                                                                               
         CLI   0(RF),C'O'          IF TRACE PTRS INDICATOR FOUND                
         BNE   *+8                                                              
         MVI   PTRTRACE,C'Y'       SET FLAG TO TRACE ADDPTR CALLS               
*                                                                               
         LA    RF,1(RF)            BUMP TO NEXT TRACE CODE                      
         BCT   RE,IC60             REPEAT UNTIL NO MORE TRACE CODES             
*                                                                               
         XC    TOTALS(TOTLEN),TOTALS  CLEAR ACCUMS FOR TOTALS PAGE              
         XC    TOTDIRCT,TOTDIRCT                                                
         XC    TOTPST,TOTPST                                                    
         XC    NOSRTCKS,NOSRTCKS                                                
*                                                                               
         XC    WORK(10),WORK       EXTRACT CONTROL FILE INFO FOR                
         MVC   WORK+8(2),ORIGID        SIGN-ON ID INTO GLOBAL STORAGE           
         GOTO1 USERVAL,DMCB,(X'80',WORK)                                        
*                                                                               
         GOTO1 SETMOS,DMCB,CHKDATEA,MOS  SET MONTH OF SERVICE                   
         GOTO1 (RF),(R1),CHKDTEAL,MOSL   SET SAME FOR END OF LAST YEAR          
*                                                                               
         ZAP   TOTRECS,=P'0'       INITIALIZE POSTING ACCUMULATORS              
         ZAP   TOTCASH,=P'0'                                                    
         ZAP   TOTCRDTS,=P'0'                                                   
VINITCX  XIT1                                                                   
         EJECT                                                                  
* ROUTINE SETS ACC MOS BASED ON CHECK DATE PASSED IN PARAMETER 1, WHICH         
* IS OF THE FORM CCYYMMDD.  MOS IS RETURNED IN FIELD AT PARAMETER 2.            
*                                                                               
SETMOS   NTR1                                                                   
         LM    R2,R3,0(R1)         R2=A(DATE), R3=A(MOS)                        
*                                                                               
         MVC   0(1,R3),3(R2)       SET MONTH OF SERVICE                         
         MVC   1(1,R3),5(R2)                                                    
*                                                                               
         CLI   4(R2),C'1'                                                       
         BNE   SETMOSX                                                          
*                                                                               
         MVI   1(R3),C'A'                                                       
         CLI   5(R2),C'0'                                                       
         BE    SETMOSX                                                          
*                                                                               
         MVI   1(R3),C'B'                                                       
         CLI   5(R2),C'1'                                                       
         BE    SETMOSX                                                          
*                                                                               
         MVI   1(R3),C'C'                                                       
*                                                                               
SETMOSX  B     VINITCX                                                          
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*              ROUTINE TO CALL TAPPGUAR                                         
         SPACE 1                                                                
         DS    0D                                                               
VCALLTAP NMOD1 0,CTAPP                                                          
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         TM    SRTINVS,TAINSBIL    IF INVOICE BILLED                            
         BO    CALLPYES                                                         
         TM    SRTINVS2,TAINSHLP   OR PUR PRINTED                               
         BO    CALLPYES                                                         
         CLI   SRTADJS,0           OR THIS IS ADJUSTMENT INVOICE                
         BNE   CALLPYES                                                         
         CLI   REQTAPP,C'Y'        OR OPTION TO SUPPRESS TAPPGUAR CALLS         
         BE    CALLPYES            THEN RETURN                                  
*                                                                               
         CLI   TAPPTRAC,C'Y'       IF TRACING TAPPGUAR CALLS                    
         BNE   *+8                                                              
         OI    4(R1),X'40'         THEN SET TRACE BIT IN PARAMETER 2            
*                                                                               
         L     R4,AMASTD           SAVE AND CLEAR PRTQUE REMOTE KEY             
         USING MASTD,R4                                                         
         L     R4,MCVREMOT                                                      
         USING REMOTED,R4                                                       
         MVC   SVREMOT,REMOTKEY                                                 
         XC    REMOTKEY,REMOTKEY                                                
*                                                                               
         GOTO1 =V(TAPPGUAR)        CALL TAPPGUAR - PARMS ALREADY SET            
*                                                                               
         MVC   REMOTKEY,SVREMOT    RESTORE PRTQUE REMOTE KEY                    
CALLPX   XIT1                                                                   
*                                                                               
CALLPYES CR    RC,RC                                                            
         B     CALLPX                                                           
         DROP  R4                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE GETS THE CURRENT ENTRY FROM PYTDTAB FOR THIS PERFORMER           
* AND USES IT AS INPUT TO TALIM ALONG WITH THE VALUES FROM THIS CHECK           
* IN ORDER TO UPDATE PYTDTAB AND TO ADD A NEW YTD ELEMENT (TAYE) TO             
* THE CHECK.  IT ALSO SETS SEVERAL YTD SORT FIELDS.                             
*                                                                               
         DS    0D                                                               
VUPDPYTD NMOD1 0,UPPYTD                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         MVC   AIO,ASRTCHK         REMOVE EXISTING YTD EARNINGS EL.             
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 DELL,DMCB,(1,=AL1(TAYETCHK))                                     
         MVC   AIO,AIO1                                                         
         SPACE 1                                                                
         GOTO1 GETPYTD             GET ENTRY FOR THIS EMPLOYER/SSN              
         SPACE 1                                                                
         L     R5,DMCB                                                          
         USING PYTDTABD,R5         R5=A(PYTDTAB RECORD)                         
         SPACE 1                                                                
         LA    R4,BLOCK            R4=A(TALIM INTERFACE BLOCK)                  
         USING TMDD,R4                                                          
         XCEF  (R4),'TMLNQ'        CLEAR INTERFACE TO TALIM                     
         MVC   TMEMP,SRTEMP        EMPLOYER                                     
         MVI   TMCURR,C'U'         IF CURRENCY IS US THEN TMCURR = U            
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TMCURR,C'C'         ELSE TMCURR = C                              
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   TMCURR,C'E'         ELSE TMCURR = E                              
         MVC   TMSSN,SRTSSN        S/S NUMBER                                   
         MVC   TMW4TYPE,SRTW4TY    W4 TYPE                                      
         MVC   TMUNIT,SRTTXST      TAXABLE STATE                                
         MVI   TMSTAT,TMSCHK       REQUESTING PAYROLL DATA                      
         ZAP   TMCCVT,CCVTRATE     CONVERSION RATE                              
         GOTO1 DATCON,DMCB,(9,CHKDATEA),(0,TMEFDTE0)                            
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    UPYTD10                                                          
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(0,TMEFDTE0)                            
UPYTD10  ST    RC,TMRC             A(GEND)                                      
         SPACE 1                                                                
         TM    SRTADJS,TAPDADST    IF SSN TRANSFER                              
         BZ    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYCO    AND PERFORMER IS NOT CORPORATION             
         BE    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYCA    OR CANADIAN                                  
         BE    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYTR    OR TRUSTEE                                   
         BE    UPYTD20                                                          
         CLI   SRTW4TY,TAW4TYFO    OR FOREIGNER                                 
         BE    UPYTD20                                                          
         MVI   ELCODE,TACWELQ      LOOK FOR FEDERAL WITHHOLDING ELEM            
         MVC   AIO,ASRTCHK                                                      
*        GOTO1 GETL,DMCB,=C'FD '                                                
         LA    RF,=C'FD '                                                       
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         LA    RF,=C'EU '                                                       
         GOTO1 GETL,DMCB,(3,(RF))                                               
         MVC   AIO,AIO1                                                         
         BNE   UPYTD30             NOT FOUND - DON'T PASS EARNINGS              
         L     R3,TGELEM                                                        
         USING TACWD,R3                                                         
         TM    TACWSTAT,TACWSTAX   IF FEDERAL NOT TAXABLE                       
         BZ    UPYTD30             THEN NO EARNINGS TO UPDATE                   
         DROP  R3                                                               
*                                                                               
*UPYTD20  MVC   TMTXEARN,SRTEARN    PASS TAXABLE EARNINGS                       
UPYTD20  ICM   RF,15,SRTEARN       PASS TAXABLE EARNINGS                        
         ICM   RE,15,SRTTXRE       + TAXABLE REIMBURSEMENTS                     
         AR    RF,RE                                                            
*                                                                               
         STCM  RF,15,TMTXEARN                                                   
         ICM   RF,15,SRTNTAX                                                    
*                                                                               
         BRAS  RE,TSTPCAN          P+ CANADIAN?                                 
         JNE   UPYTD25                                                          
         LARL  RE,SVTXRE                                                        
         ICM   RF,15,0(RE)         TAXABLE REIMBURSEMENTS                       
         ICM   RE,15,SRTREXP       TOTAL REIMBURSEMENTS                         
         SR    RE,RF               RE = NON TAXABLE REIMBURSMENTS               
         ICM   RF,15,SRTNTAX                                                    
         SR    RF,RE                                                            
         XC    TMTXEARN,TMTXEARN   ALL EARNINGS ARE IN NON-TAX EARNINGS         
*                                                                               
UPYTD25  STCM  RF,15,TMNTEARN      NON-TAXABLE EARNINGS                         
         GOTOR CONVCAD,DMCB,TMNTEARN,TMNTCERN  CONVERT TO CAD                   
*                                                                               
UPYTD30  MVC   TMYTD(TMYTDN*4),PYTDAMTS  PASS PREVIOUS YTD AMOUNTS              
         MVC   TMCYTD(TMCYTDN*4),PYTDCAMT  PASS PREVIOUS P+ CAN AMOUNTS         
         L     RF,=A(SUIYTD)                                                    
         MVC   0(4,RF),TMYSUI      SET PREVIOUS YTD SUI                         
*                                                                               
         BRAS  RE,TSTPCAN          P+ CANADIAN?                                 
         JNE   UPYTD35                                                          
         OI    TMSTAT2,TMSPCAN                                                  
         MVC   TGTHREE,SRTW4CP                                                  
         MVI   TGTHREE+2,C' '                                                   
         MVI   ELCODE,TAATELQ      GET CAST UNIT                                
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,0                                                      
         MVC   AIO,AIO1                                                         
         BNE   UPYTD35             NOT FOUND - DON'T PASS EARNINGS              
         L     R3,TGELEM                                                        
         USING TAATD,R3                                                         
         CLI   TAATLEN,TAATLN2Q                                                 
         JL    UPYTD35                                                          
         OC    TAATCCVT,TAATCCVT                                                
         JZ    *+10                                                             
         MVC   TMCCVT,TAATCCVT     CONVERSION RATE WHEN CHECK WAS CUT           
*                                                                               
UPYTD35  MVI   ELCODE,TACAELQ      GET CAST UNIT                                
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,0                                                      
         MVC   AIO,AIO1                                                         
         BNE   UPYTD40             NOT FOUND - DON'T PASS EARNINGS              
         L     R3,TGELEM                                                        
         USING TACAD,R3                                                         
         GOTO1 =A(GETPSTR),DMCB,TACAUNIT   GET PST RATE AND HST INDICAT         
*                                                                               
UPYTD40  GOTO1 =V(TALIM),DMCB,TMD    OFF TO TALIM                               
*                                                                               
         GOTO1 DOTRACE,DMCB,(C'H',TMD),TMLNQ,=C'TALIM BLK'                      
*                                                                               
         LA    R3,ELEMENT          BUILD NEW YTD ELEMENT                        
         USING TAYED,R3                                                         
         XC    TAYEEL(TAYELN2Q),TAYEEL                                          
         MVI   TAYEEL,TAYEELQ                                                   
         MVI   TAYELEN,TAYELNQ                                                  
         MVI   TAYETYPE,TAYETCHK            CHECK TYPE                          
         MVC   TAYENYTD(TMYTDN*4),TMNYTD    NEW YTD AMOUNTS                     
         MVC   TAYETXBL(TMTNAMT*4),TMTAXBLE TAXABLE AMOUNTS THIS CHECK          
*                                                                               
         BRAS  RE,TSTPCAN          P+ CANADIAN?                                 
         BNE   UPYTD50                                                          
         MVI   TAYELEN,TAYELN2Q                                                 
         MVC   TAYECYTD(TMCYTDN*4),TMCNYTD   NEW YTD AMOUNTS                    
         MVC   TAYECTXB(TMCTNAMT*4),TMCTTAXB TAXABLE AMOUNTS THIS CHECK         
*                                                                               
UPYTD50  MVC   SRTYEARN,TAYEEARN   SAVE NEW YTD TAXABLE EARNINGS                
         MVC   SRTYNTAX,TAYENTAX                NON-TAXABLE EARNINGS            
         SPACE 1                                                                
*                                                                               
         MVC   AIO,ASRTCHK         ADD NEW ELEMENT TO CHECK RECORD              
         GOTO1 ADDELEM                                                          
         MVC   AIO,AIO1                                                         
         SPACE 1                                                                
         LA    R5,WORK             R5=A(PYTDTAB ENTRY)                          
         USING PYTDTABD,R5                                                      
         XC    0(PYTDLEN,R5),0(R5) BUILD PYTDTAB ENTRY                          
         MVC   PYTDEMP,SRTEMP      EMPLOYER                                     
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN      SOCIAL SECURITY NUMBER                       
         SPACE 1                                                                
         LA    RE,TMNYTD           RE=A(NEW YTD AMOUNTS)                        
         LA    RF,TMYTD            RF=A(OLD YTD AMOUNTS)                        
         LA    R2,PYTDAMTS         R2=A(YTD AMOUNTS THIS TIME)                  
         LA    R0,L'PYTDAMTS/4     R0=N'YTD AMOUNTS                             
         SPACE 1                                                                
UPYTD90  L     R1,0(RE)            NEW YTD                                      
         S     R1,0(RF)            LESS OLD YTD                                 
         ST    R1,0(R2)            IS YTD THIS TIME                             
         SPACE 1                                                                
         LA    RE,4(RE)            BUMP TO NEXTS                                
         LA    RF,4(RF)                                                         
         LA    R2,4(R2)                                                         
         BCT   R0,UPYTD90                                                       
*                                                                               
         BRAS  RE,TSTPCAN          P+ CANADIAN?                                 
         JNE   UPYTD100                                                         
*                                                                               
         LA    RE,TMCNYTD          RE=A(NEW YTD AMOUNTS)                        
         LA    RF,TMCYTD           RF=A(OLD YTD AMOUNTS)                        
         LA    R2,PYTDCAMT         R2=A(YTD AMOUNTS THIS TIME)                  
         LA    R0,L'PYTDCAMT/4     R0=N'YTD AMOUNTS                             
         SPACE 1                                                                
UPYTD95  L     R1,0(RE)            NEW YTD                                      
         S     R1,0(RF)            LESS OLD YTD                                 
         ST    R1,0(R2)            IS YTD THIS TIME                             
         SPACE 1                                                                
         LA    RE,4(RE)            BUMP TO NEXTS                                
         LA    RF,4(RF)                                                         
         LA    R2,4(R2)                                                         
         BCT   R0,UPYTD95                                                       
         SPACE 1                                                                
UPYTD100 GOTO1 ADDBIN,DMCB,APYTDTAB,(R5)  ADD TO PYTDTAB ENTRY                  
         SPACE 1                                                                
         GOTO1 ADDBIN,DMCB,APYTDINV,(R5)  ADD TO PYTDINV ENTRY                  
UPYTDX   XIT1                                                                   
*                                                                               
SVNSUI   DS    F                   SUI                                          
SVTSUI   DS    F                   TAXABLE SUI                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*----------------------------------------------------------------------         
*        SETUP FOR HST CALCULATIONS                                             
*----------------------------------------------------------------------         
GETPSTR  NMOD1 0,GETPST                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         L     R5,0(R1)            CANADIAN PROVINCE                            
         NI    TGPRVST,X'FF'-TGPSTHST-TGPSTQUE                                  
         XC    TGPSTRAT,TGPSTRAT         NO PST                                 
         TM    TGPRVST,TGPSNHST    NO HST PRIOR TO 2010 JUL 01                  
         BO    GETPX                                                            
*                                                                               
         CLC   0(3,R5),=C'   '                                                  
         BNH   GETPX                                                            
*                                                                               
         MVC   AIO,AIO2            GET SYSTEM RECORD FOR CANADIAN PROV          
         GOTO1 RECVAL,DMCB,TLSYCDQ,(X'20',0)                                    
         BE    *+6                 RATE                                         
         DC    H'0'                                                             
*                                                                               
         MVC   AIO,AIO1                                                         
         L     R4,AIO2                                                          
         USING TAPCD,R4                                                         
         MVI   ELCODE,TAPCELQ         GET CANADIAN PROVINCE ELEMENT             
         BRAS  RE,GETEL                                                         
         BNE   GETPX                                                            
         ZIC   R1,TAPCNSUB                                                      
         LA    R3,TAPCPROV                                                      
E1       USING TAPCPROV,R3                                                      
GETP100  CLC   E1.TAPCPROV,0(R5)      MATCH ON PROVINCE                         
         BE    GETP200                                                          
         AHI   R3,TAPCSLNQ            BUMP TO NEXT PROVINCE                     
         BCT   R1,GETP100                                                       
         B     GETPX                  LEAVE, PROVINCE NOT IN TABLE              
*                                                                               
GETP200  MVC   TGBYTE3,TGPRVST                                                  
         MVC   TGPRVST,E1.TAPCSTAT    SET HST INDICATOR                         
         TM    TGPRVST,TGPSTHST       FOR NOW, ONLY CALC PST WHEN HST           
         BZ    GETPX                                                            
         MVC   TGPSTRAT,E1.TAPCPSTR   SET PST RATE                              
         CLC   0(3,R5),=C'QC '        QUEBEC                                    
         BE    GETP210                                                          
         CLC   0(3,R5),=C'PE '        OR PRINCE EDWARD ISLAND                   
         BNE   GETPX                                                            
GETP210  OI    TGPRVST,TGPSTQUE       USE QUEBEC CALCULATION                    
         TM    TGBYTE3,TGPRE111       IF BILLED PRIOR TO JAN01/11               
         BZ    GETPX                                                            
         MVC   TGPSTRAT,=F'750'       PST RATE IS 7.50%                         
*                                                                               
GETPX    XIT1                                                                   
         DROP  R4                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE LOOPS THROUGH THE ESUTAB, DUETAB, LINTAB, & TRSTABS AND          
* CLEARS OUT THE INVOICE TOTALS IF THE INVOICE WAS NOT IN ERROR.                
* OTHERWISE IT PUTS BACK THE INVOICE TOTALS INTO THE TABLE ENTRIES SO           
* AS TO UNDO THE EFFECT OF THE INVOICE ON THE TABLES.                           
*                                                                               
         DS    0D                                                               
VFLUSHI  NMOD1 0,FLUSHI                                                         
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         SAM31                                                                  
         L     R2,AESUINV          R2 = A(ESUINV BINSRCH PARAMETERS)            
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO ESUINV ENTRIES THEN SKIP               
         BZ    FI40                                                             
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS NOT IN ERROR THEN              
         BE    FI30                CLEAR INVOICE TABLE                          
*                                                                               
         LA    R5,BINTABLE         ELSE SET TO BACK OUT INV AMOUNTS             
         USING ESUTABD,R5          R5 = A(ESUINV)                               
         L     R4,BININ            R4 = NUMBER OF ESUINV ENTRIES                
*                                                                               
FI10     MVC   SRTEMP,ESUEMP       SET FIELDS FOR ESUTAB LOOKUP                 
         MVC   SRTSSN,ESUSSN                                                    
*        GOTO1 GETESU,DMCB,ESUUNIT  GET CORRESPONDING ESUTAB ENTRY              
         MVC   TGTHREE,ESUUNIT                                                  
         GOTO1 GETESU,DMCB,TGTHREE  GET CORRESPONDING ESUTAB ENTRY              
*                                                                               
*        L     R3,DMCB             R3 = A(AMOUNTS IN ESUTAB ENTRY)              
*                                                                               
         L     RF,0(R1)            GET ESUTAB ENTRY INTO BLOCK                  
         LA    R3,BLOCK                                                         
         XC    BLOCK,BLOCK                                                      
         MVC   BLOCK(ESULEN),0(RF)                                              
*                                                                               
         LA    R3,ESUAMNTS-ESUTABD(R3)                                          
*                                                                               
         LA    RE,ESUAMNTS         RE = A(AMOUNTS IN ESUINV ENTRY)              
         LA    R1,ESUAMNTL/4       R1 = NUMBER OF AMOUNTS                       
*                                                                               
FI20     L     RF,0(R3)            SUBTRACT INVOICE AMOUNT FROM AMOUNT          
         S     RF,0(RE)                FOR ENTIRE CHECK RUN                     
         ST    RF,0(R3)                                                         
*                                                                               
         LA    RE,4(RE)            BUMP TO NEXT AMONNTS                         
         LA    R3,4(R3)                                                         
         BCT   R1,FI20             REPEAT UNTIL NO MORE AMOUNTS                 
*                                                                               
         LA    R5,ESULEN(R5)       BUMP TO NEXT ESUINV ENTRY                    
         BCT   R4,FI10             REPEAT UNTIL NO MORE ENTRIES                 
*                                                                               
FI30     SAM31                                                                  
         L     R2,AESUINV                                                       
         XC    BININ,BININ         CLEAR ESUINV TABLE                           
         L     RF,=AL4(MAXCAST*5*ESULEN)                                        
         XCEFL BINTABLE                                                         
         SAM24                                                                  
*                                                                               
FI40     BAS   RE,FLUSHDUE         FLUSH DUE COMPANY TABLE                      
         BAS   RE,FLUSHLIN         FLUSH LIEN TABLE                             
         BAS   RE,FLUSHTRS         FLUSH TRUSTEE TABLE                          
*                                                                               
         L     R2,APYTDINV         R2 = A(PYTDINV BINSRCH PARAMETERS)           
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO PYTDINV ENTRIES THEN SKIP              
         BZ    FI200                                                            
*                                                                               
         CLI   INVTERR,0           IF INVOICE IS NOT IN ERROR THEN              
         BE    FI130               CLEAR INVOICE TABLE                          
*                                                                               
         LA    R5,BINTABLE         ELSE SET TO BACK OUT INV AMOUNTS             
         USING PYTDTABD,R5         R5 = A(PYTDINV)                              
         L     R4,BININ            R4 = NUMBER OF PYTDINV ENTRIES               
*                                                                               
FI110    MVC   SRTEMP,PYTDEMP      SET FIELDS FOR PYTDTAB LOOKUP                
         MVC   SRTSSN,PYTDSSN                                                   
         GOTO1 GETPYTD             GET CORRESPONDING PYTDTAB ENTRY              
*                                                                               
         L     R3,DMCB             R3 = A(AMOUNTS IN PYTDTAB ENTRY)             
         LA    R3,PYTDAMTS-PYTDTABD(R3)                                         
*                                                                               
         LA    RE,PYTDAMTS         RE = A(AMOUNTS IN PYTDINV ENTRY)             
         LA    R1,L'PYTDAMTS/4     R1 = NUMBER OF AMOUNTS                       
*                                                                               
FI120    L     RF,0(R3)            SUBTRACT INVOICE AMOUNT FROM AMOUNT          
         S     RF,0(RE)                FOR ENTIRE CHECK RUN                     
         ST    RF,0(R3)                                                         
*                                                                               
         LA    RE,4(RE)            BUMP TO NEXT AMONNTS                         
         LA    R3,4(R3)                                                         
         BCT   R1,FI120            REPEAT UNTIL NO MORE AMOUNTS                 
*                                                                               
         LA    R5,PYTDLEN(R5)      BUMP TO NEXT PYTDINV ENTRY                   
         BCT   R4,FI110            REPEAT UNTIL NO MORE ENTRIES                 
*                                                                               
FI130    XC    BININ,BININ         CLEAR PYTDINV TABLE                          
         LH    RF,=Y(MAXCAST*PYTDLEN)                                           
         XCEFL BINTABLE                                                         
*                                                                               
FI200    CLI   INVTERR,0           IF NO ERROR THIS INVOICE                     
         BNE   FI210               THEN CALL TAPPGUAR TO CLEAR INV TOTS         
         GOTO1 CALLTAPP,DMCB,(RC),(X'10',0),,SYSCOMM                            
         B     FIX                                                              
*                                  ELSE CALL TAPPGUAR TO SUBTR INV TOTS         
FI210    GOTO1 CALLTAPP,DMCB,(RC),(X'08',0),,SYSCOMM                            
FIX      SAM24                                                                  
         XIT1                                                                   
         DROP  R5                                                               
         EJECT                                                                  
*              ROUTINE TO FLUSH DUETAB                                          
         SPACE 1                                                                
FLUSHDUE NTR1                                                                   
         L     R2,ADUETAB          R2 = A(DUETAB BINSRCH PARAMETERS)            
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO DUETAB ENTRIES THEN SKIP               
         BZ    FIX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(DUETAB)                               
         USING DUETABD,R5                                                       
         L     R4,BININ            R4 = NUMBER OF DUETAB ENTRIES                
*                                                                               
FDUE5    CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    FDUE10                                                           
*                                                                               
         LA    R3,DUEELEM          THEN R3 = DUE COMPANY DETAILS ELEM           
         USING TADUD,R3                                                         
         L     RF,TADUCOL          SUBTRACT INVOICE AMOUNT FROM                 
         ICM   RE,15,DUEINV            COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         ST    RF,TADUCOL                                                       
*                                                                               
FDUE10   XC    DUEINV,DUEINV       CLEAR INVOICE AMOUNT                         
         LA    R5,DUELEN(R5)       BUMP TO NEXT DUETAB ENTRY                    
         BCT   R4,FDUE5            REPEAT UNTIL NO MORE ENTRIES                 
         B     FIX                                                              
         DROP  R3,R5                                                            
         EJECT                                                                  
*              ROUTINE TO FLUSH LINTAB                                          
         SPACE 1                                                                
FLUSHLIN NTR1                                                                   
         L     R2,ALINTAB          R2 = A(LINTAB BINSRCH PARAMETERS)            
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO LINTAB ENTRIES THEN SKIP               
         BZ    FIX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(LINTAB)                               
         USING LINTABD,R5                                                       
         L     R4,BININ            R4 = NUMBER OF LINTAB ENTRIES                
*                                                                               
FLIN5    CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    FLIN10                                                           
         LA    R3,LINELEM          THEN R3 = LIEN DETAILS ELEMENT               
         USING TALND,R3                                                         
         ICM   RF,15,TALNCOL       SUBTRACT INVOICE AMOUNT FROM                 
         ICM   RE,15,LININV            COLLECTED AMOUNT                         
         SR    RF,RE                                                            
         STCM  RF,15,TALNCOL                                                    
*                                                                               
FLIN10   XC    LININV,LININV       CLEAR INVOICE AMOUNT                         
         LA    R5,LINLEN(R5)       BUMP TO NEXT LINTAB ENTRY                    
         BCT   R4,FLIN5            REPEAT UNTIL NO MORE ENTRIES                 
         B     FIX                                                              
         DROP  R3,R5                                                            
         SPACE 2                                                                
*              ROUTINE TO FLUSH TRUSTEE TABLE                                   
         SPACE 1                                                                
FLUSHTRS NTR1                                                                   
         L     R2,ATRSTAB          R2 = A(TRUSTEE BINSRCH PARAMETERS)           
         USING BIND,R2                                                          
         OC    BININ,BININ         IF NO TABLE ENTRIES THEN SKIP                
         BZ    FIX                                                              
*                                                                               
         LA    R5,BINTABLE         R5 = A(TRUSTEE TABLE)                        
         USING TRSTABD,R5                                                       
         L     R4,BININ            R4 = NUMBER OF ENTRIES                       
*                                                                               
FTRS5    CLI   INVTERR,0           IF INVOICE IS IN ERROR                       
         BE    FTRS10                                                           
         ICM   RF,15,TRSTCOL       SUBTRACT COLLECTED AMT FROM INV AMT          
         ICM   RE,15,TRSTINV       COLLECTED AMOUNT                             
         SR    RF,RE                                                            
         STCM  RF,15,TRSTCOL                                                    
*                                                                               
FTRS10   XC    TRSTINV,TRSTINV     CLEAR INVOICE AMOUNT                         
         LA    R5,TRSTLEN(R5)      BUMP TO NEXT ENTRY                           
         BCT   R4,FTRS5            REPEAT UNTIL NO MORE ENTRIES                 
         B     FIX                                                              
         DROP  R5                                                               
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE GETS THE CURRENT PERFORMER'S PYTDTAB ENTRY AND RETURNS           
* ITS ADDRESS IN DMCB.                                                          
*                                                                               
VGETPYTD NMOD1 0,PYTD                                                           
         L     RC,SAVERC           RESTORE RC                                   
*                                                                               
         LA    R5,BLOCK            BUILD ENTRY FOR THIS EMPLOYER/SSN            
         USING PYTDTABD,R5                                                      
         XC    0(PYTDLEN,R5),0(R5)                                              
         MVC   PYTDEMP,SRTEMP                                                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    PYTDEMP,X'DF'       TURN OFF X'40' BIT IN EMPLOYER               
         MVC   PYTDSSN,SRTSSN                                                   
*                                                                               
         L     R2,APYTDTAB         R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
*                                                                               
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),1             MUST BE FOUND                                
         BNE   *+6                                                              
         DC    H'0'                                                             
         XIT1                                                                   
         EJECT                                                                  
* THIS ROUTINE GETS THE CURRENT PERFORMER'S QTDTAB ENTRY AND RETURNS            
* ITS ADDRESS IN DMCB.                                                          
*                                                                               
VGETQTD  NMOD1 0,GQTD                                                           
         L     RC,SAVERC           RESTORE RC                                   
                                                                                
GQ10     LA    R5,BLOCK            BUILD ENTRY FOR THIS EMP/SSN/UNIT            
         USING QTDTABD,R5                                                       
         XC    BLOCK(QTDLEN),BLOCK                                              
         MVC   QTDEMP,SRTEMP       EMPLOYER                                     
         MVC   QTDSSN,SRTSSN       SOCIAL SECURITY NUMBER                       
         MVC   QTDUNIT,=C'HI '     UNIT                                         
                                                                                
         GOTO1 ADDBIN,DMCB,AQTDTAB,(R5)                                         
                                                                                
         CLI   0(R1),0             FOUND THE ENTRY, ADDRESS IN DMCB             
         JE    GQX                                                              
         BRAS  RE,BLDQTD           NOT FOUND, BUILD IT                          
         J     GQ10                GO BACK AND FIND ENTRY ADDED                 
                                                                                
GQX      XIT1                                                                   
         EJECT                                                                  
* THIS ROUTINE BUILDS THE CURRENT PERFORMER'S QTDTAB ENTRY                      
*                                                                               
BLDQTD   NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R3,KEY                                                           
         USING TLCKPD,R3                                                        
         MVI   TLCKPCD,TLCKYCDQ    PAYROLL YTD                                  
         MVC   TLCKYEAR,CHKDATEA   YEAR                                         
         MVI   TLCKYCUR,C'U'       CURRENCY                                     
         CLI   RECNUM,CN                                                        
         BNE   *+8                                                              
         MVI   TLCKYCUR,C'C'                                                    
         CLI   RECNUM,EC                                                        
         BNE   *+8                                                              
         MVI   TLCKYCUR,C'E'                                                    
                                                                                
         MVC   TLCKYEMP,SRTEMP     EMPLOYER                                     
         MVC   TLCKYSSN,SRTSSN     SSN                                          
         MVC   TLCKYTXU,=C'HI '    TAX UNIT                                     
         MVC   TLCKYDTE,CHKDATE1                                                
         XC    TLCKYDTE,=X'FFFFFF' COMPLEMENT IT                                
         MVC   BLDQTRD,CHKDATE1                                                 
         MVI   BLDQTRD+2,1         SET THIS QTR DAY TO 1                        
         MVI   BLDQTRD+1,1         SET THIS QTR MONTH TO JAN                    
         CLI   CHKDATE1+1,3                                                     
         BNH   BLDQTD5                                                          
         MVI   BLDQTRD+1,4         SET THIS QTR MONTH TO APR                    
         CLI   CHKDATE1+1,6                                                     
         BNH   BLDQTD5                                                          
         MVI   BLDQTRD+1,7         SET THIS QTR MONTH TO JUL                    
         CLI   CHKDATE1+1,9                                                     
         BNH   BLDQTD5                                                          
         MVI   BLDQTRD+1,10        SET THIS QTR MONTH TO OCT                    
                                                                                
BLDQTD5  XC    BLDQTRD,=X'FFFFFF' COMPLEMENT IT                                 
                                                                                
         MVC   FILENAME,=CL8'CHKDIR'                                            
         GOTO1 HIGH                                                             
         XC    FILENAME,FILENAME                                                
                                                                                
         CLC   KEY(TLCKYDTE-TLCKPD),KEYSAVE   SURE SAME KEY?                    
         BNE   BLDQTDX                        NO, ADD WITH ZEROS                
         CLC   TLCKYDTE,BLDQTRD        CHECK DATE BEFORE THIS QTR?              
         BH    BLDQTDX                 YES, ADD WITH ZEROS                      
                                                                                
         MVC   FILENAME,=CL8'CHKFIL'                                            
         GOTO1 GETREC                                                           
         XC    FILENAME,FILENAME                                                
                                                                                
         L     R4,AIO                                                           
         MVI   ELCODE,TACQELQ      FIND QTD ELEMENT IN THIS CHECK               
         BRAS  RE,GETEL                                                         
         BNE   BLDQTDX             NONE, ADD WITH ZEROS                         
                                                                                
         USING TACQD,R4                                                         
         USING QTDTABD,R5                                                       
         LA    R5,BLOCK            R5=A(PYTDTAB ENTRY)                          
         XC    BLOCK(QTDLEN),BLOCK BUILD QTDTAB ENTRY                           
         MVC   QTDEMP,SRTEMP       EMPLOYER                                     
         MVC   QTDSSN,SRTSSN       SOCIAL SECURITY NUMBER                       
         MVC   QTDUNIT,=C'HI '     UNIT                                         
         MVC   QTDEARN,TACQEARN    YTD TOTALS                                   
         MVC   QTDSDI,TACQSDI                                                   
                                                                                
         GOTO1 ADDBIN,DMCB,AQTDTAB,(R5)  ADD QTDTAB ENTRY                       
                                                                                
BLDQTDX  J     GQX                                                              
                                                                                
BLDQTRD  DS    PL3                 THIS QTR START DATE                          
BLDQUNIT DS    CL3                 THIS QTR UNIT                                
         EJECT                                                                  
* EXTRACT SESSION DETAILS INFORMATION IF A SESSION DETAILS ELEMENT              
* EXISTS ON THE CHECK RECORD.                                                   
*                                                                               
EXTRSESS NTR1  BASE=*,LABEL=*                                                   
         L     R4,ASRTCHK          R4 = A(SESSION DETAILS ELEMENT)              
         MVI   ELCODE,TASDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   XIT                 RETURN IF NONE FOUND                         
         USING TASDD,R4                                                         
*                                                                               
*        TM    TGCAUNI,AFM         IF MUSICIAN                                  
         GOTO1 UNITEST,DMCB,TGCAUNIS,AFM,0,0,0                                  
         JZ    ES10                                                             
         MVC   SRTSP,TASDMSP       THEN SAVE SPOTS IN SORT RECORD               
         J     ES30                                                             
*                                                                               
ES10     CLI   SRTMED,TACOMEDR     ELSE IF RADIO COMMERCIAL                     
         JNE   ES20                                                             
         MVC   SRTSP,TASDRSP       THEN SAVE SPOTS IN SORT RECORD               
         MVC   SRTTAG,TASDRTG                TAGS                               
         J     ES30                                                             
*                                                                               
ES20     MVC   SRTSP,TASDSP        ELSE SAVE SPOTS IN SORT RECORD               
         MVC   SRTDAY,TASDDAY                DAYS                               
         MVC   SRTOT,TASDOT                  OVERTIME HOURS                     
         MVC   SRTDT,TASDDT                  DOUBLE-TIME HOURS                  
         MVC   SRTTRV,TASDTRV                TRAVEL HOURS                       
         MVC   SRTPDW,TASDPDW                PRIOR DAY WARDROBE HOURS           
         MVC   SRTTAG,TASDTAG                TAGS                               
*                                                                               
ES30     CLC   SRTUSE,=C'RTK'      IF USE IS RTK                                
         JNE   XIT                                                              
         MVC   SRTIST,TASDIST      SAVE STATUS                                  
         J     XIT                                                              
         DROP  R4                                                               
                                                                                
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* THIS ROUTINE UPDATES THE TAXABLE SUI AMOUNTS FOR THE CHECK                    
* BOTH TAYETSUI AND EACH UNIT'S TACYTSUI WILL BE UPDATED IF TAXABLE             
* SUI IS TAKEN                                                                  
*                                                                               
*                                                                               
UPDSUI   NTR1  BASE=*,LABEL=*                                                   
         MVC   AIO,ASRTCHK                                                      
         CLI   RECNUM,PK           IF EVENT CHECK RUN                           
         BNE   UPDSUIX                                                          
         BRAS  RE,TSTPCAN          P+ CANADIAN?                                 
         JE    UPDSUIX                                                          
*                                                                               
         XC    SUITAX,SUITAX                                                    
*                                                                               
         GOTO1 GETPYTD             READ ENTRY FOR THIS EMPLOYER/SSN             
         L     R5,DMCB                                                          
         USING PYTDTABD,R5         R5=A(PYTDTAB RECORD)                         
*                                                                               
         LA    R2,BLOCK            R4=A(TALIM INTERFACE BLOCK)                  
         USING TMDD,R2                                                          
         XCEF  (R2),'TMLNQ'        CLEAR INTERFACE TO TALIM                     
         MVC   TMEMP,SRTEMP        EMPLOYER                                     
         MVI   TMCURR,C'U'         IF CURRENCY IS US THEN TMCURR = U            
         MVC   TMSSN,SRTSSN        S/S NUMBER                                   
         MVC   TMW4TYPE,SRTW4TY    W4 TYPE                                      
         MVI   TMSTAT,TMSCHK       REQUESTING PAYROLL DATA                      
         GOTO1 DATCON,DMCB,(9,CHKDATEA),(0,TMEFDTE0)                            
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    US20                                                             
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(0,TMEFDTE0)                            
US20     ST    RC,TMRC             A(GEND)                                      
*                                                                               
         TM    TGSYSTA2,TASYSROI   KEEP ORDER OF INPUT?                         
         BO    US500                                                            
*                                                                               
         USING TACDD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   UPDSUIX                                                          
         ICM   RF,15,TACDEARN      TEST IF NEGATIVE CHECK                       
         LTR   RF,RF                                                            
         BNP   US100                                                            
*                                                                               
         USING TACYD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACYELQ     READ THROUGH STATE YTD WITHHOLDING            
         BRAS  RE,GETEL           ELEMENTS                                      
         J     *+8                                                              
US30     BRAS  RE,NEXTEL                                                        
         JNE   US50                                                             
                                                                                
         CLI   TACYUNIT+2,C' '                                                  
         JH    US30                                                             
         CLC   TACYUNIT,=C'FD '                                                 
         JE    US30                                                             
         CLC   TACYUNIT,=C'CN '                                                 
         JE    US30                                                             
*                                                                               
         MVC   TMUNIT,TACYUNIT                                                  
*                                                                               
         ICM   RE,15,TACYEARN                                                   
         CLI   TACYLEN,TACYLN3Q                                                 
         BL    *+14                                                             
         ICM   RF,15,TACYTXRE                                                   
         AR    RE,RF                                                            
         STCM  RE,15,TMTXEARN      SET YTD TAXABLE WAGES                        
*                                                                               
         GOTO1 =V(TALIM),DMCB,TMD    OFF TO TALIM                               
*                                                                               
* LOGIC FOR CALCULATING TAXABLE SUI:                                            
* MAXIMUM BASE SUI LIMIT - YTD TAXABLE SUI = X                                  
* IF X < 0, THEN TACYTSUI=0                                                     
*    ELSE TOTAL TAXABLE WAGES - X = Y                                           
* IF Y < 0, THEN TACYTSUI = TOTAL TAXABLE WAGES                                 
*    ELSE TACYTSUI = X                                                          
*                                                                               
         XC    SUIUTAX,SUIUTAX                                                  
*                                                                               
         L     RF,TMBSUI           MAX BASE SUI LIMIT                           
         L     RE,SUIYTD           - YTD TAXABLE SUI                            
         SR    RF,RE               IF < 0 THEN NO TAXABLE SUI FOR THIS          
         BNP   US37                UNIT                                         
         ST    RF,TGFULL                                                        
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         TM    SRTADJS,TAPDADIS    ISSUE?                                       
         BZ    *+12                                                             
         L     RF,SRTEARN                                                       
         B     US35                                                             
*                                                                               
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TACYUNIT)                                           
         BNE   US40                                                             
         MVC   AIO,ASRTCHK                                                      
*                                                                               
         L     R1,TGELEM                                                        
         USING TATUD,R1                                                         
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE               TOTAL TAXABLE WAGES                          
*                                                                               
US35     ST    RF,SUIUTAX          DEFAULT = TOTAL TAXABLE WAGES                
         L     RE,TGFULL                                                        
         SR    RF,RE                                                            
         BNP   *+10                                                             
         MVC   SUIUTAX,TGFULL                                                   
*                                                                               
         L     RF,SUIYTD           UPDATE YTD TAXABLE SUI FOR                   
         L     RE,SUIUTAX          CURRENT CHECK                                
         AR    RF,RE                                                            
         ST    RF,SUIYTD                                                        
*                                                                               
US37     ST    R4,TGFULL           A(CURRENT TACY ELEM)                         
*                                                                               
         MVI   0(R4),X'FF'                                                      
         MVI   ELCODE,X'FF'                                                     
         GOTO1 REMELEM                                                          
*                                                                               
         LA    R4,ELEM                                                          
         MVI   TACYEL,TACYELQ                                                   
         MVI   TACYLEN,TACYLN4Q                                                 
         MVC   TACYTSUI,SUIUTAX    SET TAXABLE SUI FOR THIS UNIT                
         MVC   TACYYSUI,SUIYTD     SET YTD SUI INCLUDING THIS UNIT              
*                                                                               
         L     RF,SUITAX                                                        
         L     RE,SUIUTAX                                                       
         AR    RF,RE                                                            
         ST    RF,SUITAX           ACCUMULATE TOTAL CHECK TAXABLE SUI           
*                                                                               
         MVI   ELCODE,TACYELQ                                                   
         GOTO1 ADDELEM                                                          
         L     R4,TGFULL           RESTORE A(CURRENT TACY ELEM)                 
*                                                                               
US40     MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TACYELQ                                                   
         B     US30                                                             
*                                                                               
         USING TAYED,R4                                                         
US50     MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         JNE   UPDSUIX                                                          
         L     R4,TGELEM                                                        
         MVC   TAYESUI,SUIYTD      UPDATE YTD SUI AMOUNT                        
         MVC   TAYETSUI,SUITAX     UPDATE TAXABLE SUI AMOUNT FOR CHECK          
         J     UPDSUIX                                                          
         DROP  R1,R4                                                            
*                                                                               
* NEGATIVE CHECK - NEED TO START FROM LAST UNIT TO REMOVE TAXABLE               
* SUI AND UPDATE CORRECT YTD SUI                                                
*                                                                               
US100    XC    SUITAX,SUITAX                                                    
         MVI   SUIFLAG,0                                                        
*                                                                               
* GET LAST UNIT TO INITIALIZE YTD SUI                                           
*                                                                               
         BAS   RE,GETLAST          GET LAST UNIT (LASTUNIT)                     
         JNE   UPDSUIX                                                          
*                                                                               
         USING TACYD,R4            GET LAST UNIT ESUTAB ENTRY                   
         L     R4,LASTUNIT                                                      
         GOTO1 GETLIMIT,DMCB,TACYUNIT   GET SUI LIMIT FOR THIS STATE            
         GOTO1 GETESUI,DMCB,TACYUNIT  GET ESUTAB ENTRY IN AESUUNIT              
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         L     R5,AESUUNIT                                                      
         MVC   SUIYTD,ESUYSUI      INITIALIZE YTD SUI                           
         SAM24                                                                  
*                                                                               
         L     RF,SUIYTD                                                        
         L     RE,SUILIMIT                                                      
         SR    RF,RE               TEST IF VOIDING ENTIRE CHECK'S SUI           
         BP    US130                                                            
         OI    SUIFLAG,SUIFVALL    VOID ENTIRE CHECK'S SUI                      
*                                                                               
* VOID ENTIRE CHECK'S SUI                                                       
*                                                                               
         XC    TGTHREE,TGTHREE                                                  
         GOTO1 GETYEARN,DMCB,TGTHREE    GET YTD EARNINGS BEFORE VOID            
*                                                                               
* GET LAST UNIT TO INITIALIZE YTD SUI                                           
*                                                                               
         BAS   RE,GETLAST          GET LAST UNIT (LASTUNIT)                     
         JNE   UPDSUIX                                                          
*                                                                               
         USING TACYD,R4            GET LAST UNIT ESUTAB ENTRY                   
         L     R4,LASTUNIT                                                      
         GOTO1 GETESUI,DMCB,TACYUNIT  GET ESUTAB ENTRY IN AESUUNIT              
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         L     R5,AESUUNIT                                                      
         MVC   SUIYTD,ESUYSUI      INITIALIZE YTD SUI                           
         SAM24                                                                  
*                                                                               
* SET TAXABLE SUI FOR EACH STATE AND TOTAL SUI AFTER VOID                       
*                                                                               
US110    BAS   RE,GETLAST          GET LAST UNIT (LASTUNIT)                     
         JNE   US120                                                            
*                                                                               
         USING TACYD,R4                                                         
         L     R4,LASTUNIT                                                      
         GOTO1 GETESUI,DMCB,TACYUNIT    GET ESUTAB ENTRY IN AESUUNIT            
         GOTO1 GETLIMIT,DMCB,TACYUNIT   GET SUI LIMIT FOR THIS STATE            
         GOTO1 GETYSUI,DMCB,TACYUNIT,0  GET YTD SUI AFTER THE VOID              
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         L     R5,AESUUNIT                                                      
         MVC   TACYTSUI,ESUTSUI    SET TAXABLE SUI FOR STATE                    
         MVI   TACYUNIT+3,X'FF'    MARK PROCESSED                               
         SAM24                                                                  
         J     US110                                                            
*                                                                               
* SET STATES WITH YTD SUI AFTER VOID                                            
*                                                                               
US120    BAS   RE,CLEARFF          CLEAR PROCESSED FLAG                         
*                                                                               
         USING TACYD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACYELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
US125    BRAS  RE,NEXTEL                                                        
         JNE   US160                                                            
         CLI   TACYUNIT+2,C' '     NO CITIES                                    
         JNE   US125                                                            
         CLI   TACYLEN,TACYLN4Q                                                 
         JL    US125                                                            
*                                                                               
         GOTO1 GETESUI,DMCB,TACYUNIT   GET ESUTAB ENTRY IN AESUUNIT             
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         L     R5,AESUUNIT                                                      
         MVC   TACYYSUI,SUIYTD     SET STATE W/ YTD SUI AFTER VOID              
         MVC   ESUYSUI,TACYYSUI    UPDATE YTD SUI IN ESUTAB                     
         SAM24                                                                  
         J     US125                                                            
         DROP  R4,R5                                                            
*                                                                               
* START FROM LAST UNIT                                                          
*                                                                               
* CALCULATION:                                                                  
*                                                                               
* IF YTD EARNINGS > YTD SUI                                                     
*        TAX SUI = 0                                                            
*        YTD SUI DOES NOT CHANGE                                                
* ELSE                                                                          
*        TAX SUI = YTD EARNINGS - YTD SUI                                       
*        YTD SUI = YTD EARNINGS                                                 
*                                                                               
US130    BAS   RE,GETLAST          GET LAST UNIT (LASTUNIT)                     
         JNE   US160                                                            
*                                                                               
         USING TACYD,R4                                                         
         L     R4,LASTUNIT                                                      
         GOTO1 GETYEARN,DMCB,TACYUNIT   GET YTD EARNINGS BEFORE VOID            
         GOTO1 GETLIMIT,DMCB,TACYUNIT   GET SUI LIMIT FOR THIS STATE            
*                                                                               
         L     RF,CURYEARN                                                      
         L     RE,SUIYTD                                                        
         SR    RF,RE               IS YTD EARN > YTD SUI?                       
         BNP   US140                                                            
         XC    TACYTSUI,TACYTSUI                                                
         MVC   TACYYSUI,SUIYTD                                                  
         J     US150                                                            
*                                                                               
US140    MVC   TACYYSUI,CURYEARN   YTD SUI = YTD EARNINGS                       
         MVC   SUIYTD,CURYEARN                                                  
*                                                                               
         L     RF,CURYEARN                                                      
         L     RE,SUILIMIT                                                      
         SR    RF,RE               YTD EARN - SUI LIMIT                         
         STCM  RF,15,TACYTSUI                                                   
*                                                                               
         L     RE,SUITAX           UPDATE CHECK'S SUI                           
         AR    RF,RE                                                            
         ST    RF,SUITAX                                                        
*                                                                               
         USING ESUTABD,R5                                                       
US150    SAM31                                                                  
         L     R5,AESUUNIT                                                      
         MVC   ESUYSUI,SUIYTD      UPDATE YTD SUI IN ESUTAB                     
         SAM24                                                                  
*                                                                               
         MVI   TACYUNIT+3,X'FF'    MARK PROCESSED                               
         J     US130                                                            
*                                                                               
US160    BAS   RE,CLEARFF          CLEAR PROCESSED FLAG                         
*                                                                               
         USING TAYED,R4                                                         
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         JNE   UPDSUIX                                                          
         L     R4,TGELEM                                                        
         MVC   TAYETSUI,SUITAX     UPDATE TAXABLE SUI AMOUNT FOR CHECK          
         J     UPDSUIX                                                          
*                                                                               
* CALCULATE TAXABLE SUI AND YTD SUI (RETAIN ORDER OF INPUT)                     
*                                                                               
         USING TACDD,R4                                                         
US500    L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   UPDSUIX                                                          
         ICM   RF,15,TACDEARN      TEST IF NEGATIVE CHECK                       
         LTR   RF,RF                                                            
         BM    US600                                                            
*                                                                               
* SUITTSUI FILLED IN FROM TACKTAX                                               
*                                                                               
         USING SUITABD,R3                                                       
         L     R3,ASUITAB                                                       
US510    CLI   0(R3),X'FF'                                                      
         JE    US550                                                            
         OC    SUITTSUI,SUITTSUI   ANY TAXABLE SUI EARNINGS?                    
         JZ    US540                                                            
*                                                                               
         MVC   TGTHREE,SUITUNIT                                                 
         CLI   SUITUNIT+2,C' '     STATE?                                       
         BE    US512                                                            
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
*                                                                               
US512    L     RF,SUIYTD           UPDATE YTD TAXABLE SUI FOR                   
         ICM   RE,15,SUITTSUI      CURRENT CHECK                                
         AR    RF,RE                                                            
         ST    RF,SUIYTD                                                        
*                                                                               
         USING TACYD,R4                                                         
US520    MVI   ELCODE,TACYELQ                                                   
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   US540                                                            
         L     R4,TGELEM                                                        
*                                                                               
         CLI   TACYLEN,TACYLN4Q                                                 
         BNL   US530                                                            
*                                                                               
         MVI   0(R4),X'FF'                                                      
         MVI   ELCODE,X'FF'                                                     
         GOTO1 REMELEM                                                          
*                                                                               
         LA    R4,ELEM                                                          
         MVI   TACYEL,TACYELQ                                                   
         MVI   TACYLEN,TACYLN4Q                                                 
         MVC   TACYTSUI,SUITTSUI   SET TAXABLE SUI FOR THIS UNIT                
         GOTO1 ADDELEM                                                          
         B     US535                                                            
*                                                                               
US530    ICM   RF,15,TACYTSUI                                                   
         ICM   RE,15,SUITTSUI      SET TAXABLE SUI FOR THIS UNIT                
         AR    RF,RE                                                            
         STCM  RF,15,TACYTSUI                                                   
*                                                                               
US535    L     RF,SUITAX                                                        
         L     RE,SUIUTAX                                                       
         AR    RF,RE                                                            
         ST    RF,SUITAX           ACCUMULATE TOTAL CHECK TAXABLE SUI           
*                                                                               
US540    AHI   R3,SUITABLN                                                      
         J     US510                                                            
         DROP  R3                                                               
*                                                                               
         USING TACYD,R4                                                         
US550    L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACYELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
US555    BRAS  RE,NEXTEL                                                        
         JNE   US570                                                            
         CLI   TACYUNIT+2,C' '                                                  
         JNE   US555                                                            
         CLC   TACYUNIT,=C'FD '                                                 
         JE    US555                                                            
         CLC   TACYUNIT,=C'CN '                                                 
         JE    US555                                                            
         CLI   TACYLEN,TACYLN4Q                                                 
         JL    US555                                                            
*                                                                               
         USING SUITABD,R3                                                       
         L     R3,ASUITAB                                                       
US560    CLI   0(R3),X'FF'         TOOK SUI IN THIS CHECK?                      
         JE    US555                                                            
         MVC   TGTHREE,SUITUNIT                                                 
*                                                                               
         CLI   SUITUNIT+2,C' '     STATE?                                       
         BE    US565                                                            
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
*                                                                               
US565    CLC   TACYUNIT,TGTHREE                                                 
         JE    *+12                                                             
         AHI   R3,SUITABLN                                                      
         J     US560                                                            
         DROP  R3                                                               
*                                                                               
         GOTO1 GETESUI,DMCB,TACYUNIT  GET ESUTAB ENTRY IN AESUUNIT              
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         L     R5,AESUUNIT                                                      
         MVC   FULL,ESUYSUI        INITIALIZE YTD SUI                           
         SAM24                                                                  
*                                                                               
         ICM   RF,15,TACYTSUI      UPDATE SUI YTD FOR UNIT                      
         A     RF,FULL                                                          
         STCM  RF,15,TACYYSUI                                                   
         J     US555                                                            
*                                                                               
         USING TAYED,R4                                                         
US570    MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         JNE   UPDSUIX                                                          
         L     R4,TGELEM                                                        
         MVC   TAYESUI,SUIYTD      UPDATE YTD SUI AMOUNT                        
         MVC   TAYETSUI,SUITAX     UPDATE TAXABLE SUI AMOUNT FOR CHECK          
         J     UPDSUIX                                                          
         DROP  R4                                                               
*                                                                               
* VOID                                                                          
*                                                                               
US600    GOTO1 GETYEARN,DMCB,0     GET YTD EARNINGS                             
         XR    R0,R0                                                            
*                                                                               
US610    BAS   RE,GETLAST          GET LAST UNIT                                
         JNE   US680                                                            
         USING SUITABD,R3                                                       
         L     R3,LASTUNIT                                                      
         MVC   TGTHREE,SUITUNIT                                                 
*                                                                               
         CLI   SUITUNIT+2,C' '     STATE?                                       
         BE    US620                                                            
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
*                                                                               
US620    GOTO1 GETLIMIT,DMCB,TGTHREE    GET SUI LIMIT FOR THIS STATE            
         GOTO1 GETESUI,DMCB,TGTHREE     GET ESUTAB ENTRY IN AESUUNIT            
*                                                                               
         L     RE,SUILIMIT         STATE LIMIT                                  
         L     R1,CURYEARN         YTD NOT INCLUDING THIS CHECK                 
         SR    R1,R0               - SUI FROM PREVIOUS STATE                    
         ICM   R3,15,SUITAMNT                                                   
         LTR   R3,R3               IF STATE AMOUNT IS NEGATIVE,                 
         BNM   *+6                 MAKE POSITIVE                                
         LCR   R3,R3                                                            
         SR    R1,R3               YTD - NEW SUI - STATE TAXABLE AMOUNT         
         CR    R1,RE               IF NEW YTD >= STATE SUI LIMIT,               
         BL    US630                                                            
         L     R4,SUIYTD           OLD SUIYTD                                   
         SR    R4,R0               - SUI FROM PREVIOUS STATE                    
         SR    RE,R4               SUI LIMIT - SUIYTD                           
         BNP   US670               DON'T TAKE SUI ON THIS STATE                 
         CR    RE,R3               IF MAX TAXABLE >= STATE TAXABLE AMT          
         BNL   US640               USE ENTIRE AMOUNT                            
         LR    R3,RE               OTHERWISE USE DIFFERENCE                     
         B     US640               OF SUI LIMIT - SUIYTD                        
US630    SR    RE,R1               STATE LIMIT - NEW YTD                        
         CR    RE,R3               IF DIFFERENCE < STATE TAXABLE AMT            
         BNL   US640                                                            
         AR    R0,RE               ADD DIFFERENCE TO SUI TAXABLE AMOUNT         
         ST    RE,TGFULL                                                        
         B     US650                                                            
US640    AR    R0,R3               ADD TO SUI TAXABLE AMOUNT                    
         ST    R3,TGFULL                                                        
*                                                                               
         USING TACYD,R4                                                         
US650    MVI   ELCODE,TACYELQ                                                   
         MVC   AIO,ASRTCHK                                                      
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         L     R4,TGELEM                                                        
         CLI   TACYLEN,TACYLN4Q                                                 
         BL    US670                                                            
         LR    RF,R0                                                            
         LNR   RF,RF                                                            
         STCM  RF,15,TACYTSUI      SET NEW TAXABLE SUI FOR THIS CHECK           
*                                                                               
         ICM   RF,15,TACYYSUI      UPDATE YTD SUI                               
         S     RF,TGFULL                                                        
         BNM   US660                                                            
         SR    RF,RF                                                            
US660    STCM  RF,15,TACYYSUI                                                   
*                                                                               
         L     RF,SUITAX           ACCUMULATE SUI REMOVED FOR CHECK             
         A     RF,TGFULL                                                        
         ST    RF,SUITAX                                                        
*                                                                               
         USING ESUTABD,R5                                                       
         SAM31                                                                  
         L     R5,AESUUNIT                                                      
         MVC   ESUYSUI,TACYYSUI    UPDATE YTD SUI IN ESUTAB                     
         SAM24                                                                  
*                                                                               
         USING SUITABD,R3                                                       
US670    L     R3,LASTUNIT                                                      
         MVI   SUITFLAG,SUITFPRO   MARK PROCESSED                               
         J     US610                                                            
*                                                                               
         USING TAYED,R4                                                         
US680    MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         JNE   UPDSUIX                                                          
         L     R4,TGELEM                                                        
         L     RF,SUIYTD                                                        
         S     RF,SUITAX                                                        
         STCM  RF,15,TAYESUI       UPDATE YTD SUI AMOUNT                        
*        MVC   TAYESUI,SUIYTD      UPDATE YTD SUI AMOUNT                        
         MVC   TAYETSUI,SUITAX     UPDATE TAXABLE SUI AMOUNT FOR CHECK          
         DROP  R4                                                               
*                                                                               
UPDSUIX  J     XIT                                                              
         DROP  R5                                                               
*                                                                               
* GET LAST UNIT (ADDRESS SET IN LASTUNIT)                                       
*                                                                               
GETLAST  NTR1                                                                   
         XC    LASTUNIT,LASTUNIT                                                
*                                                                               
         TM    TGSYSTA2,TASYSROI   KEEP ORDER OF INPUT?                         
         BO    GLAST50                                                          
*                                                                               
         USING TACYD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TACYELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
GLAST10  BRAS  RE,NEXTEL                                                        
         JNE   GLAST70                                                          
         CLI   TACYUNIT+2,C' '     NO CITIES                                    
         JNE   GLAST10                                                          
         CLI   TACYUNIT+3,X'FF'    ALREADY PROCESSED                            
         JE    GLAST10                                                          
         CLI   TACYLEN,TACYLN4Q                                                 
         JL    GLAST10                                                          
         ST    R4,LASTUNIT         SET A(LAST TACY)                             
         J     GLAST10                                                          
*                                                                               
* RETAIN ORDER OF INPUT                                                         
*                                                                               
         USING SUITABD,R3                                                       
GLAST50  L     R3,ASUITAB                                                       
GLAST55  CLI   0(R3),X'FF'                                                      
         JE    GLAST70                                                          
         TM    SUITSTAT,SUITWAGE+SUITTAX                                        
         JZ    GLAST60                                                          
*        CLI   SUITUNIT+2,C' '     NO CITIES                                    
*        JNE   GLAST60                                                          
         CLI   SUITFLAG,SUITFPRO   ALREADY PROCESSED?                           
         JE    GLAST60                                                          
         ST    R3,LASTUNIT                                                      
GLAST60  AHI   R3,SUITABLN                                                      
         J     GLAST55                                                          
*                                                                               
GLAST70  OC    LASTUNIT,LASTUNIT                                                
         JZ    NO                                                               
*                                                                               
GETLASTX J     YES                                                              
         DROP  R3,R4                                                            
*                                                                               
* CLEAR OUT PROCESSED MARKER                                                    
*                                                                               
CLEARFF  NTR1                                                                   
         USING TACYD,R4                                                         
         L     R4,ASRTCHK          CLEAR OUT TEMPORARY PROCESSED FLAG           
         MVI   ELCODE,TACYELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
CFF10    BRAS  RE,NEXTEL                                                        
         JNE   XIT                                                              
         MVI   TACYUNIT+3,0                                                     
         J     CFF10                                                            
         DROP  R4                                                               
*                                                                               
* CALCULATION (SET IN SUIYTD)                                                   
*        YTD EARNINGS - VOID STATE EARNINGS = X                                 
*        IF X < SUI LIMIT                                                       
*           YTD SUI = YTD SUI - VOIDED STATE EARNINGS                           
*           ESUTSUI = X                                                         
*        ELSE                                                                   
*           YTD SUI STAYS THE SAME                                              
*                                                                               
GETYSUI  NTR1                                                                   
         L     RF,0(R1)                                                         
         MVC   TGTHREE,0(RF)                                                    
         MVC   BYTE,7(R1)                                                       
*                                                                               
         XC    TGFULL,TGFULL                                                    
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         JNE   GETYSUIX                                                         
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
*                                                                               
*        ICM   RF,15,TATUWAGE                                                   
*        ICM   RE,15,TATUSTRE                                                   
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         ST    RF,CURUEARN         VOIDED STATE EARNINGS                        
*                                                                               
         L     RF,CURYEARN         YTD EARNINGS -                               
         L     RE,CURUEARN         VOIDED STATE EARNINGS                        
         AR    RF,RE               = X                                          
*                                                                               
         SAM31                                                                  
         USING ESUTABD,R5                                                       
         L     R5,AESUUNIT                                                      
*                                                                               
         L     RE,SUILIMIT         SUILIMIT                                     
         SR    RF,RE               X - SUILIMIT                                 
*                                                                               
         L     RE,CURUEARN         IF X - SUILIMIT IS MORE THAN VOIDED          
         CR    RF,RE               EARNINGS,                                    
         BH    *+8                                                              
         L     RF,CURUEARN         THEN REMOVE ALL EARNINGS FROM SUI            
*                                                                               
         STCM  RF,15,ESUTSUI       SET TAXABLE SUI FOR STATE                    
*                                                                               
         L     RE,SUIYTD           YTD SUI - TAXABLE SUI FOR STATE              
         AR    RF,RE                                                            
         ST    RF,SUIYTD           UPDATED YTD SUI                              
*                                                                               
         L     RE,SUITAX           UPDATE CHECK'S TAXABLE SUI                   
         ICM   RF,15,ESUTSUI                                                    
         AR    RF,RE                                                            
         ST    RF,SUITAX                                                        
*                                                                               
         L     RF,CURYEARN         UPDATE YTD EARNINGS                          
         L     RE,CURUEARN                                                      
         AR    RF,RE                                                            
         ST    RF,CURYEARN                                                      
         SAM24                                                                  
*                                                                               
GETYSUIX J     XIT                                                              
         DROP  R4,R5                                                            
*                                                                               
* SET YTD SUI TO YTD EARNINGS BEFORE VOID                                       
*                                                                               
GETYEARN NTR1                                                                   
         L     RF,0(R1)                                                         
         MVC   TGTHREE,0(RF)                                                    
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TAYEELQ                                                   
         GOTO1 GETL,DMCB,(1,=AL1(TAYETCHK))                                     
         L     RF,TGELEM                                                        
         USING TAYED,RF                                                         
         MVC   CURYEARN,TAYEEARN   YTD EARNINGS AFTER VOID                      
         DROP  RF                                                               
*                                                                               
         TM    TGSYSTA2,TASYSROI   KEEP ORDER OF INPUT?                         
         BO    GETY50                                                           
*                                                                               
         USING TATUD,R4                                                         
         L     R4,ASRTCHK          TATU'S AFTER DUE COMP                        
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
GETY10   BRAS  RE,NEXTEL                                                        
         JNE   GETYX                                                            
         CLC   TATUUNIT,=C'FD '                                                 
         JE    GETY10                                                           
         CLC   TATUUNIT,=C'CN '                                                 
         JE    GETY10                                                           
         CLI   TATUUNIT+2,C' '                                                  
         JNE   GETY10                                                           
*                                                                               
         TM    SUIFLAG,SUIFVALL    VOID ENTIRE CHECK'S SUI?                     
         JO    *+14                                                             
         CLC   TATUUNIT,TGTHREE                                                 
         JE    GETYX                                                            
*                                                                               
         ICM   RF,15,TATUWAAD                                                   
         ICM   RE,15,TATUTNAD                                                   
         AR    RF,RE                                                            
         LPR   RF,RF                                                            
*                                                                               
         L     RE,CURYEARN                                                      
         AR    RF,RE                                                            
         ST    RF,CURYEARN                                                      
*                                                                               
         TM    SUIFLAG,SUIFVALL    VOID ENTIRE CHECK'S SUI?                     
         JZ    GETY10                                                           
         OC    TGTHREE,TGTHREE     UNIT SUPPLIED?                               
         JZ    GETY10                                                           
         CLC   TATUUNIT,TGTHREE                                                 
         JNE   GETY10                                                           
         J     GETYX                                                            
*                                                                               
         USING SUITABD,R3                                                       
GETY50   L     R3,ASUITAB                                                       
GETY55   CLI   0(R3),X'FF'         GET YTD EARNINGS UP TO THIS UNIT             
         JE    GETYX                                                            
*        L     RF,LASTUNIT                                                      
*        CR    R3,RF                                                            
*        BE    GETYX                                                            
         TM    SUITSTAT,SUITWAGE+SUITTAX                                        
         JZ    GETY57                                                           
         L     RF,CURYEARN                                                      
         ICM   RE,15,SUITAMNT                                                   
         AR    RF,RE                                                            
         ST    RF,CURYEARN                                                      
GETY57   AHI   R3,SUITABLN                                                      
         B     GETY55                                                           
*                                                                               
GETYX    J     XIT                                                              
         DROP  R4                                                               
*                                                                               
* GET SUI LIMIT FOR STATE (SET IN SUILIMIT)                                     
*     PARAM 1 = STATE                                                           
*                                                                               
GETLIMIT NTR1                                                                   
         XC    SUILIMIT,SUILIMIT                                                
*                                                                               
         L     R3,0(R1)                                                         
         MVC   TGTHREE,0(R3)                                                    
*                                                                               
         LA    R2,BLOCK            R4=A(TALIM INTERFACE BLOCK)                  
         USING TMDD,R2                                                          
         XCEF  (R2),'TMLNQ'        CLEAR INTERFACE TO TALIM                     
         MVC   TMEMP,SRTEMP        EMPLOYER                                     
         MVI   TMCURR,C'U'         IF CURRENCY IS US THEN TMCURR = U            
         MVC   TMSSN,SRTSSN        S/S NUMBER                                   
         MVC   TMW4TYPE,SRTW4TY    W4 TYPE                                      
         MVI   TMSTAT,TMSCHK       REQUESTING PAYROLL DATA                      
         GOTO1 DATCON,DMCB,(9,CHKDATEA),(0,TMEFDTE0)                            
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    GL10                                                             
         GOTO1 DATCON,DMCB,(9,CHKDTEAL),(0,TMEFDTE0)                            
GL10     ST    RC,TMRC             A(GEND)                                      
         MVC   TMUNIT,TGTHREE                                                   
*                                                                               
         GOTO1 =V(TALIM),DMCB,TMD    OFF TO TALIM                               
*                                                                               
         MVC   SUILIMIT,TMBSUI                                                  
GLX      J     XIT                                                              
         DROP  R2                                                               
*                                                                               
* GET ESUTAB ENTRY FOR THIS UNIT                                                
*                                                                               
GETESUI  NTR1                                                                   
         XC    AESUUNIT,AESUUNIT                                                
*                                                                               
         L     RF,0(R1)                                                         
         MVC   TGTHREE,0(RF)                                                    
*                                                                               
         LA    R5,BLOCK            GET YTD SUI                                  
         USING ESUTABD,R5                                                       
*                                                                               
         XC    BLOCK(ESULEN),BLOCK  BUILD LOOKUP ENTRY FOR UNIT                 
         MVC   ESUEMP,SRTEMP              REQUESTED BY CALLER                   
         TM    SRTINVS2,TAINSFRC   IF FORCING DATE TO END OF LAST YEAR          
         BZ    *+8                                                              
         NI    ESUEMP,X'DF'        TURN OFF X'40' BIT IN EMPLOYER               
         MVC   ESUSSN,SRTSSN                                                    
         MVC   ESUUNIT,TGTHREE                                                  
*                                                                               
         GOTO1 ADDBIN,DMCB,AESUTAB,(R5),1  LOOKUP/ADD ENTRYTO TABLE             
         TM    0(R1),X'80'         IF ENTRY WAS FOUND THEN RETURN IT            
         JO    GESUIX                  (DMCB HAS ADDRESS)                       
         SAM31                                                                  
         L     R5,DMCB                                                          
         ST    R5,AESUUNIT                                                      
         SAM24                                                                  
*                                                                               
GESUIX   J     XIT                                                              
         DROP  R5                                                               
*                                                                               
SUIBLK   DS    0X                                                               
SUIYTD   DS    F                   YTD TAXABLE SUI                              
SUITAX   DS    F                   TAXABLE SUI AMOUNT FOR CHECK                 
SUIUTAX  DS    F                   TAXABLE SUI AMOUNT FOR UNIT                  
SUIFLAG  DS    X                                                                
SUIFVALL EQU   X'80'               VOID ENTIRE CHECK'S SUI                      
SUIBLKLN EQU   *-SUIBLK                                                         
*                                                                               
CURYEARN DS    F                   CURRENT YTD EARNINGS                         
CURUEARN DS    F                   CURRENT STATE'S EARNINGS                     
SUILIMIT DS    F                   SUI LIMIT FOR STATE                          
LASTUNIT DS    F                   A(LAST UNIT)                                 
AESUUNIT DS    F                   A(UNIT IN ESUTAB)                            
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* APPLY DUE COMPANY TO UNITS FOR SUI                                            
*                                                                               
SUIDUEC  NTR1  BASE=*,LABEL=*                                                   
         OC    SRTDUEC(SRTDUECL),SRTDUEC ANY DUE COMPANY?                       
         JZ    SUIDUECX                                                         
*                                                                               
         MVC   SVDWAGE,SRTDUE      DUE COMPANY WAGES                            
         MVC   SVDTNAD,SRTDUETR    DUE COMPANY TAXABLE REIMBURSEMENTS           
*                                                                               
         USING TATUD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
SUIDC10  BRAS  RE,NEXTEL                                                        
         JNE   SUIDUECX                                                         
*                                                                               
         OC    SVDWAGE,SVDWAGE                                                  
         JNZ   *+14                                                             
         OC    SVDTNAD,SVDTNAD                                                  
         JZ    SUIDUECX                                                         
*                                                                               
         CLI   TATUUNIT+2,C' '                                                  
         JNE   SUIDC10                                                          
         CLC   TATUUNIT,=C'FD '                                                 
         JE    SUIDC10                                                          
         CLC   TATUUNIT,=C'CN '                                                 
         JE    SUIDC10                                                          
*                                                                               
         ICM   RF,15,TATUWAGE                                                   
         ICM   RE,15,TATUWAAD                                                   
         SR    RF,RE               ANY DUE COMPANY TAKEN ON WAGES?              
         LTR   RF,RF                                                            
         JZ    SUIDC50                                                          
*                                                                               
         USING SUITABD,R3                                                       
         L     R3,ASUITAB                                                       
SUIDC20  CLI   0(R3),X'FF'                                                      
         JE    SUIDC50                                                          
         CLC   SUITUNIT,TATUUNIT                                                
         JNE   SUIDC30                                                          
*                                                                               
         TM    SUITSTAT,SUITWAGE   TAXABLE WAGE?                                
         JZ    SUIDC30                                                          
*                                                                               
         ICM   RF,15,SUITAMNT                                                   
         L     RE,SVDWAGE                                                       
         SR    RF,RE               WAGES - DUE COMP                             
         BM    SUIDC25                                                          
*                                                                               
         STCM  RF,15,SUITAMNT      SAVE NEW WAGE                                
         XC    SVDWAGE,SVDWAGE     NO MORE DUE COMP                             
         B     SUIDC10                                                          
*                                                                               
SUIDC25  LPR   RF,RF                                                            
         ST    RF,SVDWAGE          SET NEW DUE COMP                             
*                                                                               
SUIDC30  AHI   R3,SUITABLN                                                      
         J     SUIDC20                                                          
*                                                                               
SUIDC50  ICM   RF,15,TATUTNWA                                                   
         ICM   RE,15,TATUTNAD                                                   
         SR    RF,RE               ANY DUE COMPANY TAKEN ON REIMB?              
         LTR   RF,RF                                                            
         JZ    SUIDC10                                                          
*                                                                               
         USING SUITABD,R3                                                       
         L     R3,ASUITAB                                                       
SUIDC60  CLI   0(R3),X'FF'                                                      
         JE    SUIDC10                                                          
         CLC   SUITUNIT,TATUUNIT                                                
         JNE   SUIDC70                                                          
*                                                                               
         TM    SUITSTAT,SUITTAX    TAXABLE REIMBURSEMENTS?                      
         JZ    SUIDC70                                                          
*                                                                               
         ICM   RF,15,SUITAMNT                                                   
         L     RE,SVDTNAD                                                       
         SR    RF,RE               TAX REIM - DUE COMP                          
         BM    SUIDC65                                                          
*                                                                               
         STCM  RF,15,SUITAMNT      SAVE NEW TAX REIM                            
         XC    SVDTNAD,SVDTNAD     NO MORE DUE COMP                             
         B     SUIDC10                                                          
*                                                                               
SUIDC65  LPR   RF,RF                                                            
         ST    RF,SVDTNAD          SET NEW DUE COMP                             
*                                                                               
SUIDC70  AHI   R3,SUITABLN                                                      
         J     SUIDC60                                                          
*                                                                               
SUIDUECX J     XIT                                                              
         LTORG                                                                  
*                                                                               
SVDWAGE  DS    F                   DUE COMPANY WAGES                            
SVDTNAD  DS    F                   DUE COMPANY TAXABLE REIMBURSEMENTS           
*                                                                               
* THIS ROUTINE DETERMINES WHETHER THE CURRENT INVOICE IS ELIGIBLE TO            
* BE PICKED UP BY THE URGENT CHECK RUN.  DUE DATE REQUIREMENTS HAVE             
* ALREADY BEEN MET.                                                             
*                                                                               
TESTURG  NTR1  BASE=*,LABEL=*                                                   
         L     R4,AINVIO           GET INVOICE STATUS ELEMENT                   
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         USING TAIND,R4                                                         
         TM    TAINSTA2,TAINSADJ+TAINSURG  WANT ADJUSTMENTS/URGENTS             
         JNZ   YES                                                              
*                                                                               
         TM    TGUSSTA2,HLDTYPE    WANT HOLDING FEE PAYMENTS                    
         JO    YES                                                              
         TM    TGUSSTA3,SOAPUSE    BUT NEVER WANT SOAP PAYMENTS                 
         JO    NO                                                               
*                                                                               
         TM    PRGSTAT,TESTSYS+CSCSYS+FQASYS                                    
         JNZ   YES                                                              
*                                                                               
         L     R4,AINVIO           LOOK FOR UNIONS PAID ON THIS INVOICE         
         MVI   ELCODE,TAULELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   NO                                                               
         USING TAULD,R4                                                         
*                                                                               
         ZIC   R0,TAULNULS         R0=N'SUB ELEMENTS                            
         LA    R1,TAULULS          R1=A(FIRST SUB ELEMENT)                      
*                                                                               
TURG20   CLC   =C'AFT',0(R1)       IF AFT HERE THEN INVOICE ELIGIBLE            
         JE    YES                                                              
         LA    R1,L'TAULULS(R1)    TRY NEXT SUB ELEMENT                         
         BCT   R0,TURG20                                                        
         J     NO                  INVOICE NOT ELIGIBLE FOR URGENT RUN          
         LTORG                                                                  
         EJECT                                                                  
* THIS ROUTINE REMOVES MISCELLANEOUS DEDUCTIONS FROM THE CHECK.                 
*                                                                               
MISCDED  NTR1  BASE=*,LABEL=*                                                   
         L     RE,SRTMDED          IF MISC DED + UNION DUES > CHECK AMT         
         A     RE,SRTDUES                                                       
         L     R1,SRTNET                                                        
         CR    RE,R1                                                            
         BNH   MISCD40                                                          
*                                                                               
         L     RE,SRTMDED          THEN CHECK MISC DED > CHECK AMOUNT           
         CR    RE,R1                                                            
         BE    MISCD20               IF   EQ, CLEAR UNION DUES                  
         BH    MISCD10               IF   >,  MDED=CHECK & CLEAR DUES           
         SR    R1,RE                 ELSE <,  REMAINDER TO DUES                 
         ST    R1,SRTDUES                                                       
         B     MISCD40                                                          
*                                                                               
MISCD10  MVC   SRTMDED,SRTNET      MISC DED = CHECK AMOUNT                      
MISCD20  XC    SRTDUES,SRTDUES     CLEAR UNION DUES                             
*                                                                               
MISCD40  L     RE,SRTMDED          RE=(NEW MISC AMT + UNION DUES)               
         A     RE,SRTDUES                                                       
*                                                                               
         L     RF,SRTNET           SUBTRACT FROM CHECK AMOUNT                   
         SR    RF,RE                                                            
         ST    RF,SRTNET                                                        
*                                                                               
         A     RE,SRTTDED          ADD TO TOTAL DED AMOUNT                      
         ST    RE,SRTTDED                                                       
         J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* THIS ROUTINE LOOKS IN THE W4 TRUSTEE TABLE FOR AN ENTRY                       
* IF IT DOESN'T FIND ONE THEN IT BUILDS A NEW ONE                               
* IT RETURNS THE ADDRESS OF THE TABLE ENTRY IN PARAMETER 1.                     
*                                                                               
GETTRST  NTR1  BASE=*,LABEL=*                                                   
         LA    R5,BLOCK            BUILD LOOKUP ENTRY                           
         USING TRSTABD,R5                                                       
         XC    BLOCK(TRSTLEN),BLOCK BUILD LOOKUP ENTRY                          
         MVC   TRSTSSN,SRTSSN                                                   
         MVC   TRSTEMP,SRTEMP                                                   
*                                  LOOKUP ENTRY IN TABLE                        
         L     R2,ATRSTAB          R2=A(BINSRCH PARAMETERS/TABLE)               
         USING BIND,R2                                                          
         MVC   DMCB+8(16),BININ    SET PARAMTERS TO BINSRCH                     
         GOTO1 BINSRCH,DMCB,(R5),BINTABLE                                       
*                                                                               
         CLI   0(R1),X'01'         IF ENTRY IS FOUND THEN USE IT                
         JNE   XIT                                                              
*                                                                               
         LA    R5,BLOCK            ADD ENTRY TO TABLE                           
         GOTO1 ADDBIN,DMCB,ATRSTAB,(R5)                                         
         J     XIT                 AND RETURN SUCCESS                           
         DROP  R2,R5                                                            
         LTORG                                                                  
         EJECT                                                                  
* EXTRACT INFORMATION FROM THE PAYMENT DETAILS ELEMENT IN THE INVOICE           
* RECORD INTO THE SORT RECORD.                                                  
*                                                                               
EXTRIPAY NTR1  BASE=*,LABEL=*                                                   
         L     R4,AINVIO           R4 = A(PAYMENT DETAILS ELEMENT)              
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   ERRECPI                                                          
         USING TAPDD,R4                                                         
*                                                                               
         MVC   SRTCOM,TAPDCOM      SAVE INTERNAL COMMERCIAL NUMBER              
         MVC   SRTUSE,TAPDUSE           USE CODE                                
         MVC   SRTUTYP,TAPDTYPE         USE TYPE                                
         MVC   SRTCYCS,TAPDCYCS         CYCLE START DATE                        
         MVC   SRTCYCE,TAPDCYCE         CYCLE END DATE                          
         MVC   SRTMAJ,TAPDMAJ           MAJORS                                  
         CLC   TAPDUSE,=C'ITN'                                                  
         JE    *+10                                                             
         MVC   SRTUNT,TAPDUNIT          UNITS                                   
         MVC   SRTINS,TAPDINS           INSERTS                                 
         MVC   SRTITAG,TAPDTAGS         TAGS                                    
         MVC   SRTDEMO,TAPDDEMS         DEMOS                                   
         MVC   SRTEMP,TAPDEMP           EMPLOYER OF RECORD                      
         MVC   SRTCLI,TAPDCLI           CLIENT                                  
         MVC   SRTPRD,TAPDPRD           PRODUCT                                 
         MVC   SRTADJS,TAPDADJS         PAYROLL ADJUSTMENT INDICATORS           
         MVC   SRTPST1,TAPDPST1         PAYMENT STATUS BYTE                     
         MVC   SRTTPOF,TAPDOFF          TP OFFICE                               
*                                                                               
EIPX     J     XIT                                                              
         DROP  R4                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* GET EVTIME RECORD                                                             
*                                                                               
GETEVT   NTR1  BASE=*,LABEL*                                                    
         CLI   RECNUM,PK                                                        
         BNE   GETEVTX                                                          
*                                                                               
         L     RF,ASUITAB                                                       
         XC    0(250,RF),0(RF)                                                  
         XC    250(250,RF),0(RF)                                                
         MVI   0(RF),X'FF'                                                      
*                                                                               
         L     R4,ASRTCHK                                                       
         USING TLCKD,R4                                                         
*                                                                               
         LA    R3,KEY                                                           
         USING TLTMD,R3                                                         
         XC    TLTMKEY,TLTMKEY                                                  
         MVI   TLTMCD,TLTMCDQ                                                   
         OI    TLTMSTA,TLTMSPPL                                                 
         MVC   TLTMSSN,TLCKSSN     SSN                                          
         MVC   TLTMSORT+5(1),TLCKSORT+5                                         
*                                                                               
         USING TAPDD,R4                                                         
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         JNE   GETEVTX                                                          
*                                                                               
         MVC   TLTMCOM,TAPDCOM     INTERNAL COM CODE                            
         MVC   TLTMINV,TAPDINV     INVOICE                                      
         XC    TLTMINV,=X'FFFFFFFFFFFF'                                         
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'TLTMKEY),KEYSAVE                                           
         JNE   GETEVTX                                                          
*                                                                               
         MVC   AIO,AEVTIO                                                       
         GOTO1 GETREC                                                           
*                                                                               
         BRAS  RE,BLDSUIT          BUILD SUI TABLE                              
*                                                                               
GETEVTX  MVC   AIO,AIO1                                                         
         J     XIT                                                              
         DROP  R3                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* THIS ROUTINE UPDATES TATUWAAD AFTER DUE COMPANY                               
*                                                                               
PKDUE    NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,PK                                                        
         BNE   PKDUEX                                                           
*                                                                               
         MVC   SVDUE,SRTDUE        SET DUE COMP TO REMOVE                       
         MVC   SVDUETR,SRTDUETR                                                 
         MVC   SVDUENR,SRTDUENR                                                 
*                                                                               
         XC    SVDUEREM(SVDUERLN),SVDUEREM   WAGES/REIM REM FOR UNIT            
*                                                                               
         OC    SRTDUEC(SRTDUECL),SRTDUEC   ANY DUE COMPANY?                     
         JZ    PKDUEX                                                           
*                                                                               
         TM    TGSYSTA2,TASYSROI   KEEP ORDER OF INPUT?                         
         BO    PKDUE100                                                         
*                                                                               
         BAS   RE,BLDCSTAB         BUILD CITY/STATE TABLE FROM TATU'S           
*                                                                               
         USING CITYSTAD,R3                                                      
         LA    R3,CITYSTAB         CITY/STATE TABLE                             
*                                                                               
         USING TATUD,R4                                                         
         L     R4,ASRTCHK          UPDATE TATU WAGES AFTER DUE COMP             
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+12                                                             
PKDUE30  AHI   R3,CITYSLNQ         SYNC TATU WITH ENTRY IN TABLE                
         BRAS  RE,NEXTEL                                                        
         BNE   PKDUEX                                                           
         CLC   TATUUNIT,=C'FD '                                                 
         BE    PKDUE30                                                          
         CLC   TATUUNIT,=C'CN '                                                 
         BE    PKDUE30                                                          
*                                                                               
         OC    SRTDUEC(SRTDUECL),SRTDUEC   ANY DUE COMPANY LEFT?                
         JZ    PKDUEX                                                           
*                                                                               
         GOTO1 ISCITY,DMCB,TATUUNIT  TEST IF CITY                               
         JE    PKDUE40                                                          
*                                                                               
         GOTO1 APPDUE,DMCB,0(R4)   TAKE DUE COMPANY FROM STATE                  
         BRAS  RE,PROCCITY         MAKE SURE CITIES AND STATE MATCH             
         J     PKDUE70                                                          
*                                                                               
PKDUE40  MVC   TGTHREE,TATUUNIT    SAVE CITY                                    
         CLC   TATUUNIT(1),CSTATE  CITY < STATE ALPHABETICALLY?                 
         JH    PKDUE50                                                          
*                                                                               
* CITY IS LOWER ALPHABETICALLY THAN STATE SO WE MUST REMOVE DUE COMPANY         
* FROM THE CITY FIRST, THEN REMOVE IT FROM THE STATE (TGTHREE = CITY)           
*                                                                               
         GOTO1 APPDUE,DMCB,0(R4)   TAKE DUE COMPANY FROM CITY                   
         DROP  R4                                                               
*                                                                               
         MVC   AIO,ASRTCHK         NOW REMOVE WAGES FROM STATE                  
         GOTO1 GETL,DMCB,(3,CSTATE)                                             
         JNE   PKDUE70                                                          
*                                                                               
         L     R5,TGELEM                                                        
         USING TATUD,R5                                                         
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVWAGREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVTXRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVNTRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
         J     PKDUE70                                                          
         DROP  R5                                                               
*                                                                               
* CITY IS HIGHER ALPHABETICALLY THAN STATE SO WE MUST REMOVE DUE COMP           
* FROM THE STATE FIRST, THEN REMOVE IT FROM THE CITY (TGTHREE = CITY)           
*                                                                               
PKDUE50  MVC   AIO,ASRTCHK         REMOVE WAGES FROM STATE                      
         GOTO1 GETL,DMCB,(3,CSTATE)                                             
         JNE   PKDUE60                                                          
*                                                                               
         L     R5,TGELEM                                                        
         GOTO1 APPDUE,DMCB,0(R5)   TAKE DUE COMPANY FROM STATE                  
*                                                                               
         MVC   AIO,ASRTCHK         NOW REMOVE WAGES FROM CITY                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         JNE   PKDUE70                                                          
*                                                                               
         L     R5,TGELEM                                                        
         USING TATUD,R5                                                         
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVWAGREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVTXRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVNTRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
         J     PKDUE70                                                          
         DROP  R5                                                               
*                                                                               
PKDUE60  GOTO1 APPDUE,DMCB,0(R4)   NO STATE, SO JUST REMOVE FROM CITY           
*                                                                               
PKDUE70  MVC   AIO,ASRTCHK         SUBTRACT WAGES REMOVED FROM FED              
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   PKDUE75                                                          
*                                                                               
         L     R5,TGELEM                                                        
         USING TATUD,R5                                                         
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVWAGREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVTXRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVNTRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
PKDUE75  MVC   AIO,ASRTCHK         SUBTRACT WAGES REMOVED FROM CN               
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   PKDUE80                                                          
*                                                                               
         L     R5,TGELEM                                                        
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVWAGREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVTXRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVNTRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
         DROP  R5                                                               
*                                                                               
PKDUE80  B     PKDUE30                                                          
*                                                                               
* RETAIN ORDER OF INPUT                                                         
*                                                                               
         USING SUITABD,R3                                                       
PKDUE100 L     R3,ASUITAB                                                       
PKDUE105 CLI   0(R3),X'FF'                                                      
         JE    PKDUE210                                                         
*                                                                               
         OC    SVDUECOM(SVDUECLN),SVDUECOM  ANY DUE COMPANY LEFT?               
         JZ    PKDUE210                                                         
*                                                                               
         TM    SUITSTAT,SUITWAGE   WAGES?                                       
         BO    PKDUE110                                                         
         TM    SUITSTAT,SUITTAX    TAXABLE RIEMBURSEMENT?                       
         BO    PKDUE130                                                         
         B     PKDUE150            NON TAX REIMBURSEMENT                        
*                                                                               
PKDUE110 OC    SVDUE,SVDUE         ANY DUE COMP WAGES?                          
         JZ    PKDUE200                                                         
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,SUITAMNT                                                   
         L     RE,SVDUE                                                         
         SR    RF,RE               WAGES - DUE COMP                             
         BM    PKDUE112                                                         
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW WAGE                                
         XC    SVDUE,SVDUE         NO MORE DUE COMP                             
         B     PKDUE114                                                         
*                                                                               
PKDUE112 LPR   RF,RF                                                            
         ST    RF,SVDUE            SET NEW DUE COMP                             
*                                                                               
PKDUE114 ICM   RF,15,SUITAMNT      ORIGINAL WAGE                                
         L     RE,TGFULL           NEW WAGE                                     
         SR    RF,RE                                                            
         ST    RF,FULL                                                          
         L     RE,SVWAGREM                                                      
         AR    RF,RE                                                            
         ST    RF,SVWAGREM         WAGES REMOVED                                
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,SUITUNIT)                                           
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUWAAD      UPDATE DATE WITH WAGES REMOVED               
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
*                                                                               
         CLI   SUITUNIT+2,C' '     CITY?                                        
         BE    PKDUE116                                                         
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   PKDUE116                                                         
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUWAAD      UPDATE DATE WITH WAGES REMOVED               
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
*                                                                               
PKDUE116 MVC   SUITAMNT,TGFULL     SET NEW WAGE FOR STATE                       
*                                                                               
         ICM   RF,15,SUITYTDE      UPDATE SUI YTD IN SUITAB                     
         S     RF,FULL                                                          
         STCM  RF,15,SUITYTDE                                                   
*                                                                               
*        GOTO1 UPDSUIY,DMCB,SUITUNIT   UPDATE SUI YTD IN SUITAB                 
         B     PKDUE200                                                         
*                                                                               
PKDUE130 OC    SVDUETR,SVDUETR     ANY DUE COMP TAX REIM?                       
         JZ    PKDUE200                                                         
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,SUITAMNT                                                   
         L     RE,SVDUETR                                                       
         SR    RF,RE               TAX REIM - DUE COMP                          
         BM    PKDUE132                                                         
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW TAX REIM                            
         XC    SVDUETR,SVDUETR     NO MORE DUE COMP                             
         B     PKDUE134                                                         
*                                                                               
PKDUE132 LPR   RF,RF                                                            
         ST    RF,SVDUETR          SET NEW DUE COMP                             
*                                                                               
PKDUE134 ICM   RF,15,SUITAMNT      ORIGINAL TAX REIM                            
         L     RE,TGFULL           NEW TAX REIM                                 
         SR    RF,RE                                                            
         ST    RF,FULL                                                          
         L     RE,SVTXRREM                                                      
         AR    RF,RE                                                            
         ST    RF,SVTXRREM         TAX REIM REMOVED                             
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,SUITUNIT)                                           
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUTNAD      UPDATE DATE WITH TAX REIM REMOVED            
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
*                                                                               
         CLI   SRTW4TY,TAW4TYCA    CANADIAN?                                    
         BNE   PKDUE135            TAX REIM IS ALSO IN TATUNNAD                 
         ICM   RF,15,TATUNNAD      UPDATE DATE WITH TAX REIM REMOVED            
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
PKDUE135 CLI   SUITUNIT+2,C' '     CITY?                                        
         BE    PKDUE136                                                         
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   PKDUE136                                                         
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUTNAD                                                   
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
*                                                                               
         CLI   SRTW4TY,TAW4TYIN    INDIVIDUAL?                                  
         BE    PKDUE136            TAX REIM IS ALSO IN TATUNNAD                 
         ICM   RF,15,TATUNNAD      UPDATE DATE WITH TAX REIM REMOVED            
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
PKDUE136 MVC   SUITAMNT,TGFULL     SET NEW TAX REIM FOR STATE                   
*                                                                               
         ICM   RF,15,SUITYTDE      UPDATE SUI YTD IN SUITAB                     
         S     RF,FULL                                                          
         STCM  RF,15,SUITYTDE                                                   
*                                                                               
*        GOTO1 UPDSUIY,DMCB,SUITUNIT   UPDATE SUI YTD IN SUITAB                 
         B     PKDUE200                                                         
*                                                                               
PKDUE150 OC    SVDUENR,SVDUENR     ANY DUE COMP NON TAX REIM?                   
         JZ    PKDUE200                                                         
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,SUITAMNT                                                   
         L     RE,SVDUENR                                                       
         SR    RF,RE               NON TAX REIM - DUE COMP                      
         BM    PKDUE152                                                         
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW NON TAX REIM                        
         XC    SVDUENR,SVDUENR     NO MORE DUE COMP                             
         B     PKDUE154                                                         
*                                                                               
PKDUE152 LPR   RF,RF                                                            
         ST    RF,SVDUENR          SET NEW DUE COMP                             
*                                                                               
PKDUE154 ICM   RF,15,SUITAMNT      ORIGINAL NON TAX REIM                        
         L     RE,TGFULL           NEW NON TAX REIM                             
         SR    RF,RE                                                            
         ST    RF,FULL                                                          
         L     RE,SVNTRREM                                                      
         AR    RF,RE                                                            
         ST    RF,SVNTRREM         NON TAX REIM REMOVED                         
*                                                                               
         MVC   AIO,ASRTCHK                                                      
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,SUITUNIT)                                           
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUNNAD      UPDATE DATE W/ NON TAX REIM REMOVED          
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
         CLI   SUITUNIT+2,C' '     CITY?                                        
         BE    PKDUE156                                                         
         GOTO1 TAXVAL,DMCB,(3,SUITUNIT)                                         
         MVC   TGTHREE,TGTASTCY                                                 
         MVI   ELCODE,TATUELQ                                                   
         GOTO1 GETL,DMCB,(3,TGTHREE)                                            
         BNE   PKDUE156                                                         
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUNNAD                                                   
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
PKDUE156 MVC   SUITAMNT,TGFULL     SET NEW NON TAX REIM FOR STATE               
*                                                                               
         ICM   RF,15,SUITYTDE      UPDATE SUI YTD IN SUITAB                     
         S     RF,FULL                                                          
         STCM  RF,15,SUITYTDE                                                   
*                                                                               
*        GOTO1 UPDSUIY,DMCB,SUITUNIT   UPDATE SUI YTD IN SUITAB                 
         B     PKDUE200                                                         
*                                                                               
PKDUE200 AHI   R3,SUITABLN                                                      
         B     PKDUE105                                                         
*                                                                               
PKDUE210 MVC   AIO,ASRTCHK         SUBTRACT WAGES REMOVED FROM FED              
         GOTO1 GETL,DMCB,(3,=C'FD ')                                            
         BNE   PKDUE220                                                         
*                                                                               
         L     R4,TGELEM                                                        
         USING TATUD,R4                                                         
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVWAGREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVTXRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVNTRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
         CLI   SRTW4TY,TAW4TYIN    INDIVIDUAL?                                  
         BE    PKDUE220            TAX REIM IS ALSO IN TATUNNAD                 
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVTXRREM                                                      
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
PKDUE220 MVC   AIO,ASRTCHK         SUBTRACT WAGES REMOVED FROM CN               
         GOTO1 GETL,DMCB,(3,=C'CN ')                                            
         BNE   PKDUEX                                                           
*                                                                               
         L     R4,TGELEM                                                        
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVWAGREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUWAAD                                                   
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVTXRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUTNAD                                                   
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVNTRREM         AMOUNT TO SUBTRACT                           
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
*                                                                               
         CLI   SRTW4TY,TAW4TYIN    INDIVIDUAL?                                  
         BE    PKDUEX              TAX REIM IS ALSO IN TATUNNAD                 
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVTXRREM                                                      
         SR    RF,RE                                                            
         STCM  RF,15,TATUNNAD                                                   
         DROP  R4                                                               
*                                                                               
PKDUEX   MVC   AIO,AIO1                                                         
         J     XIT                                                              
         LTORG                                                                  
*                                                                               
* UPD YTD SUI IN ASUITAB AFTER DUE COMPANY                                      
*                                                                               
UPDSUIY  NTR1                                                                   
         L     R4,0(R1)            A(SUI UNIT)                                  
*                                                                               
         USING SUITABD,R3                                                       
         L     R3,ASUITAB                                                       
UPDSY10  CLI   0(R3),X'FF'                                                      
         JE    UPDSUIYX                                                         
         CLC   SUITUNIT,0(R4)                                                   
         JNE   UPDSY20                                                          
         ICM   RF,15,SUITYTDE                                                   
         L     RE,FULL                                                          
         SR    RF,RE                                                            
         STCM  RF,15,SUITYTDE                                                   
UPDSY20  AHI   R3,SUITABLN                                                      
         J     UPDSY10                                                          
*                                                                               
UPDSUIYX J     XIT                                                              
         DROP  R3                                                               
*                                                                               
* APPLY DUE COMPANY TO CURRENT TATU                                             
* ON INPUT, PARAM1 = TATU ELEMENT                                               
* ON EXIT, SVDUE IS UPDATED TO HAVE DUE COMPANY LEFT                            
*          SVWAGREM IS UPDATED TO HAVE WAGES REMOVED                            
*          TATUWAAD IS UPDATED TO HAVE WAGES AFTER DUE COMPANY APPLIED          
*          SVDUETR IS UPDATED TO HAVE DUE COMPANY LEFT                          
*          SVTXRREM IS UPDATED TO HAVE TAX REIM REMOVED                         
*          TATUTNAD IS UPDATED TO HAVE TAX REIM AFTER DUE COMP APPLIED          
*          SVDUENR IS UPDATED TO HAVE DUE COMPANY LEFT                          
*          SVNTRREM IS UPDATED TO HAVE NONTAX REIM REMOVED                      
*          TATUNNAD IS UPDATED TO HAVE NONTAX REIM AFT DUE COMP APPLIED         
*                                                                               
APPDUE   NTR1                                                                   
         L     R4,0(R1)            A(TATU ELEMENT)                              
         USING TATUD,R4                                                         
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,TATUWAAD                                                   
         L     RE,SVDUE                                                         
         SR    RF,RE               WAGES - DUE COMP                             
         BM    APPDUE10                                                         
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW WAGE                                
         XC    SVDUE,SVDUE         NO MORE DUE COMP                             
         B     APPDUE20                                                         
*                                                                               
APPDUE10 LPR   RF,RF                                                            
         ST    RF,SVDUE            SET NEW DUE COMP                             
*                                                                               
APPDUE20 ICM   RF,15,TATUWAAD      ORIGINAL WAGE                                
         L     RE,TGFULL           NEW WAGE                                     
         SR    RF,RE                                                            
         ST    RF,SVWAGREM         WAGES REMOVED                                
*                                                                               
         MVC   TATUWAAD,TGFULL     SET NEW WAGE FOR STATE                       
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,TATUTNAD                                                   
         L     RE,SVDUETR                                                       
         SR    RF,RE               TAX REIM - DUE COMP                          
         BM    APPDUE30                                                         
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW WAGE                                
         XC    SVDUETR,SVDUETR     NO MORE DUE COMP                             
         B     APPDUE40                                                         
*                                                                               
APPDUE30 LPR   RF,RF                                                            
         ST    RF,SVDUETR          SET NEW DUE COMP                             
*                                                                               
APPDUE40 ICM   RF,15,TATUTNAD      ORIGINAL WAGE                                
         L     RE,TGFULL           NEW WAGE                                     
         SR    RF,RE                                                            
         ST    RF,SVTXRREM         WAGES REMOVED                                
*                                                                               
         MVC   TATUTNAD,TGFULL     SET NEW TAX REIM FOR STATE                   
*                                                                               
         XC    TGFULL,TGFULL                                                    
         ICM   RF,15,TATUNNAD                                                   
         L     RE,SVDUENR                                                       
         SR    RF,RE               NON TAX REIM - DUE COMP                      
         BM    APPDUE50                                                         
*                                                                               
         STCM  RF,15,TGFULL        SAVE NEW WAGE                                
         XC    SVDUENR,SVDUENR     NO MORE DUE COMP                             
         B     APPDUE60                                                         
*                                                                               
APPDUE50 LPR   RF,RF                                                            
         ST    RF,SVDUENR          SET NEW DUE COMP                             
*                                                                               
APPDUE60 ICM   RF,15,TATUNNAD      ORIGINAL WAGE                                
         L     RE,TGFULL           NEW WAGE                                     
         SR    RF,RE                                                            
         ST    RF,SVNTRREM         WAGES REMOVED                                
*                                                                               
         MVC   TATUNNAD,TGFULL     SET NEW NON TAX REIM FOR STATE               
         J     XIT                                                              
         DROP  R4                                                               
*                                                                               
* TEST IF CITY                                                                  
*                                                                               
ISCITY   NTR1                                                                   
         L     RF,0(R1)                                                         
         CLI   2(RF),C' '                                                       
         JNE   YES                                                              
         CLC   =C'SG',0(RF)        SAN DIEGO?                                   
         JE    YES                                                              
         CLC   =C'SF',0(RF)        SAN FRANCISCO?                               
         JE    YES                                                              
         CLC   =C'SL',0(RF)        SAINT LOUIS?                                 
         JE    YES                                                              
         J     NO                                                               
*                                                                               
* TOOK DUE COMPANY FROM STATE, NOW CHECK IF STATE'S CITY COME AFTER             
* IT ALPHABETICALLY.  IF IT DOES, WE MUST MAKE SURE THE TOTAL OF THE            
* CITIES AREN'T MORE THAN THE STATE                                             
*                                                                               
* ON ENTRY:  R4 = A(STATE'S TATU)                                               
*            R3 = A(TATU TABLE ENTRY)                                           
*                                                                               
PROCCITY NTR1                                                                   
         USING TATUD,R4                                                         
         USING CITYSTAD,R3                                                      
*                                                                               
         MVC   TGFULL,TATUWAAD     TOTAL WAGES FOR STATE                        
         MVC   TGTHREE,TATUUNIT                                                 
*                                                                               
PROCC10  BRAS  RE,NEXTEL                                                        
         JNE   PROCCX                                                           
         AHI   R3,CITYSLNQ                                                      
*                                                                               
         OC    CCITY,CCITY                                                      
         JZ    PROCC10                                                          
         CLC   CSTATE,TGTHREE      SAME STATE?                                  
         JNE   PROCC10                                                          
*                                                                               
         OC    TGFULL,TGFULL                                                    
         JNZ   *+14                                                             
         XC    TATUWAAD,TATUWAAD                                                
         J     PROCC10                                                          
*                                                                               
         L     RF,TGFULL           TOTAL WAGES FOR STATE                        
         ICM   RE,15,TATUWAAD      GET CITY'S WAGES                             
         SR    RF,RE                                                            
         BP    PROCC20                                                          
*                                                                               
         MVC   TATUWAAD,TGFULL                                                  
         XC    TGFULL,TGFULL                                                    
         J     PROCC10                                                          
*                                                                               
PROCC20  ST    RF,TGFULL                                                        
         J     PROCC10                                                          
*                                                                               
PROCCX   J     XIT                                                              
*                                                                               
* BUILD CITY/STATE TABLE FROM TATU'S                                            
*                                                                               
BLDCSTAB NTR1                                                                   
         XC    CITYSTAB,CITYSTAB                                                
         LA    R3,CITYSTAB                                                      
         USING CITYSTAD,R3                                                      
         MVI   0(R3),X'FF'                                                      
*                                                                               
         USING TATUD,R4                                                         
         L     R4,ASRTCHK                                                       
         MVI   ELCODE,TATUELQ                                                   
         BRAS  RE,GETEL                                                         
         J     *+8                                                              
BLDCST10 BRAS  RE,NEXTEL                                                        
         JNE   BLDCSTX                                                          
         MVC   TGTHREE,TATUUNIT                                                 
         XC    CCITY,CCITY                                                      
*                                                                               
         GOTO1 ISCITY,DMCB,TATUUNIT  TEST IF CITY                               
         JNE   BLDCST20                                                         
*                                                                               
         MVC   CCITY,TATUUNIT      IT'S A CITY                                  
         GOTO1 TAXVAL,DMCB,(3,TATUUNIT)                                         
         MVC   TGTHREE,TGTASTCY    SAVE STATE                                   
*                                                                               
BLDCST20 MVC   CSTATE,TGTHREE      SET STATE                                    
         AHI   R3,CITYSLNQ                                                      
         MVI   0(R3),X'FF'                                                      
         J     BLDCST10                                                         
*                                                                               
BLDCSTX  J     XIT                                                              
         LTORG                                                                  
         DROP  R3                                                               
*                                                                               
*                                                                               
SVDUECOM DS    0X                                                               
SVDUE    DS    F                   DUE COMP                                     
SVDUETR  DS    F                   DUE COMP TAX REIM                            
SVDUENR  DS    F                   DUE COMP NON TAX REIM                        
SVDUECLN EQU   *-SVDUECOM                                                       
*                                                                               
SVDUEREM DS    0X                                                               
SVWAGREM DS    F                   WAGES REMOVED B/C OF DUE COMP                
SVTXRREM DS    F                   TAX REIM REMOVED B/C OF DUE COMP             
SVNTRREM DS    F                   NON TAX REIM REMOVED B/C OF DUE COMP         
SVDUERLN EQU   *-SVDUEREM                                                       
*                                                                               
CURTATU  DS    F                   A(CURRENT TATU BEING PROCESSED)              
SVSWAGE  DS    F                   SAVED STATE WAGES                            
CITYSTAB DS    XL181               CITY + STATE TABLE                           
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
* BUILD SUI TABLE WITH STATES FROM EVTIME                                       
*                                                                               
         USING SUITABD,R3                                                       
BLDSUIT  NTR1  BASE=*,LABEL=*                                                   
         CLI   RECNUM,PK                                                        
         BNE   BSUITX                                                           
*                                                                               
         L     R3,ASUITAB                                                       
         MVI   0(R3),X'FF'                                                      
*                                                                               
         USING TATDD,R4                                                         
         L     R4,AEVTIO           BUILD STATE TABLE FOR SUI                    
         MVI   ELCODE,TATDELQ                                                   
         BRAS  RE,GETEL                                                         
         B     *+12                                                             
BSUIT10  BRAS  RE,NEXTEL                                                        
         BNE   BSUIT20                                                          
*                                                                               
         TM    TATDSTAT,TATDSTAX   ONLY TAKE TAXABLE WAGES                      
         BZ    *+12                                                             
         OI    SUITSTAT,SUITTAX                                                 
         B     BSUIT12                                                          
         TM    TATDSTAT,TATDSSTX                                                
         BZ    *+12                                                             
         OI    SUITSTAT,SUITTAX                                                 
         B     BSUIT12                                                          
         TM    TATDSTAT,TATDSWAG                                                
         BZ    *+8                                                              
         OI    SUITSTAT,SUITWAGE                                                
*                                                                               
BSUIT12  MVC   SUITUNIT,TATDUNIT                                                
*        CLI   TATDUNIT+2,C' '     STATE?                                       
*        BE    BSUIT15                                                          
*        GOTO1 TAXVAL,DMCB,(3,TATDUNIT)                                         
*        MVC   SUITUNIT,TGTASTCY                                                
BSUIT15  MVC   SUITAMNT,TATDAMNT   AMOUNT                                       
*                                                                               
         USING ESUTABD,R5                                                       
         GOTO1 GETESU,DMCB,(3,SUITUNIT)                                         
*                                                                               
         SAM31                                                                  
         L     R5,0(R1)                                                         
         ICM   RE,15,ESUEARN       ACCUMULATE EARNINGS AND                      
         ICM   RF,15,ESUTXRE       TAXABLE REIMBURSEMENTS                       
         AR    RE,RF                                                            
         STCM  RE,15,SUITYTDE      SAVE YTD EARNINGS                            
         SAM24                                                                  
*                                                                               
         AHI   R3,SUITABLN                                                      
         MVI   0(R3),X'FF'                                                      
         B     BSUIT10                                                          
         DROP  R4,R5                                                            
*                                                                               
         USING SUITABD,R3                                                       
BSUIT20  L     R3,ASUITAB          UPDATE YTD AMOUNTS IN TABLE                  
BSUIT22  CLI   0(R3),X'FF'                                                      
         BE    BSUITX                                                           
*                                                                               
         XC    TGFULL,TGFULL                                                    
         L     R4,ASUITAB                                                       
BSUIT24  CLI   0(R4),X'FF'                                                      
         BE    BSUIT30                                                          
*                                                                               
         CLC   SUITUNIT,0(R4)      SAME UNIT?                                   
         BNE   BSUIT26                                                          
         ICM   RE,15,3(R4)         ACCUMULATE EARNINGS TO UPDATE YTD            
         L     RF,TGFULL                                                        
         AR    RF,RE                                                            
         ST    RF,TGFULL                                                        
*                                                                               
         CR    R3,R4                                                            
         BNE   BSUIT26                                                          
*                                                                               
         ICM   RE,15,SUITYTDE      UPDATE YTD                                   
         AR    RF,RE                                                            
         STCM  RF,15,SUITYTDE                                                   
         B     BSUIT30                                                          
*                                                                               
BSUIT26  AHI   R4,SUITABLN                                                      
         B     BSUIT24                                                          
*                                                                               
BSUIT30  AHI   R3,SUITABLN                                                      
         B     BSUIT22                                                          
*                                                                               
BSUITX   J     XIT                                                              
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        MISCELLANEOUS IO AREAS                                                 
*                                                                               
CHKFLAGS DS    XL1                 CHECK FLAGS                                  
*                                                                               
ESUCUR   DS    XL200               CURRENT ESU ENTRY                            
ESUICUR  DS    XL200               CURRENT ESU INVOICE ENTRY                    
*                                                                               
INVIO    DS    CL4000              INVOICE RECORD                               
EMPIO    DS    CL4000              EMPLOYER RECORD                              
W4IO     DS    CL4000              W4 RECORD                                    
EVTIO    DS    CL4000              EVTIME RECORD                                
SUITAB   DS    XL500               SUI TABLE (EVTIME TATD'S FOR STATES)         
YTDTAB   DS    CL(55*YTDLNQ)       TAYTD TABLE                                  
         EJECT                                                                  
       ++INCLUDE TACKREPD                                                       
         EJECT                                                                  
       ++INCLUDE DDDLCB                                                         
         EJECT                                                                  
TMDD     DSECT                                                                  
       ++INCLUDE TALIMD                                                         
         EJECT                                                                  
*TAREPFFD                                                                       
*TAREPF4D                                                                       
*DDGENTWA                                                                       
*DDTWADCOND                                                                     
*DDREPMASTD                                                                     
*DDREMOTED                                                                      
*DDSPOOLD                                                                       
*DDSPLWORKD                                                                     
*DDBIGBOX                                                                       
*TAGENFILE                                                                      
*CTGENFILE                                                                      
*TASYSDSECT                                                                     
*TASYSEQUS                                                                      
*TAREPWORKD                                                                     
*TALDCPTRD                                                                      
*DDCOMFACS                                                                      
*DDGETRETD                                                                      
       ++INCLUDE TAREPFFD                                                       
         ORG   CONTAGH                                                          
       ++INCLUDE TAREPF4D                                                       
       ++INCLUDE DDGENTWA                                                       
       ++INCLUDE DDTWADCOND                                                     
       ++INCLUDE DDMASTD                                                        
       ++INCLUDE DDREMOTED                                                      
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDBIGBOX                                                       
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE CTGENFILE                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE TALDCPTRD                                                      
       ++INCLUDE DDCOMFACSD                                                     
       ++INCLUDE DDGETRETD                                                      
       ++INCLUDE TAREPWORKD                                                     
*                                                                               
CITYSTAD DSECT                                                                  
CCITY    DS    XL3                 CITY                                         
CSTATE   DS    XL3                 STATE                                        
CITYSLNQ EQU   *-CCITY                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'206TACKREP   06/23/16'                                      
         END                                                                    
