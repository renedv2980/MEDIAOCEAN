*          DATA SET FASWITCH   AT LEVEL 002 AS OF 10/04/12                      
*CATALP FASWITCH                                                                
         TITLE 'FASWITCH - SWITCH DATAMGR SYSTEMS ONLINE'                       
FASWITCH CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**SWITCH,CLEAR=YES                                   
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         ST    R1,APARMS                                                        
         MVC   PARMS(PARML),0(R1)                                               
         L     R9,VSYSFAC                                                       
         USING SYSFACD,R9          R9=A(SYSFACS)                                
         L     RF,VSSB                                                          
         L     R8,SSBTKADR-SSBD(RF)                                             
         USING TCBD,R8             R8=A(TCB)                                    
         L     R7,TCBUTL                                                        
         USING UTLD,R7             R7=A(UTL)                                    
         USING SELISTD,R5          R5=A(SELIST)                                 
         MVI   RETCODE,0           RESET ERROR RETURN BYTE                      
         MVI   ACTION,0            NORMAL SWITCH ACTION                         
*                                                                               
         PROT  OFF                 DISABLE CALLER'S STORAGE PROTECTION          
         EJECT                                                                  
***********************************************************************         
* CHECK SPECIAL CALLS IN PARAMETER LIST                               *         
***********************************************************************         
SW       CLC   PARMSE+1(3),=3X'FF' TEST FACPAK CALL (MONITOR/ABEND ETC)         
         BNE   SW0                                                              
         XC    PARMSE+1(3),PARMSE+1                                             
         MVC   ACTION,PARMSE                                                    
         CLI   PARMSE,X'FB'        TEST FOR DELETE                              
         BE    SWDEL                                                            
         CLI   PARMSE,X'FD'        TEST FOR SERVICE CONNECT                     
         BE    SERVICE                                                          
         CLI   PARMSE,X'FC'        SPECIAL BACKOUT SERVICE CONNECT              
         BE    SERVICE                                                          
         CLI   PARMSE,X'FE'        TEST SPECIAL SYSFAC CALL                     
         BNE   *+14                                                             
         MVC   PARMSYS,VSYSFAC     SYSTEM X'FE' RETURNS A(SYSFAC)               
         B     SWX                                                              
         TM    PARMSE,X'FF'        TEST SPECIAL CALL                            
         BM    SW1                                                              
         LA    R0,TCBD             SYSTEM X'00' RETURNS A(TCB)                  
         BZ    *+8                                                              
         LA    R0,UTLD             SYSTEM X'FF' RETURNS A(UTL)                  
         ST    R0,PARMSYS                                                       
         B     SWX                                                              
*                                                                               
SW0      SR    RF,RF               TEST PROGRAM AUTHORISED FOR SWITCH           
         ICM   RF,7,TAPRG                                                       
         JZ    *+2                 DIE IF NOT CONNECTED TO A SYS/PGM            
         L     R1,VSSB                                                          
         TM    SSBSTAT4-SSBD(R1),SSBPGDSP                                       
         BZ    SW0A                TAPRG IS REAL VALUE                          
         XR    R1,R1                                                            
         ICM   R1,7,TARSYS                                                      
         JZ    *+2                 DIE IF NO SYSTEM DEFINED                     
         ICM   R1,15,SEPGMS-SELISTD(R1)                                         
         AR    RF,R1                                                            
*                                                                               
SW0A     EQU   *                   ALL PROGRAMS CAN NOW DO SWITCH               
*                                                                               
SW1      OC    TCBRCVF(3),TCBRCVF  DID PROGRAM ADD TO RECOVERY FILE             
         BZ    SW6                                                              
         SR    R1,R1               YES-SAVE SELIST DATA FOR CHKPT               
         ICM   R1,1,TCBSWNUM                                                    
         JZ    *+2                 DIE IF NO ENTRIES IN TCBSWTAB                
         LA    R6,TCBSWTAB                                                      
         USING TCBSWTAB,R6                                                      
SW2      CLC   TCBSEN,TCBSWSYS     FIND ENTRY FOR SE IN CHKPT TABLE             
         BE    SW4                                                              
         LA    R6,TCBSWLEN(R6)                                                  
         BCT   R1,SW2                                                           
         DC    H'0'                CANT FIND TABLE ENTRY FOR SAVE DATA          
SW4      MVC   TCBSWRVF(3),TCBRCVF SAVE RECOVERY DISK ADDRESSES                 
         MVC   TCBSWRVL(3),TCBRCVL                                              
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* VALIDATE SYSTEM NAME                                                *         
***********************************************************************         
SW6      OC    PARMSE+1(3),PARMSE+1                                             
         BZ    SW10                                                             
         L     R1,PARMSYS                                                       
         MVC   DUB(3),0(R1)        EXTRACT SWITCH-TO SYSTEM NAME                
         L     R1,VSYSLST                                                       
         LH    RE,0(R1)                                                         
         L     RF,2(R1)                                                         
         LA    R1,6(R1)                                                         
         USING SYSLSTD,R1                                                       
SW7      CLC   SYSLNAME(3),DUB     MATCH ON 3 CHARACTER SYSTEM NAME             
         BE    SW8                                                              
         CLC   SYSLSHRT(3),DUB     OR ON SYSTEM SHORT NAME                      
         BE    SW8                                                              
         BXLE  R1,RE,SW7                                                        
         DC    H'0'                INVALID SYSTEM NAME                          
SW8      MVC   DUB(1),SYSLNUM      SAVE OVERLAY SYSTEM NUMBER                   
         SR    R1,R1                                                            
         ICM   R1,1,TCBSWNUM                                                    
         JZ    *+2                 DIE IF NO TCBSWTAB IN TCB                    
         LA    R6,TCBSWTAB                                                      
         USING TCBSWTAB,R6                                                      
SW9      CLC   TCBSWSOV,DUB        MATCH ON OVERLAY SYSTEM NUMBER               
         BNE   *+14                                                             
         MVC   PARMSE,TCBSWSYS     SET PARMSE FOR SELIST LOOK-UP                
         B     SW10                                                             
         LA    R6,TCBSWLEN(R6)                                                  
         BCT   R1,SW9                                                           
         MVI   RETCODE,1           RETURN INVALID SYSTEM                        
         B     SWX                                                              
         DROP  R6                                                               
*                                                                               
SW10     CLI   TSVCREQ+1,$IAM      SEND $IAM DOWN APPL ROUT                     
         BE    SWITCH                                                           
         CLC   TSVCREQ,$UNWIND     TEST $ABEND PROCESSING                       
         BE    SWITCH                                                           
         OC    TSVCREQ,TSVCREQ                                                  
         BNZ   SRSWITCH            SERVICE REQ SWITCHING                        
         EJECT                                                                  
***********************************************************************         
* SWITCH FOR APPLICATION PROGRAMS                                     *         
***********************************************************************         
SWITCH   L     R5,VSELIST          LOCATE SELIST ENTRY FOR PARMSE               
         LH    RE,0(R5)                                                         
         L     RF,2(R5)                                                         
         LA    R5,6(R5)                                                         
         CLC   SESYS,PARMSE        MATCH SE NUMBER TO SELIST                    
         BE    *+10                                                             
         BXLE  R5,RE,*-10                                                       
         DC    H'0'                INVALID SWITCH SYSTEM NUMBER                 
         ST    R5,ASE              SAVE A(SELIST ENTRY)                         
*                                                                               
         SR    RE,RE               RE=A(FREE ENTRY IN TCBSWTAB)                 
         SR    R1,R1                                                            
         MVC   SAVESE,PARMSE                                                    
         LA    R6,TCBSWTAB                                                      
         USING TCBSWTAB,R6                                                      
         ICM   R1,1,TCBSWNUM       R1=N'ENTRIES IN TCBSWTAB                     
         BZ    SW12                                                             
SW11     CLC   SAVESE,TCBSWSYS     FIND TCB ENTRY FOR SYSTEM                    
         BE    SW14                                                             
         OC    TCBSWAGB(TCBSWRVL-TCBSWAGB),TCBSWAGB                             
         BNZ   *+6                                                              
         LR    RE,R6               SAVE A(FREE ENTRY) IN SWITCH TABLE           
         LA    R6,TCBSWLEN(R6)                                                  
         BCT   R1,SW11                                                          
*                                                                               
SW12     LTR   RE,RE               TEST FREE ENTRY FOUND                        
         BZ    *+10                                                             
         LR    R6,RE               YES-USE IT                                   
         B     SW13                                                             
         IC    R1,TCBSWNUM         CREATE ENTRY IN TCBSWTAB                     
         LA    R1,1(R1)                                                         
         LA    R0,TCBSWMAX                                                      
         CR    R1,R0                                                            
         JH    *+2                 DIE IF TOO MANY SYSTEM SWITCHES              
         STC   R1,TCBSWNUM                                                      
*                                                                               
SW13     XC    TCBSWTAB(TCBSWLEN),TCBSWTAB                                      
         L     R5,ASE                                                           
         MVC   TCBSWSYS,SAVESE                                                  
         MVC   TCBSWSOV,SEOVSYS                                                 
         EJECT                                                                  
***********************************************************************         
* SYSTEM IS VALID - NOW FIX ALL CONTROL BLOCKS WITH RELEVENT FIELDS   *         
***********************************************************************         
SW14     L     R5,ASE                                                           
         ST    R5,TCBSE                                                         
         MVC   TCBSEN,SESYS        SET TCB DATA FOR SWITCHED SYSTEM             
         MVC   TCBOVSYS,SEOVSYS                                                 
         MVC   TCBRCVF(3),TCBSWRVF                                              
         MVC   TCBRCVL(3),TCBSWRVL                                              
         MVC   TCBDTFS(L'TCBDTFS+L'TCBDTFX),SEFILES                             
*                                                                               
         MVC   TSYS,TCBSEN         SET UTL DATA FOR DATAMGR CALLS               
         MVC   TOVSYS,TCBOVSYS                                                  
         MVC   TAGYB,TCBSWAGB                                                   
         STCM  R5,7,TASYS                                                       
*                                                                               
         TM    TTEST,TTESTTAC      TEST IN IAM TEST MODE                        
         BNZ   *+14                                                             
         MVC   TACCS,TCBSWACS                                                   
         B     *+14                                                             
         ICM   RF,15,TACCS                                                      
         MVC   TSTTACCS-TSTTABD(L'TSTTACCS,RF),TCBSWACS                         
*                                                                               
SW14B    NI    TFLAG,X'FF'-(TFLAGNOP+TFLAGRCV)                                  
         CLI   PARMSE,1            TEST SWITCH TO SERVICE SYSTEM                
         BE    *+8                                                              
         OI    TFLAG,TFLAGSSW      NO-SET SWITCHED                              
*                                                                               
         OC    TCBRCVF(3),TCBRCVF  IGNORE STARTED NOP IF RECOVERY PEND          
         BNZ   *+12                                                             
         TM    SEIND,SEINOP        TEST SYSTEM IS UP & OPEN                     
         BNZ   *+12                                                             
         TM    SEIND,SEISTRT                                                    
         BNZ   *+16                                                             
         OI    TFLAG,TFLAGNOP                                                   
         MVI   RETCODE,2           RETURN SYSTEM NO-OP ERROR                    
         B     SW15                                                             
*                                                                               
         OC    SESIN,SESIN         TEST SYSTEM IS ACTIVE                        
         BNZ   SW15                                                             
         TM    SEIND,SEIACTV                                                    
         BNZ   SW15                                                             
         OI    SEIND,SEIACTV       NO-SET SYSTEM IS ACTIVE                      
         GOTO1 VCHKOUT             AND TAKE SYSTEM CHECKPOINT                   
*                                                                               
SW15     L     RF,TCBTWA           SET TWA DATA FOR APPLICATION                 
         USING TWAD,RF                                                          
         MVC   TWAACCS,TCBSWACS                                                 
         LH    RE,=Y(TWAACCS2)                                                  
         AR    RE,RF                                                            
         MVC   0(4,RE),TCBSWAC2                                                 
*                                                                               
         GOTO1 VDTFIOA,DUB,(C'I',TCBD)                                          
*                                                                               
         ICM   RE,15,PARMFAC       RETURN SYSTEM FACILITIES LIST                
         BZ    SW16                                                             
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
         ICM   RE,15,PARMFAC       PROT CRAPS ON RE                             
         XC    0(256,RE),0(RE)     CLEAR FACILITIES LIST                        
         PROT  OFF                 DISABLE CALLER'S STORAGE PROTECTION          
         ICM   RE,15,PARMFAC       PROT CRAPS ON RE                             
         L     R5,ASE                                                           
         SR    RF,RF                                                            
         ICM   RF,1,SEFACSET       RF=FACILITIES LIST INDEX                     
         BZ    SW16                                                             
         SLL   RF,2                                                             
         L     RF,VSYSFAC0(RF)                                                  
         MVC   0(256,RE),0(RF)     RETURN FACILITIES LIST                       
*                                                                               
         TM    TTEST,X'03'                                                      
         BZ    SW16                                                             
         BAS   RE,MFACSET                                                       
*                                                                               
SW16     XC    PARMSYS,PARMSYS     CLEAR PARAMETER 1                            
         MVC   PARMSE,TAGYB        RETURN AGENCY BINARY VALUE                   
         B     SWX                                                              
         DROP  R6,RF                                                            
         EJECT                                                                  
***********************************************************************         
* SWITCH FOR SERVICE REQUESTS                                         *         
***********************************************************************         
SRSWITCH L     R5,VSELIST          LOCATE SELIST ENTRY FOR PARMSE               
         LH    RE,0(R5)                                                         
         L     RF,2(R5)                                                         
         LA    R5,6(R5)                                                         
         CLC   SESYS,PARMSE        MATCH SE NUMBER TO SELIST                    
         BE    *+10                                                             
         BXLE  R5,RE,*-10                                                       
         DC    H'0'                INVALID SWITCH SYSTEM NUMBER                 
         ST    R5,ASE              SAVE A(SELIST ENTRY)                         
*                                                                               
         SR    RE,RE               RE=A(FREE ENTRY IN TCBSWTAB)                 
         SR    R1,R1                                                            
         MVC   SAVESE,PARMSE                                                    
         LA    R6,TCBSWTAB                                                      
         USING TCBSWTAB,R6                                                      
         ICM   R1,1,TCBSWNUM       R1=N'ENTRIES IN TCBSWTAB                     
         BZ    SRS12                                                            
SRS11    CLC   SAVESE,TCBSWSYS     FIND TCB ENTRY FOR SYSTEM                    
         BE    SRS14                                                            
         OC    TCBSWTAB(TCBSWLEN),TCBSWTAB                                      
         BNZ   *+6                                                              
         LR    RE,R6               SAVE A(FREE ENTRY) IN SWITCH TABLE           
         LA    R6,TCBSWLEN(R6)                                                  
         BCT   R1,SRS11                                                         
*                                                                               
SRS12    LTR   RE,RE               TEST FREE ENTRY FOUND                        
         BZ    *+10                                                             
         LR    R6,RE               YES-USE IT                                   
         B     SRS13                                                            
         IC    R1,TCBSWNUM         CREATE ENTRY IN TCBSWTAB                     
         LA    R1,1(R1)                                                         
         LA    R0,TCBSWMAX                                                      
         CR    R1,R0                                                            
         JH    *+2                 DIE IF TOO MANY SYSTEM SWITCHES              
         STC   R1,TCBSWNUM                                                      
*                                                                               
SRS13    XC    TCBSWTAB(TCBSWLEN),TCBSWTAB                                      
         L     R5,ASE                                                           
         MVC   TCBSWSYS,SAVESE                                                  
         MVC   TCBSWSOV,SEOVSYS                                                 
         EJECT                                                                  
***********************************************************************         
* SYSTEM IS VALID - NOW FIX ALL CONTROL BLOCKS WITH RELEVENT FIELDS   *         
***********************************************************************         
SRS14    L     R5,ASE                                                           
         ST    R5,TCBSE                                                         
         CLI   TCBSEN,1            ARE WE SWITCHING FROM SERVICE                
         BNE   SRS14A                                                           
         PUSH  USING                                                            
         DROP  R6                                                               
         MVC   TCBSWSOV,TSYS       YES SO SAVE CONNECT TSYS                     
         POP   USING                                                            
*                                                                               
SRS14A   MVC   TCBSEN,SESYS        SET TCB DATA FOR SWITCHED SYSTEM             
         MVC   TCBOVSYS,SEOVSYS                                                 
         MVC   TCBRCVF(3),TCBSWRVF                                              
         MVC   TCBRCVL(3),TCBSWRVL                                              
         MVC   TCBDTFS(L'TCBDTFS+L'TCBDTFX),SEFILES                             
*                                                                               
         MVC   TSYS,TCBSEN         SET UTL DATA                                 
         STCM  R5,7,TASYS                                                       
         CLI   TCBSEN,0            TEST CONNECTED                               
         BNE   *+8                                                              
         MVI   TCBSEN,1            NO-SET SERVICE SYSTEM                        
         CLI   PARMSE,1                                                         
         BNE   SRS14C                                                           
*                                                                               
         PUSH  USING                                                            
         DROP  R6                                                               
         OC    TCBSWSOV,TCBSWSOV   TEST CONNECTED                               
         BZ    SRS14B                                                           
         L     R5,VSELIST          RESET TASYS TO CORRECT SYSTEM                
         LH    RE,0(R5)                                                         
         L     RF,2(R5)                                                         
         LA    R5,6(R5)                                                         
         CLC   SESYS,TCBSWSOV                                                   
         BE    *+10                                                             
         BXLE  R5,RE,*-10                                                       
         DC    H'0'                                                             
         STCM  R5,7,TASYS                                                       
SRS14B   MVC   TSYS,TCBSWSOV       RESTORE CONNECT TSYS                         
         CLI   TCBSWSYS,1                                                       
         JNE   *+2                 DIE IF BAD SWITCH SEQUENCE                   
         POP   USING                                                            
*                                                                               
SRS14C   NI    TFLAG,X'FF'-(TFLAGNOP+TFLAGRCV)                                  
         CLI   PARMSE,1            TEST SWITCH TO SERVICE SYSTEM                
         BE    *+8                                                              
         OI    TFLAG,TFLAGSSW      NO-SET SWITCHED                              
         CLC   TSVCREQ,$ABEND      TEST ABEND PROCESSING                        
         BE    SRS15               YES-ALLOW SWITCH                             
*                                                                               
*NOP     TM    SEIND,SEINOP        TEST SYSTEM IS UP & OPEN                     
*NOP     BNZ   *+12                                                             
         TM    SEIND,SEISTRT                                                    
         BNZ   SRS15A                                                           
         OI    TFLAG,TFLAGNOP                                                   
         MVI   RETCODE,2           RETURN SYSTEM NO-OP ERROR                    
*                                                                               
SRS15    L     RF,TCBTWA           SET TWA DATA FOR APPLICATION                 
         USING TWAD,RF                                                          
         MVC   TWAACCS,TCBSWACS                                                 
         LH    RE,=Y(TWAACCS2)                                                  
         AR    RE,RF                                                            
         MVC   0(4,RE),TCBSWAC2                                                 
*                                                                               
SRS15A   GOTO1 VDTFIOA,DUB,(C'I',TCBD)                                          
*                                                                               
         ICM   RE,15,PARMFAC       RETURN SYSTEM FACILITIES LIST                
         BZ    SRS16                                                            
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
         ICM   RE,15,PARMFAC       PROT CRAPS ON RE                             
         XC    0(256,RE),0(RE)     CLEAR FACILITIES LIST                        
         PROT  OFF                 DISABLE CALLER'S STORAGE PROTECTION          
         ICM   RE,15,PARMFAC       PROT CRAPS ON RE                             
         L     R5,ASE                                                           
         SR    RF,RF                                                            
         ICM   RF,1,SEFACSET       RF=FACILITIES LIST INDEX                     
         BZ    SRS16                                                            
         SLL   RF,2                                                             
         L     RF,VSYSFAC0(RF)                                                  
         MVC   0(256,RE),0(RF)     RETURN FACILITIES LIST                       
*                                                                               
         TM    TTEST,X'03'                                                      
         BZ    SRS16                                                            
         BAS   RE,MFACSET                                                       
*                                  RETURN PARAMETER LIST TO CALLER              
SRS16    XC    PARMSYS,PARMSYS     CLEAR PARAMETER 1                            
         MVC   PARMSE,TAGYB        RETURN AGENCY BINARY VALUE                   
         B     SWX                                                              
         DROP  R6,RF                                                            
         EJECT                                                                  
***********************************************************************         
* DELETE SWITCH TAB ENTRIES. ALWAYS LEAVE FIRST ENTRY INTACT.         *         
* ANY ENTRIES WITH ACTIVE RECOVERY INFO WILL NOT BE DELETED.          *         
* PASS IN PLIST+4(1) THE OV SYSTEM NUMBER TO KEEP OR 0 FOR NO SYSTEMS *         
***********************************************************************         
SWDEL    LA    R6,TCBSWTAB         R6=A(SWITCH TABLE)                           
         USING TCBSWTAB,R6                                                      
         MVC   SAVESOV,PARMSOV     SAVE SYSTEM NUMBER TO KEEP                   
         SR    R0,R0               R0=N'ENTRIES DELETED FROM TCBSWTAB           
         SR    R1,R1                                                            
         ICM   R1,1,TCBSWNUM       R1=N'ENTRIES IN TCBSWTAB                     
         BZ    SWDELX                                                           
         B     SWDEL5              SKIP PAST FIRST ENTRY                        
*                                                                               
SWDEL1   OC    TCBSWRVF,TCBSWRVF   SKIP PAST ENTRIES WITH RECOVERY              
         BNZ   SWDEL5                                                           
         CLI   SAVESOV,0           WAS AN OV SYS NUMBER PASSED                  
         BE    SWDEL2              NO                                           
         CLC   SAVESOV,TCBSWSOV    YES SKIP ENTRIES FOR THIS OV SYS             
         BE    SWDEL5                                                           
*                                                                               
SWDEL2   AHI   R0,1                BUMP DELETED ENTRIES COUNT                   
         LR    RE,R6               RE=A(ENTRY TO BE DELETED)                    
         LR    RF,R1                                                            
         AHI   RF,-1               RF=NUM OF ENTRIES TO SHIFT DOWN              
         BNP   SWDEL4                                                           
SWDEL3   MVC   0(TCBSWLEN,RE),TCBSWLEN(RE)                                      
         LA    RE,TCBSWLEN(RE)                                                  
         BCT   RF,SWDEL3                                                        
SWDEL4   XC    0(TCBSWLEN,RE),0(RE) CLEAR LAST ENTRY IN LIST                    
         B     SWDEL6               ENTRY IS NOW DELETED SO DONT BUNP           
*                                                                               
SWDEL5   LA    R6,TCBSWLEN(R6)     BUMP TO NEXT SWTAB ENTRY                     
SWDEL6   BCT   R1,SWDEL1                                                        
*                                                                               
SWDEL7   LTR   R0,R0               EXIT IF NO ENTRIES DELETED                   
         BZ    SWDELX                                                           
         SR    R1,R1                                                            
         ICM   R1,1,TCBSWNUM       R1=N'ENTRIES IN TCBSWTAB                     
         SR    R1,R0                                                            
         BNM   SWDEL8                                                           
         LA    R1,1                MAKE NEW COUNT LOOK GOOD                     
SWDEL8   STCM  R1,1,TCBSWNUM       SET NEW LIST LENGTH                          
*                                                                               
SWDELX   B     SWX                 NORMAL EXIT WITH RETCODE=0                   
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* COMPLETE TASK FOR APPLICATION AND INITIALIZE FOR SE1 TASK           *         
***********************************************************************         
SERVICE  OC    TSVCREQ,TSVCREQ     MUST BE RUNNING A SERVICE REQ                
         JZ    *+2                                                              
*                                                                               
         GOTO1 VTICTOC,DUB,C'TGET' GET APPLICATION END TIME                     
         L     R1,DUB                                                           
         ST    R1,TCBNDTM                                                       
         S     R1,TCBTIME          CALCULATE CPU TIME                           
         A     R1,TCBCPUTM         USED BY APPLICATION                          
         ST    R1,TCBCPUTM                                                      
*                                                                               
         TIMEUSED STORADR=DUB,LINKAGE=SYSTEM,CPU=MIC                            
         LG    GR1,DUB                                                          
         SLG   GR1,TCBCPUSE        GR1=CPU USED BY TASK THIS GO ROUND           
         AL    R1,TCBCPUTK                                                      
         ST    R1,TCBCPUTK         BUMP CPU USED BY THIS TASK                   
*                                                                               
         XC    TCBMSGO,TCBMSGO     NO MESSAGE THIS TIME                         
         OC    TNUM,TNUM                                                        
         BZ    SRV01                                                            
         LR    R1,R8                                                            
         GOTO1 =V(LOGGER),(R1)     PUT RECORD TO RECORDER FILE                  
*                                                                               
SRV01    GOTO1 VTICTOC,DUB,C'1SET',1                                            
         L     R1,DUB              RESET TCBSTATS FOR S/R                       
         ST    R1,TCBTIME                                                       
         ST    R1,TCBINTM                                                       
         ST    R1,TCBSTTM                                                       
         TIMEUSED STORADR=TCBCPUSE,LINKAGE=SYSTEM,CPU=MIC                       
*                                                                               
         XC    TCBCPUTM,TCBCPUTM                                                
         XC    TCBCPUTK,TCBCPUTK                                                
         XC    TCBMSGI,TCBMSGI                                                  
         XC    TCBIOCNT,TCBIOCNT                                                
         XC    TCBOVCNT,TCBOVCNT                                                
*                                                                               
         MVI   TCBSEN,1            SET TCB DATA FOR SE1                         
         MVI   TCBOVSYS,1                                                       
         MVC   TCBPRG,TSVCREQ+1                                                 
         MVI   TCBPRTY,0                                                        
         MVI   TCBPRTYF,0                                                       
         L     R1,VSELIST                                                       
         LA    R1,6(R1)                                                         
         ST    R1,TCBSE                                                         
         MVC   TCBDTFS(8),SEFILES-SELISTD(R1)                                   
*                                                                               
         CLI   PARMSE,X'FC'        TEST IF SR WANTS RCVR DATA INTACT            
         BE    SRV02                                                            
         XC    TCBRCVF,TCBRCVF     ELSE CLEAR ALL RECOVERY DETAILS              
         XC    TCBRCVL,TCBRCVL                                                  
         XC    TCBRCVC,TCBRCVC                                                  
*                                                                               
         XC    TCBSWTAB((TCBSWMAX/2)*TCBSWLEN),TCBSWTAB                         
         LA    R1,TCBSWTAB+((TCBSWMAX/2)*TCBSWLEN)                              
         XC    0((TCBSWMAX/2)*TCBSWLEN,R1),0(R1)                                
         MVI   TCBSWNUM,1          SET SWITCHED TO SE1                          
         MVI   TCBSWSYS,1                                                       
         MVI   TCBSWSOV,1                                                       
*                                                                               
SRV02    GOTO1 VDTFIOA,DUB,(C'I',TCBD)                                          
         B     SWX                                                              
         EJECT                                                                  
SWX      DS    0H                                                               
         PROT  ON                  RESTORE CALLER'S STORAGE PROTECTION          
         CLI   RETCODE,2                                                        
*&&US*&& B     SWX1                                                             
         BNE   SWX1                                                             
*                                                                               
         TM    SEIND,SEISTRT                                                    
         BZ    *+12                                                             
         LA    R0,7                TEMPORARILY INHIBITED                        
         B     *+8                                                              
         LA    R0,11               INFORM USER OF SYSTEM DOWN                   
         GOTO1 =V(GETTXT),DUB,(X'FF',(R0))                                      
         DC    H'0',C'$ABEND'      REVERSE ANY UPDATES SO FAR                   
         DC    H'0'                                                             
*                                                                               
SWX1     MVC   PARMERR,RETCODE     RETURN ERROR INDICATOR                       
         L     R1,APARMS                                                        
         MVC   0(PARML,R1),PARMS                                                
*                                                                               
SWXX     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PROCESS ADDRESSES OF CORE RESIDENT PHASES IN SYSFACS     *         
* AND COMFACS FOR TERMINALS SIGNED ON IN TEST MODE                    *         
* P1 A(MAIN WORKING STORAGE)                                          *         
* P2 DISPLACEMENT OF SYSFAC ENTRY FROM VSYSFAC0                       *         
***********************************************************************         
MFACSET  NTR1                                                                   
         LR    R4,RF               R4=A(FACILITIES LIST)                        
*                                                                               
         LA    R2,X'04'            CRNDX USES 01=A,02=B,04=C                    
         TM    TTEST,X'03'         TEST FOR 'C'                                 
         BO    *+14                                                             
         IC    R2,TTEST            TTEST USES 01=A,02=B,03=C                    
         LA    R0,3                                                             
         NR    R2,R0                                                            
*                                                                               
         XC    PARM(12),PARM                                                    
         MVC   PARM+4(4),=X'D9000AFF'                                           
         GOTO1 VCALLOV,PARM                                                     
         CLI   4(R1),X'FF'                                                      
         BE    MFACSETX                                                         
         L     R5,0(R1)            R5=A(CORE RESIDENT INDEX)                    
         USING CRNDXD,R5                                                        
*                                                                               
MFACSET1 MVC   PARM+7(1),CRNDPHS   MOVE PHASE NUMBER FOR CALLOV                 
         EX    R2,*+8                                                           
         B     *+8                                                              
         TM    CRNDINDS,0          TEST SAME TEST VERSION                       
         BO    MFACSET2                                                         
         LA    R5,CRNDTBL                                                       
         LA    R5,4(R5)                                                         
         CLI   0(R5),X'FF'                                                      
         BNE   *-8                                                              
         B     MFACSET7                                                         
MFACSET2 LA    R5,CRNDTBL                                                       
*                                                                               
MFACSET3 SR    R1,R1               SET R1 TO A(FACILITIES)                      
         ICM   R1,3,0(R5)                                                       
         A     R1,VSYSFAC                                                       
         C     R4,0(R1)            IS THIS THE ONE WE COPIED                    
         BNE   MFACSET6                                                         
*                                                                               
         GOTO1 VCALLOV,PARM        GET ADDRESS FROM CALLOV                      
         SR    RE,RE                                                            
         ICM   RE,3,2(R5)          GET DSPL IN TABLE                            
         L     R0,PARMFAC                                                       
         AR    RE,R0                                                            
         MVC   0(4,RE),0(R1)       MOVE ADDRESS TO COPIED TABLE                 
*                                                                               
MFACSET6 LA    R5,4(R5)            BUMP TO NEXT TABLE ENTRY                     
         CLI   0(R5),X'FF'                                                      
         BNE   MFACSET3                                                         
*                                                                               
MFACSET7 LA    R5,1(R5)                                                         
         CLI   CRNDPHS,0           TEST FOR END OF TABLE                        
         BNE   MFACSET1                                                         
*                                                                               
MFACSETX XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
$IAM     EQU   X'26'                                                            
$ABEND   DC    X'01FF'                                                          
$UNWIND  DC    X'01FB'                                                          
VSYSFAC  DC    V(SYSFAC)                                                        
VSYSLST  DC    V(SYSLST)                                                        
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE AND OTHER DSECTS                                    *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
PARM     DS    6F                                                               
APARMS   DS    A                   A(CALLING PARAMETER LIST)                    
ASE      DS    A                   A(SELIST ENTRY TO SWITCH TO)                 
*                                                                               
PARMS    DS    0F                  FASWITCH PARAMETER LIST                      
PARMSE   DS    0X                  OVERRIDE SESYS                               
PARMSYS  DS    A                   A(3 CHARACTER SYSTEM MNEMONIC)               
PARMSOV  DS    0X                  DO NOT DELETE THIS SYS                       
PARMERR  DS    0X                  RETURN ERROR BYTE                            
*                                  0=SWITCH SUCCESSFUL                          
*                                  1=INVALID OR UNAUTHORISED SYSTEM             
*                                  2=SWITCH SUCCESSFUL BUT SE IS NO-OP          
PARMFAC  DS    A                   A(256 BYTE FACILITIES LIST AREA)             
PARML    EQU   *-PARMS                                                          
*                                                                               
RETCODE  DS    X                   RETURN CODE (SEE PARMERR)                    
ACTION   DS    X                   ACTION CODE                                  
SAVESE   DS    X                   SAVED SE NUMBER                              
SAVESOV  DS    X                   SAVED SYS PASSED FOR DELETE                  
*                                                                               
WORKX    EQU   *                                                                
                                                                                
*FACRNDXD                                                                       
       ++INCLUDE FACRNDXD                                                       
*FAPGMLST                                                                       
       ++INCLUDE FAPGMLST                                                       
*FASELIST                                                                       
       ++INCLUDE FASELIST                                                       
*FASYSLSTD                                                                      
       ++INCLUDE FASYSLSTD                                                      
*FASYSFAC                                                                       
       ++INCLUDE FASYSFAC                                                       
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
*FATSTTAB                                                                       
       ++INCLUDE FATSTTAB                                                       
*FATWA                                                                          
       ++INCLUDE FATWA                                                          
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
*IHAASCB                                                                        
         PRINT OFF                                                              
         IHAASCB                                                                
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FASWITCH  10/04/12'                                      
         END                                                                    
