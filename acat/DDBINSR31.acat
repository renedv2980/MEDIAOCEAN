*          DATA SET DDBINSR31  AT LEVEL 014 AS OF 07/20/10                      
*CATALP BINSR31                                                                 
*&&      SET   NOP=N                                                            
         SPACE 1                                                   *            
*==================================================================*            
*                                                                  *            
*   PARAMETER 1            A(RECORD TO BE ADDED OR FOUND)          *            
*                                                                  *            
*   PARAMETER 2            A(TABLE)                                *            
*                                                                  *            
*   PARAMETER 3            NUMBER OF RECORDS IN TABLE SO FAR.      *            
*                          THIS WILL BE UPDATED BY BINSRCH.        *            
*                                                                  *            
*   PARAMETER 4  BYTE 0    X'00' = FIND A RECORD                   *            
*                          X'01' = INSERT IF NOT FOUND             *            
*                          X'02' = READ HIGH                       *            
*                          X'80' = DELETE                          *            
*                                                                  *            
*                BYTE 1    X'80' = P7 HOLDS ACCESS REGISTER TO USE *            
*                                                                  *            
*                BYTE 2-3  L'RECORD                                *            
*                                                                  *            
*   PARAMETER 5  BYTE 0    DISPLACEMENT OF KEY INTO RECORD         *            
*                BYTE 1-3  L'KEY (MAX=255)                         *            
*                                                                  *            
*   PARAMETER 6            MAXIMUM NUMBER OF RECORDS IN TABLE      *            
*                                                                  *            
*   AFTER BINSRCH                                                  *            
*   -------------                                                  *            
*                                                                  *            
*   PARAMETER 1            X'80' = RECORD NOT FOUND                *            
*                          A(RECORD FOUND) OR                      *            
*                          A(WHERE RECORD HAS BEEN INSERTED) OR    *            
*                          A(0) WHEN TABLE IS FULL                 *            
*==================================================================*            
BINSR31  TITLE '31-BIT BINARY SEARCH ROUTINES'                                  
BINSRCH  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WRKX-WRKD,BINSRCH                                                
         USING WRKD,RC                                                          
*                                                                               
         LR    R9,R1                                                            
         ST    R1,SAVER1                                                        
         USING BSPARA,R9                                                        
         XC    ALET,ALET                                                        
         TM    BSP4+1,X'80'                                                     
         JZ    *+10                                                             
         MVC   ALET,BSP7           MAKE SURE THIS ZERO                          
*                                                                               
         LAM   AR0,ARF,=16F'0'                                                  
         LM    R2,R3,BSP2          R2-->TABLE   R3-->COUNT                      
         LAM   AR2,AR2,ALET                                                     
         SAC   512                                                              
*                                                                               
         LH    R4,BSP4+2           R4 --> RECORD LENGTH                         
         LH    R5,BSP5+2           R5 --> KEY LENGTH                            
         BCTR  R5,0                  -1 (FOR EX)                                
         LLC   R6,BSP5             R6 --> KEY DSPL                              
*                                                                               
         L     R7,BSP6             R7 --> MAX RECORDS                           
         L     R8,BSP1             R8 --> A(RECORD)                             
*                                                                               
         LTR   R3,R3               ANY DATA IN TABLE                            
         JNZ   BS1                  YES - SEARCH FOR EQUAL                      
         TM    BSP4,X'01'          INSERT IF NOT FOUND                          
         JO    AFRST                YES - ADD TO TABLE                          
*                                                                               
         XC    BSPAREC,BSPAREC                                                  
         OI    BSP1,X'80'          SET RECORD NOT FOUND                         
         J     BSEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* ADD FIRST RECORD                                                    *         
***********************************************************************         
         SPACE 1                                                                
AFRST    LA    R3,1(R3)            BUMP RECORD COUNTER                          
         ST    R3,BSPNOR                                                        
         ST    R2,BSPAREC                                                       
*                                                                               
         LR    R3,R4                                                            
         LR    R9,R4                                                            
         MVCL  R2,R8               MOVE RECORD TO TABLE                         
         JNO   *+6                 DESTRUCTIVE MOVE?                            
         DC    H'0'                YES!                                         
*                                                                               
         L     R9,SAVER1                                                        
         OI    BSP1,X'80'          SET RECORD NOT FOUND                         
         J     BSEXIT                                                           
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH ROUTINE INITIALISATION                                *         
***********************************************************************         
         SPACE 1                                                                
BS1      LR    R1,R3               CALCULATE END OF TABLE                       
         BCTR  R1,0                                                             
         MH    R1,BSPLENR+2                                                     
         AR    R1,R2               R1 = A(LAST RECORD)                          
         LAM   AR1,AR1,ALET                                                     
*                                                                               
         LA    RF,0(R6,R8)         CALCULATE KEY DISPLACEMENTS                  
         LA    RA,0(R6,R2)                                                      
         LAM   ARA,ARA,ALET                                                     
*                                                                               
         LR    R0,R4               INCREMENT VALUE                              
*                                                                               
*&&NOP                                                                          
         MVC   HOLD(8),=C'**KEY=**'                                             
         MVC   HOLD+8(48),0(RF)                                                 
         MVC   HOLD+56(8),=C'**REGS**'                                          
         STM   R0,RF,HOLD+64       <===== TEST ONLY                             
*&&                                                                             
*                                                                               
         CHI   R3,10               LT. 10 ENTRIES - LINEAR SEARCH               
         JH    BSS1                                                             
*                                                                               
         AR    R1,R6               BUMP BY KEY DISPLACEMENT                     
BS2      EX    R5,BSCMP                                                         
         JNE   BS3                                                              
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6               R2 = A(RECORD)                               
         J     SETOUTA                                                          
*                                                                               
BS3      JH    *+8                 PAST RECORD                                  
         BRXLE RA,R0,BS2                                                        
         SR    R1,R6               MINUS KEY DISPLACEMENT                       
         LR    R2,RA                                                            
         SR    R2,R6                                                            
         J     BS4                                                              
*                                                                               
BSCMP    CLC   0(0,RA),0(RF)                                                    
         EJECT                                                                  
***********************************************************************         
* BINARY SEARCH ROUTINE                                               *         
***********************************************************************         
         SPACE 1                                                                
BSS1     AR    R1,R4                                                            
         BCTR  R1,0                R1=A(END-1)                                  
         LR    R0,R4                                                            
         SR    R2,R0                                                            
         SR    R1,R2                                                            
         AR    R0,R0                                                            
         CR    R0,R1                                                            
         JNH   *-4                                                              
*                                                                               
         AR    R1,R2                                                            
         BASR  RE,0                                                             
         SRL   R0,1                                                             
         CR    R0,R4                                                            
         JL    BSS3                                                             
         BRXH  R2,R0,BSS2                                                       
*                                                                               
         LA    RA,0(R6,R2)                                                      
         EX    R5,BSSCMP                                                        
         JE    SETOUTA             MATCHED                                      
         BHR   RE                  HIGH                                         
*                                                                               
BSS2     SR    R2,R0               LOW                                          
         BR    RE                                                               
*                                                                               
BSS3     LA    R1,1(R1)                                                         
         AR    R2,R4               R2=A(INSERTION POINT)                        
         J     BS4                                                              
*                                                                               
BSSCMP   CLC   0(0,RF),0(RA)                                                    
         EJECT                                                                  
***********************************************************************         
* RECORD NOT FOUND                                                    *         
***********************************************************************         
         SPACE 1                                                                
BS4      OI    BSP1,X'80'          SET RECORD NOT FOUND                         
         TM    BSP4,X'01'          ADD IF NOT FOUND?                            
         JZ    BSEXIT              NO                                           
*                                                                               
         MVI   BSP4,X'01'          SLIDE TABLE FOR INSERT                       
         L     R3,BSPNOR                                                        
         CHI   R3,10               DID SEQUENTIAL SEARCH?                       
         JH    *+6                 NO                                           
         AR    R1,R4                                                            
         LR    RF,R1                                                            
         CR    R3,R7               INPUT NO ENTRY LT. MAX NO ENTRY              
         JL    *+14                YES - ADD ENTRY                              
         XC    BSPAREC,BSPAREC     NO - RETURN END OF TABLE                     
         J     BSEXIT                                                           
*                                                                               
         CR    R2,RF               ANY ENTRIES IN TABLE?                        
         JE    AFRST               NO                                           
*                                                                               
         LR    RA,RF                                                            
         SR    RA,R2               LENGTH OF MOVE                               
         LR    RE,RF                                                            
         AR    RF,R4                                                            
*                                                                               
         LAM   ARE,ARE,ALET        SET ACCESS REGISTERS FOR THIS MOVE           
         LAM   ARF,ARF,ALET                                                     
*                                                                               
SLD1     CHI   RA,255                                                           
         JL    SLD2                                                             
         SHI   RE,255                                                           
         SHI   RF,255                                                           
         MVC   HOLD,0(RE)                                                       
         MVC   0(255,RF),HOLD                                                   
         SHI   RA,255                                                           
         J     SLD1                                                             
*                                                                               
SLD2     SR    RE,RA                                                            
         SR    RF,RA                                                            
         LTR   RA,RA                                                            
         JZ    SLD3                                                             
         BCTR  RA,0                                                             
         EX    RA,SLDMVE                                                        
         EX    RA,SLDMVE1                                                       
*                                                                               
SLD3     LAM   ARE,ARF,=2F'0'      FOR GOD SAKE RESET THESE!                    
         J     AFRST                                                            
*                                                                               
SLDMVE   MVC   HOLD(0),0(RE)                                                    
*                                                                               
SLDMVE1  MVC   0(0,RF),HOLD                                                     
         EJECT                                                                  
***********************************************************************         
* EXACT MATCH ON RECORD                                               *         
***********************************************************************         
         SPACE 1                                                                
SETOUTA  ST    R2,BSPAREC          SET RECORD ADDRESS                           
         NI    BSP1,X'7F'          TURN OFF NOT FOUND                           
*                                                                               
         TM    BSP4,X'80'          DELETE RECORD FROM TABLE?                    
         JZ    BSEXIT2             NO                                           
*                                                                               
         L     R3,BSPNOR           SET END OF TABLE ADDRESS                     
         BCTR  R3,0                DECREMENT FOR DELETED SLOT                   
         MH    R3,BSPLENR+2                                                     
         A     R3,BSP2             ADD TABLE ADDRESS                            
         SR    R3,R2               SET LENGTH TO MOVE                           
*                                                                               
         LR    R4,R2                                                            
         LAM   AR4,AR4,ALET                                                     
         A     R4,BSPLENR                                                       
         LR    R5,R3                                                            
         MVCL  R2,R4               CLOSE UP TABLE                               
         JNO   *+6                 DESTRUCTIVE MOVE?                            
         DC    H'0'                YES!                                         
*                                                                               
         L     R5,BSPNOR                                                        
         BCTR  R5,0                                                             
         ST    R5,BSPNOR                                                        
         EJECT                                                                  
***********************************************************************         
* EXIT POINT                                                          *         
***********************************************************************         
         SPACE 1                                                                
BSEXIT   TM    BSP4,X'02'          TEST READ HIGH                               
         JZ    BSEXIT2                                                          
*                                                                               
         XC    BSPAREC,BSPAREC                                                  
         MH    R7,BSPLENR+2        CALCULATE TABLE END ADDRESS                  
         A     R7,BSPSTRT                                                       
         BCTR  R7,0                                                             
         CLR   R2,R7               FZSA                                         
         JNL   BSEXIT2             NOT IN TABLE: MAINTAIN REC NOT FOUND         
*                                                                               
         ST    R2,BSPAREC                                                       
         NI    BSP1,X'7F'          TURN OFF NOT FOUND                           
*                                                                               
BSEXIT2  LAM   AR2,AR2,=F'0'                                                    
         SAC   0                                                                
         XIT1  ,                                                                
         LTORG                                                                  
         SPACE 2                                                                
WRKD     DSECT                                                                  
SAVER1   DS    A                                                                
ALET     DS    A                                                                
HOLD     DS    CL256                                                            
WRKX     EQU   *                                                                
       ++INCLUDE DDBSPARA                                                       
         ORG   BSPARA                                                           
BSP1     DS    F                                                                
BSP2     DS    F                                                                
BSP3     DS    F                                                                
BSP4     DS    F                                                                
BSP5     DS    F                                                                
BSP6     DS    F                                                                
BSP7     DS    F                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014DDBINSR31 07/20/10'                                      
         END                                                                    
