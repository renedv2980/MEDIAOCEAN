*          DATA SET DDBRACKET  AT LEVEL 003 AS OF 05/01/02                      
*CATALP BRACKET                                                                 
*                                                                               
*********************************************************************           
*                                                                   *           
*  BRACKET                                                          *           
*                                                                   *           
*  FUNCTIONS 1 CALCULATION   FORMULA, OPERATE +-*X/% DECIMAL & HEX  *           
*                            ALLOWS  FOR BRACKETS                   *           
*  INPUT     P1  BYTE  0     L'FORMULA                              *           
*                      1-3   A(FORMULA)                             *           
*            P2        0     TYPE                                   *           
*                      1-3   A(ANSWER)                              *           
*            P3        1-3   A(COMMON FACILITY LIST)                *           
*            P4        1-3   A(CUREDIT)                             *           
*            P5        1-3   A(FORMULA)                             *           
*  OUTPUT    P1        0     PRECISION                              *           
*            P2        O     L'ANSWER    IF VALID                   *           
*                            X'FF'       IF INVALID                 *           
*            3               ANSWER IN PASSED ADDRESS               *           
*     TYPE= X-HEXADECIMAL INPUT                                     *           
*           0-STRING OUTPUT                                         *           
*           1-PACKED OUTPUT                                         *           
*********************************************************************           
         TITLE 'BRACKET  MODULE   '                                             
         PRINT NOGEN                                                            
BRACKET  CSECT                                                                  
         NMOD1 DATX-DATD,*FORMULA*,RR=R5,CLEAR=YES                              
         USING DATD,RC                                                          
*                                                                               
***************************************************************                 
*                                                                               
*  CONTROL BLOCK                                                                
*                                                                               
***************************************************************                 
*                                                                               
C1       BAS   RE,PREL             PRELIMINARIES                                
*                                                                               
C2       GOTO1 CHECKB,DMCB,(LFOR,ASTART)     CHECK BRACKETS                     
         CLI   DMCB,0                     NUMBER OF ( = NUMBER OF )             
         BE    C3                                                               
         MVI   ERR,1                                                            
         B     EXIT                                                             
*                                                                               
C3       GOTO1 INTBRA,DMCB,(LFOR,ASTART)    LOC INTERNAL BRACKETS               
         CLI   DMCB,0                                                           
         BE    C4                                                               
         B     ALL                 ALL BRACKETS HAVE BEEN PROCESSED             
*                                                                               
C4       MVC   ADDR1(4),DMCB+4     ADDR1=A'(                                    
         MVC   ADDR2(4),DMCB+8     ADDR1=A')                                    
*                                                                               
         GOTO1 DETFOR,DMCB,ADDR1,ADDR2    LOC STRING INSIDE BRACKETS            
         CLI   DMCB,0                                                           
         BE    C5                                                               
         MVI   ERR,1                                                            
         B     EXIT                                                             
*                                                                               
C5       ZIC   R2,DMCB+8                                                        
         STC   R2,LINFOR                                                        
         L     R3,DMCB+8                                                        
         LA    R3,0(R3)                                                         
         ST    R3,AINFOR                                                        
*                                                                               
C6       GOTO1 VFORMULA,DMCB,(LINFOR,AINFOR),(TYPE,ANS),ACOMFACS,      *        
               VCUREDIT            CALC FORMULA INSIDE BRACKETS                 
         CLI   DMCB+4,X'FF'                                                     
         BNE   C7                                                               
         MVI   ERR,1                                                            
         B     EXIT                                                             
*                                                                               
C7       MVC   LANS(1),DMCB+4      STORE L'ANSWER                               
         CLC   DMCB(1),PREC                                                     
         BNH   C8                                                               
         MVC   PREC(1),DMCB        CORRECT PRECISION                            
*                                                                               
C8       GOTO1 MOVERES,DMCB,ADDR1,ADDR2,(LANS,ANS)   MOVE ANSWER                
         B     C2                 CYCLE              INSIDE BRACKETS            
*                                                                               
*           CYCLE   COMPLETE  NO MORE BRACKETS IN FORMULA                       
*                                                                               
ALL      GOTO1 VALSTR,DMCB,(LFOR,ASTART)   LOC NEW START ADDRESS AND L          
         MVC   LFOR(1),DMCB+4              NEW L'FORMULA                        
         MVC   ASTART+1(3),DMCB+5          NEW A(START OF FORMULA)              
*                                                                               
C9       GOTO1 VFORMULA,DMCB,(LFOR,ASTART),(TYPE,ANS),ACOMFACS,        *        
               VCUREDIT                                                         
         CLI   DMCB+4,X'FF'                                                     
         BNE   C10                                                              
         MVI   ERR,1                                                            
         B     EXIT                                                             
*                                                                               
C10      MVC   LANS(1),DMCB+4                                                   
         CLC   DMCB(1),PREC                                                     
         BNH   C11                                                              
         MVC   PREC(1),DMCB                                                     
*                                                                               
*                    CALCULATION IS COMPLETED                                   
*                                                                               
C11      L     R2,APARAM                                                        
         MVC   0(1,R2),PREC        MOVE PRECISION                               
         MVC   4(1,R2),LANS        MOVE L'ANSWER                                
         L     R4,AANS             R4=A(ANSWER IN CALLING PROGRAM)              
         ZIC   R3,LANS                                                          
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),ANS         MOVE ANSWER                                  
*                                                                               
EXIT     L     R2,APARAM                                                        
         CLI   ERR,0                                                            
         BE    C12                                                              
         MVI   4(R2),X'FF'                                                      
C12      XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                   *           
*  PRELIMINARIES                                                    *           
*********************************************************************           
*                                                                               
PREL     DS    0H                                                               
         NTR1                                                                   
*                                                                               
         ST    R1,APARAM           APARAM=A(PARAMETER LIST)                     
         L     R2,0(R1)                                                         
         LA    R2,0(R2)            R2=A(START OF FORMULA)                       
*                                                                               
         ZIC   R3,0(R1)            R3=L'FORMULA                                 
         STC   R3,LFOR                                                          
         BCTR  R3,0                                                             
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   STRING,0(R2)        MOVE FORMULA                                 
         LA    R2,STRING                                                        
         ST    R2,ASTART                                                        
         AR    R2,R3                                                            
         ST    R2,AEND             R2=A(END OF FORMULA)                         
*                                                                               
         ZIC   R3,4(R1)                                                         
         STC   R3,TYPE                                                          
*                                                                               
         L     R3,4(R1)                                                         
         LA    R3,0(R3)                                                         
         ST    R3,AANS                                                          
*                                                                               
         L     R2,8(R1)                                                         
         LA    R2,0(R2)                                                         
         ST    R2,ACOMFACS         R2=A(COMMON FACILITY LIST)                   
         L     R3,12(R1)                                                        
         LA    R3,0(R3)                                                         
         ST    R3,VCUREDIT         R3=A(CUREDITR)                               
*                                                                               
         L     R3,16(R1)                                                        
         LA    R3,0(R3)                                                         
         ST    R3,VFORMULA                                                      
*                                                                               
*                                                                               
         ST    R5,RELO                                                          
         XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                   *           
*  CHECKB                                                           *           
*                                                                   *           
*  FUNCTIONS 1 CHECK NUMBER OF BRACKETS NUMBER OF ( = NUMBER OF )   *           
*  INPUT     P1  BYTE  0     L'FORMULA                              *           
*                      1-3   A(FORMULA)                             *           
*  OUTPUT    P1        0     0-  VALID  1-  INVALID                 *           
*                                                                   *           
*********************************************************************           
*                                                                               
CHECKB   NTR1                                                                   
*                                                                               
         ZIC   R2,DMCB             R2=L'FORMULA                                 
         L     R3,DMCB                                                          
         LA    R3,0(R3)            R3=A(FORMULA)                                
         XR    R4,R4                                                            
*                                                                               
*                  CALC NUMBER (                                                
*                                                                               
CH1      CLI   0(R3),C'('                                                       
         BE    CH2                                                              
         LA    R3,1(R3)                                                         
         BCT   R2,CH1                                                           
         B     CH3                                                              
*                                                                               
CH2      LA    R3,1(R3)                                                         
         LA    R4,1(R4)            R4=NUMBER OF (                               
         BCT   R2,CH1                                                           
*                                                                               
*                  CALC NUMBER )                                                
*                                                                               
CH3      ZIC   R2,DMCB             R2=L'FORMULA                                 
         L     R3,DMCB                                                          
         LA    R3,0(R3)            R3=A(FORMULA)                                
         XR    R5,R5                                                            
*                                                                               
CH4      CLI   0(R3),C')'                                                       
         BE    CH5                                                              
         LA    R3,1(R3)                                                         
         BCT   R2,CH4                                                           
         B     CH6                                                              
*                            CHECK NUMBERS                                      
CH5      LA    R3,1(R3)                                                         
         LA    R5,1(R5)            R5=NUMBER OF )                               
         BCT   R2,CH4                                                           
*                                                                               
CH6      CR    R4,R5                                                            
         BE    CH7                                                              
         MVI   DMCB,1                                                           
         B     ENDCH                                                            
*                                                                               
CH7      MVI   DMCB,0                                                           
ENDCH    XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                   *           
*  INTBRA                                                           *           
*                                                                   *           
*  FUNCTIONS 1   LOC INTERNAL BRACKETS                              *           
*  INPUT     P1  BYTE  0     L'FORMULA                              *           
*                      1-3   A(FORMULA)                             *           
*  OUTPUT    P1        0     0- BRACKETS EXIST  1-  DONT EXIST      *           
*            P2        1-3   A(()                                   *           
*            P3        1-3   A())                                   *           
*                                                                   *           
*********************************************************************           
*                                                                               
INTBRA   NTR1                                                                   
*                                                                               
         ZIC   R2,DMCB             R2=L'FORMULA                                 
         L     R3,DMCB                                                          
         LA    R3,0(R3)            R3=A(FORMULA)                                
*                                                                               
*                       LOC (                                                   
*                                                                               
IN1      CLI   0(R3),C'('                                                       
         BE    IN2                R3=A(()                                       
         LA    R3,1(R3)                                                         
         BCT   R2,IN1                                                           
         MVI   DMCB,1                                                           
         B     ENDIN              NO BRACKETS                                   
*                                                                               
*                                 LOC NEXT BRACKET                              
*                                                                               
IN2      ST    R3,A1                                                            
IN21     LA    R3,1(R3)                                                         
         CLI   0(R3),C'('                                                       
         BE    IN3                                                              
         CLI   0(R3),C')'                                                       
         BE    IN3                                                              
         B     IN21                                                             
*                                                                               
IN3      CLI   0(R3),C')'                                                       
         BE    IN4                                                              
         B     IN2                                                              
*                                                                               
IN4      MVC   DMCB+4(4),A1        MOVE A(()                                    
         ST    R3,DMCB+8           STORE A())                                   
         MVI   DMCB,0                                                           
*                                                                               
ENDIN    XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                   *           
*  DETFOR                                                           *           
*                                                                   *           
*  FUNCTIONS 1   LOC INTERNAL FORMULA                               *           
*  INPUT     P1  BYTE  1-3   A(()                                   *           
*            P2        1-3   A())                                   *           
*  OUTPUT    P1        0     0- VALID      1-  INVALID              *           
*            P3        0-    L'INFOR                                *           
*                      1-3   A(INFOR)                               *           
*********************************************************************           
*                                                                               
DETFOR   NTR1                                                                   
*                                                                               
         L     R2,DMCB                                                          
         LA    R2,0(R2)            R2=A(()                                      
         L     R3,DMCB+4                                                        
         LA    R3,0(R3)            R3=A())                                      
*                                                                               
*                       LOC A(START)                                            
*                                                                               
         LA    R2,1(R2)                                                         
         CR    R2,R3                                                            
         BNE   D1                                                               
         MVI   DMCB,1             EMPTY                                         
         B     ENDDET                                                           
*                                                                               
D1       CLI   0(R2),X'40'                                                      
         BH    D2                                                               
         LA    R2,1(R2)                                                         
         CR    R2,R3                                                            
         BL    D1                                                               
         MVI   DMCB,1             EMPTY                                         
         B     ENDDET                                                           
*                                                                               
*                                                                               
D2       ST    R2,DMCB+8          STORE A(START)                                
         MVI   DMCB,0                                                           
*                                                                               
*                           LOC A(END)                                          
D3       BCTR  R3,0                                                             
         CLI   0(R3),X'40'                                                      
         BH    D4                                                               
         B     D3                                                               
*                                                                               
D4       SR    R3,R2                                                            
         LA    R3,1(R3)             R3=L'FORMULA                                
         STC   R3,DMCB+8                                                        
*                                                                               
ENDDET   XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                   *           
*  MOVERES                                                          *           
*                                                                   *           
*  FUNCTIONS 1   MOVE RESULT                                        *           
*  INPUT     P1  BYTE  1-3   A(()                                   *           
*            P2        1-3   A())                                   *           
*            P3        0     L'ANSWER                               *           
*                      1-3   A(ANSWER)                              *           
*  OUTPUT    P1        ANSWER INSTEAD  OF  BRACKETS IN FORMULA      *           
*********************************************************************           
*                                                                               
MOVERES  NTR1                                                                   
*                                                                               
         L     R2,DMCB                                                          
         LA    R2,0(R2)            R2=A(()                                      
         L     R3,DMCB+4                                                        
         LA    R3,0(R3)            R3=A())                                      
         ZIC   R4,DMCB+8           R4=L'ANSWER                                  
         L     R5,DMCB+8                                                        
         LA    R5,0(R5)            R5=A(ANSWER)                                 
*                                                                               
*         CLEAN  BRACKETS                                                       
*                                                                               
         SR    R3,R2               R3=L'BRACKETS-1                              
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),=60C' '                                                  
*                                                                               
         BCTR  R4,0                                                             
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(R5)       MOVE ANSWER                                  
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
*                                                                   *           
*  VALSTR                                                           *           
*                                                                   *           
*  FUNCTIONS 1   LOC NEW ADDRESS AND LENGTH FOR FORMULA             *           
*  INPUT     P1  BYTE  0     L'FORMULA                              *           
*                      1-3   A(FORMULA                              *           
*  OUTPUT    P2        0     NEW L'FORMULA                          *           
*                      1-3   NEW A(FORMULA)                         *           
*********************************************************************           
*                                                                               
VALSTR   NTR1                                                                   
*                                                                               
         ZIC   R2,DMCB             R2=L                                         
         L     R3,DMCB                                                          
         LA    R3,0(R3)            R3=A                                         
*                                                                               
*                       LOC A(START)                                            
*                                                                               
V1       CLI   0(R3),X'40'                                                      
         BH    V2                                                               
         LA    R3,1(R3)                                                         
         B     V1                                                               
*                                                                               
*                                                                               
V2       ST    R3,DMCB+4          STORE A(START)   NEW                          
*                                                                               
*                           LOC A(END)                                          
         L     R4,DMCB                                                          
         LA    R4,0(R4)                                                         
         AR    R4,R2                                                            
         BCTR  R4,0                R4=A(END)   OLD                              
*                                                                               
V3       CLI   0(R4),X'40'                                                      
         BH    V4                                                               
         BCT   R4,V3                                                            
*                                                                               
V4       SR    R4,R3                                                            
         LA    R4,1(R4)                                                         
         STC   R4,DMCB+4           STORE NEW LENGTH                             
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
DATD     DSECT                                                                  
DMCB     DS    6F                                                               
ASTART   DS    A                                                                
AEND     DS    A                                                                
AANS     DS    A                                                                
ACOMFACS DS    A                                                                
VCUREDIT DS    V                                                                
VFORMULA DS    V                                                                
ADDR1    DS    A                                                                
ADDR2    DS    A                                                                
AINFOR   DS    A                                                                
APARAM   DS    A                                                                
RELO     DS    A                                                                
A1       DS    A                                                                
*                                                                               
ERR      DS    CL1                                                              
MEM      DS    CL1                                                              
TYPE     DS    CL1                                                              
LFOR     DS    CL1                                                              
LINFOR   DS    CL1                                                              
PREC     DS    CL1                                                              
LANS     DS    CL1                                                              
ANS      DS    CL20                                                             
STRING   DS    CL60                                                             
DATX     DS    0C                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDBRACKET 05/01/02'                                      
         END                                                                    
