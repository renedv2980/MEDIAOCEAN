*          DATA SET REREPIO    AT LEVEL 006 AS OF 05/01/02                      
*          DATA SET REREPIO    AT LEVEL 003 AS OF 03/02/96                      
*CATALP REPIOA                                                                  
         TITLE 'REREPIO - REPIO - REP IO MODULE'                                
*                                                                               
*********************************************************************           
*                                                                   *           
* REP IO MODULE                                                     *           
*                                                                   *           
*   * REPIOBLK HAS ADDR OF DATAMGR AND IOAREA                       *           
*                                                                   *           
*   * R2 COVERS REPIOBLK                                            *           
*   * R3 POINTS TO KEY                                              *           
*                                                                   *           
* ----------------------------------------------------------------- *           
*                                                                   *           
*                                                                   *           
* UPDATE HISTORY:                                                   *           
*                                                                   *           
* 13FEB96 01 EFJ -- SUPPORT SKIPPING GETREC                         *           
*                                                                   *           
* 05MAR96 02 PXZ -- SUPPORT NEGATIVE FILTER FOR RIPCON              *           
*                                                                   *           
* 01DEC98 11 PXZ -- SUPPORT BACK SCROLLING IN RIS                   *           
*                                                                   *           
* 11DEC98    BU  -- SENSITIZE TEAM FILTER IF S/P ENTERED            *           
*                                                                   *           
*                                                                   *           
*                                                                   *           
*                    **  END TOMBSTONE  **                          *           
*********************************************************************           
*                                                                               
REPIO    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,*REPIO*,RA,RR=R3                                               
         USING REPIO,RB,RA                                                      
         L     R2,0(R1)            PASS ADDR OF REPIOBLK                        
         USING REPIOD,R2                                                        
         ST    R3,RELO                                                          
                                                                                
                                                                                
* TEST INITIAL VALUES                                                           
                                                                                
         OI    RIPSTAT,RIPRQERR    SET REP CODE/DATES ERROR                     
         OC    RIPREP,RIPREP       REP CODE                                     
         BZ    RIPX                ERROR                                        
         OC    RIPDATS,RIPDATS     START DATE                                   
         BZ    RIPX                ERROR                                        
         OC    RIPDATE,RIPDATE     END DATE                                     
         BZ    RIPX                ERROR                                        
         OC    RIPDATSS,RIPDATSS   START DATE - 1 YEAR                          
         BZ    RIPX                ERROR                                        
         OC    RIPIOARE,RIPIOARE   IO AREA                                      
         BZ    RIPX                ERROR                                        
         OC    RIPDTMGR,RIPDTMGR   DATAMGR                                      
         BZ    RIPX                ERROR                                        
         NI    RIPSTAT,X'FF'-RIPRQERR           OK/CLEAR ERROR                  
         B     RIP10                                                            
*                                                                               
         EJECT                                                                  
*                                                                               
RIP10    DS    0H                                                               
         LA    R3,RIPKEY                                                        
         TM    RIPSTAT,X'80'       RIPKEY+28 HAS DISK ADDR ?                    
         BNO   RIP11               NO                                           
*                                                                               
*                                  YES                                          
         BAS   RE,GETREC                  GET RECORD                            
*                                                                               
         L     R4,RIPIOARE                CONSTRUCT PASSIVE POINTER             
         USING RCONREC,R4                                                       
*                                                                               
         CLI   RIPSTA,0               ARE WE FILTERING ON STATION?              
         BE    RIP10D                 NO                                        
         CLC   RIPSTA,RCONKSTA        YES/DOES IT MATCH REC?                    
         BE    RIP10D                     YES                                   
         MVC   RIPSTA,RCONKSTA            NO - ASSUME COMBO/SET FILTER          
         OI    RIPSTAT2,X'01'         RESET ACOMSTA POINTER                     
*                                                                               
RIP10D   XC    RIPKEY+3(5),RIPKEY+3       CLEAR STATION FIELD                   
         CLI   RIPKEY,X'8E'               IF 8E KEY                             
         BNE   *+10                                                             
         MVC   RIPKEY+3(5),RCONKSTA       SET STATION FIELD FROM RECORD         
                                                                                
* A LITTLE FUDGE HERE - APPLICATION PROGRAM PASSES DATCON IN RIPFULL            
         GOTO1 RIPFULL,DMCB,(3,RCONDATE),(2,RIPKEY+8)                           
         GOTO1 RIPFULL,DMCB,(3,RCONDATE+3),(2,RIPKEY+10)                        
         MVC   RIPKEY+12(4),RCONKCON                                            
         MVI   RIPKEY+16,1                                                      
         MVC   RIPKEY+17(6),RCONKAGY                                            
         MVC   RIPKEY+23(4),RCONKADV                                            
         MVI   RIPSTAT,0                 CLEAR STATUS KEY                       
         XC    RIPFULL,RIPFULL           AND THE FUDGE                          
         B     RIPHIGH                   NOW RESUME NORMAL FLOW                 
         DROP  R4                                                               
*                                                                               
RIP11    OC    RIPKEY,RIPKEY             IS IT FIRST TIME                       
         BZ    RIP12               YES                                          
         CLI   RIPKEY,X'8D'              ,,IF NOT X'8D' KEY                     
         BE    RIP15                                                            
         CLI   RIPKEY,X'8E'              ,,IF NOT X'8E' KEY                     
         BE    RIP15                                                            
         XC    RIPKEY,RIPKEY             ,,ASSUME FIRST TIME                    
                                                                                
                                                                                
* - FIRST TIME - SET UP KEY                                                     
RIP12    MVI   0(R3),X'8E'         ,,X'8E' KEY                                  
         CLI   RIPSTA,0            ,,EXCEPT IF ALL STATIONS                     
         BNE   *+8                                                              
         MVI   0(R3),X'8D'         ,,THEN X'8D' KEY                             
         MVC   1(2,R3),RIPREP      REP                                          
         MVC   3(5,R3),RIPSTA      STATION                                      
         MVC   8(2,R3),RIPDATSS    START DATE COMPRESSED                        
         CLI   RIPSKIP,C'Y'        IF NOT LIST REQUEST                          
         BNE   RIPHIGH             THEN READ ALL RECS                           
         MVC   10(2,R3),RIPDATS    END DATE = REQ START DATE                    
         MVC   RIPKDTSV,8(R3)      SAVE START DATE FOR SKIP READ                
         B     RIPHIGH                                                          
                                                                                
RIPHIGH  DS    0H                                                               
         BAS   RE,HIGH                                                          
         B     RIP22                                                            
                                                                                
RIP15    TM    RIPSTAT,RIPRDHIO            READ HI ONLY                         
         BNO   RIP17                                                            
         NI    RIPSTAT,X'FF'-RIPRDHIO      YES/TURN OFF FLAG                    
         BAS   RE,HIGH                                                          
         B     RIP22                       AND GO CHECK                         
                                                                                
RIP17    TM    RIPSTAT,RIPRDHI             READ HI BEFORE SEQ                   
         BNO   RIPSEQ                                                           
         NI    RIPSTAT,X'FF'-RIPRDHI       YES/CLEAR FLAG                       
         BAS   RE,HIGH                         READ HI                          
         B     RIPSEQ                                                           
                                                                                
                                                                                
RIPSEQ   DS    0H                                                               
         CLC   RIPFULL,=F'100000'   CHECK MAX IO                                
         BNH   *+12                                                             
         OI    RIPSTAT,RIPMAXIO                                                 
         B     RIPX                                                             
         BAS   RE,SEQ                                                           
                                                                                
                                                                                
* - TEST IF KEY MATCHES REQUEST                                                 
RIP22    DS    0H                                                               
         CLC   RIPKEY(3),RIPKEYSV       TYPE/REP CODE                           
         BNE   RIPEOF                                                           
*                                                                               
         EJECT                                                                  
*                                                                               
         CLI   RIPSTA,0         ARE WE READING FOR SPECIFIC STATION ?           
         BNE   RIP24                                                            
                                                                                
* WHEN REQUEST IS FOR ALL STATIONS    X'8D' KEY                                 
         CLC   8(2,R3),RIPDATE    IF RECORD START > REQ END                     
         BH    RIPEOF              THAT'S ALL                                   
         CLC   10(2,R3),RIPDATS    IS RECORD END < REQ START                    
         BNL   RIP26               NO/OK                                        
                                                                                
* REC NOT WITHIN REQUESTED DATE RANGE                                           
         CLI   RIPSKIP,C'Y'    BUT IF RIPSKIP=0 THEN NOT LIST TYPE              
         BNE   RIP26               SO PASS REC TO APPLICATION                   
                                                                                
         OC    RIPKDTSV,RIPKDTSV       ARE WE SKIP READING NOW?                 
         BZ    RIP23                   NO                                       
         CLC   RIPKEY+8(2),RIPKDTSV    YES-HAS START DATE CHANGED ?             
         BE    RIP23                       NO                                   
         MVC   RIPKDTSV,RIPKEY+8           YES/SAVE KEY START DATE              
         MVC   RIPKEY+10(2),RIPDATS          REQ START TO KEY END DATE          
         XC    RIPKEY+12(4),RIPKEY+12        CLEAR CONTRACT ID                  
         B     RIPHIGH                                                          
                                                                                
RIP23    CLI   16(R3),1            SHOULD ALWAYS BE REC ID=1 HERE               
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   16(R3),X'FF'        BUMP OVER NEXT TWO                           
         B     RIPHIGH             TO NEXT ID=1 REC                             
                                                                                
*                                                                               
* WHEN READING FOR SPECIFIC STATION    X'8E' KEY                                
RIP24    CLC   RIPSTA,3(R3)        IF STATIONS DON'T MATCH                      
         BNE   RIPEOF              THAT'S ALL FOLKS                             
*                                                                               
* TESTING                                                                       
**       CLC   =X'04117038',12(R3)                                              
**       BNE   *+6                                                              
**       DC    H'0'                                                             
*                                                                               
         CLC   8(2,R3),RIPDATE    IF REC START > REQ END                        
         BH    RIPEOF              NO MORE RECS                                 
         CLC   10(2,R3),RIPDATS    IS REC END < REQ START                       
         BNL   RIP26               NO-OK                                        
                                                                                
* REC NOT WITHIN REQUESTED DATE RANGE                                           
         CLI   RIPSKIP,0           IF RIPSKIP=0 THEN NOT LIST REQUEST           
         BE    RIP26               SO PASS REC TO APPLICATION FOR               
*                                  PREVIOUS DOLLARS                             
                                                                                
         OC    RIPKDTSV,RIPKDTSV       ARE WE SKIP READING NOW?                 
         BZ    RIP25                   NO                                       
         CLC   RIPKEY+8(2),RIPKDTSV    YES-HAS START DATE CHANGED ?             
         BE    RIP25                     NO                                     
         MVC   RIPKDTSV,RIPKEY+8         YES/SAVE KEY START DATE                
         MVC   RIPKEY+10(2),RIPDATS          REQ START TO KEY END DATE          
         XC    RIPKEY+12(4),RIPKEY+12        CLEAR CONTRACT ID                  
         B     RIPHIGH                                                          
                                                                                
RIP25    CLI   16(R3),1            SHOULD ALWAYS BE REC ID=1 HERE               
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   16(R3),X'FF'        BUMP OVER NEXT TWO                           
         B     RIPHIGH             TO NEXT ID=1 REC                             
                                                                                
                                                                                
RIP26    CLI   16(R3),1                                                         
         BE    RIPK01                                                           
         CLI   16(R3),2                                                         
         BE    RIPK02                                                           
         CLI   16(R3),3                                                         
         BE    RIPK03                                                           
         DC    H'0'                                                             
                                                                                
* ID=1 KEY ENTERS HERE                                                          
RIPK01   CLI   RIPAGY,0            AGENCY                                       
         BE    RIP27                                                            
         CLC   RIPAGY,17(R3)                                                    
         BNE   RIP1NO                                                           
                                                                                
RIP27    CLI   RIPAGOFF,0          AGENCY-OFFICE                                
         BE    RIP28                                                            
         CLC   RIPAGOFF,21(R3)                                                  
         BNE   RIP1NO                                                           
                                                                                
RIP28    CLI   RIPADV,0          ADVERTISER                                     
         BE    RIP1YES                                                          
         CLC   RIPADV,23(R3)                                                    
         BE    RIP1YES                                                          
                                                                                
*                                  IF ID=1 FAILS TEST                           
RIP1NO   EQU   *                                                                
*        BAS   RE,SEQ              GET ID=2 KEY                                 
*        BAS   RE,SEQ              GET ID=3 KEY                                 
*        BAS   RE,SEQ              GET NEXT KEY                                 
         MVI   16(R3),X'FF'        BUMP TO NEXT KEY                             
         B     RIPHIGH                                                          
                                                                                
RIP1YES  BAS   RE,SEQ              GET ID=2 KEY                                 
         CLI   16(R3),2                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
                                                                                
* ID=2 KEY ENTERS HERE                                                          
RIPK02   CLI   RIPSAL,0            SALES PERSON                                 
         BE    *+14                                                             
         CLC   RIPSAL,17(R3)                                                    
         BNE   RIP2NO                                                           
                                                                                
         CLI   RIPCON,0            CONTRACT TYPE                                
         BE    RIP210                                                           
         TM    RIPCON,X'FF'-X'BF'  NEGATIVE FILTERING?                          
         BO    RIP208              NO                                           
         MVC   MYWORK(1),RIPCON    YES                                          
         OI    MYWORK,X'FF'-X'BF'                                               
         CLC   MYWORK(1),20(R3)    NEGATIVE FILTER MATCHES                      
         BE    RIP2NO              THEN REJECT                                  
         B     RIP210              OK                                           
                                                                                
RIP208   CLC   RIPCON,20(R3)                                                    
         BNE   RIP2NO                                                           
                                                                                
                                                                                
                                                                                
RIP210   CLI   RIPGRP,0            GROUP                                        
         BE    *+14                                                             
         CLC   RIPGRP,21(R3)                                                    
         BNE   RIP2NO                                                           
                                                                                
         CLI   RIPSBGP,0            SUBGROUP                                    
         BE    *+14                                                             
         CLC   RIPSBGP,22(R3)                                                   
         BNE   RIP2NO                                                           
                                                                                
         CLI   RIPCAT,0            CATEGORY                                     
         BE    *+14                                                             
         CLC   RIPCAT,23(R3)                                                    
         BNE   RIP2NO                                                           
                                                                                
         CLI   RIPTEAM,X'40'        TEAM (DIV)                                  
         BNH   RIP2END                                                          
         CLC   RIPTEAM(1),25(R3)                                                
         BNE   RIP2NO                                                           
                                                                                
         CLI   RIPSAL,0            ANY SALESPERSON FILTER?                      
         BNE   RIP2TM01            YES - DIV/TEAM MUST BE EXACT MATCH           
         CLI   RIPTEAM+1,X'40'       TEAM (TEAM)                                
         BNH   *+14                                                             
RIP2TM01 EQU   *                                                                
         CLC   RIPTEAM+1(1),26(R3)                                              
         BNE   RIP2NO                                                           
                                                                                
RIP2END  EQU   *                    PASSED ALL ID=2 FILTERS                     
         B     RIP2YES                                                          
                                                                                
RIP2NO   DS    0H                                                               
         BAS   RE,SEQ              GET ID=3 KEY                                 
         BAS   RE,SEQ              GET NEXT KEY                                 
         BE    RIP22               CONTINUE NORMAL PROCESSING                   
*                                                                               
         EJECT                                                                  
                                                                                
*                                                                               
RIP2YES  EQU   *                                                                
         BAS   RE,SEQ              GET ID=3 KEY                                 
         CLI   16(R3),3                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
* ID=3 KEY ENTERS HERE                                                          
RIPK03   OC    RIPOFF,RIPOFF       OFFICE                                       
         BZ    *+14                                                             
         CLC   RIPOFF,17(R3)                                                    
         BNE   RIP3NO                                                           
                                                                                
         OC    RIPDEMO,RIPDEMO     DEMO                                         
         BZ    RIP310                                                           
         CLC   RIPDEMO,19(R3)      COMPARE DEMOS                                
         BE    RIP310              OK                                           
*                                  NOT OK                                       
* RIPDEMO VALIDATED FOR RADIO - RADIO IMP DEMOS HAVE 'I' IN 2ND POST            
*                               TV IMP DEMOS HAVE 'T' IN 2ND POST               
*                               CHECK FOR BOTH                                  
         CLI   RIPDEMO+1,C'I'      IS IT IMP DEMO ?                             
         BNE   RIP3NO              NO - SO REJECT IT                            
         MVC   MYWORK(3),RIPDEMO   YES - TRY 'T' INSTEAD OF 'I'                 
         MVI   MYWORK+1,C'T'                                                    
         CLC   MYWORK(3),19(R3)                                                 
         BNE   RIP3NO                                                           
                                                                                
RIP310   OC    RIPCDATS,RIPCDATS     CREATION DATE                              
         BZ    RIP3END                                                          
         CLC   22(2,R3),RIPCDATS                                                
         BL    RIP3NO                                                           
         CLC   22(2,R3),RIPCDATE                                                
         BH    RIP3NO                                                           
                                                                                
RIP3END  EQU   *                   PASSED ALL ID=3 FILTERS                      
         B     RIP3YES                                                          
                                                                                
RIP3NO   EQU   *                                                                
         OC    RIPKDTSV,RIPKDTSV       ARE WE SKIP READING NOW?                 
         BZ    RIP3N10                 NO                                       
         CLC   RIPKEY+8(2),RIPKDTSV    YES-HAS START DATE CHANGED ?             
         BE    RIP3N10                   NO                                     
         MVC   RIPKDTSV,RIPKEY+8         YES/SAVE KEY START DATE                
         MVC   RIPKEY+10(2),RIPDATS          REQ START TO KEY END DATE          
         XC    RIPKEY+12(4),RIPKEY+12        CLEAR CONTRACT ID                  
         B     RIPHIGH                                                          
RIP3N10  B     RIPSEQ                                                           
                                                                                
                                                                                
                                                                                
RIP3YES  EQU   *                                                                
                                                                                
         TM    RIPSTAT,RIPNOGR     SKIP GETREC?                                 
         BZ    *+12                                                             
         NI    RIPSTAT,X'FF'-RIPNOGR  RESET FLAG                                
         B     RIPX                                                             
                                                                                
         BAS   RE,GETREC                                                        
*                                                                               
RIP3N20  L     R4,RIPIOARE                                                      
         USING RCONREC,R4                                                       
         TM    RCONCNTL,X'80'      IF DELETED                                   
         BO    RIPSEQ                                                           
*                                                                               
         OI    RIPSTAT,RIPRQREC        SET GOT REC FLAG                         
         CLI   RIPSKIP,C'Y'        IS IT LIST TYPE REQUEST ?                    
         BNE   *+10                NO                                           
         MVC   RIPKDTSV,8(R3)      YES SAVE DATE FOR SKIP READ                  
         B     RIPX                                                             
         EJECT                                                                  
* - END OF FILE                                                                 
RIPEOF   MVI   RIPSTAT,RIPENDF                                                  
         B     RIPX                                                             
                                                                                
RIPX     XIT1                                                                   
*                                                                               
         EJECT                                                                  
*                                                                               
HIGH     NTR1                                                                   
         L     R6,RIPFULL          KEEP I/0 COUNTER                             
         LA    R6,1(R6)                                                         
         ST    R6,RIPFULL                                                       
         MVC   RIPKEYSV,RIPKEY                                                  
         GOTO1 RIPDTMGR,DMCB,=C'DMRDHI',=C'REPDIR',RIPKEYSV,RIPKEY,0            
         B     RIPX                                                             
*                                                                               
SEQ      NTR1                                                                   
         L     R6,RIPFULL          KEEP I/0 COUNTER                             
         LA    R6,1(R6)                                                         
         ST    R6,RIPFULL                                                       
         MVC   RIPKEYSV,RIPKEY                                                  
         GOTO1 RIPDTMGR,DMCB,=C'DMRSEQ',=C'REPDIR',RIPKEYSV,RIPKEY,0            
         B     RIPX                                                             
*                                                                               
GETREC   NTR1                                                                   
         MVC   RIPKEYSV,RIPKEY                                                  
         L     R4,RIPIOARE                                                      
         LA    R5,RIPKEY+28                                                     
         GOTO1 RIPDTMGR,DMCB,=C'GETREC',=C'REPFILE',(R5),(R4),DMWORK            
         B     RIPX                                                             
*                                                                               
         EJECT                                                                  
                                                                                
*                                                                               
DMWORK   DS    12D                                                              
DMCB     DS    6F                                                               
MYWORK   DS    CL20                                                             
RELO     DS    F                                                                
                                                                                
*                                                                               
*        LITERAL POOL                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE REPIOBLK                                                       
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006REREPIO   05/01/02'                                      
         END                                                                    
