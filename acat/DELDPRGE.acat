*          DATA SET DELDPRGE   AT LEVEL 012 AS OF 05/08/20                      
*PROCESS USING(WARN(15))                                                        
*CATALP DELDPRGE                                                                
*                                                                               
* SET THE &&TEST FLAG TO "Y" TO ENABLE TEST LOGIC. THIS FLAG MUST               
* ALWAYS DEFAULT TO "N" IN PRODUCTION!                                          
*&&      SET   TEST=N                                                           
*                                                                               
         TITLE 'LDPURGE - DEMOS - DIRECTORY LOAD PURGE TEST'                    
*        PARAMS VIA R1                                                          
*        XL1   X'00'  RETURN  X'00'=NOPURGE  OR  X'FF'=PURGE                    
*        AL3   A(RECORD)                                                        
*        AL4   A(ISDTF#)           INTERNAL I/S FILE NUMBER                     
*                                                                               
         PRINT NOGEN                                                            
LDPURGE  CSECT                                                                  
         NMOD1 0,DELDPRGE                                                       
*                                                                               
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
*                                                                               
         LR    R9,R1               SAVE A(PARMS)                                
*                                                                               
         BC    0,CHECKREC                                                       
         MVI   *-3,X'F0'           *** SELF-MODIFYING CODE ***                  
         B     INIT                                                             
*                                                                               
         EJECT                                                                  
*                                                                               
*======================================================================         
*                                                                               
* LOCAL DAILIES SPORTS EVENT TABLE                                              
*                                                                               
* CHANGE THIS EVERY YEAR!                                                       
*                                                                               
* KEEP SPORTS PROGRAMMING WEEKS FOR FOUR YEARS, NOT THREE.                      
* DON'T ATTEMPT TO KEEP ONLY PARTIAL WEEKS. ALL WEEKS START ON MONDAY.          
* USE THE STANDARD BROADCAST CALENDAR TO DERIVE THE WEEK NUMBERS.               
*                                                                               
* SEE JIRA TICKET: SPEC-7396                                                    
*                                                                               
LD_SPORTS_TAB DS 0H           LOCAL DAILIES SPORTS DATE TABLE                   
*                                                                               
* FORMAT:AL1(YEAR),AL2(WEEK_RANGE_1[,WEEK_RANGE_2][,...]),X'FF'                 
*        WEEK RANGE FORMAT:AL1(START_WEEK,END_WEEK)                             
*        MUST BE NO GAPS IN YEARS.                                              
*        WEEK RANGES MUST BE IN SEQUENCE BY START WEEK (I.E., WEEK              
*         RANGES MAY OVERLAP WITHIN A GIVEN YEAR).                              
*                                                                               
 DC AL1(YR_2014)                                                                
   DC AL1(WEEK_6,WEEK_8)      WINTER OLYMPICS (FEB7-FEB23)                      
   DC AL1(WEEK_24,WEEK_28)    FIFA WORLD CUP (JUN12-JUL13)                      
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2015)                                                                
   DC AL1(WEEK_22,WEEK_25)    FIFA U-20 WORLD CUP (MAY30-JUN20)                 
   DC AL1(WEEK_23,WEEK_27)    FIFA WOMEN'S WORLD CUP (JUN6-JUL5)                
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2016)                                                                
   DC AL1(WEEK_32,WEEK_34)    SUMMER OLYMPICS (AUG5-AUG21)                      
   DC AL1(WEEK_46,WEEK_49)    FIFA U-20 WOMEN'S WORLD CUP (NOV13-DEC3)          
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2017)                                                                
   DC AL1(WEEK_21,WEEK_24)    FIFA U-20 WORLD CUP (MAY20-JUN11)                 
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2018)                                                                
   DC AL1(WEEK_6,WEEK_8)      WINTER OLYMPICS (FEB9-FEB25)                      
   DC AL1(WEEK_24,WEEK_28)    FIFA WORLD CUP (JUN14-JUL15)                      
   DC AL1(WEEK_31,WEEK_34)    FIFA U-20 WOMEN'S WORLD CUP (AUG05-AUG24)         
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2019)                                                                
   DC AL1(WEEK_21,WEEK_24)    FIFA U-20 WORLD CUP (MAY23-JUN15)                 
   DC AL1(WEEK_23,WEEK_27)    FIFA WOMEN'S WORLD CUP (JUN07-JUL07)              
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2020)              ** NO EVENTS DUE TO COVID-19 **                   
 DC X'FF'                                                                       
*                                                                               
 DC AL1(YR_2021)                                                                
   DC AL1(WEEK_30,WEEK_32)    SUMMER OLYMPICS (JUL23-AUG08)                     
 DC X'FF'                                                                       
*                                                                               
         DC    X'FF'          EOT                                               
*                                                                               
* ========= PUT OLD DATES HERE FOR HISTORICAL REFERENCE                         
*                                                                               
**  KEEP FEB07/2014 THRU FEB23/2014                                             
**  ERGO: KEEP COMPLETE WEEKS 6/2014 THROUGH 8/2014.                            
**SPORTS_START_WEEK DC AL1(YR_2014,WEEK_6)                                      
**SPORTS_END_WEEK   DC AL1(YR_2014,WEEK_8)                                      
**                                                                              
*  KEEP JUL27/2012 THRU AUG12/2012, AND AUG19/2012 THRU SEP08/2012              
*  TO SIMPLIFY: JUL27/2012 THROUGH SEP08/2012.                                  
*  ERGO: KEEP COMPLETE WEEKS 31/2012 THROUGH 37/2012.                           
**SPORTS_START_WEEK DC AL1(YR_2012,WEEK_31)                                     
**SPORTS_END_WEEK   DC AL1(YR_2012,WEEK_37)                                     
*                                                                               
*======================================================================         
*                                                                               
         EJECT                                                                  
*                                                                               
INIT     DS    0H                                                               
         L     R1,4(R9)                                                         
         CLC   =AL1(DEMDIRN#),0(R1)  PROCESSING DEMDIRN ?                       
         BNE   CHECKREC            NO: NO LOCAL DATA TO PURGE                   
*                                                                               
         ZAP   LINE,=P'99'                                                      
         MVC   MID1,SPACES                                                      
         MVC   MID1(15),=C'PURGE MESSAGES:'                                     
         MVC   MID2,SPACES                                                      
         MVC   MID2(15),=C'---------------'                                     
*                                                                               
* THE CALLER SHOULD SET AN ENTRY POINT FOR "DATEVAL" AND POPULATE THE           
* FIELD. "DATEVAL" MUST BE A CL8 DATE FIELD IN FORMAT MM/DD/YY.                 
* NORMALLY THE CALLER WILL SET THE FIELD TO TODAY'S DATE, BUT IT CAN            
* BE OVERRIDDEN. THIS IS HELPFUL FOR REGRESSION (OR OTHER) TESTING.             
* IF THE ENTRY POINT IS UNRESOLVED, WE DEFAULT TO TODAY'S DATE.                 
*                                                                               
         ICM   R3,15,=V(DATEVAL)   SHOULD BE SET AND RESOLVED BY CALLER         
         IF (NZ)                                                                
           GOTO1 =V(DATCON),DMCB,(4,(R3)),TODAY  MM/DD/YY --> YYMMDD            
         ELSE ,                                                                 
           GOTO1 =V(DATCON),DMCB,(5,0),TODAY     TODAY:       YYMMDD            
         ENDIF ,                                                                
*                                                                               
* COMPUTE THE DATE 4 YEARS AGO FROM TODAY. LOCAL DAILIES PRIOR TO               
* THAT DATE WILL BE PURGED UNCONDITIONALLY.                                     
*                                                                               
         LHI   R0,-((365*4)+1)     4 YEARS AGO                                  
         GOTO1 =V(ADDAY),DMCB,TODAY,DUB,(R0)  SUBTRACT THE DAYS                 
*&&TEST                                                                         
*********TEST CODE*************                                                 
         LT    RE,=V(TEST$$4Y)     SET FROM TEST HARNESS PROGRAM                
         JZ    *+8                                                              
         CLI   0(RE),0                                                          
         JE    *+10                                                             
         MVC   DUB(6),0(RE)        OVERRIDE "4 YEARS AGO" DATE                  
*********TEST CODE*************                                                 
*&&                                                                             
         XC    DMCB(24),DMCB                                                    
*                                  SET START DAY TO MONDAY                      
         GOTO1 =V(NSIWEEK),DMCB,DUB,(1,V(GETDAY)),V(ADDAY),V(DATCON)            
*                                                                               
         MVC   OLDWEEK4,0(R1)      WEEK                                         
         MVC   OLDYEAR4,4(R1)      YEAR                                         
*                                                                               
* CONVERT THAT YEAR/WEEK TO A PRINTABLE DATE. THIS WILL BE THE MONDAY           
* THAT STARTS THE WEEK.                                                         
*                                                                               
         GOTO1 =V(NSIWEEK),DMCB,(C'D',FOUR_YEARS_AGO),                 +        
               (1,V(GETDAY)),V(ADDAY),V(DATCON)                                 
         SR    R0,R0                                                            
         ICM   R0,B'0111',1(R1)    "FUNNY" YYMMDD DATE                          
         MVC   P(44),=C'UNCONDITIONAL PURGE: LOCAL DAILIES PRIOR TO '           
         GOTO1 =V(DATCON),DMCB,(R0),(21,P+44)                                   
         MVC   P+54(10),=C' (WEEK NN)'                                          
         EDIT  OLDWEEK4,(2,P+61)                                                
         GOTO1 =V(PRINTER)                                                      
*                                                                               
* COMPUTE THE DATE 3 YEARS AGO FROM TODAY. LOCAL DAILIES PRIOR TO               
* THAT DATE WILL BE PURGED UNLESS IS IT A SPORTS PROGRAMMING WEEK.              
*                                                                               
         LHI   R0,-((365*3)+1)     3 YEARS AGO                                  
         GOTO1 =V(ADDAY),DMCB,TODAY,DUB,(R0)  SUBTRACT THE DAYS                 
*&&TEST                                                                         
*********TEST CODE*************                                                 
         LT    RE,=V(TEST$$3Y)     SET FROM TEST HARNESS PROGRAM                
         JZ    *+8                                                              
         CLI   0(RE),0                                                          
         JE    *+10                                                             
         MVC   DUB(6),0(RE)        OVERRIDE "3 YEARS AGO" DATE                  
*********TEST CODE*************                                                 
*&&                                                                             
         XC    DMCB(24),DMCB                                                    
*                                  SET START DAY TO MONDAY                      
         GOTO1 =V(NSIWEEK),DMCB,DUB,(1,V(GETDAY)),V(ADDAY),V(DATCON)            
*                                                                               
         MVC   OLDWEEK3,0(R1)      WEEK                                         
         MVC   OLDYEAR3,4(R1)      YEAR                                         
*                                                                               
* CONVERT THAT YEAR/WEEK TO A PRINTABLE DATE. THIS WILL BE THE MONDAY           
* THAT STARTS THE WEEK.                                                         
*                                                                               
         GOTO1 =V(NSIWEEK),DMCB,(C'D',THREE_YEARS_AGO),                +        
               (1,V(GETDAY)),V(ADDAY),V(DATCON)                                 
         SR    R0,R0                                                            
         ICM   R0,B'0111',1(R1)    "FUNNY" YYMMDD DATE                          
         MVC   P(44),=C'*CONDITIONAL* PURGE: LOCAL DAILIES PRIOR TO '           
         GOTO1 =V(DATCON),DMCB,(R0),(21,P+44)                                   
         MVC   P+54(10),=C' (WEEK NN)'                                          
         EDIT  OLDWEEK3,(2,P+61)                                                
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVI   P,0                 SKIP A LINE                                  
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    RE,LD_SPORTS_TAB    LOOK FOR LOCAL DAILIES SPORTS DATES          
SETSP1   CLC   OLDYEAR4,0(RE)                                                   
         JNE   *+12                                                             
         ST    RE,A_LD_SPORTS_TAB_4 SAVE ADDR FOUR YEARS AGO ENTRY              
         J     SETSP2                                                           
*                                                                               
         CLC   OLDYEAR3,0(RE)                                                   
         JNE   *+12                                                             
         ST    RE,A_LD_SPORTS_TAB_3 SAVE ADDR THREE YEARS AGO ENTRY             
         J     SETSPX                                                           
*                                                                               
         CLI   0(RE),X'FF'                                                      
         JE    *+2                 DIE IF YEAR IS NOT DEFINED YET               
*                                                                               
SETSP2   LA    RE,1(,RE)           BUMP PAST YEAR # TO FIRST WEEK               
         DO WHILE=(CLI,0(RE),NE,X'FF')  UNTIL WE HIT EOT FOR THIS YEAR:         
           LA   RE,2(,RE)                 BUMP TO THE NEXT WEEK                 
         ENDDO ,                                                                
*                                                                               
         LA    RE,1(,RE)           BUMP PAST THIS YEAR'S EOT TO NEXT YR         
         J     SETSP1                                                           
*                                                                               
SETSPX   DS    0H                                                               
         ZAP   LINE,=P'99'                                                      
*                                                                               
         EJECT                                                                  
CHECKREC DS    0H                                                               
*                                                                               
       L     R3,0(R9)              R3 = A(RECORD)                               
*                                                                               
       TM    18(R3),STATUS_DEL     MARKED FOR DELETION?                         
       BO    PURGE                 YES: *** EXIT AND PURGE ***                  
*                                                                               
       SELECT CLC,0(3,R3),EQ       EXAMINE FILE/MEDIA/SOURCE IN KEY             
*                                                                               
*                                  === RECORDS TO PURGE UNCONDITIONALLY         
         WHEN (=C'RWN',            NIELSEN METERED MARKET WEEKLIES              
               =C'SWN',            "                                            
               =C'MWN',            "                                            
               =C'CUN',            NIELSEN COUNTY COVERAGE                      
               =C'MUN',            "                                            
               =C'RUN',            "                                            
               =C'SUN',            "                                            
               =C'MPN')            MARKET PLANNING ANALYSIS (NOT "MPA")         
           B     PURGE             *** EXIT AND PURGE ***                       
*                                                                               
*                                  === NIELSEN LOCAL DAILIES ===                
         WHEN (=C'RON')            TIME PERIOD                                  
           MVC   BOOK,DRBOOK-DRKEY(R3)                                          
           XC    BOOK,=X'FFFF'     NORMALIZE BOOK (FOR COMPARISON)              
           NEXTWHEN ,                                                           
*                                                                               
         WHEN (=C'SON')            STATION PASSIVE                              
           MVC   BOOK,SBBOOK-SBKEY(R3)                                          
           NEXTWHEN ,                                                           
*                                                                               
         WHEN (=C'MON')            MARKET PASSIVE                               
           MVC   BOOK,BSBOOK-BSKEY(R3)                                          
           XC    BOOK,=X'FFFF'     NORMALIZE BOOK (FOR COMPARISON)              
           NEXTWHEN ,                                                           
*                                                                               
         WHEN (=C'RON',=C'SON',=C'MON')                                         
           CLC   BOOK,FOUR_YEARS_AGO   IF BOOK IS OLDER THAN 4 YEARS:           
           BL    PURGE                    *** EXIT AND PURGE ***                
           CLC   BOOK,THREE_YEARS_AGO  IF BOOK IS WITHIN LAST 3 YEARS:          
           BNL   KEEP                     *** EXIT AND KEEP ***                 
*                                                                               
*                                      IT'S BETWEEN 3 AND 4 YEARS OLD:          
*                                       SEE IF IT'S A SPORTS WEEK               
           L     RE,A_LD_SPORTS_TAB_3  GET SPORTS_TAB ENTRY 3 YRS AGO           
           IF (CLC,YEAR,NE,0(RE))      IF THAT'S NOT THE YEAR:                  
             L     RE,A_LD_SPORTS_TAB_4 GET SPORTS_TAB ENTRY 4 YRS AGO          
             IF (CLC,YEAR,NE,0(RE))      IF THAT'S NOT THE YEAR:                
               J     *+2                   SERIOUS LOGIC ERROR !                
             ENDIF ,                                                            
           ENDIF ,                                                              
*                                                                               
           LA    RE,1(,RE)         BUMP PAST YEAR TO FIRST WEEK RANGE           
           DO INF                  (LOOP UNTIL EXIT)                            
             CLI   0(RE),X'FF'     LOOP THROUGH WEEK RANGES FOR YEAR            
             JE    PURGE           ** PURGE ** IF NO (MORE) RANGES              
             CLC   WEEK,0(RE)      COMPARE WITH RANGE START WEEK                
             JL    PURGE           ** PURGE ** IF WK BEFORE RANGE START         
             CLC   WEEK,1(RE)      COMPARE WITH RANGE END WEEK                  
             JNH   KEEP            ** KEEP ** IF WITHIN RANGE                   
             LA    RE,2(,RE)       NEXT WEEK RANGE                              
           ENDDO ,                                                              
*                                                                               
         OTHRWISE ,                                                             
           B     KEEP                *** DEFAULT IS TO KEEP ***                 
*                                                                               
       ENDSEL ,                                                                 
*                                                                               
       EJECT                                                                    
*                                                                               
PURGE    DS    0H                                                               
         MVI   0(R9),X'FF'         RETURN PURGE FLAG                            
         OI    18(R3),STATUS_DEL   SET DELETE BIT                               
*                                                                               
KEEP     DS    0H                                                               
         SPACE 2                                                                
         XMOD1 ,                                                                
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
DEMDIRA# EQU   X'2F'                                                            
DEMDIRN# EQU   X'2D'                                                            
DEMDIRR# EQU   X'30'                                                            
*                                                                               
STATUS_DEL EQU X'80'                                                            
         SPACE 2                                                                
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    CL17                                                             
*                                                                               
TODAY    DS    CL6                 YYMMDD (CAN BE OVERRIDDEN)                   
*                                                                               
A_LD_SPORTS_TAB_3 DS A             A(LD_SPORTS_TAB ENTRY: 3 YEARS AGO)          
A_LD_SPORTS_TAB_4 DS A             A(LD_SPORTS_TAB ENTRY: 4 YEARS AGO)          
*                                                                               
BOOK     DS    XL2                                                              
         ORG   BOOK                                                             
YEAR     DS    X                                                                
MONTH    DS    0X                                                               
WEEK     DS    X                                                                
*                                                                               
FOUR_YEARS_AGO  DS 0XL2            *WEEKLY* BOOK                                
OLDYEAR4 DS    X                                                                
OLDWEEK4 DS    X                                                                
THREE_YEARS_AGO DS 0XL2            *WEEKLY* BOOK                                
OLDYEAR3 DS    X                                                                
OLDWEEK3 DS    X                                                                
         EJECT                                                                  
* ++INCLUDE DEDEMFILE                                                           
* ++INCLUDE DDDPRINT                                                            
* ++INCLUDE DDMONYREQU                                                          
         PRINT OFF                                                              
       ++INCLUDE DEDEMFILE                                                      
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE DDMONYREQU                                                     
         PRINT ON                                                               
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012DELDPRGE  05/08/20'                                      
         END                                                                    
