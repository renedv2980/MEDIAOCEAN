*          DATA SET DELMTIME2  AT LEVEL 001 AS OF 07/27/15                      
*CATALP LMTIME2                                                                 
         TITLE 'CONVERT ALPHA OR MILITARY TIME TO QTR HR SEGEMENT'              
         TITLE 'CONVERT QTR HR TO ALPHA OR MILITARY TIME'                       
***********************************************************************         
* THIS MODULE DIFFERS FROM DEDEMTIME IN THAT IT IS USED TO CONVERT              
* BETWEEN NIELSEN TIME AND BINARY TIME USING 5:00AM AS ZERO QHR.                
* THIS MODULE IS ONLY USED AS PART OF THE LOCAL MONTHLY PROJECT.                
***********************************************************************         
HRTOQH3  CSECT                                                                  
*        PARAMETERS 1.   BYTE  0   0=MILITARY TIME 1=ALPHA TIME                 
*                        BYTES 1-3 A(INPUT TIME)                                
*                                                                               
*                   2.   BYTES 0-3 A(QTR HR NUMBER)                             
*                   3.   BYTE  0   0=5AM START  1= 3AM START                    
*                                                                               
         NMOD1 0,HRTOQH3                                                        
         L     R2,0(R1)            SET INPUT ADDRESS                            
         L     R3,4(R1)            SET OUTPUT ADDRESS                           
         LA    R2,0(R2)            CLEAR HIGH ORDER BYTE                        
         MVC   HALF,0(R2)                                                       
         L     R4,8(R1)                                                         
         MVC   STARTQH,0(R4)       START DAY QH                                 
         CLI   0(R1),1             ALPHA TIME GIVEN                             
         BNE   CNVMIL               NO - CONVERT MILITARY TIME                  
         GOTO1 =V(TIMVAL),DUB,(X'05',(R2)),HALF                                 
         CLI   0(R1),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                                                             
CNVMIL   LH    R4,HALF                                                          
*                                                                               
         CLI   STARTQH,1           3A START OF DAY?                             
         BE    CNVMIL3A                                                         
         B     CNVMIL5A            5A START TIME                                
*                                                                               
CNVMIL5A DS    0C                                                               
         CH    R4,=H'500'          TIME GT MIDNIGHT                             
         BNL   *+8                                                              
         AH    R4,=H'2400'          YES - ADD 1 DAY                             
         SH    R4,=H'500'          SET FOR 5A START                             
         B     CNVMILX                                                          
*                                                                               
CNVMIL3A DS    0C                  TIME GT MIDNIGHT                             
                                                                                
         CH    R4,=H'300'          TIME GT MIDNIGHT                             
         BNL   *+8                                                              
         AH    R4,=H'2400'          YES - ADD 1 DAY                             
         SH    R4,=H'300'          SET FOR 5A START                             
                                                                                
CNVMILX  DS    0C                                                               
         LTR   R4,R4                                                            
         BNZ   *+12                                                             
         MVI   0(R3),0                                                          
         B     HREXIT                                                           
         SRDA  R4,32                                                            
         D     R4,=F'100'                                                       
         MH    R5,=H'4'                                                         
         STC   R5,0(R3)                                                         
         SRDA  R4,32                                                            
         D     R4,=F'15'                                                        
         XC    HALF,HALF                                                        
         MVC   HALF+1(1),0(R3)                                                  
         AH    R5,HALF                                                          
         STC   R5,0(R3)            STORE IN OUTPUT                              
HREXIT   XMOD1 1                                                                
HALF     DS    H                                                                
         DS    H                                                                
DUB      DS    D                                                                
STARTQH  DS    X                                                                
         LTORG                                                                  
QHTOHR3  CSECT                                                                  
*        PARAMETERS 1.   BYTES 0-3 A(INPUT QTR HR)                              
*                                                                               
*                   2.   BYTE  0   0=MILITARY TIME 1=ALPHA TIME                 
*                              1-3 A(OUTPUT AREA)                               
*                                                                               
         NMOD1 0,QHTOHR                                                         
         L     R2,0(R1)            SET INPUT ADDRESS                            
         L     R3,4(R1)            SET OUTPUT ADDRESS                           
         L     R4,8(R1)                                                         
         MVC   STARTQH2,0(R4)       START DAY QH                                
         ST    R1,STORER1                                                       
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE                        
         SR    R4,R4                                                            
         SR    R5,R5                                                            
         IC    R5,0(R2)                                                         
         D     R4,=F'4'                                                         
         MH    R5,=H'100'                                                       
         MH    R4,=H'15'           CALCULATE MILITARY TIME                      
         AR    R4,R5                                                            
*                                                                               
         CLI   STARTQH2,1          3A START TIME?                               
         BNE   *+12                                                             
         AH    R4,=H'300'                                                       
         B     *+8                                                              
         AH    R4,=H'500'          5A START TIME                                
*                                                                               
         CH    R4,=H'2400'         AFTER MIDNIGHT                               
         BNH   *+8                                                              
         SH    R4,=H'2400'          YES - START AT 0                            
         STH   R4,HALF2            SAVE MILITARY TIME IN OUTPUT AREA            
         MVC   0(2,R3),HALF2                                                    
         CLI   4(R1),1             ALPHA TIME REQUESTED                         
         BNE   QHEXIT               NO - EXIT                                   
         GOTO1 =V(UNTIME),DUB2,HALF2,(R3)  CONVERT TO ALPHA                     
QHEXIT   XMOD1 1                                                                
DUB2     DS    D                                                                
HALF2    DS    H                                                                
         DC    H'0'                                                             
STORER1  DS    F                                                                
STARTQH2 DS    X                                                                
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DELMTIME2 07/27/15'                                      
         END                                                                    
