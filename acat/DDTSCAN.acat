*          DATA SET DDTSCAN    AT LEVEL 003 AS OF 07/01/83                      
*CATALP TSCAN                                                                   
         TITLE 'TSCAN - TEXT SCAN AND REPLACE'                                  
         PRINT NOGEN                                                            
TSCAN    CSECT                                                                  
         SPACE 2                                                                
*        PARAM1    BYTE   0        NOT USED                                     
*                  BYTES 1-3       A(TEXT)                                      
*        PARAM2    BYTES 1-4       L'TEXT (HAS NEW LENGTH ON OUTPUT)            
*                                                                               
*        PARAM3    BYTE   0        L'STRING                                     
*                  BYTES 1-3       A(STRING)                                    
*        PARAM4    BYTE   0        L'REPLACE STRING                             
*                  BYTES 1-3       A(REPLACE STRING)                            
*                                  **OUTPUT**                                   
*        PARAM5    BYTE   0        NO. OF OCCURENCES                            
*                  BYTES 1-3       A(WHERE STRING LAST FOUND)                   
*                                  =0 IF STRING NOT FOUND                       
         SPACE 3                                                                
         NMOD1 0,TSCAN                                                          
         SPACE 2                                                                
         L     R2,0(R1)            A(TEXT)                                      
         LA    R2,0(R2)                                                         
         L     R5,4(R1)            L'TEXT                                       
         L     R3,8(R1)            A(STRING)                                    
         L     R4,12(R1)           A(REPL)                                      
         XC    16(4,R1),16(R1)                                                  
         SPACE 2                                                                
TSC1     DS    0H                                                               
         SR    R6,R6                                                            
         SR    R7,R7                                                            
         IC    R6,8(R1)            L'STRING                                     
         IC    R7,12(R1)           L'REPL                                       
         L     R9,0(R1)                                                         
         LA    R9,0(R9,R5)                                                      
         SR    R9,R6                                                            
         BCTR  R6,R0                                                            
         SPACE 2                                                                
TSC2     DS    0H                                                               
         CR    R2,R9                                                            
         BH    TSCX                                                             
         EX    R6,COMP                                                          
         BE    TSC4                                                             
         LA    R2,1(R2)                                                         
         B     TSC2                                                             
         SPACE 2                                                                
TSC4     DS    0H                                                               
         IC    RF,16(R1)                                                        
         ST    R2,16(R1)                                                        
         LA    RF,1(RF)                                                         
         STC   RF,16(R1)           COUNT OF HITS                                
         LA    R6,1(R6)                                                         
         SR    R6,R7                                                            
         BP    TSC6                REPL SHORTER                                 
         BM    TSC8                REPL LONGER                                  
         BCTR  R7,R0                                                            
         EX    R7,MOVE             LENGTHS EQUAL                                
         LA    R2,1(R2,R7)                                                      
         B     TSC1                                                             
         SPACE 2                                                                
*                                  REPL SHORTER                                 
TSC6     DS    0H                                                               
         LTR   R7,R7                                                            
         BZ    TSC7                DELETE STRING                                
         BCTR  R7,R0                                                            
         EX    R7,MOVE                                                          
         LA    R7,1(R7)                                                         
TSC7     DS    0H                                                               
         LA    R2,0(R2,R7)                                                      
         LA    RE,0(R2,R6)                                                      
         L     R5,4(R1)                                                         
         LR    R8,R1               SAVE R1                                      
         L     R1,0(R1)                                                         
         LA    R1,0(R5,R1)                                                      
         SR    R1,RE                                                            
*                                  SQUEEZE UP TEXT                              
         MOVE  ((R2),(R1)),(RE)                                                 
         SPACE 1                                                                
         LR    R1,R8               RESTORE R1                                   
         SR    R5,R6                                                            
         ST    R5,4(R1)            STORE NEW LENGTH                             
         B     TSC1                                                             
         SPACE 2                                                                
*                                  REPL LONGER                                  
TSC8     DS    0H                                                               
         L     R5,4(R1)                                                         
         L     R8,0(R1)                                                         
         LA    R8,0(R8,R5)         END OF TEXT                                  
         LPR   R6,R6                                                            
         STC   R6,*+9                                                           
         BCTR  R8,R0                                                            
         MVC   0(1,R8),0(R8)       MOVE LEFT 1 BYTE AT A TIME                   
         CR    R8,R2                                                            
         BH    *-10                                                             
         BCTR  R7,R0                                                            
         EX    R7,MOVE                                                          
         LA    R2,1(R2,R7)                                                      
         AR    R5,R6                                                            
         ST    R5,4(R1)            STORE NEW LENGTH                             
         B     TSC1                                                             
         SPACE 2                                                                
MOVE     MVC   0(0,R2),0(R4)                                                    
COMP     CLC   0(0,R2),0(R3)                                                    
         SPACE 2                                                                
TSCX     DS    0H                                                               
         XMOD1 1                                                                
         SPACE 2                                                                
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDTSCAN   07/01/83'                                      
         END                                                                    
