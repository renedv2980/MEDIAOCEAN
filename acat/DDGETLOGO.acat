*          DATA SET DDGETLOGO  AT LEVEL 009 AS OF 05/01/02                      
*CATALP GETLOGO                                                                 
         TITLE 'MODULE DIGS LOGO INFO OUT OF CONTROL FILE'                      
GETLOGO  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 130,**GTLOGO                                                     
         USING GETLD,RC                                                         
         LM    R2,R5,0(R1)         PARAMETER 1    A(ID CODE)                    
         LR    RA,R3                         2    A(LOGOC)                      
         ST    R4,VDATAMGR                   3    A(DATAMGR)                    
         LR    R3,R2                                                            
         LR    R9,R1                                                            
         XC    ATTN,ATTN                                                        
         CLI   0(R1),0             IF PARA1 BYTE 1 NOT=0                        
         BE    *+10                                                             
         MVC   ATTN,0(R5)             PARA4 WILL BE A(ATTENTION CODE)           
         CLI   ATTN+2,C' '                                                      
         BH    *+8                                                              
         MVI   ATTN+2,C' '                                                      
         USING LOGOD,RA                                                         
         CLI   OPENSW,C'Y'                                                      
         BE    GL2                                                              
         GOTO1 VDATAMGR,DMCB,=CL8'OPEN',=CL8'CONTROL',=C'NCTFILE X'             
         MVI   OPENSW,C'Y'                                                      
         SPACE 2                                                                
GL2      LA    R2,IO                                                            
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKEY,C'I'                                                      
         MVC   CTIKID+8(2),0(R3)                                                
         GOTO1 VDATAMGR,DMCB,=CL8'DMREAD',=CL8'CTFILE',(R2),(R2),0              
         CLI   DMCB+8,0                                                         
         BNE   XIT                                                              
         MVI   ELCODE,X'30'                                                     
         LR    R4,R2                                                            
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         USING CTDSTD,R4                                                        
         MVC   0(4,R9),CTDSTPOW    PASS USER POWER CODE IN PARA 1               
         MVC   LOGO1,CTDSTLG1                                                   
         MVC   LOGO2,CTDSTLG2                                                   
         MVC   LOGONAME,CTDSTNAM                                                
         MVC   LOGOADD,CTDSTADD                                                 
         MVC   LOGOADD2,SPACES                                                  
         MVC   LOGOADD3,SPACES                                                  
         CLI   CTDSTLEN,166                                                     
         BL    GL4                                                              
         MVC   LOGOADD2,CTDSTAD2                                                
         MVC   LOGOADD3,CTDSTAD3                                                
         EJECT                                                                  
*              ROUTINE TO CHECK FOR ATTENTION NAMES                             
         SPACE 3                                                                
GL4      LR    R4,R2                                                            
         MVI   ELCODE,X'31'                                                     
         BAS   RE,GETEL                                                         
         B     GL8                                                              
         SPACE 2                                                                
GL6      BAS   RE,NEXTEL                                                        
         SPACE 2                                                                
GL8      BNE   GL14                                                             
         USING CTATTND,R4                                                       
         LA    R6,ATTN                                                          
         LA    R7,CTATTTYP                                                      
         LA    R8,3                                                             
         SPACE 2                                                                
GL10     CLI   0(R7),C'*'          * IS THE WILD CARD FEATURE                   
         BE    GL12                                                             
         CLC   0(1,R7),0(R6)                                                    
         BNE   GL6                                                              
         SPACE 2                                                                
GL12     LA    R6,1(R6)                                                         
         LA    R7,1(R7)                                                         
         BCT   R8,GL10                                                          
         SR    R5,R5                                                            
         IC    R5,CTATTLEN                                                      
         SH    R5,=H'6'                                                         
         MVC   LOGOADD3,SPACES                                                  
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   LOGOADD3(0),CTATTDET                                             
         B     GL6                 TAKE THE LAST VERSION                        
         SPACE 2                                                                
GL14     LR    R4,R2               ANY SHIPPING INSTRUCTIONS                    
         MVI   ELCODE,X'4C'                                                     
         BAS   RE,GETEL                                                         
         BNE   XIT                                                              
         MVC   LOGOINFO,SPACES                                                  
         USING CTSHPD,R4                                                        
         SR    R5,R5                                                            
         IC    R5,CTSHPLEN                                                      
         SH    R5,=H'7'                                                         
         CH    R5,=H'80'                                                        
         BNH   *+8                                                              
         LA    R5,79                                                            
         EX    R5,*+8                                                           
         B     XIT                                                              
         MVC   LOGOINFO(0),CTSHPINS                                             
         SPACE 2                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
         GETEL (R4),DATADISP,ELCODE                                             
OPENSW   DC    C'N'                                                             
SPACES   DC    CL132' '                                                         
DATADISP DC    H'28'                                                            
         EJECT                                                                  
*              DSECTS                                                           
         SPACE 3                                                                
GETLD    DSECT                                                                  
DMCB     DS    6F                                                               
VDATAMGR DS    V                                                                
IO       DS    1000C                                                            
ELCODE   DS    CL1                                                              
ATTN     DS    CL3                                                              
         PRINT OFF                                                              
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'009DDGETLOGO 05/01/02'                                      
         END                                                                    
