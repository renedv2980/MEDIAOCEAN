*          DATA SET DDMASTER   AT LEVEL 024 AS OF 07/31/20                      
*CATALP MASTER                                                                  
         TITLE 'MASTER CONTROL - INITIALIZATION'                                
         ENTRY REQFILE                                                          
         ENTRY RUNSTART                                                         
         ENTRY REQSTART                                                         
         ENTRY ADWAIT                                                           
         ENTRY LOADEM                                                           
         ENTRY LOADER                                                           
         ENTRY TESTPOOL                                                         
         ENTRY JOBREG                                                           
         ENTRY JCREQSTR                                                         
         ENTRY JOBWAIT                                                          
         ENTRY JCREQEND                                                         
         ENTRY JOBDONE                                                          
         ENTRY JOBERR                                                           
         ENTRY XTRSMF                                                           
*                                                                               
         PRINT NOGEN                                                            
         IEABRCX DEFINE                                                         
*                                                                               
RUNSTART CSECT                                                                  
         NMOD1 0,**RUNST*,R8                                                    
         L     RC,=A(MASTC)                                                     
         USING MASTC,RC            RC=A(GLOBAL W/S)                             
         L     R9,MCVLOGOC                                                      
         USING LOGOD,R9            R9=A(LOGO W/S)                               
*                                                                               
         L     R1,X'10'(,R0)       GET CVT ADDRESS                              
         USING CVT,R1                                                           
         TM    CVTDCB,CVTMVSE      X'80' TEST XA                                
         BZ    RUNS010                                                          
         SR    R1,R1                                                            
         B     RUNS012                                                          
         DROP  R1                                                               
*                                                                               
RUNS010  L     R1,X'220'(,R0)      GET ASCB ADDRESS FROM PSAAOLD                
         L     R1,X'030'(R1)       LDA- VSM LOCAL DATA AREA                     
         L     R1,X'5C0'(R1)       GET CURRENT TOP OF REGION ADDRESS            
*                                                                               
RUNS012  ST    R1,MCDMPEND         STORE END OF REGION FOR PRGCHK               
*                                                                               
         CSVQUERY INEPNAME=ASMIDF  TEST IF RUNNING UNDER IDF                    
         LTR   RF,RF                                                            
         BNZ   *+8                                                              
         OI    MCFLAGS,MCFIDF                                                   
*                                                                               
         XC    MCREQV(MCREQVL),MCREQV                                           
         XC    MCRUNV(MCRUNVL),MCRUNV                                           
*                                                                               
         TIME  BIN                 GET TIME IN SEC/100                          
         ST    R0,MCJOBSTM         SAVE JOB START TIME                          
         GOTOR MCVDTCON,MCPARA,(5,0),(10,MCDATE)  GET TODAYS DATE               
*                                                                               
         LA    R3,MCPARA           GET JOB AND STEP NAMES                       
         EXTRACT (3),'S',FIELDS=TIOT                                            
         L     R3,MCPARA                                                        
         USING TIOT1,R3                                                         
         MVC   MCJOB,TIOCNJOB                                                   
         MVC   MCSTEP,TIOCSTEP                                                  
         DROP  R3                                                               
*                                                                               
         LA    R3,ASIDFLD          GET JOB AND STEP NAMES                       
         EXTRACT (3),'S',FIELDS=ASID                                            
         SAM31 ,                                                                
*                                                                               
         LA    R3,MCWORK                                                        
         IAZXJSAB READ,JOBID=(R3) WORKS FOR ASCH TOO                            
         PACK  MCDUB,MCWORK+3(5)   JOB00000                                     
         CVB   R1,MCDUB            CONVERT TO JUST JOB NUMBER                   
         SAM24 ,                                                                
*                                                                               
         ICM   RF,15,MCSSB         SET A(MASTC) IN EXTENDED SSB                 
         USING SSBD,RF                                                          
         BZ    GETCTL                                                           
         CLI   SSOXTND,FF                                                       
         BNE   GETCTL                                                           
         STCM  R1,3,SSOJOBNO       SAVE OFF JOB NUMBER                          
         ST    RC,SSOMASTC                                                      
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* READ AND VALIDATE CONTROL CARDS                                     *         
***********************************************************************         
GETCTL   NI    MCFLAGS2,X'FF'-MCFINVH0-MCFSOFDT                                 
         GOTOR MCVCARDS,MCPARA,MCP,=C'RE00'                                     
*                                                                               
         L     R1,=A(CTLTAB)                                                    
GETCTL02 CLI   0(R1),FF            TEST E-O-L                                   
         BNE   *+14                                                             
         MVC   MCP+72(L'CARDERR1),CARDERR1                                      
         B     CTLERR                                                           
         SR    RF,RF                                                            
         IC    RF,0(R1)                                                         
         BCTR  RF,0                RF=L'TABLE COMPARE-1                         
         EX    RF,*+8                                                           
         BE    GETCTL04                                                         
         CLC   MCP(0),4(R1)        MATCH CARD TO TABLE KEYWORD                  
         LA    R1,5(RF,R1)         NO MATCH - BUMP TO NEXT ENTRY                
         B     GETCTL02                                                         
*                                                                               
GETCTL04 SR    RF,RF                                                            
         ICM   RF,7,1(R1)          RF=A(VALIDATION ROUTINE)                     
         LA    RE,CTLOK                                                         
         BR    RF                                                               
*                                                                               
CTLERR   GOTOR MCVLOGIO,MCPARA,('FF',1),(72,MCP)                                
         GOTOR (RF),(R1),,(30,MCP+72)                                           
*                                                                               
         TM    MCFLAGS2,MCFINVH0   TEST DIE ON INVALID CARD FLAG                
         BZ    CTLERR10            NO-PROCEED AS USUAL                          
*                                                                               
         GOTOR (RF),(R1),,(L'CARDMSG2,CARDMSG2)                                 
         TM    MCPRTIND,MCPRTICN   TEST DONT PRINT CONTROL CARDS                
         JNZ   *+2                                                              
         GOTOR MCVPRINT,MCPARA,MCP-1,=C'BL01'                                   
         DC    H'0'                                                             
*                                                                               
CTLERR10 GOTOR (RF),(R1),,(L'CARDMSG1,CARDMSG1)                                 
         EJECT                                                                  
***********************************************************************         
* CONTROL CARD VALID. TEST TO PRINT OR WRITE TO CONCARDS DATA SET     *         
***********************************************************************         
CTLOK    TM    MCPRTIND,MCPRTICN   TEST DONT PRINT CONTROL CARDS                
         BO    CTL0K04                                                          
         TM    MCPRTIND,MCPRTICD   TEST CONTROL CARDS WRITTEN TO DISK           
         BO    CTLOK06                                                          
         TM    INDICS,INDIHEAD     TEST HEADLINES PRINTED                       
         BO    CTLOK02                                                          
         GOTOR MCVPRINT,MCPARA,MCSPACES-1,=C'BL02'                              
         MVC   MCSPACES(13),=C'CONTROL CARDS'                                   
         GOTOR MCVPRINT,MCPARA,MCSPACES-1,=C'BL01'                              
         MVI   MCSPACES,C'-'                                                    
         MVC   MCSPACES+1(12),MCSPACES                                          
         GOTOR (RF),(R1),,=C'BL02'                                              
         MVI   MCSPACES,C' '                                                    
         MVC   MCSPACES+1(L'MCSPACES-1),MCSPACES                                
         OI    INDICS,INDIHEAD     SET HEADLINES PRINTED                        
*                                                                               
CTLOK02  GOTOR MCVPRINT,MCPARA,MCP-1,=C'BL01'                                   
         TM    MCFLAGS2,MCFSOFDT   HAVE SOFT DATE?                              
         BZ    CTL0K04                                                          
         GOTOR MCVPRINT,MCPARA,MCWORK,=C'BL01'                                  
*                                                                               
CTL0K04  MVC   MCP,MCSPACES                                                     
         B     GETCTL                                                           
*                                                                               
CTLOK06  TM    INDICS,INDIHEAD     TEST HEADLINES PRINTED                       
         BO    CTLOK08                                                          
         GOTOR =V(DYNALLOC),MCPARA,(X'FE',CONCARD),CONDEST                      
         OPEN  (CONCARDS,OUTPUT)   OPEN CONCARDS                                
         MVI   MCSPACES,C' '                                                    
         MVC   MCSPACES+1(80),MCSPACES                                          
         MVC   MCSPACES(13),=C'CONTROL CARDS'                                   
         PUT   CONCARDS,MCSPACES                                                
         MVI   MCSPACES,C'-'                                                    
         MVC   MCSPACES+1(12),MCSPACES                                          
         PUT   CONCARDS,MCSPACES                                                
         MVI   MCSPACES,C' '                                                    
         MVC   MCSPACES+1(L'MCSPACES-1),MCSPACES                                
         OI    INDICS,INDIHEAD     SET HEADLINES PRINTED                        
*                                                                               
CTLOK08  PUT   CONCARDS,MCP                                                     
CTLOK10  MVC   MCP,MCSPACES                                                     
         B     GETCTL                                                           
*                                                                               
ASIDFLD  DC    F'0'                ASID FROM EXTRACT                            
CONCARD  DC    CL8'CONCARDS'                                                    
CONDEST  DC    CL17'8DDLMVS  PRT     '                                          
         DC    CL1' '                                                           
CONCARDS DCB   DDNAME=CONCARDS,MACRF=PM,DSORG=PS,RECFM=FB,LRECL=80              
         EJECT                                                                  
***********************************************************************         
* INITIALIZE OTHER MCBLOCK VALUES                                     *         
***********************************************************************         
RUNINI   DS    0H                                                               
*&&UK                                                                           
         OC    MCMEDDSP,MCMEDDSP   TEST IF MEDDSPC= CARD                        
         BNZ   RUNINI08                                                         
         CLC   MCSYSTEM,=C'ME'     GET ALET/ORIGIN OF MEDDSPC                   
         BNE   RUNINI10                                                         
         MVC   MCMEDDSP,=C'MED1'                                                
*                                                                               
RUNINI08 LA    R0,21               GET MEDIA SYSTEM DATASPACE INFO              
         LNR   R0,R0                                                            
         LA    R1,MCWORK                                                        
         MVC   MCWORK(4),=CL4'GETA'                                             
         MVC   MCWORK+4(4),MCMEDDSP                                             
         MVC   MCWORK+8(8),=CL8'MEDDSPC '                                       
*                                                                               
         SVC   247                                                              
         LTR   RF,RF               TEST OK RETURN                               
         BNZ   RUNINI10                                                         
         OC    MCWORK+24(4),MCWORK+24  TEST DATASPACE FOUND                     
         BZ    RUNINI10                                                         
*                                                                               
         ICM   RF,15,MCSSB                                                      
         USING SSBD,RF                                                          
         MVC   SSOMEDTB,MCWORK+20  SET ORIGIN/ALET                              
         DROP  RF                                                               
*&&                                                                             
RUNINI10 CLI   MCAGCTRY,0          TEST AGENCY COUNTRY VALUE IS SET             
         BNE   *+10                                                             
         MVC   MCAGCTRY,MCCTRY     NO-COPY FROM COUNTRY CODE                    
*                                                                               
         L     RF,MCUTL                                                         
         MVI   4(RF),10            SET CONTROL SYSTEM                           
         LA    R1,MCWORK                                                        
         MVI   0(R1),C'N'          BUILD LIST OF FILES TO OPEN                  
         MVC   1(7,R1),=C'CTFILE'                                               
         AHI   R1,8                                                             
         MVI   0(R1),C'N'                                                       
         MVC   1(7,R1),GENDIR                                                   
         AHI   R1,8                                                             
         MVI   0(R1),C'N'                                                       
         MVC   1(7,R1),GENFIL                                                   
         AHI   R1,8                                                             
         MVI   0(R1),C'X'                                                       
         GOTOR MCVDMGR,MCPARA,DMOPEN,CONSYS,MCWORK,MCIO                         
*                                                                               
         CLI   MCAUTOLD,C'Y'       TEST NEED TO LOOK UP LOAD NAME               
         BNE   RUNINI16                                                         
*&&US                                                                           
***********************************************************************         
* READ LOAD RECORD                                                    *         
* THE FIRST REQUEST CARD SHOULD CONTAIN REAL PROGRAM NAME             *         
* SO READ IT NOW UNLESS WE SOMEHOW HAVE IT ALREADY                    *         
***********************************************************************         
         MVC   MCP(2),MCPROG                                                    
         CLC   MCP(2),=C'**'                                                    
         BNE   RUNINI14                                                         
         CLI   MCREQFIL,C'Y'       CAN COME FROM FILE                           
         BE    RUNINI12                                                         
         GOTOR MCVCARDS,MCPARA,MCP,=C'RE00'                                     
         MVC   MCPROG,MCP          MOVE REQUEST ID                              
         B     RUNINI14                                                         
*                                                                               
RUNINI12 L     R3,=A(REQFILE)                                                   
         OPEN  ((R3),INPUT)                                                     
         LA    R2,MCP                                                           
         GET   (R3),(R2)                                                        
         MVC   MCPROG,MCP                                                       
         CLOSE (R3)                                                             
*                                                                               
RUNINI14 LA    R2,MCIO             R2=A(ID RECORD)                              
         USING CTLDRECD,R2                                                      
         XC    CTLDKEY,CTLDKEY                                                  
         MVI   CTLDKID,CTLDKIDQ                                                 
         MVI   CTLDKSB,CTLDKSBQ                                                 
         MVC   CTLDKSY,MCSYSTEM                                                 
         MVC   CTLDKPR,MCP         PHASE ID                                     
         GOTOR MCVDMGR,MCPARA,=C'DMREAD',=C'CTFILE',(R2),(R2)                   
         CLI   8(R1),0                                                          
         JNE   *+2                                                              
         LA    R1,CTLDFRST         POINT TO FIRST ELEMENT                       
         USING CTLD05D,R1                                                       
         CLI   0(R1),5                                                          
         JNE   *+2                                                              
         MVC   MCLOAD,2(R1)        MOVE SSPP                                    
*&&                                                                             
RUNINI16 CLI   MCRECOVR,C'W'       RECOVER=W IS OK FOR SOONS                    
         BE    RUNINI18                                                         
         OC    MCREMPQK,MCREMPQK   TEST SOON JOB                                
         BZ    RUNINI18            NO                                           
         MVI   MCRECOVR,C'N'       SET RECOVER=N FOR SOON JOBS                  
         TM    MCFLAGS,MCFOFFPK    OFF PEAK SOON JOB?                           
         BO    RUNINI18            YES-DONT SET RUN=TEST MODE                   
*&&US                                                                           
         TM    MCPRTIND,MCPRTXFI   DONT SET RUN=TEST FOR XFILE JOBS!            
         BNZ   *+8                                                              
         MVI   MCTSTRUN,FF         AND SET RUN=TEST MODE                        
*&&                                                                             
RUNINI18 ICM   R4,15,MCVREMOT      INITIALIZE REMOTED                           
         BZ    RUNINI24                                                         
*                                                                               
REM2     USING REMOTED,MCREMOT2                                                 
         MVC   MCDUB(L'REMOTCLS),REM2.REMOTCLS                                  
         MVC   MCDUB+L'REMOTCLS(L'REMOTJID),REM2.REMOTJID                       
         MVC   MCDUB+L'REMOTCLS+L'REMOTJID(L'REMOTFNO),REM2.REMOTFNO            
         MVC   MCREMOT2,MCREMOTE   COPY MCREMOTE TO MCREMOT2                    
*                                                                               
         CLI   MCDUB,0                       OVERRIDE CLASS IF USED             
         BE    *+10                                                             
         MVC   REM2.REMOTCLS,MCDUB                                              
*                                                                               
         CLI   MCDUB+L'REMOTCLS,0            OVERRIDE ID IF USED                
         BE    *+10                                                             
         MVC   REM2.REMOTJID,MCDUB+L'REMOTCLS                                   
*                                                                               
         CLI   MCDUB+L'REMOTCLS+L'REMOTJID,0 OVERRIDE FORM IF USED              
         BE    *+10                                                             
         MVC   REM2.REMOTFNO,MCDUB+L'REMOTCLS+L'REMOTJID                        
*                                                                               
         USING REMOTED,R4                                                       
         MVC   REMOTED(L'MCREMOTE),MCREMOTE                                     
         MVC   REMOTABF,MCVPQBUF                                                
         MVC   REMOTADM,MCVDMGR                                                 
         MVC   REMOTAOP,MCVPQOPN                                                
*                                                                               
         L     R3,=A(SVEZCTL)      EZCTL CARDS MUST PRINT 1ST IF REMOTE         
         LHI   R0,EZCTLMAX                                                      
RUNINI20 CLC   0(L'SVEZCTL,R3),MCSPACES                                         
         BNH   RUNINI22                                                         
         MVI   REMOTCLS,C'G'       FORCE OUTPUT CLASS                           
         MVC   MCP,MCSPACES                                                     
         MVC   MCP(L'SVEZCTL),0(R3)                                             
         GOTOR MCVPRINT,MCPARA,MCP-1,=C'BL01'                                   
         AHI   R3,L'SVEZCTL                                                     
         BCT   R0,RUNINI20                                                      
*                                                                               
RUNINI22 ICM   R4,15,MCVREMO2      INITIALIZE 2ND REMOTED (IF THERE)            
         BZ    RUNINI24                                                         
         USING REMOTED,R4                                                       
         MVC   REMOTED(L'MCREMOTE),MCREMOT2                                     
         MVC   REMOTABF,MCVPQBU2                                                
         MVC   REMOTADM,MCVDMGR                                                 
         MVC   REMOTAOP,MCVPQOPN                                                
*                                                                               
RUNINI24 ICM   RF,15,MCVSPLAR      ACTIVATE SPOOLING IF REQUESTED               
         BZ    RUNINI26                                                         
         OC    4(20,RF),4(RF)      TEST DSN INFO DEFINED                        
         BZ    RUNINI26                                                         
         MVI   0(RF),1                                                          
         MVI   1(RF),1                                                          
*                                                                               
RUNINI26 ICM   RF,15,MCBXAREA      INITIALIZE BOX                               
         BZ    RUNINI28                                                         
*                                                                               
BOX1     USING BOXD,RF                                                          
         MVI   BOX1.BOXOFF,0                                                    
         MVC   BOX1.BOXMAXL,MCMAXLIN                                            
         MVC   BOX1.BOXOFFST,MCOFFSET                                           
*                                                                               
         ICM   R1,15,MCBXARE2      COPY DETAILS TO BOXAREA2                     
         BZ    RUNINI28                                                         
BOX2     USING BOXD,R1                                                          
         MVC   BOX2.BOXOFF,BOX1.BOXOFF                                          
         MVC   BOX2.BOXMAXL,BOX1.BOXMAXL                                        
         MVC   BOX2.BOXOFFST,BOX1.BOXOFFST                                      
         MVC   BOX2.BOXSHADE,BOX1.BOXSHADE                                      
         MVC   BOX2.BOXSHCH1,BOX1.BOXSHCH1                                      
         MVC   BOX2.BOXWIDTH,BOX1.BOXWIDTH                                      
         MVC   BOX2.BOXAWIDE,BOX1.BOXAWIDE                                      
         MVC   BOX2.BOXOPTCD,BOX1.BOXOPTCD                                      
         MVC   BOX2.BOXHEADF,BOX1.BOXHEADF                                      
         MVC   BOX2.BOXFONT,BOX1.BOXFONT                                        
         DROP  R4,BOX1,BOX2                                                     
*                                                                               
RUNINI28 CLI   MCESTAE,C'Y'        ACTIVATE ESTAE IF REQUIRED                   
         BNE   RUNINI30                                                         
         ESTAE PRGCHK,CT,ASYNCH=YES,TERM=YES                                    
*                                                                               
RUNINI30 CLI   MCRECOVR,C' '       SET RECOVERY=N IF NOT SPECIFIED              
         BNE   *+8                                                              
         MVI   MCRECOVR,C'N'                                                    
*                                                                               
         LA    R2,MCIO             R2=A(ID RECORD)                              
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         OC    CTIKID,MCUSERID     USE USER-ID CODE IF GIVEN                    
         BNZ   *+14                                                             
         OC    CTIKNUM,MCORIGID    OR ORIGIN ID IF NOT                          
         BZ    RUNINI32                                                         
         GOTOR GETCFR,1            READ ID RECORD AND POST VALUES               
         BE    RUNINI32            R2 --> VALID ID RECORD                       
         GOTOR MCVLOGIO,MCPARA,('FF',1),(L'CARDERR3,CARDERR3)                   
         MVC   MCP,MCSPACES                                                     
         MVC   MCP(L'CARDERR3),CARDERR3                                         
         GOTOR MCVPRINT,MCPARA,MCP-1,=C'BL01'                                   
         DC    H'0'                INVALID USERID                               
*                                                                               
         USING CT5REC,R2                                                        
RUNINI32 XC    CT5KEY,CT5KEY       BUILD KEY OF ACCESS RECORD                   
         MVI   CT5KTYP,CT5KTYPQ                                                 
         MVC   CT5KALPH,MCUSER                                                  
         MVC   MCDUB(L'MCORIGID),MCORIGID                                       
         MVC   MCAGYSEC,MCAGYALP                                                
         GOTOR GETCFR,2            READ ACCESS RECORD AND POST VALUES           
         OC    MCDUB(L'MCORIGID),MCDUB                                          
         BNZ   RUNINI34                                                         
*                                                                               
         OC    MCORIGID,MCORIGID   TEST ORIGIN SET FROM ACCESS RECORD           
         BZ    RUNINI34                                                         
         USING CTIREC,R2                                                        
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,MCORIGID                                                 
         GOTOR GETCFR,1            READ ID RECORD AND POST VALUES               
*                                                                               
RUNINI34 OC    MCDESTID,MCDESTID   TEST DESTINATION ID GIVEN                    
         BNZ   *+10                                                             
         OC    MCDESTID,MCORIGID   NO-USE ORIGIN ID                             
         BZ    RUNINI36                                                         
         XC    CTIKEY,CTIKEY                                                    
         MVI   CTIKTYP,CTIKTYPQ                                                 
         MVC   CTIKNUM,MCDESTID                                                 
         GOTOR GETCFR,3            READ ID RECORD AND POST VALUES               
*                                                                               
RUNINI36 L     RF,MCAEXTRA         CHECK COMSCORE                               
         USING MCEXTRA,RF                                                       
         CLI   MCCSMODE,MCCSMOD1                                                
         BNE   RUNINI38                                                         
         CLC   =C'ID000000',MCMVSDSN+13 WAS THE ID FILLED IN?                   
         BNE   RUNINI38                                                         
         ICM   R1,3,MCORIGID       ORIGIN ID                                    
         CVD   R1,MCDUB                                                         
         OI    MCDUB+L'MCDUB-1,X'0F'                                            
         UNPK  MCMVSDSN+15(6),MCDUB                                             
         DROP  RF                                                               
*                                                                               
RUNINI38 GOTOR GETAPP              LOAD APPLICATION PHASES                      
*                                                                               
         OC    MCIDSENO,MCIDSENO   ENSURE REQUIRED VALUES ARE SET               
         BZ    *+10                                                             
         OC    MCORIGID,MCORIGID                                                
         BZ    *+10                                                             
         OC    MCIDAGYA,MCIDAGYA                                                
         JNZ   EXIT                                                             
         DC    H'0'                INVALID CONTROL CARDS                        
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - REQUEST CONTROLS                         *         
***********************************************************************         
CTLINPUT MVC   MCINPUT,MCP+6       INPUT=                                       
         BR    RE                                                               
*                                                                               
CTLSTART MVI   MCRESTRT,C'Y'       START=                                       
         GOTOR VALNUM,MCPARA,(X'01',MCP+6),(L'MCSTART,MCSTART)                  
*                                                                               
CTLEND   DS    0H                  END=                                         
         GOTOR VALNUM,MCPARA,(X'01',MCP+4),(L'MCEND,MCEND)                      
*                                                                               
CTLREQST MVC   MCREQCOD,MCP+8      REQUEST=                                     
         BR    RE                                                               
*                                                                               
CTLRUNDO OI    MCFLAGS2,MCFUNDO    UNDO OR UNWIND                               
         LT    RF,MCSSB                                                         
         USING SSBD,RF                                                          
         BZR   RE                                                               
         CLI   SSOXTND,FF          EXTENDED SSB                                 
         BNER  RE                  NO                                           
         OI    SSOFLAG1,SSOFRCVR   FULL RECOVERY + UNWIND (GLOBAL)              
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
CTLNUMRQ DS    0H                  NUMREQS=                                     
         GOTOR VALNUM,MCPARA,(0,MCP+8),(L'MCNREQS,MCNREQS)                      
*                                                                               
CTLUPSI  LA    R1,MCP+5            UPSI=                                        
         LHI   R0,8                                                             
         LHI   RF,X'80'                                                         
CTLUPSI2 CLI   0(R1),C' '                                                       
         BER   RE                                                               
         CLI   0(R1),C'0'                                                       
         BE    CTLUPSI4                                                         
         CLI   0(R1),C'1'                                                       
         BNE   NUMERR                                                           
         EX    RF,*+8                                                           
         B     *+8                                                              
         OI    MCUPSI,0                                                         
CTLUPSI4 AHI   R1,1                                                             
         SRL   RF,1                                                             
         BCT   R0,CTLUPSI2                                                      
         BR    RE                                                               
*                                                                               
CTLREQFL L     RF,MCAREQFL         REQFILE= (ALTERNATE SYSIN FILE)              
         MVC   40(8,RF),MCP+8      MOVE DDNAME TO DCB                           
         MVI   MCREQFIL,C'Y'       SET FLAG ALT SYSIN ASSIGNED                  
         BR    RE                                                               
*                                                                               
CTLFACPK MVC   MCFACPAK,MCP+7      FACPAK= (ID FOR SOON FACWRK KEY)             
         BR    RE                                                               
*                                                                               
CTLBILRF LR    R0,RE               BILLREF=XXX...... (24 HEX CHRS)              
         GOTOR MCVHEXIN,MCPARA,MCP+8,MCBILL,24                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - PRINTING CONTROLS                        *         
***********************************************************************         
CTLPRNTR MVI   MCOUTPUT,C'P'       PRINTER                                      
         BR    RE                                                               
*                                                                               
CTLREPTS CLI   MCP+8,C'2'          REPORTS= N'SIMULTANEOUS REPORTS              
         BLR   RE                                                               
         MVI   MCREPTS,2                                                        
         BR    RE                                                               
*                                                                               
CTLHSKIP CLI   MCP+7,C'0'          HDSKIP= N'LINES TO PRINT B4 HEADS            
         BL    NUMERR                                                           
         CLI   MCP+7,C'9'                                                       
         BH    NUMERR                                                           
         ICM   RF,15,MCBXAREA                                                   
         BZR   RE                                                               
         MVC   BOXHDSKP-BOXD(,RF),MCP+7                                         
         NI    BOXHDSKP-BOXD(RF),X'0F'                                          
         BR    RE                                                               
*                                                                               
CTLDIRCT LA    RF,MCREMOTE         DIRECT=SPP,FFFF,IIIII,P,DITACSL,C,ID         
         USING REMOTED,RF          =XXXXXXXXXXXXXXXXXX,DDDDDDDDDDD,XXR          
         MVI   REMOTFLG,X'FA'      SET NEW STYLE LAYOUT FOR REMOTEC             
         MVC   REMOTSYS,MCP+7      SYSTEM                                       
         MVC   REMOTPRG,MCP+8      PROGRAM                                      
         MVC   REMOTJID,MCP+7                                                   
         MVC   REMOTFNO,MCP+11                                                  
         PACK  MCDUB,MCP+16(5)                                                  
         CVB   R0,MCDUB                                                         
         STH   R0,MCDUB                                                         
         MVC   REMOTDST,MCDUB                                                   
         CLC   MCP+21(6),=C',PRINT' OPTION TO PRINT AS WELL                     
         BNE   *+12                                                             
         MVI   REMOTPRT,1                                                       
         B     CTLDIR10                                                         
         CLC   MCP+21(3),=C',Q,'   TEST IF NEW STYLE ,QUEUE IE ,Q,              
         BE    CTLDIR02                                                         
         CLC   MCP+21(3),=C',P,'   TEST IF NEW STYLE ,PRINT IE ,P,              
         BNE   CTLDIR10                                                         
         MVI   REMOTPRT,1                                                       
*                                                                               
CTLDIR02 CLI   MCP+24,C'D'         DOWNLOADABLE                                 
         BNE   CTLDIR03                                                         
         OI    REMOTTYP,X'10'                                                   
         B     CTLDIR04                                                         
CTLDIR03 CLI   MCP+24,C'S'         SQL                                          
         BNE   CTLDIR06                                                         
         OI    REMOTTYP,X'08'                                                   
CTLDIR04 L     RF,MCVLOGO          DOWN/SQL - PATCH LOGOS NOT TO PRINT          
         MVC   0(2,RF),=X'07FE'                                                 
         LA    RF,MCREMOTE                                                      
*                                                                               
CTLDIR06 CLI   MCP+25,C'I'         INVISIBLE                                    
         BNE   *+8                                                              
         OI    REMOTSTA,X'02'                                                   
*                                                                               
CTLDIR08 MVC   REMOTRTY,MCP+26     REPORT TYPE                                  
         MVC   REMOTARC,MCP+27     ARCHIVE CLASS                                
         CLI   REMOTARC,C'A'       UPPER CASE CLASS MEANS ARCHIVABLE            
         BL    *+12                                                             
         OI    REMOTTY1,X'40'      ARCHIVABLE                                   
         B     CTLDIR10                                                         
         CLI   REMOTARC,X'81'      LOWER CASE CLASS MEANS ARCHIVED              
         BL    CTLDIR10                                                         
         OI    REMOTARC,X'40'      CONVERT TO UPPER CASE                        
         OI    REMOTTY1,X'20'      ARCHIVED                                     
*                                                                               
CTLDIR10 MVC   REMOTCLS,MCP+28     CLASS                                        
*                                                                               
         CLI   MCP+29,C'E'         ARCHIVE STATUS                               
         BNE   *+8                                                              
         MVI   REMOTTY1,X'80'      ARCE - ELIGIBLE                              
         CLI   MCP+29,C'A'                                                      
         BNE   *+8                                                              
         MVI   REMOTTY1,X'40'      ARCA - ARCHIVABLE                            
         CLI   MCP+29,C'D'                                                      
         BNE   *+8                                                              
         MVI   REMOTTY1,X'20'      ARCD - ARCHIVED                              
*                                                                               
         MVC   REMOTCPY,MCP+32                                                  
         NI    REMOTCPY,X'0F'      COPIES                                       
         CLI   REMOTPRT,0                                                       
         BNE   CTLDIR11                                                         
         DROP  RF                                                               
*                                                                               
         CLI   MCP+30,C'Y'         USER WANTED LOGOS ANYWAY                     
         BE    CTLDIR11                                                         
         L     RF,MCVLOGO          PATCH LOGOS NOT TO PRINT                     
         MVC   0(2,RF),=X'07FE'                                                 
         LA    RF,MCREMOTE                                                      
*                                                                               
CTLDIR11 MVI   MCOUTPUT,C'D'       SET PQ HEX DATA ID=XX.. OR I=XXX..           
         MVI   MCREMPQF,0                                                       
         XC    MCREMPQK,MCREMPQK                                                
         CLC   MCP+34(3),=C'XF='   TEST XFILE RUN                               
         BNE   *+12                                                             
         OI    MCPRTIND,MCPRTXFI                                                
         B     CTLDIR13                                                         
*                                                                               
         CLC   MCP+34(3),=C'ID='   TEST ID PASSED (FOR JOB NOTIFY)              
         BE    CTLDIR13                                                         
         CLC   MCP+34(2),=C'X='    TEST XFILE RUN                               
         BNE   *+12                                                             
         OI    MCPRTIND,MCPRTXFI                                                
         B     *+14                                                             
*                                                                               
         CLC   MCP+34(2),=C'I='    TEST ID PASSED (FOR JOB NOTIFY)              
         BNE   CTLDIR14                                                         
         SR    R0,R0               GET FIRST T OF 20-BIT TRACK TTTTT            
         IC    R0,MCP+36                                                        
         CHI   R0,240              TEST C'0' THRU C'9'                          
         BL    *+12                                                             
         AHI   R0,-240             C'0' THRU C'9' TO 00-09                      
         B     *+8                                                              
         AHI   R0,-183             C'A' THRU C'F' TO 0A-OF                      
         STC   R0,MCREMPQF         SET FIRST T OF 20-BIT TRACK TTTTT            
*                                                                               
CTLDIR13 LR    R0,RE                                                            
         GOTOR MCVHEXIN,MCPARA,MCP+37,MCREMPQK,18                               
         LR    RE,R0                                                            
         LA    RF,MCREMOTE         SET SPP FOR SOONS                            
         USING REMOTED,RF                                                       
         MVC   REMOTSYS,MCSYSTEM                                                
         CLC   MCSYSTEM,=C'MP'     ADJUST IF 1ST CHR NOT THE SAME               
         BNE   *+8                                                              
         MVI   REMOTSYS,C'L'                                                    
         CLC   MCSYSTEM,=C'MB'                                                  
         BNE   *+8                                                              
         MVI   REMOTSYS,C'B'                                                    
         CLC   MCSYSTEM,=C'PE'                                                  
         BNE   *+8                                                              
         MVI   REMOTSYS,C'E'                                                    
         MVC   REMOTPRG,MCPROG                                                  
         CLI   REMOTCLS,C' '       FORCE ABSENT CLASS TO 'Z' FOR SOONS          
         BH    *+8                                                              
         MVI   REMOTCLS,C'Z'                                                    
*                                                                               
CTLDIR14 CLI   MCNETPAK,C'Y'       DID THIS COME FROM NETPAK?                   
         BNE   *+8                                                              
         MVI   REMOTSYS,C'N'       YES-CHANGE 'S' BACK TO 'N'                   
         CLI   MCP+55,C','         TEST IF REPORT PQ DESC PASSED                
         BNE   *+10                                                             
         MVC   REMOTDSC,MCP+56                                                  
         CLC   MCP+56(3),=C'XF='   TEST XFILE RUN                               
         BNE   *+8                                                              
         OI    MCPRTIND,MCPRTXFI                                                
         CLI   MCP+67,C','         TEST IF REPORT PQ FORM PASSED                
         BNE   *+16                                                             
         MVC   REMOTSUB,MCP+68                                                  
         MVC   REMOTRET,MCP+70     LAST CHR IS RETAIN CLASS                     
         DROP  RF                                                               
*&&UK                                                                           
         OC    LOGOIDNO,LOGOIDNO   SET LOGO ID NUM IF NOT SET                   
         BNZ   *+10                                                             
         MVC   LOGOIDNO,MCREMPQK                                                
*&&                                                                             
         BR    RE                                                               
*                                                                               
CTLMAXLN DS    0H                  MAXLINES=NNN                                 
         GOTOR VALNUM,MCPARA,(0,MCP+9),(L'MCMAXLIN,MCMAXLIN)                    
*                                                                               
CTLSEGMT DS    0H                  SEGMENT=NNNNN                                
         GOTOR VALNUM,MCPARA,(0,MCP+8),(L'LOGOSGMX,LOGOSGMX)                    
*                                                                               
CTLSTRIP ICM   RF,15,MCBXAREA      STRIPE=YES                                   
         BZR   RE                                                               
         USING BOXD,RF                                                          
         MVI   BOXSHADE,4                                                       
         MVI   BOXSHCH1,X'42'                                                   
         MVI   BOXOFF,C'Y'                                                      
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
CTLLOGST MVC   LOGOSTTS,MCP+10     LOGOSTATS=  (X=EXTENDED STATS)               
         BR    RE                                                               
*                                                                               
CTLLOGAD MVC   LOGODDSA,MCP+8      LOGOADR=                                     
         BR    RE                                                               
*                                                                               
CTLLOGDV MVC   LOGOSTRP,MCP+8      LOGODIV=                                     
         BR    RE                                                               
*                                                                               
CTLNOLOG L     RF,MCVLOGO          NOLOGOS - PATCH LOGOS NOT TO PRINT           
         MVC   0(2,RF),=X'07FE'                                                 
*                                                                               
CTLLOGUS OI    MCPRTIND,MCPRTINL   LOGOS=USER                                   
         BR    RE                                                               
*                                                                               
CTLRQREP MVC   MCREQREP,MCP+7      REQREP=                                      
         BR    RE                                                               
*                                                                               
CTLNOCTL OI    MCPRTIND,MCPRTICN   NOCONTROL                                    
         BR    RE                                                               
*                                                                               
CTLCTL   CLI   MCP+8,C'Y'          CONTROL=Y MONACC defaults to NO              
         BNE   *+10                                                             
         NI    MCPRTIND,FF-MCPRTICN                                             
         BR    RE                                                               
*                                                                               
         CLI   MCP+8,C'N'          CONTROL=N (DONT PRINT CONTROL CARDS)         
         BNE   *+10                                                             
         OI    MCPRTIND,MCPRTICN                                                
         BR    RE                                                               
*                                                                               
         CLI   MCP+8,C'D'          CONTROL=D (WRITE CTRL CARDS TO DISK)         
         BNER  RE                                                               
         OI    MCPRTIND,MCPRTICD                                                
         BR    RE                                                               
*                                                                               
CTLNOREQ OI    MCPRTIND,MCPRTIRQ   NOREQUEST                                    
         BR    RE                                                               
*                                                                               
CTLWIDTH ICM   RF,15,MCBXAREA      WIDTH=NN                                     
         BZR   RE                                                               
         USING BOXD,RF                                                          
         ICM   RF,15,MCBXAREA                                                   
         MVC   BOXAWIDE,MCVWIDE                                                 
         GOTOR VALNUM,MCPARA,(0,MCP+6),(L'BOXWIDTH,BOXWIDTH)                    
         DROP  RF                                                               
*                                                                               
CTLOFSET DS    0H                  OFFSET=NN                                    
         GOTOR VALNUM,MCPARA,(0,MCP+7),(L'MCOFFSET,MCOFFSET)                    
*                                                                               
CTLSPOOL L     RF,MCVSPLAR         SPOOL=                                       
         MVC   4(20,RF),MCP+6                                                   
         PACK  MCDUB,MCP+44(3)     GEN=NNN (COLUMN 41)                          
         CVB   R1,MCDUB                                                         
         STC   R1,2(RF)                                                         
         BR    RE                                                               
*                                                                               
CTLPASWD DS    0H                  PASSWORD=                                    
         MVC   MCREMOTE+(REMOTPAS-REMOTED)(L'REMOTPAS),MCP+9                    
         MVI   MCREMOTE+(REMOTSF1-REMOTED),REMOTPIN                             
         MVI   MCREMOTE+(REMOTSF2-REMOTED),0                                    
         BR    RE                                                               
*                                                                               
CTLPQPIN DS    0H                  PQPIN=                                       
         MVC   MCREMOTE+(REMOTPAS-REMOTED)(L'REMOTPAS),MCP+6                    
         MVI   MCREMOTE+(REMOTSF1-REMOTED),REMOTPIN                             
         MVI   MCREMOTE+(REMOTSF2-REMOTED),0                                    
         BR    RE                                                               
*                                                                               
CTLPQPID DS    0H                  PQPID=                                       
         MVC   MCREMOTE+(REMOTPAS-REMOTED)(L'REMOTPAS),MCP+6                    
         MVI   MCREMOTE+(REMOTSF1-REMOTED),REMOTPID                             
         MVI   MCREMOTE+(REMOTSF2-REMOTED),0                                    
         CLC   MCP+6(4),=C'*   '                                                
         BNER  RE                                                               
         MVI   MCREMOTE+(REMOTPAS-REMOTED),X'FF' REQUEST MY PID                 
         BR    RE                                                               
*                                                                               
CTLRETIN DS    0H                  RETAIN=                                      
         MVC   MCREMOTE+(REMOTRET-REMOTED)(L'REMOTRET),MCP+7                    
         BR    RE                                                               
*                                                                               
CTLCHARS DS    0H                  CHARS=                                       
         MVC   MCREMOTE+(REMOTCHR-REMOTED)(L'REMOTCHR),MCP+6                    
         BR    RE                                                               
*                                                                               
CTLFORMS DS    0H                  FNO=                                         
         MVC   MCREMOTE+(REMOTFNO-REMOTED)(L'REMOTFNO),MCP+4                    
         BR    RE                                                               
*                                                                               
CTLMAKER DS    0H                  MAKER=XX SETS ...XX                          
         MVC   MCREMOTE+(REMOTSUB-REMOTED)(L'REMOTSUB),MCP+6                    
         BR    RE                                                               
*                                                                               
CTLRDESC DS    0H                  DESC=                                        
         MVC   MCREMOTE+(REMOTDSC-REMOTED)(L'REMOTDSC),MCP+5                    
         BR    RE                                                               
*                                                                               
CTLOPTCD ICM   RF,15,MCBXAREA      OPTCD=J                                      
         BZR   RE                                                               
         MVI   BOXOPTCD-BOXD(RF),C'J'                                           
         BR    RE                                                               
*                                                                               
CTLHFONT ICM   RF,15,MCBXAREA      HEADFONT=NN                                  
         BZR   RE                                                               
         USING BOXD,RF                                                          
         GOTOR VALNUM,MCPARA,(0,MCP+9),(L'BOXHEADF,BOXHEADF)                    
         DROP  RF                                                               
*                                                                               
CTLPFONT CLI   MCP+5,C'0'          FONT=N (0-3)                                 
         BLR   RE                                                               
         CLI   MCP+5,C'3'                                                       
         BHR   RE                                                               
         ICM   RF,15,MCBXAREA                                                   
         BZR   RE                                                               
         MVC   BOXFONT-BOXD(,RF),MCP+5                                          
         NI    BOXFONT-BOXD(RF),X'0F'                                           
         BR    RE                                                               
*                                                                               
CTLFRMDF CLI   MCP+8,C'Y'          FORMDEF=Y                                    
         BNER  RE                                                               
         OI    MCPRTIND,MCPRTIFD   SET FORMDEF CARD ENCOUNTERED                 
         BR    RE                                                               
*                                                                               
         USING BOXD,RF                                                          
CTLPAGDF ICM   RF,15,MCBXAREA      PAGEDEF=                                     
         BZR   RE                                                               
         ICM   RF,15,BOXPGDEF-BOXD(RF)                                          
         BZR   RE                                                               
         LHI   R0,10               MAX 10                                       
CTLPAGD2 CLC   0(6,RF),MCSPACES    DID WE SAVE ONE HERE ?                       
         BNH   CTLPAGD4            NO                                           
         AHI   RF,6                LOOK AT NEXT LOCATION                        
         BCT   R0,CTLPAGD2         TRY AGAIN                                    
         BR    RE                  ONLY ALLOWED 10                              
CTLPAGD4 MVC   0(6,RF),MCP+8       SAVE OFF PAGEDEF VALUE                       
         BR    RE                                                               
*                                                                               
CTLPIT10 ST    RE,MCSAVRE          PITCH=10                                     
         L     R3,MCVPRNTR                                                      
         SETPRT (3),CHARS=(DG10)                                                
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
CTLPIT12 ST    RE,MCSAVRE          PITCH=12                                     
         L     R3,MCVPRNTR                                                      
         SETPRT (3),CHARS=(DG12)                                                
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
CTLPIT15 ST    RE,MCSAVRE          PITCH=15                                     
         L     R3,MCVPRNTR                                                      
         SETPRT (3),CHARS=(DG15)                                                
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
CTLDOWNL MVI   MCDOWNLD,1          DOWNLOAD ACTIVE                              
         BR    RE                                                               
*                                                                               
CTLBURST CLI   MCP+8,C'R'          BURSTPQ=R                                    
         BNE   *+10                                                             
         MVI   MCBURST,1           BURST PQ AT EVERY NEW REQUEST                
         BR    RE                                                               
         CLI   MCP+8,C'P'          BURSTPQ=P                                    
         BNE   *+10                                                             
         MVI   MCBURST,2           BURST PQ AT CHANGE OF PERSON                 
         BR    RE                                                               
         CLI   MCP+8,C'Y'          BURSTPQ=Y DEFAULTS TO P FOR PERSON           
         BNER  RE                                                               
         MVI   MCBURST,2                                                        
         BR    RE                                                               
*                                                                               
CTLEZCTL L     RF,=A(SVEZCTL)      EZCTL=...                                    
         LHI   R0,EZCTLMAX                                                      
         CLC   0(L'SVEZCTL,RF),MCSPACES                                         
         BNH   *+14                                                             
         AHI   RF,L'SVEZCTL                                                     
         BCT   R0,*-14                                                          
         DC    H'0'                MAX N'EZCTL CARDS EXCEEDED                   
         MVC   0(L'SVEZCTL,RF),MCP+6                                            
         BR    RE                                                               
*                                                                               
CTLARNUM DS    0H                  ALTREFNUM=NNNN                               
         GOTOR VALNUM,MCPARA,(0,MCP+10),(L'MCALTREF,MCALTREF)                   
*                                                                               
CTLCLAS2 DS    0H                  CLASS2=C                                     
         MVC   MCREMOT2+(REMOTCLS-REMOTED)(L'REMOTCLS),MCP+7                    
         BR    RE                                                               
*                                                                               
CTLID2   DS    0H                  ID2=XXX                                      
         MVC   MCREMOT2+(REMOTJID-REMOTED)(L'REMOTJID),MCP+4                    
         BR    RE                                                               
*                                                                               
CTLFORM2 DS    0H                  FORM2=XXXX                                   
         MVC   MCREMOT2+(REMOTFNO-REMOTED)(L'REMOTFNO),MCP+6                    
         BR    RE                                                               
*                                                                               
CTLPRQIX OI    MCOPT1,MCQ1PQIX     PQIX=Y                                       
         CLI   MCP+5,C'Y'                                                       
         BER   RE                                                               
         NI    MCOPT1,FF-MCQ1PQIX                                               
         BR    RE                                                               
*                                                                               
CTLARC   DS    0H                  ARC=AG,........                              
         CLI   MCP+6,C','                                                       
         BNER  RE                                                               
         MVC   MCARC,MCP                                                        
         BR    RE                                                               
*                                                                               
CTLSOON  OI    MCFLAGS2,MCISSOON   IS A SOON JOB                                
         L     RF,MCAEXTRA         SOON=CL,P,F,...                              
         USING MCEXTRA,RF                                                       
         MVC   MCSOON,MCP                                                       
         BR    RE                                                               
*                                                                               
CTLWHY   DS    0H                  WHY=...........                              
         MVC   MCREQWHY,MCP+4                                                   
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - RUN CONTROLS                             *         
***********************************************************************         
CTLTRACE MVC   MCTRACE,MCP+6       TRACE=                                       
         BR    RE                                                               
*                                                                               
CTLDUMPO MVC   MCDUMP,MCP+5        DUMP=                                        
         BR    RE                                                               
*                                                                               
CTLDUDMP MVC   MCDUDMP,MCP+8       DUPDUMP=                                     
         BR    RE                                                               
*                                                                               
CTLWRITE MVC   MCWRITE,MCP+6       WRITE=N FOR APPLICATION FILES                
         ICM   RF,15,MCSSB                                                      
         USING SSBD,RF                                                          
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   MCP+6,C'N'          SET WRITE=N FLAG IN SSB                      
         BNER  RE                                                               
         OI    SSOMTIND,SSOWRTN                                                 
         BR    RE                                                               
*                                                                               
CTLWRSRV ICM   RF,15,MCSSB         WRSRV=N FOR SERVICE SYSTEM FILES             
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   MCP+6,C'N'          SET WRSRV=N FLAG IN SSB                      
         BNER  RE                                                               
         OI    SSOMTIND,SSOWSRN                                                 
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
CTLWKDTE L     RF,MCAEXTRA         WKDATE=                                      
         USING MCEXTRA,RF                                                       
         MVC   MCWKDATE,MCP+7                                                   
         BR    RE                                                               
*                                                                               
CTLEMAX  L     RF,MCAEXTRA                                                      
         GOTOR VALNUM,MCPARA,(X'80',MCP+9),(L'MCMAXEML,MCMAXEML)                
         CLC   MCMAXEML,=AL2(5000)                                              
         BNH   CTLOK                                                            
         DC    H'00'                                                            
*                                                                               
CTLEMAIL CLI   MCP+6,C'Y'          EMAIL=                                       
         BNER  RE                                                               
         OI    MCFLAGS2,MCEMAIL                                                 
         BR    RE                                                               
*                                                                               
CTLEIDTO L     RF,MCAEXTRA         EID=   (max length 72)                       
         MVC   MC@EMAIL,MCP+4                                                   
         BR    RE                                                               
*&&US                                                                           
***********************************************************************         
* USED FOR COMSCORE REQUEST, IDENTIFIES WHICH PASS RUN IT IS                    
***********************************************************************         
CTLCOMSC MVC   MCCSMODE,MCP+9      COMSCORE=PASS 1 or PASS 2                    
         L     RF,MCAEXTRA         Temporary code                               
         MVC   MCCSPASS,MCP+9      Temporary code                               
         CLI   MCCSMODE,MCCSMOD1   PASS 1                                       
         BNER  RE                  NO                                           
         L     RF,=V(PRINT00)      ENTRY POINT IN DDPRINT TO NO-OP              
         MVC   0(2,RF),=X'07FE'    NO-OP PRINTING                               
         BR    RE                                                               
                                                                                
***********************************************************************         
* ABOUT OF TIME IN MINUTES TO WAIT FOR THE RESPONSE FROM COMSCORE               
* COMSWAIT=15  DEFAULT VALUE                                                    
***********************************************************************         
CTLCWAIT L     RF,MCAEXTRA         COMSWAIT=                                    
         GOTOR VALNUM,MCPARA,(X'80',MCP+9),(L'MCCSWAIT,MCCSWAIT)                
         L     R1,MCCSWAIT                                                      
         MHI   R1,(60*100)         ADJUST FOR MINUTES                           
         ST    R1,MCCSWAIT                                                      
         B     CTLOK                                                            
                                                                                
***********************************************************************         
* THIS WORKS IN CONJUCTION WITH CTLCWAIT AND MCCSRSP SET TO "W"                 
* COMSRTRY=3  DEFAULT VALUE                                                     
* NOTE: VALNUM DOES NOT RETURN TO CALLER BUT TO CTLOK OR CTLERR                 
***********************************************************************         
CTLCSRTY L     RF,MCAEXTRA         COMSRTRY=                                    
         GOTOR VALNUM,MCPARA,(0,MCP+9),(L'MCCSRTRY,MCCSRTRY)                    
                                                                                
***********************************************************************         
* ACTION OR RESPONSE ACTION FOR JOB TO TAKE IF IT TIMES OUT WAITING             
* COMSRSP=OPER   - JOB WILL ASK OPERATOR WHAT TO DO                             
* COMSRSP=WAIT   - JOB WILL WAIT AGAIN FOR TIME SET IN CTLCWAIT.                
* COMSRSP=CANCEL - JOB WILL CAUSE AN ABEND IF IT TIMES OUT                      
* COMSRSP=IGNORE - JOB WILL SKIP WAITING FOR DATA AND GO TO NEXT REQ.           
***********************************************************************         
CTLCSRSP L     RF,MCAEXTRA         COMSRSP=                                     
         MVC   MCCSRSP,MCP+8       ACTION VALUE                                 
         CLI   MCCSRSP,C'O'        OPERATOR RESPONSE                            
         BER   RE                                                               
         CLI   MCCSRSP,C'W'        WAIT, CONT. WAITING MCCSRTRY TIMES           
         BER   RE                                                               
         CLI   MCCSRSP,C'C'        CANCEL JOB                                   
         BER   RE                                                               
         CLI   MCCSRSP,C'I'        IGNORE COMSCORE PROCESSING                   
         BER   RE                                                               
         J     CTLERR                                                           
                                                                                
***********************************************************************         
* CAN BE USED FOR ANY JOB BUT WAS DESIGNED FOR COMSCORE REQUEST                 
***********************************************************************         
CTLMVSDS L     RF,MCAEXTRA         MVSDSN=                                      
         MVC   MCMVSDSN,MCP+7      PASS 1 OR PASS 2                             
         CLI   MCMVSDSN,C' '       WAS IT SET TO ANYTHING?                      
         BHR   RE                                                               
         CLI   MCCSMODE,0          IT IS A COMSCORE REQUEST                     
         BER   RE                  NO                                           
*                              0123456789012345678901234567890123456789         
         MVC   MCMVSDSN(39),=C'CMPRDFTP.ADV.ID000000.PQ000000.SOON.REQ'         
*                                                                               
         LA    RF,MCREMOTE                                                      
         USING REMOTED,RF                                                       
         XR    R3,R3                                                            
         ICM   R3,3,REMOTDST       PRINTQ DESTINATION                           
         CVD   R3,MCDUB                                                         
         OI    MCDUB+L'MCDUB-1,X'0F'                                            
         L     RF,MCAEXTRA         RE-LOAD                                      
         USING MCEXTRA,RF                                                       
         UNPK  MCMVSDSN+15(6),MCDUB                                             
         TM    MCFLAGS2,MCISSOON   SOON JOB?                                    
         BO    CTLMVSD1            YES                                          
*        OC    MCREMPQK,MCREMPQK   SOON JOB?                                    
*        BNZ   CTLMVSD1            YES IT IS.                                   
***********************************************************************         
* CODE IS USED WHEN NOT GOING TO PQ (DIRECT=) AND IT IS A BATCH JOB             
***********************************************************************         
         LA    R3,MCMVSDSN+22                                                   
         SAM31 ,                                                                
         ST    RE,SVRE                                                          
         IAZXJSAB READ,JOBID=(R3)                                               
         L     RE,SVRE                                                          
         L     RF,MCAEXTRA               RELOAD                                 
         MVC   MCMVSDSN+31(4),=C'R001'   REQUEST ON                             
         SAM24 ,                                                                
         B     CTLMVSD2                                                         
SVRE     DS    A                                                                
                                                                                
**********************************************************************          
* IF USER HAS PUT DIRECT= CARD IT WON'T HAVE A PQ REPORT NUMBER                 
* USER WILL NEED TO EXTRACT PQ REPORT NUMBER AND FILL IN MCMVSDSN               
* PQ000000 VALUE.                                                               
*                                                                               
**********************************************************************          
CTLMVSD1 ICM   R3,3,MCREMPQK+5     PRINTQ REPORT NUMBER                         
         CVD   R3,MCDUB                                                         
         OI    MCDUB+L'MCDUB-1,X'0F'                                            
         UNPK  MCMVSDSN+24(6),MCDUB   CONVERT TO DECIMAL                        
*                                                                               
CTLMVSD2 ICM   R3,15,MCSSB                                                      
         USING SSBD,R3                                                          
         CLI   SSODSPAC,C'R'       REP DSPACE                                   
         BNE   CTLMVSD3                                                         
         MVC   MCMVSDSN+9(3),=C'REP'                                            
         BR    RE                                                               
*                                                                               
CTLMVSD3 CLI   SSODSPAC,C'Q'       FQA DSPACE                                   
         BNE   *+14                                                             
         MVC   MCMVSDSN+9(3),=C'FQA'                                            
         B     CTLMVSD4                                                         
*                                                                               
         CLI   SSODSPAC,C'C'       CSC DSPACE                                   
         BNE   *+14                                                             
         MVC   MCMVSDSN+9(3),=C'CSC'                                            
         B     CTLMVSD4                                                         
*                                                                               
         CLI   SSODSPAC,C'T'       TST DSPACE                                   
         BNER  RE                                                               
         MVC   MCMVSDSN+9(3),=C'TST'                                            
*                                                                               
CTLMVSD4 MVC   MCMVSDSN(5),=C'CMDEV'                                            
         BR    RE                                                               
         DROP  R3                  SSB                                          
         DROP  RF                  MCEXTRA                                      
*&&                                                                             
CTLRUNDT LR    R0,RE               DATE=                                        
         BRAS  RE,VALDATE                                                       
         BNE   CTLRUND2            INVALID SOFT DATE                            
         TM    MCFLAGS2,MCFSOFDT   PROCESS AS NORMAL IF NO SOFT DATES           
         BZ    CTLRUND1                                                         
*                                                                               
         MVC   MCWORK,MCSPACES     BUILD GENERATED BY MESSAGE                   
         MVC   MCWORK(L'CARDMSG3),CARDMSG3                                      
         MVC   MCWORK+L'CARDMSG3(L'MCP-L'CARDMSG3),MCP                          
*                                                                               
         MVC   MCP+5(L'MCP-5),MCSPACES REPLACE SOFT DATE WITH REAL DATE         
         MVC   MCP+5(L'MCDATE),MCDATE                                           
*                                                                               
         TM    MCPRTIND,MCPRTICN   TEST DONT PRINT CONTROL CARDS                
         BZ    CTLRUND1                                                         
*                                                                               
         LHI   RF,80               NOT PRINTING SO OUTPUT TO CONSOLE            
         GOTOR MCVLOGIO,MCPARA,('FF',1),((RF),MCP)                              
         LHI   RF,80                                                            
         GOTOR MCVLOGIO,MCPARA,('FF',1),((RF),MCWORK)                           
*                                                                               
CTLRUND1 LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
CTLRUND2 MVC   MCP+72(L'CARDERR4),CARDERR4 INVALID SOFT DATE                    
         J     CTLERR                                                           
*                                                                               
CTLNUMBR MVC   MCNUMBER(1),MCP+7   NUMBER=                                      
         GOTOR VALNUM,MCPARA,(X'01',MCP+8),(L'MCNUMBER-1,MCNUMBER+1)            
*                                                                               
CTLBILLN DS    0H                  BILLNUM=                                     
         GOTOR VALNUM,MCPARA,(X'01',MCP+8),(L'MCNUMBER,MCNUMBER)                
*                                                                               
CTLRERUN MVC   MCRERUN,MCP+6       RERUN=                                       
         CLI   MCP+6,C'0'                                                       
         BLR   RE                                                               
         MVI   MCRERUN,C'Y'                                                     
         LR    R0,RE                                                            
         GOTOR MCVDTCON,MCPARA,(4,MCP+6),(2,MCRERUND)                           
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
CTLRECVR MVC   MCRECOVR,MCP+8      RECOVER=                                     
         B     *+10                                                             
*                                                                               
CTLRECV2 MVC   MCRECOVR,MCP+9      RECOVERY=                                    
*                                                                               
         CLI   MCRECOVR,C'V'       Write to VTS & tag to recovery               
         BE    *+10                                                             
         CLI   MCRECOVR,C'K'       Write to MVS DSN & tag to recovery           
         BNER  RE                                                               
*                                                                               
         MVI   MCRECOVR,C'Y'       Change it to "Y" but turn on bits            
         USING SSBD,RF                                                          
         ICM   RF,15,MCSSB                                                      
         BZR   RE                                                               
         OI    SSOSTAT2,SSOSRSQF   RECOVER TO MVS DATASET                       
         MVC   SSOSQRTN,=F'1'      USE DMRCVR internal routine                  
         CLI   MCRECOVR,C'K'       Write to MVS DSN?                            
         BNER  RE                                                               
         OI    SSOFLAG3,SSO3RDSK                                                
         BR    RE                                                               
*&&UK                                                                           
CTLHARP  CLI   MCP+5,C'N'          HARP=N                                       
         BNER  RE                                                               
         ICM   RF,15,MCSSB                                                      
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         OI    SSOFLAG3,SSO3RCNH                                                
         BR    RE                                                               
*&&                                                                             
CTLTICK  L     RF,MCAEXTRA         TICKET=                                      
         USING MCEXTRA,RF                                                       
         MVC   MCTICKET,MCP+7                                                   
         ICM   RF,15,MCSSB                                                      
         USING SSBD,RF                                                          
         TM    SSOFLAG3,SSO3XUTL                                                
         BZR   RE                                                               
         L     RF,MCUTL                                                         
         USING UTLD,RF                                                          
         MVC   TTICKET,MCP+7                                                    
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
CTLMARK  ICM   RF,15,MCSSB         MARKER=Y                                     
         USING SSBD,RF                                                          
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   MCP+7,C'Y'          SET MARKER=Y IN SSB                          
         BNER  RE                                                               
         OI    SSOMTIND,SSOMRKY                                                 
         BR    RE                                                               
*                                                                               
CTLFLASH ICM   RF,15,MCSSB         FLASH=X                                      
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   MCP+6,C'A'          TEST IF VALID FLASH CHARACTER                
         BLR   RE                                                               
         CLI   MCP+6,C'N'          FLASH=N MEANS NO FLASH COPY                  
         BER   RE                                                               
         OI    SSOFLAG2,SSO2FLSH                                                
         MVC   SSOFLSHI,MCP+6                                                   
         CLI   MCP+6,C'Y'          FLASH=Y IS DEFAULT SO SET TO FLASH=S         
         BNER  RE                  TO CAUSE DSN=FLS.XXX...                      
         MVI   SSOFLSHI,C'S'                                                    
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
CTLCMT   DS    0H                  COMMIT=NNN                                   
         GOTOR VALNUM,MCPARA,(0,MCP+7),(L'MCCOMMIT,MCCOMMIT)                    
*                                                                               
CTLPSTNG MVC   MCPOSTNG,MCP+8                                                   
         BR    RE                                                               
*                                                                               
CTLTESTR MVI   MCTSTRUN,FF         SET AS PROGRAMMER TEST RUN                   
         BR    RE                                                               
*                                                                               
CTLOFFPK OI    MCFLAGS,MCFOFFPK    TURN ON THE OFF PEAK SOON FLAG               
         BR    RE                                                               
*                                                                               
CTLFXOUT OI    MCFLAGS,MCOUT198    FIX OUTPUT LEN AT 198                        
         OI    MCPRTIND,MCPRTICN   NOCONTROL IS AUTOMATIC                       
         BR    RE                                                               
*                                                                               
CTLRUNMD MVI   MCRUNMOD,C'S'       SET SINGLE REQUEST MODE FOR RRG              
         BR    RE                                                               
*                                                                               
CTLCTRY  DS    0H                  CTRY=NNNN                                    
         GOTOR VALNUM,MCPARA,(0,MCP+5),(L'MCCTRY,MCCTRY)                        
*                                                                               
CTLLANG  DS    0H                  LANG=NN                                      
         GOTOR VALNUM,MCPARA,(0,MCP+5),(L'MCLANG,MCLANG)                        
*                                                                               
CTLESTAE MVC   MCESTAE,MCP+6       ESTAE=Y/N                                    
         BR    RE                                                               
*                                                                               
CTLUPDID MVC   MCUPDID,MCP+6       UPDID=XX                                     
         LR    R0,RE                                                            
         GOTOR MCVDMGR,MCPARA,=C'UPDID'                                         
         L     R1,12(R1)                                                        
         MVC   0(2,R1),MCUPDID                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
CTLPARM  MVC   MCFFPARM,MCP+5      PARM=XXXXXXXX                                
         BR    RE                                                               
*                                                                               
CTLCORET LR    R0,RE               CORETAB=AAAAAAAA                             
         GOTOR MCVHEXIN,MCPARA,MCP+8,MCAMCORE,8                                 
         OC    12(4,R1),12(R1)                                                  
         BNZ   *+6                                                              
         DC    H'0'                MONSOON PASSED A BAD ADDRESS HERE            
         LR    RE,R0                                                            
         BR    RE                                                               
*                                                                               
CTLSPEND MVC   MCSPCEND,MCP+8      SPACEND=F(ORCE) OR U(SE)                     
         BR    RE                                                               
*                                                                               
CTLSTOKN MVC   MCSTOKEN,MCP+7      STOKEN=XXXXXXXXYYYY                          
         MVC   MCDSORIG,MCP+15                                                  
         BR    RE                                                               
*                                                                               
CTLDSPCE ICM   RF,15,MCSSB         DSPACE=                                      
         USING SSBD,RF                                                          
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         TM    SSOFLAG3,SSO3DSPS                                                
         JO    *+14                Yes                                          
         MVC   SSODSPAC,MCP+7                                                   
         OI    SSOFLAG3,SSO3DSPS                                                
*&&US                                                                           
         CLI   SSODSPAC,C'T'       IF DSPACE=T                                  
         BNER  RE                                                               
         ICM   R1,15,MCVDDSIO                                                   
         BZR   RE                                                               
         CLC   0(6,R1),=CL6'DDSIO'  AND IF NO OVERRIDE DDSIO SET                
         BNER  RE                                                               
         MVC   0(8,R1),=CL8'DDSION' THEN FORCE DDSIO=DDSION                     
*&&                                                                             
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
CTLPROF  NI    MCRFLAG1,FF-MCRPROF PROFILE=                                     
         CLC   MCP+8(4),=C'DISK'                                                
         BNER  RE                                                               
         OI    MCRFLAG1,MCRPROF                                                 
         BR    RE                                                               
*                                                                               
CTLUPSO  CLI   MCP+7,C'N'          UPSOON=Y/N                                   
         BNER  RE                                                               
         OI    MCRFLAG1,MCRUSDIN                                                
         BR    RE                                                               
*                                                                               
CTLQ0    MVC   MCDBSSID,MCP+8      DB2SSID=                                     
         BR    RE                                                               
*                                                                               
CTLR0    ICM   RF,15,=V(SSB)       LOCKER=Y                                     
         USING SSBD,RF                                                          
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   MCP+7,C'Y'                                                       
         BNER  RE                                                               
*&&UK*&& OI    SSOSTAT2,SSOSLOCK+SSOSROLC                                       
*&&US*&& OI    SSOSTAT2,SSOSLOCK+SSOSROLC+SSOSGALO                              
         OI    SSOFLAG1,SSOFRCVR+SSOFXCPY                                       
         NI    SSOSTAT2,FF-SSOSNRCV                                             
         BR    RE                                                               
*                                                                               
CTLS0    ICM   RF,15,=V(SSB)       GLOBAL=N/Y                                   
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   MCP+7,C'N'                                                       
         BNE   *+10                                                             
         OI    SSOFLAG1,SSOFLOCL                                                
         BR    RE                                                               
         CLI   MCP+7,C'Y'                                                       
         BNER  RE                                                               
         OI    SSOFLAG1,SSOFGLOB                                                
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - PHASE CONTROLS                           *         
***********************************************************************         
CTLTEST  LA    R2,MCP+5            TEST=                                        
         B     CTLTEST1                                                         
CTLMUST  LA    R2,MCP+9            MUSTTEST=                                    
*                                                                               
CTLTEST1 CLI   3(R2),C' '          NON-BLANK IN 3RD POS INDICATES               
         BE    CTLTEST2            NEW STYLE (WITH OPTIONAL PATCH)              
         L     R1,=A(TESTPOOL)     REQUIRES TEST POOL                           
         LHI   R0,TESTPMAX                                                      
         CLI   0(R1),0                                                          
         BE    *+16                                                             
         AHI   R1,TESTPLEN                                                      
         BCT   R0,*-12                                                          
         B     CTLERR                                                           
         MVC   0(1,R1),MCP         'T'(EST=) OR C'M'(USTTEST=)                  
         MVC   1(TESTPLEN-1,R1),0(R2)                                           
         BR    RE                                                               
*                                                                               
CTLTEST2 LA    R1,MCTEST1          SYSTEM CONTROLLER STYLE                      
         CLI   1(R2),C'1'                                                       
         BE    CTLTEST3                                                         
         LA    R1,MCTEST2                                                       
         CLI   1(R2),C'2'                                                       
         BE    CTLTEST3                                                         
         LA    R1,MCTEST3                                                       
         CLI   1(R2),C'3'                                                       
         BE    CTLTEST3                                                         
         LA    R1,MCTEST4                                                       
CTLTEST3 MVC   0(1,R1),2(R2)                                                    
         CLI   0(R1),C' '          TEST IF CHARACTER GIVEN                      
         BNE   *+8                                                              
         MVI   0(R1),C'T'                                                       
         CLI   MCP,C'M'            TEST 'M'(USTTEST=) CONTROL CARD              
         BNER  RE                                                               
         NI    0(R1),FF-C' '       TURN UPPER TO LOWER IF MUSTTEST=             
         BR    RE                                                               
*                                                                               
CTLLOAD  MVC   MCLOAD,MCP+5        LOAD=                                        
         MVC   MCLOADSM,MCP+9                                                   
         BR    RE                                                               
*                                                                               
CTLAPLIC L     R1,MCAPHAS2         APPLIC=AUTO                                  
         MVC   MCAPHAS2,0(R1)                                                   
         BR    RE                                                               
*                                                                               
CTLDDSIO ICM   R1,15,MCVDDSIO      DDSIO=                                       
         BZR   RE                                                               
         MVC   0(8,R1),MCP+6                                                    
         BR    RE                                                               
*                                                                               
CTLAUTLD MVI   MCAUTOLD,C'Y'       AUTOLOAD (READ CTFILE LOAD RECORD)           
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - LOGO AND USER-ID CONTROLS                *         
***********************************************************************         
CTLLOGO  MVC   LOGO1,MCP+5         LOGO=                                        
         MVC   LOGO2,MCP+12                                                     
         MVC   LOGOJOB,MCP+19                                                   
         CLC   MCP+27(5),MCSPACES                                               
         BE    CTLLOG2                                                          
         PACK  MCDUB,MCP+27(5)                                                  
         CVB   R0,MCDUB                                                         
         STH   R0,MCDESTID                                                      
         STH   R0,LOGOIDNO                                                      
*                                                                               
         LHI   RF,MCLUID-MCBLOCK                                                
         AR    RF,RC                                                            
         MVC   0(8,RF),MCP+60      SAVE LUID                                    
*                                                                               
CTLLOG2  CLC   MCP+33(7),=C'ORIGIN='                                            
         BNER  RE                                                               
         PACK  MCDUB,MCP+40(5)                                                  
         CVB   R0,MCDUB                                                         
         STH   R0,MCORIGID                                                      
         BR    RE                                                               
*                                                                               
CTLLOGO2 PACK  MCDUB,MCP+6(5)      LOGO2=NNNNN TRMNUM                           
         CVB   R0,MCDUB                                                         
         LHI   RF,MCTNUM-MCBLOCK                                                
         AR    RF,RC                                                            
         STH   R0,0(RF)            SAVE LUID                                    
         BR    RE                                                               
*                                                                               
CTLDEST  MVC   LOGONAME,MCP+12     DESTINATION=                                 
         MVC   LOGOADD,MCP+45                                                   
         BR    RE                                                               
*                                                                               
CTLDEST2 MVC   LOGOADD2,MCP+6      DEST2=                                       
         MVC   LOGOADD3,MCP+39                                                  
         BR    RE                                                               
*                                                                               
CTLSHIP  MVC   LOGOINFO(75),MCP+5  SHIP=                                        
         BR    RE                                                               
*                                                                               
CTLSHIP2 MVC   LOGOSHP2(75),MCP+5  SHP2=                                        
         BR    RE                                                               
*                                                                               
CTLORIG  MVC   MCORIGIN,MCP+7      ORIGIN=                                      
         MVC   MCORIGAD,MCP+40                                                  
         BR    RE                                                               
*                                                                               
CTLBRK1  MVC   LOGOBRK(75),MCP+5   BRK1=                                        
         BR    RE                                                               
*                                                                               
CTLBRK2  MVC   LOGOBRK2(75),MCP+5                                               
         BR    RE                                                               
*                                                                               
CTLORGID DS    0H                  ORIGID=NNNNN                                 
         GOTOR VALNUM,MCPARA,(0,MCP+7),(L'MCORIGID,MCORIGID)                    
*                                                                               
CTLDSTID DS    0H                  DESTID=NNNNN                                 
         GOTOR VALNUM,MCPARA,(0,MCP+7),(L'MCDESTID,MCDESTID)                    
*                                                                               
CTLID    MVC   MCUSERID,MCP+3      ID=XXXXXXXXXX (ORIGIN)                       
         BR    RE                                                               
*                                                                               
CTLTAPE  MVC   MCTAPETY,MCP+5      TAPE=                                        
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - SPECIAL SYSTEM CONTROLS                  *         
***********************************************************************         
CTLPHASE MVC   MCACPHSE,MCP+6      PHASE=                                       
         BR    RE                                                               
*                                                                               
CTLFILI  MVI   MCACIFIL,C'Y'       FILE=INPUT                                   
         BR    RE                                                               
*                                                                               
CTLFILO  MVI   MCACOFIL,C'Y'       FILE=OUTPUT                                  
         BR    RE                                                               
*                                                                               
CTLFILW  MVI   MCACWFIL,C'Y'       FILE=WORK                                    
         BR    RE                                                               
*                                                                               
CTLOUTP  MVC   MCACFCHE,MCP+7      OUTPUT=                                      
         BR    RE                                                               
*                                                                               
CTLLEDGR MVC   MCACLDGE,MCP+7      LEDGER=                                      
         BR    RE                                                               
*                                                                               
CTLMEDIA MVC   MCMEDIA,MCP+6       MEDIA=              MEDLINE SPECIAL          
         BR    RE                                                               
*&&UK                                                                           
CTLMEDSP MVC   MCMEDDSP,MCP+8      MEDDSPC=            MEDLINE SPECIAL          
         CLI   MCMEDDSP+L'MCMEDDSP-1,C'1'                                       
         BNLR  RE                  ASSUME NUMERIC 1-5                           
         LA    RF,MEDDSTAB         LOOK FOR SYNONYM                             
         LA    R0,MEDDSNQ                                                       
         CLC   MCMEDDSP,0(RF)                                                   
         BE    *+14                                                             
         LA    RF,8(RF)                                                         
         BCT   R0,*-14                                                          
         BR    RE                                                               
         MVC   MCMEDDSP,4(RF)                                                   
         BR    RE                                                               
*&&                                                                             
CTLAGNCY MVC   MCUSER,MCP+7        AGENCY=                                      
         BR    RE                                                               
*                                                                               
CTLREPRT MVC   MCMEDREP,MCP+7      REPORT=                                      
         BR    RE                                                               
*                                                                               
CTLXSPOT CLI   MCP+6,C'Y'          XSPOT=              SPOTPAK SPECIAL          
         BNER  RE                                                               
         OI    MCOPT1,MCQ1XSP      SET TO OPEN XSPOT FILES                      
         BR    RE                                                               
*                                                                               
CTLSTAPK MVC   MCSTAVER,MCP+8      MOVE STAPACK VERSION                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - SYSTEM CONTROLS                          *         
***********************************************************************         
CTLMPL   MVI   MCOVSYS,5           MPXPPAA                                      
         MVC   MCUSER,MCP+5                                                     
         B     CTLALLX2                                                         
*                                                                               
CTLACC   MVI   MCOVSYS,6           ACXPPAA                                      
         CLI   MCP+5,C' '                                                       
         BE    *+10                                                             
         MVC   MCUSER,MCP+5        SET USER IF SPECIFIED                        
         B     CTLALLX2                                                         
*                                                                               
CTLMBA   MVI   MCOVSYS,9           MBXPPAA                                      
         MVC   MCUSER,MCP+5                                                     
         B     CTLALLX2                                                         
*                                                                               
CTLCON   MVI   MCOVSYS,10          CTPP                                         
         CLI   MCP+4,C' '                                                       
         BE    CTLALLX                                                          
         MVC   MCUSER,MCP+4        SET USER IF SPECIFIED                        
         B     CTLALLX                                                          
*                                                                               
CTLPER   MVI   MCOVSYS,14          PEPPAA                                       
         CLI   MCP+4,C' '                                                       
         BE    *+10                                                             
         MVC   MCUSER,MCP+4        SET USER IF SPECIFIED                        
         B     CTLALLX                                                          
*&&US                                                                           
CTLSPT   MVI   MCOVSYS,2           SPXPPAA                                      
         B     CTLSETRC                                                         
*                                                                               
CTLNET   MVI   MCOVSYS,3           NEXPPAA                                      
         MVI   MCNETPAK,C'Y'       SET SPECIAL NETPAK FLAG                      
         MVC   MCP(2),=C'SP'       SET USES SPOT PROGRAMS                       
         B     CTLSETRC                                                         
*                                                                               
CTLSTR   MVI   MCOVSYS,13          STXPPAA                                      
         B     CTLSETRC                                                         
*                                                                               
CTLSETRC MVC   MCUSER,MCP+5                                                     
         CLI   MCRECOVR,C' '       SET RECOVERY ON IF NOT SPECIFIED             
         BNE   *+8                                                              
         MVI   MCRECOVR,C'Y'                                                    
         B     CTLALLX2                                                         
*                                                                               
CTLCPP   MVI   MCOVSYS,12          CPXPPAA                                      
         MVC   MCUSER,MCP+5                                                     
         B     CTLALLX2                                                         
*                                                                               
CTLPRT   MVI   MCOVSYS,4           PPXPPAAM                                     
         MVC   MCMEDIA,MCP+7                                                    
         MVC   MCUSER,MCP+5                                                     
         B     CTLALLX2                                                         
*                                                                               
CTLTAL   MVI   MCOVSYS,7           TAXPPAA                                      
         MVC   MCUSER,MCP+5                                                     
         B     CTLALLX2                                                         
*                                                                               
CTLREP   MVI   MCOVSYS,8           REXPPRR                                      
         MVC   MCUSER,MCP+5                                                     
         CLI   MCRECOVR,C' '       SET RECOVERY ON IF NOT SPECIFIED             
         BNE   *+8                                                              
         MVI   MCRECOVR,C'Y'                                                    
         B     CTLALLX2                                                         
*&&                                                                             
*&&UK                                                                           
CTLMED   MVI   MCOVSYS,4           MEPP                                         
         B     CTLALLX                                                          
*                                                                               
CTLFEE   MVI   MCOVSYS,7           FEXPPAA                                      
         MVC   MCUSER,MCP+5                                                     
         B     CTLALLX2                                                         
*&&                                                                             
CTLALLX  MVC   MCPROG,MCP+2        SSPP                                         
         B     *+10                                                             
CTLALLX2 MVC   MCPROG,MCP+3        SSXPP                                        
*                                                                               
         MVC   MCSYSTEM,MCP                                                     
         ICM   RF,15,MCSSB                                                      
         USING SSBD,RF                                                          
         BZR   RE                                                               
         CLI   SSOXTND,FF                                                       
         BNER  RE                                                               
         CLI   SSODSPAC,C' '                                                    
         BHR   RE                                                               
         MVI   SSODSPAC,C'A'       DEFAULT TO ADV DSPACE                        
*&&US                                                                           
         CLI   MCOVSYS,8                                                        
         BNER  RE                                                               
         MVI   SSODSPAC,C'R'       DEFAULT TO REP DSPACE                        
*&&                                                                             
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - RUN TIME PARAMETERS                      *         
***********************************************************************         
CTLRTPT  ST    RE,MCSAVRE          RTPTCB= OR RTPTIME=                          
         GOTOR RTPDATA             TOTAL TCB TIME                               
         MHI   R0,100              CONVERT TO SECS/100                          
         ST    R0,MCRTPTCB                                                      
         LTR   R0,R0               CARD SAYS RTPTCB=0 (OR RTPTIME=0)            
         BZ    CTLRTPTX            YES-PREVENT TIMEOUT UNDER IDF                
         LAY   R2,MCCPUWRK                                                      
         TIMEUSED STORADR=(R2),LINKAGE=SYSTEM,CPU=MIC                           
         LM    RE,RF,0(R2)                                                      
         D     RE,=F'10000'                                                     
         LR    R0,RF               R0=TCB TIME (IN 1/100 SECS)                  
         A     R0,MCRTPTCB                                                      
CTLRTPTX DS    0H                                                               
         ST    R0,MCNXTTCB         SET NEXT TCB EVENT TIME                      
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
CTLRTPP  ST    RE,MCSAVRE          RTPPAGES=                                    
         GOTOR RTPDATA             NUMBER OF PRINTED PAGES                      
         ST    R0,MCRTPPGS                                                      
         ST    R0,MCNXTPGS                                                      
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
CTLRTPE  ST    RE,MCSAVRE          RTPEXCP=                                     
         GOTOR RTPDATA             EXCP COUNT                                   
         ST    R0,MCRTPIOS                                                      
         ST    R0,MCNXTIOS                                                      
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
CTLRTPC  ST    RE,MCSAVRE          RTPCPU=                                      
         GOTOR RTPDATA             UNINTERRUPTED CPU TIME IN SECONDS            
         MHI   R0,100              CONVERT TO UNITS OF .01 SEC                  
         ST    R0,MCRTPCPU                                                      
         L     RE,MCSAVRE                                                       
         BR    RE                                                               
*                                                                               
***********************************************************************         
* CARD VALIDATION ROUTINES - RUN TIME PARAMETERS                      *         
***********************************************************************         
RTPDATA  LA    R1,MCP+20           SCAN BACKWARDS FOR LAST DIGIT                
         CLI   0(R1),C' '                                                       
         BNE   *+8                                                              
         BCT   R1,*-8                                                           
         LR    RF,R1                                                            
RTPDATA2 CLI   0(RF),C'='                                                       
         BE    RTPDATA4                                                         
         CLI   0(RF),C'0'                                                       
         BL    NUMERR              LESS THAN 0 IS ERROR                         
         CLI   0(RF),C'9'                                                       
         BH    NUMERR                                                           
         BCT   RF,RTPDATA2                                                      
RTPDATA4 SR    R1,RF               GIVES DATA LEN                               
         BNP   NUMERR                                                           
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  MCDUB,1(0,RF)                                                    
         CVB   R0,MCDUB                                                         
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* CARD VALIDATION ROUTINES - NUMBER VALIDATER                         *         
* THIS ROUTINE DOES NOT RETURN TO CALL, BUT PRINTS CARD AND GETS NEXT *         
* CONTROL CARD.                                                                 
* NEW OPTION TO RETURN TO CALLER                                                
***********************************************************************         
VALNUM   STM   RE,RC,12(RD)                                                     
         LM    R2,R3,0(R1)         R2=A(INPUT),R3=A(OUTPUT)                     
         LR    RE,R2               RE=A(INPUT)                                  
         SR    RF,RF               RF=K'INPUT                                   
         LHI   R0,8                R0=MAX LEN OF INPUT                          
VALNUM2  CLI   0(RE),C'0'                                                       
         BL    VALNUM4                                                          
         CLI   0(RE),C'9'                                                       
         BH    VALNUM4                                                          
         AHI   RE,1                                                             
         AHI   RF,1                                                             
         BCT   R0,VALNUM2                                                       
*                                                                               
VALNUM4  LTR   RF,RF               TEST GOOD INPUT LENGTH                       
         BZ    VALNUMN                                                          
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         PACK  MCDUB,0(0,R2)       MCDUB CONTAINS PACKED VALUE                  
*                                                                               
         L     RE,4(R1)            RE=A(OUTPUT)                                 
         SR    RF,RF                                                            
         IC    RF,4(R1)            RF=L'OUTPUT                                  
         TM    0(R1),B'00000001'   TEST BINARY OUTPUT                           
         BO    VALNUM6             NO, DON'T WANT BINARY                        
*                                                                               
         CVB   R0,MCDUB            R0=BINARY VALUE                              
         IC    RF,ICMMASK-1(RF)                                                 
         EX    RF,*+8                                                           
         B     VALNUMY                                                          
         STCM  R0,0,0(RE)                                                       
*                                                                               
VALNUM6  BCTR  RF,0                PACKED OUTPUT                                
         SLL   RF,4                                                             
         EX    RF,*+8                                                           
         B     VALNUMY                                                          
         ZAP   0(0,RE),MCDUB                                                    
*                                                                               
VALNUMY  LM    RE,RC,12(RD)                                                     
         TM    0(R1),B'10000000'                                                
         BOR   RE                  CALLER WANT TO RETURN                        
         J     CTLOK                                                            
*                                                                               
VALNUMN  LM    RE,RC,12(RD)                                                     
*                                                                               
NUMERR   MVC   MCP+72(L'CARDERR2),CARDERR2                                      
         J     CTLERR                                                           
*                                                                               
         USING MCEXTRA,RF                                                       
CTLEXTR  L     RF,MCAEXTRA                                                      
         MVC   MCXTRACT,MCP+8      THIS PROGRAM IS AN EXTRACT (Y/N)             
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
         USING MCEXTRA,RF                                                       
CTLXMOD  L     RF,MCAEXTRA                                                      
         MVC   MCXTRMOD,MCP+8      EXTRACT MODE (LOAD,UPDATE)                   
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
         USING MCEXTRA,RF                                                       
CTLXTYP  L     RF,MCAEXTRA                                                      
         MVC   MCXTRTYP,MCP+8      EXTRACT RECORD TYPE (BUY, GOAL, ETC)         
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
         USING MCEXTRA,RF                                                       
CTLEXF1  L     RF,MCAEXTRA                                                      
         MVC   MCEXFIL1,MCP+8      DDNAME OF EXTRACT OUTPUT FILE                
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
         USING MCEXTRA,RF                                                       
CTLXSMF  L     RF,MCAEXTRA                                                      
         MVC   MCXTRSMF,MCP+7      WRITE EXTRACT SMF RECORDS (Y,N)              
         BR    RE                                                               
         DROP  RF                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
INDICS   DC    X'00'               INDICTATOR BYTE                              
INDIHEAD EQU   X'80'               CONTROL CARD HEADING PRINTED                 
*                                                                               
FF       EQU   X'FF'                                                            
*                                                                               
CONSYS   DC    C'CONTROL'                                                       
GENDIR   DC    C'GENDIR '                                                       
GENFIL   DC    C'GENFIL '                                                       
DMOPEN   DC    C'DMOPEN '                                                       
ASMIDF   DC    CL8'ASMIDF'         DEBUGGER EXECUTABLE NAME                     
*                                                                               
CARDERR1 DC    C'CONTROL CARD NOT RECOGNISED'                                   
CARDERR2 DC    C'INVALID NUMERIC VALUE'                                         
CARDERR3 DC    C'*** INVALID USERID ON CONTROL CARD ***'                        
CARDERR4 DC    C'*** INVALID SOFT DATE KEYWORD ***'                             
CARDMSG1 DC    C'RUN IS CONTINUING'                                             
CARDMSG2 DC    C'STOPPING THE PROGRAM'                                          
CARDMSG3 DC    C'***GENERATED FROM '                                            
ICMMASK  DC    X'0103070F'                                                      
*&&UK                                                                           
MEDDSTAB DC    C'ADV MED1'                                                      
         DC    C'FQA MED2'                                                      
         DC    C'NEW MED3'                                                      
         DC    C'TTS MED3'                                                      
         DC    C'TST MED4'                                                      
         DC    C'CSC MED5'                                                      
         DC    C'BAR MED6'                                                      
MEDDSNQ  EQU   (*-MEDDSTAB)/L'MEDDSTAB                                          
*&&                                                                             
         TITLE 'MASTER CONTROL - PROCESS CONTROL FILE RECORDS'                  
***********************************************************************         
* READ AND PROCESS ID AND ACCESS RECORDS                              *         
*                                                                     *         
* NTRY - R1=1 FOR ORIGIN ID                                           *         
*          =2 FOR ACCESS RECORD                                       *         
*          =3 FOR DESTINATION ID RECORD                               *         
*          =4 FOR PASSWORD RECORD                                     *         
*        R2=A(CONTROL FILE I/O AREA CONTAINING KEY OF RECORD          *         
*                                                                     *         
* EXIT - CC SET EQUAL IF RECORD IS FOUND OK                           *         
*        CC SET NOT EQUAL OTHERWISE                                   *         
***********************************************************************         
GETCFR   NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         STC   R1,MCWORK           SAVE RECORD TYPE VALUE                       
         USING CTIREC,R2                                                        
         MVC   CTILEN(4),=X'001D0000' SET CLEAN EMPTY RECORD                    
*                                                                               
         L     RF,MCUTL            SAVE AND SET CONTROL SYSTEM IN UTL           
         IC    R0,4(RF)                                                         
         MVI   4(RF),10                                                         
         GOTOR MCVDMGR,MCPARA,=C'DMREAD',=C'CTFILE',CTIREC,CTIREC               
         L     RF,MCUTL                                                         
         STC   R0,4(RF)                                                         
         CLI   8(R1),0                                                          
         BNE   GETCFRN                                                          
*                                                                               
         LA    R1,CTIDATA          POINT TO FIRST ELEMENT                       
         SR    R0,R0                                                            
*                                                                               
GETCFR04 CLI   0(R1),0             TEST E-O-R                                   
         BE    GETCFRY             YES-EXIT WITH CC EQUAL                       
*                                                                               
         USING CTDSCD,R1                                                        
         CLI   CTDSCEL,CTDSCELQ    TEST DESCRIPTION ELEMENT                     
         BNE   GETCFR06                                                         
         CLI   MCWORK,4            DONT WANT DECRIPTION FOR AUTH RECS           
         BE    GETCFR40                                                         
         CLI   CTDSCLEN,L'MCUSERID+2                                            
         BNE   *+14                                                             
         MVC   MCUSERID,CTDSC      SET USER-ID CODE                             
         B     GETCFR40                                                         
         CLI   CTDSCLEN,L'MCORIGID+2                                            
         BNE   GETCFR40                                                         
         CLI   MCWORK,1            TEST ORIGIN-ID RECORD                        
         BNE   *+14                                                             
         MVC   MCORIGID,CTDSC      SET USER-ID NUMBER                           
         B     GETCFR40                                                         
*                                                                               
         CLI   MCWORK,2            TEST ACCESS RECORD                           
         BNE   GETCFR40                                                         
         MVC   MCIDPRIN,CTDSC      SET PRINCIPAL ID NUMBER                      
         OC    MCORIGID,MCORIGID   PROCESSING ACCESS RECORD SO ONLY             
         BNZ   *+10                                                             
         MVC   MCORIGID,CTDSC      SET ORIGIN IF NOT SET ALREADY                
         B     GETCFR40                                                         
*                                                                               
         USING CTAGYD,R1                                                        
GETCFR06 CLI   CTAGYEL,CTAGYELQ    TEST AGENCY ELEMENT                          
         BNE   GETCFR10                                                         
         CLI   MCWORK,4            DONT WANT AGENCY FOR AUTH RECORDS            
         BE    GETCFR40                                                         
         CLI   MCWORK,1            TEST ORIGIN ID RECORD                        
         BNE   GETCFR08                                                         
         MVC   MCUSER,CTAGYID      SET AGENCY ALPHA                             
         MVC   MCIDAGYA,CTAGYID                                                 
         CLI   CTAGYIDT,CTAGYTXQ   ACROSS FILE READING                          
         BNE   GETCFR40                                                         
         OI    MCFLAGS2,MCCROSSF   SET FLAG TO SAY CROSS FILE READS             
         B     GETCFR40                                                         
*                                                                               
GETCFR08 CLI   MCWORK,3            TEST DESTINATION ID RECORD                   
         BNE   GETCFR40                                                         
         CLI   MCLANG,0            TEST LANGUAGE OVERRIDE SET                   
         BNE   *+10                                                             
         MVC   MCLANG,CTAGYLNG     NO-SET FROM AGENCY LANGUAGE                  
         B     GETCFR40                                                         
*                                                                               
         USING CTIDOD,R1                                                        
GETCFR10 CLI   0(R1),CTIDOELQ      TEST ID OPTIONS ELEMENT                      
         BNE   GETCFR12                                                         
         CLI   MCWORK,1            TEST ORIGIN ID RECORD                        
         BNE   GETCFR40                                                         
         MVC   MCIDOPTS,CTIDOFL1                                                
         MVC   MCIDOPT2,CTIDOFL2                                                
         B     GETCFR40                                                         
*                                                                               
         USING CTSYSD,R1                                                        
GETCFR12 CLI   CTSYSEL,CTSYSELQ    TEST SYSTEM ELEMENT                          
         BNE   GETCFR16                                                         
         CLI   MCWORK,3            TEST DESTINATION ID RECORD                   
         BE    GETCFR40                                                         
         CLC   CTSYSNUM,MCS2OSYS   MATCH ON SECOND OVERLAY SYSTEM               
         BNE   GETCFR14                                                         
         CLI   MCWORK,4            TEST AUTHORISATION RECORD                    
         BNE   *+14                                                             
         MVC   MCP2ACCS,CTSYSLMT                                                
         B     GETCFR40                                                         
         MVC   MCS2SENO,CTSYSSE                                                 
         MVC   MCS2AGYB,CTSYSAGB                                                
         CLI   MCWORK,1            TEST ORIGIN ID                               
         BNE   GETCFR14                                                         
         MVC   MCS2ACCS,CTSYSLMT   YES-SET LIMIT ACCESS                         
         MVC   MCC2ACCS,CTSYSLMT                                                
*                                                                               
GETCFR14 CLC   CTSYSNUM,MCOVSYS    MATCH ON OVERLAY SYSTEM NUMBER               
         BNE   GETCFR40                                                         
         CLI   MCWORK,4            TEST AUTHORISATION RECORD                    
         BNE   *+14                                                             
         MVC   MCP1ACCS,CTSYSLMT                                                
         B     GETCFR40                                                         
         MVC   MCIDSENO,CTSYSSE                                                 
         MVC   MCIDAGYB,CTSYSAGB   YES-SET AGYBIN AND LIMIT ACCESS              
         CLI   MCWORK,1            TEST ORIGIN ID                               
         BNE   GETCFR40                                                         
         MVC   MCIDACCS,CTSYSLMT   YES-SET LIMIT ACCESS                         
         MVC   MCC1ACCS,CTSYSLMT                                                
         B     GETCFR40                                                         
*                                                                               
         USING CTDSTD,R1                                                        
GETCFR16 CLI   CTDSTEL,CTDSTELQ    TEST ORIGIN DETAILS                          
         BNE   GETCFR18                                                         
         CLI   MCWORK,1            TEST ORIGIN ID RECORD                        
         BNE   GETCFR18                                                         
         MVC   MCIDPOWC,CTDSTPOW   SET POWER CODE                               
         CLC   MCORIGIN,MCSPACES   TEST ORIGIN NAME SET                         
         BNE   *+10                                                             
         MVC   MCORIGIN,CTDSTNAM                                                
         CLC   MCORIGAD,MCSPACES   TEST ORIGIN ADDRESS SET                      
         BNE   *+10                                                             
         MVC   MCORIGAD,CTDSTADD                                                
         B     GETCFR40                                                         
*                                                                               
         USING CTAGDD,R1                                                        
GETCFR18 CLI   CTAGDEL,CTAGDELQ    TEST AGENCY DESCRIPTION ELEMENT              
         BNE   GETCFR20                                                         
         CLI   MCWORK,2            TEST ACCESS RECORD                           
         BNE   *+10                                                             
         MVC   MCAGCOPT,CTAGOPTS   SET AGENCY OPTIONS                           
         CLI   CTAGDLEN,CTAGDL2Q   TEST EXTENDED RECORD                         
         BNE   GETCFR20                                                         
         CLI   MCWORK,2            TEST ACCESS RECORD                           
         BNE   GETCFR40                                                         
         MVC   MCAGCURR,CTAGDCUR                                                
         CLI   MCCTRY,0            TEST CTRY=NN CARD PROCESSED                  
         BNE   GETCFR40                                                         
         MVC   MCCTRY,CTAGDCTY     NO-SET COUNTRY VALUES                        
         MVC   MCAGCTRY,CTAGDCTY                                                
*                                                                               
         USING SAPALD,R1                                                        
GETCFR20 CLI   0(R1),SAPALELQ      TEST PID ELEMENT                             
         BNE   GETCFR22                                                         
         CLI   MCWORK,4            TEST AUTHORISATION RECORD                    
         BNE   GETCFR22                                                         
         L     RE,MCAEXTRA         POINT TO EXTRA DATA AREA                     
         USING MCEXTRA,RE                                                       
         MVC   MCPID,SAPALPID                                                   
         B     GETCFR40                                                         
         DROP  RE                                                               
*                                                                               
         USING CTSEAD,R1                                                        
GETCFR22 CLI   0(R1),CTSEAELQ      SECURITY AGENCY ELEMENT                      
         BNE   GETCFR24                                                         
         CLI   MCWORK,2            TEST AGENCY/ACCESS RECORD                    
         BNE   GETCFR24                                                         
         MVC   MCAGYSEC,CTSEAAID                                                
         B     GETCFR40                                                         
*                                                                               
         USING CTAGCD,R1                                                        
GETCFR24 CLI   CTAGCEL,CTAGCELQ    AGENCY LABEL ELEMENT                         
         BNE   GETCFR26                                                         
         CLI   MCWORK,2            TEST AGENCY/ACCESS RECORD                    
         BNE   GETCFR26                                                         
         L     RF,MCAEXTRA                                                      
         USING MCEXTRA,RF                                                       
         MVC   MCAGYCOD,CTAGCCOD                                                
         B     GETCFR40                                                         
         DROP  RF                                                               
*                                                                               
         USING CTAADD,R1                                                        
GETCFR26 CLI   CTAADEL,CTAADELQ    ACCESS DETAIL ELEMENT                        
         BNE   GETCFR28                                                         
         CLI   MCWORK,2            ACCESS RECORD                                
         BNE   GETCFR28                                                         
         CLI   CTAACSTN,X'FE'      CROSS FILE ID                                
         BNE   GETCFR40            NO                                           
         OI    MCFLAGS2,MCCROSSF   SET FLAG TO SAY CROSS FILE READS             
         B     GETCFR40                                                         
*                                                                               
GETCFR28 EQU   *                                                                
*                                                                               
GETCFR40 ICM   R0,1,1(R1)          BUMP TO NEXT ELEMENT                         
         BZ    GETCFRY             EXIT IF BAD ELEMENT LENGTH                   
         AR    R1,R0                                                            
         B     GETCFR04                                                         
*                                                                               
GETCFRY  CLI   *+1,0               SET CC=EQUAL IF GOOD                         
         J     EXIT                                                             
*                                                                               
GETCFRN  CLI   *+0,0               SET CC=NOT EQUAL ON ERROR                    
         J     EXIT                                                             
         DROP  R1,R2                                                            
*                                                                               
         LTORG                                                                  
         TITLE 'MASTER CONTROL - LOAD PHASES'                                   
GETAPP   NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
*                                                                               
         MVC   MCDUB,MCSPACES                                                   
         CLI   MCLOAD,C'*'         TEST ONLINE PROGRAM RUNNING OFFLINE          
         BNE   GETAPP02                                                         
         MVI   MCDUB,C'T'          YES-LOAD TSPP00T                             
         MVC   MCDUB+1(3),MCLOAD+1                                              
         MVC   MCDUB+4(2),=C'00'                                                
         MVC   MCDUB+6(1),MCTEST1                                               
         B     GETAPP06                                                         
*                                                                               
GETAPP02 MVC   MCDUB(4),MCSYSTEM   OFFLINE PHASES ARE SSPP01T/SSPP1T            
         CLC   MCLOAD,MCSPACES                                                  
         BE    GETAPP04                                                         
         CLI   MCLOADSM,C'2'                                                    
         BE    GETAPP04                                                         
         MVC   MCDUB(4),MCLOAD                                                  
*                                                                               
GETAPP04 MVC   MCDUB+4(2),=C'01'                                                
         MVC   MCDUB+6(1),MCTEST1                                               
         CLI   MCRERUN,C'U'        TEST RERUN=UNDO                              
         BNE   *+10                                                             
         MVC   MCDUB+2(2),=C'UN'   YES-SET SSUN                                 
*                                                                               
GETAPP06 GOTOR LOADEM,MCPARA,0     LOAD SPEC (OR ROOT) PHASE                    
         BNE   GETAPPN                                                          
         MVC   MCAPHAS1,4(R1)                                                   
         CLI   MCLOAD,C'*'         NO 02 PHASE FOR ONLINE PROGS                 
         BE    GETAPPX                                                          
*                                                                               
GETAPP10 MVC   MCDUB(4),MCSYSTEM                                                
         CLC   MCLOAD,MCSPACES                                                  
         BE    GETAPP12                                                         
         CLI   MCLOADSM,C'1'                                                    
         BE    GETAPP12                                                         
         MVC   MCDUB(4),MCLOAD                                                  
*                                                                               
GETAPP12 MVC   MCDUB+4(2),=C'02'                                                
         MVC   MCDUB+6(1),MCTEST2                                               
         CLI   MCRERUN,C'U'                                                     
         BNE   *+10                                                             
         MVC   MCDUB+2(2),=C'UN'                                                
         GOTOR LOADEM,MCPARA,0     LOAD 02 PHASE                                
         BNE   GETAPPN                                                          
         MVC   MCAPHAS2,4(R1)                                                   
*                                                                               
GETAPPX  J     EXIT                                                             
*                                                                               
GETAPPN  MVC   MCWORK(16),=C'CANT FIND PHASE '                                  
         MVC   MCWORK+16(8),MCDUB                                               
         GOTOR MCVLOGIO,MCPARA,('FF',1),(24,MCWORK)                             
         ABEND 668                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LOAD A PHASE AND SET NEW END OF DUMP                                *         
* P1=A(LOAD ADDR OF ONLINE PROGS)                                     *         
* MCDUB=CL8'PHASE NAME'                                               *         
***********************************************************************         
LOADEM   NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         LR    R9,R1               R9=A(CALLER'S PLIST)                         
         L     R2,0(R1)            R2=A(SCREEN LOAD POINT) OR ZERO              
*                                                                               
         NI    MCRFLAG1,FF-(MCRMUST+MCRTEST)                                    
         LA    R1,MCDUB+6          POINT TO TEST LEVEL                          
*&&UK                                                                           
         CLI   MCOVSYS,4                                                        
         BNE   LOADEM02                                                         
         CLI   MCDUB,C'T'                                                       
         BE    LOADEM02                                                         
         MVC   MCDUB+4(3),MCDUB+5                                               
         BCTR  R1,0                                                             
*&&                                                                             
LOADEM02 CLI   0(R1),C' '          TEST IF LOADING A TEST PHASE                 
         BE    LOADEM06                                                         
         TM    0(R1),C' '          TEST MUSTTEST CARD USED                      
         BZ    LOADEM04                                                         
         OI    MCRFLAG1,MCRTEST    SET TEST=                                    
         B     LOADEM06                                                         
*                                                                               
LOADEM04 OI    MCRFLAG1,MCRMUST    SET MUSTTEST=                                
         OI    0(R1),C' '                                                       
*                                                                               
LOADEM06 CLI   MCDUB,C'T'          ON-LINE PHASE?                               
         BE    LOADEM08                                                         
         CLC   MCDUB+4(2),=C'01'   NO-CHECK IF 01 PHASE                         
         BNE   LOADEM20                                                         
         B     LOADEM10            YES-FETCH THE PHASE RECORD                   
*                                                                               
LOADEM08 CLC   MCDUB+1(3),MCLOAD+1 SAME SYSTEM/PROGRAM?                         
         BNE   LOADEM20                                                         
         CLC   MCDUB+4(2),=C'00'   CONTROLLER?                                  
         BNE   LOADEM20                                                         
         TM    MCRFLAG1,MCRTEST+MCRMUST                                         
         BNZ   LOADEM20                                                         
*                                                                               
LOADEM10 LA    R3,MCIO             SET UP TO READ THE PHASE REC                 
         USING CTPHPKEY,R3                                                      
         XC    CTPHPKEY,CTPHPKEY                                                
         MVI   CTPHID,CTPHIDQ                                                   
         MVI   CTPHSUBI,CTPHSUBQ                                                
         CLI   MCDUB,C'T'          IF OFF-LINE PHASE                            
         BE    *+14                                                             
         MVC   CTPHNAME,MCDUB      THEN USE THE FULL NAME                       
         B     LOADEM12                                                         
         MVI   MCDUB,C'0'          ELSE WE USE THE HEX EQUIVALENT               
         GOTOR MCVHEXIN,MCPARA,MCDUB,CTPHHEXN,6                                 
         MVI   MCDUB,C'T'                                                       
         OC    12(4,R1),12(R1)                                                  
         BZ    LOADEM20                                                         
*                                                                               
LOADEM12 MVC   CTPHLANG,MCLANG                                                  
         GOTOR MCVDMGR,MCPARA,=C'DMREAD',=C'CTFILE',CTPHPKEY,CTPHPKEY           
         CLI   8(R1),0                                                          
         BNE   LOADEM20                                                         
*                                                                               
         LA    R1,CTPHFRST                                                      
         USING CTPHVRSD,R1                                                      
         SR    R0,R0                                                            
LOADEM14 CLI   CTPHVCDE,0          END OF RECORD?                               
         BE    LOADEM20            YES- NO VERSION CONTROL ELEMENT              
         CLI   CTPHVCDE,CTPHVCEQ   VERSION CONTROL ELEMENT?                     
         BE    *+14                                                             
         IC    R0,CTPHVLEN                                                      
         AR    R1,R0                                                            
         B     LOADEM14                                                         
*                                                                               
         IC    R0,CTPHVLEN                                                      
         SHI   R0,CTPHVOVQ                                                      
         BNP   LOADEM20            NO ENTRIES IN VERSION CTL ELEMENT            
         LA    R1,CTPHVFAC                                                      
         USING CTPHVFAC,R1                                                      
*                                                                               
LOADEM16 CLI   CTPHVSEN,0          ANY SE NUMBER TO TEST AGAINST?               
         BE    *+14                                                             
         CLC   CTPHVSEN,MCIDSENO   MATCHED?                                     
         BNE   LOADEM18                                                         
         OC    CTPHVAGY,CTPHVAGY   ANY AGY POWER CODE TO TEST AGAINST?          
         BZ    *+14                                                             
         CLC   CTPHVAGY,MCUSER     MATCHED?                                     
         BNE   LOADEM18                                                         
         MVC   MCDUB+6(1),CTPHVVRS YES-WE GOT A VERSION CONTROL BYTE            
         OI    MCRFLAG1,MCRTEST                                                 
         B     LOADEM20                                                         
*                                                                               
LOADEM18 LA    R1,CTPHVNXT         LOOK AT NEXT SET                             
         SHI   R0,CTPHVNXT-CTPHVFAC                                             
         BP    LOADEM16                                                         
         DROP  R1,R3                                                            
*                                                                               
LOADEM20 L     RF,=A(TESTPOOL)                                                  
LOADEM22 CLI   0(RF),0             MATCH ON PHASE NAME AFTER TEST=              
         BE    LOADEM24                                                         
         CLC   MCDUB(6),1(RF)      1ST 6 CHARS ONLY                             
         BE    *+12                                                             
         AHI   RF,TESTPLEN                                                      
         B     LOADEM22                                                         
*                                                                               
         CLI   7(RF),C' '          TEST IF LOADING A TEST PHASE                 
         BE    LOADEM24                                                         
         MVC   MCDUB+6(1),7(RF)                                                 
         NI    MCRFLAG1,FF-(MCRTEST+MCRMUST)                                    
         CLI   0(RF),C'M'          TEST MUSTTEST= CONTROL CARD                  
         BNE   *+8                                                              
         OI    MCRFLAG1,MCRMUST    YES-SET MUST RESOLVE TEST PHASE              
         CLI   0(RF),C'T'          TEST TEST= CONTROL CARD                      
         BNE   *+8                                                              
         OI    MCRFLAG1,MCRTEST    YES-SET TO RESOLVE TEST/PROD                 
*                                                                               
LOADEM24 CLI   MCDUB+7,C' '                                                     
         BNE   LOADEMN                                                          
*                                                                               
LOADEM28 CLI   MCLOAD,C'*'         ONLINE PROGS                                 
         BE    *+6                                                              
         SR    R2,R2                                                            
*                                                                               
         CLC   MCDUB(6),=C'T00A7A' STAPACK ?                                    
         BE    LOADEM30                                                         
         CLC   MCDUB(6),=C'T00A63' ADTRANS ?                                    
         BNE   LOADEM32                                                         
*                                                                               
LOADEM30 DS    0H                                                               
*&&DO                                                                           
         CLC   MCDUB(4),=C'T00A'   TEST A CORE-RES PHASE                        
         BE    *+14                                                             
         CLC   MCDUB(4),=C'T00B'                                                
         BNE   LOADEM32                                                         
*&&                                                                             
         ICM   RE,15,=V(CORELIST)  LOOK FOR PHASE IN CORE-RES LIST              
         BZ    LDEM30C             NO CORELIST LINKED - UGH!                    
*                                                                               
LDEM30A  OC    0(8,RE),0(RE)       NEW ENTRY                                    
         BZ    LDEM30C             YES-SO NOT IN LIST                           
         CLC   0(8,RE),MCDUB       IF WE'VE ALREADY SEEN THIS ONE               
         BE    LDEM30B             THEN WE'RE NEARLY THERE                      
         LA    RE,12(RE)                                                        
         B     LDEM30A                                                          
*                                                                               
LDEM30B  OC    8(4,RE),8(RE)       IF ADDRESS NON-ZERO, LOADED ALREADY          
         BNZ   LDEM30D             SO JUST RETURN ADDRESS                       
***********************************************************************         
* THIS CODE NEEDED BECAUSE CALLOFF PUTS THE NAME IN THE LIST BEFORE   *         
* THE PHASE IS LOADED AND WE FIND THE NAME BUT THERE'S NO ADDRESS!    *         
***********************************************************************         
         XC    0(8,RE),0(RE)       CLEAR NAME - RESTORE BELOW                   
LDEM30C  LR    R0,RE               SAVE CORELIST ENTRY ADDR IN R0               
         B     LOADEM32                                                         
*                                                                               
LDEM30D  XC    0(4,R9),0(R9)       CLEAR LENGTH SINCE DONT KNOW IT              
         MVC   4(4,R9),8(RE)       RETURN ADDRESS FROM TABLE                    
         B     LOADEMY             AND GET OUT!                                 
*                                                                               
LOADEM32 GOTOR LOADER,MCPARA,MCDUB,(R2),(C'M',(R2))                             
*                                                                               
         OC    4(4,R1),4(R1)       TEST PHASE FOUND                             
         BNZ   LOADEM34                                                         
         TM    MCRFLAG1,MCRMUST    TEST MUSTTEST= IS SET                        
         BNZ   LOADEMN                                                          
         TM    MCRFLAG1,MCRTEST    TEST TEST= IS SET                            
         BZ    LOADEMN                                                          
         NI    MCRFLAG1,FF-(MCRTEST)                                            
         CLI   MCDUB+6,C' '        YES-TRY LOADING PRODUCTION PHASE             
         BNE   *+12                                                             
         MVI   MCDUB+5,C' '                                                     
         B     LOADEM32                                                         
         MVI   MCDUB+6,C' '                                                     
         B     LOADEM32                                                         
*                                                                               
LOADEM34 DS    0H                                                               
***********************************************************************         
* THIS IS TEMPORARY UNTIL WE GET THE REUSABILITY ISSUE STRAIGHTED OUT.*         
* IN THE MEANTIME, THESE HARD-CODED PHASE CHECKS NEED TO MATCH THE    *         
* ONES JUST ABOVE.                                                    *         
***********************************************************************         
         CLC   MCDUB(6),=C'T00A7A' STAPACK ?                                    
         BE    LOADEM35                                                         
         CLC   MCDUB(6),=C'T00A63' ADTRANS ?                                    
         BNE   LOADEM36                                                         
*                                                                               
LOADEM35 DS    0H                                                               
*&&DO                                                                           
         CLC   MCDUB(4),=C'T00A'                                                
         BE    *+14                                                             
         CLC   MCDUB(4),=C'T00B'                                                
         BNE   LOADEM36                                                         
*&&                                                                             
         LTR   RE,R0               POINT TO EMPTY SLOT IN CORELIST              
         BZ    LOADEM36            BUT DONT SAVE IF NO CORELIST!                
         MVC   0(8,RE),MCDUB       SAVE PHASE NAME                              
         MVC   8(4,RE),4(R1)       AND ADDRESS                                  
*                                                                               
LOADEM36 L     RF,X'10'(,R0)       GET CVT ADDRESS                              
         TM    X'74'(RF),X'80'     TEST XA                                      
         BO    LOADEM37                                                         
         L     RF,X'220'(,R0)      GET ASCB ADDRESS FROM PSAAOLD                
         L     RF,X'30'(RF)        GET LDA ADDRESS                              
         L     RF,X'5C0'(RF)       GET CURRENT TOP OF REGION ADDRESS            
         B     LOADEM38                                                         
*                                                                               
LOADEM37 L     RF,4(R1)            GET ENTRY POINT ADDRESS                      
         A     RF,0(R1)            ADD LENGTH                                   
         LA    RF,0(RF)            CLEAR HOB                                    
         C     RF,MCDMPEND                                                      
         BL    LOADEM40                                                         
*                                                                               
LOADEM38 ST    RF,MCDMPEND         STORE END OF REGION FOR PRGCHK               
*                                                                               
LOADEM40 LM    RE,RF,0(R1)         RE=LENGTH, RF=ENTRY POINT ADDRESS            
         LA    R1,7(RE,RF)                                                      
         SRL   R1,3                                                             
         SLL   R1,3                R1=A(NEXT DBLWD BOUNDARY)                    
         STM   RE,RF,0(R9)         RETURN LENGTH, A(START), A(NEXT)             
*                                                                               
         LR    R3,RF                                                            
         L     R2,=A(TESTPOOL)     TEST=NNNNNN DDDDDD PPPP...                   
LOADEM42 CLI   0(R2),0             MATCH ON PHASE NAME AFTER TEST=              
         BE    LOADEMY                                                          
         CLC   MCDUB(6),1(R2)      1ST 6 CHARS ONLY                             
         BE    *+12                                                             
LOADEM44 AHI   R2,TESTPLEN                                                      
         B     LOADEM42                                                         
*                                                                               
         LA    R4,7(R2)            SCAN FOR END OF PHASE NAME                   
         CLI   0(R4),C' '                                                       
         BE    *+12                                                             
         AHI   R4,1                                                             
         B     *-12                                                             
         CLI   1(R4),C' '          IS THERE A PATCH                             
         BE    LOADEM44            NO                                           
*                                                                               
         XC    MCWORK,MCWORK       CONVERT DISPLACEMENT TO BINARY               
         GOTOR MCVHEXIN,MCWORK,1(R4),MCWORK+17,6                                
         OC    12(4,R1),12(R1)                                                  
         BZ    TESTERR                                                          
*                                                                               
         LA    RF,8(R4)            CALCULATE LENGTH OF PATCH                    
         CLI   0(RF),C' '                                                       
         BE    *+12                                                             
         AHI   RF,1                                                             
         B     *-12                                                             
         LA    R0,8(R4)                                                         
         SR    RF,R0                                                            
         BZ    TESTERR                                                          
         GOTOR MCVHEXIN,MCWORK,8(R4),MCWORK+20,(RF)                             
         ICM   RF,15,12(R1)                                                     
         BZ    TESTERR                                                          
         BCTR  RF,0                RF=L'PATCH-1                                 
         LR    R1,R3               R1=A(PHASE)                                  
         A     R1,MCWORK+16        +DISPLACEMENT                                
         EX    RF,*+8                                                           
         B     LOADEM44            LOOK FOR ANOTHER ZAP CARD                    
         MVC   0(0,R1),MCWORK+20                                                
*                                                                               
LOADEMY  CLI   *+1,0               SET CC=EQUAL IF SUCCESSFUL LOAD              
         J     EXIT                                                             
*                                                                               
LOADEMN  CLI   *+0,0               SET CC=NOT EQUAL IF NOT SUCCESSFUL           
         J     EXIT                                                             
*                                                                               
TESTERR  MVC   MCP(L'MCREQREC),0(R3)                                            
         MVC   MCP+L'MCREQREC+2(9),=C'BAD TEST='                                
         GOTOR MCVPRINT,MCPARA,MCP-1,=C'BL01'                                   
         ABEND 669                                                              
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* PARAMETERS TO LOADER                                                *         
*                                                                     *         
* P1     AL4(PHASE NAME)                                              *         
* P2     AL4(LOAD ADDRESS), AL4(0) LOAD PHASE AND RETURN ADDRESS OR   *         
*        X'FFFFFFFF' TO DELETE NAMED PHASE                            *         
* P3 B0  C'M' TO LOAD AND MOVE TO ACTUAL ADDRESS (FOR SCREENS)        *         
*        C'A' TO LOAD AND MOVE TO DOWBLEWORD ALIGNED ADDRESS          *         
*        ALL OTHER VALUES LOAD PHASE AND RETURN ACTUAL LOAD ADDRESS   *         
*                                                                     *         
* AFTER LOADER                                                        *         
*                                                                     *         
* P1     AL4(PHASE LENGTH)                                            *         
* P2     AL4(PHASE LOAD ADDRESS)                                      *         
***********************************************************************         
LOADER   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LR    R3,R1               R3=A(PARAMETER LIST)                         
         SR    R2,R2                                                            
         ICM   R2,7,1(R3)          R2=A(PHASE NAME)                             
         CLC   DELPHASE,4(R3)      TEST FOR DELETE MODE                         
         BE    LOADER04                                                         
         SR    R4,R4                                                            
         ICM   R4,7,5(R3)          R4=A(MOVE TO ADDRESS)                        
         BZ    LOADER02                                                         
         CLI   8(R3),C'M'          TEST FOR MOVE MODE                           
         BE    LOADER02                                                         
         CLI   8(R3),C'A'          TEST FOR ADDR MODE                           
         BE    *+10                                                             
         SR    R4,R4               NO-CLEAR MOVE TO ADDRESS                     
         B     LOADER02                                                         
         AHI   R4,7                ROUND ADDRESS TO DOUBLEWORD BOUNDARY         
         SRL   R4,3                                                             
         SLL   R4,3                                                             
*                                                                               
LOADER02 XC    0(8,R3),0(R3)       CLEAR VALUES FOR ERROR EXIT                  
         LOAD  EPLOC=(2),ERRET=EXIT                                             
         SLL   R1,3                R1=LEN OF MODULE IN BYTES                    
         STCM  R1,7,1(R3)          SET LENGTH FOR USER                          
         STCM  R0,15,4(R3)         SET ENTRY ADDRESS FOR USER                   
         LTR   RE,R4               TEST MOVE TO ADDRESS SET                     
         BZ    LOADERX             EXIT IF NOT ADDR MODE                        
         LR    RF,R1                                                            
         MVCL  RE,R0                                                            
*                                                                               
LOADER04 DELETE EPLOC=(2)          DELETE PHASE                                 
         J     EXIT                                                             
*                                                                               
LOADERX  ICM   RF,15,=V(PHSCNT)    COUNT THE PHASE LOADED                       
         JZ    EXIT                                                             
         GOTOR (RF),PHSPARS,(C'A',(R2)),0                                       
         J     EXIT                                                             
*                                                                               
EXITOK   LTR   R1,R1               SET CC TO EQUAL                              
EXIT     XIT1  ,                   MUST BE HERE FOR LOAD MACRO                  
*                                                                               
DELPHASE DC    X'FFFFFFFF'                                                      
*                                                                               
PHSPARS  DS    F                                                                
         DS    F                                                                
*                                                                               
         LTORG                                                                  
                                                                                
***********************************************************************         
         TITLE 'MASTER CONTROL - REQUEST HANDLING'                              
***********************************************************************         
REQSTART NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         MVI   MCBLOW,C'N'                                                      
*                                                                               
REQSTR00 TM    MCFLAGS,MCFSPOOF+MCFRLST SPOOF PREV CARD READ WAS LAST           
         BNO   *+12                                                             
         NI    MCFLAGS,255-MCFRLST                                              
         B     REQSTR26                                                         
         TM    MCFLAGS,MCFSPOOF+MCFRNXT SPOOF WANTS NEXT REQUEST CARD           
         BO    REQSTR09            READ NEXT REQUEST CARD                       
*                                                                               
REQSTR01 MVI   MCRQNUM,0           SET NUMBER OF CARDS READ                     
         MVI   MCBLOW,C'N'                                                      
         CLI   MCRQEOF,0           TEST E-O-F ON REQUEST FILE                   
         BNE   REQSTR30                                                         
*                                                                               
REQSTR02 CLI   MCDOWNLD,1          TEST IF DOWNLOADING                          
         BNE   REQSTR03                                                         
         GOTOR REQREOPN,1          REOPEN PQ REPORT                             
         B     REQSTR04                                                         
*                                                                               
REQSTR03 EQU   *                   TEST FOR BURSTPQ IN RFHDR ROUTINE            
*                                                                               
REQSTR04 LAY   R2,MCCPUWRK                                                      
         TIMEUSED STORADR=(R2),LINKAGE=SYSTEM,CPU=MIC                           
         LM    RE,RF,0(R2)                                                      
         D     RE,=F'10000'                                                     
         LR    R3,RF               R3=TCB TIME (IN 1/100 SECS)                  
         TIME  BIN                                                              
         LR    R2,R0               R2=TIME NOW (IN 1/100 SECS)                  
*                                                                               
REQSTR06 CP    MCREQNO,=P'0'       FIRST ITEM THROUGH                           
         BE    REQSTR08                                                         
         LR    R0,R2                                                            
         S     R0,MCRUNLPS                                                      
         ST    R0,MCREQLPS                                                      
*                                                                               
         LR    R0,R3               GET CURRENT TCB TIME                         
         S     R0,MCRUNTCB                                                      
         ST    R0,MCREQTCB         SET REQUEST VALUE                            
*                                                                               
         LG    GR1,MCCPUWRK                                                     
         SG    GR1,MCRUNCPU                                                     
         STG   GR1,MCREQCPU        SET REQUEST CPU                              
*                                                                               
         L     R0,LOGORUNL                                                      
         S     R0,MCRUNLIN                                                      
         ST    R0,MCREQLIN                                                      
         MVC   MCRUNLIN,LOGORUNL                                                
*                                                                               
         L     R0,LOGORUNP                                                      
         S     R0,MCRUNPAG                                                      
         ST    R0,MCREQPAG                                                      
         MVC   MCRUNPAG,LOGORUNP                                                
*                                                                               
         L     R0,MCACTIOS                                                      
         S     R0,MCRUNSIO                                                      
         ST    R0,MCREQSIO                                                      
         MVC   MCRUNSIO,MCACTIOS                                                
*                                                                               
REQSTR07 TM    MCFLAGS,MCFIDF      NO SMF RECS IF RUNNING UNDER IDF             
         BO    REQSTR7B                                                         
         TM    MCFLAGS,MCFSPOOF    SPOOF DOESNT DO OLD JOB ACCOUNTING           
         NOP   REQSTR7B            *NOP* BO                                     
         ICM   RF,15,=V(SMFOUT)    OLD JOB ACCOUNTING SMF RECORD                
         BZ    REQSTR7A                                                         
         GOTO1 (RF),MCDUB,F'1',A(MASTC)                                         
         B     REQSTR7B                                                         
REQSTR7A L     R1,=A(MASTC)                                                     
         LHI   R0,-6                                                            
         SVC   247                                                              
REQSTR7B TM    MCFLAGS,MCFSPOOF    TEST SPOOF START REQUEST REACHED             
         BZ    *+14                                                             
         CP    MCREQNO,MCSTART                                                  
         BL    REQSTR7X                                                         
         BRAS  RE,SMFJACC          NEW JOB ACCOUNTING SMF RECORD                
REQSTR7X EQU   *                                                                
*                                                                               
         LHI   R0,MCCARDSH-MCBLOCK CLEAR LEN AND REQUEST CARDS AREA             
         AR    R0,RC                                                            
         LHI   R1,MCCARDSL+2                                                    
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
REQSTR08 ST    R2,MCRUNLPS         SAVE CURRENT TIME                            
         ST    R3,MCRUNTCB         SAVE TCB TIME                                
         LAY   R1,MCRUNCPU                                                      
         LAY   RE,MCCPUWRK                                                      
         MVC   0(8,R1),0(RE)                                                    
*                                                                               
REQSTR09 LA    R2,MCIO             R2=A(REQUEST CARD STACK)                     
         LHI   R4,MCCARDS-MCBLOCK                                               
         AR    R4,RC               R4=A(SAVED REQUEST CARDS)                    
*                                                                               
REQSTR10 CP    MCREQNO,MCEND       TEST END REQUEST REACHED                     
         BE    REQSTR30                                                         
         CLI   MCREQFIL,C'Y'       TEST ALTERNATE SYSIN ASSIGNED                
         BNE   REQSTR16                                                         
         BC    0,REQSTR12          OPEN IS ONE TIME ONLY                        
         OI    *-3,X'F0'                                                        
         OPEN  (REQFILE,INPUT)                                                  
*                                                                               
REQSTR12 GET   REQFILE,(R2)                                                     
         B     REQSTR18                                                         
*                                                                               
REQSTR14 CLOSE REQFILE             THIS IS EOF ON REQFILE LOGIC                 
         MVI   MCRQEOF,1           SET E-O-F ON REQUEST FILE                    
         MVC   0(2,R2),=C'/*'                                                   
         B     REQSTR18                                                         
*                                                                               
REQSTR16 GOTOR MCVCARDS,MCPARA,(R2),=C'RE00'                                    
*                                                                               
REQSTR18 CLC   0(2,R2),=C'/*'      TEST E-O-F ON INPUT FILE                     
         BNE   REQSTR20                                                         
         MVI   MCRQEOF,1           YES-SET E-O-F ENCOUNTERED FLAG               
         TM    MCFLAGS,MCFSPOOF                                                 
         BZ    REQSTR19                                                         
         NI    MCFLAGS,255-MCFRNXT                                              
         B     REQSTR30                                                         
*                                                                               
REQSTR19 CLI   MCRQNUM,0           TEST ANY REQUEST CARDS READ                  
         BNE   REQSTR26            YES-PROCESS REQUEST CARD(S)                  
         B     REQSTR30                                                         
*                                                                               
REQSTR20 CLI   MCSTOP,C'R'         IF STOP AT END OF REQUEST                    
         BE    REQSTR10            READ UNTIL /* ENCOUNTERED                    
*                                                                               
         CLC   0(5,R2),=C'PATCH'   PROCESS PATCH CARD                           
         BNE   *+12                                                             
         GOTOR PATCH                                                            
         B     REQSTR10                                                         
*                                                                               
REQSTR21 CLC   0(6,R2),=C'RFHDR='  PROCESS RFHDR CARD                           
         BNE   REQSTR24                                                         
         GOTOR RFHDR                                                            
         TM    THISFLG,X'02'       TEST IF BURST BECAUSE BURSTPQ=CARD           
         BZ    REQSTR22                                                         
         GOTOR REQREOPN,2                                                       
         B     REQSTR23                                                         
REQSTR22 TM    THISFLG,X'04'       TEST IF BURST ON SECURITY CHANGE             
         BZ    REQSTR23                                                         
         GOTOR REQREOPN,4                                                       
REQSTR23 MVC   LASTREQ,THISREQ     SAVE STATUS OF LAST REQUEST                  
         MVC   LASTFLG,THISFLG                                                  
         B     REQSTR10                                                         
*                                                                               
REQSTR24 CLC   0(4,R2),=C'DUMP'    PROCESS DUMP CARD                            
         BNE   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         SR    R1,R1               R1=NUMBER OF REQUEST CARDS                   
         ICM   R1,1,MCRQNUM                                                     
         BNZ   *+10                                                             
         MVC   MCREQREC,0(R2)      SAVE FIRST CARD IN MCREQREC                  
*                                                                               
         LHI   R4,MCCARDS-MCBLOCK  POSITION TO START OF MCCARDS STACK           
         AR    R4,RC                                                            
         LR    R0,R1                                                            
         MHI   R0,80                                                            
         AR    R4,R0               R4=A(NEXT CARD SLOT IN MCCARDS)              
         MVC   0(80,R4),0(R2)                                                   
         AHI   R0,80                                                            
         LHI   RF,MCCARDSH-MCBLOCK                                              
         AR    RF,RC                                                            
         STH   R0,0(RF)            SET LENGTH OF CARDS IN MCCARDS               
*                                                                               
         TM    MCFLAGS,MCFSPOOF    TEST IF SPOOF                                
         BZ    REQSTR25                                                         
         MVC   MCREQREC,0(R2)      SPOOF ALWAYS GETS LAST CARD READ             
         AHI   R1,1                                                             
         STCM  R1,1,MCRQNUM        SET NUMBER OF REQUEST CARDS READ             
         B     REQSTRX                                                          
*                                                                               
REQSTR25 LA    RE,MCRQCOL1(R1)     POINT TO DISP VALUE FOR THIS CARD            
         AHI   R1,1                                                             
         STCM  R1,1,MCRQNUM        SET NUMBER OF REQUEST CARDS READ             
         ICM   R1,1,MCRQCOL                                                     
         BZ    REQSTR26                                                         
         CLI   MCRQCOL,MCRQCMLT    TEST MULTIPLE DISPLACEMENTS                  
         BNE   *+12                                                             
         ICM   R1,1,0(RE)          YES-GET VALUE FROM LIST                      
         BZ    REQSTR26                                                         
         BCTR  R1,0                                                             
         LA    R1,0(R1,R2)         POINT TO CONTINUATION COLUMN                 
         AHI   R2,L'MCREQREC       BUMP TO NEXT CARD IN STACK                   
         CLC   MCRQCHAR,0(R1)      TEST CONTINUATION FOLLOWS                    
         BE    REQSTR10                                                         
*                                                                               
REQSTR26 AP    MCREQNO,=P'1'       BUMP REQUEST COUNT                           
         CP    MCREQNO,MCSTART     TEST START REQUEST REACHED                   
         BNL   REQSTR27                                                         
         TM    MCFLAGS,MCFSPOOF    EXIT BACK TO SPOOF AT END OF REQUEST         
         BO    REQSTRX                                                          
         MVI   MCRQNUM,0           RESET NUMBER OF CARDS                        
         B     REQSTR09            BACK FOR NEXT REQUEST                        
*                                                                               
REQSTR27 TM    MCFLAGS,MCFIDF      NO SMF RECS IF RUNNING UNDER IDF             
         BO    REQSTR29                                                         
         TM    MCFLAGS,MCFSPOOF    SPOOF DOESNT DO SINGLE REQUEST CARDS         
         NOP   REQSTR29            *NOP* BO                                     
         ICM   RF,15,=V(SMFOUT)    OUTPUT SINGLE REQUEST CARD TO SMF            
         BZ    REQSTR28                                                         
         GOTO1 (RF),MCDUB,F'2',MCREQREC                                         
         B     REQSTR29                                                         
REQSTR28 LA    R1,MCREQREC         POINT TO REQUEST CARD                        
         LHI   R0,-10                                                           
         SVC   247                                                              
*                                                                               
REQSTR29 CLC   MCPROG,MCREQREC                                                  
         BE    REQSTR32                                                         
         CLC   MCREQCOD,MCREQREC                                                
         BE    REQSTR32                                                         
         MVC   MCPROG,MCREQREC                                                  
         CLC   MCPROG,MCSPACES                                                  
         BE    REQSTR00                                                         
         GOTOR REQSTRGA            LOAD APPLICATION PHASES                      
         B     REQSTR32                                                         
*                                                                               
REQSTR30 MVC   MCREQREC,MCSPACES   SET E-O-F FOR CALLER                         
         MVC   MCREQREC(2),=C'/*'                                               
*                                                                               
REQSTR32 ICM   RF,15,=V(DEMVER)                                                 
         JZ    REQSTRX                                                          
         OI    *-3,X'F0'           Only do once                                 
         BASR  RE,RF                                                            
*                                                                               
REQSTRX  MVI   MCBLOW,C' '                                                      
         J     EXIT                                                             
*                                                                               
REQSTRGA NTR1  WORK=(R3,128)       GETAPP USES MCIO FOR CTFILE IO               
         LR    R0,R3               SAVE FIRST 1024 BYTES                        
         LA    RE,MCIO                                                          
         LA    R1,1024                                                          
         LR    RF,R1                                                            
         MVCL  R0,RE               SAVE MCIO                                    
*                                                                               
         GOTOR GETAPP              LOAD APPLICATION PHASES                      
*                                                                               
         LR    RE,R3                                                            
         LA    R0,MCIO                                                          
         LA    R1,1024                                                          
         LR    RF,R1                                                            
         MVCL  R0,RE               RESTORE MCIO                                 
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* HANDLE PATCH CARDS - NTRY R2=A(PATCH CARD)                          *         
***********************************************************************         
PATCH    NTR1                                                                   
         ICM   R1,15,MCVPATCH      TEST IF PATCH POOL AVAILABLE                 
         BZ    PATCH02                                                          
         CLI   0(R1),0             YES-ADD PATCH CARD TO PATCH POOL             
         BE    *+12                                                             
         AHI   R1,L'MCREQREC                                                    
         B     *-12                                                             
         MVC   0(L'MCREQREC,R1),0(R2)                                           
         B     PATCHX                                                           
*                                                                               
PATCH02  GOTOR MCVHEXIN,MCPARA,9(R2),MCDUB,6                                    
         OC    12(4,R1),12(R1)                                                  
         BZ    PATCHERR                                                         
         IC    R1,7(R2)            GET ADDRESS OF PHASE                         
         SLL   R1,28                                                            
         SRL   R1,28                                                            
         LTR   R1,R1               PHASE S/B 1 THROUGH 4                        
         BZ    PATCHERR                                                         
         CHI   R1,4                                                             
         BH    PATCHERR                                                         
         SLL   R1,2                                                             
         LA    R1,MCAPHAS1-L'MCAPHAS1(R1)                                       
         ICM   R3,15,0(R1)         ADDRESS MUST BE NON-ZERO                     
         BZ    PATCHERR                                                         
         SR    R1,R1                                                            
         ICM   R1,7,MCDUB                                                       
         AR    R3,R1               R3=A(PATCH)                                  
*                                                                               
         LA    R1,16(R2)           CALCULATE LENGTH OF PATCH                    
         SR    R0,R0                                                            
PATCH04  CLI   0(R1),C' '                                                       
         BE    PATCH06                                                          
         AHI   R1,1                                                             
         AHI   R0,1                                                             
         B     PATCH04                                                          
*                                                                               
PATCH06  GOTOR MCVHEXIN,MCPARA,16(R2),MCWORK,(R0)                               
         ICM   RF,15,12(R1)                                                     
         BZ    PATCHERR                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     PATCHX                                                           
         MVC   0(0,R3),MCWORK                                                   
*                                                                               
PATCHX   J     EXIT                                                             
*                                                                               
PATCHERR MVC   MCP(L'MCREQREC),0(R2)                                            
         MVC   MCP+L'MCREQREC+2(9),=C'BAD PATCH'                                
         GOTOR MCVPRINT,MCPARA,MCP-1                                            
         ABEND 669                                                              
         EJECT                                                                  
***********************************************************************         
* HANDLE RFHDR CARDS                                                  *         
* IF MULTIPLE REQUESTS IN JOB EOD HAS RESORTED REQUESTS WITH KEY      *         
* XL1=TYPE 0=NORMAL,1=OSID,2=PIN,3=PID/XL4=REQSTR PID/XL4=SEC PIN/PID *         
***********************************************************************         
RFHDR    NTR1                                                                   
         MVC   MCWORK(72),6(R2)                                                 
         CLI   MCWORK+20,C' '      TEST NEW STYLE RFHDR=CARD                    
         BNE   RFHDR7                                                           
         MVC   MCRFHDR(20),MCWORK  FIRST 20 CHRS ARE IN BINARY                  
         MVC   MCHSECI(6),MCWORK+21 SECURITY DATA AND TYPE                      
         CLC   MCHSECI(6),MCSPACES TEST SPACES FILLED HEADER                    
         BNE   *+10                                                             
         XC    MCHSECI(6),MCHSECI                                               
         MVC   MCREQWHY,MCWORK+28  THEN HAVE REQUEST REASON WHY TEXT            
*                                                                               
RFHDR1   XC    THISREQ,THISREQ     EXTRACT PID AND SECURITY DATA                
         MVI   THISFLG,0                                                        
         MVC   THISPID+0(2),MCRHSAGP                                            
         MVC   THISPID+2(2),MCRHPSWD                                            
RFHDR1A  CLI   MCHSECF,MCHFPIN     PIN SECURITY IS SUPPORTED                    
         BNE   RFHDR1B                                                          
         OI    THISFLG,X'10'       SET PIN PROTECTED                            
         B     RFHDR1C                                                          
                                                                                
RFHDR1B  CLI   MCHSECF,MCHFPID     PID SECURITY IS SUPPORTED                    
         BNE   RFHDR2                                                           
         OI    THISFLG,X'20'       SET PID PROTECTED                            
         MVC   THISPID,MCHSECD     SET REQUESTOR PID TO SECURITY PID            
         MVC   MCRHSAGP,THISPID                                                 
         MVC   MCRHPSWD,THISPID+2                                               
RFHDR1C  MVC   THISSEC,MCHSECI     EXTRACT REQUEST SECURITY DATA                
         B     RFHDR3                                                           
*                                                                               
RFHDR2   CLI   MCBURST,1           REGULAR REQUEST WITH NO SECURITY             
         BE    RDHDR4B                                                          
         CLC   THISPID,LASTPID     TEST IF SAME PID AS LAST REQUEST             
         BE    RDHDR4N                                                          
RFHDR2A  OC    LASTPID,LASTPID     TEST IF THERE WAS A LAST PID                 
         BZ    RDHDR4N                                                          
         OI    THISFLG,X'80'       SET REQUESTOR PID HAS CHANGED                
         CLI   MCBURST,2           TEST BURST IF PID CHANGES                    
         BE    RDHDR4B                                                          
         B     RDHDR4N                                                          
*                                                                               
RFHDR3   OI    THISFLG,X'01'       THIS IS A SECURE REQUEST                     
         CLI   MCBURST,1                                                        
         BE    RDHDR4B                                                          
         CLC   THISPID,LASTPID     SECURE REQUEST FOR SAME REQUESTOR            
         BNE   RFHDR3A                                                          
         CLC   LASTSEC,THISSEC     SAME SECURITY AS LAST REQUEST                
         BNE   RDHDR4S                                                          
         B     RDHDR4N                                                          
RFHDR3A  OC    LASTPID,LASTPID     TEST IF THERE WAS A LAST PID                 
         BZ    RDHDR4S                                                          
         OI    THISFLG,X'80'       SET REQUESTOR PID HAS CHANGED                
         B     RDHDR4S                                                          
*                                                                               
RDHDR4B  OI    THISFLG,X'02'       BURST BECAUSE OF PQBURST OPTION              
         B     RDHDR4N                                                          
RDHDR4S  OI    THISFLG,X'04'       BURST BECAUSE SECURITY CHANGED               
RDHDR4N  EQU   *                   DONT WANT TO BURST REQUEST                   
         TM    THISFLG,X'01'       TEST IF THIS IS A SECURE REQUEST             
         BZ    RFHDR5A                                                          
*                                                                               
RFHDR5   ICM   R4,15,MCVREMOT      SET SECURITY FIELDS IN REMOTED               
         BZ    RFHDR8                                                           
         USING REMOTED,R4                                                       
         MVC   REMOTPAS,THISSEC+1  SET PID/PIN IN REMOTED                       
         MVC   REMOTSF1,THISFLG                                                 
         NI    REMOTSF1,X'30'      SET PID OR PIN FLAG                          
         MVI   REMOTSF2,0                                                       
         MVC   MCREMOTE+(REMOTPAS-REMOTED)(L'REMOTPAS),REMOTPAS                 
         MVC   MCREMOTE+(REMOTSF1-REMOTED)(L'REMOTSF1),REMOTSF1                 
         MVC   MCREMOTE+(REMOTSF2-REMOTED)(L'REMOTSF2),REMOTSF2                 
         B     RFHDR6                                                           
RFHDR5A  ICM   R4,15,MCVREMOT      CLEAR SECURITY FIELDS IN REMOTED             
         BZ    RFHDR8                                                           
         XC    REMOTPAS,REMOTPAS                                                
         MVI   REMOTSF1,0                                                       
         MVI   REMOTSF2,0                                                       
         MVC   MCREMOTE+(REMOTPAS-REMOTED)(L'REMOTPAS),REMOTPAS                 
         MVC   MCREMOTE+(REMOTSF1-REMOTED)(L'REMOTSF1),REMOTSF1                 
         MVC   MCREMOTE+(REMOTSF2-REMOTED)(L'REMOTSF2),REMOTSF2                 
         B     RFHDR6                                                           
*                                                                               
RFHDR6   ICM   R1,15,MCVLRMKY      TEST IF WRITING TO PQ                        
         BZ    RFHDR6X                                                          
         OC    0(L'REMOTKEY,R1),0(R1)                                           
         BZ    RFHDR6X                                                          
         CP    MCREQNO,=P'0'       TEST IF FIRST REQUEST                        
         BNE   RFHDR6X                                                          
         ICM   R4,15,MCVREMOT      SET PID/SEC FIELDS FROM REMOTED              
         BZ    RFHDR6X                                                          
         L     RF,REMOTABF         POINT TO PQ BUFFER                           
         BZ    RFHDR6X                                                          
         USING PQRECD,RF                                                        
         TM    PQTYPE,PQTYNEW      TEST OLD/NEW PQ FORMAT                       
         BZ    RFHDR6B                                                          
RFHDR6A  OC    PQPIDNUM,PQPIDNUM   SET PID IF NOT ALREADY SET                   
         BNZ   *+10                                                             
         MVC   PQPIDNUM,THISPID                                                 
         TM    PQATTB,PQATPW       EXIT IF SECURITY ALREADY SET                 
         BO    RFHDR6X                                                          
         MVC   PQPSWD,REMOTPAS     SET REPORT SECURITY DATA                     
         MVC   PQSECF1,REMOTSF1                                                 
         MVC   PQSECF2,REMOTSF2                                                 
         OC    PQPSWD,PQPSWD       TEST IF SECURITY DATA DEFINED                
         BZ    RFHDR6X                                                          
         TM    PQSECF1,PQSIPID+PQSIPIN                                          
         BZ    RFHDR6X                                                          
         OI    PQATTB,PQATPW                                                    
         B     RFHDR6X                                                          
                                                                                
RFHDR6B  OC    OQPIDNUM,OQPIDNUM                                                
         BNZ   *+10                                                             
         MVC   OQPIDNUM,THISPID                                                 
         TM    PQATTB,PQATPW                                                    
         BO    RFHDR6X                                                          
         MVC   OQPSWD,REMOTPAS                                                  
         MVC   OQSECF1,REMOTSF1                                                 
         MVC   OQSECF2,REMOTSF2                                                 
         OC    OQPSWD,OQPSWD       TEST IF SECURITY DATA DEFINED                
         BZ    RFHDR6X                                                          
         TM    OQSECF1,PQSIPID+PQSIPIN                                          
         BZ    RFHDR6X                                                          
         OI    PQATTB,PQATPW                                                    
RFHDR6X  B     RFHDR8                                                           
         DROP  R4,RF                                                            
*                                                                               
RFHDR7   GOTOR MCVHEXIN,MCPARA,MCWORK,MCRFHDR,40                                
         OC    12(4,R1),12(R1)                                                  
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
RFHDR8   L     RE,MCAEXTRA         CLEAR PID                                    
         MVC   MCPID-MCEXTRA(8,RE),MCSPACES                                     
         L     RE,MCASECRT         CLEAR SECRET BLOCK                           
         XC    0(4,RE),0(RE)                                                    
         TM    MCIDOPTS,X'80'      TEST PASSWORD REQUIRED FOR USER-ID           
         BZ    RFHDRX                                                           
         MVC   MCC1ACCS,MCIDACCS   PRESET ID LEVEL LIMIT ACCESS                 
         MVC   MCC2ACCS,MCS2ACCS                                                
         LA    R2,MCIO                                                          
         USING CT0REC,R2           BUILD KEY OF AUTH RECORD                     
         XC    CT0KEY,CT0KEY                                                    
         MVI   CT0KTYP,CT0KTEQU                                                 
         MVC   CT0KAGY,MCIDAGYA                                                 
         OC    MCRHSAGP,MCRHSAGP                                                
         BZ    *+10                                                             
         MVC   CT0KAGY,MCRHSAGP                                                 
         MVC   CT0KNUM,MCRHPSWD                                                 
         OC    CT0KNUM,CT0KNUM     TEST ZERO PASSWORD                           
         BZ    RFHDRX              YES-USE ID LEVEL LIMIT ACCESS                
         GOTOR GETCFR,4                                                         
         BNE   RFHDRX              IF NOT FOUND, USE ID LEVEL(NOT BEST)         
*                                                                               
RFHDR9   OC    MCP1ACCS,MCP1ACCS   SET COMPOSITE LIMIT ACCESS                   
         BZ    *+10                                                             
         MVC   MCC1ACCS,MCP1ACCS                                                
         OC    MCP2ACCS,MCP2ACCS                                                
         BZ    *+10                                                             
         MVC   MCC2ACCS,MCP2ACCS                                                
         OC    MCRHACCS,MCRHACCS   SET COMPOSITE LIMIT ACCESS FROM              
         BZ    *+10                REQUEST HEADER                               
         MVC   MCC1ACCS,MCRHACCS                                                
*                                                                               
RFHDR10  TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         BO    RFHDR11             FIX FASPOON TO TAKE THIS OUT                 
         MVI   MCCSMODE,0          Reset per request                            
         MVIY  MCCSPASS,0          temporary                                    
         L     RF,=V(PRINT00)      Make sure to can print                       
         MVC   0(2,RF),=X'90EC'                                                 
         CLI   MCRHCTRL,1          ComScore?                                    
         BNE   RFHDR11                                                          
         MVI   MCCSMODE,MCCSMOD1   Set for pass one                             
         MVIY  MCCSPASS,MCCSP001   temporary                                    
         MVC   0(2,RF),=X'07FE'    NO-OP PRINTING                               
                                                                                
RFHDR11  L     RE,MCAEXTRA         IF PID DEFINED READ PERSON RECORD            
         USING MCEXTRA,RE                                                       
         MVC   MCCSPASS,MCCSMODE   Temporary code                               
         MVC   MCFNAME,MCSPACES                                                 
         MVC   MCMNAME,MCSPACES                                                 
         MVC   MCLNAME,MCSPACES                                                 
         CLC   MCPID,MCSPACES                                                   
         BE    RFHDRX                                                           
         LA    R2,MCWORK           R2=A(PERSON RECORD KEY)                      
         USING SAPEREC,R2                                                       
         XC    SAPEKEY,SAPEKEY                                                  
         MVI   SAPETYP,SAPETYPQ                                                 
         MVI   SAPESUB,SAPESUBQ                                                 
         MVC   SAPEAGY,MCIDAGYA                                                 
         OC    MCRHSAGP,MCRHSAGP                                                
         BZ    *+10                                                             
         MVC   SAPEAGY,MCRHSAGP                                                 
         MVC   SAPEPID,MCPID                                                    
         GOTOR MCVDTCON,MCPARA,(4,MCDATE),(2,SAPEDEF)                           
         XC    SAPEDEF,=X'FFFF'    COMPLEMENT DATE                              
         LA    R2,MCIO                                                          
         GOTOR MCVDMGR,MCPARA,=C'DMRDHI',=C'CTFILE',MCWORK,(R2)                 
         CLI   8(R1),0                                                          
         BNE   RFHDRX                                                           
         L     RE,MCAEXTRA                                                      
         USING MCEXTRA,RE                                                       
         CLC   MCWORK(SAPEDEF-SAPEKEY),0(R2)                                    
         BNE   RFHDRX                                                           
         MVC   MCWORK,MCSPACES                                                  
         LA    R3,MCIO+(SAPEDATA-SAPEREC)                                       
*                                                                               
RFHDR13  CLI   0(R3),0             TEST E-O-R                                   
         BE    RFHDR23                                                          
         CLI   0(R3),SANAMELQ      TEST NAME ELEMENT                            
         BE    RFHDR17                                                          
         CLI   0(R3),SAPERELQ      TEST PERSONNEL DETAILS                       
         BE    RFHDR21                                                          
RFHDR15  XR    RF,RF                                                            
         IC    RF,1(R3)                                                         
         BXH   R3,RF,RFHDR13                                                    
*                                                                               
         USING SANAMD,R3           R3=A(NAME ELEMENT)                           
RFHDR17  XR    RE,RE                                                            
         LA    RF,SANAMES                                                       
         USING SANAMES,RF          RF=A(NAME SUB ELEMENT)                       
*                                                                               
         IC    RE,SANAMELN         COPY FIRST NAME                              
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   MCWORK+00(0),SANAME                                              
         LA    RF,L'SANAMELN+1(RE,RF)  BUMP RF TO NEXT SUB-ELEMENT              
*                                                                               
         TM    SANAMIND,SANAMIMN   TEST MIDDLE NAME PRESENT                     
         BZ    RFHDR19                                                          
         IC    RE,SANAMELN         COPY MIDDLE NAME                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   MCWORK+20(0),SANAME                                              
         LA    RF,L'SANAMELN+1(RE,RF)  BUMP RF TO NEXT SUB-ELEMENT              
*                                                                               
RFHDR19  IC    RE,SANAMELN         COPY LAST NAME                               
         LHI   R1,L'MCLNAME        SHORTENED TO FIT MCLNAMEL                    
         CR    R1,RE                                                            
         BH    *+6                                                              
         LR    RE,R1                                                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   MCWORK+40(0),SANAME                                              
         B     RFHDR15                                                          
*                                                                               
         USING SAPERD,R3           EXTRACT OFFICE/DEPARTMENT IDS                
RFHDR21  MVC   MCWORK+60(L'SAPEROFF),SAPEROFF                                   
         MVC   MCWORK+80(L'SAPERDID),SAPERDID                                   
         B     RFHDR15                                                          
*                                                                               
RFHDR23  L     RE,MCAEXTRA         FILL IN DATA SAVED FROM RECORD               
         USING MCEXTRA,RE                                                       
         MVC   MCFNAME,MCWORK+00                                                
         MVC   MCMNAME,MCWORK+20                                                
         MVC   MCLNAME,MCWORK+40                                                
*                                                                               
RFHDR30  ICM   RF,15,=V(SECRET)    CALL SECRET IF INCLUDED                      
         BZ    RFHDRX                                                           
         L     R0,MCASECRT                                                      
         XC    MCPARA(24),MCPARA                                                
         GOTOR (RF),MCPARA,(R0)                                                 
         BE    RFHDRX                                                           
         L     RE,MCASECRT                                                      
         XC    0(4,RE),0(RE)                                                    
RFHDRX   J     EXIT                                                             
         DROP  R2,R3,RE,RF                                                      
         LTORG                                                                  
         DS    0D                                                               
         DC    C'THISREQ*'                                                      
THISREQ  DS    0XL10               THIS REQUEST PERSON PLUS SECURITY            
THISPID  DC    XL4'00'                                                          
THISSEC  DC    XL6'00'                                                          
THISFLG  DC    X'00'                                                            
         DC    X'00'                                                            
LASTREQ  DS    0XL10                                                            
LASTPID  DC    XL4'00'                                                          
LASTSEC  DC    XL6'00'                                                          
LASTFLG  DC    X'00'                                                            
         DC    X'00'                                                            
         EJECT                                                                  
***********************************************************************         
* REOPEN THE PQ AT CHANGE OF REQUEST. R1 HAS REASON FOR CHANGE.       *         
* 1=DOWNLOADABLE, 2=BURSTPQ OPTION, 4=SECURITY CHANGE                 *         
* THIS IS DONE BY CHANGING V(LSTRMKEY) IN V(PRINT) TO FORCE CLOSE/OPEN*         
* THE PID IS NOT IN DDREMOTED BUT IS EXTRACTED BY DMPRTQ FROM RFHDR   *         
***********************************************************************         
REQREOPN NTR1                                                                   
         STC   R1,REQROPTH         SAVE REASON WHY CALLED                       
         ICM   R1,15,MCVLRMKY      TEST IF WRITING TO PQ                        
         BZ    REQROPX                                                          
         OC    0(L'REMOTKEY,R1),0(R1)                                           
         BZ    REQROPX                                                          
         CLI   REQROPTH,1          TEST IF DOWN LOADABLE REPORT                 
         BE    REQROP9                                                          
         LA    RF,REQTABB                                                       
*                                                                               
REQROP2  CLI   0(RF),C' '          SEARCH TABLE OF NON-BURST REQUESTS           
         BE    REQROP3                                                          
         CLC   MCSYSTEM(4),0(RF)   IF PROGRAM IN TABLE CANT BURST               
         BE    REQROPX                                                          
         LA    RF,L'REQTABB(RF)                                                 
         B     REQROP2                                                          
*                                                                               
REQROP3  CP    MCREQNO,=P'0'       EXIT IF FIRST REQUEST                        
         BE    REQROPX                                                          
*                                                                               
REQROP9  ICM   R1,15,MCVLRMKY      CHANGE LAST PQ KEY TO FORCE RE-OPEN          
         MVC   0(6,R1),=C'REOPEN'                                               
         MVC   6(1,R1),REQROPTH    PASS RE-OPEN REASON                          
         MVI   7(R1),0                                                          
         LA    RF,THISREQ          PASS THIS REQUEST DATA                       
         ST    RF,8(R1)                                                         
*                                                                               
REQROPX  MVC   REQROPLA,REQROPTH   MOVE THIS CALL REASON CODE TO LAST           
         XIT1                                                                   
         LTORG                                                                  
REQROPTH DC    X'00'                                                            
REQROPLA DC    X'00'                                                            
         DS    0F                                                               
REQTABB  DS    0CL4                REQUESTS THAT ARE NOT TO BE BURST            
*&&UK*&& DC    C'ACDC'                                                          
*&&UK*&& DC    C'ACD0'                                                          
*&&UK*&& DC    C'ACHP'                                                          
*&&UK*&& DC    C'AC12'                                                          
*&&UK*&& DC    C'AC5P'                                                          
         DC    C'AC55'                                                          
*&&UK*&& DC    C'MEBA'                                                          
*&&UK*&& DC    C'ME09'                                                          
*&&UK*&& DC    C'ME12'                                                          
         DC    C'    '                                                          
         DC    C'    '                                                          
         DC    C'    '                                                          
         DC    C'    '                                                          
REQTABBX DC    C'    '             END OF TABLE                                 
*                                                                               
REQFILE  DCB   DDNAME=REQFILE,MACRF=(GM),DSORG=PS,RECFM=FB,            X        
               EODAD=REQSTR14,LRECL=80                                          
                                                                                
         TITLE 'COMSCORE JOB REGESTIR HANDLING'                                 
***********************************************************************         
* THIS IS CALLED TO REGISTER A JOB INTO THE TABS DATA SPACE COMJOBS             
* IT IS CALLED FROM SPOOF AND SPONSOR ONLY AND IS FOR COMSCORE REQUEST          
***********************************************************************         
         DS    0H                                                               
JOBREG   NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    EXITOK                                                           
         L     R2,MCAEXTRA                                                      
         USING MCEXTRA,R2                                                       
         CLI   MCCSRSP,CBJTOP      OPERATOR HANDLES RESPONSE?                   
         BNE   *+8                 NO                                           
         MVI   JBCOMND,CBJTOP      YES, SET TO OPERATOR THEN                    
         GOTOR =V(CBJOB),JOBPARM,('CBJREG',0),(JBCOMND,MCCSWAIT),0              
*TEMP    CLI   JOBPARM+4,0                                                      
         J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
JBCOMND  DC    AL1(CBJTRE)         DEFAULT PROGRAM AUTO RESPONSE                
         LTORG                                                                  
                                                                                
         TITLE 'COMSCORE JOB PROCESS REQUEST HANDLING'                          
***********************************************************************         
* THIS IS CALLED TO PROCESS A REQUEST FOR COMSCORE BATCH JOBS                   
***********************************************************************         
         DS    0H                                                               
JCREQSTR NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    EXITOK              IF SOON GO AWAY                              
         CLI   MCCSMODE,MCCSMOD1   IS IT COMSCORE PASS=1 ?                      
         JL    EXITOK              NO, SO GO AWAY                               
         JH    *+2                 WRONG PASS NUMBER                            
         L     R2,MCAEXTRA                                                      
         USING MCEXTRA,R2                                                       
         ZAP   MCDUB,MCREQNO                 REQUEST NUMBER                     
         OI    MCDUB+L'MCDUB-1,X'0F'                                            
         UNPK  MCMVSDSN+32(3),MCDUB                                             
         MVC   MCMVSDSN+36(3),=C'REQ'        RESET EXTENTION ON FILE            
         GOTOR =V(CBJOB),JOBPARM,('CBJNREQ',MCMVSDSN),0,0                       
         CLI   JOBPARM+4,0                                                      
         J     EXIT                                                             
         DROP  R2                                                               
         LTORG                                                                  
                                                                                
         TITLE 'COMSCORE REQUEST END HANDLING'                                  
***********************************************************************         
* THIS IS CALLED TO RESET FOR NEXT REQUEST                                      
***********************************************************************         
         DS    0H                                                               
JCREQEND NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    EXITOK                                                           
         CLI   MCCSMODE,MCCSMOD1   IS IT COMSCORE PASS=2 ?                      
         JL    EXITOK              NO, SO GO AWAY                               
         JE    *+2                 WRONG PASS NUMBER                            
         L     RF,=V(PRINT00)      TURN ON PRINTING AGAIN                       
         MVC   0(2,RF),=X'07FE'    NO-OP PRINTING                               
         MVI   MCCSMODE,MCCSMOD1   SET TO PASS=1 TO PROCESS NEXT REQ.           
         L     R2,MCAEXTRA         Temporary code                               
         USING MCEXTRA,R2          Temporary code                               
         MVC   MCCSPASS,MCCSMODE   Temporary code                               
         DROP  R2                  Temporary code                               
         MVI   COMSRTRY,0          RESET RETRY                                  
         CLI   JOBPARM+4,0                                                      
         J     EXIT                                                             
                                                                                
         TITLE 'COMSCORE JOB WAIT FOR RESPONSE FROM COMSCORE'                   
***********************************************************************         
* THIS IS CALLED TO PUT THE JOB IN A WAIT STATE FOR THIRD PART                  
* REPSONSE WITH COMSCORE DATA.                                                  
***********************************************************************         
         DS    0H                                                               
JOBWAIT  NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    EXITOK                                                           
         CLI   MCCSMODE,MCCSMOD1   IS IT COMSCORE PASS=1 ?                      
         JL    EXITOK              NO, SO GO AWAY                               
         JH    *+2                 WRONG PASS NUMBER                            
*                                                                               
         L     R2,MCAEXTRA                                                      
         USING MCEXTRA,R2                                                       
JOBWAIT1 GOTOR =V(CBJOB),JOBPARM,('CBJWAIT',MCMVSDSN),0,0                       
         CLI   4(R1),0             ANY RESPONSE OR ERROR?                       
         BE    JOBWAIT9                                                         
         CLI   4(R1),CBRC03        RETRUN CALL (TIMER EXPIRED)                  
         JNE   *+2                                                              
         LLC   RF,COMSRTRY         COUNT RE-TRY                                 
         AHI   RF,1                                                             
         STC   RF,COMSRTRY                                                      
         CLC   COMSRTRY,MCCSRTRY   MAX RETRIES                                  
         BH    JOBWAIT2            Done waiting. What next                      
         WTO   'Timed out but will auto wait'                                   
         B     JOBWAIT1            LOOP TO WAIT AGAIN                           
*                                                                               
JOBWAIT2 CLI   MCCSRSP,C'C'        CANCEL JOB ON TIME OUT                       
         JE    *+2                                                              
         CLI   MCCSRSP,C'I'        IGNORE OR TURN OFF COMSCORE INFO             
         JNE   *+2                 SAY WHAT OPTION IS THIS?                     
         MVI   MCCSMODE,0          TURN OFF THE PROCESS                         
         MVI   MCCSPASS,0          Temporary code                               
         MVI   JOBPARM+4,0         FUDGE FACTOR ON EXIT                         
         WTO   '<0882> ComScore Turned off for job'                             
         L     RF,=V(PRINT00)      TURN ON PRINTING AGAIN                       
         MVC   0(2,RF),=X'90EC'                                                 
         J     EXIT                                                             
*                                                                               
*OBWAIT6 CLI   4(R1),CBRC03                                                     
*        JNE   *+2                                                              
*                                                                               
JOBWAIT9 L     RF,=V(PRINT00)      TURN ON PRINTING AGAIN                       
         MVC   0(2,RF),=X'90EC'                                                 
         MVI   MCCSMODE,MCCSMOD2   SET TO PASS TWO                              
         MVC   MCCSPASS,MCCSMODE   Temporary code                               
         CLI   JOBPARM+4,0                                                      
         J     EXIT                CC PASSED BACK TO CALLER                     
         DROP  R2                                                               
*                                                                               
COMSRTRY DS    AL1                                                              
         LTORG                                                                  
                                                                                
         TITLE 'COMSCORE JOB RAN TO COMPLETION'                                 
***********************************************************************         
* SET FOR CLEAN UP AND LET MARK THE ENTRY IN THE DATA SPACE AS COMPLETE         
***********************************************************************         
         DS    0H                                                               
JOBDONE  NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    EXITOK                                                           
         L     R9,MCVLOGOC                                                      
         L     RF,=V(PRINT00)      TURN ON PRINTING AGAIN                       
         MVC   0(2,RF),=X'90EC'                                                 
         GOTOR =V(CBJOB),JOBPARM,('CBJCOMP',0),0,0                              
         CLI   JOBPARM+4,0                                                      
         J     EXIT                                                             
*____________________________________________________________________           
* USE JOBPARM FOR ALL CALLS EXCEPT JOBERR. ELSE YOU WILL LOSE THE               
* THE ERROR VALUE WHEN THE JOB DUMPS.                                           
*____________________________________________________________________           
         DS    0F                                                               
         DC    C'>>>>'                                                          
JOBPARM  DC    6F'0'                                                            
         DC    C'<<<<'                                                          
*                                                                               
         LTORG                                                                  
                                                                                
         TITLE 'COMSCORE JOB REPORTED ERROR WHILE RUNNING'                      
***********************************************************************         
* MARK JOB IN ERROR IN DATASPACE                                                
***********************************************************************         
         DS    0H                                                               
JOBERR   NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         TM    MCFLAGS2,MCISSOON   SOON JOB ?                                   
         JO    EXITOK                                                           
         CLI   MCCSMODE,MCCSMOD1   IS IT COMSCORE PASS=2 ?                      
         JL    EXITOK              NO, SO GO AWAY                               
         GOTOR =V(CBJOB),MCPARA,('CBJERR',0),0,0                                
         J     EXIT                                                             
         LTORG                                                                  
                                                                                
         TITLE 'MASTER CONTROL - INTERCEPT PROGRAM CHECKS AND LOOPS'            
***********************************************************************         
* PROGRAM CHECK USED FOR ABENDS (ESTAE) OR LOOPS                                
***********************************************************************         
         DS    0H                                                               
         USING *,RF                                                             
PRGCHK   CHI   R0,12               TEST SDWA ACQUIRED                           
         BNE   *+8                 NO-JUST LET MVS PERC                         
         SR    RF,RF                                                            
         BR    RE                                                               
*                                                                               
         L     RC,=A(MASTC)                                                     
         STM   RE,RC,12(RD)        SAVE CALLING REGS                            
         ST    RD,ESTAERD          AND POINTER                                  
         ST    R1,ESTAER1          AND POINTER TO SDWA                          
*                                                                               
         USING SDWA,R1                                                          
         MVC   MCUSREGS+0(08),SDWAEC1  PSW AT ABEND                             
         MVC   MCINTCD,SDWAICD1        SAVE INTERRUPT CODE                      
         MVC   MCUSREGS+8(64),SDWAGRSV R0-RF AT ABEND                           
*                                                                               
PRGCHK02 MVI   MCREASON,C'P'                                                    
         OC    MCCHKPSW,MCCHKPSW   TEST INST ADDRESS SAVED BY LOOP RTN          
         BZ    PROGCHKX                                                         
         SR    RE,RE                                                            
         ICM   RE,7,MCCHKPSW+1                                                  
         MVC   0(1,RE),MCCHKPSW    RESTORE OP CODE                              
         XC    MCCHKPSW,MCCHKPSW                                                
         MVI   MCREASON,C'L'                                                    
         MVI   MCINTCD,FF          SET LOOP INDICATOR                           
*                                                                               
PROGCHKX GOTOR CHKALL                                                           
         DROP  RF                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
CHKALL   BASR  RB,0                                                             
         SHI   RB,2                                                             
         USING CHKALL,RB                                                        
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         L     RD,=A(COMWORK)                                                   
         MVI   MCPOPSW,FF          INHIBIT TIMER INTERRUPTS                     
         TTIMER CANCEL                                                          
*                                                                               
         CLI   MCBLOW,C'P'                                                      
         BE    CHKALL04                                                         
         CLI   MCBLOW,C'Q'         STOP LOOP OF REQLAST/RUNLAST DUMPS           
         BNE   CHKALL06                                                         
*                                                                               
CHKALL02 MVI   MCBLOW,C'P'                                                      
         LA    R1,MCPARA                                                        
         MVC   4(8,R1),=C'CLO/ERR='                                             
         MVC   12(1,R1),MCCLOERR   SET FIRST BYTE OF RETURN CODE                
         LA    RE,MCPARA+4                                                      
         ST    RE,0(R1)                                                         
         MVI   0(R1),7             DDPRINT REQUIRES THIS (L'CLO/ERR)            
         GOTOR MCVPRINT                                                         
*                                                                               
CHKALL04 L     R1,ESTAER1                                                       
         SETRP DUMP=NO,RC=0,COMPCOD=(665,USER)                                  
         GOTOR JOBERR                                                           
         B     CHKALLX                                                          
*                                                                               
CHKALL06 CLC   MCUSREGS+5(3),MCLSTPSW+1                                         
         BE    CHKALL16            AVOID MULTIPLE DUMPS                         
         MVC   MCLSTPSW,MCUSREGS+4                                              
         AP    MCNDUMPS,=P'1'      INCREMENT DUMP COUNT                         
         L     R1,ESTAER1                                                       
*                                                                               
         USING SDWA,R1                                                          
         CLC   ABEND666,SDWACMPC   TEST STOP NOW (OPERATOR COMMAND)             
         BE    CHKALL18                                                         
         CLC   ABEND667,SDWACMPC   TEST RUN-TIME PARAMETER EXCEEDED             
         BNE   CHKALL08                                                         
         TM    MCPRTIND,MCPRTXFI   TEST XFILE RUN                               
         BO    CHKALL08                                                         
         OC    MCREMPQK,MCREMPQK   DONT BOTHER DUMPING SOON REQUESTS            
         BNZ   CHKALL18                                                         
CHKALL08 CLC   ABEND669,SDWACMPC   TEST BAD PATCH CARD                          
         BE    CHKALL18                                                         
         CLC   ABEND670,SDWACMPC   TEST BAD CPUID OR CONCURRENT UPDATE          
         BE    CHKALL18                                                         
         CLC   OPERCANC,SDWACMPC   TEST OPERATOR CANCEL                         
         BE    CHKALL46                                                         
         CLC   ABEND13E,SDWACMPC   TEST DETACHED FROM ABOVE(MONSOON)            
         BE    CHKALL48                                                         
         CLC   ABEND899,SDWACMPC   TEST FOR THE FREDDIE ROE SPECIAL             
         BE    CHKALL46                                                         
         ICM   R1,7,SDWACMPC       HANG ON TO COMPLETION CODE                   
         DROP  R1                                                               
*                                                                               
         ICM   R0,15,=V(SPOOF)     ESTABLISH START OF DUMP                      
         BNZ   *+8                                                              
         L     R0,=A(RUNSTART)                                                  
         CLI   MCDUMP,C'Y'                                                      
         BE    CHKALL10                                                         
         CLI   MCDUMP,C'O'         TEST FULL OS DUMP REQUIRED                   
         BE    CHKALL46                                                         
         ICM   RF,15,MCAWORK       POINT TO START OF DUMP                       
         BZ    *+6                                                              
         LR    R0,RF                                                            
*                                                                               
CHKALL10 L     RF,MCDMPEND         GET END OF REGION ADDRESS                    
         MVI   MCPOPSW,X'FE'       TIMER RESTART INVALID                        
         ICM   RE,15,MCBXAREA                                                   
         BZ    *+8                                                              
         MVI   BOXOFF-BOXD(RE),C'Y'                                             
         ICM   R3,15,MCNXTPGS      SAVE PAGE LIMIT                              
         XC    MCNXTPGS,MCNXTPGS   NO INTERRUPT FOR DUMP                        
         XC    MCNXTTCB,MCNXTTCB   LET'S NOT HAVE THIS AGAIN EITHER             
*                                                                               
         TM    MCPRTIND,MCPRTXFI   TEST XFILE                                   
         BO    CHKALL12                                                         
         OC    MCREMPQK,MCREMPQK   TEST 'SOON'                                  
         BZ    CHKALL12                                                         
         ICM   RE,15,=V(PDMPSOON)  TEST PDUMPER AVAILABLE                       
         BZ    CHKALL12                                                         
         MVC   0(8,RE),MCUSERID    FIRST 8 CHARACTERS OF USERID                 
         MVC   8(5,RE),MCREMPQK+2  REPORT ID AND REPORT NUMBER                  
         MVC   13(2,RE),MCPROG     PROGRAM ID                                   
         STCM  R1,7,15(RE)         COMPLETION CODE (X'SSSUUU')                  
         MVC   18(2,RE),MCDESTID   USERID #                                     
         MVC   21(1,RE),MCOVSYS    SYSTEM ID                                    
*                                                                               
CHKALL12 MVC   SVREMPQK,MCREMPQK   SAVE 'SOON' JOB ID                           
         XC    MCREMPQK,MCREMPQK   AND CLEAR DURING PDUMP                       
*                                                                               
         OC    MCVMDUMP,MCVMDUMP   DO WE HAVE MDUMPER                           
         JZ    CHKALL13                                                         
         GOTOR MCVMDUMP,MCPARA,=C'DUMP',0                                       
         J     CHKALL14                                                         
*                                                                               
CHKALL13 TM    MCRFLAG1,MCRDUMP                                                 
         BO    CHKALL14                                                         
         GOTOR MCVPDUMP,MCPARA,MCUSREGS,(R0),(RF)                               
*                                                                               
         OC    MCUSRDMP+0(4),MCUSRDMP+0 TEST ANY USER DUMP AREAS                
         BZ    CHKALL14                                                         
         ICM   R0,15,MCUSRDMP+00                                                
         ICM   RF,15,MCUSRDMP+04                                                
         GOTOR MCVPDUMP,(R1),,(R0),(RF)                                         
*                                                                               
         OC    MCUSRDMP+8(4),MCUSRDMP+8 TEST SECOND USER DUMP AREA              
         BZ    CHKALL14                                                         
         ICM   R0,15,MCUSRDMP+08                                                
         ICM   RF,15,MCUSRDMP+12                                                
         GOTOR MCVPDUMP,(R1),,(R0),(RF)                                         
*                                                                               
CHKALL14 MVC   MCREMPQK,SVREMPQK   RESTORE 'SOON' ID                            
         C     R3,LOGORUNP         IF PAGES PRINTED IS MORE THAN LIMIT          
         BNL   *+8                                                              
         A     R3,LOGORUNP                                                      
         STCM  R3,15,MCNXTPGS      RESTORE PAGE LIMIT                           
         ICM   R1,15,MCBXAREA                                                   
         BZ    *+8                                                              
         MVI   BOXOFF-BOXD(R1),0                                                
         MVI   MCPOPSW,FF          ALLOW TIMER RESTART NEXT VALID               
*                                                                               
CHKALL16 L     R1,ESTAER1          TEST ANY NON-ZERO ABEND CODES                
         USING SDWA,R1                                                          
         ICM   RF,7,SDWACMPC       GET SYST AND USER                            
         SLL   RF,20               SHIFT OUT ALL BUT USER 12 BITS               
         LTR   RF,RF               SEE IF USER CODE NON-ZERO                    
         BNZ   CHKALL18            GO DIRECTLY TO TERMINATION                   
         CLC   OPERCANC,SDWACMPC   TEST OPERATOR CANCEL                         
         BE    CHKALL18                                                         
         CLC   ABEND13E,SDWACMPC   TEST DETACH BY MONSOON                       
         BE    CHKALL48                                                         
         B     CHKALL20                                                         
         DROP  R1                                                               
*                                                                               
CHKALL18 LA    R1,MCPARA                                                        
         MVC   4(8,R1),=C'CLO/ERR='                                             
         MVC   12(1,R1),MCCLOERR   MOVE RETURN CODE                             
         CLI   12(R1),0            WAS THE CODE SET PREVIOUSLY?                 
         BNE   *+8                 YES-LEAVE IT ALONE                           
         MVI   12(R1),C'C'         SET OPERATOR CANCEL                          
         LA    RE,MCPARA+4                                                      
         ST    RE,0(R1)                                                         
         MVI   0(R1),7             DDPRINT REQUIRES THIS (L'CLO/ERR)            
         GOTOR MCVPRINT                                                         
         GOTOR JOBERR                                                           
         L     R1,ESTAER1                                                       
*                                                                               
         SETRP DUMP=NO,RC=0                                                     
         B     CHKALLX                                                          
         EJECT                                                                  
***********************************************************************         
* PROGRAM CHECKS AND LOOPS - OPERATOR INFO AND OPTIONS                *         
***********************************************************************         
CHKALL20 EDIT  MCREQNO,(4,PCMA+3),DUB=MCDUB,WRK=MCWORK                          
         CLI   PCMA+3,C'0'                                                      
         BNE   *+8                                                              
         MVI   PCMA+3,C' '                                                      
         LA    R1,0(R1)                                                         
         MVC   PCMA+20(15),=CL15'UNKNOWN-SEE PSW'                               
         C     R1,MCDMPEND         COMPARE TO CURR TOP OF REGION                
         BH    CHKALL22                                                         
         L     RF,X'21C'(,R0)      GET PSATOLD (CURR TCB ADDRESS)               
         C     R1,X'70'(RF)        GET TCBFSA (FIRST SAVE AREA)                 
         BL    CHKALL22                                                         
         MVC   PCMA+28(3),=CL3' + '                                             
         MVC   PCMA+20(8),22(R2)   SHOW NMOD NAME AND DISPLACEMENT              
         SR    R1,R2                                                            
         ST    R1,MCDUB                                                         
         GOTOR MCVHEXOU,MCPARA,MCDUB+2,PCMA+31,2,=C'TOG'                        
*                                                                               
CHKALL22 CP    MCNREQS,=P'0'                                                    
         BE    CHKALL24                                                         
         EDIT  (P3,MCNREQS),(3,PCMA+8),ALIGN=LEFT,DUB=MCDUB,WRK=MCWORK          
*                                                                               
CHKALL24 EDIT  (P3,MCNDUMPS),(3,PCMB),ALIGN=LEFT,DUB=MCDUB,WRK=MCWORK           
*                                                                               
CHKALL26 MVC   PCMB+17(L'CANCAPL),CANCAPL                                       
         MVI   MCCLOERR,C'L'       SET LOOP                                     
         CLI   MCREASON,C'L'                                                    
         BE    CHKALL30                                                         
         MVI   MCCLOERR,C'Q'       SET PRINTQ ERROR                             
         MVC   PCMB+17(L'CANCPQE),CANCPQE                                       
         L     R1,MCUSREGS+4                                                    
         CLC   0(10,R1),=C'PRINTQ FUL'                                          
         BE    CHKALL30                                                         
*                                                                               
         LA    RF,CANCTAB          LOOK-UP PROGRAM CHECK TYPE                   
CHKALL28 CLI   0(RF),0                                                          
         BE    *+10                                                             
         CLC   0(1,RF),MCINTCD                                                  
         BE    *+12                                                             
         AHI   RF,L'CANCTAB                                                     
         B     CHKALL28                                                         
         MVC   PCMB+17(L'CANCTAB-1),1(RF)                                       
         MVI   MCCLOERR,C'A'       SET ABEND                                    
*                                                                               
CHKALL30 MVC   MCWORK,MCSPACES                                                  
CHKALL32 LA    R3,LPCMA+10         STD LEN IF NO SQUASHER                       
         MVC   MCWORK+10(LPCMA),PCMA MOVE FOR SQUASHER                          
         ICM   RF,15,MCVSQSHR                                                   
         BZ    CHKALL34                                                         
         GOTOR (RF),MCPARA,MCWORK,LPCMA+10                                      
         L     R3,MCPARA+4         GET LENGTH AFTER SQUASHER                    
CHKALL34 CLI   MCREASON,C'L'       TEST TERMINATING LOOP                        
         BE    CHKALL38            YES-OPERATOR GETS NO CHOICE                  
*                                                                               
         LA    R4,ATIOT                                                         
         EXTRACT (R4),'S',FIELDS=(TIOT)                                         
         L     R4,ATIOT                                                         
         MVC   WTORNAME+00(8),00(R4) JOB NAME                                   
         MVC   WTORNAME+09(8),08(R4) PROC NAME                                  
         MVC   WTORNAME+18(8),16(R4) STEP NAME                                  
*                                                                               
         LA    R1,PCREPLY          ADDR OF REPLY AREA                           
         ST    R1,WTORPARM         STORE IN APRM LIST                           
         MVC   PCREPLY,MCSPACES                                                 
         MVI   WTORPARM,L'PCREPLY  RESTORE LENGTH                               
         LA    R1,WTORPARM         PARM LIST ADDR                               
         AHI   R3,L'WTORNAME+4                                                  
         STH   R3,WTORMLEN                                                      
         XC    WTORECB,WTORECB                                                  
         CLI   MCTSTRUN,FF         IF RUN=TEST                                  
         BNE   CHKALL36            NO REPLY WANTED                              
         LH    R0,WTORMLEN                                                      
         SHI   R0,L'PCMC           DROP REPLY PART OF MESSAGE                   
         STH   R0,WTORMLEN                                                      
         LA    R1,WTORPARM+8       GET NO REPLY                                 
*                                                                               
CHKALL36 SVC   35                  ISSUE WTOR                                   
         CLI   MCTSTRUN,FF         IF RUN=TEST                                  
         BE    CHKALL38            ALWAYS STOP                                  
         LA    R1,WTORECB                                                       
         WAIT  ECB=(1)                                                          
         CLC   PCREPLY(4),=C'STOP'                                              
         BE    CHKALL38                                                         
         CLC   PCREPLY(4),=C'NEXT'                                              
         BE    CHKALL40                                                         
         MVC   MCWORK,MCSPACES                                                  
         MVC   MCWORK(10),=CL10'BAD REPLY-'                                     
         B     CHKALL32                                                         
CHKALL38 MVC   MCUSREGS+8(64),MCRUNLST                                          
         B     CHKALL42                                                         
*                                                                               
CHKALL40 MVC   MCUSREGS+8(64),MCREQLST                                          
*                                                                               
CHKALL42 MVI   MCBLOW,C'Q'                                                      
         OC    MCUSREGS+8(64),MCUSREGS+8 TEST IF REGS INITIALIZED               
         BZ    CHKALL46            NO-NO RETRY                                  
         L     R1,ESTAER1          RESTORE SDWA POINTER                         
         USING SDWA,R1                                                          
         TM    SDWAERRD,SDWACLUP   IS RETRY ALLOWED?                            
         BO    CHKALL46            NO                                           
         DROP  R1                                                               
*                                                                               
         OC    MCRTPCPU,MCRTPCPU   TEST TO RESET LOOP TIMER                     
         BZ    CHKALL44                                                         
         CP    MCREQNO,=P'0'                                                    
         BE    CHKALL44                                                         
         LA    R2,MCRTPCPU                                                      
         L     R3,=A(ADPOP)        TIMER ROUTINE                                
         STIMER TASK,(R3),BINTVL=(R2)                                           
*                                                                               
CHKALL44 L     R3,=A(RETRY)        SET RETRY ROUTINE ADDRESS                    
         L     R1,ESTAER1          RESTORE SDWA POINTER                         
         USING SDWA,R1                                                          
         SETRP DUMP=YES,RC=4,RETADDR=(3),FRESDWA=YES                            
         B     CHKALLX                                                          
*                                                                               
CHKALL46 LA    R1,MCPARA                                                        
         MVC   4(8,R1),=C'CLO/ERR='                                             
         MVC   12(1,R1),MCCLOERR    MOVE RETURN CODE                            
         LA    RE,MCPARA+4                                                      
         ST    RE,0(R1)                                                         
         MVI   0(R1),7              DDPRINT REQUIRES THIS (L'CLO/ERR)           
         GOTOR MCVPRINT                                                         
         GOTOR JOBERR                                                           
         L     R1,ESTAER1                                                       
         SETRP DUMP=YES,RC=0                                                    
         B     CHKALLX                                                          
*                                                                               
CHKALL48 LAY   R2,MCCPUWRK                                                      
         TIMEUSED STORADR=(R2),LINKAGE=SYSTEM,CPU=MIC                           
         LM    RE,RF,0(R2)                                                      
         D     RE,=F'10000'                                                     
         LR    R3,RF               R3=TCB TIME (IN 1/100 SECS)                  
*                                                                               
         TIME  BIN                                                              
         LR    R2,R0               R2=TIME NOW (IN 1/100 SEC)                   
         S     R0,MCRUNLPS         ELAPSED TIME                                 
         ST    R0,MCREQLPS                                                      
*                                                                               
         LR    R0,R3                                                            
         S     R0,MCRUNTCB                                                      
         ST    R0,MCREQTCB         SET REQUEST VALUE                            
*                                                                               
         LG    GR1,MCCPUWRK                                                     
         SG    GR1,MCRUNCPU                                                     
         STG   GR1,MCREQCPU        SET REQUEST CPU                              
*                                                                               
         L     R0,LOGORUNL                                                      
         S     R0,MCRUNLIN                                                      
         ST    R0,MCREQLIN                                                      
         MVC   MCRUNLIN,LOGORUNL                                                
*                                                                               
         L     R0,LOGORUNP                                                      
         S     R0,MCRUNPAG                                                      
         ST    R0,MCREQPAG                                                      
         MVC   MCRUNPAG,LOGORUNP                                                
*                                                                               
         L     R0,MCACTIOS                                                      
         S     R0,MCRUNSIO                                                      
         ST    R0,MCREQSIO                                                      
         MVC   MCRUNSIO,MCACTIOS                                                
*                                                                               
CHKALL50 TM    MCFLAGS,MCFIDF      NO SMF RECS IF RUNNING UNDER IDF             
         BO    CHKALL56                                                         
         TM    MCFLAGS,MCFSPOOF    SPOOF DOESNT DO OLD JOB ACCOUNTING           
         NOP   CHKALL54            *NOP* BO                                     
         ICM   RF,15,=V(SMFOUT)    OLD JOB ACCOUNTING SMF RECORD                
         BZ    CHKALL52                                                         
         GOTO1 (RF),MCDUB,F'1',A(MASTC)                                         
         B     CHKALL54                                                         
CHKALL52 L     R1,=A(MASTC)                                                     
         LHI   R0,-6                                                            
         SVC   247                                                              
CHKALL54 BRAS  RE,SMFJACC          NEW JOB ACCOUNTING SMF RECORD                
*                                                                               
CHKALL56 L     R1,ESTAER1                                                       
         SETRP DUMP=YES,RC=0       WE'RE DONE GO BACK TO RTM                    
         GOTOR JOBERR                                                           
*                                                                               
CHKALLX  L     RD,ESTAERD                                                       
         LM    RE,RC,12(RD)                                                     
         BR    RE                                                               
*                                                                               
         USING *,RF                                                             
RETRY    L     RC,=A(MASTC)                                                     
         LM    R0,RF,MCUSREGS+8                                                 
         BR    RE                                                               
         DROP  RF                                                               
         EJECT                                                                  
SVREMPQK DS    CL8                                                              
*                                                                               
         LTORG                                                                  
*                                                                               
VPDMPSOO DC    V(PDMPSOON)                                                      
ABEND666 DC    AL3(666)                                                         
ABEND667 DC    AL3(667)                                                         
ABEND669 DC    AL3(669)                                                         
ABEND670 DC    AL3(670)                                                         
ABEND899 DC    AL3(899)                                                         
OPERCANC DC    X'222000'                                                        
ABEND13E DC    X'13E000'           TASK BEING DETACHED (BY MONSOON)             
*                                                                               
CANCTAB  DS    0CL21                                                            
         DC    AL1(01),CL20'OPERATION EXCEPTION'                                
         DC    AL1(02),CL20'PRIVL OPER EXCEPTION'                               
         DC    AL1(03),CL20'EXECUTE EXCEPTION'                                  
         DC    AL1(04),CL20'PROTECTION EXCEPTION'                               
         DC    AL1(05),CL20'ADDRESSING EXCEPTION'                               
         DC    AL1(06),CL20'SPECIFICATION EXCEPT'                               
         DC    AL1(07),CL20'DATA EXCEPTION'                                     
         DC    AL1(00),CL20'UNKNOWN INTERRUPTION'                               
CANCPQE  DC    CL20'*PRINT QUEUE ERROR*'                                        
CANCAPL  DC    CL20'LOOP'                                                       
*                                                                               
ATIOT    DC    F'0'                                                             
ESTAERD  DC    F'0'                                                             
ESTAER1  DC    F'0'                                                             
WTORECB  DC    F'0'                                                             
*                                                                               
         DS    0F                  ENSURE FULLWORD ALIGNMENT                    
WTORPARM DC    AL1(5)              LENGTH OF REPLY                              
         DC    AL3(0)              ADDRESS OF REPLY                             
         DC    A(WTORECB)                                                       
WTORMLEN DC    AL2(LPCMA)          LENGTH OF MESSAGE                            
         DC    B'0000000000000000'                                              
WTORNAME DC    C'JOB-NAME.PROCNAME.STEPNAME-'                                   
PCREPLY  DC    CL5' '                                                           
PCMA     DC    C'REQ 000     DIED IN XXXXXXXX + DDDD '                          
PCMB     DC    C'NNN FAILS,REASON=12345678901234567890 '                        
PCMC     DC    C'REPLY=NEXT/STOP'                                               
LPCMA    EQU    *-PCMA                                                          
         TITLE 'MASTER CONTROL - OPERATOR INTERRUPTION'                         
OPCOM    NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         TTIMER CANCEL                                                          
         CLI   MCPOPSW,C'P'        IS THIS FROM ADPOP (TIMER RTN)               
         BE    OPCOM02             YES-CAN'T HAVE A TIMER POP AGAIN             
         MVI   MCPOPSW,FF          INHIBIT TIMER INTERRRUPTS                    
*                                                                               
OPCOM02  DS    0H                                                               
         MVC   OPMA+4(3),=C'  1'                                                
         TM    MCFLAGS,MCFOFFPK    OFF PEAK SOON JOB?                           
         BO    OPCOM03             YES                                          
         EDIT  (P3,MCREQNO),(3,OPMA+4),WRK=MCWORK,DUB=MCDUB                     
*                                                                               
OPCOM03  TIME  BIN                 R0=TIME NOW (IN SECS/100)                    
         S     R0,MCJOBSTM         GET ELAPSED FROM START OF JOB                
         BNM   *+8                 IF PLUS, OK                                  
         A     R0,=A(24*3600*100)  ADD 24HRS, JUST WENT PAST MIDNIGHT           
         GOTOR OPTIME1                                                          
         MVC   OPMA+11(8),MCWORK                                                
*                                                                               
         LAY   R2,MCCPUWRK                                                      
         TIMEUSED STORADR=(R2),LINKAGE=SYSTEM,CPU=MIC                           
         LM    RE,RF,0(R2)                                                      
         D     RE,=F'10000'                                                     
         LR    R0,RF               R0=TCB TIME (IN 1/100 SECS)                  
         S     R0,MCRUNTCB                                                      
         GOTOR OPTIME1                                                          
         MVC   OPMA+24(8),MCWORK                                                
*                                                                               
         EDIT  (B4,LOGORUNP),(5,OPMB+4),WRK=MCWORK,DUB=MCDUB                    
         EDIT  (B4,LOGORUNL),(6,OPMB+14),WRK=MCWORK,DUB=MCDUB                   
         EDIT  (B4,MCACTIOS),(7,OPMB+26),WRK=MCWORK,DUB=MCDUB                   
*                                                                               
         MVC   MCWORK,MCSPACES                                                  
OPCOM04  LA    R3,LOPMA+4          STD LEN IF NO SQUASHER                       
         MVC   MCWORK+4(LOPMA),OPMA  MOVE FOR SQUASHER                          
         ICM   RF,15,MCVSQSHR                                                   
         BZ    OPCOM06                                                          
         GOTOR (RF),MCDUB,MCWORK,LOPMA+4                                        
         L     R3,MCDUB+4          GET LENGTH AFTER SQUASHER                    
OPCOM06  SR    R0,R0                                                            
         CLI   MCTSTRUN,FF         IF RUN=TEST                                  
         BNE   OPCOM08                                                          
         LHI   R0,FF               SET FOR WRITE IMMEDIATE                      
         SHI   R3,L'OPMC           DO NOT PRINT REPLIES FOR OPERATOR            
         MVI   MCOPTION,C'D'       FORCE DUMP                                   
*                                                                               
OPCOM08  LA    RE,1                                                             
         ST    RE,MCPARA                                                        
         STC   R0,MCPARA                                                        
         TM    MCFLAGS,MCFOFFPK    OFF PEAK SOON JOB?                           
         BNO   OPCOM09                                                          
         OI    MCPARA+1,X'40'      YES-OVERRIDE THE STEPNAME                    
         MVC   MCP(8),MCJOB        THIS IS MONSOON'S JOBNAME                    
         MVI   MCP+8,C'.'                                                       
         MVC   MCP+9(8),LOGOJOB    SOON REQUEST'S JOBNAME AS STEPNAME           
         MVI   MCP+17,C'.'                                                      
         MVC   MCP+18(3),MCREMPQK+2     PQ SUBID                                
         MVI   MCP+21,C','              PQ REPORT NUMBER FOLLOW                 
         EDIT  (B2,MCREMPQK+5),(4,MCP+22),ALIGN=LEFT,ZERO=NOBLANK,     +        
               WRK=MCP+26,DUB=MCDUB                                             
OPCOM09  GOTOR MCVLOGIO,MCPARA,,((R3),MCWORK),(26,MCP)                          
*                                                                               
         CLI   MCTSTRUN,FF         IF RUN=TEST                                  
         BE    OPCOM14             USE OPTION DUMP                              
         MVC   MCOPTION,MCSPACES                                                
         GOTOR (RF),(R1),0,(4,MCOPTION)                                         
*                                                                               
         MVI   MCSTOP,C' '                                                      
         CLC   MCOPTION(2),=C'GO'                                               
         BE    OPCOM10                                                          
         CLC   MCOPTION(3),=C'EOR'                                              
         BE    OPCOM12                                                          
         CLC   MCOPTION(3),=C'NOW'                                              
         BE    OPCOM14                                                          
         CLC   MCOPTION(4),=C'DUMP'                                             
         BE    OPCOM14                                                          
         CLC   MCOPTION(3),=C'IGN'                                              
         BE    OPCOM18                                                          
         CLC   MCOPTION(3),=C'RTP'                                              
         BE    OPCOM20                                                          
         MVC   MCWORK(4),=CL4'BAD-'                                             
         MVC   MCWORK+4(L'MCWORK-4),MCSPACES                                    
         B     OPCOM04                                                          
*                                                                               
OPCOM10  L     R0,MCRTPTCB                                                      
         A     R0,MCNXTTCB                                                      
         ST    R0,MCNXTTCB                                                      
*                                                                               
         L     R0,MCRTPPGS                                                      
         A     R0,MCNXTPGS                                                      
         ST    R0,MCNXTPGS                                                      
*                                                                               
         L     R0,MCRTPIOS                                                      
         A     R0,MCNXTIOS                                                      
         ST    R0,MCNXTIOS                                                      
         B     OPCOMX                                                           
*                                                                               
OPCOM12  MVI   MCSTOP,C'R'                                                      
         XC    MCNXT,MCNXT                                                      
         B     OPCOMX                                                           
*                                                                               
OPCOM14  MVI   MCSTOP,C'N'                                                      
         XC    MCNXT,MCNXT                                                      
         XC    MCRTPCPU,MCRTPCPU                                                
         CLI   MCPOPSW,C'P'        TEST TIMER POP ACTIVE                        
         BE    OPCOMX                                                           
         CLI   MCOPTION,C'D'       TEST DUMP OPTION                             
         BE    OPCOM16                                                          
         ABEND 666                                                              
*                                                                               
OPCOM16  ABEND 667                                                              
*                                                                               
OPCOM18  XC    MCNXT,MCNXT         CLEAR RUN TIME PARS                          
         XC    MCRTPCPU,MCRTPCPU                                                
         B     OPCOMX                                                           
*                                                                               
OPCOM20  EDIT  (B4,MCRTPPGS),(7,OPCMA+4),WRK=MCWORK,DUB=MCDUB                   
         EDIT  (B4,MCRTPIOS),(7,OPCMB+5),WRK=MCWORK,DUB=MCDUB                   
         L     R0,MCRTPTCB                                                      
         SRDL  R0,32                                                            
         D     R0,=F'100'          CONVERT TCB TIME TO SECONDS                  
         LR    R0,R1                                                            
         EDIT  (R0),(7,OPCMC+4),WRK=MCWORK,DUB=MCDUB                            
         L     R0,MCRTPTCB                                                      
         SRDL  R0,32                                                            
         D     R0,=F'100'          CONVERT TCB TIME TO SECONDS                  
         LR    R0,R1                                                            
         EDIT  (R0),(7,OPCMD+5),WRK=MCWORK,DUB=MCDUB                            
         MVC   MCWORK,MCSPACES                                                  
*                                                                               
OPCOM22  LA    R3,LOPCMA+15         STD LEN IF NO SQUASHER                      
         MVC   MCWORK+15(LOPCMA),OPCMA  MOVE FOR SQUASHER                       
         ICM   RF,15,MCVSQSHR                                                   
         BZ    OPCOM24                                                          
         GOTOR (RF),MCDUB,MCWORK,LOPCMA+15                                      
         L     R3,MCDUB+4          GET LENGTH AFTER SQUASHER                    
*                                                                               
OPCOM24  GOTOR MCVLOGIO,MCDUB,1,((R3),MCWORK)                                   
         MVC   OPREPLY,MCSPACES                                                 
         GOTOR (RF),(R1),0,(10,OPREPLY)                                         
         CLC   OPREPLY(2),=C'GO'                                                
         BE    OPCOM10             GO RESET MCNXT CTRS                          
         CLI   OPREPLY+1,C'='                                                   
         BNE   OPCOM28                                                          
         GOTOR OPDATA                                                           
*                                                                               
         CLI   OPREPLY,C'P'                                                     
         BNE   *+16                                                             
         LA    R1,MCRTPPGS                                                      
         LA    R2,MCNXTPGS                                                      
         B     OPCOM26                                                          
*                                                                               
         CLI   OPREPLY,C'S'                                                     
         BNE   *+16                                                             
         LA    R1,MCRTPIOS                                                      
         LA    R2,MCNXTIOS                                                      
         B     OPCOM26                                                          
*                                                                               
         CLI   OPREPLY,C'C'                                                     
         BNE   *+16                                                             
         MHI   R0,100              CONVERT TO SEC/100                           
         ST    R0,MCRTPCPU                                                      
         B     OPCOM20                                                          
*                                                                               
         CLI   OPREPLY,C'T'                                                     
         BNE   OPCOM28                                                          
         LA    R1,MCRTPTCB                                                      
         LA    R2,MCNXTTCB                                                      
OPCOM26  ST    R0,0(R1)                                                         
         LTR   R0,R0               WAS ENTRY ZERO                               
         BNZ   OPCOM20                                                          
         ST    R0,0(R2)            ZERO MCNXT                                   
         B     OPCOM20                                                          
*                                                                               
OPCOM28  MVC   MCWORK,MCSPACES                                                  
         MVC   MCWORK(14),=C'INVALID REPLY,'                                    
         B     OPCOM22                                                          
*                                                                               
OPCOMX   MVI   OPMA+10,C'='        ELAPSED TIME                                 
         MVI   OPMA+23,C'='        CPU TIME                                     
         MVI   OPMB+3,C'='         PAGES                                        
         MVI   OPMB+25,C'='        SIOS                                         
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* VALIDATE OPERATOR RESPONSE IS NUMERIC                               *         
***********************************************************************         
OPDATA   LA    R5,OPREPLY+9        SCAN BKWDS FOR LAST DIGIT                    
         CLI   0(R5),C' '                                                       
         BNE   *+8                                                              
         BCT   R5,*-8                                                           
         LR    R4,R5                                                            
OPDATA2  CLI   0(R4),C'='                                                       
         BE    OPDATA4                                                          
         CLI   0(R4),C'0'                                                       
         BL    OPCOM24             LESS THAN 0 IS ERROR                         
         CLI   0(R4),C'9'                                                       
         BH    OPCOM24                                                          
         BCT   R4,OPDATA2                                                       
OPDATA4  SR    R5,R4               GIVES DATA LEN                               
         BNP   OPCOM24                                                          
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         PACK  MCDUB,1(0,R4)                                                    
         CVB   R0,MCDUB                                                         
         BR    RE                                                               
*                                                                               
***********************************************************************         
* FORMAT TIME FOR OPERATOR DISPLAY                                    *         
***********************************************************************         
OPTIME1  SRDL  R0,32               CONVERT SEC/100 TO SEC                       
         D     R0,=F'100'                                                       
         SR    R0,R0               LEAVES SECONDS IN R1                         
         B     *+8                                                              
OPTIME2  SRDL  R0,32                                                            
         D     R0,=F'3600'         GIVES HOURS IN R1                            
         CVD   R1,MCDUB                                                         
         OI    MCDUB+7,X'0F'                                                    
         UNPK  MCWORK(2),MCDUB                                                  
         MVI   MCWORK+2,C'.'                                                    
         SRDL  R0,32                                                            
         D     R0,=F'60'           GIVES MINUTES IN R1/SECS IN R0               
         CVD   R1,MCDUB                                                         
         OI    MCDUB+7,X'0F'                                                    
         UNPK  MCWORK+3(2),MCDUB                                                
         MVI   MCWORK+5,C'.'                                                    
         CVD   R0,MCDUB                                                         
         OI    MCDUB+7,X'0F'                                                    
         UNPK  MCWORK+6(2),MCDUB                                                
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
OPMA     DC    CL33'REQ 000 ET=HH.MM.SS TCB=HH.MM.SS '                          
OPMB     DC    CL34'PGS=NNNNN LNS=NNNNNN SIOS=NNNNNNN '                         
OPMC     DC    CL29'REPLY=GO/EOR/NOW/DUMP/IGN/RTP'                              
LOPMA    EQU   *-OPMA                                                           
*                                                                               
OPCMA    DC    C'PGS=NNNNNNN '                                                  
OPCMB    DC    C'SIOS=NNNNNNN '                                                 
OPCMC    DC    C'TCB=NNNNNNN '                                                  
OPCMD    DC    C'TIME=NNNNNNN '                                                 
         DC    C'REPLY=P/S/C/T/GO'                                              
LOPCMA   EQU   *-OPCMA                                                          
OPREPLY  DC    CL10' '                                                          
         EJECT                                                                  
***********************************************************************         
* OUTPUT NEW SMF REQUEST JOB ACCOUNTING DATA - USES DDSMFJACC         *         
***********************************************************************         
SMFJACC  NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
         XC    SMFPARMS,SMFPARMS                                                
         XC    SMFRECJ,SMFRECJ                                                  
         USING SMFJACCD,SMFRECJ                                                 
         LA    RF,SMFRECJ                                                       
         ST    RF,SMFP1                                                         
*                                                                               
SMFJAC1  MVC   SMFJRECL,=AL2(SMFJACCL)                                          
         MVC   SMFJSYS,MCSYSTEM                                                 
         MVC   SMFJPROG,MCPROG                                                  
         MVC   SMFJAGYA,MCUSER                                                  
         MVC   SMFJMED,MCMEDIA                                                  
         MVC   SMFJREQN,MCREQNO                                                 
         MVC   SMFJSEGN,MCSEGNO                                                 
         MVC   SMFJABND,MCREASON                                                
         MVC   SMFJRSTT,MCRUNLPS   REQUEST START TIME                           
         MVC   SMFJRELT,MCREQLPS   REQUEST ELAPSED TIME                         
         MVC   SMFJRTCB,MCREQTCB   REQUEST CPU TIME                             
         MVC   SMFJRSIO,MCREQSIO                                                
         MVC   SMFJRPAG,MCREQPAG                                                
         MVC   SMFJRLIN,MCREQLIN                                                
         MVC   SMFJPQK,MCREMPQK                                                 
         MVC   SMFJOVSY,MCOVSYS                                                 
         ICM   RF,15,MCUTL                                                      
         BZ    *+10                                                             
         MVC   SMFJSEN,4(RF)                                                    
         MVC   SMFJPOWC,MCIDPOWC                                                
         MVC   SMFJORIG,MCORIGID                                                
         MVC   SMFJDEST,MCDESTID                                                
         MVC   SMFJAGYP,MCRHSAGP                                                
         MVC   SMFJPIDN,MCRHPSWD                                                
         LAY   R1,MCREQCPU                                                      
         MVC   SMFCPUJ,0(R1)                                                    
         LAY   R1,MCRUNCPU                                                      
         MVC   SMFCPUR,0(R1)                                                    
*                                                                               
SMFJAC2  L     R5,MCAEXTRA                                                      
         USING MCEXTRA,R5                                                       
         MVC   SMFJPID,MCPID                                                    
*                                                                               
         CLI   MCCSMODE,1          COMSCORE                                     
         BL    *+8                                                              
         MVI   SMFJSCLT,C'C'       SET CLASS TYPE TO COMSCORE                   
                                                                                
*                                  01234567890123456789                         
         CLC   MCSOON(8),MCSPACES  SOON=CL,P,F,T,XXXXXX                         
         BNH   SMFJAC4                                                          
         LA    RF,MCSOON                                                        
         MVC   SMFJSCLS,5(RF)      SOON CLASS                                   
         MVC   SMFJSPTY,8(RF)      SOON PRIORITY                                
         MVC   SMFJSFID,10(RF)     SOON FACPAK ID                               
         MVC   SMFJSCLT,12(RF)     SOON CLASS TYPE                              
         GOTOR MCVHEXIN,MCPARA,14(RF),MCDUB,6                                   
         OC    12(4,R1),12(R1)     TEST VALID HEX                               
         BZ    *+10                                                             
         MVC   SMFJSSUT+1(3),MCDUB SOON REQEST SUBMITTED TIME                   
*                                                                               
SMFJAC4  LA    RF,MCCARDSH         REQUESTS CARDS                               
         ST    RF,SMFP2                                                         
         LH    R0,MCCARDSH         LEN INCLUDES LEN                             
         AHI   R0,2                                                             
         STH   R0,MCCARDSH                                                      
         DROP  R5                                                               
*                                                                               
SMFJAC5  LA    RF,MCREMOTE         MCREMOTE IF DIRECT=CARD INPUT                
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY                                                
         BZ    SMFJAC6                                                          
         ST    RF,SMFP3                                                         
         DROP  RF                                                               
*                                                                               
SMFJAC6  LA    RF,MCARC            MCARC IF ARC=CARD INPUT                      
         CLC   MCARC(8),MCSPACES                                                
         BNH   SMFJAC8                                                          
         ST    RF,SMFP4                                                         
*                                                                               
SMFJAC8  TM    MCFLAGS,MCFIDF      NO SMF RECS IF RUNNING UNDER IDF             
         BO    SMFJAC8B                                                         
         ICM   RF,15,=V(SMFOUT)    NEW JOB ACCOUNTING SMF RECORD                
         JZ    SMFJAC8A                                                         
         GOTO1 (RF),MCDUB,F'9',SMFPARMS                                         
         J     SMFJAC8B                                                         
SMFJAC8A LHI   R0,-25                                                           
         LA    R1,SMFPARMS                                                      
         SVC   247                                                              
SMFJAC8B J     EXIT                                                             
*&&DO                                                                           
SMFJAC9  EQU   *                   DEBUG START - PRINT SMF RECORD               
         GOTO1 (RF),MCDUB,X'80000009',SMFPARMS                                  
         ICM   RF,15,=V(PRNTBL)                                                 
         JZ    EXIT                                                             
         L     R5,72(RD)           GET A(SMFREC) IN SMFOUT'S W/S                
         L     R0,=A(SMFREC)                                                    
         LH    R1,0(R5)            GET L'SMFREC                                 
         CHI   R1,2048                                                          
         JNH   *+8                                                              
         LHI   R1,2048                                                          
         LR    RE,R5                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY TO MY W/S AREA                          
         L     R5,=A(SMFREC)                                                    
         LH    R6,0(R5)            GET L'SMFREC                                 
         CHI   R6,2048                                                          
         JNH   *+8                                                              
         LHI   R6,2048                                                          
         GOTO1 =V(PRNTBL),PLIST,=C'SMFREC',(R5),C'DUMP',(R6),=C'2D'             
         J     EXIT                                                             
PLIST    DC    3D'0'                                                            
         DC    C'*SMFREC*'                                                      
SMFREC   DC    2048X'00'           DEBUG END                                    
*&&                                                                             
         DS    0F                                                               
SMFRECJ  DC    (SMFJACCL)X'00'                                                  
         DS    0F                                                               
SMFPARMS DS    0XL16                                                            
SMFP1    DS    A                                                                
SMFP2    DS    A                                                                
SMFP3    DS    A                                                                
SMFP4    DS    A                                                                
         LTORG                                                                  
         EJECT                                                                  
         TITLE 'MASTER CONTROL - I/O WAITS'                                     
ADWAIT   NTR1  BASE=*,LABEL=*      COMMON I/O WAIT ROUTINE                      
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         ST    R1,MCSVECB          SAVE ECB POINTER                             
*                                                                               
         OC    MCRTPCPU,MCRTPCPU   TEST TO RESET CPU TIMER                      
         BZ    ADWAIT2                                                          
         CLI   MCPOPSW,X'FE'       IF IN PDUMPER DONT RESTART TIMER             
         BE    ADWAIT2                                                          
         CP    MCREQNO,=P'1'                                                    
         BL    ADWAIT2                                                          
         LA    R2,MCRTPCPU                                                      
         STIMER TASK,ADPOP,BINTVL=(R2)                                          
         MVI   MCPOPSW,0           ENABLE TIMER INTERRRUPTS                     
*                                                                               
ADWAIT2  TM    MCSVECB,X'80'       TEST ENTRY FROM PRINT                        
         BO    ADWAITPT                                                         
*                                                                               
         L     RE,MCACTIOS         GET ACTUAL EXCP COUNT                        
         AHI   RE,1                                                             
         ST    RE,MCACTIOS                                                      
         ICM   RF,15,MCNXTIOS                                                   
         BZ    ADWAIT4                                                          
         CR    RE,RF                                                            
         BL    ADWAIT4                                                          
         CP    MCREQNO,=P'1'                                                    
         BL    ADWAIT6                                                          
         L     R1,=A(OPMB)                                                      
         MVI   25(R1),C'*'         SIOS                                         
         GOTOR OPCOM                                                            
         B     ADWAIT6                                                          
*                                                                               
ADWAIT4  OC    MCNXTTCB,MCNXTTCB   TEST THERE WAS A TIME LIMIT                  
         BZ    ADWAIT6                                                          
         CP    MCREQNO,=P'1'                                                    
         BL    ADWAIT6                                                          
         LAY   R2,MCCPUWRK                                                      
         TIMEUSED STORADR=(R2),LINKAGE=SYSTEM,CPU=MIC                           
         LM    RE,RF,0(R2)                                                      
         D     RE,=F'10000'                                                     
*                                                                               
         LR    R0,RF               R0=TCB TIME (IN 1/100 SECS)                  
         S     R0,MCRUNTCB                                                      
         C     R0,MCNXTTCB         COMPARE TO THRESHOLD                         
         BL    ADWAIT6                                                          
         L     R1,=A(OPMA)                                                      
         MVI   23(R1),C'*'         TCB TIME                                     
         GOTOR OPCOM                                                            
*                                                                               
ADWAIT6  L     R1,MCSVECB                                                       
         WAIT  ECB=(1)                                                          
*                                                                               
ADWAITX  J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CHECK HERE FOR MCNXTPGS EXCEEDED, NEVER WAIT ON PRINT               *         
***********************************************************************         
ADWAITPT ICM   R1,15,MCNXTPGS      IS THERE A PAGE LIMIT                        
         BZ    ADWAITX             NO                                           
         L     R9,MCVLOGOC                                                      
         C     R1,LOGORUNP                                                      
         BH    ADWAITX                                                          
         L     R1,=A(OPMB)                                                      
         MVI   3(R1),C'*'          PAGES                                        
         MVI   MCCLOERR,C'P'       SET TOO MANY PAGES                           
         GOTOR OPCOM                                                            
         B     ADWAITX                                                          
*                                                                               
         LTORG                                                                  
         TITLE 'MASTER CONTROL - TIMER POP HANDLER'                             
ADPOP    NBASE 0,**POP***,A(COMWORK)                                            
         L     RC,=A(MASTC)                                                     
         L     R9,MCVLOGOC                                                      
         CLI   MCPOPSW,0           ARE TIMER INTERRRUPTS ENABLED                
         BNE   ADPOPX              NO-BYPASS                                    
         MVI   MCPOPSW,C'P'        SET TIMER EXIT ACTIVE                        
*                                                                               
         LHI   R0,L'POPMSG1+L'POPMSG2                                           
         SR    RF,RF                                                            
         CLI   MCTSTRUN,FF         IF RUN=TEST                                  
         BNE   *+12                                                             
         LHI   RF,FF               SET FOR WRITE IMMEDIATE                      
         LHI   R0,L'POPMSG1        DO NOT PRINT 'HIT ENTER' MESSAGE             
         GOTOR MCVLOGIO,MCPARA,((RF),1),((R0),POPMSG1)                          
         CLI   MCTSTRUN,FF         IF RUN=TEST                                  
         BE    ADPOP2              BYPASS REPLY                                 
         GOTOR (RF),(R1),0,(1,POPMSG3)                                          
*                                                                               
ADPOP2   L     R1,=A(OPMA)                                                      
         MVI   23(R1),C'*'         CPU TIME                                     
         GOTOR OPCOM                                                            
*                                                                               
         CLI   MCOPTION,C'N'       TEST OPER RESP = NOW                         
         BE    ADPOPDMP                                                         
         CLI   MCOPTION,C'D'       TEST OPER RESP = DUMP                        
         BE    ADPOPDMP                                                         
*                                                                               
         OC    MCRTPCPU,MCRTPCPU   SEE IF CPU TIME WAS RESET                    
         BZ    ADPOPX                                                           
         LA    R2,MCRTPCPU                                                      
         STIMER TASK,ADPOP,BINTVL=(R2)                                          
         MVI   MCPOPSW,0           SET TIMER EXIT ENABLED                       
         B     ADPOPX                                                           
                                                                                
***********************************************************************         
* KILL THE APPLICATION                                                *         
***********************************************************************         
ADPOPDMP L     RF,X'21C'(,R0)      GET PSA TCB OLD                              
         L     RF,0(RF)            POINT TO CURRENT RB                          
ADPOPNX  L     R1,28(RF)           POINT TO PREVIOUS RB                         
         CLC   96(8,R1),=CL8'SORT' IF SORT KILL HERE                            
         BE    ADPOPIT                                                          
         CLC   29(3,R1),X'21D'(R0) DOES IT POINT TO TCB                         
         BE    ADPOPIT                                                          
         LR    RF,R1                                                            
         B     ADPOPNX                                                          
*                                                                               
ADPOPIT  L     RE,20(R1)           GET PSW INSTRUCTION ADDRESS                  
         ST    RE,MCCHKPSW         SAVE INST ADDRESS                            
         MVC   MCCHKPSW(1),0(RE)   AND OP CODE                                  
         MVI   0(RE),0             SET TO CAUSE PROGRAM CHECK                   
*                                                                               
ADPOPX   J     EXIT                                                             
*                                                                               
         LTORG                                                                  
*                                                                               
POPMSG1  DC    C'THIS PROGRAM MAY BE LOOPING'                                   
POPMSG2  DC    C' - HIT ENTER FOR DETAILS'                                      
POPMSG3  DC    C' '                                                             
TESTPMAX EQU   15                                                               
TESTPLEN EQU   71                                                               
TESTPOOL DC    ((TESTPMAX*TESTPLEN)+1)X'00'                                     
EZCTLMAX EQU   5                   MAX NUMBER OF EZCTL CARDS ALLOWED            
SVEZCTL  DC    (EZCTLMAX)CL74' '   EZCTL CARD POOL                              
COMWORK  DS    2400D                                                            
         EJECT                                                                  
***********************************************************************         
* VALIDATE THE DATE= CARD (EXPECTED IN MCP)                           *         
* NOTE THAT &&&& ESCAPE SEQUENCE IS NEEDED TO ASSEMBLE AS &&          *         
***********************************************************************         
VALDATE  NTR1  BASE=*,LABEL=*                                                   
         CLC   MCP+5(2),=C'&&&&'   TEST IF SOFT DATE INPUT                      
         BE    *+14                YES-VALIDATE                                 
         MVC   MCDATE,MCP+5                                                     
         J     VALDQX              EXIT WITH CC EQUAL                           
         OI    MCFLAGS2,MCFINVH0+MCFSOFDT                                       
*                                                                               
         LA    RF,SDTAB            SEARCH SOFT DATE KEYWORD TABLE               
VALDAT10 CLI   0(RF),X'FF'                                                      
         JE    VALDNQX             KEYWORD NOT FOUND                            
         LLC   RE,0(RF)            LENGTH OF TABLE ENTRY                        
         AHI   RE,-(1+4)                                                        
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   MCP+5(0),5(RF)      SOFT DATE KEYWORD IN THE TABLE               
         BE    VALDAT20                                                         
         LLC   RE,0(RF)                                                         
         AR    RF,RE                                                            
         B     VALDAT10            BACK FOR NEXT TABLE ENTRY                    
*                                                                               
VALDAT20 ICM   RF,15,1(RF)         A(VALIDATION ROUTINE)                        
         BASR  RE,RF                                                            
         BNE   VALDNQX                                                          
*                                                                               
VALDQX   CLI   *+1,0               SET CC EQUAL                                 
         J     EXIT                                                             
VALDNQX  CLI   *,0                 SET CC NOT EQUAL                             
         J     EXIT                                                             
         LTORG                                                                  
                                                                                
***********************************************************************         
* SOFT DATE TABLE                                                     *         
* AL1    ENTRY LENGTH                                                 *         
* AL4    A(VALIDATION ROUTINE), MM/DD/YY DATE RETURNED IN MCDATE      *         
* CLN    VARIABLE LENGTH SOFT DATE KEYWORD                            *         
***********************************************************************         
SDTAB    DS    0H                                                               
*                                                                               
SD01     DC    AL1(SD01LQ)                                                      
         DC    AL4(GETSOF01)                                                    
         DC    C'&&&&'             TWO AMPERSANDS                               
         DC    C'ENDMONTH'         KEYWORD                                      
         DC    C'&&&&'             TWO AMPERSANDS                               
SD01LQ   EQU   *-SD01                                                           
*                                                                               
SD02     DC    AL1(SD02LQ)                                                      
         DC    AL4(GETSOF02)                                                    
         DC    C'&&&&'                                                          
         DC    C'FIRSTSUNDAY'                                                   
         DC    C'&&&&'                                                          
SD02LQ   EQU   *-SD02                                                           
*                                                                               
SD03     DC    AL1(SD03LQ)                                                      
         DC    AL4(GETSOF03)                                                    
         DC    C'&&&&'                                                          
         DC    C'TODAY'                                                         
         DC    C'&&&&'                                                          
SD03LQ   EQU   *-SD03                                                           
*                                                                               
         DC    X'FF'               EOT                                          
                                                                                
***********************************************************************         
* GET END OF MONTH                                                    *         
* IF TODAY'S DATE IS < Q, GET LAST DATE OF *PREVIOUS* MONTH           *         
* IF > Q - GET LAST DATE OF *THIS* MONTH                              *         
***********************************************************************         
GETSOF01 NTR1  BASE=*,LABEL=*                                                   
         GOTOR MCVDTCON,MCPARA,(5,0),(3,MCWORK) GET TODAYS DATE                 
*                                                                               
         MVI   MCWORK+3,1          LAST DAY OF INPUT MONTH                      
         CLI   MCWORK+2,10         COMPARE TO CUTOFF FOR PREV MONTH             
         BH    *+8                                                              
         MVI   MCWORK+3,5          LAST DAY OF PREVIOUS MONTH                   
*                                  TODAY > Q, GET END OF THIS MONTH             
         GOTOR MCVDTCON,MCPARA,(X'35',0),(10,MCDATE),(MCWORK+3,0)               
         J     EXIT                                                             
*                                                                               
         LTORG                                                                  
                                                                                
***********************************************************************         
* GET FIRST SUNDAY OF CURRENT MONTH                                   *         
***********************************************************************         
GETSOF02 NTR1  BASE=*,LABEL=*                                                   
         GOTOR MCVDTCON,MCPARA,(5,0),(0,MCWORK)  GET TODAYS DATE                
         MVC   MCWORK+4(2),=C'01'                                               
         GOTOR =V(GETDAY),MCPARA,MCWORK,MCWORK+6                                
         LLC   RE,MCPARA           DAY OF WEEK IN BINARY                        
         LHI   RF,7                SUNDAY                                       
         SR    RF,RE               DIFFERENCE                                   
*                                                                               
         GOTOR =V(ADDAY),MCPARA,(C'D',MCWORK),MCWORK+6,(RF)                     
         GOTOR MCVDTCON,MCPARA,(0,MCWORK+6),(10,MCDATE)                         
         J     EXIT                                                             
*                                                                               
         LTORG                                                                  
                                                                                
***********************************************************************         
* TODAY'S DATE                                                        *         
***********************************************************************         
GETSOF03 NTR1  BASE=*,LABEL=*                                                   
         GOTOR MCVDTCON,MCPARA,(5,0),(10,MCDATE) GET TODAYS DATE                
         J     EXIT                                                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* TABLE OF CONTROL CARD VALUES                                        *         
***********************************************************************         
CTLTAB   DS    0X                                                               
         DC    AL1(06),AL3(CTLSTART),C'START='         REQUEST CONTROL          
         DC    AL1(04),AL3(CTLEND),C'END='                                      
         DC    AL1(08),AL3(CTLREQST),C'REQUEST='                                
         DC    AL1(08),AL3(CTLNUMRQ),C'NUMREQS='                                
         DC    AL1(05),AL3(CTLUPSI),C'UPSI='                                    
         DC    AL1(08),AL3(CTLREQFL),C'REQFILE='                                
         DC    AL1(05),AL3(CTLOK),C'CARD '             **NO-OP**                
         DC    AL1(05),AL3(CTLOK),C'DISK '             **NO-OP**                
         DC    AL1(06),AL3(CTLINPUT),C'INPUT='                                  
         DC    AL1(05),AL3(CTLOK),C'LOCK='             **NO-OP**                
         DC    AL1(07),AL3(CTLFACPK),C'FACPAK='                                 
         DC    AL1(08),AL3(CTLBILRF),C'BILLREF='                                
*                                                                               
         DC    AL1(08),AL3(CTLPRNTR),C'PRINTER '       PRINTING CONTROL         
         DC    AL1(08),AL3(CTLREPTS),C'REPORTS='                                
         DC    AL1(07),AL3(CTLHSKIP),C'HDSKIP='                                 
         DC    AL1(07),AL3(CTLDIRCT),C'DIRECT='                                 
         DC    AL1(09),AL3(CTLMAXLN),C'MAXLINES='                               
         DC    AL1(08),AL3(CTLSEGMT),C'SEGMENT='                                
         DC    AL1(07),AL3(CTLSTRIP),C'STRIPE='                                 
         DC    AL1(10),AL3(CTLLOGST),C'LOGOSTATS='                              
         DC    AL1(08),AL3(CTLLOGAD),C'LOGOADR='                                
         DC    AL1(08),AL3(CTLLOGDV),C'LOGODIV='                                
         DC    AL1(07),AL3(CTLNOLOG),C'NOLOGOS'                                 
         DC    AL1(08),AL3(CTLNOLOG),C'LOGOS=NO'                                
         DC    AL1(10),AL3(CTLLOGUS),C'LOGOS=USER'                              
         DC    AL1(07),AL3(CTLRQREP),C'REQREP='                                 
         DC    AL1(09),AL3(CTLNOCTL),C'NOCONTROL'  <== SEE ALSO FIXOUT          
         DC    AL1(08),AL3(CTLCTL),C'CONTROL='                                  
         DC    AL1(09),AL3(CTLNOREQ),C'NOREQUEST'                               
         DC    AL1(06),AL3(CTLWIDTH),C'WIDTH='                                  
         DC    AL1(07),AL3(CTLOFSET),C'OFFSET='                                 
         DC    AL1(06),AL3(CTLSPOOL),C'SPOOL='                                  
         DC    AL1(09),AL3(CTLPASWD),C'PASSWORD='  <== NOW USE PQPIN            
         DC    AL1(06),AL3(CTLPQPIN),C'PQPIN='                                  
         DC    AL1(06),AL3(CTLPQPID),C'PQPID='                                  
         DC    AL1(07),AL3(CTLRETIN),C'RETAIN='                                 
         DC    AL1(06),AL3(CTLCHARS),C'CHARS='                                  
         DC    AL1(04),AL3(CTLFORMS),C'FNO='                                    
         DC    AL1(06),AL3(CTLMAKER),C'MAKER='                                  
         DC    AL1(05),AL3(CTLRDESC),C'DESC='                                   
         DC    AL1(07),AL3(CTLOPTCD),C'OPTCD=J'                                 
         DC    AL1(09),AL3(CTLHFONT),C'HEADFONT='                               
         DC    AL1(05),AL3(CTLPFONT),C'FONT='                                   
         DC    AL1(08),AL3(CTLFRMDF),C'FORMDEF='                                
         DC    AL1(08),AL3(CTLPAGDF),C'PAGEDEF='                                
         DC    AL1(08),AL3(CTLPIT10),C'PITCH=10'                                
         DC    AL1(08),AL3(CTLPIT12),C'PITCH=12'                                
         DC    AL1(08),AL3(CTLPIT15),C'PITCH=15'                                
         DC    AL1(08),AL3(CTLDOWNL),C'DOWNLOAD'                                
         DC    AL1(08),AL3(CTLBURST),C'BURSTPQ='                                
         DC    AL1(06),AL3(CTLEZCTL),C'EZCTL='                                  
         DC    AL1(10),AL3(CTLARNUM),C'ALTREFNUM='                              
         DC    AL1(07),AL3(CTLCLAS2),C'CLASS2='                                 
         DC    AL1(04),AL3(CTLID2),C'ID2='                                      
         DC    AL1(06),AL3(CTLFORM2),C'FORM2='                                  
         DC    AL1(05),AL3(CTLPRQIX),C'PQIX='                                   
         DC    AL1(04),AL3(CTLARC),C'ARC='                                      
         DC    AL1(04),AL3(CTLWHY),C'WHY='                                      
*                                                                               
         DC    AL1(06),AL3(CTLTRACE),C'TRACE='         RUN CONTROL              
         DC    AL1(05),AL3(CTLDUMPO),C'DUMP='                                   
         DC    AL1(08),AL3(CTLDUDMP),C'DUPDUMP='                                
         DC    AL1(06),AL3(CTLWRITE),C'WRITE='                                  
         DC    AL1(06),AL3(CTLWRSRV),C'WRSRV='                                  
         DC    AL1(05),AL3(CTLRUNDT),C'DATE='                                   
         DC    AL1(07),AL3(CTLNUMBR),C'NUMBER='                                 
         DC    AL1(08),AL3(CTLBILLN),C'BILLNUM='                                
         DC    AL1(06),AL3(CTLRERUN),C'RERUN='                                  
         DC    AL1(08),AL3(CTLRECVR),C'RECOVER='                                
         DC    AL1(09),AL3(CTLRECV2),C'RECOVERY='                               
         DC    AL1(04),AL3(CTLRUNDO),C'UNDO'                                    
         DC    AL1(06),AL3(CTLRUNDO),C'UNWIND'                                  
*&&UK*&& DC    AL1(05),AL3(CTLHARP),C'HARP='                                    
         DC    AL1(06),AL3(CTLOK),C'RFHDR='            **NO-OP**                
         DC    AL1(08),AL3(CTLPSTNG),C'POSTING='                                
         DC    AL1(08),AL3(CTLTESTR),C'RUN=TEST'                                
         DC    AL1(11),AL3(CTLOFFPK),C'RUN=OFFPEAK'                             
         DC    AL1(08),AL3(CTLFXOUT),C'FIXOUT=Y'                                
         DC    AL1(14),AL3(CTLRUNMD),C'RUNMODE=SINGLE'                          
         DC    AL1(05),AL3(CTLCTRY),C'CTRY='                                    
         DC    AL1(05),AL3(CTLLANG),C'LANG='                                    
         DC    AL1(06),AL3(CTLESTAE),C'ESTAE='                                  
         DC    AL1(06),AL3(CTLUPDID),C'UPDID='                                  
         DC    AL1(05),AL3(CTLPARM),C'PARM='                                    
         DC    AL1(08),AL3(CTLCORET),C'CORETAB='       **NO-OP**                
         DC    AL1(07),AL3(CTLOK),C'DMPARM='           **NO-OP**                
         DC    AL1(08),AL3(CTLSPEND),C'SPACEND='                                
         DC    AL1(07),AL3(CTLSTOKN),C'STOKEN='                                 
         DC    AL1(03),AL3(CTLOK),C'XP='               **NO-OP**                
         DC    AL1(07),AL3(CTLDSPCE),C'DSPACE='                                 
         DC    AL1(08),AL3(CTLPROF),C'PROFILE='                                 
         DC    AL1(07),AL3(CTLUPSO),C'UPSOON='                                  
         DC    AL1(08),AL3(CTLQ0),C'DB2SSID='                                   
         DC    AL1(07),AL3(CTLR0),C'LOCKER='                                    
         DC    AL1(07),AL3(CTLS0),C'GLOBAL='                                    
         DC    AL1(07),AL3(CTLCMT),C'COMMIT='                                   
         DC    AL1(07),AL3(CTLTICK),C'TICKET='                                  
         DC    AL1(07),AL3(CTLMARK),C'MARKER='                                  
         DC    AL1(06),AL3(CTLFLASH),C'FLASH='                                  
         DC    AL1(05),AL3(CTLSOON),C'SOON='                                    
         DC    AL1(07),AL3(CTLWKDTE),C'WKDATE='                                 
         DC    AL1(06),AL3(CTLEMAIL),C'EMAIL='                                  
         DC    AL1(09),AL3(CTLEMAX),C'MAXEMAIL='                                
         DC    AL1(04),AL3(CTLEIDTO),C'EID='                                    
*                                                                               
*&&US*&& DC    AL1(09),AL3(CTLCOMSC),C'COMSCORE='                               
*&&US*&& DC    AL1(07),AL3(CTLMVSDS),C'MVSDSN='                                 
*&&US*&& DC    AL1(08),AL3(CTLCSRSP),C'COMSRSP='                                
*&&US*&& DC    AL1(09),AL3(CTLCSRTY),C'COMSRTRY='                               
*&&US*&& DC    AL1(09),AL3(CTLCWAIT),C'COMSWAIT='                               
*                                                                               
         DC    AL1(05),AL3(CTLTEST),C'TEST='           PHASE CONTROL            
         DC    AL1(09),AL3(CTLMUST),C'MUSTTEST='                                
         DC    AL1(05),AL3(CTLLOAD),C'LOAD='                                    
         DC    AL1(07),AL3(CTLAPLIC),C'APPLIC='                                 
         DC    AL1(06),AL3(CTLDDSIO),C'DDSIO='                                  
         DC    AL1(08),AL3(CTLAUTLD),C'AUTOLOAD'                                
*                                                                               
         DC    AL1(05),AL3(CTLLOGO),C'LOGO='           WRAPPING                 
         DC    AL1(06),AL3(CTLLOGO2),C'LOGO2='                                  
         DC    AL1(12),AL3(CTLDEST),C'DESTINATION='                             
         DC    AL1(06),AL3(CTLDEST2),C'DEST2='                                  
         DC    AL1(05),AL3(CTLSHIP),C'SHIP='                                    
         DC    AL1(07),AL3(CTLORIG),C'ORIGIN='                                  
         DC    AL1(05),AL3(CTLSHIP2),C'SHP2='                                   
         DC    AL1(05),AL3(CTLBRK1),C'BRK1='                                    
         DC    AL1(05),AL3(CTLBRK2),C'BRK2='                                    
         DC    AL1(07),AL3(CTLORGID),C'ORIGID='                                 
         DC    AL1(07),AL3(CTLDSTID),C'DESTID='                                 
         DC    AL1(03),AL3(CTLID),C'ID='                                        
         DC    AL1(05),AL3(CTLTAPE),C'TAPE='                                    
*                                                                               
         DC    AL1(06),AL3(CTLPHASE),C'PHASE='         ACCPAK SPECIAL           
         DC    AL1(10),AL3(CTLFILI),C'FILE=INPUT'                               
         DC    AL1(11),AL3(CTLFILO),C'FILE=OUTPUT'                              
         DC    AL1(09),AL3(CTLFILW),C'FILE=WORK'                                
         DC    AL1(07),AL3(CTLOUTP),C'OUTPUT='                                  
         DC    AL1(07),AL3(CTLLEDGR),C'LEDGER='                                 
         DC    AL1(08),AL3(CTLOK),C'HEXCOMP='          **NO-OP**                
*                                                                               
         DC    AL1(06),AL3(CTLMEDIA),C'MEDIA='         MEDLINE SPECIAL          
*&&UK*&& DC    AL1(08),AL3(CTLMEDSP),C'MEDDSPC='                                
         DC    AL1(07),AL3(CTLAGNCY),C'AGENCY='                                 
         DC    AL1(07),AL3(CTLREPRT),C'REPORT='                                 
*                                                                               
         DC    AL1(06),AL3(CTLXSPOT),C'XSPOT='         SPOTPAK SPECIAL          
         DC    AL1(08),AL3(CTLSTAPK),C'STAPACK='                                
*                                                                               
         DC    AL1(07),AL3(CTLRTPT),C'RTPTCB='         RUNTIME PARAMS           
         DC    AL1(08),AL3(CTLRTPT),C'RTPTIME='                                 
         DC    AL1(09),AL3(CTLRTPP),C'RTPPAGES='                                
         DC    AL1(08),AL3(CTLRTPE),C'RTPEXCP='                                 
         DC    AL1(07),AL3(CTLRTPC),C'RTPCPU='                                  
*                                                                               
         DC    AL1(02),AL3(CTLMPL),C'MP'               SYSTEM CONTROL           
         DC    AL1(02),AL3(CTLMPL),C'BU'                                        
         DC    AL1(02),AL3(CTLACC),C'AC'                                        
         DC    AL1(02),AL3(CTLMBA),C'MB'                                        
         DC    AL1(02),AL3(CTLCON),C'CT'                                        
         DC    AL1(02),AL3(CTLPER),C'PE'                                        
*&&US                                                                           
         DC    AL1(02),AL3(CTLSPT),C'SP'                                        
         DC    AL1(02),AL3(CTLNET),C'NE'                                        
         DC    AL1(02),AL3(CTLPRT),C'PP'                                        
         DC    AL1(02),AL3(CTLTAL),C'TA'                                        
         DC    AL1(02),AL3(CTLREP),C'RE'                                        
         DC    AL1(02),AL3(CTLCPP),C'CP'                                        
         DC    AL1(02),AL3(CTLSTR),C'ST'                                        
*&&                                                                             
*&&UK                                                                           
         DC    AL1(02),AL3(CTLMED),C'ME'                                        
         DC    AL1(02),AL3(CTLFEE),C'FE'                                        
*&&                                                                             
         DC    AL1(01),AL3(RUNINI),C'/'                END OF FILE              
         DC    AL1(01),AL3(CTLOK),C'*'                 COMMENT CARD             
*                                                                               
         DC    AL1(08),AL3(CTLEXTR),C'EXTRACT='                                 
         DC    AL1(08),AL3(CTLXMOD),C'XTRMODE='                                 
         DC    AL1(08),AL3(CTLXTYP),C'XTRTYPE='                                 
         DC    AL1(08),AL3(CTLEXF1),C'EXFILE1='                                 
         DC    AL1(07),AL3(CTLXSMF),C'XTRSMF='                                  
*                                                                               
CTLTABX  DC    AL1(FF)                                 END OF LIST              
*                                                                               
*                                                                               
*                                                                               
XTRSMF   NTR1  BASE=*,LABEL=*                                                   
         L     RC,=A(MASTC)                                                     
*                                                                               
         XC    XSMFREC,XSMFREC                                                  
         LA    R2,XSMFREC                                                       
         USING SMFXCRD,R2                                                       
*                                                                               
         MVC   SMFXCLEN,=AL2(SMFXCRDLQ)                                         
         MVC   SMFXCAGY,MCUSER                                                  
         MVC   SMFXCSYS,MCS2SENO                                                
*                                                                               
         L     R3,MCAEXTRA                                                      
         USING MCEXTRA,R3                                                       
         MVC   SMFXCMOD,MCXTRMOD                                                
         MVC   SMFXCTYP,MCXTRTYP                                                
         DROP  R3                                                               
*                                                                               
         LGF   GR0,MCJOBSTM        SAVED JOB START TIME                         
         MGHI  GR0,10000           CONVERT FROM 1/100S TO MICROSECONDS          
         STG   GR0,MCDUB                                                        
         MVC   SMFXCST,MCDUB                                                    
*                                                                               
         TIME  MIC,MCDUB,LINKAGE=SYSTEM                                         
         LG    GRF,MCDUB                                                        
         SRLG  GRF,GRF,12                                                       
         STG   GRF,MCDUB                                                        
         MVC   SMFXCET,MCDUB                                                    
*                                                                               
         IEABRCX ENABLE                                                         
         TIMEUSED STORADR=MCDUB,LINKAGE=SYSTEM,CPU=MIC                          
         IEABRCX DISABLE                                                        
         MVC   SMFXCCPU,MCDUB                                                   
*                                                                               
         EXTRACT MCDUB,'S',FIELDS=(ASID)                                        
         L     R3,MCDUB            MVS JOB ACCOUNTING INFORMATION               
         LOCASCB ASID=(R3)         PUTS A(ASCB) INTO R1                         
         MVC   SMFXCIOC,ASCBIOSX-ASCB(R1) IOS FOR THIS ADDRESS SPACE            
*                                                                               
         L     R3,MCAEXTRA                                                      
         USING MCEXTRA,R3                                                       
         GOTO1 =V(DYNALLOC),MCPARA,(C'D',MCEXFIL1),SMFXCDSN                     
         DROP  R3                                                               
*                                                                               
         GOTO1 =V(SMFOUT),MCPARA,F'17',XSMFREC                                  
*                                                                               
         J     EXIT                                                             
         LTORG                                                                  
XSMFREC  DC    XL(SMFXCRDLQ)'00'                                                
*                                                                               
*                                                                               
*                                                                               
         TITLE 'MASTER CONTROL - MASTC'                                         
MASTC    CSECT                                                                  
       ++INCLUDE DDMASTC                                                        
*                                                                               
*                                                                               
*                                                                               
* FASSBOFF                                                                      
       ++INCLUDE FASSB                                                          
         ORG   SSBD                                                             
       ++INCLUDE FASSBOFF                                                       
* UTLD                                                                          
       ++INCLUDE FAUTL                                                          
* DMREQHDRA                                                                     
       ++INCLUDE DMREQHDRA                                                      
* DDREMOTED                                                                     
       ++INCLUDE DDREMOTED                                                      
* DNPRTQD                                                                       
       ++INCLUDE DNPRTQD                                                        
* DDLOGOD                                                                       
       ++INCLUDE DDLOGOD                                                        
* DDBIGBOX                                                                      
       ++INCLUDE DDBIGBOX                                                       
* DDCBJOBD                                                                      
       ++INCLUDE DDCBJOBD                                                       
* DDMONSPARMD                                                                   
MONSPRMD DSECT                                                                  
       ++INCLUDE DDMONSPARM                                                     
* CTGENFILE/CTGENLOAD                                                           
       ++INCLUDE CTGENFILE                                                      
*&&US                                                                           
       ++INCLUDE CTGENLOAD                                                      
*&&                                                                             
* SEACSFILE                                                                     
       ++INCLUDE SEACSFILE                                                      
* DDSMFJACC                                                                     
       ++INCLUDE DDSMFJACC                                                      
* CVT                                                                           
         CVT     DSECT=YES                                                      
* TIOT                                                                          
         IEFTIOT1                                                               
* IHASDWA                                                                       
         IHASDWA GR32=YES                                                       
* IKJTCB                                                                        
         IKJTCB  LIST=YES                                                       
* IHAPSA                                                                        
         IHAPSA  LIST=YES                                                       
* IHAASCB                                                                       
         IHAASCB LIST=YES                                                       
* IHASTCB                                                                       
         IHASTCB LIST=YES                                                       
* IHAASSB                                                                       
         IHAASSB LIST=YES                                                       
* IHAJSAB                                                                       
         IAZJSAB LIST=YES                                                       
* IHALDA                                                                        
         IHALDA                                                                 
*                                                                               
       ++INCLUDE DDSMFXCR                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024DDMASTER  07/31/20'                                      
         END                                                                    
