*          DATA SET DDESSWTO   AT LEVEL 028 AS OF 12/22/99                      
*CATALP ESSWTO                                                                  
         SPACE 2                                                                
*********************************************************************           
* HOSTESS VERSION OF LOGIO                                          *           
*                                                                   *           
* PARAMETER LISTS ARE AS FOLLOWS                                    *           
*                                                                   *           
*   FORMAT STANDARD 132 BYTE PRINT LINE AND WRITE TO ESS SYSOUT LOG *           
*   P1 - XL1 = X'FF' WRITE TO CONSOLE                               *           
*            = X'FE' WRITE TO HARD COPY ONLY                        *           
*        XL3 = LENGTH MESSAGE                                       *           
*   P2 - A(MESSAGE DATA)                                            *           
*                                                                   *           
*                                                                   *           
*********************************************************************           
         TITLE '** ESSWTO **  READ AND WRITE OPERATOR CONSOLE '                 
ESSWTO   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**EWTO**                                             
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1               SAVE PARAM POINTER                           
         CLI   4(RA),0             TEST LENGTH ZERO                             
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLI   4(RA),128           OR LENGTH GREATER THAN 128                   
         BNH   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         OC    0(4,RA),0(RA)                                                    
         BZ    READ                                                             
         EJECT                                                                  
* WRITE IMMEDIATE *                                                             
         SPACE 1                                                                
         XC    WORK,WORK                                                        
         ZIC   RE,4(RA)            DATA LEN                                     
         CLI   4(RA),128           MAX MESSAGE LEN                              
         BNH   *+8                                                              
         LA    RE,128              FORCE TO MAX                                 
         STH   RE,WORK             AND SET IN PARAM LIST                        
         L     RF,4(RA)            DATA ADDR                                    
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   WORK+2(0),0(RF)                                                  
*                                                                               
         CLI   0(RA),X'FE'         TEST WRITE HARDCOPY                          
         BE    HRDCPY                                                           
*                                                                               
         LA    R3,WORK                                                          
         WTO   TEXT=(R3)                                                        
         B     EXIT                                                             
*                                                                               
HRDCPY   EQU   *                                                                
         LA    R3,WORK                                                          
         WTO   TEXT=(R3),MCSFLAG=HRDCPY                                         
         B     EXIT                                                             
         SPACE 1                                                                
         EJECT                                                                  
* BUILD PARAM LIST FOR WTOR *                                                   
         SPACE 1                                                                
READ     DS    0H                                                               
         LA    R1,WORK             POINT TO PARAM LIST                          
         L     RF,4(RA)            PICK UP REPLY LEN/ADDR                       
         ST    RF,0(R1)            AND SET IN PARAM LIST                        
*                                                                               
         LA    RF,0(RF)            THEN CLEAR HOB OF RF                         
         LA    RE,CONSECB          POINT TO ECB                                 
         ST    RE,4(R1)            AND SET IN PARAM LIST                        
*                                                                               
         XC    CONSECB,CONSECB     CLEAR ECB                                    
         SVC   35                  ISSUE WTOR                                   
         LA    R1,CONSECB                                                       
         WAIT  ECB=(1)                                                          
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
*                                                                               
         DS    0D                                                               
WORK     DS    CL128                                                            
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
CONSECB  DC    F'0'                                                             
ATIOT    DC    F'0'                                                             
WORKX    EQU   *                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028DDESSWTO  12/22/99'                                      
         END                                                                    
