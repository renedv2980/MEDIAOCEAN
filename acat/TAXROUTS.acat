*          DATA SET TAXROUTS   AT LEVEL 028 AS OF 02/24/11                      
*CATALP TAXROUTS                                                                
         TITLE 'EXTRACT ROUTINES FOR TALENT/PWC'                                
         PRINT NOGEN                                                            
TAXROUTS CSECT                                                                  
*                                                                               
         ENTRY TALCMLC           COMMERCIALS                                    
         ENTRY TALRESC           RESIDUALS                                      
         ENTRY TALSESC           SESSION FEES                                   
         ENTRY TALFNLC           FINAL CAST                                     
         ENTRY TALUSEC           UES AUTH                                       
         ENTRY TALPRFC           PERFORMER REF                                  
         ENTRY TALOVRC           OVERSCALE REF                                  
         ENTRY TALPRDC           PRODUCT REF                                    
         ENTRY TALREFC           OTHER REFERENCE TABLES                         
*                                                                               
EQXIT    CR    RB,RB                                                            
         J     EXIT                                                             
NEQXIT   LTR   RB,RB                                                            
EXIT     XIT1                                                                   
*                                                                               
***********************************************************************         
* TALCML - EXTRACT COMMERCIAL INFORMATION FOR PERFORMER SESSIONS                
*                                                                               
* AL4 A(WORKD)                                                                  
* AL4 A(TALFILE RECORD)                                                         
* AL4 0=PROCESS CMML REC, 1=NEXT CAST MEMBER                                    
* AL4 A(SXDTABD = SYSTEM EXTRACT TABLE)                                         
*                                                                               
***********************************************************************         
                                                                                
*======================================================================         
* TAXTRACT CALLS TALCML WITH PARM3=0 TO BEGIN A NEW COMMERCIAL                  
* ON EXIT, RETURN THE FIRST CAST MEMBER AT ADDR IN EXTRACT RECORD               
*                                                                               
* TAXTRACT THEN RETURNS WITH PARM3=1 FOR THE NEXT CAST MEMBER                   
* ON EXIT, RETURN X'80' IN PARM3 FOR 'NO MORE DATA'                             
*======================================================================         
                                                                                
TALCMLC  NMOD1 0,*TALCML*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(SSNPARMS)                                                  
         ST    RE,ASSNPARM                                                      
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         OC    PARM3,PARM3         TEST BEGIN NEW EXTRACT                       
         BNE   TCML40              NO - GO OUTPUT NEXT RECORD                   
*                                                                               
         MVI   FILETYPE,C'T'       INDICATE READING TALDIR/TALFIL               
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R3,4(R1)            GET INPUT RECORD ADDRESS                     
         USING TLCOD,R3            R3=A(INPUT RECORD)                           
         MVC   SVCMML,TLCOCOM      SAVE INTERNAL CMML CODE                      
*                                                                               
         BRAS  RE,CLRBUFF1                                                      
*                                                                               
         L     R2,=A(BUFFER1)      SET BUFFER POINTER                           
         USING CMLRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,CMLRECL          R1=RECLEN                                    
         GOTO1 AINITALL            INITIALIZE OUTPUT RECORD                     
*                                                                               
         L     R1,=V(RECCML)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     RE,=A(TLPWCMLQ)                                                  
         MVC   CMLRID,0(RE)        MOVE RECID                                   
*                                                                               
         MVC   CMLAGYCL,MYSPACES                                                
         MVC   CMLAGYCL(6),TLCOAGY                                              
         LA    R1,CMLAGYCL+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),TLCOCLI                                                  
         OC    CMLAGYCL,MYSPACES                                                
*                                                                               
         L     R1,=A(PRDPARMS)                                                  
         GOTO1 VBINSRCH,(R1),(1,TLCOAGY)  ADD AGY/CLT/PRD TO PRDTAB             
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MAKE TABLE BIGGER                            
         DROP  R3                                                               
*                                                                               
         GOTO1 VHEXOUT,DMCB,SVCMML,CMLTPCML,4,=C'TOG'                           
*                                                                               
         MVI   ELCODE,TACOELQ      X'72'                                        
         LR    R6,R3                                                            
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACOD,R6                                                         
         MVC   CMLCMLCD(L'TACOCID),TACOCID MOVE 12 CHAR CMML CODE               
*                                                                               
         MVC   CMLCMLTY,=CL5'ISCI'                                              
         CLI   TACOCID+8,C' '      TEST FOR A NINTH CHARACTER                   
         BNH   *+10                                                             
         MVC   CMLCMLTY,=CL5'AD-ID'                                             
         DROP  R6                                                               
*                                                                               
         MVI   ELCODE,TACSELQ      X'73'                                        
         LR    R6,R3                                                            
         BRAS  RE,GETEL                                                         
         BNE   TCML10                                                           
*                                                                               
         USING TACSD,R6                                                         
TCML4    MVC   CMLFLM_DAT,MYSPACES                                              
         MVC   CMLFLM_LOC,MYSPACES                                              
         MVC   CMLRCD_DAT,MYSPACES                                              
         MVC   CMLRCD_LOC,MYSPACES                                              
*                                                                               
TCML6    CLI   TACSTYPE,C'F'       FIND FILM ELEMENT                            
         BE    TCML8                                                            
         BRAS  RE,NEXTEL                                                        
         BNE   TCML10                                                           
         B     TCML6                                                            
*                                                                               
TCML8    LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,TACSDATE),(21,CMLFLM_DAT) GET MMMDD/YYYY            
*                                                                               
         MVC   CMLFLM_LOC(L'TACSCITY),TACSCITY                                  
         LA    RE,CMLFLM_LOC+L'TACSCITY+1  FIND LAST CHAR OF CITY               
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         CLI   TACSLEN,TACSLNQ     TEST ELEM HAS STATE IN IT                    
         BL    *+10                                                             
         MVC   2(2,RE),TACSSTAT                                                 
*                                                                               
         OC    CMLFLM_LOC,MYSPACES                                              
         L     RE,AMXTRT                                                        
         TR    CMLFLM_LOC,0(RE)                                                 
*                                                                               
TCML10   LR    R6,R3                                                            
         BRAS  RE,GETEL            LOOK FOR RECORD ELEMENT                      
         BNE   TCML20                                                           
*                                                                               
TCML12   CLI   TACSTYPE,C'R'                                                    
         BE    TCML14                                                           
*                                                                               
         BRAS  RE,NEXTEL                                                        
         BNE   TCML20                                                           
         B     TCML12                                                           
*                                                                               
TCML14   LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,TACSDATE),(21,CMLRCD_DAT) GET MMMDD/YYYY            
*                                                                               
         MVC   CMLRCD_LOC(L'TACSCITY),TACSCITY                                  
         LA    RE,CMLRCD_LOC+L'TACSCITY+1  FIND LAST CHAR OF CITY               
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         CLI   TACSLEN,TACSLNQ     TEST ELEM HAS STATE IN IT                    
         BL    *+10                                                             
         MVC   2(2,RE),TACSSTAT                                                 
*                                                                               
         OC    CMLRCD_LOC,MYSPACES                                              
         L     RE,AMXTRT                                                        
         TR    CMLRCD_LOC,0(RE)                                                 
         EJECT                                                                  
*===============================================================                
* READ ALL CAST MEMBERS FOR THIS COMMERCIAL                                     
*===============================================================                
                                                                                
K        USING TLCAKEY,KEY                                                      
                                                                                
TCML20   DS    0H                                                               
         XC    KEY,KEY                                                          
         MVI   K.TLCACD,TLCACDQ      X'80'                                      
         MVC   K.TLCACOM,SVCMML                                                 
         MVC   KEYSAVE,KEY                                                      
         GOTO1 HIGH                                                             
*                                                                               
TCML22   CLC   KEY(12),KEYSAVE      SAME CMML                                   
         BNE   TCML30                                                           
*                                                                               
         MVC   SVCKSSN,K.TLCASSN                                                
*&&DO*&& MVC   CMLPRFID,SVCKSSN                                                 
         GOTOR SSNPCK,DMCB,SVCKSSN,CMLPRFID                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1  GET                                                             
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACAD,R6                                                         
         CLC   TACAUN,=C'SAG'                                                   
         BE    TCML24                                                           
         CLC   TACAUN,=C'AFT'                                                   
         BNE   TCML28                                                           
*                                                                               
TCML24   GOTO1 VHEXOUT,DMCB,K.TLCASEQ,CMLCAST,2,=C'TOG'                         
                                                                                
* NOW ADD PERFORMER SSN TO PERFORMER TABLE                                      
                                                                                
         L     R1,ASSNPARM                                                      
         GOTO1 VBINSRCH,(R1),(1,SVCKSSN)  ADD IF NOT THERE                      
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   CMLRECL(CMLRECL,R2),0(R2)   COPY CMML DATA TO NEXT               
                                                                                
* REMEMBER R2 POINTS TO COMMERCIAL DATA IN BUFFER                               
                                                                                
         LA    R2,CMLRECL(R2)      NEXT BUFFER ENTRY                            
*                                                                               
TCML28   GOTO1 SEQ                 READ NEXT CAST RECORD                        
         B     TCML22                                                           
         DROP  K                                                                
*                                                                               
TCML30   XC    NEXTOUT,NEXTOUT                                                  
         XC    0(64,R2),0(R2)      SET END OF BUFFER FLAG                       
*                                                                               
TCML40   ICM   R2,15,NEXTOUT       POINT TO NEXT OUTPUT RECORD                  
         BNZ   *+8                                                              
         L     R2,=A(BUFFER1)      ELSE POINT TO FIRST OUTPUT RECORD            
*                                                                               
         OC    CMLRID,CMLRID       TEST (MORE) PERFORMERS                       
         BZ    TCML42              NO                                           
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R0,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         LHI   R1,CMLRECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT                        
*                                                                               
         LA    R2,CMLRECL(R2)                                                   
         ST    R2,NEXTOUT          SAVE BUFFER POINTER                          
*                                                                               
         MVI   PARM3,0             SET TO PROCESS OUTPUT                        
         J     EXIT                                                             
*                                                                               
TCML42   BRAS  RE,CLRBUFF          CLEAR BUFFER JUST USED                       
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         MVI   PARM3,X'80'         SET NO MORE OUTPUT                           
         J     EXIT                                                             
*                                                                               
         LTORG                                                                  
         DROP  R2                                                               
*======================================================================         
* TAXTRACT CALLS TALFNL WITH PARM3=0 TO BEGIN A NEW COMMERCIAL                  
* ON EXIT, RETURN THE FIRST CAST MEMBER AT ADDR IN EXTRACT RECORD               
*                                                                               
* TAXTRACT THEN RETURNS WITH PARM3=1 FOR THE NEXT CAST MEMBER                   
* ON EXIT, RETURN X'80' IN PARM3 FOR 'NO MORE DATA'                             
*======================================================================         
                                                                                
TALFNLC  NMOD1 0,*TALFNL*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(SSNPARMS)                                                  
         ST    RE,ASSNPARM                                                      
         L     RE,=A(OVRPARMS)                                                  
         ST    RE,AOVRPARM                                                      
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         OC    PARM3,PARM3         TEST BEGIN NEW EXTRACT                       
         BNZ   TFNL60              NO - GO OUTPUT NEXT RECORD                   
*                                                                               
         MVI   FILETYPE,C'T'       INDICATE READING TALDIR/TALFIL               
         MVC   AIO,AIO1                                                         
*                                                                               
         L     R3,4(R1)            GET INPUT RECORD ADDRESS                     
         USING TLCOD,R3            R3=A(INPUT RECORD)                           
         MVC   SVCMML,TLCOCOM      SAVE INTERNAL CMML CODE                      
                                                                                
         L     R0,=A(VRSNTAB)      CLEAR VERSION TABLE                          
         L     R1,=A(VRSNTABX)                                                  
         SR    R1,R0                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         BRAS  RE,CLRBUFF1                                                      
*                                                                               
TFNL2    L     R2,=A(BUFFER1)      SET BUFFER POINTER                           
         USING FNLRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,FNLRECL          R1=RECLEN                                    
         GOTO1 AINITALL            INITIALIZE OUTPUT RECORD                     
*                                                                               
         L     R1,=V(RECFNL)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     RE,=A(TLPWFNLQ)                                                  
         MVC   FNLRID,0(RE)        MOVE RECID                                   
*                                                                               
         MVI   ELCODE,TACOELQ                                                   
         L     R6,AIO                                                           
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACOD,R6                                                         
         LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,TACOAIR),(21,FNLAIRDT)   GET MMMDD/YYYY             
*                                                                               
         MVC   FNLAGYCL,MYSPACES      DDS AGY_CLI                               
         MVC   FNLAGYCL(6),TLCOAGY                                              
         LA    R1,FNLAGYCL+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),TLCOCLI                                                  
         OC    FNLAGYCL,MYSPACES                                                
*                                                                               
         MVC   FNLADVID(6),THISADV    ADVERTISER=CLIENT GROUP                   
         OC    FNLADVID,MYSPACES                                                
*                                                                               
         MVC   FNLAGYID(6),TLCOAGY                                              
*                                                                               
         MVC   FNLPRDID(6),TLCOAGY    PRD=AGY_CLI_PRD                           
         LA    R1,FNLPRDID+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),TLCOCLI                                                  
         LA    R1,6(R1)                                                         
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),TLCOPRD                                                  
         OC    FNLPRDID,MYSPACES                                                
*                                                                               
         CLI   0(R1),C' '          MAKE SURE PRODUCT IS THERE                   
         BH    *+10                                                             
         MVC   FNLPRDID,MYSPACES   IF NOT, GET RID OF ALL OF IT!                
*                                                                               
         L     R1,=A(PRDPARMS)                                                  
         GOTO1 VBINSRCH,(R1),(1,TLCOAGY)  ADD AGY/CLT/PRD TO PRDTAB             
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MAKE TABLE BIGGER                            
         DROP  R3                                                               
*                                                                               
         L     RF,VHEXOUT                                                       
         GOTO1 (RF),DMCB,SVCMML,FNLTPCML,4,=C'TOG'                              
                                                                                
* FILL IN DATA FOR ORIGINAL VERSION                                             
                                                                                
         MVI   ELCODE,TACOELQ      X'72'                                        
         LR    R6,R3                                                            
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACOD,R6                                                         
         L     RE,=A(VRSNTAB)                                                   
         USING VRSNTABD,RE                                                      
*                                                                               
         MVI   VRSNNUM,1                                                        
         MVI   SVHIVER,1                                                        
         MVC   VRSNID,TACOCID                                                   
         MVC   VRSNLEN,TACOSEC                                                  
         MVC   VRSNTYPE,TACOTYPE                                                
         DROP  RE                                                               
*                                                                               
         MVC   FNLPRCML(L'TACOCID),TACOCID   CMML CODE = PARENT CMML            
*                                                                               
         MVC   FNLCMLTY,=CL5'ISCI'                                              
         CLI   TACOCID+8,C' '      TEST FOR A NINTH CHARACTER                   
         BNH   *+10                                                             
         MVC   FNLCMLTY,=CL5'AD-ID'                                             
*                                                                               
         LLC   R0,TACOSEC          CMML LENGTH                                  
         EDIT  (R0),(3,FNLPRLEN),ALIGN=LEFT                                     
         DROP  R6                                                               
*                                                                               
         MVC   FNLPAYCO,=C'TP '                                                 
                                                                                
* BUILD VERSION TABLE *                                                         
*                                                                               
K        USING TLVRD,KEY                                                        
         XC    KEY,KEY                                                          
         MVI   K.TLVRCD,TLVRCDQ      X'C8'                                      
         MVC   K.TLVRCOM,SVCMML                                                 
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
TFNL12   CLC   KEY(17),KEYSAVE                                                  
         BNE   TFNL20                                                           
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACOD,R6                                                         
*                                                                               
         LLC   RE,K.TLVRVER        GET VERSION NUMBER                           
         BCTR  RE,0                                                             
         MHI   RE,L'VRSNTAB                                                     
         L     R0,=A(VRSNTAB)                                                   
         AR    RE,R0               POINT TO ENTRY                               
         USING VRSNTABD,RE                                                      
*                                                                               
         MVC   VRSNNUM,K.TLVRVER                                                
         MVC   VRSNID,TACOCID                                                   
         MVC   VRSNLEN,TACOSEC                                                  
         MVC   VRSNTYPE,TACOTYPE                                                
*                                                                               
         CLC   SVHIVER,VRSNNUM     SAVE HIGH VERSION NUMBER                     
         BH    *+10                                                             
         MVC   SVHIVER,VRSNNUM                                                  
*                                                                               
         GOTO1 SEQ                                                              
         B     TFNL12                                                           
*                                                                               
         DROP  K,R6,RE                                                          
         EJECT                                                                  
*===============================================================                
* READ ALL CAST MEMBERS FOR THIS COMMERCIAL                                     
*===============================================================                
                                                                                
K        USING TLCAKEY,KEY                                                      
                                                                                
TFNL20   DS    0H                                                               
         XC    KEY,KEY                                                          
         MVI   K.TLCACD,TLCACDQ      X'80'                                      
         MVC   K.TLCACOM,SVCMML                                                 
         MVC   KEYSAVE,KEY                                                      
         GOTO1 HIGH                                                             
*                                                                               
TFNL22   CLC   KEY(12),KEYSAVE      SAME CMML                                   
         BNE   TFNL50                                                           
*                                                                               
         MVC   SVCAST,K.TLCASEQ     SAVE CAST SEQNUM                            
         MVC   FNLCTGID,K.TLCACAT    CATEGORY CODE                              
*                                                                               
         MVI   FNLSWEET,C'N'                                                    
         MVI   FNLMULTI,C'N'                                                    
         LA    R1,CTGLIST                                                       
         LA    R0,CTGLISTX-CTGLIST                                              
*                                                                               
TFNL22A  CLC   FNLCTGID,0(R1)                                                   
         BE    TFNL22B                                                          
         LA    R1,L'CTGLIST(R1)                                                 
         BCT   R0,TFNL22A                                                       
         B     TFNL22X                                                          
*                                                                               
TFNL22B  LA    RE,FNLSWEET                                                      
         CLI   3(R1),C'S'                                                       
         BE    *+8                                                              
         LA    RE,FNLMULTI                                                      
         MVI   0(RE),C'Y'                                                       
         B     TFNL22X                                                          
*                                                                               
CTGLIST  DS    0CL4                CATEGORY CODE,MULTI/SWEET FLAG               
         DC    C'GSMM'                                                          
         DC    C'G3MM'                                                          
         DC    C'G6MM'                                                          
         DC    C'G9MM'                                                          
         DC    C'SM M'                                                          
         DC    C'SSMM'                                                          
         DC    C'GSSS'                                                          
         DC    C'SS1S'                                                          
         DC    C'SS2S'                                                          
         DC    C'SS3S'                                                          
         DC    C'SS4S'                                                          
*                                                                               
CTGLISTX EQU   *                                                                
*                                                                               
TFNL22X  GOTO1 VHEXOUT,DMCB,K.TLCASEQ,FNLCAST,2,=C'TOG'                         
*                                                                               
         MVC   SVCKSSN,K.TLCASSN                                                
*&&DO*&& MVC   FNLPRFID,K.TLCASSN                                               
         GOTOR SSNPCK,DMCB,K.TLCASSN,FNLPRFID                                   
         BZ    *+6                                                              
         DC    H'0'                                                             
                                                                                
* NOW ADD PERFORMER SSN TO PERFORMER TABLE                                      
                                                                                
         L     R1,ASSNPARM                                                      
         GOTO1 VBINSRCH,(R1),(1,SVCKSSN)  ADD IF NOT THERE                      
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACAD,R6                                                         
*                                                                               
         CLC   TACAUN,=C'SAG'                                                   
         BE    TFNL24                                                           
         CLC   TACAUN,=C'AFT'                                                   
         BNE   TFNL48                                                           
*                                                                               
TFNL24   MVC   FNLUNION,TACAUN                                                  
*                                                                               
         MVC   FNLCONDT(10),=C'04/01/2009'                                      
         CLC   TACAYEAR,=C'09'                                                  
         BE    TFNL26                                                           
         MVC   FNLCONDT(10),=C'08/27/2008'                                      
         CLC   TACAYEAR,=C'08'                                                  
         BE    TFNL26                                                           
         MVC   FNLCONDT(10),=C'10/30/2006'                                      
         CLC   TACAYEAR,=C'06'                                                  
         BE    TFNL26                                                           
         MVC   FNLCONDT(10),=C'10/30/2003'                                      
         CLC   TACAYEAR,=C'03'                                                  
         BE    TFNL26                                                           
*                                                                               
         MVC   FNLCONDT(8),=C'01/01/20'                                         
         MVC   FNLCONDT+8(2),TACAYEAR                                           
*                                                                               
TFNL26   MVC   FNLCAMCD,TACAONOF                                                
*                                                                               
         MVC   SVGUARCD,TACAGUA    SAVE GUARANTEE CODE                          
*                                                                               
         BRAS  RE,BLDOVR           ADD OVERSCALE ENTRIES IF NECESSARY           
*                                                                               
TFNL30   L     R6,AIO              LOOK FOR VERSION ELEMENT                     
         MVI   ELCODE,TAFNELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   TFNL34                                                           
*                                                                               
         USING TAFND,R6                                                         
TFNL32   CLI   TAFNTYPE,TAFNTVER   TEST TYPE=VERSION                            
         BE    TFNL36                                                           
         BRAS  RE,NEXTEL                                                        
         BE    TFNL32                                                           
*                                                                               
TFNL34   LA    R6,=X'FF040001'     POINT TO A DUMMY ELEMENT                     
*                                                                               
TFNL36   L     RE,=A(SVELEM)                                                    
         XC    0(L'SVELEM,RE),0(RE)                                             
         LLC   R5,1(R6)            GET ELEMENT LENGTH                           
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RE),0(R6)       SAVE THE VERSION ELEMENT                     
*                                                                               
         MVC   SVCASTKY,KEY        SAVE CURRENT CAST KEY                        
*                                                                               
         MVC   FNLGUAR,MYSPACES                                                 
         MVC   FNLGUART,MYSPACES                                                
         OC    SVGUARCD,SVGUARCD                                                
         BZ    TFNL38                                                           
*                                                                               
         BRAS  RE,GETGFNL          GET GUARANTEE DATA IF ANY                    
         MVC   AIO,AIO1            RESTORE AIO ADDRESS                          
         MVC   KEY,SVCASTKY                                                     
         GOTO1 HIGH                RESTORE TALDIR KEY                           
         CLC   KEY(20),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
TFNL38   AHI   R5,-2               ENTRY COUNT (SUBTRACTED 1 BEFORE)            
         BP    *+6                                                              
         DC    H'0'                                                             
         L     RE,=A(SVELEM)                                                    
         LA    R6,3(RE)            POINT TO VERSION DATA!                       
*                                                                               
         CLI   0(R6),251           TEST IN ALL VERSIONS                         
         BNE   TFNL46                                                           
                                                                                
* PUT OUT ONE RECORD FOR EACH VERSION IN VRSNTAB                                
                                                                                
         L     R4,=A(VRSNTAB)                                                   
         LLC   R5,SVHIVER                                                       
*                                                                               
TFNL40   CLI   0(R4),0             TEST NO ENTRY THIS VERSION                   
         BE    TFNL42              IT'S NOT THERE                               
*                                                                               
         BRAS  RE,BLDVRSN                                                       
*                                                                               
         MVC   FNLRECL(FNLRECL,R2),0(R2)   COPY TO NEXT BUFFER ENTRY            
         LA    R2,FNLRECL(R2)              POINT TO IT                          
         MVC   FNLCHCML,MYSPACES           KILL CHILD FIELDS                    
         MVC   FNLCHLEN,MYSPACES                                                
*                                                                               
TFNL42   LA    R4,L'VRSNTAB(R4)    POINT TO NEXT VERSION                        
         BCT   R5,TFNL40                                                        
         B     TFNL48                                                           
*                                                                               
TFNL46   LLC   R4,0(R6)            GET THIS VERSION NUMBER                      
         BCTR  R4,0                                                             
         MHI   R4,L'VRSNTAB                                                     
         A     R4,=A(VRSNTAB)                                                   
         BRAS  RE,BLDVRSN                                                       
*                                                                               
         MVC   FNLRECL(FNLRECL,R2),0(R2)   COPY TO NEXT BUFFER ENTRY            
         LA    R2,FNLRECL(R2)              POINT TO IT                          
         MVC   FNLCHCML,MYSPACES           KILL CHILD FIELDS                    
         MVC   FNLCHLEN,MYSPACES                                                
*                                                                               
         LA    R6,1(R6)            NEXT VERSION ENTRY                           
         BCT   R5,TFNL46                                                        
*                                                                               
TFNL48   GOTO1 SEQ                 READ NEXT CAST RECORD                        
         B     TFNL22                                                           
                                                                                
*========================================================                       
* ALL CAST MEMBERS READ - PUT RECORDS NOW                                       
*========================================================                       
                                                                                
TFNL50   XC    NEXTOUT,NEXTOUT                                                  
         XC    0(64,R2),0(R2)      SET END OF BUFFER FLAG                       
*                                                                               
TFNL60   ICM   R2,15,NEXTOUT       POINT TO NEXT OUTPUT RECORD                  
         BNZ   *+8                                                              
         L     R2,=A(BUFFER1)      ELSE POINT TO FIRST OUTPUT RECORD            
*                                                                               
         OC    FNLRID,FNLRID       TEST (MORE) PERFORMERS                       
         BZ    TFNL62              NO                                           
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R0,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         LHI   R1,FNLRECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT                        
*                                                                               
         LA    R2,FNLRECL(R2)                                                   
         ST    R2,NEXTOUT          SAVE BUFFER POINTER                          
*                                                                               
         MVI   PARM3,0             SET TO PROCESS OUTPUT                        
         J     EXIT                                                             
*                                                                               
TFNL62   BRAS  RE,CLRBUFF                                                       
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         MVI   PARM3,X'80'         SET NO MORE OUTPUT                           
         J     EXIT                                                             
         DROP  R2                                                               
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*============================================================                   
* READ GUARANTEE DATA FOR FINAL CAST RECORDS                                    
* NOTE READS RECORDS INTO AIO2                                                  
*============================================================                   
                                                                                
         USING FNLRECD,R2                                                       
GETGFNL  NTR1                                                                   
         MVI   FILETYPE,C'T'       READ TALENT FILES                            
         MVC   AIO,AIO2            INTO IOA2                                    
*                                                                               
K        USING TLGUD,KEY                                                        
*                                                                               
         XC    KEY,KEY                                                          
         MVI   K.TLGUCD,TLGUCDQ    X'6C'                                        
         MVC   K.TLGUSSN,SVCKSSN                                                
         MVC   K.TLGUGUA,SVGUARCD                                               
         XC    K.TLGUGUA,=4X'FF'                                                
         DROP  K                                                                
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAGUELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAGUD,R6                                                         
         OC    TAGUCOM,TAGUCOM     TEST CMML PRESENT                            
         BNZ   GETGFNL2                                                         
*                                                                               
         MVC   FNLGUART,=C'LO'     SET LARGE OVERSCALE FLAG                     
         L     R0,TAGUAMT          THIS IS LARGE OVERSCALE GUAR                 
         LA    R4,FNLGUAR                                                       
         BRAS  RE,GETDOL                                                        
         B     GETGFNLX                                                         
                                                                                
*==================================================================             
* DEAL WITH PER CYCLE GUARANTEE                                                 
*==================================================================             
                                                                                
GETGFNL2 MVC   FNLGUART,=C'PC'     SET PER CYCLE FLAG                           
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAGCELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   GETGFNLX                                                         
*                                                                               
         USING TAGCD,R6                                                         
         L     R0,TAGCAMT          OUTPUT CYCLE GUAR AMOUNT                     
         LA    R4,FNLGUAR                                                       
         BRAS  RE,GETDOL                                                        
*                                                                               
GETGFNLX J     EXIT                                                             
         EJECT                                                                  
*================================================================               
* FILL IN A TABLE ENTRY FOR THIS VERSION                                        
* ON ENTRY R4 POINTS TO VERSION TABLE ENTRY                                     
*          R2 POINTS TO BUFFER ENTRY                                            
*================================================================               
                                                                                
BLDVRSN  NTR1                                                                   
         USING VRSNTABD,R4                                                      
         USING FNLRECD,R2                                                       
*                                                                               
         MVC   FNLLANG,=C'ENGLISH'                                              
         CLI   VRSNTYPE,C'S'                                                    
         BNE   *+10                                                             
         MVC   FNLLANG,=C'SPANISH'                                              
*                                                                               
         MVI   FNLINORG,C'Y'                                                    
         MVI   FNLINEDT,C'N'                                                    
         L     RE,=A(SVELEM)                                                    
         CLI   3(RE),251           TEST IN ALL VERSIONS                         
         BE    BLDVRS2                                                          
         CLI   3(RE),1             TEST SPECIFICALLY IN VERSION 1               
         BE    BLDVRS2                                                          
         MVI   FNLINORG,C'N'                                                    
*                                                                               
BLDVRS2  MVI   FNLPRCH,C'P'        SET DOING PARENT                             
         CLI   VRSNNUM,1           TEST DOING ORIGINAL NOW                      
         BE    BLDVRSX             YES                                          
         MVI   FNLPRCH,C'C'        IF NOT, SET AS CHILD                         
         MVC   FNLCHCML,VRSNID     SET CHILD CMML CODE                          
         LLC   R0,VRSNLEN                                                       
         EDIT  (R0),(3,FNLCHLEN),ALIGN=LEFT                                     
         MVI   FNLINEDT,C'Y'                                                    
*                                                                               
BLDVRSX  J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*================================================================               
* ADD OVERSCALE TABLE ENTRIES IF NEEDED                                         
*================================================================               
                                                                                
BLDOVR   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
W        USING OVRTABD,WORK                                                     
         XC    WORK,WORK                                                        
         MVC   W.OVRTBCML,SVCMML                                                
         MVC   W.OVRTBCST,SVCAST                                                
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAOAELQ      LOOK FOR OVERSCALE AMT ELEM                  
         BRAS  RE,GETEL                                                         
         BNE   BLDOVR20                                                         
*                                                                               
         USING TAOAD,R6                                                         
         LLC   R0,TAOANUM          GET NUMBER OF OVERSCALE AMOUNTS              
*                                                                               
         XC    SVSUBEL,SVSUBEL                                                  
         LA    R6,3(R6)            POINT TO FIRST SUB-EL                        
         USING TAOASBEL,R6                                                      
*                                                                               
BLDOVR12 MVC   W.OVRTBCOD,=C'OA '                                               
         BAS   RE,FLTUSE                                                        
         BNE   BLDOVR14                                                         
*                                                                               
         MVC   W.OVRTBUSE,TAOAUSE                                               
         ICM   RE,15,TAOAAMT                                                    
         MHI   RE,100              SCALE TO 4 DECIMALS                          
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         BAS   RE,ADDUSE                                                        
*                                                                               
BLDOVR14 LA    R6,L'TAOASBEL(R6)    NEXT SUBEL                                  
         BCT   R0,BLDOVR12                                                      
*                                                                               
         OC    SVSUBEL,SVSUBEL                                                  
         BZ    BLDOVR20                                                         
*                                                                               
         MVC   W.OVRTBUSE,SVSUBEL                                               
         ICM   RE,15,SVSUBEL+3                                                  
         MHI   RE,100              SCALE TO 4 DECIMALS                          
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         LA    R6,SVSUBEL                                                       
         BAS   RE,ADDUSE                                                        
         DROP  R6                                                               
*                                                                               
BLDOVR20 L     R6,AIO                                                           
         MVI   ELCODE,TACAELQ      LOOK FOR CAST DETAIL ELEM                    
         BRAS  RE,GETEL                                                         
         BNE   BLDOVR30                                                         
*                                                                               
         USING TACAD,R6                                                         
*                                                                               
         OC    TACAOV2,TACAOV2     TEST OLD STYLE 2ND OVERSCALE AMT             
         BZ    BLDOVR30                                                         
*                                                                               
         MVC   W.OVRTBCOD,=C'OP2'  IT'S A PERCENTAGE                            
         MVC   W.OVRTBUSE,=C'ARE'  ALWAYS GENERATES ALL USES                    
*                                                                               
         ICM   RE,15,TACAOV2                                                    
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         LA    R6,=C'ARE'                                                       
         BAS   RE,ADDUSE                                                        
         B     BLDOVR40                                                         
         DROP  R6                                                               
*                                                                               
BLDOVR30 L     R6,AIO                                                           
         MVI   ELCODE,TAO2ELQ      LOOK FOR OVERSCALE AMT ELEM                  
         BRAS  RE,GETEL                                                         
         BNE   BLDOVR40                                                         
*                                                                               
         USING TAO2D,R6                                                         
         LLC   R0,TAO2NUM          GET NUMBER OF OVERSCALE AMOUNTS              
*                                                                               
         XC    SVSUBEL,SVSUBEL                                                  
         LA    R6,3(R6)            POINT TO FIRST SUB-EL                        
         USING TAO2SBEL,R6                                                      
*                                                                               
BLDOVR32 MVC   W.OVRTBCOD,=C'OP2'                                               
         BAS   RE,FLTUSE                                                        
         BNE   BLDOVR34                                                         
*                                                                               
         MVC   W.OVRTBUSE,TAO2USE                                               
         ICM   RE,15,TAO2PCT                                                    
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         BAS   RE,ADDUSE                                                        
*                                                                               
BLDOVR34 LA    R6,L'TAO2SBEL(R6)   NEXT SUBEL                                   
         BCT   R0,BLDOVR32                                                      
*                                                                               
         OC    SVSUBEL,SVSUBEL                                                  
         BZ    BLDOVR40                                                         
*                                                                               
         MVC   W.OVRTBUSE,SVSUBEL                                               
         ICM   RE,15,SVSUBEL+3                                                  
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         LA    R6,SVSUBEL                                                       
         BAS   RE,ADDUSE                                                        
         DROP  R6                                                               
                                                                                
*================================================================               
* LOOK FOR OVERSCALE PERCENT ELEM                                               
*================================================================               
                                                                                
BLDOVR40 L     R6,AIO                                                           
         MVI   ELCODE,TAOPELQ      LOOK FOR OVERSCALE PCT ELEM                  
         BRAS  RE,GETEL                                                         
         BNE   BLDOVRX                                                          
*                                                                               
         USING TAOPD,R6                                                         
         LLC   R0,TAOPNUM          GET NUMBER OF OVERSCALE AMOUNTS              
*                                                                               
         XC    SVSUBEL,SVSUBEL                                                  
         LA    R6,3(R6)            POINT TO FIRST SUB-EL                        
         USING TAOPSBEL,R6                                                      
*                                                                               
BLDOVR42 MVC   W.OVRTBCOD,=C'OP '                                               
         BAS   RE,FLTUSE                                                        
         BNE   BLDOVR44                                                         
*                                                                               
         MVC   W.OVRTBUSE,TAOPUSE                                               
*                                                                               
         ICM   RE,15,TAOPPCT                                                    
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         BAS   RE,ADDUSE                                                        
*                                                                               
BLDOVR44 LA    R6,L'TAOASBEL(R6)   NEXT SUBEL                                   
         BCT   R0,BLDOVR42                                                      
*                                                                               
         OC    SVSUBEL,SVSUBEL                                                  
         BZ    BLDOVRX                                                          
*                                                                               
         MVC   W.OVRTBUSE,SVSUBEL                                               
         ICM   RE,15,SVSUBEL+3                                                  
         STCM  RE,15,W.OVRTBAMT                                                 
*                                                                               
         LA    R6,SVSUBEL                                                       
         BAS   RE,ADDUSE                                                        
         DROP  R6                                                               
*                                                                               
BLDOVRX  J     EXIT                                                             
*                                                                               
FLTUSE   NTR1                                                                   
         CLC   0(3,R6),=C'ARE'     ARE ALWAYS WINS                              
         BE    FLTUSE4                                                          
         CLC   0(3,R6),=C'   '                                                  
         BNE   FLTUSE10                                                         
         CLC   SVSUBEL(3),=C'ARE'  SPACES LOSE TO ARE                           
         JE    NEQXIT                                                           
FLTUSE4  MVC   SVSUBEL,0(R6)       SAVE ALLUSE SUBEL                            
         J     NEQXIT              AND PROCESS LATER                            
*                                                                               
FLTUSE10 LA    RE,USELIST                                                       
         LA    RF,USELISTN                                                      
*                                                                               
FLTUSE12 CLC   0(3,R6),0(RE)                                                    
         JE    EQXIT                                                            
         LA    RE,3(RE)                                                         
         BCT   RF,FLTUSE12                                                      
         J     NEQXIT                                                           
*                                                                               
ADDUSE   NTR1                                                                   
         CLC   0(3,R6),=C'ARE'                                                  
         BE    ADDUSE2                                                          
         CLC   0(3,R6),=C'   '                                                  
         BNE   ADDUSE10                                                         
*                                                                               
ADDUSE2  LA    R4,USELIST                                                       
         LA    R5,USELISTN                                                      
         B     ADDUSE12                                                         
*                                                                               
ADDUSE10 LR    R4,R6               POINT R4 TO USECODE                          
         LA    R5,1                SET TO ADD THIS ONE CODE                     
*                                                                               
ADDUSE12 MVC   W.OVRTBUSE,0(R4)    MOVE USETYPE TO RECORD                       
*                                                                               
         L     R1,AOVRPARM                                                      
         GOTO1 VBINSRCH,(R1),(1,WORK)     ADD IF NOT THERE                      
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R4,3(R4)                                                         
         BCT   R5,ADDUSE12                                                      
         J     EXIT                                                             
*                                                                               
USELIST  DC    C'CBL'                                                           
         DC    C'CLA'                                                           
         DC    C'LNA'                                                           
         DC    C'LNC'                                                           
         DC    C'LNN'                                                           
         DC    C'PAX'                                                           
         DC    C'SCB'                                                           
         DC    C'SNT'                                                           
USELISTN EQU   (*-USELIST)/3                                                    
         LTORG                                                                  
         EJECT                                                                  
*=================================================================              
* OUTPUT RESIDUAL DATA                                                          
*=================================================================              
                                                                                
TALRESC  NMOD1 0,*TALRES*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(SSNPARMS)                                                  
         ST    RE,ASSNPARM                                                      
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         OC    PARM3,PARM3         TEST BEGIN NEW EXTRACT                       
         BNE   TRES40              NO - GO OUTPUT NEXT RECORD                   
*                                                                               
         BRAS  RE,CLRBUFF1                                                      
*                                                                               
         L     R2,=A(BUFFER1)      SET BUFFER POINTER                           
         USING RESRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,RESRECL          R1=RECLEN                                    
         GOTO1 AINITALL            INITIALIZE OUTPUT RECORD                     
*                                                                               
         L     R1,=V(RECRES)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
                                                                                
* SAVE DATA FROM INVOICE RECORD                                                 
         L     R6,AIO1                                                          
         USING TLIND,R6                                                         
         MVC   SVINV,TLININV       INVOICE NUMBER COMPLEMENTED!                 
         MVC   SVAGY,TLINAGY       AGENCY CODE                                  
         DROP  R6                                                               
                                                                                
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAPDD,R6                                                         
         MVC   SVCMML,TAPDCOM      SAVE INTERNAL CMML NUMBER                    
         MVC   SVUSETYP,TAPDUSE    SAVE USE TYPE                                
         MVC   SVCYCSTR,TAPDCYCS   SAVE CYCLE START                             
         MVC   SVCYCEND,TAPDCYCE   AND END                                      
         MVC   SVPAYST,TAPDPST1    SAVE PAYMENT STATUS                          
         MVC   SVCLT,TAPDCLI                                                    
*                                                                               
         MVC   WORK(6),SVAGY                                                    
         MVC   WORK+6(6),TAPDCLI                                                
         MVC   WORK+12(6),TAPDPRD                                               
         L     R1,=A(PRDPARMS)                                                  
         GOTO1 VBINSRCH,(R1),(1,WORK)  ADD AGY/CLT/PRD TO PRDTAB                
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MAKE TABLE BIGGER                            
         DROP  R6                                                               
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAIND,R6                                                         
         MVC   SVINVDT,TAINBDTE    SAVE INVOICE DATE                            
         MVC   SVINSTAT,TAINSTAT                                                
         DROP  R6                                                               
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACOD,R6                                                         
         MVC   SVCMMLID,TACOCID    SAVE CMML ID                                 
         DROP  R6                                                               
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAVRELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   *+10                                                             
         USING TAVRD,R6                                                         
         MVC   SVCMMLID,TAVRCID    USE CHILD ID IF PRESENT                      
*                                                                               
         BRAS  RE,GETCMML          UPDATE SVCMMLID IF NEEDED                    
         DROP  R6                                                               
*                                                                               
* READ CHECK RECORDS FOR THIS INVOICE                                           
*                                                                               
K        USING TLCKD,KEY                                                        
         XC    KEY,KEY                                                          
         MVI   K.TLCKCD,TLCKCDQ                                                 
*                                                                               
         L     R6,AIO1                                                          
         USING TLIND,R6                                                         
         MVC   K.TLCKAGY,TLINAGY                                                
         MVC   K.TLCKINV,TLININV                                                
         XC    K.TLCKINV,=6X'FF'   UNCOMPLEMENT                                 
*                                                                               
         GOTO1 =V(TINVCON),DMCB,K.TLCKINV,RESINV,VDATCON                        
*                                                                               
TRES10   MVI   FILETYPE,C'C'       INDICATE READING CHKDIR/CHKFIL               
         MVC   AIO,AIO2                                                         
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
TRES12   CLC   KEY(13),KEYSAVE     SAME AGENCY/INVOICE                          
         JNE   TRES30              NO                                           
*                                                                               
         MVC   SVCHKKEY,KEY        SAVE CHECK KEY!                              
         MVC   SVCKSORT,K.TLCKSORT SAVE SORT CODE FROM KEY                      
         MVC   SVCKSSN,K.TLCKSSN   SAVE SSN FROM KEY                            
*                                                                               
         CLC   SVCKSORT+4(2),=X'FFFF' IF NOT A REAL SEQNUM                      
         BE    TRES25                 JUST SKIP IT                              
*                                                                               
         GOTO1 GET                 GET THE CHECK RECORD                         
*                                                                               
         L     R6,AIO2                                                          
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   TRES14                                                           
         USING TACDD,R6                                                         
         TM    TACDSTAT,TACDSTRS   SKIP TRUSTEE CHECKS                          
         BO    TRES25                                                           
         DROP  R6                                                               
*                                                                               
TRES14   L     RE,=A(TLPWRESQ)                                                  
         MVC   RESRID,0(RE)        MOVE RECID                                   
*                                                                               
         MVC   RESGUART,MYSPACES   CLEAR GUARANTEE TYPE                         
         MVC   RESGUAR,MYSPACES    AND AMOUNT                                   
         MVC   RESGUARR,MYSPACES   AND REMAINDER                                
*                                                                               
         L     RF,VHEXOUT                                                       
         GOTO1 (RF),DMCB,SVCMML,RESTPCML,4,=C'TOG'                              
*                                                                               
         MVC   RESAGYCL,MYSPACES   BUILD TP AGY_CLT FIELD                       
         MVC   RESAGYCL(6),SVAGY                                                
         LA    R1,RESAGYCL+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),SVCLT                                                    
         OC    RESAGYCL,MYSPACES                                                
*                                                                               
         MVC   RESCMLCD,SVCMMLID                                                
         MVC   RESCMLTY,=CL5'ISCI'                                              
         CLI   SVCMMLID+8,C' '                                                  
         BNH   *+10                                                             
         MVC   RESCMLTY,=CL5'AD-ID'                                             
*                                                                               
         MVC   RESUSECD,SVUSETYP                                                
         MVI   RESDRCR,C'D'        ASSUME DEBIT STATUS                          
*                                                                               
         TM    SVPAYST,TAPDPCRD    TEST CREDIT                                  
         BZ    *+8                                                              
         MVI   RESDRCR,C'C'                                                     
*                                                                               
         TM    SVINSTAT,TAINSCIN   TEST CREDIT                                  
         BZ    *+8                                                              
         MVI   RESDRCR,C'C'                                                     
*                                                                               
TRES16   LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,SVCYCSTR),(21,RESCYSTR)                             
         GOTO1 (RF),(R1),(1,SVCYCEND),(21,RESCYEND)                             
         GOTO1 (RF),(R1),(1,SVINVDT),(21,RESINVDT)                              
*                                                                               
*&&DO*&& MVC   RESPRFID,SVCKSSN                                                 
         GOTOR SSNPCK,DMCB,SVCKSSN,RESPRFID                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
         GOTO1 VHEXOUT,DMCB,SVCKSORT+4,RESCAST,2,=C'TOG'                        
*                                                                               
         MVC   RESAGYID,SVAGY                                                   
                                                                                
* NOW ADD PERFORMER SSN TO PERFORMER TABLE                                      
                                                                                
         L     R1,ASSNPARM                                                      
         GOTO1 VBINSRCH,(R1),(1,SVCKSSN)  ADD IF NOT THERE                      
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R6,AIO2                                                          
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R6                                                         
*                                                                               
         MVC   SVUSESPD,TAPDUSES   SAVE USES PAID                               
         MVC   SVAPPCOD,TAPDACDE   SAVE APPLY CODE                              
         MVC   SVAPPL,TAPDAPPL     SAVE APPLIED CREDIT                          
*                                                                               
         ICM   R0,15,TAPDGRS                                                    
         LA    R4,RESPRFGR                                                      
         BRAS  RE,GETDOL                                                        
         MVI   RESPRFSC,C'N'                                                    
         MVI   RESPRFHC,C'N'                                                    
         MVC   RESPRFSF,MYSPACES   CLEAR PREVIOUS FEES                          
         MVC   RESPRFHF,MYSPACES   CLEAR PREVIOUS FEES                          
*                                                                               
         MVI   FILETYPE,C'T'       BACK TO TALENT FILES                         
         MVC   AIO,AIO1            BACK TO IOA1                                 
*                                                                               
         CLI   TAPDACDE,C'4'                                                    
         BNE   TRES20                                                           
*                                                                               
         L     R6,AIO2                                                          
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACAD,R6                                                         
         CLC   TACAUN,=C'SAG'                                                   
         BE    TRES18                                                           
         CLC   TACAUN,=C'AFT'                                                   
         BE    TRES18                                                           
         B     TRES25                                                           
*                                                                               
TRES18   MVC   SVGUARCD,TACAGUA                                                 
         DROP  R6                                                               
         CLI   SVAPPCOD,C'4'       TEST APPLIED AGAINST GUAR                    
         BNE   *+8                                                              
         BAS   RE,GETGUAR          GO GET GUARANTEE                             
*                                                                               
TRES20   BAS   RE,GETUSES                                                       
*                                                                               
         OC    SVAPPL,SVAPPL       TEST APPLIED INDICATOR                       
         BZ    TRES22                                                           
         BAS   RE,GETFEES                                                       
*                                                                               
TRES22   LA    R0,RESRECL(R2)      POINT TO NEXT RECORD SLOT                    
         LHI   R1,RESRECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY THIS RECORD TO NEXT                     
                                                                                
         LA    R2,RESRECL(R2)      NOW ADVANCE TO NEXT BUFFER ENTRY             
*                                                                               
TRES25   MVC   KEY,SVCHKKEY        RESTORE LAST CHECK KEY                       
         MVI   FILETYPE,C'C'       READING CHKFILE                              
         MVC   AIO,AIO2                                                         
         GOTO1 HIGH                                                             
         GOTO1 SEQ                                                              
         B     TRES12                                                           
*                                                                               
TRES30   XC    NEXTOUT,NEXTOUT                                                  
         XC    0(64,R2),0(R2)      SET END OF BUFFER FLAG                       
*                                                                               
TRES40   ICM   R2,15,NEXTOUT       POINT TO NEXT OUTPUT RECORD                  
         BNZ   *+8                                                              
         L     R2,=A(BUFFER1)      ELSE POINT TO FIRST OUTPUT RECORD            
*                                                                               
         OC    RESRID,RESRID       TEST (MORE) PERFORMERS                       
         BZ    TRES42              NO                                           
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R0,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         LHI   R1,RESRECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT                        
*                                                                               
         LA    R2,RESRECL(R2)                                                   
         ST    R2,NEXTOUT          SAVE BUFFER POINTER                          
*                                                                               
         MVI   PARM3,0             SET TO PROCESS OUTPUT                        
         J     EXIT                                                             
*                                                                               
TRES42   BRAS  RE,CLRBUFF                                                       
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         MVI   PARM3,X'80'         SET NO MORE OUTPUT                           
         J     EXIT                                                             
         EJECT                                                                  
*============================================================                   
* READ GUARANTEE DATA FOR RESIDUALS                                             
*============================================================                   
                                                                                
GETGUAR  NTR1                                                                   
         XC    SVGRSTR(6),SVGRSTR  CLEAR GUAR CYCLE DATES                       
*                                                                               
         MVI   FILETYPE,C'T'       READ TALENT FILES                            
         MVC   AIO,AIO1            INTO IOA1                                    
*                                                                               
K        USING TLGUD,KEY                                                        
*                                                                               
         XC    KEY,KEY                                                          
         MVI   K.TLGUCD,TLGUCDQ    X'6C'                                        
         MVC   K.TLGUSSN,SVCKSSN                                                
         MVC   K.TLGUGUA,SVGUARCD                                               
         XC    K.TLGUGUA,=4X'FF'                                                
         DROP  K                                                                
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         JNE   EXIT        <=====  IGNORE MISSING GUARANTEE                     
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAGUELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAGUD,R6                                                         
         OC    TAGUCOM,TAGUCOM     TEST CMML PRESENT                            
         BNZ   GETGU10                                                          
*                                                                               
         MVC   RESGUART,=C'LO'                                                  
         MVI   BYTE,C'L'           SET LARGE OVERSCALE FLAG                     
         L     R0,TAGUAMT          THIS IS LARGE OVERSCALE GUAR                 
         LA    R4,RESGUAR                                                       
         BRAS  RE,GETDOL                                                        
         B     GETGU20                                                          
                                                                                
*==================================================================             
* DEAL WITH PER CYCLE GUARANTEE                                                 
*==================================================================             
                                                                                
GETGU10  MVC   RESGUART,=C'PC'                                                  
         MVI   BYTE,C'C'           SET PER CYCLE FLAG                           
         L     R6,AIO1                                                          
         MVI   ELCODE,TAGCELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAGCD,R6                                                         
GETGU12  CLC   SVCYCSTR,TAGCEND    CYCLE START AFTER ELEM END                   
         BH    GETGU14                                                          
         CLC   SVCYCEND,TAGCSTRT   CYCLE END BEFORE ELEM STR                    
         BL    GETGU14                                                          
         B     GETGU16                                                          
GETGU14  BRAS  RE,NEXTEL                                                        
         BE    GETGU12                                                          
         DC    H'0'                                                             
*                                                                               
GETGU16  MVC   SVGRSTR,TAGCSTRT    SAVE GUAR CYCLE DATES                        
         MVC   SVGREND,TAGCEND                                                  
*                                                                               
         L     R0,TAGCAMT          OUTPUT CYCLE GUAR AMOUNT                     
         LA    R4,RESGUAR                                                       
         BRAS  RE,GETDOL                                                        
         EJECT                                                                  
*===============================================================                
* NOW LOOK UP BALANCE AFTER THIS PAYMENT                                        
*===============================================================                
                                                                                
K        USING TLGTD,KEY                                                        
*                                                                               
GETGU20  XC    KEY,KEY                                                          
         MVI   K.TLGTCD,TLGTCDQ                                                 
         MVC   K.TLGTSSN,SVCKSSN                                                
         MVC   K.TLGTGUA,SVGUARCD  GUARANTEE CODE                               
         CLI   BYTE,C'L'           TEST LARGE OVERSCALE                         
         BE    *+16                                                             
         MVC   K.TLGTSTRT,SVGRSTR                                               
         MVC   K.TLGTEND,SVGREND                                                
         DROP  K                                                                
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
GETGU22  CLC   KEY(28),KEYSAVE                                                  
         JNE   EXIT   <=========== IGNORE IF MISSING                            
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAGTELQ      X'87'                                        
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAGTD,R6                                                         
*                                                                               
         MVC   DUB(6),TAGTINV                                                   
         XC    DUB,=6X'FF'                                                      
         CLC   SVINV,DUB                                                        
         BE    GETGU24                                                          
         GOTO1 SEQ                                                              
         B     GETGU22                                                          
*                                                                               
GETGU24  L     R0,TAGTBAL                                                       
         LA    R4,RESGUARR         GUAR REMAINING BAL                           
         BRAS  RE,GETDOL                                                        
         J     EXIT                                                             
         EJECT                                                                  
*============================================================                   
* GET USE COUNT IF NEEDED                                                       
*============================================================                   
                                                                                
GETUSES  NTR1                                                                   
*                                                                               
         CLC   SVUSETYP,=C'CLA'                                                 
         BE    GETUSE2                                                          
         CLC   SVUSETYP,=C'LNA'                                                 
         BE    GETUSE2                                                          
         CLC   SVUSETYP,=C'LNC'                                                 
         BE    GETUSE2                                                          
         CLC   SVUSETYP,=C'LNN'                                                 
         BE    GETUSE2                                                          
         CLC   SVUSETYP,=C'PAX'                                                 
         BE    GETUSE2                                                          
         J     EXIT                                                             
*                                                                               
K        USING TLUHD,KEY                                                        
*                                                                               
GETUSE2  MVC   RESUSECT,MYSPACES   CLEAR                                        
         XC    KEY,KEY                                                          
*                                                                               
         MVI   K.TLUHCD,TLUHCDQ    X'98'                                        
         MVC   K.TLUHCOM,SVCMML                                                 
         MVC   K.TLUHCSEQ,SVCKSORT+4  CAST SEQNUM                               
         MVC   K.TLUHUSE,SVUSETYP                                               
         MVC   K.TLUHINV,SVINV                                                  
*                                                                               
         MVI   FILETYPE,C'T'       READ TALENT FILES                            
         MVC   AIO,AIO1            INTO IOA1                                    
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(30),KEYSAVE                                                  
         BE    GETUSE4                                                          
*                                                                               
         MVC   KEY,KEYSAVE             RESTORE KEY                              
         XC    K.TLUHCSEQ,K.TLUHCSEQ   CLEAR CAST SEQNUM                        
         GOTO1 HIGH                                                             
         CLC   KEY(30),KEYSAVE                                                  
         JNE   EXIT  <<================ SHOULD BE DC H'0' IF NOT FOUND!         
         DROP  K                                                                
*                                                                               
GETUSE4  GOTO1 GET                                                              
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAUHELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAUHD,R6                                                         
         LH    R0,SVUSESPD                                                      
         EDIT  (R0),(4,RESUSECT),ALIGN=LEFT                                     
         J     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
*=================================================================              
* GET SESSION FEE OR HOLDING FEE CREDIT AMOUNTS                                 
*=================================================================              
                                                                                
GETFEES  NTR1                                                                   
*                                                                               
         TM    SVINSTAT,TAINSCIN   TEST CANCELLER INVOICE                       
         JO    EXIT                                                             
*                                                                               
K        USING TLCAPD,KEY                                                       
*                                                                               
         XC    KEY,KEY                                                          
*                                                                               
         MVI   K.TLCAPCD,TLCACCDQ    X'81'                                      
         MVC   K.TLCACSSN,SVCKSSN                                               
         MVC   K.TLCACCOM,SVCMML                                                
*                                                                               
         MVI   FILETYPE,C'T'       READ TALENT FILES                            
         MVC   AIO,AIO1            INTO IOA1                                    
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
GETFEE2  CLC   KEY(27),KEYSAVE                                                  
         JNE   EXIT                                                             
*                                                                               
         CLC   K.TLCACSEQ,SVCKSORT+4 MATCH CAST SEQNUM                          
         BE    GETFEE4                                                          
         DROP  K                                                                
*                                                                               
GETFEE2A GOTO1 SEQ                                                              
         B     GETFEE2                                                          
*                                                                               
GETFEE4  GOTO1 GET                                                              
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TACRELQ      X'68'                                        
         BRAS  RE,GETEL                                                         
         JNE   EXIT                                                             
         USING TACRD,R6                                                         
*                                                                               
GETFEE6  CLC   SVCYCEND,TACRSTRT   CYCLE END BEFORE ELEM START                  
         BL    GETFEE8                                                          
         CLC   SVCYCSTR,TACREND    CYCLE START AFTER ELEM END                   
         BNH   GETFEE10            NO - USE THIS ELEM                           
*                                                                               
GETFEE8  BRAS  RE,NEXTEL                                                        
         BNE   GETFEE2A                                                         
*                                                                               
GETFEE10 LA    R4,RESPRFSF         POINT TO SESSION DOLLARS                     
         LA    R5,RESPRFSC         POINT TO SESSION FLAG                        
*                                                                               
         CLC   TACRUSE,=C'HLD'                                                  
         BE    GETFEE12                                                         
         CLC   TACRUSE,=C'SHL'                                                  
         BE    GETFEE12                                                         
         CLC   TACRUSE,=C'ADH'                                                  
         BE    GETFEE12                                                         
*                                                                               
         LA    R4,RESPRFHF         HOLDING FEE CREDIT DOLLARS                   
         LA    R5,RESPRFHC         HOLDING FEE FLAG                             
*                                                                               
GETFEE12 MVI   0(R5),C'Y'          SET SESS/HLDNG FEE CREDIT FLAG               
*                                                                               
         ICM   R0,15,TACRAPPL                                                   
         BRAS  RE,GETDOL           OUTPUT DOLLARS AT 0(R4)                      
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*=================================================================              
* OUTPUT PERFORMER REFERENCE DATA FROM BINSRCH TABLE                            
*=================================================================              
                                                                                
TALPRFC  NMOD1 0,*TALPRF*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(SSNPARMS)                                                  
         ST    RE,ASSNPARM                                                      
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R2,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         USING PRFRECD,R2                                                       
*                                                                               
         MVI   FILETYPE,C'T'       INDICATE TALDIR/TALFIL                       
         MVC   AIO,AIO1                                                         
*                                                                               
         LR    R0,R2                                                            
         LHI   R1,PRFRECL                                                       
         GOTO1 AINITALL                                                         
*                                                                               
         L     R1,=V(RECPRF)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     RE,=A(TLPWPRFQ)                                                  
         MVC   PRFRID,0(RE)        MOVE RECID                                   
*                                                                               
         ICM   R3,15,NEXTOUT       TEST FIRST RECORD                            
         BNZ   *+8                                                              
         L     R3,=A(SSNTAB)                                                    
*                                                                               
         OC    0(9,R3),0(R3)       TEST MORE DATA                               
         BZ    TPRF10                                                           
                                                                                
* GET PERFORMER DATA                                                            
                                                                                
K        USING TLW4D,KEY           (PASSIVE SSN KEY)                            
                                                                                
         XC    KEY,KEY                                                          
         MVI   K.TLW4CD,TLW4CDQ     X'60'                                       
         MVC   K.TLW4SSN,0(R3)      SSN IS THE DATA                             
         GOTO1 HIGH                                                             
*                                                                               
         CLC   KEY(30),KEYSAVE      FIND PERFORMER                              
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAW4ELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAW4D,R6                                                         
*                                                                               
*&&DO*&& MVC   PRFSSN,0(R3)                                                     
         GOTOR SSNPCK,DMCB,(R3),PRFSSN                                          
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   PRFFIRST,TAW4NAM1                                                
         OC    PRFFIRST,MYSPACES                                                
         L     RE,AMXTRT                                                        
         TR    PRFFIRST,0(RE)                                                   
*                                                                               
         CLI   1(R6),TAW4LN2Q      TEST MIDDLE NAME IN ELEM                     
         BL    *+10                                                             
         MVC   PRFMIDDL,TAW4MIDN                                                
         OC    PRFMIDDL,MYSPACES                                                
         L     RE,AMXTRT                                                        
         TR    PRFMIDDL,0(RE)                                                   
*                                                                               
         MVC   PRFLAST,TAW4NAM2                                                 
         OC    PRFLAST,MYSPACES                                                 
         L     RE,AMXTRT                                                        
         TR    PRFLAST,0(RE)                                                    
*                                                                               
         MVC   PRFETHN,=CL24'ASIAN, PACIFIC ISLANDER'                           
         CLI   TAW4RACE,C'A'                                                    
         BE    TPRF5                                                            
*                                                                               
         MVC   PRFETHN,=CL24'BLACK'                                             
         CLI   TAW4RACE,C'B'                                                    
         BE    TPRF5                                                            
*                                                                               
         MVC   PRFETHN,=CL24'WHITE'                                             
         CLI   TAW4RACE,C'C'                                                    
         BE    TPRF5                                                            
*                                                                               
         MVC   PRFETHN,=CL24'AMERICAN INDIAN'                                   
         CLI   TAW4RACE,C'I'                                                    
         BE    TPRF5                                                            
*                                                                               
         MVC   PRFETHN,=CL24'LATINO, HISPANIC'                                  
         CLI   TAW4RACE,C'L'                                                    
         BE    TPRF5                                                            
*                                                                               
         MVC   PRFETHN,=CL24'UNKNOWN'                                           
*                                                                               
TPRF5    MVI   PARM3,0             INDICATE DATA RETURNED                       
         LA    R3,9(R3)            POINT TO NEXT SSN                            
         ST    R3,NEXTOUT                                                       
         J     EXIT                                                             
*                                                                               
TPRF10   MVI   PARM3,X'80'         SET NO MORE DATA FLAG                        
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*GETEL                                                                          
         GETEL R6,DATADISP,ELCODE                                               
                                                                                
MYDATCON NTR1  BASE=*,LABEL=*                                                   
         GOTO1 VDATCON,(R1)                                                     
                                                                                
* NEED TO REPLACE MMM WITH NN/  (E.G., FEB WITH 02/)                            
                                                                                
         L     RF,0(R1)            GET INPUT DATE                               
         UNPK  DUB(3),1(2,RF)      UNPACK MONTH + 1 EXTRA BYTE                  
*                                                                               
         L     RF,4(R1)            GET OUTPUT DATE                              
         MVC   0(2,RF),DUB                                                      
         MVI   2(RF),C'/'          REPLACE MMM WITH MM/                         
         J     EXIT                                                             
*                                                                               
GETDOL   NTR1  BASE=*,LABEL=*                                                   
         LPR   R0,R0               NEVER HAVE NEGATIVE NUMBERS                  
         EDIT  (R0),(15,(R4)),2,ALIGN=LEFT                                      
         J     EXIT                                                             
*                                                                               
BACKUP   CLI   0(R1),C' '                                                       
         JH    BACKUP2                                                          
         BCTR  R1,0                                                             
         J     BACKUP                                                           
*                                                                               
BACKUP2  MVI   1(R1),C'_'                                                       
         LA    R1,2(R1)                                                         
         BR    RE                                                               
         LTORG                                                                  
         EJECT                                                                  
*=================================================================              
* OUTPUT SESSION FEE DATA                                                       
*=================================================================              
                                                                                
TALSESC  NMOD1 0,*TALSES*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(SSNPARMS)                                                  
         ST    RE,ASSNPARM                                                      
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         OC    PARM3,PARM3         TEST BEGIN NEW EXTRACT                       
         BNE   TSES50              NO - GO OUTPUT NEXT RECORD                   
*                                                                               
         BRAS  RE,CLRBUFF1                                                      
*                                                                               
         L     R2,=A(BUFFER1)      SET BUFFER POINTER                           
         USING SESRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,SESRECL          R1=RECLEN                                    
         GOTO1 AINITALL            INITIALIZE OUTPUT RECORD                     
*                                                                               
         L     R1,=V(RECSES)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
                                                                                
* SAVE DATA FROM INVOICE RECORD                                                 
                                                                                
         L     R6,AIO1                                                          
         USING TLIND,R6                                                         
         MVC   SVINV,TLININV       INVOICE NUMBER COMPLEMENTED!                 
         MVC   SVAGY,TLINAGY       AGENCY CODE                                  
         DROP  R6                                                               
                                                                                
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAPDD,R6                                                         
         MVC   SVCMML,TAPDCOM      SAVE INTERNAL CMML NUMBER                    
         MVC   SVUSETYP,TAPDUSE    SAVE USE TYPE                                
         MVC   SVCYCSTR,TAPDCYCS   SAVE CYCLE START                             
         MVC   SVCYCEND,TAPDCYCE   AND END                                      
         MVC   SVPAYST,TAPDPST1    SAVE PAYMENT STATUS                          
         MVC   SVCLT,TAPDCLI                                                    
         MVC   SVUSESPD,TAPDUSES    SAVE USES PAID                              
*                                                                               
         MVC   WORK(6),SVAGY                                                    
         MVC   WORK+6(6),SVCLT                                                  
         MVC   WORK+12(6),TAPDPRD                                               
         L     R1,=A(PRDPARMS)                                                  
         GOTO1 VBINSRCH,(R1),(1,WORK)  ADD AGY/CLT/PRD TO PRDTAB                
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MAKE TABLE BIGGER                            
*                                                                               
         MVI   SESDRCR,C'D'                                                     
         TM    TAPDPST1,TAPDPCRD                                                
         BZ    *+8                                                              
         MVI   SESDRCR,C'C'                                                     
         DROP  R6                                                               
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACOD,R6                                                         
         MVC   SVCMMLID,TACOCID    SAVE CMML ID                                 
         DROP  R6                                                               
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAIND,R6                                                         
         MVC   SVINSTAT,TAINSTAT                                                
         TM    SVINSTAT,TAINSCIN   TEST CREDIT                                  
         BZ    *+8                                                              
         MVI   SESDRCR,C'C'                                                     
         DROP  R6                                                               
*                                                                               
* READ CHECK RECORDS FOR THIS INVOICE                                           
*                                                                               
K        USING TLCKD,KEY                                                        
         XC    KEY,KEY                                                          
         MVI   K.TLCKCD,TLCKCDQ                                                 
*                                                                               
         L     R6,AIO1                                                          
         USING TLIND,R6                                                         
         MVC   K.TLCKAGY,TLINAGY                                                
         MVC   K.TLCKINV,TLININV                                                
         XC    K.TLCKINV,=6X'FF'   UNCOMPLEMENT                                 
*                                                                               
TSES10   MVI   FILETYPE,C'C'       INDICATE READING CHKDIR/CHKFIL               
         MVC   AIO,AIO2                                                         
*                                                                               
         GOTO1 HIGH                                                             
*                                                                               
TSES12   CLC   KEY(13),KEYSAVE     SAME AGENCY/INVOICE                          
         BNE   TSES40              NO                                           
*                                                                               
         MVC   SVCHKKEY,KEY        SAVE CHECK KEY IDIOT!                        
         MVC   SVCKSORT,K.TLCKSORT SAVE SORT CODE FROM KEY                      
         MVC   SVCKSSN,K.TLCKSSN   SAVE SSN FROM KEY                            
*                                                                               
         GOTO1 GET                 GET THE CHECK RECORD                         
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TACDELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   TSES14                                                           
         USING TACDD,R6                                                         
         TM    TACDSTAT,TACDSTRS   SKIP TRUSTEE CHECKS                          
         BO    TSES36                                                           
*                                                                               
TSES14   L     R6,AIO                                                           
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAPDD,R6                                                         
         OC    TAPDAPPL,TAPDAPPL   TEST FOR APPLIED AMOUNT                      
         BZ    TSES36                                                           
         CLI   TAPDACDE,C'1'                                                    
         BE    *+12                                                             
         CLI   TAPDACDE,C'2'                                                    
         BNE   TSES36                                                           
*                                                                               
         L     RE,=A(TLPWSESQ)                                                  
         MVC   SESRID,0(RE)        MOVE RECID                                   
*                                                                               
         L     RF,VHEXOUT                                                       
         GOTO1 (RF),DMCB,SVCMML,SESTPCML,4,=C'TOG'                              
*                                                                               
         MVC   SESAGYCL,MYSPACES                                                
         MVC   SESAGYCL(6),SVAGY                                                
         LA    R1,SESAGYCL+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),SVCLT                                                    
         OC    SESAGYCL,MYSPACES                                                
*                                                                               
         MVC   SESCMLCD,SVCMMLID                                                
         MVC   SESCMLTY,=CL5'ISCI'                                              
         CLI   SESCMLCD+8,C' '                                                  
         BNH   *+10                                                             
         MVC   SESCMLTY,=CL5'AD-ID'                                             
         MVC   SESUSECD,SVUSETYP                                                
*&&DO*&& MVC   SESPRFID,SVCKSSN                                                 
         GOTOR SSNPCK,DMCB,SVCKSSN,SESPRFID                                     
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 VHEXOUT,DMCB,SVCKSORT+4,SESCAST,2,=C'TOG'                        
*                                                                               
         LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,SVCYCSTR),(21,SESSDATE) GET MMMDD/YYYY              
         GOTO1 (RF),DMCB,(1,SVCYCEND),(21,SESEDATE) GET MMMDD/YYYY              
                                                                                
* NOW ADD PERFORMER SSN TO PERFORMER TABLE                                      
                                                                                
         L     R1,ASSNPARM                                                      
         GOTO1 VBINSRCH,(R1),(1,SVCKSSN)  ADD IF NOT THERE                      
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   SESGUAR,MYSPACES    CLEAR PREVIOUS VALUES                        
         MVC   SESGUART,MYSPACES                                                
         MVC   SESSDATE,MYSPACES                                                
         MVC   SESEDATE,MYSPACES                                                
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TAPDD,R6                                                         
*                                                                               
         CLI   TAPDACDE,C'1'                                                    
         BNE   TSES20                                                           
*                                                                               
         MVI   SESSFCR,C'Y'                                                     
         L     R0,TAPDGRS                                                       
         LA    R4,SESSFAMT                                                      
         BRAS  RE,GETDOL                                                        
*                                                                               
         MVI   SESHFCR,C'N'                                                     
         SR    R0,R0                                                            
         LA    R4,SESHFAMT                                                      
         BRAS  RE,GETDOL                                                        
         B     TSES22                                                           
*                                                                               
TSES20   CLI   TAPDACDE,C'2'                                                    
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   SESSFCR,C'N'                                                     
         SR    R0,R0                                                            
         LA    R4,SESSFAMT                                                      
         BRAS  RE,GETDOL                                                        
*                                                                               
         MVI   SESHFCR,C'Y'                                                     
         L     R0,TAPDGRS                                                       
         LA    R4,SESHFAMT                                                      
         BRAS  RE,GETDOL                                                        
         DROP  R6                                                               
*                                                                               
TSES22   L     R6,AIO                                                           
         MVI   ELCODE,TACAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TACAD,R6                                                         
*                                                                               
         CLC   TACAUN,=C'SAG'                                                   
         BE    TSES24                                                           
         CLC   TACAUN,=C'AFT'                                                   
         BE    TSES24                                                           
         B     TSES36                                                           
*                                                                               
TSES24   OC    TACAGUA,TACAGUA     TEST GUARANTEE CODE PRESENT                  
         BZ    TSES34              NO                                           
         MVC   SVGUARCD,TACAGUA    SAVE GUARANTEE CODE                          
         DROP  R6                                                               
*                                                                               
         MVI   FILETYPE,C'T'       INDICATE READING TALDIR/TALFIL               
         MVC   AIO,AIO1                                                         
*                                                                               
K        USING TLGUD,KEY                                                        
         XC    KEY,KEY                                                          
         MVI   K.TLGUCD,TLGUCDQ                                                 
         MVC   K.TLGUSSN,SVCKSSN                                                
         MVC   K.TLGUGUA,SVGUARCD                                               
         XC    K.TLGUGUA,=4X'FF'     COMPLEMENT                                 
         DROP  K                                                                
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(30),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAGUELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAGUD,R6                                                         
         OC    TAGUCOM,TAGUCOM                                                  
         BNZ   TSES26                                                           
*                                                                               
         MVC   SESGUART,=C'LO'                                                  
         ICM   R0,15,TAGUAMT       LARGE OVERSCALE GUARANTEE                    
         LA    R4,SESGUAR                                                       
         BRAS  RE,GETDOL                                                        
         B     TSES34                                                           
         DROP  R6                                                               
*                                                                               
TSES26   L     R6,AIO                                                           
         MVI   ELCODE,TAGCELQ      X'62' - PER CYCLE GUARANTEE                  
         BRAS  RE,GETEL                                                         
         BNE   TSES34  <<<<<<<<<<<<---- TEMP FIX TO STOP DUMPS                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAGCD,R6                                                         
TSES28   CLC   TAGCSTRT,SVCYCEND   ELEM START AFTER CYCLE END                   
         BH    *+14                                                             
         CLC   TAGCEND,SVCYCSTR    ELEM END BEFORE CYCLE START                  
         BNL   TSES30                                                           
         BRAS  RE,NEXTEL                                                        
         BE    TSES28                                                           
         DC    H'0'                                                             
*                                                                               
TSES30   MVC   SESGUART,=C'PC'                                                  
         ICM   R0,15,TAGCAMT                                                    
         LA    R4,SESGUAR                                                       
         BRAS  RE,GETDOL                                                        
         DROP  R6                                                               
*                                                                               
TSES34   LA    R0,SESRECL(R2)      POINT TO NEXT RECORD SLOT                    
         LHI   R1,SESRECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               COPY THIS RECORD TO NEXT                     
*                                                                               
         LA    R2,SESRECL(R2)      NOW ADVANCE TO NEXT BUFFER ENTRY             
*                                                                               
TSES36   MVC   KEY,SVCHKKEY        RESTORE LAST CHECK KEY                       
         MVI   FILETYPE,C'C'       READING CHKFILE                              
         MVC   AIO,AIO2                                                         
         GOTO1 HIGH                                                             
         GOTO1 SEQ                                                              
         B     TSES12                                                           
*                                                                               
TSES40   XC    NEXTOUT,NEXTOUT                                                  
         XC    0(64,R2),0(R2)      SET END OF BUFFER FLAG                       
*                                                                               
TSES50   ICM   R2,15,NEXTOUT       POINT TO NEXT OUTPUT RECORD                  
         BNZ   *+8                                                              
         L     R2,=A(BUFFER1)      ELSE POINT TO FIRST OUTPUT RECORD            
*                                                                               
         OC    SESRID,SESRID       TEST (MORE) PERFORMERS                       
         BZ    TSES52              NO                                           
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R0,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         LHI   R1,SESRECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT                        
*                                                                               
         LA    R2,SESRECL(R2)                                                   
         ST    R2,NEXTOUT          SAVE BUFFER POINTER                          
*                                                                               
         MVI   PARM3,0             SET TO PROCESS OUTPUT                        
         J     EXIT                                                             
*                                                                               
TSES52   BRAS  RE,CLRBUFF                                                       
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         MVI   PARM3,X'80'         SET NO MORE OUTPUT                           
         J     EXIT                                                             
         EJECT                                                                  
*=================================================================              
* USE AUTHORIZATIONS                                                            
*=================================================================              
                                                                                
TALUSEC  NMOD1 0,*TALUSE*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         OC    PARM3,PARM3         TEST BEGIN NEW EXTRACT                       
         BNE   TUSE50              NO - GO OUTPUT NEXT RECORD                   
*                                                                               
         BRAS  RE,CLRBUFF1                                                      
*                                                                               
         L     R2,=A(BUFFER1)      SET BUFFER POINTER                           
         USING USERECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,USERECL          R1=RECLEN                                    
         GOTO1 AINITALL            INITIALIZE OUTPUT RECORD                     
*                                                                               
         L     R1,=V(RECUSE)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC          BUT DO NOT INSERT RECID                      
                                                                                
* SAVE DATA FROM INVOICE RECORD                                                 
                                                                                
         L     R6,AIO1                                                          
         USING TLIND,R6                                                         
         MVC   SVINV,TLININV       INVOICE NUMBER COMPLEMENTED!                 
         MVC   SVAGY,TLINAGY       AGENCY CODE                                  
         DROP  R6                                                               
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(6),SVINV                                                    
         XC    WORK(6),=6X'FF'                                                  
         GOTO1 =V(TINVCON),DMCB,WORK,USEINV,VDATCON                             
*                                                                               
         MVI   ELCODE,TAPDELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAPDD,R6                                                         
         MVC   SVCMML,TAPDCOM      SAVE INTERNAL CMML NUMBER                    
         MVC   SVUSETYP,TAPDUSE    SAVE USE TYPE                                
         MVC   SVCYCSTR,TAPDCYCS   SAVE CYCLE START                             
         MVC   SVCYCEND,TAPDCYCE   AND END                                      
         MVC   SVPAYST,TAPDPST1    SAVE PAYMENT STATUS                          
         MVC   SVCLT,TAPDCLI                                                    
         MVC   SVPRD,TAPDPRD                                                    
         MVC   SVDSTUS,TAPDSTUS    SAVE STARTING USE NUMBER                     
*                                                                               
         MVC   USEUSECD,TAPDUSE    SET USE CATEGORY CODE                        
         MVI   USEDRCR,C'D'                                                     
         TM    TAPDPST1,TAPDPCRD                                                
         BZ    *+8                                                              
         MVI   USEDRCR,C'C'                                                     
*                                                                               
         LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,TAPDCYCS),(21,USECYSTR) GET MMMDD/YYYY              
         GOTO1 (RF),DMCB,(1,TAPDCYCE),(21,USECYEND)                             
         DROP  R6                                                               
*                                                                               
         MVC   WORK(6),SVAGY                                                    
         MVC   WORK+6(6),SVCLT                                                  
         MVC   WORK+12(6),SVPRD                                                 
         L     R1,=A(PRDPARMS)                                                  
         GOTO1 VBINSRCH,(R1),(1,WORK)  ADD AGY/CLT/PRD TO PRDTAB                
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                MAKE TABLE BIGGER                            
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAINELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAIND,R6                                                         
         MVC   SVINVDT,TAINBDTE    SAVE INVOICE DATE                            
         MVC   SVINSTAT,TAINSTAT                                                
         TM    SVINSTAT,TAINSCIN   TEST CREDIT                                  
         BZ    *+8                                                              
         MVI   USEDRCR,C'C'                                                     
         DROP  R6                                                               
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TACOD,R6                                                         
         MVC   SVCMMLID,TACOCID    SAVE CMML ID                                 
*                                                                               
         MVC   USEMDCTG,TACOMED    SET USE MEDIA CATEGORY                       
         MVC   USECMLTC,TACOTYPE   SET USE CMML TYPE CODE                       
         MVI   USESPAN,C'N'        SET NOT SPANISH                              
         CLI   TACOTYPE,C'S'                                                    
         BNE   *+8                                                              
         MVI   USESPAN,C'Y'                                                     
         DROP  R6                                                               
*                                                                               
         MVC   USEAGYCL,MYSPACES                                                
         MVC   USEAGYCL(6),SVAGY                                                
         LA    R1,USEAGYCL+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),SVCLT                                                    
         OC    USEAGYCL,MYSPACES                                                
*                                                                               
         MVC   USEAGYID,SVAGY                                                   
         MVC   USEADVID,THISADV       USE CLTGRP NOT CLIENT FOR ADVTSR          
         MVC   USENETID,MYSPACES                                                
         MVC   USENETCD,MYSPACES                                                
*                                                                               
         MVC   USEPRDID(6),SVAGY      PRD=AGY_CLI_PRD                           
         LA    R1,USEPRDID+6                                                    
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),SVCLT                                                    
         LA    R1,6(R1)                                                         
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),SVPRD                                                    
         OC    USEPRDID,MYSPACES                                                
         CLI   0(R1),C' '          MAKE SURE PRODUCT IS THERE                   
         BH    *+10                                                             
         MVC   USEPRDID,MYSPACES   ELSE GET RID OF IT ALL                       
*                                                                               
         GOTO1 VHEXOUT,DMCB,SVCMML,USETPCML,4,=C'TOG'                           
*                                                                               
         L     R6,AIO1                                                          
         MVI   ELCODE,TAVRELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   *+10                                                             
         USING TAVRD,R6                                                         
         MVC   SVCMMLID,TAVRCID    USE CHILD ID IF PRESENT                      
         DROP  R6                                                               
*                                                                               
         BRAS  RE,GETCMML          UPDATE SVCMMLID IF NEEDED                    
         MVC   USECMLCD,SVCMMLID                                                
*                                                                               
         MVC   USECMLTY,=CL5'ISCI'                                              
         CLI   SVCMMLID+8,C' '     TEST FOR A NINTH CHARACTER                   
         BNH   *+10                                                             
         MVC   USECMLTY,=CL5'AD-ID'                                             
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TAFNELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TAFND,R6                                                         
TUSE6    CLI   TAFNTYPE,TAFNTTTL                                                
         BE    TUSE8                                                            
         BRAS  RE,NEXTEL                                                        
         BE    TUSE6                                                            
         DC    H'0'                                                             
*                                                                               
TUSE8    MVC   USECMLNM,MYSPACES                                                
         LLC   RE,1(R6)                                                         
         AHI   RE,-(TAFNLNQ+1)     SET FOR EX TO MOVE NAME                      
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   USECMLNM(0),TAFNNAME                                             
         DROP  R6                                                               
         EJECT                                                                  
*===============================================================                
* PROCESS CNET/CSYS MARKET ELEMENTS                                             
*===============================================================                
                                                                                
         MVC   USENETID,MYSPACES                                                
                                                                                
         L     R6,AIO                                                           
         MVI   ELCODE,TAMTELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   TUSE20                                                           
*                                                                               
         USING TAMTD,R6                                                         
TUSE10   LA    R0,L'TAMTCODE                                                    
         LA    R1,TAMTCODE                                                      
*                                                                               
TUSE12   CLI   0(R1),C'A'          SEARCH FOR ALPHA CHAR IN STRING              
         BL    *+12                                                             
         CLI   0(R1),C'Z'                                                       
         BNH   TUSE14                                                           
*                                                                               
         LA    R1,1(R1)                                                         
         BCT   R0,TUSE12                                                        
         B     TUSE20                                                           
*                                                                               
TUSE14   MVC   USERECL(USERECL,R2),0(R2)   COPY RECORD TO NEXT                  
         L     RE,=A(TLPWUSEQ)                                                  
         MVC   USERID,0(RE)                INSERT RECID                         
         MVC   USENETID,TAMTCODE                                                
         OC    USENETID,MYSPACES                                                
*                                                                               
         LA    R2,USERECL(R2)              POINT TO NEXT RECORD                 
         DROP  R6                                                               
*                                                                               
TUSE16   BRAS  RE,NEXTEL                                                        
         BE    TUSE10                                                           
                                                                                
*===============================================================                
* PROCESS PROGRAM DETAILS ELEMENTS                                              
*===============================================================                
                                                                                
TUSE20   MVC   USENETCD,MYSPACES                                                
         MVC   USEUSENUM,MYSPACES                                               
         MVC   USEUSEDT,MYSPACES                                                
         MVC   USEPRGNM,MYSPACES                                                
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TANPELQ                                                   
         BRAS  RE,GETEL                                                         
         BNE   TUSE40                                                           
*                                                                               
         LH    R5,SVDSTUS          STARTING USE NUMBER                          
*                                                                               
         USING TANPD,R6                                                         
*                                                                               
TUSE22   LA    R0,L'TANPLIST                                                    
         LA    R1,TANPLIST                                                      
*                                                                               
TUSE24   CLC   TANPNWK,0(R1)       SEARCH LIST                                  
         BE    TUSE30                                                           
         LA    R1,1(R1)                                                         
         BCT   R0,TUSE24                                                        
         B     TUSE32                                                           
*                                                                               
TANPLIST DC    C'ACFINSUWX'        LIST OF TANPNWK'S TO PROCESS                 
*                                                                               
*                                                                               
TUSE30   MVC   USERECL(USERECL,R2),0(R2)   COPY RECORD TO NEXT                  
*                                                                               
         L     RE,=A(TLPWUSEQ)                                                  
         MVC   USERID,0(RE)                INSERT RECID                         
*                                                                               
         MVC   USENETCD,TANPNWK                                                 
*                                                                               
         LR    R0,R5                                                            
         EDIT  (R0),(3,USEUSENUM),ALIGN=LEFT                                    
*                                                                               
         LAY   RF,MYDATCON                                                      
         GOTO1 (RF),DMCB,(1,TANPDATE),(21,USEUSEDT) GET MMMDD/YYYY              
*                                                                               
         MVC   USEPRGNM,TANPPNME                                                
         DROP  R6                                                               
*                                                                               
         LA    R2,USERECL(R2)              POINT TO NEXT RECORD                 
*                                                                               
TUSE32   LA    R5,1(R5)            BUMP USE NUMBER                              
         BRAS  RE,NEXTEL                                                        
         BE    TUSE22                                                           
         EJECT                                                                  
*==============================================================                 
* OUTPUT RECORDS TO EXTRACT PROGRAM NOW                                         
*==============================================================                 
                                                                                
TUSE40   L     R2,=A(BUFFER1)                                                   
         L     RE,=A(TLPWUSEQ)                                                  
         MVC   USERID,0(RE)        ALWAYS OUTPUT FIRST REC IN BUFFER!           
         XC    NEXTOUT,NEXTOUT                                                  
*                                                                               
TUSE50   ICM   R2,15,NEXTOUT       POINT TO NEXT OUTPUT RECORD                  
         BNZ   *+8                                                              
         L     R2,=A(BUFFER1)      ELSE POINT TO FIRST OUTPUT RECORD            
*                                                                               
         CLI   USERID,C' '         TEST MORE                                    
         BNH   TUSE52              NO                                           
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R0,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         LHI   R1,USERECL                                                       
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT                        
*                                                                               
         LA    R2,USERECL(R2)                                                   
         ST    R2,NEXTOUT          SAVE BUFFER POINTER                          
*                                                                               
         MVI   PARM3,0             SET TO PROCESS OUTPUT                        
         J     EXIT                                                             
*                                                                               
TUSE52   BRAS  RE,CLRBUFF                                                       
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         MVI   PARM3,X'80'         SET NO MORE OUTPUT                           
         J     EXIT                                                             
         EJECT                                                                  
*=================================================================              
* OUTPUT ALL REF DATA EXCEPT PERFORMERS AND OVERSCALE                           
* NOTE THAT RECORDS ARE OUTPUT DIRECTLY FROM THIS ROUTINE!                      
*=================================================================              
                                                                                
TALREFC  NMOD1 0,*TALREF*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         MVI   FILETYPE,C'T'       SET TO READ TALENT FILES                     
         MVC   AIO,AIO1                                                         
                                                                                
*==============================================================                 
* ADVERTISER RECORDS                                                            
*==============================================================                 
                                                                                
         L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         L     R4,VCGRTAB                                                       
         USING CGRTABD,R4                                                       
*                                                                               
         USING ADVRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,ADVRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWADVQ)                                                  
         MVC   ADVRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECADV)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
K        USING TLCGD,KEY                                                        
*                                                                               
TREF02   XC    KEY,KEY                                                          
         MVI   K.TLCGCD,TLCGCDQ                                                 
         MVC   K.TLCGCLG,CGRCGR                                                 
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ADVID,K.TLCGCLG                                                  
         DROP  K                                                                
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TANAD,R6                                                         
*                                                                               
         LA    R1,ADVNAME                                                       
         BRAS  RE,MOVENAME                                                      
         DROP  R6                                                               
*                                                                               
         LHI   R1,ADVRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
TREF04   LA    RE,CGRCGR           POINT TO CLTGRP                              
*                                                                               
         LA    R4,CGRNEXT          POINT TO NEXT                                
*                                                                               
         CLI   0(R4),X'FF'         TEST EOL                                     
         BE    TREF10                                                           
*                                                                               
         CLC   CGRCGR,0(RE)        TEST NEXT IS SAME CLTGRP                     
         BE    TREF04                                                           
*                                                                               
         CLC   CGRCGR,=CL6'NEST  '  SKIP NESTLE II                              
         BE    TREF04                                                           
         B     TREF02                                                           
                                                                                
*==============================================================                 
* AGENCY RECORDS                                                                
* OUTPUT ALL AGENCIES IN CLTGRP TABLE                                           
* DUPLICATE AGENCIES WILL BE DELETED BY TAXTRAFT PROCESSING                     
*==============================================================                 
                                                                                
TREF10   L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         USING AGYRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,AGYRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWAGYQ)                                                  
         MVC   AGYRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECAGY)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     R4,VCGRTAB          GET ADDRESS OF CLTGRP TABLE                  
         USING CGRTABD,R4                                                       
*                                                                               
         OC    CGRAGY,CGRAGY       TEST AGENCY THERE                            
         BNZ   TREF12                                                           
         LA    R4,CGRNEXT                                                       
         B     *-14                                                             
*                                                                               
K        USING TLAYD,KEY                                                        
*                                                                               
TREF12   XC    KEY,KEY                                                          
         MVI   K.TLAYCD,TLAYCDQ                                                 
         MVC   K.TLAYAGY,CGRAGY                                                 
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BNE   TREF14              IGNORE IF NOT FOUND                          
         MVC   AGYID,K.TLAYAGY                                                  
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING TANAEL,R6                                                        
*                                                                               
         LA    R1,AGYNAME                                                       
         BAS   RE,MOVENAME                                                      
         DROP  R6,K                                                             
*                                                                               
         LHI   R1,AGYRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
TREF14   LA    RE,CGRAGY           POINT TO AGENCY CODE                         
*                                                                               
TREF16   LA    R4,CGRNEXT                                                       
*                                                                               
         CLI   0(R4),X'FF'         TEST EOL                                     
         BE    TREF20                                                           
*                                                                               
         CLC   CGRAGY,0(RE)        TEST NEXT IS SAME AGY                        
         BE    TREF16              YES - SKIP                                   
         B     TREF12                                                           
                                                                                
*==============================================================                 
* PRODUCT RECORDS                                                               
* READ THROUGH CLTGRP PASSIVES FOR COMMERCIAL RECORDS                           
*==============================================================                 
                                                                                
TREF20   DS    0H                                                               
*&&DO                                                                           
         L     R0,=A(SSNTAB)       USE SSNTAB TO BUFFER PRD ENTRIES             
         L     R1,=A(SSNTABX)                                                   
         SR    R1,R0                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     RE,=A(SSNPARMS)                                                  
         ST    RE,ASSNPARM                                                      
         XC    8(4,RE),8(RE)       PARM3 = RECORD COUNT                         
         LA    R0,18                                                            
         ST    R0,12(RE)           PARM4 = RECLEN                               
         ST    R0,16(RE)           PARM5 = KEYLEN (=RECLEN)                     
         L     R0,=A(SSNTABX-SSNTAB)                                            
         SRDL  R0,32                                                            
         D     R0,=F'18'                                                        
         ST    R1,20(RE)           PARM6 = MAX RECS                             
*                                                                               
         L     R2,=A(SSNTAB)                                                    
*                                                                               
         L     R4,VCGRTAB                                                       
         USING CGRTABD,R4                                                       
*                                                                               
K        USING TLCOPD,KEY                                                       
*                                                                               
TREF21   XC    KEY,KEY                                                          
         MVI   K.TLCOPCD,TLCOGCDQ                                               
         MVC   K.TLCOGCLG,CGRCGR                                                
         GOTO1 HIGH                                                             
         DROP  K                                                                
*                                                                               
TREF22   CLC   KEY(8),KEYSAVE                                                   
         BNE   TREF26                                                           
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         USING TLCOD,R6                                                         
*                                                                               
         MVC   WORK(6),TLCOAGY                                                  
         MVC   WORK+6(6),TLCOCLI                                                
         CLI   TLCOPRD,C' '        IF NO PRODUCT THIS CML                       
         BNH   *+10                THEN DON'T MOVE PRD                          
         MVC   WORK+12(6),TLCOPRD                                               
         DROP  R6                                                               
* ADD TO TABLE                                                                  
         L     R1,ASSNPARM                                                      
         GOTO1 VBINSRCH,(R1),(1,WORK)  ADD IF NOT THERE                         
         OC    1(3,R1),1(R1)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
TREF24   GOTO1 SEQ                                                              
         B     TREF22                                                           
*                                                                               
TREF26   LA    RE,CGRCGR           SAVE CURRENT CGR                             
*                                                                               
TREF28   LA    R4,CGRNEXT                                                       
*                                                                               
         CLI   0(R4),X'FF'                                                      
         BE    TREF30                                                           
*                                                                               
         CLC   CGRCGR,0(RE)        TEST NEXT IS SAME                            
         BE    TREF28                                                           
         B     TREF21                                                           
                                                                                
* READ PRODUCT NAMES AND OUTPUT TABLE ENTRIES *                                 
                                                                                
TREF30   L     R4,=A(SSNTAB)                                                    
         OC    0(18,R4),0(R4)      MAKE SURE WE GOT SOMETHING                   
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R2,=A(BUFFER1)      POINT TO RECORD                              
         XC    0(256,R2),0(R2)                                                  
         USING PRDRECD,R2                                                       
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,PRDRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWPRDQ)                                                  
         MVC   PRDRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECPRD)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
K        USING TLPRD,KEY                                                        
*                                                                               
TREF32   XC    KEY,KEY                                                          
         MVI   K.TLPRCD,TLPRCDQ                                                 
         MVC   K.TLPRAGY,0(R4)                                                  
         MVC   K.TLPRCLI,6(R4)                                                  
         MVC   K.TLPRPRD,12(R4)                                                 
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BE    TREF34                                                           
*                                                                               
         MVC   KEY,KEYSAVE          RESTORE                                     
         XC    K.TLPRAGY,K.TLPRAGY  CLEAR AGY/CLT TO READ GLOBAL PRD            
         XC    K.TLPRCLI,K.TLPRCLI                                              
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BNE   TREF36                                                           
*                                                                               
TREF34   MVC   PRDID,MYSPACES                                                   
         MVC   PRDID(6),0(R4)      BUILD CONCATENATED PRDID                     
         LA    R1,PRDID+6                                                       
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),6(R4)                                                    
         LA    R1,6(R1)                                                         
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),12(R4)                                                   
         OC    PRDID,MYSPACES                                                   
         DROP  K                                                                
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TANAD,R6                                                         
         LA    R1,PRDNAME                                                       
         BAS   RE,MOVENAME                                                      
         DROP  R6                                                               
*                                                                               
         LHI   R1,PRDRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
TREF36   LA    R4,18(R4)           NEXT AGY/CL/PRD                              
         CLI   0(R4),0             TEST EOL                                     
         BNE   TREF32                                                           
                                                                                
* AND NOW ONE MORE RECORD FOR THE TRUE IMBECILES                                
                                                                                
         MVC   PRDID,MYSPACES                                                   
         MVC   PRDNAME,MYSPACES                                                 
         MVC   PRDNAME(16),=C'NO PRODUCT CODE!'                                 
         LHI   R1,PRDRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*&&                                                                             
         EJECT                                                                  
*==============================================================                 
* NETWORK RECORDS                                                               
*==============================================================                 
                                                                                
         L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         USING NETRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,NETRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWNETQ)                                                  
         MVC   NETRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECPRD)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
K        USING TLMTD,KEY                                                        
*                                                                               
         XC    KEY,KEY                                                          
         MVI   K.TLMTCD,TLMTCDQ                                                 
         MVI   K.TLMTTYPE,C'N'                                                  
         GOTO1 HIGH                                                             
*                                                                               
TREF40   CLC   KEY(8),KEYSAVE                                                   
         BNE   TREF50                                                           
*                                                                               
         MVC   NETID,K.TLMTCODE                                                 
         OC    NETID,MYSPACES                                                   
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TANAD,R6                                                         
         LA    R1,NETNAME                                                       
         BAS   RE,MOVENAME                                                      
         DROP  R6,K                                                             
*                                                                               
         LHI   R1,NETRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
         GOTO1 SEQ                                                              
         B     TREF40                                                           
         EJECT                                                                  
*==============================================================                 
* USE TYPES                                                                     
*==============================================================                 
                                                                                
TREF50   L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         USING UTYRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,UTYRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWUTYQ)                                                  
         MVC   UTYRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECUTY)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     R4,VTGAUSES         POINT TO USE CODE TABLE                      
         USING USETABD,R4                                                       
*                                                                               
TREF52   MVC   UTYID,USECDE                                                     
*                                                                               
         MVC   UTYNAME,MYSPACES                                                 
         LLC   RE,USEDSP           LENGTH OF ELEM INCLUDING NAME                
         AHI   RE,-(USENAME-USETABD) LESS FIXED LEN GIVES LEN OF NAME           
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   UTYNAME(0),USENAME                                               
         OC    UTYNAME,MYSPACES                                                 
*                                                                               
         LHI   R1,UTYRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
         AH    R4,USELEN                                                        
         CLI   0(R4),X'FF'                                                      
         BNE   TREF52                                                           
         DROP  R4                                                               
         EJECT                                                                  
*==============================================================                 
* MEDIA CATEGORIES                                                              
*==============================================================                 
                                                                                
         L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         USING MDCRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,MDCRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWMDCQ)                                                  
         MVC   MDCRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECMDC)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     R4,VT00A88          POINT TO SYSTEM TABLES                       
         ICM   R0,15,TGAMEDS-TGTABLED(R4)                                       
         AR    R4,R0               ADD TO PHASE ADDRESS                         
         USING MEDIAD,R4                                                        
*                                                                               
TREF162  MVC   MDCID,MEDNAME       FIRST CHAR IS ID                             
         MVC   MDCNAME,MEDNAME                                                  
*                                                                               
         LHI   R1,MDCRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
         LA    R4,MEDNEXT                                                       
         CLI   0(R4),X'FF'                                                      
         BNE   TREF162                                                          
         DROP  R4                                                               
         EJECT                                                                  
*==============================================================                 
* CATEGORIES                                                                    
*==============================================================                 
                                                                                
         L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         USING CTGRECD,R2          R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,CTGRECL          R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWCTGQ)                                                  
         MVC   CTGRID,0(RE)                                                     
*                                                                               
         L     R1,=V(RECCTG)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     R4,VT00A88          POINT TO SYSTEM TABLES                       
         ICM   R0,15,TGACATS-TGTABLED(R4)                                       
         AR    R4,R0               ADD TO PHASE ADDRESS                         
         USING CATTABD,R4                                                       
*                                                                               
TREF72   TM    CATUNI,X'20'        TEST AFM                                     
         BO    TREF74              AND SKIP IF SO                               
*                                                                               
         CLC   =C'ACR',CATCDE      SKIP THESE TOO                               
         BE    TREF74                                                           
         CLC   =C'ANN',CATCDE                                                   
         BE    TREF74                                                           
*                                                                               
         MVC   CTGCODE,CATCDE      MOVE CODE                                    
*                                                                               
         MVC   CTGNAME,MYSPACES                                                 
         SR    RE,RE                                                            
         IC    RE,CATLEN                GET ENTRY LENGTH                        
         AHI   RE,-(CATNAME-CATTABD)    LESS LEN OF FIXED PORTION               
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   CTGNAME(0),CATNAME                                               
*                                                                               
         LHI   R1,CTGRECL                                                       
         BRAS  RE,PUTITOUT                                                      
*                                                                               
TREF74   LLC   R0,CATLEN                                                        
         AR    R4,R0               POINT TO NEXT ENTRY                          
         CLI   0(R4),X'FF'                                                      
         BNE   TREF72                                                           
         EJECT                                                                  
*==============================================================                 
* COMMERCIAL TYPES                                                              
*==============================================================                 
                                                                                
         L     R2,=A(BUFFER1)                                                   
         XC    0(256,R2),0(R2)                                                  
*                                                                               
         USING CMTYRECD,R2         R2=A(EXTRACT RECORD)                         
*                                                                               
         LR    R0,R2               POINT TO RECORD                              
         LHI   R1,CMTYRECL         R1=RECLEN                                    
         GOTO1 AINITALL                                                         
*                                                                               
         L     RE,=A(TLPWCMTQ)                                                  
         MVC   CMTYRID,0(RE)                                                    
*                                                                               
         L     R1,=V(RECCMTY)      POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     R4,VT00A88          POINT TO SYSTEM TABLES                       
         ICM   R0,15,TGACOMT-TGTABLED(R4)                                       
         AR    R4,R0               ADD TO PHASE ADDRESS                         
         USING CTYD,R4                                                          
* CREATE AN ENTRY FOR STANDARD                                                  
         MVC   CMTYID,=C'Z  '                                                   
         MVC   CMTYNAME,=CL7'STD'                                               
         LHI   R1,CMTYRECL                                                      
         BRAS  RE,PUTITOUT                                                      
*                                                                               
TREF82   MVC   CMTYID,CTYEQU                                                    
         MVC   CMTYNAME,CTYNAME                                                 
*                                                                               
         LHI   R1,CMTYRECL                                                      
         BRAS  RE,PUTITOUT                                                      
*                                                                               
         LA    R4,CTYNEXT                                                       
         CLI   0(R4),X'FF'                                                      
         BNE   TREF82                                                           
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*=================================================================              
* OUTPUT OVERSCALE REFERENCE DATA FROM BINSRCH TABLE                            
*=================================================================              
                                                                                
TALOVRC  NMOD1 0,*TALOVR*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R2,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         USING OVRRECD,R2                                                       
*                                                                               
         LR    R0,R2                                                            
         LHI   R1,OVRRECL                                                       
         GOTO1 AINITALL                                                         
*                                                                               
         L     R1,=V(RECOVR)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     RE,=A(TLPWOVRQ)                                                  
         MVC   OVRRID,0(RE)        MOVE RECID                                   
*                                                                               
         ICM   R3,15,NEXTOUT       TEST FIRST RECORD                            
         BNZ   *+8                                                              
         L     R3,=A(OVRTAB)                                                    
         USING OVRTABD,R3                                                       
*                                                                               
         OC    0(16,R3),0(R3)      TEST MORE DATA                               
         BZ    TOVR10                                                           
*                                                                               
         GOTO1 VHEXOUT,DMCB,OVRTBCML,OVRTPCML,4,=C'TOG'                         
         GOTO1 (RF),(R1),OVRTBCST,OVRCAST,2                                     
         MVC   OVRCODE,OVRTBCOD                                                 
         MVC   OVRUSE,OVRTBUSE                                                  
*                                                                               
         ICM   R0,15,OVRTBAMT                                                   
         EDIT  (R0),(15,OVRAMT),4,ALIGN=LEFT                                    
         CLI   OVRAMT,C'.'                                                      
         BNE   TOVR2                                                            
         ICM   R0,15,OVRAMT+1      GET THE DIGITS                               
         MVI   OVRAMT,C'0'                                                      
         MVI   OVRAMT+1,C'.'                                                    
         STCM  R0,15,OVRAMT+2      SET IT AS 0.1234                             
*                                                                               
TOVR2    MVI   PARM3,0             INDICATE DATA RETURNED                       
         LA    R3,OVRTABL(R3)      POINT TO NEXT SSN                            
         ST    R3,NEXTOUT                                                       
         J     EXIT                                                             
*                                                                               
TOVR10   MVI   PARM3,X'80'         SET NO MORE DATA FLAG                        
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         J     EXIT                                                             
         LTORG                                                                  
         EJECT                                                                  
*=================================================================              
* OUTPUT PRODUCT REFERENCE DATA FROM BINSRCH TABLE                              
*=================================================================              
                                                                                
TALPRDC  NMOD1 0,*TALCLP*                                                       
*                                                                               
         L     RC,0(R1)                                                         
         USING WORKD,RC                                                         
*                                                                               
         L     RA,AADDRESS                                                      
         USING ADDRESSD,RA                                                      
*                                                                               
         L     R4,12(R1)                                                        
         USING SXDTABD,R4          R4=A(EXTRACT DRIVER TABLE)                   
*                                                                               
         L     RE,=A(MXTRT)                                                     
         ST    RE,AMXTRT                                                        
*                                                                               
         L     RF,ADXBLOCK                                                      
         L     R2,DXAXREC-DXBLOCKD(RF)  GET EXTRACT RECORD ADDRESS              
         USING PRDRECD,R2                                                       
*                                                                               
         LR    R0,R2                                                            
         LHI   R1,OVRRECL                                                       
         GOTO1 AINITALL                                                         
*                                                                               
         L     R1,=V(RECPRD)       POINT TO FIELD TABLE                         
         BRAS  RE,INITREC                                                       
*                                                                               
         L     RE,=A(TLPWPRDQ)                                                  
         MVC   PRDRID,0(RE)        MOVE RECID                                   
*                                                                               
         ICM   R3,15,NEXTOUT       TEST FIRST RECORD                            
         BNZ   *+8                                                              
         L     R3,=A(PRDTAB)                                                    
         USING PRDTABD,R3                                                       
*                                                                               
         OC    0(16,R3),0(R3)      TEST MORE DATA                               
         BZ    TPRD10                                                           
*                                                                               
K        USING TLPRD,KEY                                                        
*                                                                               
TPRD2    XC    KEY,KEY                                                          
         MVI   K.TLPRCD,TLPRCDQ                                                 
         MVC   K.TLPRAGY,PRDTBAGY                                               
         MVC   K.TLPRCLI,PRDTBCLT                                               
         MVC   K.TLPRPRD,PRDTBPRD                                               
         CLI   PRDTBPRD,C' '                                                    
         BNH   TPRD4                                                            
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BE    TPRD4                                                            
*                                                                               
         MVC   KEY,KEYSAVE          RESTORE                                     
         XC    K.TLPRAGY,K.TLPRAGY  CLEAR AGY/CLT TO READ GLOBAL PRD            
         XC    K.TLPRCLI,K.TLPRCLI                                              
         GOTO1 HIGH                                                             
*                                                                               
TPRD4    MVC   PRDID,MYSPACES                                                   
         LA    R6,NOPRDEL                                                       
         CLI   PRDTBPRD,C' '       TEST PRODUCT IS THERE                        
         BNH   TPRD6               AND PUT OUT IDIOTIC ELEMENT                  
*                                                                               
         MVC   PRDID(6),0(R3)      BUILD CONCATENATED PRDID                     
         LA    R1,PRDID+6                                                       
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),6(R3)                                                    
         LA    R1,6(R1)                                                         
         BRAS  RE,BACKUP                                                        
         MVC   0(6,R1),12(R3)                                                   
         OC    PRDID,MYSPACES                                                   
         DROP  K                                                                
*                                                                               
         LA    R6,NOPRDEL          POINT TO DUMMY ELEMENT                       
         CLC   KEY(32),KEYSAVE     TEST PRD ON FILE                             
         BNE   TPRD6                                                            
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         MVI   ELCODE,TANAELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         USING TANAD,R6                                                         
TPRD6    LA    R1,PRDNAME                                                       
         BAS   RE,MOVENAME                                                      
*                                                                               
         MVI   PARM3,0             INDICATE DATA RETURNED                       
         LA    R3,L'PRDTAB(R3)     POINT TO NEXT PRD                            
         ST    R3,NEXTOUT                                                       
         J     EXIT                                                             
*                                                                               
TPRD10   MVI   PARM3,X'80'         SET NO MORE DATA FLAG                        
         XC    NEXTOUT,NEXTOUT     CLEAR OUTPUT POINTER                         
         J     EXIT                                                             
*                                                                               
NOPRDEL  DC    AL1(0),AL1(NOPRDELX-NOPRDEL)                                     
NOPRDNAM DC    C'** PRODUCT NOT ON FILE **'                                     
NOPRDELX EQU   *                                                                
         LTORG                                                                  
         EJECT                                                                  
*=========================================================                      
* GET CURRENT COMMERCIAL ID                                                     
* R6 POINTS TO TAVRELQ IN AIO1 (OR EOR)                                         
*=========================================================                      
                                                                                
GETCMML  NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   SVINVKEY,KEY        SAVE INVOICE RECORD KEY!                     
         XC    KEY,KEY                                                          
*                                                                               
K        USING TLCOPD,KEY                                                       
         MVI   K.TLCOPCD,TLCOCCDQ  SET FOR PASSIVE COMMERCIAL                   
         MVC   K.TLCOCCOM,SVCMML   INTERNAL COMMERCIAL NUMBER                   
         DROP  K                                                                
         CLI   0(R6),0             TEST FOUND TAVREL                            
         BE    GETCMML2            NO                                           
*                                                                               
         USING TAVRD,R6                                                         
         CLI   TAVRVERS,1          TEST VERSION 1                               
         BE    GETCMML2                                                         
         BH    GETCMML4                                                         
         DC    H'0'                                                             
*                                                                               
GETCMML2 GOTO1 HIGH                                                             
         CLC   KEY(30),KEYSAVE                                                  
         BE    GETCMML6                                                         
         DC    H'0'                                                             
*                                                                               
K        USING TLVRD,KEY                                                        
GETCMML4 XC    KEY,KEY                                                          
         MVI   K.TLVRCD,TLVRCDQ    SET FOR VERSION KEY                          
         MVC   K.TLVRCOM,SVCMML    INTERNAL COMMERCIAL NUMBER                   
         MVC   K.TLVRVER,TAVRVERS  AND VERSION                                  
         DROP  K                                                                
         GOTO1 HIGH                                                             
         CLC   KEY(30),KEYSAVE     TEST SAME THROUGH VERSION                    
         BNE   GETCMMLX            IGNORE IF VERSION NOT FOUND                  
*                                                                               
GETCMML6 MVC   AIO,AIO2                                                         
         GOTO1 GET                                                              
*                                                                               
         L     R6,AIO                                                           
         USING TACOD,R6                                                         
         MVI   ELCODE,TACOELQ                                                   
         BRAS  RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   SVCMMLID,TACOCID    MOVE CURRENT CMML ID                         
*                                                                               
GETCMMLX MVC   KEY,SVINVKEY                                                     
         GOTO1 HIGH                                                             
         CLC   KEY(32),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AIO,AIO1            RESTORE IOAREA  ADDRESS                      
         J     EXIT                                                             
         EJECT                                                                  
*=========================================================                      
* MOVE VARIABLE LEN NAME FROM TANANAME ELEMENT                                  
* R1 IS MOVE TO ADDRESS                                                         
* R6 IS TANAEL                                                                  
*=========================================================                      
                                                                                
MOVENAME NTR1  BASE=*,LABEL=*                                                   
         MVC   0(50,R1),MYSPACES                                                
         LLC   RF,1(R6)                                                         
         AHI   RF,-3                                                            
         EX    RF,MOVENEXE                                                      
         J     EXIT                                                             
MOVENEXE MVC   0(0,R1),2(R6)                                                    
                                                                                
*=========================================================                      
* MOVE RECORD TO OUTPUT BUFFER, CONVERT TO SQL FORMAT                           
* AND GIVE IT TO DXTRACT                                                        
* R1 HAS RECORD LENGTH                                                          
*=========================================================                      
                                                                                
PUTITOUT NTR1  BASE=*,LABEL=*                                                   
         L     RF,ADXBLOCK                                                      
         L     R0,DXAXREC-DXBLOCKD(RF)  GET EXTRACT OUTPUT ADDRESS              
         LR    RE,R2                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE               MOVE RECORD TO OUTPUT                        
*                                                                               
         GOTO1 VTAXCNVX,PARMS,(RC)      CONVERT TO SQL FORMAT                   
*                                                                               
         L     RF,ADXBLOCK                                                      
         USING DXBLOCKD,RF                                                      
         GOTO1 DXPUT,DMCB,DXASQLB,ADXBLOCK                                      
         J     EXIT                                                             
         DROP  RF                                                               
         LTORG                                                                  
         EJECT                                                                  
*================================================================               
* SET DELIMITERS AT END OF EACH FIELD IN RECORD                                 
* ON ENTRY R1 POINTS TO FIELD TABLE IN TAXCNVX                                  
*================================================================               
                                                                                
INITREC  NTR1  BASE=*,LABEL=*                                                   
         LH    R5,0(R1)            GET TABLE LENGTH                             
         AR    R5,R1               POINT TO END OF TABLE                        
         LA    R4,2(R1)            POINT TO FIRST FIELD                         
*                                                                               
INITREC2 SR    RE,RE                                                            
         ICM   RE,3,2(R4)          GET DSPL TO FIELD                            
         AR    RE,R2               POINT TO FIELD                               
         BCTR  RE,0                BACK UP TO END OF PREV                       
         MVI   0(RE),EOFCHAR                                                    
*                                                                               
         LA    R4,9(R4)                                                         
         CR    R4,R5                                                            
         BL    INITREC2                                                         
*                                                                               
         AHI   R4,-9               BACK UP TO LAST ENTRY                        
         SR    RF,RF                                                            
         ICM   RF,3,0(R4)          GET LEN OF LAST FIELD                        
         LA    RE,1(RE,RF)         POINT TO END OF LAST FIELD                   
         MVI   0(RE),EOFCHAR                                                    
         MVI   1(RE),EOFCHAR                                                    
         XIT1                                                                   
         LTORG                                                                  
         EJECT                                                                  
*===============================================================                
* CLEAR FROM TOP OF BUFFER1 TO NEXTOUT                                          
*===============================================================                
                                                                                
CLRBUFF  NTR1  BASE=*,LABEL=*                                                   
         L     R0,=A(BUFFER1)                                                   
         ICM   R1,15,NEXTOUT                                                    
         BZ    CLRBUFFX                                                         
         LA    R1,256(R1)          ADD FOR GOOD MEASURE                         
         SR    R1,R0                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
CLRBUFFX XIT1                                                                   
                                                                                
*===============================================================                
* CLEAR ALL OF BUFFER1                                                          
*===============================================================                
                                                                                
CLRBUFF1 NTR1  BASE=*,LABEL=*                                                   
         L     R0,=A(BUFFER1)                                                   
         L     R1,=A(BUFFER1X)                                                  
         SR    R1,R0                                                            
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         J     CLRBUFFX                                                         
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS REQUIRED FOR ALL ROUTINES                    *         
***********************************************************************         
*                                                                               
         DS    0D                                                               
SSNPARMS DS    0XL24                                                            
SSNPARM1 DC    A(0)                                                             
SSNPARM2 DC    A(SSNTAB)                                                        
SSNPARM3 DC    F'0'                                                             
SSNPARM4 DC    F'9'                                                             
SSNPARM5 DC    AL1(0),AL3(9)                                                    
SSNPARM6 DC    A((SSNTABX-SSNTAB)/9)                                            
                                                                                
         DS    0D                                                               
PRDPARMS DS    0XL24                                                            
PRDPARM1 DC    A(0)                                                             
PRDPARM2 DC    A(PRDTAB)                                                        
PRDPARM3 DC    F'0'                                                             
PRDPARM4 DC    A(L'PRDTAB)                                                      
PRDPARM5 DC    AL1(0),AL3(L'PRDTAB)                                             
PRDPARM6 DC    A((PRDTABX-PRDTAB)/L'PRDTAB)                                     
*                                                                               
         DS    0D                                                               
OVRPARMS DS    0XL24                                                            
OVRPARM1 DC    A(0)                                                             
OVRPARM2 DC    A(OVRTAB)                                                        
OVRPARM3 DC    F'0'                                                             
OVRPARM4 DC    A(OVRTABL)                                                       
OVRPARM5 DC    AL1(0),AL3(OVRTABKL)                                             
OVRPARM6 DC    A((OVRTABX-OVRTAB)/OVRTABL)                                      
*                                                                               
*MXTRT                                                                          
       ++INCLUDE MXTRT                                                          
         EJECT                                                                  
*          DATA SET TASYSVAL   AT LEVEL 185 AS OF 05/12/10                      
*=====================================================================          
*              SSNPCK - ENCRYPTS SS#                                            
*                                  P1=A(SS#)                                    
*                                  P2=A(PID)                                    
*                                    CC=EQ GOOD ENCRYPTION                      
*                                    CC=NEQ BAD ENCRYPTION                      
*=====================================================================          
                                                                                
SSNPCK   NTR1  BASE=*,LABEL=*                                                   
         L     R2,4(R1)                                                         
         MVC   0(6,R2),MYSPACES    BLANK OUT PID                                
*                                                                               
         LA    R4,9                TEST SS# FOR NON NUMERIC                     
         L     R3,0(R1)                                                         
SSNPCK5  CLI   0(R3),C'0'                                                       
         BL    *+12                                                             
         CLI   0(R3),C'9'                                                       
         BNH   SSNPCK7                                                          
         LTR   RB,RB               CC=NEQ                                       
         B     SSNPKXT                                                          
*                                                                               
SSNPCK7  LA    R3,1(R3)            BUMP TO NEXT CHAR                            
         BCT   R4,SSNPCK5                                                       
*                                                                               
         L     R3,0(R1)                                                         
         PACK  DUB,0(9,R3)         CONVERT CHARACTER SSN TO HEX SSN             
         NI    DUB+7,X'FC'                                                      
         CVB   RE,DUB                                                           
         ST    RE,FULL             FULL=HEX SSN                                 
*                                                                               
         LA    R3,6                6 DIGITS                                     
         L     R2,4(R1)            PID # ADDRESS                                
         LA    RF,BASE36TB                                                      
SSNPK10  CLI   0(RF),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'00'                                                            
         BAS   RE,SSNPCALC                                                      
         BNE   SSNPKXT                                                          
         MVC   0(1,R2),BYTE        NEXT DIGIT OF PID #                          
         LA    RF,L'BASE36TB(RF)                                                
         LA    R2,1(R2)                                                         
         BCT   R3,SSNPK10                                                       
         CR    RB,RB               CC=EQ                                        
*                                                                               
SSNPKXT  XIT1                                                                   
         EJECT                                                                  
*                                                                               
SSNPCALC NTR1                                                                   
         XR    R0,R0                                                            
         L     R1,FULL             SSN                                          
         D     R0,0(RF)            DIVIDE BY BASE 36 NUMBER                     
         XC    HALF,HALF                                                        
         STC   R1,HALF+1                                                        
         ZICM  R0,0(RF),4                                                       
         MH    R0,HALF             MULTIPLY QUOTIENT BY POWER OF 36             
         L     RE,FULL                                                          
         SR    RE,R0               SUBTRACT PRODUCT FROM REMAINING SSN          
         ST    RE,FULL             KEEP TRACK OF REMAINING SSN                  
*                                                                               
         LA    R3,BASE36T2                                                      
SCALC30  CLI   0(R3),X'FF'                                                      
         BE    SCALCNO                                                          
         CLC   HALF+1(1),0(R3)     FIND MATCH IN TABLE                          
         BE    SCALC40                                                          
         LA    R3,L'BASE36T2(R3)                                                
         B     SCALC30                                                          
SCALC40  MVC   BYTE,1(R3)          COMPLEMENTED BASE 36 NUMBER                  
*                                                                               
SCALCYES CR    RB,RB                                                            
         B     *+6                                                              
SCALCNO  LTR   RB,RB                                                            
         B     SSNUPXT                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*=====================================================================          
*              SSNUNPK - DECRYPTS PID                                           
*                                  P1=A(PID)                                    
*                                  P2=A(SSN)                                    
*                                    CC=EQ GOOD DECRYPTION                      
*                                    CC=NEQ BAD DECRYPTION                      
*=====================================================================          
                                                                                
SSNUPK   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         L     R3,4(R1)            CLEAR OUT SS#, IN CASE PID INVALID           
         MVC   0(9,R3),MYSPACES                                                 
*                                                                               
         L     R2,0(R1)            PID #                                        
         LA    R3,6                6 DIGITS                                     
         LA    RF,BASE36TB                                                      
         XR    R0,R0                                                            
SSNUP10  CLI   0(RF),X'FF'         EOT?                                         
         BNE   SSNUP20                                                          
         LTR   RB,RB                                                            
         B     SSNUPXT             LEAVE, WITH SS# AS BLANK                     
SSNUP20  BAS   RE,SSNUCALC         UNPACK CALCULATIONS                          
         BNE   SSNUPXT                                                          
         A     R0,FULL                                                          
         LA    RF,L'BASE36TB(RF)                                                
         LA    R2,1(R2)                                                         
         BCT   R3,SSNUP10                                                       
         L     R3,4(R1)                                                         
         EDIT  (R0),(9,(R3)),ZERO=NOBLANK,FILL=0                                
         CR    RB,RB                                                            
SSNUPXT  XIT1                                                                   
         EJECT                                                                  
*                                                                               
SSNUCALC NTR1                                                                   
         LA    R3,BASE36T2                                                      
SCALU10  CLI   0(R3),X'FF'                                                      
         BE    SCALUNO                                                          
         CLC   0(1,R2),1(R3)       FIND MATCH OF BASE 36 NUM IN TABLE           
         BE    SCALU20                                                          
         LA    R3,L'BASE36T2(R3)                                                
         B     SCALU10                                                          
SCALU20  XC    HALF,HALF                                                        
         MVC   HALF+1(1),0(R3)     HEX EQUIVALENT NUMBER                        
         ZICM  RE,0(RF),4                                                       
         MH    RE,HALF                                                          
         STCM  RE,15,FULL                                                       
*                                                                               
SCALUYES CR    RB,RB                                                            
         B     *+6                                                              
SCALUNO  LTR   RB,RB                                                            
         B     SSNUPXT                                                          
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
BASE36TB DS    0F                                                               
         DC    F'60466176'         36 TO THE 5TH POWER                          
         DC    F'1679616'          36 TO THE 4TH POWER                          
         DC    F'46656'            36 TO THE 3RD POWER                          
         DC    F'1296'             36 TO THE 2ND POWER                          
         DC    F'36'               36 TO THE 1ST POWER                          
         DC    F'1'                36 TO THE ZERO POWER                         
         DC    X'FFFFFFFF'                                                      
         SPACE                                                                  
BASE36T2 DS    0XL2                                                             
         DC    XL1'0',CL1'C'                                                    
         DC    XL1'1',CL1'U'                                                    
         DC    XL1'2',CL1'7'                                                    
         DC    XL1'3',CL1'N'                                                    
         DC    XL1'4',CL1'2'                                                    
         DC    XL1'5',CL1'A'                                                    
         DC    XL1'6',CL1'T'                                                    
         DC    XL1'7',CL1'K'                                                    
         DC    XL1'8',CL1'Z'                                                    
         DC    XL1'9',CL1'8'                                                    
         DC    XL1'A',CL1'R'                                                    
         DC    XL1'B',CL1'Q'                                                    
         DC    XL1'C',CL1'D'                                                    
         DC    XL1'D',CL1'0'                                                    
         DC    XL1'E',CL1'X'                                                    
         DC    XL1'F',CL1'B'                                                    
         DC    XL1'10',CL1'J'                                                   
         DC    XL1'11',CL1'V'                                                   
         DC    XL1'12',CL1'5'                                                   
         DC    XL1'13',CL1'S'                                                   
         DC    XL1'14',CL1'G'                                                   
         DC    XL1'15',CL1'P'                                                   
         DC    XL1'16',CL1'W'                                                   
         DC    XL1'17',CL1'L'                                                   
         DC    XL1'18',CL1'9'                                                   
         DC    XL1'19',CL1'6'                                                   
         DC    XL1'1A',CL1'Y'                                                   
         DC    XL1'1B',CL1'1'                                                   
         DC    XL1'1C',CL1'F'                                                   
         DC    XL1'1D',CL1'M'                                                   
         DC    XL1'1E',CL1'4'                                                   
         DC    XL1'1F',CL1'I'                                                   
         DC    XL1'20',CL1'3'                                                   
         DC    XL1'21',CL1'E'                                                   
         DC    XL1'22',CL1'H'                                                   
         DC    XL1'23',CL1'O'                                                   
         DC    X'FF'                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*SSNTAB*'                                                    
SSNTAB   DS    10000CL9                                                         
SSNTABX  EQU   *                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*PRDTAB*'                                                    
PRDTAB   DS    5000CL18                                                         
PRDTABX  EQU   *                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*OVRTAB*'                                                    
OVRTAB   DS    20000XL16                                                        
OVRTABX  EQU   *                                                                
*                                                                               
PRDTABD  DSECT                                                                  
PRDTBAGY DS    CL6                                                              
PRDTBCLT DS    CL6                                                              
PRDTBPRD DS    CL6                                                              
*                                                                               
OVRTABD  DSECT                                                                  
OVRTBCML DS    XL4                 INTERNAL CMML CODE                           
OVRTBCST DS    XL2                 CAST SEQNUM                                  
OVRTBCOD DS    XL3                 OVERSCALE CODE                               
OVRTBUSE DS    CL3                 USE CTGY CODE                                
OVRTABKL EQU   *-OVRTABD                                                        
         DS    CL1                                                              
OVRTBAMT DS    XL4                 PERCENTAGE AMOUNT                            
OVRTABL  EQU   *-OVRTABD                                                        
*                                                                               
TAXROUTS CSECT                                                                  
*                                                                               
         DS    0D                                                               
         DC    CL8'*VRSNTAB'                                                    
VRSNTAB  DS    250CL16                                                          
VRSNTABX EQU   *                                                                
*                                                                               
         DS    0D                                                               
         DC    CL8'*SVELEM*'                                                    
SVELEM   DS    XL256                                                            
*                                                                               
         DS    0D                                                               
         DC    CL8'*BUFFER1'                                                    
BUFFER1  DS    1000000C                                                         
BUFFER1X EQU   *                                                                
*                                                                               
VRSNTABD DSECT                                                                  
VRSNNUM  DS    XL1                                                              
VRSNID   DS    CL12                                                             
VRSNLEN  DS    XL1                                                              
VRSNTYPE DS    XL1                                                              
         DS    XL1                                                              
*                                                                               
TAXROUTS CSECT                                                                  
                                                                                
         EJECT                                                                  
       ++INCLUDE TAXWORKD                                                       
         ORG   OVSTOR                                                           
APARMS   DS    A                                                                
ASSNPARM DS    A                                                                
AOVRPARM DS    A                                                                
AMXTRT   DS    A                                                                
NEXTADDR DS    A                                                                
SVCMML   DS    F         INTERNAL CMML NUMBER                                   
SVGUARCD DS    F         SAVE GUARANTEE CODE                                    
SVAPPL   DS    F         APPLIED INDICATOR                                      
SVDSTUS  DS    H         STARTING USE NUMBER                                    
SVUSESPD DS    H         USES PAID                                              
SVUSETYP DS    CL3       USE TYPE                                               
SVCYCSTR DS    XL3       CYCLE START                                            
SVCYCEND DS    XL3       CYCLE END                                              
SVGRSTR  DS    XL3       GUARANTEE CYCLE START                                  
SVGREND  DS    XL3       GUARANTEE CYCLE END                                    
SVPAYST  DS    XL1       PAYMENT STATUS                                         
SVINVDT  DS    XL3       INVOICE DATE                                           
SVCMMLID DS    CL12      CMML ID                                                
SVCKSORT DS    CL6       LAST 2 BYTES ARE CAST NUMBER                           
SVCKSSN  DS    CL9       SSN                                                    
SVINV    DS    CL6       INVOICE NUMBER COMPLEMENTED                            
SVAGY    DS    CL6       AGENCY CODE                                            
SVCLT    DS    CL6       CLIENT CODE                                            
SVPRD    DS    CL6       PRODUCT CODE                                           
SVCAST   DS    XL2       SAVED CAST SEQNUM                                      
SVAPPCOD DS    CL1       APPLY CODE                                             
SVINSTAT DS    XL1                                                              
SVHIVER  DS    XL1                                                              
         DS    0D                                                               
SVCASTKY DS    XL32      SAVED CAST KEY                                         
SVINVKEY DS    XL32      SAVED INVOICE KEY                                      
SVSUBEL  DS    D                                                                
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE DXDSECTS                                                       
       ++INCLUDE TAGENFILE                                                      
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE DDCOMFACS                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'028TAXROUTS  02/24/11'                                      
         END                                                                    
