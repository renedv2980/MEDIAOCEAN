*          DATA SET FALOCKET   AT LEVEL 047 AS OF 02/13/09                      
*CATALP LOCKET                                                                  
LOCKET   TITLE 'LOCKET - BUILD AND MAINTAIN LOCK TABLE IN DSPACE'               
LOCKET   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,*LOCKET*,CLEAR=YES                                         
         USING WORKD,RC                                                         
         ST    RD,SAVERD                                                        
         LR    R9,R1                                                            
         USING LOCKETD,R9          R9=A(LOCKET CONTROL BLOCK)                   
         MVI   LKFLAG,NO                                                        
                                                                                
         TIME                                                                   
         STCM  R0,14,LKTIME        GET DATE/TIME                                
         MVC   LKRTIME,LKTIME                                                   
         XC    LKRTIME,EFFS        COMPLEMENTED TIME                            
         SRL   R1,4                                                             
         STCM  R1,7,LKDATE         SET DATE                                     
                                                                                
         XR    RF,RF                                                            
         ICM   RF,7,LKKEY                                                       
         ST    RF,AKEY             SET A(KEY)                                   
                                                                                
         ICM   RF,7,LKCOMFAC       RF=A(COMFACS)                                
         USING COMFACSD,RF         EXTRACT REQUIRED VALUES                      
         MVC   ADMGR,CDATAMGR                                                   
         MVC   ASWITCH,CSWITCH                                                  
         MVC   ALOCKSPC,CLOCKSPC                                                
         MVC   APROTON,CPROTON                                                  
         MVC   APROTOFF,CPROTOFF                                                
         DROP  RF                                                               
                                                                                
         ICM   RF,15,ASWITCH       NO SWITCH DEFINED OFFLINE                    
         BZ    INIT02                                                           
                                                                                
         MVI   OFFLINE,NO          DEFAULT TO ONLINE                            
         GOTOR (RF),DMCB,X'FEFFFFFF',0                                          
         L     RF,0(R1)                                                         
         USING SYSFACD,RF                                                       
         USING SSBD,R2                                                          
         L     R2,VSSB             EXTRACT REQUIRED VALUES                      
*                                                                               
         GOTOR APROTOFF            PROTECTION OFF                               
         OI    SSBSTAT4,SSBDSLCK   SET LOCKET IN DATASPACE                      
         GOTOR APROTON             PROTECTION ON                                
         DROP  RF                                                               
*                                                                               
         MVC   ALET,SSBTBLET                                                    
         MVC   FAC,SSBSYSID                                                     
                                                                                
         ICM   R1,15,SSBTKADR      GET TASKADR IF ACTIVE                        
         BZ    INIT04                                                           
         USING TCBD,R1                                                          
         MVC   AUTL,TCBUTL         A(UTL)                                       
         MVC   LKLUID,TCBSYM       LUID                                         
                                                                                
         ICM   R1,15,AUTL                                                       
         BZ    *+10                                                             
         MVC   LKSENUM,TSYS-UTLD(R1)                                            
         B     INIT04                                                           
                                                                                
INIT02   MVI   OFFLINE,YES         SET OFFLINE                                  
         LA    RF,RETURN           Dummy up call                                
         ST    RF,APROTOFF         This will just return                        
         ST    RF,APROTON          This will just return                        
*                                                                               
         ICM   R2,15,=V(SSB)                                                    
         BZ    INIT03                                                           
*AH      BNZ   INIT02A                                                          
*AH      GOTOR ADMGR,DMCB,=CL8'SSBAD',0,0                                       
*AH      ICM   R2,15,4(R1)                                                      
*AH      BNZ   INIT02A                                                          
*AH      DC    H'00'                                                            
*AH      MVC   AUTL,8(R1)          Need to put into DMGR                        
                                                                                
         USING SSBD,R2                                                          
INIT02A  CLI   SSOXTND,X'FF'       Require extended SSB                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   ALET,SSOTBLET                                                    
*                                  Off-line only                                
INIT03   ICM   RF,15,=V(UTL)                                                    
         BZ    INIT04                                                           
*AH      OC    AUTL,AUTL           Was this set                                 
*AH      BNZ   *+8                 Yes, must be from DMGR call                  
         ST    RF,AUTL                                                          
         MVC   LKSENUM,TSYS-UTLD(RF)                                            
                                                                                
INIT04   XC    DUB,DUB             ENQUIRE FOR LOCKET TABLE                     
         MVC   DUB(4),=AL4(DTLOCKET)                                            
         MVI   DUB,X'20'                                                        
         BRAS  RE,LOCKPROT                                                      
                                                                                
         ICM   RF,15,4(R1)         GET A(TABLE)                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVC   ATAB,DSPTFRST-DMSPACED(RF)                                       
         MVI   ATAB,0              REMOVE ALL POST INFORMATION                  
         MVC   ATABX,DSPTEND-DMSPACED(RF)                                       
         MVC   ATABW,DSPTWIDE-DMSPACED(RF)                                      
         DROP  R2                                                               
                                                                                
LOCAL    USING UPDTABD,WRKKEY                                                   
         EJECT                                                                  
***********************************************************************         
* GO TO ROUTINE BY ACTION                                             *         
***********************************************************************         
                                                                                
         CLI   LKACTN,LKLOCKQ      ADD A LOCK ENTRY                             
         BE    LKLOCK                                                           
         CLI   LKACTN,LKTESTQ      TEST A LOCK ENTRY                            
         BE    LKTEST                                                           
         CLI   LKACTN,LKTESTW      TEST A WHOLE LOCK ENTRY                      
         BE    LKTEST                                                           
         CLI   LKACTN,LKUNLKQ      REMOVE A LOCK ENTRY (SOON)                   
         BE    LKUNLK                                                           
         CLI   LKACTN,LKUNGLQ      REMOVE A LOCK ENTRY (GLOBAL)                 
         BE    LKGLOB                                                           
         CLI   LKACTN,LKSTOPQ      INHIBIT LOCKING                              
         BE    LKSTOP                                                           
         CLI   LKACTN,LKFREEQ      UNINHIBIT LOCKING                            
         BE    LKFREE                                                           
         CLI   LKACTN,LKATABQ      GET A(UPDTAB)                                
         BE    LKADDR                                                           
         CLI   LKACTN,LKBILDQ      BUILD UPDTAB (NO LONGER SUPPORTED)           
         BE    EXITOK                                                           
         DC    H'0'                WHAT ARE YOU DOING?                          
         EJECT                                                                  
***********************************************************************         
* ADD A LOCK ENTRY                                                    *         
***********************************************************************         
                                                                                
LKLOCK   CLI   OFFLINE,YES         OFFLINE?                                     
         BE    XMOD                YES - DO NOTHING                             
                                                                                
         GOTOR LKQBIT              LOCKS INHIBITED?                             
         BL    ERR04               YES - ERROR                                  
                                                                                
         GOTOR KEYPAD              BUILD WORK KEY                               
         GOTOR TABLOCK             LOCK TABLE                                   
         BNE   ERR02                                                            
         GOTOR CHKKEY              KEY ALREADY LOCKED?                          
         BE    ERR01               YES - ERROR                                  
         GOTOR ADDKEY              ADD THIS KEY                                 
         BNE   ERR03               ERROR - TABLE FULL                           
         GOTOR TABFREE             FREE TABLE                                   
                                                                                
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* TEST A LOCK ENTRY                                                   *         
***********************************************************************         
                                                                                
LKTEST   CLI   OFFLINE,YES         DO NOTHING OFFLINE                           
         BE    XMOD                                                             
         GOTOR KEYPAD              BUILD WORK KEY                               
         GOTOR CHKKEY              LOOK FOR KEY                                 
         BE    ERR01               KEY ALREADY LOCKED                           
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* REMOVE A LOCK ENTRY (SOON)                                          *         
***********************************************************************         
                                                                                
LKUNLK   GOTOR KEYPAD              BUILD WORK KEY                               
                                                                                
         CLI   OFFLINE,YES         TEST FOR OFFLINE                             
         BNE   LKUN02              NO                                           
         GOTOR CTSWIT                                                           
         BNE   ERR06               CAN'T SWITCH TO CONTROL                      
         LA    R7,IO                                                            
         USING CT8REC,R7                                                        
         XC    CT8KEY,CT8KEY       CLEAR KEY                                    
         MVI   CT8KTYP,CT8KTYPQ    FILL IN KEY                                  
         MVC   CT8KDATE,LOCAL.UPDLDATE                                          
         MVC   CT8KSE,LOCAL.UPDLTSE                                             
         MVC   CT8KAG,LOCAL.UPDLTAG                                             
         MVC   CT8KTYPE,LOCAL.UPDLTYP                                           
         MVC   CT8KKEY,LOCAL.UPDLKEY                                            
         MVI   CT8STAT,X'40'       SET UNLOCKED                                 
         LHI   R0,28                                                            
         STCM  R0,3,CT8LEN                                                      
         GOTOR ADMGR,DMCB,DMADD,CTFILE,IO,IO                                    
         CLI   8(R1),0                                                          
         BE    LKUNX                                                            
         DC    H'0'                                                             
         DROP  R7                                                               
                                                                                
LKUN02   GOTOR TABLOCK                                                          
         BNE   ERR02               UNABLE TO LOCK TABLE                         
         GOTOR DELKEY                                                           
         BNE   ERR05               KEY NOT FOUND - CAN'T DELETE IT              
         GOTOR TABFREE                                                          
                                                                                
LKUNX    CLI   OFFLINE,YES         TEST FOR OFFLINE                             
         BNE   XMOD                NO                                           
         GOTOR SESWIT              SWITCH BACK TO NATIVE                        
         BNE   ERR06                                                            
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* REMOVE A LOCK ENTRY (GLOBAL)                                        *         
***********************************************************************         
                                                                                
LKGLOB   GOTOR KEYPAD              BUILD WORK KEY                               
                                                                                
         GOTOR TABLOCK                                                          
         BNE   ERR02               UNABLE TO LOCK TABLE                         
         GOTOR DELKEY                                                           
         BNE   ERR05               KEY NOT FOUND - CAN'T DELETE IT              
         GOTOR TABFREE                                                          
                                                                                
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* INHIBIT LOCKING                                                     *         
***********************************************************************         
                                                                                
LKSTOP   LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTOFF                                                         
         LAM   AR2,AR2,ALET                                                     
         ICM   R2,15,=AL4(DTLOCKET)                                             
         N     R2,=XL4'00000FFF'                                                
         SLL   R2,6                                                             
         SAC   512                                                              
         USING DMSPACED,R2                                                      
         MVI   DSPUSER,X'FF'                                                    
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
         GOTOR APROTON                                                          
         B     XMOD                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* UNINHIBIT LOCKING                                                   *         
***********************************************************************         
                                                                                
LKFREE   LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTOFF                                                         
         LAM   AR2,AR2,ALET                                                     
         ICM   R2,15,=AL4(DTLOCKET)                                             
         N     R2,=XL4'00000FFF'                                                
         SLL   R2,6                                                             
         SAC   512                                                              
         USING DMSPACED,R2                                                      
         MVI   DSPUSER,X'00'                                                    
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
         GOTOR APROTON                                                          
         B     XMOD                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* RETURN A(LOCKTAB)                                                   *         
***********************************************************************         
                                                                                
LKADDR   L     RF,ATAB             GET A(UPDTAB)                                
         STCM  RF,7,LKKEY                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* PAD OUT AKEY TO PRODUCE INTERNAL WRKKEY                             *         
***********************************************************************         
                                                                                
KEYPAD   NTR1  ,                                                                
         L     R2,AKEY                                                          
         USING LKKEYD,R2                                                        
         XC    WRKKEY,WRKKEY                                                    
         MVI   LOCAL.UPDTABT,UPDTLQ      SET LOCKET ENTRY                       
         MVC   LOCAL.UPDLDATE,LKDATE     TODAYS DATE                            
         MVC   LOCAL.UPDLFAC,FAC         FACPAK                                 
         MVC   LOCAL.UPDLTSE,LKSENUM     SYSTEM SE NUMBER                       
         CLI   LOCKSE,0                                                         
         BE    *+10                                                             
         MVC   LOCAL.UPDLTSE,LOCKSE      CALLER CAN OVERRIDE                    
         CLI   LOCAL.UPDLTSE,0                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVC   LOCAL.UPDLTAG,LOCKAGY                                            
         MVC   LOCAL.UPDLTYP,LOCKRTY                                            
         MVC   LOCAL.UPDLKEY,LOCKKEY                                            
         MVC   LOCAL.UPDLTIME,LKTIME                                            
         MVI   LOCAL.UPDLSTAT,0                                                 
         MVC   LOCAL.UPDLUID,LKLUID                                             
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* DELETE WRKKEY FROM CONTROL FILE                                     *         
* EXIT: CC EQ  RECORD DELETED OK                                      *         
*          NE  DMGR ERROR                                             *         
***********************************************************************         
                                                                                
CTDEL    NTR1  ,                                                                
         CLI   OFFLINE,YES         TEST FOR OFFLINE                             
         BNE   EXITOK              NO - DO NOTHING                              
                                                                                
         EJECT                                                                  
***********************************************************************         
* LOCK TABLE IF POSSIBLE. EXIT CC=EQU IF OK                           *         
***********************************************************************         
                                                                                
TABLOCK  NTR1  ,                                                                
         CLI   LKFLAG,YES          I LOCKED TABLE ALREADY?                      
         BE    EXITOK              YES                                          
                                                                                
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTLOCKET)                                            
         BRAS  RE,LOCKPROT         LOCK TABLE                                   
         LAM   AR0,ARF,ARZERO      DON'T TRUST LOCKSPC                          
                                                                                
         MVI   LKFLAG,YES          FLAG THAT I LOCKED TABLE                     
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* FREE TABLE                                                          *         
***********************************************************************         
                                                                                
TABFREE  NTR1  ,                                                                
         CLI   LKFLAG,YES          FLAG THAT I LOCKED TABLE                     
         BNE   EXITOK                                                           
                                                                                
         XC    DUB,DUB                                                          
         MVC   DUB(4),=AL4(DTLOCKET)                                            
         MVI   DUB,X'10'                                                        
         BRAS  RE,LOCKPROT         FREE TABLE                                   
         LAM   AR0,ARF,ARZERO      DON'T TRUST LOCKSPC                          
                                                                                
         MVI   LKFLAG,NO           FLAG THAT I UNLOCKED TABLE                   
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* TEST IF LOCKING HAS BEEN STOPPED                                    *         
***********************************************************************         
                                                                                
LKQBIT   NTR1  ,                                                                
         XC    DUB,DUB             ENQUIRE FOR LOCKET TABLE                     
         MVC   DUB(4),=AL4(DTLOCKET)                                            
         MVI   DUB,X'20'                                                        
         BRAS  RE,LOCKPROT         FREE TABLE                                   
         LAM   AR0,ARF,ARZERO                                                   
                                                                                
         ICM   RF,15,4(R1)          GET A(TABLE HEADER)                         
         BNZ   *+6                                                              
         DC    H'0'                                                             
                                                                                
         CLI   DSPUSER-DMSPACED(RF),X'FF'                                       
         BE    EXITL                                                            
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* CHECK FOR WORK KEY IN TABLE                                         *         
* EXIT: CC EQ  MATCHED A KEY                                          *         
***********************************************************************         
                                                                                
CHKKEY   NTR1  ,                                                                
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTOFF                                                         
         LAM   AR2,AR2,ALET                                                     
         L     R2,ATAB                                                          
         LH    RE,ATABW                                                         
         L     RF,ATABX                                                         
         SAC   512                                                              
         USING UPDTABD,R2                                                       
                                                                                
CKL02    CLI   UPDTABT,0           END OF TABLE                                 
         BE    CKL14                                                            
         CLI   UPDTABT,UPDTLQ      CHECK IF LOCKET TYPE LOCK                    
         BNE   CKL12                                                            
         CLC   UPDLDATE,LOCAL.UPDLDATE                                          
         BNE   CKL12               DIFFERENT DATE                               
         CLC   UPDLTSE,LOCAL.UPDLTSE                                            
         BNE   CKL12               DIFFERENT SE                                 
                                                                                
         XR    R1,R1                                                            
         ICM   R1,3,UPDLTAG        CHECK AGENCY - ZERO MATCHES ALL              
         BZ    CKL04                                                            
         ICM   R1,3,LOCAL.UPDLTAG                                               
         BZ    CKL04                                                            
         CLC   UPDLTAG,LOCAL.UPDLTAG                                            
         BE    CKL04                                                            
         B     CKL12                                                            
                                                                                
CKL04    ICM   R1,3,UPDLTYP        CHECK RTYPE - ZERO MATCHES ALL               
         BZ    CKL06                                                            
         ICM   R1,3,LOCAL.UPDLTYP                                               
         BZ    CKL06                                                            
         CLC   UPDLTYP,LOCAL.UPDLTYP                                            
         BE    CKL06                                                            
         B     CKL12                                                            
                                                                                
CKL06    LHI   R0,L'UPDLKEY        KEY TEST                                     
         LA    R3,LOCAL.UPDLKEY                                                 
         LA    R1,UPDLKEY                                                       
         CPYA  AR1,AR2                                                          
                                                                                
CKL08    CLI   LKACTN,C'W'         TEST COMPARE WHOLE KEY                       
         BNE   CKL09                                                            
         CLC   0(L'UPDLKEY,R1),0(R3)                                            
         BNE   CKL12                                                            
         B     CKL11                                                            
                                                                                
CKL09    CLI   0(R1),0             ZERO IN KEY MATCHES ALL                      
         BE    CKL10                                                            
         CLI   0(R3),0                                                          
         BE    CKL10                                                            
         CLC   0(1,R1),0(R3)       COMPARE KEY BYTE                             
         BE    CKL10                                                            
         B     CKL12                                                            
                                                                                
CKL10    AHI   R1,1                TEST NEXT BYTE                               
         AHI   R3,1                                                             
         BCT   R0,CKL09                                                         
                                                                                
CKL11    SAC   0                   KEY PREVIOUSLY LOCKED                        
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTON                                                          
         B     EXITOK                                                           
                                                                                
CKL12    BXLE  R2,RE,CKL02         NEXT ENTRY IN TABLE                          
                                                                                
CKL14    SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTON                                                          
         B     EXITL                                                            
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* ADD WORK KEY TO TABLE                                               *         
* EXIT CC EQ  KEY ADDED OK                                            *         
*      CC NE  TABLE FULL                                              *         
***********************************************************************         
                                                                                
ADDKEY   NTR1  ,                                                                
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTOFF                                                         
         LAM   AR2,AR2,ALET                                                     
         L     R2,ATAB                                                          
         LH    RE,ATABW                                                         
         L     RF,ATABX                                                         
         SAC   512                                                              
         USING UPDTABD,R2                                                       
                                                                                
ADK02    CLI   UPDTABT,0           LOOK FOR FREE ENTRY                          
         BE    ADK04                                                            
         BXLE  R2,RE,ADK02                                                      
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
         GOTOR APROTON                                                          
         B     EXITL                                                            
                                                                                
ADK04    MVC   UPDTABT(UPDTABL),WRKKEY                                          
         SAC   0                                                                
         LAM   AR2,AR2,ARZERO                                                   
         GOTOR APROTON                                                          
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* DELETE KEY FROM TABLE                                               *         
* EXIT: CC EQ  KEY DELETED                                            *         
*       CC NE  KEY NOT FOUND                                          *         
***********************************************************************         
                                                                                
DELKEY   NTR1  ,                                                                
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTOFF                                                         
         LAM   AR2,AR2,ALET                                                     
         L     R2,ATAB                                                          
         LH    RE,ATABW                                                         
         L     RF,ATABX                                                         
         SAC   512                                                              
         USING UPDTABD,R2                                                       
                                                                                
DLK02    CLC   UPDTABT(UPDKEYL),WRKKEY                                          
         BE    DLK04               MUST BE EXACT MATCH FOR DELETE               
         BXLE  R2,RE,DLK02                                                      
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTON                                                          
         B     EXITL                                                            
                                                                                
DLK04    LA    R3,1(RF)                                                         
         SR    R3,R2               R3=L'DESTINATION                             
         LR    RF,R3                                                            
         SR    RF,RE               RF=L'SOURCE                                  
         AR    RE,R2               RE=FROM ADDRESS                              
         CPYA  ARE,AR2                                                          
         MVCL  R2,RE               MOVE & PAD LAST ENTRY WITH ZERO              
         SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         GOTOR APROTON                                                          
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* SWITCH TO CONTROL SYSTEM                                            *         
***********************************************************************         
                                                                                
CTSWIT   NTR1  ,                                                                
         MVC   SVSENUM,LKSENUM                                                  
         CLI   LKSENUM,0           HAVE AN SE NUMBER?                           
         BE    EXITOK                                                           
                                                                                
         CLI   OFFLINE,YES         OFFLINE?                                     
         BNE   CSW02               NO                                           
         ICM   R1,15,AUTL                                                       
         BZ    EXITOK                                                           
         MVI   TSYS-UTLD(R1),CTLSYSQ                                            
         B     EXITOK                                                           
                                                                                
CSW02    ICM   R0,7,EFFS                                                        
         ICM   R0,8,=AL1(CTLSYSQ)  CONTROL SYSTEM                               
         GOTOR ASWITCH,DMCB,(R0),0                                              
         CLI   4(R1),0                                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* SWITCH TO NATIVE SYSTEM                                             *         
***********************************************************************         
                                                                                
SESWIT   NTR1  ,                                                                
         CLI   SVSENUM,0           HAVE AN SE NUMBER SAVED?                     
         BE    EXITOK              NO                                           
                                                                                
         CLI   OFFLINE,YES         OFFLINE?                                     
         BNE   SSW02               NO                                           
         ICM   R1,15,AUTL                                                       
         BZ    EXITOK                                                           
         MVC   TSYS-UTLD(L'TSYS,R1),SVSENUM                                     
         B     EXITOK                                                           
                                                                                
SSW02    ICM   R0,7,EFFS                                                        
         ICM   R0,8,SVSENUM                                                     
         GOTOR ASWITCH,DMCB,(R0),0                                              
         CLI   4(R1),0                                                          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* Fake protection call for off-line                                             
***********************************************************************         
RETURN   BR    RE                                                               
***********************************************************************         
* Call Lockspc with dub, turn off storage protection off then on      *         
***********************************************************************         
                                                                                
LOCKPROT ST    RE,SVRE                                                          
         GOTOR APROTOFF            Protection off                               
         GOTOR ALOCKSPC,DUB                                                     
         GOTOR APROTON             Protection on                                
         L     RE,SVRE                                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS + HANDY ROUTINES                                        *         
***********************************************************************         
                                                                                
ERR01    MVI   LKERR,X'01'         KEY IS ALREADY LOCKED                        
         B     ERRX                                                             
ERR02    MVI   LKERR,X'02'         CAN'T LOCK THE TABLE (IS BUSY)               
         B     ERRX                                                             
ERR03    MVI   LKERR,X'03'         LOCK TABLE FULL                              
         B     ERRX                                                             
ERR04    MVI   LKERR,X'04'         LOCKING INHIBITED                            
         B     ERRX                                                             
ERR05    MVI   LKERR,X'05'         KEY NOT FOUND FOR UNLOCK                     
         B     ERRX                                                             
ERR06    MVI   LKERR,X'06'         CONTROL NOT STARTED                          
         B     ERRX                                                             
                                                                                
ERRX     GOTOR TABFREE             YES SO FREE IT                               
         GOTOR SESWIT              GET BACK TO NATIVE SYSTEM                    
         B     XMOD                                                             
                                                                                
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
                                                                                
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
                                                                                
XMOD     SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         L     RD,SAVERD                                                        
         CLI   LKERR,0             SET CC FOR RETURN                            
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
*        CONSTANTS & LTORG                                            *         
***********************************************************************         
                                                                                
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
CTLSYSQ  EQU   10                  CONTROL SYSTEM NUMBER                        
                                                                                
ARZERO   DC    16F'0'                                                           
EFFS     DC    X'FFFFFFFF'                                                      
                                                                                
DMADD    DC    C'DMADD   '                                                      
CTFILE   DC    C'CTFILE  '                                                      
                                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
                                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVERD   DS    A                                                                
SVRE     DS    A                                                                
DMCB     DS    6F                                                               
                                                                                
AKEY     DS    A                   A(LOCK KEY)                                  
ALET     DS    A                                                                
ATAB     DS    A                   A(LOCK TABLE IN TABS DSPACE)                 
ATABX    DS    A                   A(END OF LOCK TABLE)                         
ATABW    DS    H                   WIDTH OF 1 ENTRY IN LOCK TABLE               
                                                                                
LKDATE   DS    XL3                 DATE NOW JULIAN                              
LKTIME   DS    XL3                 TIME NOW HH:MM:SS                            
LKRTIME  DS    XL3                 TIME NOW HH:MM:SS (COMPLEMENTED)             
LKSENUM  DS    X                   SENUM ONLINE OR OFFLINE                      
LKLUID   DS    CL8                 LUID IF ONLINE                               
                                                                                
ADMGR    DS    V                   ADDRESSES                                    
ASWITCH  DS    V                                                                
AUTL     DS    V                                                                
ALOCKSPC DS    V                                                                
APROTON  DS    A                                                                
APROTOFF DS    A                                                                
                                                                                
WRKKEY   DS    XL(UPDTABL)         WORK KEY                                     
UPDKEYL  EQU   UPDLTIME-UPDTLOCK   LENGTH FOR COMPARE                           
                                                                                
OFFLINE  DS    C                   SET TO Y IF OFFLINE                          
LKFLAG   DS    X                   SET TO Y IF TABLE WAS LOCKED                 
SVSENUM  DS    X                   SAVED SENUM BEFORE SWITCH                    
FAC      DS    X                   FACPAK ID                                    
                                                                                
IO       DS    XL2048              I/O AREA FOR CTFILE                          
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
                                                                                
* DMDSHDR                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMDSHDR                                                        
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
* FAUPDTAB                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAUPDTAB                                                       
         PRINT ON                                                               
* FALOCKETD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FALOCKETD                                                      
         PRINT ON                                                               
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
* FASYSFAC                                                                      
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
* DMSPACED                                                                      
         PRINT OFF                                                              
       ++INCLUDE DMSPACED                                                       
         PRINT ON                                                               
* FATABSDEQU                                                                    
         PRINT OFF                                                              
       ++INCLUDE FATABSDEQU                                                     
         PRINT ON                                                               
* FASSB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'047FALOCKET  02/13/09'                                      
         END                                                                    
