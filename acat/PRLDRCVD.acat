*          DATA SET PRLDRCVD   AT LEVEL 002 AS OF 03/14/13                      
*CATALP PRLDRCVD                                                                
         TITLE 'LDRCVD - PRINT - SYSTEM RECOVERY DUMP ROUTINE'                  
                                                                                
* PARAMS VIA R1                                                                 
* XL1    X'00'=INIT,X'01'=PROCESS,X'FF'=FINAL. RETURN X'FF'=DELETE              
* AL3    A(RECOVERY FILE RECORD)                                                
                                                                                
         PRINT NOGEN                                                            
LDRCVD   CSECT                                                                  
         ENTRY LDRCVDT             TABLE OF PRINT ERRORS/WARNINGS               
                                                                                
         NMOD1 WORKX-WORKD,PRLDRCVD                                             
         USING WORKD,RC                                                         
         ST    R1,APARM                                                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)          R3=A(RECOVERY HEADER)                        
         USING RCVRECD,R3                                                       
         CLI   0(R1),1             TEST ACTION                                  
         BL    INIT                                                             
         BE    PROCESS                                                          
         B     FINAL                                                            
                                                                                
INIT     LA    RE,LDRCVDT          CLEAR COUNTERS ON INIT CALL                  
INIT1    CLC   0(4,RE),=4X'FF'                                                  
         BE    EXIT                                                             
         XC    0(4,RE),0(RE)                                                    
         LA    RE,L'LDRCVDT(RE)                                                 
         B     INIT1                                                            
                                                                                
FINAL    B     EXIT                NO SPECIAL FINAL CODE                        
                                                                                
DELETE   L     R1,APARM            SET FLAG TO DELETE RECORD                    
         MVI   0(R1),X'FF'                                                      
                                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
* PROCESS A RECOVERY FILE RECORD                                                
* UPDATE TABLE OF ANY ACTIONS THAT NEED PRINTING AND ERRORS/WARNINGS            
* EXIT VIA DELETE TO REQUEST RECORD NOT WRITTEN TO RECOVERY DUMP TAPE           
                                                                                
PROCESS  EQU   *                   PROCESS PRT OR PUB RECORD                    
                                                                                
PRTDIR   CLI   RFILTY,X'40'        TEST PRTDIR REC                              
         BNE   PUBDIR                                                           
         CLI   RECSTAT+1,X'FF'     KEEP ALL DIRECTOTY ONLY RECORDS              
         BNE   PRTDIR1                                                          
         L     R0,DIRONLY                                                       
         AHI   R0,1                                                             
         ST    R0,DIRONLY                                                       
         B     EXIT                                                             
*                                                                               
PRTDIR1  OC    RECKEY+21(3),RECKEY+21   TEST IF PASSIVE                         
         BNZ   DELETE                                                           
*                                                                               
PRTDIR2  CLI   RRECTY,1            TEST IF COPY                                 
         BNE   PRTDIR3                                                          
         MVC   SAVREC,RECREC       SAVE PRTDIR RECORD                           
         B     DELETE                                                           
*                                                                               
PRTDIR3  CLI   RRECTY,2            TEST IF CHANGE                               
         BNE   PRTDIR4                                                          
         CLC   SAVKEY,RECKEY       TEST SAME KEY                                
         BNE   DELETE                                                           
         CLC   SAVADDR,RECADDR     TEST SAME DISK ADDR                          
         BE    DELETE                                                           
         CLI   SAVSTAT,X'FF'       FF DELETES DO NOT CAUSE                      
         BE    DELETE              DIRECTORY OVERLAYS                           
         L     R0,DIROLAY                                                       
         AHI   R0,1                                                             
         ST    R0,DIROLAY                                                       
         B     EXIT                                                             
*                                                                               
PRTDIR4  DS    0H                  MATCHES P131 CODE                            
         B     DELETE              EVERYTHING ELSE DELETE                       
*AH3     CLI   RRECTY,3            TEST IF ADD                                  
*AH3     BNE   DELETE                                                           
*AH3     B     EXIT                                                             
                                                                                
PUBDIR   CLI   RFILTY,X'41'        TEST PUBDIR                                  
         BE    DELETE                                                           
         B     EXIT                                                             
                                                                                
         LTORG                                                                  
                                                                                
SAVREC   DS    0XL31               SAVED DIRECTORY RECORD                       
SAVKEY   DC    XL25'00'                                                         
SAVSTAT  DC    XL2'00'                                                          
SAVADDR  DC    XL4'00'                                                          
                                                                                
* THIS ENTRY POINT TABLE IS PROCESSED BY RCVDMP CALLER AFTER FINAL CALL         
* EACH ENTRY HAS A COUNTER,TEXT,TYPE,AND SPARE                                  
* ENTRIES WITH A NON ZERO COUNTER WILL BE PRINTED                               
* ERRORS/WARNINGS WILL GO TO CONSOLE AND WILL BE EMAILED                        
         DS    0F                                                               
LDRCVDT  DS    0CL24                                                            
DIRONLY  DC    F'0',CL16'PRTDIR ONLY     ',C'I ',AL2(0)                         
DIROLAY  DC    F'0',CL16'PRTDIR OVERLAYS ',C'I ',AL2(0)                         
RCVERR   DC    F'0',CL16'PRINT ERROR     ',C'E ',AL2(0)                         
RCVWARN  DC    F'0',CL16'PRINT WARNINGS  ',C'W ',AL2(0)                         
LDRCVDTX DC    4X'FF'                                                           
                                                                                
* DSECT TO LOCAL WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
APARM    DS    A                                                                
WORKX    EQU   *                                                                
                                                                                
* DSECT TO COVER RECOVERY HEADER                                                
*                                                                               
RCVRECD  DSECT                                                                  
RCVHDR   DS    0XL24                                                            
*DMRCVRHDR                         RECOVERY HEADER                              
       ++INCLUDE DMRCVRHDR                                                      
RECREC   DS    0XL31                                                            
RECKEY   DS    XL25                RECORD KEY                                   
RECSTAT  DS    XL2                 RECORD STATUS                                
RECADDR  DS    XL4                 RECORD DISKADDR                              
*DMRCVREXT                         RECOVERY TRAILER                             
       ++INCLUDE DMRCVREXT                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002PRLDRCVD  03/14/13'                                      
         END                                                                    
