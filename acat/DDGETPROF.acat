*          DATA SET DDGETPROF  AT LEVEL 006 AS OF 01/21/05                      
*CATALP GETPROF                                                                 
         TITLE 'GETPROF - MODULE TO HANDLE USER PROFILES'                       
GETPROF  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**GETPRF,CLEAR=YES                                   
         USING WORKD,RC                                                         
         LM    R2,R3,0(R1)                                                      
         USING KEYD,R2                                                          
         MVC   INDS,0(R1)          A(16 BYTE AREA)                              
         MVC   START,4(R1)         C'M'=AGENCY PROFILE PASSED                   
         CLI   START,C'M'          TEST AGENCY PROFILE PASSED                   
         BE    *+10                                                             
         XC    0(L'CTPVALUE,R3),0(R3)                                           
         MVC   DATAMGR+1(3),9(R1)  V(DATAMGR)                                   
         MVI   DATAMGR,0                                                        
         USING PROFINFD,R8                                                      
         SR    R8,R8               R8 WILL BE SET LATER IF APPROPRIATE          
         CLI   8(R1),X'FF'         TEST A(BUFFER PASSED)                        
         BNE   GETP02                                                           
*                                                                               
         MVC   OPTBUFF+1(3),13(R1)                                              
         SR    R0,R0                                                            
         IC    R0,12(R1)           R0=SIZE OF BUFFER                            
         SLL   R0,10                                                            
         A     R0,OPTBUFF                                                       
         BCTR  R0,0                                                             
         ST    R0,OPTBUFFX         SET A(END OF BUFFER)                         
*                                                                               
GETP02   LA    R9,IO                                                            
         USING CTUREC,R9                                                        
         XC    CTUKEY,CTUKEY                                                    
         MVI   CTUKTYP,CTUKTYPQ                                                 
         MVC   CTUKSYS,KEYSYS                                                   
         MVC   CTUKPROG,KEYPRG                                                  
         TM    CTUKSYS,X'40'       TEST IF SYSTEM IS LOWER CASE                 
         BO    *+12                                                             
         OI    CTUKSYS,X'40'       YES - PROGRAM IS 3 CHARS. LONG               
         B     *+8                                                              
         MVI   CTUKPROG,0                                                       
*                                                                               
         TM    INDS,INDSIND        TEST INDICATOR BYTE PRESENT                  
         BNZ   *+12                                                             
         MVI   INDS,0              NO - CLEAR INDICATORS                        
         B     GETP04                                                           
*                                                                               
         TM    INDS,INDSINF        CALLER WANTS PROFINFD RETURNED?              
         BZ    *+14                                                             
         L     R8,16(R1)           YES: GET A(OUTPUT AREA) FROM P5              
         XC    PROFINFD(PROFINFL),PROFINFD  CLEAR OUTPUT AREA                   
*                                                                               
         TM    INDS,INDSDFT        TEST CALLER WANTS DEFAULT PROFILE            
         BZ    GETP04                                                           
         BAS   RE,GETPRF                                                        
         BZ    GETPX               NO PROFILE DEFINITION RECORD                 
         EJECT                                                                  
***********************************************************************         
* GET PROFILE FOR ANY SYSTEM (EXCEPT ACCOUNT)                         *         
***********************************************************************         
         SPACE 1                                                                
GETP04   CLI   CTUKSYS,KEYSACC     ACCOUNT HAS SPECIAL CODE                     
         BE    GETP20                                                           
         CLC   KEYAGY,SPACES       TEST USER-ID PASSED                          
         BNL   GETP06                                                           
         MVC   CTUKUID,KEYAGY      USE USERID NUMBER AS PASSED                  
         BAS   RE,GETPRF           GET PROFILE                                  
         BZ    GETPX                                                            
         B     GETP10                                                           
*                                                                               
GETP06   BAS   RE,GETUID           GET USERID FROM SYSTEM TABLES                
         BZ    GETP08              NOT FOUND USE AGENCY ID AS PASSED            
         MVC   CTUKUID,USERIDN     ELSE FOUND                                   
         BAS   RE,GETPRF           GET PROFILE                                  
         BNZ   GETP10              CONTINUE IF RECORD FOUND ELSE ...            
*                                                                               
GETP08   MVC   CTUKAGY,KEYAGY      USE AGENCY ALPHA ID AS PASSED                
         CLI   START,C'M'          TEST AGENCY PROFILE PASSED                   
         BE    GETP10                                                           
         BAS   RE,GETPRF           GET PROFILE                                  
         BNZ   GETP10                                                           
         CLI   CTUKSYS,C'S'        TEST SPOT OR NET                             
         BNE   GETPX                                                            
*                                                                               
GETP10   MVC   CTUKMED,KEYMED      SPECIFIC MEDIA FOR AGENCY                    
         BAS   RE,GETPRF                                                        
         BNZ   *+8                                                              
         MVI   MEDSW,1             SET MEDIA NOT FOUND                          
         CLI   KEYOIND2,KEYOIONE   OFFICE FEATURE                               
         BNE   GETP12                                                           
         MVI   CTUKMED,0           ALL MEDIA FOR OFFICE                         
         MVC   CTUKCLT(2),KEYOIND2                                              
         MVI   CTUKCLT+2,C' '                                                   
         BAS   RE,GETPRF                                                        
         MVC   CTUKMED,KEYMED      SPECIFIC MEDIA FOR OFFICE                    
         CLI   MEDSW,0                                                          
         BNE   *+8                                                              
         BAS   RE,GETPRF                                                        
*                                                                               
GETP12   MVI   CTUKMED,0           ALL MEDIA FOR CLIENT                         
         MVC   CTUKCLT,KEYCLI                                                   
         BAS   RE,GETPRF                                                        
         MVC   CTUKMED,KEYMED      SPECIFIC MEDIA FOR CLIENT                    
         CLI   MEDSW,0                                                          
         BNE   *+8                                                              
         BAS   RE,GETPRF                                                        
         B     GETPX                                                            
         EJECT                                                                  
***********************************************************************         
* GET PROFILE FOR ACCOUNT SYSTEM                                      *         
***********************************************************************         
         SPACE 1                                                                
GETP20   CLC   KEYCPY,SPACES                                                    
         BL    GETP22                                                           
         BAS   RE,GETUID           GET USERID FROM SYSTEM TABLES                
         BZ    GETP22              NOT FOUND USE AGENCY ID AS PASSED            
         MVC   CTUKUID,USERIDN     ELSE FOUND                                   
         BAS   RE,GETPRF                                                        
         BNZ   GETP24              CONTINUE IF RECORD FOUND ELSE ...            
*                                                                               
GETP22   MVC   CTUKAGY,KEYCPY      AGENCY APLHA OR USER ID                      
         BAS   RE,GETPRF                                                        
         BZ    GETPX                                                            
*                                                                               
GETP24   OC    KEYUNT(KEYCPY-KEYUNT),KEYUNT                                     
         BZ    GETPX                                                            
         MVC   CTUKUNT,KEYUNT                                                   
         MVC   CTUKLDG,KEYLDG                                                   
         BAS   RE,GETPRF                                                        
         BNZ   *+8                                                              
         MVI   MEDSW,1             SET UNIT/LEDGER NOT FOUND                    
         CLI   KEYOIND1,KEYOITWO   TEST TWO CHARACTER OFFICE CODE               
         BNE   GETP26                                                           
         XC    CTUKUNT(CTUKACT-CTUKUNT),CTUKUNT                                 
         MVI   CTUKACT,KEYOIONE                                                 
         MVC   CTUKACT+1(L'KEYOFF2),KEYOFF2                                     
         B     GETP28                                                           
*                                                                               
GETP26   CLI   KEYOIND1,KEYOIONE                                                
         BNE   GETP30                                                           
         XC    CTUKUNT(CTUKACT-CTUKUNT),CTUKUNT                                 
         MVC   CTUKACT(2),KEYOIND1 COMPANY/OFFICE                               
         MVI   CTUKACT+2,C' '                                                   
*                                                                               
GETP28   BAS   RE,GETPRF                                                        
         MVC   CTUKUNT,KEYUNT                                                   
         MVC   CTUKLDG,KEYLDG                                                   
         CLI   MEDSW,0                                                          
         BNE   *+8                                                              
         BAS   RE,GETPRF                                                        
*                                                                               
GETP30   OC    KEYACT,KEYACT                                                    
         BZ    GETPX                                                            
         MVC   CTUKACT,KEYACT      COMPANY/UNIT/LEDGER/ACCOUNT                  
         CLI   MEDSW,0                                                          
         BNE   *+8                                                              
         BAS   RE,GETPRF                                                        
*                                                                               
GETPX    XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO LOOK FOR USER PROFILE RECORDS                            *         
***********************************************************************         
         SPACE 1                                                                
GETPRF   NTR1  ,                                                                
         XC    DEFAULT,DEFAULT     CLEAR DEFAULT VALUE                          
         MVI   FLAG,0              CLEAR FLAG BYTE                              
         ICM   R1,15,OPTBUFF                                                    
         BZ    GETPRF10                                                         
         USING OPTD,R1             R1=A(OPTIMISATION TABLE)                     
         L     RF,OPTBUFFX         RF=A(END OF TABLE)                           
         XC    OPTBUFF1(12),OPTBUFF1                                            
*                                                                               
GETPRF02 CLI   OPTCTL,OPTCEOT      TEST E-O-T                                   
         BNE   *+12                                                             
         ST    R1,OPTBUFFL         SET A(LAST ENTRY)                            
         B     GETPRF10                                                         
*                                                                               
         CLC   OPTKEY,CTUKSYS      TEST KEY MATCH                               
         BNE   GETPRF04                                                         
         TM    OPTCTL,OPTCSHT      TEST SHORT ENTRY                             
         BZ    *+10                                                             
         SR    R1,R1               YES - CLEAR R1 FOR EXIT                      
         B     GETPRFX                                                          
*                                                                               
         TM    OPTCTL,OPTCLNG      TEST LONG ENTRY                              
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R1,OPTDTA           YES - POINT TO DATA                          
         B     GETPRFX                                                          
*                                                                               
GETPRF04 TM    OPTCTL,OPTCSHT      TEST SHORT ENTRY                             
         BZ    GETPRF06                                                         
         LH    RE,=Y(OPTSHTL)      RE=LENGTH OF SHORT ENTRY                     
         TM    OPTCTL,OPTCOFC      TEST OFFICE/CLIENT ENTRY                     
         BZ    *+8                                                              
         ST    R1,OPTBUFF1         YES - SAVE ADDRESS                           
         B     GETPRF08                                                         
*                                                                               
GETPRF06 TM    OPTCTL,OPTCLNG      TEST LONG ENTRY                              
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LH    RE,=Y(OPTLNGL)      RE=LENGTH OF LONG ENTRY                      
         TM    OPTCTL,OPTCOFC      TEST OFFICE/CLIENT ENTRY                     
         BZ    *+8                                                              
         ST    R1,OPTBUFF2         YES - SAVE ADDRESS                           
*                                                                               
GETPRF08 BXLE  R1,RE,GETPRF02      PROCESS NEXT ENTRY                           
*                                                                               
GETPRF10 MVC   KEY,CTUKEY                                                       
         GOTO1 DATAMGR,DMCB,(0,DMREAD),CTFILE,CTUKEY,CTUKEY                     
         MVC   CTUKEY,KEY                                                       
         CLI   8(R1),0             TEST FOUND                                   
         BE    *+10                                                             
         SR    R1,R1               NO - CLEAR R1 AND POST TO BUFFER             
         B     GETPRF22                                                         
*                                                                               
         SR    R0,R0                                                            
         LA    R1,CTUDATA          LOCATE VALUES/DEFINITION ELEMENTS            
         USING CTPVD,R1                                                         
GETPRF12 CLI   CTPVEL,0            TEST END OF RECORD                           
         BE    GETPRF20                                                         
         CLI   CTPVEL,CTPVELQ      TEST PROFILE VALUE ELEMENT                   
         BE    GETPRF16                                                         
         CLI   CTPVEL,CTFDELQ      TEST FIELD DEFINITION ELEMENT                
         BE    GETPRF18                                                         
         CLI   CTPVEL,CTACTELQ     TEST ACTIVITY ELEMENT                        
         BE    GETPRF19                                                         
*                                                                               
GETPRF14 IC    R0,CTPVLEN          BUMP TO NEXT ELEMENT                         
         AR    R1,R0                                                            
         B     GETPRF12                                                         
*                                                                               
GETPRF16 LA    R1,CTPVALUE-CTPVD(R1)                                            
         B     GETPRF22                                                         
*                                                                               
         USING CTFDD,R1                                                         
GETPRF18 MVI   FLAG,1              SET FIELD DEFINITION FOUND                   
         SR    RF,RF                                                            
         IC    RF,CTFDNUM                                                       
         LA    RF,DEFAULT-1(RF)                                                 
         MVC   0(1,RF),CTFDDEF                                                  
         B     GETPRF14                                                         
*                                                                               
GETPRF19 LTR   R8,R8               CALLER WANTS PROFINFD?                       
         BZ    GETPRF14            NO                                           
         MVC   PROFIAGY,CTUKAGY                                                 
         MVC   PROFIMED,CTUKMED                                                 
         MVC   PROFICLT,CTUKCLT                                                 
         MVC   PROFIDAT,CTACTDT-CTACTD(R1)    SAVE LAST ACTIVITY DATE           
         B     GETPRF14                                                         
*                                                                               
GETPRF20 SR    R1,R1               CLEAR FLAG FOR SHORT ENTRY                   
         CLI   FLAG,0                                                           
         BE    GETPRF22                                                         
         OC    DEFAULT,DEFAULT     TEST THERE IS A DEFAULT                      
         BZ    GETPRF22                                                         
         LA    R1,DEFAULT          YES - POINT TO IT                            
*                                                                               
GETPRF22 ICM   RF,15,OPTBUFF       TEST OPTIMISATION BUFFER PASSED              
         BZ    GETPRFX                                                          
         L     R0,OPTBUFFX                                                      
         S     R0,OPTBUFFL                                                      
         LH    RE,=Y(OPTSHTL)                                                   
         LTR   R1,R1                                                            
         BZ    *+8                                                              
         LH    RE,=Y(OPTLNGL)                                                   
         CR    RE,R0               TEST ENOUGH ROOM FOR NEW ENTRY               
         BH    GETPRF24            NO - OVERWRITE ANOTHER ENTRY?                
*                                                                               
         L     RF,OPTBUFFL         RF=A(END OF ACTIVE TABLE)                    
         USING OPTD,RF             CREATE NEW TABLE ENTRY                       
         MVI   OPTCTL,OPTCSHT      SHORT                                        
         LTR   R1,R1                                                            
         BZ    *+8                                                              
         MVI   OPTCTL,OPTCLNG      OR LONG                                      
         OC    CTUKCLT,CTUKCLT                                                  
         BZ    *+8                                                              
         OI    OPTCTL,OPTCOFC      SET OFFICE/CLIENT FLAG                       
         MVC   OPTKEY,CTUKSYS      SET KEY                                      
         LTR   R1,R1                                                            
         BZ    *+10                                                             
         MVC   OPTDTA,0(R1)        AND DATA IF RECORD FOUND                     
         AR    RF,RE                                                            
         MVI   OPTCTL,OPTCEOT                                                   
         B     GETPRFX                                                          
*                                                                               
GETPRF24 OC    CTUKCLT,CTUKCLT     TEST CLIENT LEVEL PROFILE                    
         BNZ   GETPRFX                                                          
         LTR   R1,R1               TEST SHORT ENTRY REQUIRED                    
         BNZ   GETPRF26                                                         
         ICM   RF,15,OPTBUFF1      TEST ANY CLIENT LEVEL SHORTS AVAIL           
         BZ    GETPRFX                                                          
         NI    OPTCTL,X'FF'-OPTCOFC                                             
         MVC   OPTKEY,CTUKSYS                                                   
         B     GETPRFX                                                          
*                                                                               
GETPRF26 ICM   RF,15,OPTBUFF2      TEST ANY CLIENT LEVEL LONGS AVAIL            
         BZ    GETPRFX                                                          
         NI    OPTCTL,X'FF'-OPTCOFC                                             
         MVC   OPTKEY,CTUKSYS                                                   
         MVC   OPTDTA,0(R1)                                                     
         DROP  RF                                                               
*                                                                               
GETPRFX  LTR   R1,R1                                                            
         BZ    *+10                                                             
         MVC   0(L'OPTDTA,R3),0(R1)                                             
         B     GETPX                                                            
         EJECT                                                                  
***********************************************************************         
* GET USER ID NUMBER FROM SYSTEM CONTROL BLOCKS                       *         
***********************************************************************         
         SPACE 1                                                                
GETUID   XC    USERIDN,USERIDN                                                  
         TM    INDS,INDSAGY                                                     
         BZ    GUID01                                                           
         OC    USERIDN,USERIDN     SET CC ZERO IF DONT WANT USER ID             
         BR    RE                                                               
*                                                                               
GUID01   ICM   RF,15,GPSSB                                                      
         BZ    GUID02                                                           
         ICM   R1,3,0(RF)          TEST OFFLINE                                 
         BZ    GUID02                                                           
         L     RF,SSBTKADR-SSBD(RF)                                             
         L     RF,TCBUTL-TCBD(RF)                                               
         ICM   R1,3,TUSER-UTLD(RF)                                              
         STCM  R1,3,USERIDN                                                     
         BR    RE                                                               
*                                                                               
GUID02   ICM   RF,15,GPMASTC       EXTRACT USER-ID FROM MASTC                   
         BZR   RE                                                               
         ICM   R1,3,MCORIGID-MASTD(RF)                                          
         STCM  R1,3,USERIDN                                                     
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
SPACES   DC    CL8' '                                                           
GPSSB    DC    V(SSB)                                                           
GPMASTC  DC    V(MASTC)                                                         
DMREAD   DC    C'DMREAD '                                                       
CTFILE   DC    C'CTFILE '                                                       
         SPACE 1                                                                
WORKD    DSECT                     ** WORKING STORAGE **                        
DMCB     DS    6F                                                               
DATAMGR  DS    V                                                                
OPTBUFF  DS    V                   A(START OF OPTIMISATION BUFFER)              
OPTBUFFX DS    V                   A(END OF OPTIMISATION BUFFER)                
OPTBUFF1 DS    V                   A(SHORT CLIENT BUFFER ENTRY)                 
OPTBUFF2 DS    V                   A(LONG CLIENT BUFFER ENTRY)                  
OPTBUFFL DS    V                   A(LAST ENTRY IN OPTIMISATION BUFFER)         
FLAG     DS    XL1                                                              
DEFAULT  DS    XL(L'CTPVALUE)      DEFAULT PROFILE VALUES                       
MEDSW    DS    XL1                                                              
INDS     DS    XL1                                                              
INDSIND  EQU   X'80'               INDICATOR BYTE PRESENT                       
INDSDFT  EQU   X'40'               DEFAULT RECORD VALUES WANTED                 
INDSINF  EQU   X'20'               RETURN PROFINFD TO CALLER                    
INDSAGY  EQU   X'10'               USE AGENCY PROFILE ALWAYS                    
START    DS    CL1                 C'M'=AGENCY PROFILE PASSED                   
USERIDN  DS    XL2                 USER ID NUMBER FROM SYSTEM TABLES            
KEY      DS    XL25                                                             
IO       DS    1000C                                                            
WORKX    EQU   *                                                                
         EJECT                                                                  
OPTD     DSECT                     ** OPTIMISATION TABLE **                     
OPTCTL   DS    XL1                 CONTROL BYTE                                 
OPTCEOT  EQU   X'00'               END-OF-TABLE INDICATOR                       
OPTCSHT  EQU   X'01'               SHORT ENTRY (NO DATA)                        
OPTCLNG  EQU   X'02'               LONG ENTRY (WITH DATA)                       
OPTCOFC  EQU   X'04'               OFFICE/CLIENT ENTRY                          
OPTKEY   DS    XL12                PROFILE KEY                                  
OPTSHTL  EQU   *-OPTD                                                           
OPTDTA   DS    XL16                PROFILE DATA                                 
OPTLNGL  EQU   *-OPTD                                                           
         SPACE 1                                                                
KEYD     DSECT                     ** DSECT FOR CALLING KEY **                  
KEYSYS   DS    CL1                 SYSTEMS CODE                                 
KEYSACC  EQU   C'A'                ACCOUNT SYSTEM                               
KEYPRG   DS    CL3                 PROGRAM CODE                                 
*                                                                               
KEYOTHER DS    0X                                                               
*                                  ACCOUNT SYSTEM KEY DEFINITION                
         DS    XL1                 N/D                                          
KEYUNT   DS    CL1                 UNIT CODE                                    
KEYLDG   DS    CL1                 LEDGER CODE                                  
KEYACT   DS    XL3                 ACCOUNT CODE                                 
KEYOIND1 DS    CL1                 OFFICE INDICATOR                             
KEYOIONE EQU   C'*'                ONE CHARACTER OFFICE CODE FOLLOWS            
KEYOITWO EQU   C'+'                TWO CHARACTER OFFICE AT KEYOFF2              
KEYOFF1  DS    CL1                 ONE CHARACTER OFFICE CODE                    
KEYCPY   DS    XL2                 AGENCY ALPHA/USER-ID                         
KEYOFF2  DS    CL2                 TWO CHARACTER OFFICE CODE                    
         ORG   KEYOTHER                                                         
*                                  OTHER SYSTEMS KEY DEFINITION                 
KEYAGY   DS    XL2                 AGENCY ALPHA/USER-ID                         
KEYMED   DS    CL1                 MEDIA CODE                                   
KEYCLI   DS    XL3                 CLIENT CODE                                  
KEYOIND2 DS    CL1                 OFFICE INDICATOR                             
KEYOFF   DS    CL1                 OFFICE CODE                                  
         EJECT                                                                  
       ++INCLUDE DDGETPROFD                                                     
         EJECT                                                                  
       ++INCLUDE FADSECTS                                                       
         SPACE 1                                                                
* DDMASTD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
         SPACE 1                                                                
* CTGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006DDGETPROF 01/21/05'                                      
         END                                                                    
