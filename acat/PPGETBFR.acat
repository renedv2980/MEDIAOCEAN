*          DATA SET PPGETBFR   AT LEVEL 015 AS OF 03/19/15                      
*$PANAPT01S$ *******  FIRST LINE TO DELETE  *****************                   
*                                                           *                   
*  ATTN: THE LEVEL STAMPS ARE *LEGITIMATELY* OUT-OF-SYNC!   *                   
*        DELETE THIS COMMENT BLOCK ON THE NEXT PROMOTION!   *                   
*                                                           *                   
*  THIS SOURCE MODULE IS AT A HIGHER LEVEL NUMBER THAN ITS  *                   
*  CORRESPONDING LOAD OR OBJECT MODULE, BECAUSE THE MEMBER  *                   
*  WAS PROMOTED USING THE SRCE LIBCODE VIA MR# 045233.      *                   
*                                                           *                   
*  THIS COMMENT BLOCK WAS INSERTED *PROGRAMMATICALLY* BY    *                   
*  PANAPT, TO EXPLAIN WHY THE LEVEL STAMPS ARE OUT-OF-SYNC. *                   
*  BEFORE THIS MEMBER CAN BE PROMOTED AGAIN, THIS ENTIRE    *                   
*  COMMENT BLOCK *MUST* BE MANUALLY DELETED.                *                   
*                                                           *                   
*$PANAPT01X$ *******  LAST LINE TO DELETE  ******************                   
*CATALP PPGETBFR                                                                
         TITLE 'PPGETBFR - PRINT GET BILL FORMULA RECORDS'                      
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   NOTE- SEE PPGETBFRD FOR PARAMETER LIST AND DESCRIPTION OF                   
*         PROGRAM FUNCTION                                                      
*                                                                               
***********************************************************************         
         SPACE 2                                                                
PPGETBFR CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,**GBFR,CLEAR=YES                                          
*                                                                               
         USING WORKD,RC                                                         
         L     RA,0(R1)            A(CONTROL BLOCK)                             
         USING PPGBFRDD,RA                                                      
*                                  SET ADDRESSES                                
         L     RF,PPGBACOM                                                      
         MVC   VDMGR,CDATAMGR-COMFACSD(RF)                                      
*                                                                               
         MVI   DMINBTS,X'80'                                                    
         MVI   DMOUTBTS,X'FD'                                                   
         MVC   FFS,=X'FFFFFFFF'                                                 
*                                                                               
         MVC   KEST(2),PPGBEST   ESTIMATE                                       
         MVC   XMOS,PPGBMOS                                                     
         XC    XMOS,=X'FFFF'        COMPLEMENT MOS                              
*                                                                               
         XC    PPGBRTRN(PPGBRTNL),PPGBRTRN   CLEAR RETURN                       
*                                                                               
         BAS   RE,INITSAR          INITIALIZE TSAROFF AND BUILD BUFFR           
*                                  (IF NECESSARY)                               
         BAS   RE,GETTSAR          SEARCH TSAR BUFFER                           
*                                                                               
GF90     DS    0H                                                               
EXIT     XIT1                                                                   
         SPACE 2                                                                
***********************************************************************         
*                                                                               
*        DATAMGR CALLS                                                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
HIGH     DS    0H                                                               
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'                                             
         B     DIR                                                              
         SPACE 2                                                                
SEQ      DS    0H                                                               
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIR                                                              
*                                                                               
DIR      DS    0H                                                               
         LR    R0,RE                                                            
         MVC   FILE,=CL8'PRTDIR'                                                
         GOTO1 VDMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY,KEY,(0,DMWORK)             
         B     DMCHK                                                            
         SPACE 2                                                                
GET      DS    0H                                                               
         LR    R0,RE                                                            
         MVC   COMMAND,=CL8'GETREC'                                             
         MVC   FILE,=CL8'PRTFILE'                                               
         GOTO1 VDMGR,DMCB,(DMINBTS,COMMAND),FILE,KEY+27,IOA,(0,DMWORK)          
         B     DMCHK                                                            
*                                                                               
DMCHK    DS    0H                                                               
         LR    RE,R0                                                            
         MVC   BYTE,DMCB+8                                                      
         NC    BYTE,DMOUTBTS                                                    
         BZR   RE                                                               
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        INITSAR - INITIALIZE TSAROFF AND BUILD BUFFER                          
*                                                                               
***********************************************************************         
         SPACE 2                                                                
INITSAR  NTR1                                                                   
         LA    R9,TSARB                                                         
         USING TSARD,R9                                                         
         OC    ATSAROFF,ATSAROFF   HAVE WE GOT TSAROFF YET?                     
         BNZ   IT03                                                             
         MVC   WORK(8),=CL8'T00A7D' LOAD TSAROFF                                
         GOTO1 PPGBLODR,DMCB,WORK,0,0                                           
         OC    DMCB+4(4),DMCB+4                                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   ATSAROFF,4(R1)                                                   
*                                                                               
         L     R0,=A(BUFFSZE)                                                   
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R1,TSABUF                                                        
*                                                                               
         MVI   TSOFFACT,TSAINI                                                  
         MVC   TSAREC,=A(BUFFSZE)                                               
         MVC   TSACOM,PPGBACOM                                                  
         MVI   TSKEYL,BUFKEYL                                                   
         MVC   TSRECL,=Y(BUFRECL)                                               
         GOTO1 ATSAROFF,(R9)                                                    
*                                                                               
IT03     DS    0H                                                               
         CLC   PPGBAGY(6),LASTAM       CHANGE OF AGY/MED/CLIENT?                
         BE    ITX                    NO, DONE                                  
         MVC   LASTAM(6),PPGBAGY                                                
         MVI   TSOFFACT,TSAINI     YES, RE-INIT                                 
         MVC   TSAREC,=A(BUFFSZE)                                               
         GOTO1 ATSAROFF,(R9)                                                    
*                                                                               
         LA    R0,BUFFREC          SET RECORD ADDRESS                           
         ST    R0,TSAREC                                                        
*                                                                               
         XC    KEY,KEY           READ ALL RECORDS AND ADD TO TSAR BUFF          
         LA    R2,KEY                                                           
         USING PBFKEY,R2                                                        
         MVI   PBFKTYPE,PBFKTYPQ                                                
         MVC   PBFKAGY,PPGBAGY                                                  
         MVC   PBFKMED,PPGBMED                                                  
         MVC   PBFKCLT,PPGBCLT                                                  
*                                                                               
         BAS   RE,HIGH                                                          
         B     IT04B                                                            
*                                                                               
IT04     DS    0H                                                               
         BAS   RE,SEQ                                                           
IT04B    DS    0H                                                               
         CLC   KEY(PBFKPRD-PBFKEY),KEYSAVE TEST THRU CLIENT                     
         BNE   IT40                                                             
*                                                                               
         BAS   RE,GET                                                           
         LA    R4,IOA                                                           
         LA    R4,PBFRDATA-PBFREC(R4)                                           
*                                                                               
IT10     DS    0H                                                               
         CLI   0(R4),0             EOR                                          
         BE    IT30                                                             
         CLI   0(R4),PBFRCELQ      FORMULA ELEMENT                              
         BE    IT20                                                             
*                                                                               
IT12     DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     IT10                                                             
*                                                                               
IT20     DS    0H                                                               
         USING PBFRCELD,R4                                                      
         LA    R5,BUFFREC                                                       
         USING BUFRECD,R5                                                       
         XC    BUFFREC,BUFFREC                                                  
         MVC   BUFPRD,PBFKPRD                                                   
         MVC   BUFEST(2),PBFKEST                                                
         OC    BUFEST(2),BUFEST    0 = ALL = FFS                                
         BNZ   *+10                                                             
         MVC   BUFEST,FFS                                                       
         MVC   BUFMM,FFS                                                        
         MVC   BUFMOS,PBFRCDTE                                                  
         MVC   BUFFORM,PBFRCFML                                                 
         MVC   BUFACP,PBFRCACP                                                  
         MVC   BUFACO,PBFRCACO                                                  
*                                                                               
         MVI   TSOFFACT,TSAADD                                                  
         GOTO1 ATSAROFF,(R9)                                                    
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     IT12                NEXT ELEMENT                                 
*                                                                               
IT30     DS    0H                                                               
         B     IT04                NEXT RECORD                                  
*                                                                               
IT40     DS    0H                                                               
         XC    BUFRECD(BUFRECL),BUFRECD   ADD A DUMMY EOF REC                   
         MVI   BUFPRD,X'FF'                                                     
         MVI   TSOFFACT,TSAADD                                                  
         GOTO1 ATSAROFF,(R9)                                                    
         DROP  R5                                                               
*                                                                               
ITX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*                                                                               
*        GETTSAR - SEARCH TSAR BUFFER                                           
*                                                                               
***********************************************************************         
         SPACE 2                                                                
GETTSAR  NTR1                                                                   
         LA    R9,TSARB                                                         
         USING TSARD,R9                                                         
         LA    R2,BUFFREC                                                       
         ST    R2,TSAREC                                                        
         USING BUFRECD,R2                                                       
*                                                                               
         XC    BUFFREC,BUFFREC                                                  
         MVC   BUFPRD,PPGBPRD                                                   
         MVC   BUFEST,KEST                                                      
         MVC   BUFMM,FFS           ALWAYS FFS FOR PRINT                         
*                                                                               
GT30     DS    0H                                                               
         XC    BUFMOS,BUFMOS       CLEAR MOS AND FORM                           
         XC    BUFFORM,BUFFORM     (NOT PART OF KEY)                            
         XC    BUFACP,BUFACP                                                    
         XC    BUFACO,BUFACO                                                    
*                                                                               
         MVC   SAVBUFR,BUFREC                                                   
         MVI   TSOFFACT,TSARDH     READ HIGH                                    
         GOTO1 ATSAROFF,(R9)                                                    
         CLI   TSERRS,0                                                         
         BE    GT33                                                             
         TM    TSERRS,TSEEOF      TEST FOR END OF FILE                          
         BO    GT33               GO SET DEFAULT TO G-CD                        
         TM    TSERRS,TSERNF      TEST FOR REC NOT FOUND - EOF?                 
         BO    GT33               GO SET DEFAULT TO G-CD                        
         DC    H'0'              CAN'T HANDLE OTHER ERRORS                      
*                                                                               
GT33     DS    0H                                                               
         CLC   BUFKEY(BUFMOS-BUFKEY),SAVBUFR  = THRU MGR/MKT?                   
         BE    GT80                                                             
         CLC   BUFKEY(BUFMM-BUFKEY),SAVBUFR   = THRU EST?                       
         BE    GT50                                                             
         CLC   BUFKEY(BUFEST-BUFKEY),SAVBUFR  = THRU PRD?                       
         BE    GT40                                                             
*                                                                               
GT34     DS    0H                                                               
         CLC   SAVBUFR+BUFPRD-BUFKEY(3),=C'AAA'  NO MATCH, IF ALERADY           
         BE    GT90                              DOING AAA, QUIT                
*                                                                               
GT38     DS    0H                                                               
         MVC   BUFPRD,=C'AAA'      SET PRD TO AAA                               
         MVC   BUFEST,KEST         AND EST TO REQEST                            
         B     GT30                                                             
*                                                                               
GT40     DS    0H                  = THRU PRD                                   
         CLC   SAVBUFR+BUFEST-BUFKEY(2),FFS  IF ALREADY LOOKING FOR             
         BE    GT34                          EST=ALL, TRY PRD=ALL               
GT44     DS    0H                                                               
         MVC   BUFEST,FFS          SET FOR EST=ALL                              
         B     GT30                                                             
*                                                                               
GT50     DS    0H                  = THRU EST                                   
         CLC   SAVBUFR+BUFMM-BUFKEY(3),FFS    IF ALREADY LOOKING                
         BE    GT44                 FOR MGR/MKT = ALL, TRY EST=ALL              
         MVC   BUFMM,FFS            SET FOR MGR/MKT = ALL                       
         B     GT30                                                             
*                                  HAVE RIGHT PRD,EST,MGR/MKT                   
GT80     DS    0H                                                               
         CLC   BUFMOS,XMOS         MONTH                                        
         BNL   GT84                                                             
*                                                                               
         MVI   TSOFFACT,TSANXT     READ NEXT                                    
         GOTO1 ATSAROFF,(R9)                                                    
         CLI   TSERRS,0                                                         
         BE    GT83                                                             
         TM    TSERRS,TSEEOF      TEST FOR END OF FILE                          
         BO    GT83               GO SET DEFAULT TO G-CD                        
         TM    TSERRS,TSERNF      TEST FOR RECORD NOT FOUND - EOF?              
         BO    GT83               GO SET DEFAULT TO G-CD                        
         DC    H'0'              CAN'T HANDLE OTHER ERRORS                      
*                                                                               
GT83     DS    0H                                                               
         CLC   BUFKEY(BUFMOS-BUFKEY),SAVBUFR  STILL GOOD THRU MGR/MKT?          
         BNE   GT90                           NO, DONE                          
         B     GT80                                                             
*                                                                               
GT84     DS    0H                                                               
         MVC   PPGBEMOS,BUFMOS     EFFECTIVE MOS                                
         XC    PPGBEMOS,=X'FFFF'   UNCOMPLEMENT                                 
         MVC   PPGBFORM,BUFFORM    FORMULA                                      
         MVC   PPGBACP,BUFACP                                                   
         MVC   PPGBACO,BUFACO                                                   
         MVC   PPGBRKEY,BUFREC     RETURN FOUND KEY                             
         B     GT90                                                             
*                                                                               
GT90     DS    0H                                                               
GTX      DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    XL64                                                             
SAVR1    DS    F                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
STAT     DS    X                                                                
BYTE     DS    X                                                                
KEST     DS    XL2                                                              
XMOS     DS    XL2                                                              
         DS    0D                                                               
KEY      DS    XL32                                                             
KEYSAVE  DS    XL32                                                             
FFS      DS    XL8                                                              
IOA      DS    XL2000                                                           
VDMGR    DS    A                                                                
DMINBTS  DS    X                                                                
DMOUTBTS DS    X                                                                
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
DMWORK   DS    12D                                                              
BUFFREC  DS    XL(BUFRECL)                                                      
SAVBUFR  DS    XL(BUFRECL)                                                      
*                                                                               
WORKDL   EQU   *-WORKD                                                          
BUFFSZE  EQU   80000                                                            
*                                                                               
BUFRECD  DSECT         DSECT FOR BUFFER RECORD                                  
BUFREC   DS    0X                                                               
BUFKEY   DS    0X                                                               
BUFPRD   DS    CL3                 PRD, AAA=ALL                                 
BUFEST   DS    XL2                 EST, FFFF=ALL                                
BUFMM    DS    XL3                 FOR SPOT WAS MKTG AND MKT                    
*                                  FFFFFF=ALL                                   
BUFMOS   DS    XL2                 COMPLEMENTED                                 
BUFKEYL  EQU   *-BUFRECD                                                        
BUFFORM  DS    XL5                                                              
BUFACP   DS    XL3                                                              
BUFACO   DS    XL1                                                              
BUFRECL  EQU   *-BUFRECD                                                        
*                                                                               
         EJECT                                                                  
*                                                                               
***********************************************************************         
*                                                                               
       ++INCLUDE DDTSARD                                                        
*                                                                               
***********************************************************************         
*                                                                               
PPGBFRDD DSECT                                                                  
       ++INCLUDE PPGETBFRD                                                      
         ORG PPGBSAVS                                                           
LASTAM   DS    XL3                 LAST AGY/MED                                 
LASTCLT  DS    XL3                 LAST CLIENT                                  
         DS    0D                                                               
TSARB    DS    XL(TSPNEWL)         TSAROFF CONTROL BLOCK                        
ATSAROFF DS    A                   A(TSAROFF)                                   
         ORG   PPGBFRD+256                                                      
*                                                                               
***********************************************************************         
*                                                                               
       ++INCLUDE PPBILLFML                                                      
*                                                                               
***********************************************************************         
* ++INCLUDE DDCOMFACSD                                                          
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
*                                                                               
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015PPGETBFR  03/19/15'                                      
         END                                                                    
