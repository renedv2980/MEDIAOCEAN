*          DATA SET POLRPT     AT LEVEL 036 AS OF 05/01/02                      
*CATALP POLRPT                                                                  
         TITLE 'PRINTPAK - ON-LINE PRINT CONTROLLER'                            
REPORT   CSECT                                                                  
         NMOD1 8,REPORT                                                         
         USING PPREPD,RC                                                        
         USING POLWRKD,R9                                                       
         USING POLFILED,RA                                                      
         L     RA,PPFILEC                                                       
         EJECT                                                                  
*                   OPTION JUST TO CLEAR ALL PRINT LINES                        
         SPACE 3                                                                
         CLI   FORCECLR,C'N'                                                    
         BE    PC4                                                              
         MVI   FORCECLR,C'N'                                                    
         SPACE 2                                                                
         B     PC15                                                             
         SPACE 2                                                                
*                   CONTROL SPEC PROCESSING                                     
         SPACE 2                                                                
PC4      CLI   SKIPSPEC,C'Y'                                                    
         BE    PC6                                                              
         CLI   FORCEHED,C'Y'       DONT EXECUTE SPECS IF                        
         BE    PC5                 JUST HANDLING PLINES                         
         CLI   FORCEMID,C'Y'                                                    
         BE    PC5                                                              
         CLI   FORCEFUT,C'Y'                                                    
         BE    PC5                                                              
         CLC   LINE,MAXLINES                                                    
         BL    PC14                                                             
         SPACE 2                                                                
PC5      DS    0H                                                               
         BAS   RE,PROCSPEC                                                      
         EJECT                                                                  
*                   CONTROL OF HEADLINE PRINTING                                
         SPACE 3                                                                
PC6      CLI   FORCEHED,C'N'                                                    
         BE    PC8                                                              
         MVI   FORCEHED,C'N'                                                    
         B     PC10                                                             
         SPACE 2                                                                
PC8      CLC   LINE,MAXLINES                                                    
         BL    PC12                                                             
         SPACE 2                                                                
PC10     MVI   FORCEMID,C'Y'                                                    
         GOTO1 PRINT,P1,SPACES,=C'BC01',VTIA,VDATAMGR                           
         MVI   LINE,1                                                           
         LA    R2,HEAD1                                                         
         LA    R3,14                                                            
         BAS   RE,PRINCON                                                       
         SR    R4,R4                                                            
         MVC   REPDUB(4),PAGE      ADD 1 TO PAGE AND SUBPAGE                    
         LH    R4,REPDUB                                                        
         LA    R4,1(R4)                                                         
         STH   R4,REPDUB                                                        
         LH    R4,REPDUB+2                                                      
         LA    R4,1(R4)                                                         
         STH   R4,REPDUB+2                                                      
         MVC   PAGE(4),REPDUB                                                   
         EJECT                                                                  
*                   CONTROL OF MIDLINE PRINTING                                 
         SPACE 3                                                                
PC12     CLI   FORCEMID,C'N'                                                    
         BE    PC14                                                             
         MVI   FORCEMID,C'N'                                                    
         LA    R2,MID1                                                          
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         CLC   LINE,MAXLINES                                                    
         BNL   PC10                                                             
         SPACE 2                                                                
*                   CONTROL OF PRINT LINES                                      
         SPACE 3                                                                
PC14     LA    R2,P                                                             
         LA    R3,2                                                             
         BAS   RE,PRINP                                                         
         MVI   SPACING,1                                                        
         SPACE 2                                                                
*                   CONTROL OF FOOTLINE PRINTING                                
         SPACE 3                                                                
         CLI   FORCEFUT,C'N'                                                    
         BE    PC15                                                             
         MVI   FORCEFUT,C'N'                                                    
         GOTO1 PRINT,P,SPACES,=C'BC09',VTIA,VDATAMGR                            
         LA    R2,FOOT1                                                         
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         MVI   FORCEHED,C'Y'                                                    
         SPACE 2                                                                
PC15     DS    0H                                                               
PC16     DS    0H                                                               
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
*                                                                               
PC17     DS    0H                                                               
         MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
*                                                                               
PC18     DS    0H                                                               
         MVC   FOOT1,SPACES                                                     
         MVC   FOOT2,SPACES                                                     
*                                                                               
         B     PCEXT                                                            
         EJECT                                                                  
*                   CONTROL PRINTING OF A BLOCK                                 
         SPACE 3                                                                
PRINCON  NTR                                                                    
         MVC   REPWORK,SPACES                                                   
         LR    R4,R2                                                            
         LR    R5,R3                                                            
         LA    R6,REPWORK                                                       
         SPACE 2                                                                
PC20     CLC   0(132,R4),SPACES    BUILD A TABLE OF PRINT LINE BYTES            
         BE    *+8                                                              
         MVI   0(R6),C'P'                                                       
         LA    R4,132(R4)                                                       
         LA    R6,1(R6)                                                         
         BCT   R5,PC20                                                          
         SPACE 2                                                                
*                                  NOW CONVERT TO PRINTING NUMBERS              
         LA    R6,REPWORK                                                       
         SPACE 2                                                                
PC22     CLC   0(16,R6),SPACES     ANY LEFT                                     
         BE    PC30                NO                                           
         SPACE 2                                                                
PC26     LA    R8,2                                                             
         CLC   1(15,R6),SPACES     IS THIS THE LAST                             
         BE    PC28                                                             
         CLC   1(2,R6),=C' P'                                                   
         BE    PC28                                                             
         LA    R8,1                                                             
         CLI   1(R6),C'P'                                                       
         BE    PC28                                                             
         LA    R8,3                                                             
         SPACE 2                                                                
PC28     STC   R8,0(R6)                                                         
         AR    R6,R8                                                            
         SR    RF,RF                                                            
         IC    RF,LINE                                                          
         AR    RF,R8                                                            
         STC   RF,LINE                                                          
         B     PC22                                                             
         SPACE 2                                                                
PC30     LA    R6,REPWORK          NOW PRINT R2 = DATA                          
*                                            R3 = N'LINES                       
*                                            R6 = SKIP NUMBERS                  
PC32     CLI   0(R6),C' '                                                       
         BE    PC34                                                             
         MVC   CONTROL(3),=C'BL0'                                               
         MVC   CONTROL+3(1),0(R6)                                               
         OI    CONTROL+3,X'F0'                                                  
         OC    0(132,R2),SPACES    ENSURE PRINTABLE CHARACTERS                  
         GOTO1 PRINT,P1,(R2),CONTROL,VTIA,VDATAMGR                              
         SPACE 2                                                                
PC34     LA    R2,132(R2)                                                       
         LA    R6,1(R6)                                                         
         BCT   R3,PC32                                                          
         XIT                                                                    
         EJECT                                                                  
*                   SPECIAL ROUTINE FOR PRINT LINES                             
         SPACE 2                                                                
PRINP    NTR                                                                    
*                                  ALWAYS SINGLE-PRINT 1/2                      
         CLC   PSECOND,SPACES                                                   
         BE    PC36                                                             
         GOTO1 PRINT,P1,(R2),=C'BL01',VTIA,VDATAMGR                             
         BAS   RE,PLCNT                                                         
         LA    R2,132(R2)                                                       
         SPACE 2                                                                
PC36     MVC   CONTROL(3),=C'BL0'  USERS SPACING FOR ONE OR 2/2                 
         MVC   CONTROL+3(1),SPACING                                             
         CLC   RCSPACNG,SPACING    OPTION DOUBLE/TRIPLE RSPEC SPACING           
         BNH   *+10                                                             
         MVC   CONTROL+3(1),RCSPACNG                                            
         OI    CONTROL+3,X'F0'                                                  
         GOTO1 PRINT,P1,(R2),CONTROL,VTIA,VDATAMGR                              
         SR    RF,RF                                                            
         IC    RE,CONTROL+3                                                     
         SLL   RE,28                                                            
         SRL   RE,28                                                            
         IC    RF,LINE                                                          
         AR    RE,RF                                                            
         STC   RE,LINE                                                          
         XIT                                                                    
         SPACE 2                                                                
PCEXT    MVI   SKIPSPEC,C'N'                                                    
         XMOD1 1                                                                
PLCNT    SR    RF,RF                                                            
         IC    RF,LINE                                                          
         AH    RF,=H'1'                                                         
         STC   RF,LINE                                                          
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                   EXECUTE THE SPEC POOL                                       
         SPACE 3                                                                
PROCSPEC NTR                                                                    
         SPACE 2                                                                
         LA    RE,RELO                                                          
         S     RE,RELO                                                          
         ST    RE,REPRELO                                                       
         B     RELO+4                                                           
*                                                                               
RELO     DC    A(*)                                                             
*                                                                               
         L     R2,PPSPECS          POINT TO SPEC POOL                           
         LTR   R2,R2                                                            
         BZ    EX6                                                              
         SR    R3,R3                                                            
         MVI   PROGSW,C'Y'         PRESET SWITCHES                              
         CLI   RCSUBPRG,0                                                       
         BE    EX1                                                              
         MVI   PROGSW,C'N'                                                      
         SPACE 2                                                                
EX1      DS    0H                                                               
         SPACE 2                                                                
         SPACE 2                                                                
EX2      IC    R3,1(R2)                                                         
         AR    R2,R3               GET NEXT SPEC                                
         SPACE 2                                                                
EX4      SR    R4,R4                                                            
         IC    R4,0(R2)                                                         
         SLL   R4,2                                                             
         L     R4,INBRTAB(R4)      GO TO ROUTINE DEPENDENT                      
         A     R4,REPRELO                                                       
         BR    R4                  ON THE VALUE OF ELEMENT                      
         SPACE 2                                                                
EX6      XIT                                                                    
         SPACE 2                                                                
EX8      MVI   PROGSW,C'N'                                                      
         B     EX2                                                              
         SPACE 2                                                                
EX10     CLC   RCSUBPRG(1),2(R2)   CHECK MATCH OF SUB-PROGRAM                   
         BNE   EX2                                                              
         MVI   PROGSW,C'Y'                                                      
         B     EX2                                                              
         EJECT                                                                  
*                   PRINT SPEC CONTROL AND LITERALS                             
         SPACE 2                                                                
EX46     DS    0H                                                               
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         MVC   REPDUB(2),2(R2)     WORK OUT DISLACEMENT                         
         LH    R5,REPDUB                                                        
         LA    R5,HEAD1(R5)                                                     
         CLI   0(R2),13                                                         
         BE    EX48                                                             
         SR    R4,R4               WORK OUT BRANCH BY INSTRUCTION NUM           
         IC    R4,4(R2)                                                         
         SLL   R4,2                                                             
         L     R4,PRBRTAB(R4)                                                   
         A     R4,REPRELO                                                       
         BR    R4                                                               
         SPACE 2                                                                
EX48     SR    R4,R4               HANDLE LITERALS                              
         IC    R4,1(R2)                                                         
         SH    R4,=H'5'            LENGTH IS L'ELEMENT - 4                      
         EX    R4,MUVLIT                                                        
         B     EX2                                                              
         SPACE 2                                                                
MUVLIT   MVC   0(0,R5),4(R2)                                                    
         EJECT                                                                  
*                   TABLE OF INTERNAL BRANCH VALUES                             
         SPACE 3                                                                
         DS    0F                                                               
INBRTAB  DC    AL4(EX6)     0      END OF SPECS                                 
         DC    AL4(EX8)     1      PROGRAM RESET                                
         DC    AL4(EX10)    2              FILTER                               
         DC    9AL4(0)                                                          
         DC    AL4(EX46)   12      PRINT KEYWORD SPEC                           
         DC    AL4(EX46)   13      PRINT LITERAL SPEC                           
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (0-3)                              
         SPACE 2                                                                
EX50     MVC   0(110,R5),SPACES    SPACES                                       
         CLI   SKIPSPEC,C'T'                                                    
         BE    EX2                                                              
         MVC   0(132,R5),SPACES                                                 
         B     EX2                                                              
         SPACE 2                                                                
EX52     MVC   0(7,R5),=C'RUN ON ' RUN                                          
         MVC   7(8,R5),RCDATE                                                   
         PACK  REPDUB,RCDATE(2)                                                 
         CVB   R6,REPDUB                                                        
         BCTR  R6,R0                                                            
         MH    R6,=H'3'                                                         
         LA    R6,MONTHS(R6)                                                    
         MVC   7(3,R5),0(R6)                                                    
         MVC   15(4,R5),=C' AT '                                                
         GETIME STANDARD                                                        
         ST    R1,REPWORK                                                       
         MVC   REPDUB+5(3),REPWORK                                              
         OI    REPDUB+7,X'0F'                                                   
         CVB   R6,REPDUB                                                        
         EDIT  (R6),(5,19(R5)),2                                                
         B     EX2                                                              
         SPACE 2                                                                
EX54     MVC   0(16,R5),=C'PRINTPAK REPORT '                                    
         MVC   16(2,R5),RCPROG                                                  
         B     EX2                                                              
         SPACE 2                                                                
EX56     MVC   0(5,R5),=C'PAGE '                                                
         EDIT  PAGE,(4,5(R5)),ALIGN=LEFT                                        
         B     EX2                                                              
         SPACE 2                                                                
EX57     MVC   0(8,R5),=C'SUBPAGE '                                             
         EDIT  SUBPAGE,(4,8(R5)),ALIGN=LEFT                                     
         B     EX2                                                              
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (4-8)                              
         SPACE 2                                                                
EX58     CLC   QSTART(6),SPACES    PERIOD                                       
         BE    EX62                                                             
         CLC   QEND(6),SPACES                                                   
         BNE   EX60                                                             
         MVC   0(5,R5),=C'FROM '                                                
         LA    R5,5(R5)                                                         
         SPACE 2                                                                
EX60     GOTO1 DTCNV,REPWORK,(0,QSTART),(3,0(R5))                               
         CLC   QEND(6),SPACES                                                   
         BE    EX2                                                              
         LA    R5,9(R5)                                                         
         SPACE 2                                                                
EX62     CLC   QEND(6),SPACES                                                   
         BE    EX2                                                              
         MVC   0(5,R5),=C'THRU '                                                
         GOTO1 DTCNV,REPWORK,(0,QEND),(3,5(R5))                                 
         B     EX2                                                              
         SPACE 2                                                                
EX64     MVC   0(13,R5),=C'REQUESTOR    '                                       
         MVC   13(12,R5),QUESTOR                                                
         B     EX2                                                              
         SPACE 2                                                                
EX66     MVC   0(13,R5),=CL13'MEDIA'                                            
         MVC   9(1,R5),PAGYKMED                                                 
         MVC   13(10,R5),PAGYMED                                                
         B     EX2                                                              
         SPACE 2                                                                
EX67     MVC   0(10,R5),PAGYMED                                                 
         B     EX2                                                              
         SPACE 2                                                                
EX68     MVC   0(33,R5),PAGYNAME   AGENCY NAME                                  
         B     EX2                                                              
         SPACE 2                                                                
EX70     MVC   0(33,R5),PAGYADDR   AGENCY ADDRESS                               
         B     EX2                                                              
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (9-14)                             
         SPACE 3                                                                
EX72     MVC   0(06,R5),=C'CLIENT'                                              
         MVC   9(3,R5),PCLTKCLT                                                 
         MVC   13(20,R5),PCLTNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX74     MVC   0(20,R5),PCLTNAME                                                
         B     EX2                                                              
         SPACE 2                                                                
EX76     MVC   0(20,R5),PCLTBNAM                                                
         MVC   132(30,R5),PCLTLIN1                                              
         MVC   264(30,R5),PCLTLIN2                                              
         MVC   396(24,R5),PCLTATTN                                              
         B     EX2                                                              
         SPACE 2                                                                
EX78     CLI   PPRDKEY+1,0                                                      
         BE    EX2                                                              
         MVC   0(7,R5),=C'PRODUCT'                                              
         MVC   9(3,R5),PPRDKPRD                                                 
         MVC   13(20,R5),PPRDNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX80     MVC   0(20,R5),PPRDNAME                                                
         B     EX2                                                              
         SPACE 2                                                                
EX82     MVC   000(20,R5),PPRDBILL                                              
         MVC   132(30,R5),PPRDLIN1                                              
         MVC   264(30,R5),PPRDLIN2                                              
         B     EX2                                                              
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (15-17)                            
         SPACE 3                                                                
EX84     MVC   0(9,R5),=C'ESTIMATES'                                            
         CLC   QESTEND,SPACES                                                   
         BNE   EX88                                                             
         CLC   QEST,SPACES                                                      
         BE    EX86                                                             
         OC    PESTKEY,PESTKEY                                                  
         BNE   *+14                                                             
         MVC   0(9,R5),SPACES      NO ESTIMATE THERE                            
         B     EX2                                                              
         EDIT  (2,PESTKEST),(4,9(R5)),ALIGN=LEFT                                
         MVI   8(R5),C' '                                                       
         MVC   13(20,R5),PESTNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX86     MVC   09(16,R5),=C'    ALL TOGETHER'                                   
         B     EX2                                                              
         SPACE 2                                                                
EX88     PACK  DUB,QEST                                                         
         CVB   R6,DUB                                                           
         EDIT  (R6),(3,13(R5)),ALIGN=LEFT                                       
         AR    R5,R0                                                            
         BCTR  R5,R0                                                            
         MVI   14(R5),C'-'                                                      
         PACK  DUB,QESTEND                                                      
         CVB   R6,DUB                                                           
         EDIT  (R6),(3,15(R5)),ALIGN=LEFT                                       
         B     EX2                                                              
         SPACE 2                                                                
EX90     MVC   0(20,R5),PESTNAME                                                
         B     EX2                                                              
         SPACE 2                                                                
EX92     MVC   0(3,R5),=C'ALL'                                                  
         CLC   QEST,SPACES                                                      
         BE    EX2                                                              
         EDIT  (2,PESTKEST),(3,0(R5))                                           
         B     EX2                                                              
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (31-35)                            
         SPACE 3                                                                
EX120    CLI   QREGION,C' '                                                     
         BE    EX2                                                              
         CLI   PREGKEY,0                                                        
         BE    EX2                                                              
         MVC   0(7,R5),=C'REGION '                                              
         MVC   9(3,R5),PREGKREG                                                 
         MVC   13(20,R5),PREGNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX122    CLI   QDIST,C' '                                                       
         BE    EX2                                                              
         CLI   PDSTKEY,0                                                        
         BE    EX2                                                              
         MVC   0(9,R5),=C'DISTRICT '                                            
         MVC   9(3,R5),PDSTKDST                                                 
         MVC   13(20,R5),PDSTNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX124    CLI   QDIV,C' '                                                        
         BE    EX2                                                              
         CLI   PDIVKEY,0                                                        
         BE    EX2                                                              
         MVC   0(9,R5),=C'DIVISION '                                            
         MVC   9(3,R5),PDIVKDIV                                                 
         MVC   13(20,R5),PDIVNAME                                               
         B     EX2                                                              
         EJECT                                                                  
*                   BRANCH TABLE FOR PRINT SPECS                                
         SPACE 3                                                                
PRBRTAB  DC    AL4(EX50)    0      SPACES                                       
         DC    AL4(EX52)    1      RUN                                          
         DC    AL4(EX54)    2      REPORT                                       
         DC    AL4(EX56)    3      PAGE                                         
         DC    AL4(EX58)    4      PERIOD                                       
         DC    AL4(EX64)    5      REQUESTOR                                    
         DC    AL4(EX66)    6      MEDIA                                        
         DC    AL4(EX68)    7      AGYNAME                                      
         DC    AL4(EX70)    8      AGYADD                                       
         DC    AL4(EX72)    9      CLIENT                                       
         DC    AL4(EX74)   10      CLINAME                                      
         DC    AL4(EX76)   11      CLIADD                                       
         DC    AL4(EX78)   12      PRODUCT                                      
         DC    AL4(EX80)   13      PRONAME                                      
         DC    AL4(EX82)   14      PROADD                                       
         DC    AL4(EX84)   15      ESTIMATE                                     
         DC    AL4(EX90)   16      ESTNAME                                      
         DC    AL4(EX92)   17      ESTNUM                                       
         DC    13AL4(0)                                                         
         DC    AL4(EX120)  31      REGION                                       
         DC    AL4(EX122)  32      DISTRICT                                     
         DC    AL4(EX124)  33      DIVISION                                     
         DC    2AL4(0)                                                          
         DC    AL4(EX57)   36      SUBPAGE                                      
         DC    AL4(0)                                                           
         DC    AL4(EX67)   38      MEDIANAME                                    
         SPACE 3                                                                
         LTORG                                                                  
         EJECT                                                                  
*                   DSECT FOR THIS MODULE                                       
         SPACE 3                                                                
PPREPD   DSECT                                                                  
REPDUB   DS    D                                                                
REPWORK  DS    CL40                                                             
CONTROL  DS    CL4                                                              
PROGSW   DS    CL1                                                              
REPRELO  DS    F                                                                
         SPACE 2                                                                
*                                                                               
       ++INCLUDE POLWRK                                                         
         SPACE 2                                                                
*                                                                               
       ++INCLUDE POLFILE                                                        
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'036POLRPT    05/01/02'                                      
         END                                                                    
         EJECT                                                                  
