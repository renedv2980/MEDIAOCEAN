*          DATA SET DDCALLOFF  AT LEVEL 002 AS OF 07/27/11                      
*CATALP CALLOFF                                                                 
         TITLE 'CALLOFF - OFF-LINE CALLOV SIMULATOR'                            
CALLOFF  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 CALLOFLQ,**COFF**                                                
         USING CALLOFFD,RC                                                      
         LR    R9,R1                                                            
         L     RA,=V(MASTC)                                                     
         USING MASTD,RA                                                         
                                                                                
* BUILD EBCDIC PHASE NAME                                                       
*                                                                               
GPN      MVC   MCDUB+6(2),=C'  '   PHASE NAME BUILT IN MCDUB                    
         CLI   4(R9),0             TEST WHAT TYPE OF CALL                       
         BE    GPN2                                                             
*                                                                               
GPN1     LHI   R0,6                MAINTENANCE CALL - P2=X'D90SPPOO'            
         LA    R1,MCDUB                                                         
         ICM   RF,B'1110',5(R9)    RF=X'0SPPOO..'                               
GPN1A    SR    RE,RE                                                            
         SLDL  RE,4                                                             
         IC    RE,HEXTAB(RE)                                                    
         STC   RE,0(R1)                                                         
         LA    R1,1(R1)                                                         
         BCT   R0,GPN1A                                                         
         MVI   MCDUB,C'T'          MCDUB=C'TSPPOO'                              
         B     LD                                                               
*                                                                               
GPN2     L     R1,=V(PROGLIST)     APPLICATION CALL - P1=X'OO',P2=X'00'         
         MVC   MCDUB(4),0(R1)                                                   
         ICM   RF,B'1000',0(R9)    RF=X'OO......'                               
         SR    RE,RE                                                            
         SLDL  RE,4                                                             
         IC    RE,HEXTAB(RE)                                                    
         STC   RE,MCDUB+4                                                       
         SR    RE,RE                                                            
         SLDL  RE,4                                                             
         IC    RE,HEXTAB(RE)                                                    
         STC   RE,MCDUB+5          MCDUB=C'TSPPOO'                              
                                                                                
* HANDLE CORE-RESIDENT PHASES                                                   
*                                                                               
LD       CLC   MCDUB(4),=C'T00A'   CORE-RESIDENT PHASES                         
         BE    *+14                                                             
         CLC   MCDUB(4),=C'T00B'                                                
         BNE   LD8                                                              
         L     R2,=V(CORELIST)     LOOK IT UP IN THE LIST                       
*                                                                               
LD2      OC    0(8,R2),0(R2)       NEW ENTRY                                    
         BNZ   LD4                                                              
         MVC   0(8,R2),MCDUB       SAVE PHASE NAME                              
*                                                                               
         XR    R3,R3               DON'T NEED LOAD ADDR                         
         LA    R5,MCTEST3                                                       
         BAS   RE,LOADEM           LOAD THE PHASE                               
*                                                                               
LD3B     MVC   8(4,R2),PARAS+4     NOTE ADDRESS IN LIST                         
         MVC   0(4,R9),PARAS+4     PASS ADDRESS TO USER                         
         B     XIT                                                              
*                                                                               
LD4      CLC   0(8,R2),MCDUB       IF WE'VE ALREADY SEEN THIS ONE               
         BNE   LD6                                                              
         MVC   0(4,R9),8(R2)       SIMPLY RETURN ADDRESS FROM TABLE             
         B     XIT                                                              
*                                                                               
LD6      LA    R2,12(R2)                                                        
         B     LD2                                                              
                                                                                
* HANDLE SCREENS                                                                
*                                                                               
LD8      LA    R5,MCTEST1                                                       
         CLC   MCDUB+4(2),=C'00'                                                
         BE    *+8                                                              
         LA    R5,MCTEST2                                                       
*&&US                                                                           
         CLI   MCDUB+1,C'7'        FOR TALENT                                   
         BNE   LD9                                                              
         CLC   MCDUB+2(2),=C'F2'   ALL T7F2 PHASES ARE SCREENS                  
         BE    LD10                                                             
         CLC   MCDUB+4(2),=C'FF'   EXCEPT FOR VIRGIN SCREEN                     
         BE    LD10                                                             
         CLC   MCDUB+2(2),=C'02'   ALL T702 PHASES ARE PROGRAMS                 
         BE    LD14                                                             
*&&                                                                             
LD9      CLI   MCDUB+4,C'F'        TEST IF SCREEN                               
         BH    LD14                                                             
         CLC   MCDUB+1(3),=C'A0B'  NEW CONTROL/GEN PROGRAM?                     
         BE    LD9A                                                             
         CLC   MCDUB+1(3),=C'A0D'  NEW CONTROL/SECURITY PROGRAM?                
         BE    LD9A                                                             
         B     LD9B                                                             
*                                                                               
LD9A     CLI   MCDUB+4,C'A'        YES                                          
         BL    LD14                                                             
         B     LD10                SCREENS ARE A0-FF                            
*                                                                               
LD9B     DS    0H                                                               
*&&US*&& CLI   MCDUB+1,C'6'        US ACC SCREENS ARE A0-FF                     
*&&US*&& BE    LD10                                                             
*&&UK*&& CLI   MCDUB+4,C'A'        UK SCREENS ARE A0-FF                         
*&&US*&& CLI   MCDUB+4,C'C'        US SCREENS ARE C0-FF                         
         BL    LD14                                                             
*                                                                               
LD10     L     R3,0(R9)                                                         
         CLC   MCDUB+4(2),=C'FF'   VIRGIN SCREENS                               
         BNE   *+8                                                              
         L     R3,=V(TWA)                                                       
         BAS   RE,LOADEM                                                        
         MVC   8(4,R9),PARAS       PASS USER L'PHASE                            
*                                                                               
         OC    1(3,R9),1(R9)                                                    
         BZ    XIT                                                              
         CLC   1(3,R9),PARAS+5     DID SCREEN GET LOADED AT                     
         BE    XIT                 SPECIFIED ADDRESS                            
         L     R2,0(R9)            IF NOT DO IT NOW                             
         L     R3,PARAS+4                                                       
         L     R4,PARAS                                                         
         MOVE  ((R2),(R4)),(R3)                                                 
         B     XIT                                                              
                                                                                
* HANDLE PROGRAMS                                                               
*                                                                               
LD14     L     R2,=V(PROGLIST)     LOOK UP IN PROG LIST                         
         OC    0(12,R2),0(R2)      FOR APPLICATIONS                             
         BNZ   LD16                                                             
         MVC   8(4,R2),=V(PROGAREA)                                             
*                                                                               
LD16     OC    0(8,R2),0(R2)       TEST FIRST TIME FOR PHASE NAME               
         BNZ   LD18                                                             
         L     R3,0(R9)            DID USER SPECIFY ADDRESS                     
         SLL   R3,8                                                             
         SRL   R3,8                                                             
         LTR   R3,R3                                                            
         BNZ   *+8                                                              
         L     R3,8(R2)            IF NOT USE NEXT ADDRESS FROM TABLE           
         ST    R3,8(R2)            SAVE IN TABLE                                
         BAS   RE,LOADEM                                                        
         MVC   0(8,R2),MCDUB       SAVE NAME IN TABLE                           
         MVC   20(4,R2),PARAS+8    SAVE A(NEXT SLOT FOR NEXT GUY)               
         B     LD19                                                             
*                                                                               
LD18     CLC   0(6,R2),MCDUB       IF WE'VE ALREADY SEEN THIS PHASE             
         BNE   LD20                                                             
         L     R3,8(R2)            USE PREVIOUS LOAD ADDRESS                    
         BAS   RE,LOADEM           AND LOAD IT AGAIN                            
*                                                                               
LD19     ST    R3,0(R9)            PASS BACK LOAD ADDRESS                       
         MVC   8(4,R9),PARAS       AND LENGTH OF PHASE                          
         B     XIT                                                              
*                                                                               
LD20     LA    R2,12(R2)           TRY NEXT ENTRY                               
         B     LD16                                                             
                                                                                
* ROUTINE LOADS THE PHASE                                                       
*                                                                               
LOADEM   ST    RE,SAVERE           R3=LOAD ADDRESS                              
         CLI   0(R5),C' '                                                       
         BE    LOADEM2                                                          
         MVC   MCDUB+6(1),0(R5)    LOOK FOR TEST VERSION FIRST                  
         GOTO1 MCVLOADM,PARAS,(R3)                                              
         MVI   MCDUB+6,C' '                                                     
         BE    LOADEMX                                                          
*                                                                               
LOADEM2  GOTO1 MCVLOADM,PARAS,(R3)                                              
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
LOADEMX  L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
XIT      XIT1                                                                   
                                                                                
* CONSTANTS AND LITTERALS                                                       
*                                                                               
HEXTAB   DC    C'0123456789ABCDEF'                                              
         LTORG                                                                  
                                                                                
* DSECT FOR CALLOFF                                                             
*                                                                               
CALLOFFD DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
SAVERE   DS    F                                                                
PARAS    DS    6F                                                               
CALLOFLQ EQU   *-CALLOFFD                                                       
                                                                                
         PRINT OFF                                                              
       ++INCLUDE DDMASTD                                                        
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002DDCALLOFF 07/27/11'                                      
         END                                                                    
