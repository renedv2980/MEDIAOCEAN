*          DATA SET DDSTATE    AT LEVEL 003 AS OF 10/05/16                      
*CATALP DDSTATE                                                                 
***********************************************************************         
* DDSTATE - HANDLE SYSTEM STATE CHANGES                               *         
***********************************************************************         
         SPACE 2                                                                
         TITLE 'DDSTATE - HANDLE SYSTEM STATE CHANGES'                          
DDSTATE  CSECT                                                                  
         COPY  IEABRC                                                           
         NMOD1 WRKX-WORKD,**STATE*                                              
         USING WORKD,RC                                                         
         J     START               GOTO START OF CODE                           
                                                                                
***********************************************************************         
*                                                                     *         
* P1     A(ACTION)                                                    *         
* P2     XL4 SE NUM                                                   *         
* P3     RETURN                                                       *         
*                                                                     *         
***********************************************************************         
                                                                                
$$DATA   LOCTR ,                   DATA LOCATION CLOSE TO RB                    
                                                                                
***********************************************************************         
* RB = BASE1 LITERALS AND CONSTANTS                                   *         
* RA = RESERVED                                                       *         
* R9 = LOCAL BASE                                                     *         
* CONSTANTS & LITERALS DEFINE LATER USING $$DATA LOCTR                *         
***********************************************************************         
*                                                                               
$$CODE   LOCTR ,                   CODE AFTER DATA                              
*                                                                               
***********************************************************************         
* MAIN CODE AREA                                                      *         
***********************************************************************         
*                                                                               
START    ST    RD,SAVERD                                                        
*                                                                               
         LR    R8,R1                                                            
         USING SVPARMS,R8                                                       
*                                                                               
         CLI   INITFLG,C'N'        INITIALISE ONCE PER JOB                      
         JNE   *+8                                                              
         BRAS  RE,INITIAL                                                       
         MVI   INITFLG,C'Y'        FLAG IT DONE                                 
*                                                                               
         LA    R1,COMMS            SET POINTER AND CLEAR NUMBER                 
         ST    R1,ACOMMS                                                        
         XC    NCOMMS,NCOMMS                                                    
*                                                                               
STATE010 LARL  RE,ACTTAB           VALIDATE ACTION                              
         L     R1,SVP1                                                          
*                                                                               
STATE011 CLC   0(4,RE),0(R1)       SCAN TABLE                                   
         JE    STATE020                                                         
         LA    RE,12(RE)                                                        
         CLC   0(4,RE),=C'XXXX'                                                 
         JNE   STATE011                                                         
         DC    H'0'                UNKNOWN ACTION                               
*                                                                               
STATE020 L     RF,8(RE)            GET A(ROUTINE)                               
         BASR  RE,RF                                                            
*                                                                               
         LR    R1,R8                                                            
         XMOD1                                                                  
*                                                                               
EXITEQU  CR    RB,RB                                                            
         J     EXIT                                                             
*                                                                               
EXITNEQ  CLI   *,255                                                            
         J     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
***********************************************************************         
* INITIALISE ONCE PER JOB.                                            *         
***********************************************************************         
INITIAL  NTR1                                                                   
*                                                                               
         ICM   RF,15,=V(SSB)       MUST HAVE AN SSB                             
         JZ    *+2                                                              
         MVC   MYALET,SSBALET-SSBD(RF)                                          
         MVC   MYTLET,SSBTBLET-SSBD(RF)                                         
         OC    0(2,RF),0(RF)       TEST OFFLINE                                 
         JZ    *+14                                                             
         MVC   DSPACE,SSBDSPAC-SSBD(RF)                                         
         J     *+10                                                             
         MVC   DSPACE,SSODSPAC-SSOOFF(RF)                                       
*                                                                               
         USING FACIDD,RE                                                        
         LA    RE,FACIDTAB         FIND CORRECT TABLE                           
INIT001  CLI   FACIDSPC,X'FF'                                                   
         JE    *+2                 NOT VALID DSPACE                             
         CLC   FACIDSPC,DSPACE     MATCH ON DSPACE                              
         JE    INIT002                                                          
         LA    RE,FACIDLNQ(,RE)                                                 
         J     INIT001                                                          
*                                                                               
INIT002  MVC   AFACTAB,FACAID      SAVE OFF TABLE ADDRESS                       
         DROP  RE                                                               
*                                                                               
INIT010  LA    R2,FULL             GET JOB NAME INTO MVSNAME                    
         EXTRACT (2),FIELDS=TIOT                                                
         L     R2,FULL                                                          
         MVC   MVSNAME,0(R2)                                                    
         LA    R2,FULL                                                          
         EXTRACT (2),'S',FIELDS=(ASID)                                          
         LA    R1,FULL                                                          
         L     R2,0(R1)                                                         
         LOCASCB ASID=(R2)         GET ASCB ADDRESS INTO R1                     
         L     R2,ASCBASSB-ASCB(R1) R5 = A(ASSB)                                
*                                                                               
         SAM31 ,                   SWITCH TO 31-BIT MODE                        
         L     R1,ASSBJSAB-ASSB(R2) R2 = A(JSAB)                                
         USING JSAB,R1                                                          
         MVC   MVSJOB,JSABJBID     GET JOBID (E.G., JOB12345)                   
         PACK  DUB,MVSJOB+3(5)                                                  
         CVB   R1,DUB                                                           
         STCM  R1,3,MVSJNUM        CONVERT TO JOBNO                             
         DROP  R1                                                               
*                                                                               
         LAM   AR2,AR2,MYALET                                                   
         XR    R2,R2                                                            
         SAC   512                                                              
         ICM   R2,15,DHASTATE-DMDSHDR(R2)                                       
         ST    R2,ASTATE           SAVE ADDRESS OF STATE TABLE                  
*                                                                               
         USING DSSHDR,R2                                                        
         CLC   DSSEYE,=C'****SYSSTATE****'                                      
         JNE   *+2                 SYSSTATE AREA NOT INITIALISED                
*                                                                               
         MVC   ASTATIC,DSASSTAT    GET A(STATIC STATE TABLE)                    
         MVC   AGLOBAL,DSAGSTAT    GET A(GLOBAL STATES)                         
         MVC   ALOCALS,DSATSTAT    GET A(LOCAL TASK STATES)                     
*                                                                               
         SAM24 ,                   SWITCH BACK TO 24 BT MODE                    
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* INITIALISE LOCAL STATE ENTRY - INIT CALL -                          *         
***********************************************************************         
INIT     NTR1                                                                   
*                                                                               
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
*                                                                               
         L     R2,ASTATIC          START OF STATIC TABLE                        
         L     R3,=A(DSSLEN*DSSMAX)                                             
         AR    R3,R2               END OF STATIC TABLE                          
*                                                                               
INIT020  ST    R2,FULL             SAVE A(ENTRY) IN FULL                        
*                                                                               
         LA    R1,7                WORK OUT LEN OF NAME                         
INIT021  L     R2,FULL                                                          
         AR    R2,R1                                                            
         CLI   0(R2),C' '          FIRST NON SPACE                              
         JNE   INIT030                                                          
         JCT   R1,INIT021          SET R0 TO COMPARE LEN VALUE                  
         DC    H'0'                                                             
*                                                                               
INIT030  L     R2,FULL             COMPARE NAME                                 
         EXRL  R1,*+10                                                          
         J     *+10                                                             
         CLC   0(0,R2),MVSNAME                                                  
         JE    INIT050                                                          
         AH    R2,=Y(DSSLEN)       NO - THEN MOVE TO NEXT ENTRY                 
         CR    R2,R3                                                            
         JL    INIT020                                                          
         DC    H'0'                NO SYSSTATE TABLE ENTRY FOR JOB              
*                                                                               
INIT050  ST    R2,ASTATIC          SAVE A(MY STATIC ENTRY)                      
*                                                                               
         XR    R2,R2               FIND JOB TABLE ENTRY FOR ME                  
         USING DMDSHDR,R2                                                       
         L     R2,DHAJOBS                                                       
         USING DSJOBD,R2                                                        
         LA    R0,DSJOBMXQ                                                      
INIT051  CLC   DSJNAM,MVSNAME      SAME NAME                                    
         JNE   INIT055                                                          
         CLC   DSJNUM,MVSJNUM      SAME JOB NUM                                 
         JE    INIT059                                                          
*                                                                               
INIT055  AHI   R2,DSJOBLNQ         TRY NEXT                                     
         JCT   R0,INIT051                                                       
         DC    H'0'                NO ENTRY IN JOBTAB                           
*                                                                               
INIT059  ST    R2,MYJOBNTR         SAVE A(MY ENTRY)                             
*                                                                               
         LAM   AR2,AR2,MYALET                                                   
         LAM   AR3,AR3,MYALET                                                   
         LAM   AR4,AR4,MYALET                                                   
         L     R2,ASTATE                                                        
         SAC   512                                                              
*                                                                               
INIT100  L     R2,ASTATIC          START OF STATIC TABLES                       
         USING DSSSTATE,R2                                                      
         L     R3,AGLOBAL          GLOBAL TABLE                                 
         USING DSGSTATE,R3                                                      
         L     R4,ALOCALS          LOCAL TABLES                                 
         USING DSLSTATE,R4                                                      
*                                                                               
         XC    FULL,FULL                                                        
         LA    RF,DSLMAX                                                        
INIT120  OC    DSLJOBNM,DSLJOBNM   FREE ENTRY                                   
         JNZ   INIT121                                                          
         OC    FULL,FULL                                                        
         JNZ   *+8                                                              
         ST    R4,FULL             SAVE IT                                      
*                                                                               
INIT121  CLC   DSLJOBNM,MVSNAME                                                 
         JNE   INIT150                                                          
         CLC   DSLJOBID,MVSJOB     EXACT MATCH                                  
         JE    INIT160                                                          
*                                                                               
INIT150  AH    R4,=Y(DSLLEN)       NEXT                                         
         JCT   RF,INIT120                                                       
*                                                                               
         ICM   R4,15,FULL          ALL CHECKED. FREE ENTRY IN FULL              
         JZ    *+2                 NO FREE ENTRY IN LOCAL TABLE                 
*                                                                               
         L     R1,=F'-1'           GRAB IT WITH AN FFFFFFFF                     
         XR    R0,R0                                                            
         CS    R0,R1,DSLJOBNM                                                   
         JNE   INIT100             NO LONGER CLEAR. TRY AGAIN                   
*                                                                               
         MVC   DSLJOBNM,MVSNAME    SET MY VALUES. NAME                          
         MVC   DSLJOBID,MVSJOB     JOB ID                                       
         MVC   DSLJOBD,MYJOBNTR    JOB ENTRY                                    
         MVC   DSLJNUM,MVSJNUM     JOB NUMBER                                   
         ST    R2,DSLSTAT          STATIC ENTRY                                 
*                                                                               
INIT160  ST    R4,ALOCAL           SAVE AS ALOCAL                               
         SR    RF,RF                                                            
INIT165  SR    R1,R1                                                            
         IC    R1,DSGNEWS(RF)      GET SE STATE CHR (USE NEW STATE)             
         STC   R1,NEWSTATG                                                      
*                                                                               
         LA    RE,0                                                             
         CLI   NEWSTATG,C'O'       OPEN (0)                                     
         JE    INIT166                                                          
         LA    RE,1                                                             
         CLI   NEWSTATG,C'R'       READ (1)                                     
         JE    INIT166                                                          
         LA    RE,2                                                             
         CLI   NEWSTATG,C'C'       CLOSE (2)                                    
         JE    INIT166                                                          
         LA    RE,3                                                             
         CLI   NEWSTATG,C'U'       USER (3)                                     
         JE    INIT166                                                          
         J     INIT167             NOT DEFINED                                  
*                                                                               
INIT166  AR    RE,RF                                                            
         SR    R1,R1                                                            
         IC    R1,DSSSYS(RE)       GET MY STATE FROM STATIC                     
         STC   R1,NEWSTATL                                                      
*                                                                               
         SRL   RF,1                                                             
         STC   R1,DSLSYS(RF)       SAVE STATE TO LOCAL                          
         STC   R1,DSLSYS+1(RF)     SAVE STATE TO LOCAL NEW                      
         SLL   RF,1                                                             
         IC    R1,DSSSYS(RF)       GET DEFAULT OPEN STATE                       
         SRL   RF,1                                                             
         STC   R1,OPENSTAT(RF)                                                  
         SLL   RF,1                                                             
*                                                                               
INIT167  LA    RF,4(RF)            BUMP 4 FOR NEXT SYSTEM                       
         C     RF,=F'1024'                                                      
         JL    INIT165             ALL SYSTEMS IS 4*256                         
*                                                                               
INIT200  ICM   R2,15,MYJOBNTR      SET POINTER BACK TO LOCAL TABLE              
         USING DSJOBD,R2                                                        
         MVC   DSJLOCAL,ALOCAL                                                  
         MVC   DSJASID+2(2),=C'XX'                                              
         MVC   SVP2,ALOCAL         RETURN LOCAL                                 
         LA    R1,OPENSTAT                                                      
         ST    R1,SVP3             RETURN OPENSTAT                              
         OC    DSJLOCAL,DSJLOCAL                                                
         JZ    *+2                                                              
         J     ARSOFFOK                                                         
                                                                                
         DROP  R2,R3,R4                                                         
         EJECT                                                                  
***********************************************************************         
* DELETE MY LOCAL ENTRY                                               *         
***********************************************************************         
DEL      NTR1                                                                   
*                                                                               
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         ICM   R2,15,ALOCAL        LOCAL ENTRY IF THERE                         
         JZ    DELX                                                             
*                                                                               
         ST    R2,FULL                                                          
         USING DSLSTATE,R2         FIND JOB ENTRY AND REMOVE LINK               
         ICM   R2,15,DSLJOBD                                                    
         JZ    DEL010                                                           
         USING DSJOBD,R2                                                        
         XC    DSJLOCAL,DSJLOCAL   REMOVE LINK                                  
*                                                                               
DEL010   L     R2,FULL                                                          
         LA    R3,DSLLEN           REMOVE LOCAL ENTRY                           
         XR    R0,R0                                                            
         XR    R1,R1                                                            
         MVCL  R2,R0                                                            
                                                                                
DELX     J     ARSOFFOK                                                         
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* LOCATE LOCAL ENTRY - RETURN HEADER IF 00 OR LOCATION OF SE          *         
***********************************************************************         
LOCATE   NTR1                                                                   
*                                                                               
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         ICM   R2,15,ALOCAL        LOCAL ENTRY IF THERE                         
         JZ    LOCATEX                                                          
         ICM   R1,15,SVP2          ANY SE NUMBER                                
         JZ    LOCATEX             NO - RETURN A(TABLE)                         
*                                                                               
         SLL   R1,1                YES INDEX TO SYSTEM ENTRY                    
         USING DSLSTATE,R2                                                      
         LA    R2,DSLSYS(R1)                                                    
*                                                                               
LOCATEX  ST    R2,SVP3             RETURN TABLE OR SE ENTRY                     
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* LOCATE LOCAL ENTRY AND SET TO OPEN/READ/CLOSED/USER                 *         
***********************************************************************         
LOPEN    MVI   BYTE,0              SET POSITION IN STATIC TABLE                 
         MVI   BYTE1,C'W'          SET DEFAULT IN BYTE1                         
         J     LALL                                                             
LREAD    MVI   BYTE,1                                                           
         MVI   BYTE1,C'R'                                                       
         J     LALL                                                             
LCLOSE   MVI   BYTE,2                                                           
         MVI   BYTE1,C'C'                                                       
         J     LALL                                                             
LUSER    MVI   BYTE,3                                                           
         MVI   BYTE1,C'W'                                                       
         J     LALL                                                             
*                                                                               
LALL     NTR1                                                                   
         LAM   AR2,AR2,MYALET                                                   
         SAC   512                                                              
         L     R2,ASTATIC          STATIC TABLES                                
         L     R1,SVP2             SE                                           
         SLL   R1,2                                                             
         USING DSSSTATE,R2                                                      
         LA    R2,DSSSYS(R1)       INDEX TO SE IN STATIC                        
         SR    RE,RE                                                            
         IC    RE,BYTE                                                          
         AR    R2,RE                                                            
         MVC   BYTE,0(R2)          NEW STATE VALUE IN BYTE                      
*                                                                               
         CLI   BYTE,0              IF NOT DEFINED                               
         JNE   *+10                                                             
         MVC   BYTE,BYTE1          USE DEFAULT STATE FOR ACTION                 
*                                                                               
         ICM   R2,15,ALOCAL        LOCAL ENTRY IF THERE                         
         JZ    LOPENX                                                           
         ICM   R1,15,SVP2          ANY SE NUMBER                                
         JZ    LOPENX              NO - RETURN A(TABLE)                         
*                                                                               
         SLL   R1,1                YES INDEX TO SYSTEM ENTRY                    
         USING DSLSTATE,R2                                                      
         LA    R2,DSLSYS(R1)                                                    
         MVC   0(1,R2),BYTE        SET CURRENT STATUS                           
*        CLI   1(R2),0                                                          
*        JNE   *+10                                                             
*        MVC   1(1,R2),BYTE        IF ZERO SET NEW STATUS TOO                   
*                                                                               
LOPENX   ST    R2,SVP3             RETURN TABLE OR SE ENTRY                     
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* ENQUIRE ON STATE OF AN SE - RETURN GGLL IN P3                       *         
***********************************************************************         
ENQUIRE  NTR1                                                                   
*                                                                               
         BRAS  RE,GETGLOB          GET GLOBAL SE WORD                           
         USING DSGSTATE,R2                                                      
*                                                                               
         MVC   SVP3(2),DSGCURS     RETURN SYSTEM WORD                           
                                                                                
         ICM   R2,15,ALOCAL        LOCAL ENTRY IF THERE                         
         JZ    ENQUIREX                                                         
*                                                                               
         USING DSLSTATE,R2                                                      
         SRL   R1,1                INDEX TO SE                                  
         LA    R2,DSLSYS(R1)                                                    
         MVC   SVP3+2(2),0(R2)     COPY OUT STATE                               
                                                                                
ENQUIREX J     ARSOFFOK                                                         
                                                                                
         EJECT                                                                  
***********************************************************************         
* SET A GLOBAL SE TO OPEN STATE                                       *         
***********************************************************************         
OPEN     NTR1                                                                   
         BRAS  RE,GETGLOB          GET GLOBAL SE WORD                           
         USING DSGSTATE,R2                                                      
         MVI   DSGNEWS,C'O'        NEW STATE OPEN                               
         MVC   WKSTATE,DSGCURS                                                  
         BRAS  RE,SETSTATE                                                      
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* SET A GLOBAL SE TO READ STATE                                       *         
***********************************************************************         
READ     NTR1                                                                   
         BRAS  RE,GETGLOB          GET GLOBAL SE WORD                           
         USING DSGSTATE,R2                                                      
         MVI   DSGNEWS,C'R'        NEW STATE READ                               
         MVC   WKSTATE,DSGCURS                                                  
         BRAS  RE,SETSTATE                                                      
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* SET A GLOBAL SE TO USER  STATE                                      *         
***********************************************************************         
USER     NTR1                                                                   
         BRAS  RE,GETGLOB          GET GLOBAL SE WORD                           
         USING DSGSTATE,R2                                                      
         MVI   DSGNEWS,C'U'        NEW STATE USER                               
         MVC   WKSTATE,DSGCURS                                                  
         BRAS  RE,SETSTATE                                                      
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* SET A GLOBAL SE TO CLOSE STATE                                      *         
***********************************************************************         
CLOSE    NTR1                                                                   
         BRAS  RE,GETGLOB          GET GLOBAL SE WORD                           
         USING DSGSTATE,R2                                                      
         MVI   DSGNEWS,C'C'        NEW STATE CLOSED                             
         MVC   WKSTATE,DSGCURS                                                  
         BRAS  RE,SETSTATE                                                      
         J     ARSOFFOK                                                         
         EJECT                                                                  
***********************************************************************         
* SET STATE - CHECK ALL LOCAL JOBS AND SEND COMMANDS WHERE NECESSARY  *         
***********************************************************************         
                                                                                
SETSTATE NTR1                                                                   
         CLC   WKSTATE,=C'OC'      OPEN TO CLOSED                               
         JE    SETS005                                                          
         CLC   WKSTATE,=C'OR'      OPEN TO READ                                 
         JE    SETS005                                                          
         CLC   WKSTATE,=C'RC'      READ TO CLOSED                               
         JE    SETS005                                                          
         J     SETS006                                                          
*                                                                               
SETS005  BRAS  RE,RUNWAIT          WAIT FOR RUNNERS TO COMPLETE                 
*                                                                               
SETS006  LA    RF,DSLMAX           TEST ALL LOCAL ENTRIES                       
         L     R2,ALOCALS                                                       
         USING DSLSTATE,R2                                                      
*                                                                               
SETS010  ST    R2,WKLOCAL                                                       
         OC    0(4,R2),0(R2)       NULL ENTRY                                   
         JZ    SETS090                                                          
         ICM   R2,15,DSLSTAT                                                    
         JZ    *+2                                                              
         L     R1,SVP2             INDEX BY SE                                  
         SLL   R1,2                                                             
         USING DSSSTATE,R2                                                      
         LA    R2,DSSSYS(R1)                                                    
         SRL   R1,1                                                             
         MVC   NEWSTATG,WKSTATE+1                                               
*                                                                               
         LA    RE,0                                                             
         CLI   NEWSTATG,C'O'       OPEN (0)                                     
         JE    SETS020                                                          
         LA    RE,1                                                             
         CLI   NEWSTATG,C'R'       READ (1)                                     
         JE    SETS020                                                          
         LA    RE,2                                                             
         CLI   NEWSTATG,C'C'       CLOSE (2)                                    
         JE    SETS020                                                          
         LA    RE,3                                                             
         CLI   NEWSTATG,C'U'       USER (3)                                     
         JE    SETS020                                                          
         DC    H'0'                                                             
*                                                                               
SETS020  AR    R2,RE                                                            
         MVC   NEWSTATL,0(R2)      COPY STATE CHR FROM STATIC                   
         L     R2,WKLOCAL                                                       
         USING DSLSTATE,R2                                                      
         LA    R2,DSLSYS(R1)                                                    
         CLC   NEWSTATL,0(R2)      COMPARE NEW STATE TO CURRENT STATE           
         JE    SETS090                                                          
         MVC   1(1,R2),NEWSTATL    SET NEW STATE                                
*                                                                               
         BRAS  RE,PUTMSG           PUT MESSAGES TO SET LOCAL STATES             
*                                                                               
SETS090  L     R2,WKLOCAL          NEXT JOB                                     
         AH    R2,=Y(DSLLEN)                                                    
         JCT   RF,SETS010                                                       
*                                                                               
         OC    NCOMMS,NCOMMS       ANY COMMANDS SENT FOR THIS REQUEST           
         JZ    SETS100             NO THEN EXIT                                 
*                                                                               
         BRAS  RE,WAITFOR          WAIT FOR RETURNS AND CLEAR MESSAGE           
*                                                                               
SETS100  BRAS  RE,SETGLOB          FINAL SET GLOBAL STATUS UPDATE               
*                                                                               
SETSTATX J     ARSOFFOK                                                         
                                                                                
***********************************************************************         
* PUT MESSAGES TO GET REQUIRED SYSTEM STATE                           *         
***********************************************************************         
                                                                                
PUTTAB   DC    C'WC',AL2(05)       WRITE TO CLOSE -  SEND CLOSE 05              
         DC    C'WR',AL2(10)       WRITE TO READ  -  SEND READ  10              
         DC    C'RC',AL2(05)       READ TO CLOSE   - SEND CLOSE 05              
         DC    C'RW',AL2(11)       READ TO WRITE   - SEND WRITE 11              
         DC    C'CW',AL2(04)       CLOSED TO WRITE - SEND OPEN  04              
         DC    C'CR',AL2(10)       CLOSED TO READ  - SEND READ  10              
*                                                                               
         DC    C'W',X'00',AL2(05)  WRITE TO ND  - CLOSE                         
         DC    C'R',X'00',AL2(05)  READ TO ND   - CLOSE                         
         DC    C'C',X'00',AL2(00)  CLOSED TO ND - NO COMMAND                    
         DC    X'00',C'W',AL2(04)  ND TO WRITE  - OPEN                          
         DC    X'00',C'R',AL2(10)  ND TO READ   - READ                          
         DC    X'00',C'C',AL2(05)  ND TO CLOSED - CLOSE                         
*                                                                               
         DC    C'XXXX'                                                          
                                                                                
PUTMSG   NTR1                                                                   
         LA    R1,PUTTAB           FIND ACTION CODE FOR STATE CHANGE            
PUTM005  CLC   0(2,R2),0(R1)                                                    
         JE    PUTM006                                                          
         LA    R1,4(R1)                                                         
         CLC   0(4,R1),=C'XXXX'    EOT                                          
         JNE   PUTM005                                                          
         DC    H'0'                                                             
*                                                                               
PUTM006  CLC   2(2,R1),=X'0000'    NO ACTION                                    
         JNE   PUTM007                                                          
         MVC   0(1,R2),1(R2)       JUST COMPLETE STATE                          
         J     PUTMSG6                                                          
*                                                                               
PUTM007  MVC   HALF,2(R1)          SAVE ACTION CODE                             
*                                                                               
         L     R2,WKLOCAL                                                       
         ICM   R2,15,DSLJOBD       POINT TO JOB ENTRY                           
         JZ    *+2                                                              
         USING DSJOBD,R2                                                        
         MVC   SVJOBD,0(R2)                                                     
*                                                                               
         XC    MYWORK,MYWORK       BUILD COMLINE IN MYWORK                      
         USING DSCOMM,MYWORK                                                    
         MVC   DSCSORC,=X'FFFE'    FLAG AS DDOPER TYPE SENDER                   
         MVC   DSCJNUM,MVSJNUM     SAVE JOB NUMBER HERE                         
*                                                                               
         OC    DSJADV,DSJADV       IS THIS AN ADV                               
         JZ    *+18                                                             
         MVC   DSCDEST,DSJADV      TO ADNUM                                     
         MVI   DSCDEST+1,0                                                      
         J     *+14                                                             
         MVC   DSCDEST,DSJASID     ELSE TO JOB ASID                             
         OI    DSCFLAG,DSCJNUMQ                                                 
*                                                                               
         OC    DSCDEST,DSCDEST     TRAP ZERO DEST                               
         JZ    *+2                                                              
*                                                                               
         MVC   DSCCOMM,HALF        SET ACTION CODE SAVED EARLIER                
         MVC   DSCDATA(2),SVP2+2   SET SYSNUM IN DATA                           
*                                                                               
         SAC   0                                                                
         TIME  BIN                 SAVE TIME IN DSCTIME                         
         SAC   512                                                              
         ST    R0,DSCTIME                                                       
         OC    DSCTIME,DSCTIME     DON'T ALLOW ZERO AT MIDNIGHT                 
         JNZ   *+10                                                             
         MVC   DSCTIME,=X'00000001'                                             
*                                                                               
PUTMSG3  SR    R2,R2               POINT TO DSPACE DHACOMM                      
         LA    RE,DSCMAXQ                                                       
         L     R2,DHACOMM-DMDSHDR(,R2)                                          
         AHI   R2,4096             SKIP HEADERS                                 
*                                                                               
         USING DSCOMM,R2                                                        
PUTMSG4  L     RF,=F'-1'           SET TO FFS TO GRAB ENTRY                     
         SR    R1,R1                                                            
         CS    R1,RF,DSCTIME                                                    
         JNE   PUTMSG5                                                          
         MVC   DSCOMMS,MYWORK      MOVE IN COMMAND                              
*                                                                               
         MVC   COACTN,=C'ADD COMMAND   '                                        
         BRAS  RE,TRACEOUT                                                      
*                                                                               
         SAC   0                   GO POST THE SSBOPECB                         
         BRAS  RE,POSTIT                                                        
         JE    PUTMSG4Z                                                         
         WTO   'POST FAILED - COMMAND REMOVED',MCSFLAG=HRDCPY                   
*                                                                               
PUTMSG4Z SAC   512                 SWITCH BACK TO AR MODE,CC UNCHANGED          
         JE    *+10                                                             
         XC    DSCOMMS,DSCOMMS     IF POST FAILS THEN REMOVE IT                 
*                                                                               
         L     R1,ACOMMS           SAVE IN LIST                                 
         ST    R2,0(R1)                                                         
         LA    R1,4(R1)                                                         
         ST    R1,ACOMMS                                                        
         L     R1,NCOMMS                                                        
         AHI   R1,1                                                             
         ST    R1,NCOMMS                                                        
         CLC   NCOMMS,=F'400'      ALLOW 400 COMMANDS                           
         JL    PUTMSG6                                                          
         DC    H'0'                TOO MUCH FOR ME TO HANDLE                    
*                                                                               
PUTMSG5  LA    R2,L'DSCOMMS(,R2)                                                
         JCT   RE,PUTMSG4          ALL ENTRIES FULL                             
         DC    H'0'                                                             
*                                                                               
PUTMSG6  J     EXITEQU                                                          
         EJECT                                                                  
*************************************************************                   
*        DO X MEMORY POST TO JOB (DETAILS IN SVJOBD)        *                   
*************************************************************                   
POSTIT   NTR1                                                                   
         LARL  R9,POSTIT                                                        
         USING POSTIT,R9                                                        
         LH    R4,SVJOBD+12        ASID IS AT +12                               
         L     R2,SVJOBD+16        SSBOPECB IS AT +16                           
         LOCASCB ASID=(R4)                                                      
         LR    R3,R1                                                            
         LTR   RF,RF                                                            
         JNZ   NOPOST                                                           
         USING ASCB,R3                                                          
         CLC   ASCBASCB,=C'ASCB'                                                
         JNE   NOPOST                                                           
         L     R4,ASCBASSB                                                      
*                                                                               
         SAM31 ,                   SWITCH TO 31-BIT MODE                        
         L     R4,ASSBJSAB-ASSB(R4) R4 = A(JSAB)                                
         USING JSAB,R4                                                          
         CLC   SVJOBD(8),JSABJBNM                                               
         JNE   NOPOST                                                           
*                                                                               
         SAM24 ,                   SWITCH BACK TO 24 BT MODE                    
         LA    R5,99               SET COMPLETION CODE                          
         POST  (R2),(R5),ASCB=(R3),LINKAGE=SYSTEM,ECBKEY=8,MF=(E,POSTX)         
         J     EXITEQU                                                          
*                                                                               
NOPOST   SAM24 ,                   SWITCH BACK TO 24 BT MODE                    
         MVC   SVJOBD+10(8),SVJOBD                                              
         MVC   SVJOBD+2(8),SVJOBD+10                                            
         MVC   SVJOBD+10(18),=C' SYSTEM NOT POSTED'                             
         MVC   SVJOBD+0(2),=H'26'                                               
         WTO   TEXT=SVJOBD                                                      
         J     EXITNEQ                                                          
POSTX    POST  ERRET=DEAD,ECBKEY=YES,MF=L                                       
DEAD     DC    H'0'                                                             
         DROP  R3,R4,R9                                                         
         EJECT                                                                  
                                                                                
PUTMSGX  J     ARSOFFOK                                                         
                                                                                
***********************************************************************         
* CHECK MESSAGES AND SET STATES WHEN MESSAGES HAVE BEEN ACTIONED.     *         
***********************************************************************         
                                                                                
WAITFOR  NTR1                                                                   
         LAM   AR2,AR2,MYALET                                                   
         LAM   AR3,AR3,MYALET                                                   
         MVC   WAITTIME,=F'6000'   INITIALLY 60 SECS                            
*                                                                               
WAITFOR1 SAC   512                                                              
         MVI   BYTE,0                                                           
*                                                                               
         LA    R6,COMMS            LIST OF COMMANDS SENT                        
WAITFOR2 C     R6,ACOMMS           ARE WE AT END YET                            
         JE    WAITFOR5                                                         
         OC    0(4,R6),0(R6)       ALREADY DONE IT                              
         JZ    WAITFOR4                                                         
*                                                                               
         L     R2,0(,R6)                                                        
         USING DSCOMM,R2                                                        
         TM    DSCFLAG,DSCDONEQ    IS IT DONE YET                               
         JO    WAITFOR3                                                         
         OC    DSCOMMS,DSCOMMS     OR WAS IT CLEANED (RCVR PERHAPS)             
         JZ    WAITFO3A                                                         
         MVI   BYTE,1              FLAG NOT DONE YET                            
*                                                                               
         SAC   0                                                                
         TIME  BIN                 CALCULATE AGE OF MESSAGE                     
         SAC   512                                                              
         S     R0,DSCTIME                                                       
         C     R0,WAITTIME         60 SECONDS +30 PER RETRY                     
         JL    WAITFOR4                                                         
*                                                                               
         TM    DSCFLAG,DSCJNUMQ    JNUMQ IS AN ASID                             
         JO    WAITCHK5                                                         
*                                                                               
         USING FACITABD,R1                                                      
         L     R1,AFACTAB                                                       
         MVC   BYTE1,DSCDEST       MUST BE ADV SO STRIP OFF AOR BITS            
         NI    BYTE1,X'0F'                                                      
WAITCHK1 CLI   0(R1),X'FF'         EOT                                          
         JE    *+2                 NOT FOUND                                    
         CLC   BYTE1,FACIID        FIND SYSTEM IN FACIDTAB                      
         JE    WAITCHK2                                                         
         AHI   R1,L'FACITAB        NEXT ENTRY                                   
         J     WAITCHK1                                                         
*                                                                               
WAITCHK2 MVC   OPMSGTXT(39),OPERBUSY                                            
         MVC   OPFACID,MVSNAME                                                  
         MVC   OPMSGTXT(8),=C'SYSTEM  '                                         
         MVC   OPMSGTXT+9(7),SPACES                                             
         MVC   OPMSGTXT+9(4),FACISN4    SYSTEM NAME                             
         DROP  R1                                                               
*                                                                               
         SR    R1,R1               SET AOR CHR IN MSG                           
         IC    R1,DSCDEST                                                       
         SRL   R1,4                                                             
         LTR   R1,R1                                                            
         JZ    WAITCHK9                                                         
         LA    R1,X'C0'(R1)                                                     
         STC   R1,OPMSGTXT+13                                                   
         J     WAITCHK9                                                         
*                                                                               
WAITCHK5 XR    R3,R3                                                            
         L     R3,DHAJOBS-DMDHDR(,R3)                                           
*                                                                               
         USING DSJOBD,R3                                                        
         MVC   OPMSGTXT(39),OPERBUSY                                            
         MVC   OPFACID,MVSNAME                                                  
*                                                                               
         LA    R0,DSJOBMXQ                                                      
WAITCHK6 CLC   DSCDEST,DSJASID                                                  
         JE    WAITCHK7                                                         
         LA    R3,DSJOBLNQ(,R3)    NEXT ENTRY                                   
         JCT   R0,WAITCHK6                                                      
         J     WAITFOR3            JOB NOT FOUND SO MUST HAVE GONE              
*                                                                               
WAITCHK7 MVC   OPMSGTXT(8),DSJNAM                                               
         MVC   HALF,DSJNUM                                                      
         DROP  R3                                                               
         EDIT  (B2,HALF),(6,OPMSGTXT+10),ALIGN=LEFT                             
         SAC   0                                                                
*                                                                               
WAITCHK9 SAC   0                                                                
*                                  DEALS WITH MULTIPLE WAITS                    
         TIME  BIN                                                              
         S     R0,LASTWAIT         TIME OF LAST RETRY                           
         C     R0,WAITTIME         LESS THAT WAITTIME                           
         JL    WAITFOR4            JUST IGNORE THIS TOO                         
*                                                                               
         BRAS  RE,WTOR                                                          
         MVC   OPMSGTXT(40),SPACES                                              
         CLI   REPLY,C'I'                                                       
         JE    WAITFOR3                                                         
         TIME  BIN                 RESET TIME FOR RETRY                         
         ST    R0,LASTWAIT         SAVE TIME OF LAST WAIT MESSAGE               
         SAC   512                                                              
         L     R1,WAITTIME                                                      
         AHI   R1,6000             ADD 1 MIN TO WAITTIME                        
         C     R1,=F'60000'        UNTIL 10 MIN                                 
         JNL   *+8                                                              
         ST    R1,WAITTIME                                                      
         ST    R0,DSCTIME                                                       
         J     WAITFOR4                                                         
*                                                                               
WAITFOR3 SAC   512                                                              
         MVC   COACTN,=C'POSTED/CLEARED'                                        
         BRAS  RE,TRACEOUT                                                      
         XC    DSCOMMS,DSCOMMS     CLEAR IT                                     
         XC    0(4,R6),0(R6)                                                    
         J     WAITFOR4                                                         
*                                                                               
WAITFO3A SAC   512                                                              
         MVC   COACTN,=C'ENTRY CLEARED '                                        
         BRAS  RE,TRACEOUT                                                      
         XC    0(4,R6),0(R6)                                                    
*                                                                               
WAITFOR4 SAC   512                                                              
         AHI   R6,4                                                             
         J     WAITFOR2                                                         
         DROP  R2                                                               
*                                                                               
WAITFOR5 CLI   BYTE,0              ALL DONE                                     
         JE    WAITFOR7                                                         
*                                                                               
WAITFOR6 SAC   0                                                                
*                                                                               
         LA    R1,1                1 SECOND                                     
         SR    R0,R0                                                            
         M     R0,=F'38400'        * 38400 FOR TUS                              
         ST    R1,FULL                                                          
         STIMER WAIT,TUINTVL=FULL  WAIT 1 SECS THEN TRY AGAIN                   
         J     WAITFOR1                                                         
*                                                                               
WAITFOR7 CLI   WAIT,C'0'           ANY FORCED WAIT                              
         JNH   WAITFORX                                                         
         CLI   WAIT,C'9'           MUST BE 0 TO 9                               
         JH    WAITFORX                                                         
         LLC   R1,WAIT             COUNT DOWN                                   
         BCTR  R1,0                                                             
         STC   R1,WAIT                                                          
         J     WAITFOR6            AND WAIT 1 SECOND                            
*                                                                               
WAITFORX LAM   AR3,AR3,=F'0'       MUST CLEAR THIS                              
         J     EXITEQU                                                          
                                                                                
*************************************************************                   
*        WAIT FOR RUNNER                                    *                   
*************************************************************                   
RUNWAIT  NTR1                                                                   
*                                                                               
         SAC   0                                                                
         TIME  BIN                 SAVE TIME IN RUNTIME                         
         SAC   512                                                              
         ST    R0,RUNTIME                                                       
*                                                                               
         LAM   AR3,AR3,MYTLET      TABS ALET                                    
         XR    R3,R3                                                            
         USING FATABSD,R3                                                       
         L     R3,TABSRUN                                                       
         USING TABSRUND,R3                                                      
         LLC   R4,TABSJBWD         R4=WIDTH OF QUEUE ENTRIES                    
         LH    R5,TABSNQUE         R5=NUMBER OF ENTRIES                         
         L     R3,TABSAQUE         R3=QUEUE                                     
         USING TABSQUED,R3                                                      
         MVC   BYTE1,SVP2+3        BYTE=SE NUMBER                               
*                                                                               
RUNWT010 CLC   TQSENUM,BYTE1       ANY FOR THIS SE                              
         JNE   RUNWT020                                                         
         CLI   TQSTATUS,TQSDONE    IGNORE IF DONE                               
         JE    RUNWT020                                                         
*                                                                               
         SAC   0                                                                
         LA    R1,1                1 SECOND                                     
         SR    R0,R0                                                            
         M     R0,=F'38400'        * 38400 FOR TUS                              
         ST    R1,FULL                                                          
         STIMER WAIT,TUINTVL=FULL  WAIT 1 SECS THEN TRY AGAIN                   
*                                                                               
         TIME  BIN                 CALCULATE TOTAL WAIT TIME                    
         S     R0,RUNTIME                                                       
         C     R0,=A(6000)         LESS THAN 1 MIN                              
         JL    RUNWT019                                                         
*                                                                               
         MVC   OPMSGTXT(39),RUNRBUSY                                            
         MVC   OPFACID,MVSNAME                                                  
         BRAS  RE,WTOR                                                          
         CLI   REPLY,C'I'                                                       
         JE    RUNWAITX                                                         
         TIME  BIN                                                              
         ST    R0,RUNTIME                                                       
*                                                                               
RUNWT019 SAC   512                                                              
         J     RUNWT010                                                         
*                                                                               
RUNWT020 AR    R3,R4                                                            
         JCT   R5,RUNWT010                                                      
*                                                                               
RUNWAITX SAC   512                                                              
         J     EXITEQU                                                          
                                                                                
*************************************************************                   
*        OUTPUT TRACE                                       *                   
*************************************************************                   
TRACEOUT NTR1                                                                   
         USING DSCOMM,R2                                                        
*                                                                               
         MVC   COSPACE,SPACES                                                   
         XC    FULL,FULL                                                        
         MVC   FULL(2),DSCDEST     DISPLAY ADV OR JOB DEST                      
         MVC   FULL+2(1),DSCFLAG                                                
         BRAS  RE,DISJOB                                                        
         MVC   CODEST,DUB1                                                      
         MVC   CODESTN,DUB2                                                     
*                                                                               
         LARL  R1,COMTAB           SCAN COMTAB FOR ACTION                       
COM070   CLC   DSCCOMM,8(R1)                                                    
         JE    COM075                                                           
         LA    R1,16(R1)                                                        
         CLI   8(R1),X'FF'                                                      
         JNE   COM070                                                           
COM075   MVC   COCOMM(8),0(R1)                                                  
*                                                                               
         TM    11(R1),X'10'        IF SET NO SE INFORMATION                     
         JO    COMNXT                                                           
*                                                                               
         ST    R2,FULL             GET SE NAME FROM DATASPACE                   
         LLC   R1,DSCDATA+1                                                     
         LR    R2,R1                                                            
         SLL   R2,6                                                             
         MVC   COCOMM+9(7),DSPNAME-DMSPACED(R2)                                 
         L     R2,FULL                                                          
*                                                                               
*        MVC   BYTE,DSCDATA+1                                                   
*        JAS   RE,FINDNUM          FIND SE ENTRY                                
*        L     R6,ASENTRY                                                       
*        USING SELISTD,R6                                                       
*        MVC   COCOMM+9(7),SENAME  SHOW SYSTEM NAME                             
*                                                                               
COMNXT   EQU   *                                                                
         MVC   WORK1(2),=X'003C'    ELSE WARN AND SET TO TODAY                  
         MVC   WORK1+2(60),COACTN                                               
         SAC   0                                                                
         WTO   TEXT=WORK1,MCSFLAG=HRDCPY                                        
         SAC   512                                                              
*                                                                               
         J    EXIT                                                              
         EJECT                                                                  
***********************************************************************         
* SET GLOBAL STATE - FINAL ACTION INDICATING ALL CHANGES ARE COMPLETE *         
***********************************************************************         
                                                                                
SETGLOB  NTR1                                                                   
                                                                                
*                                                                               
*        DO FINAL GLOBAL UPDATE                                                 
*                                                                               
         BRAS  RE,GETGLOB                                                       
         MVC   0(1,R2),NEWSTATG                                                 
                                                                                
SETGLOBX J     ARSOFFOK                                                         
                                                                                
***********************************************************************         
* MISC ROUTINES                                                       *         
***********************************************************************         
                                                                                
ARSOFFOK SAC   0                   ARS OFF THEN EXIT OK                         
         LAM   AR0,ARF,ARZERO                                                   
         J     EXITEQU                                                          
                                                                                
GETGLOB  LAM   AR2,AR2,MYALET                                                   
         L     R2,AGLOBAL          GET GLOBAL TABLE                             
         SAC   512                                                              
         L     R1,SVP2             INDEX BY SE                                  
         SLL   R1,2                                                             
         AR    R2,R1                                                            
         BR    RE                                                               
                                                                                
WTOR     NTR1                                                                   
         XC    ECBAD,ECBAD                                                      
         MVC   OPLEN,=H'50'                                                     
         WTOR  TEXT=(OPLEN,REPLY,1,ECBAD,ROUTCDE=(1))                           
         WAIT  ECB=ECBAD                                                        
         J     EXITEQU                                                          
                                                                                
***********************************************************************         
* DISPLAY JOB DETAILS                                                 *         
***********************************************************************         
DISJOB   MVC   DUB1,SPACES                                                      
         MVC   DUB2,SPACES                                                      
         TM    FULL+2,X'01'        TEST OFFLINE JOB                             
         JO    DJOB085                                                          
         CLI   FULL+3,0                                                         
         JE    *+14                                                             
         MVC   FULL+0(1),FULL+3                                                 
         MVI   FULL+1,0                                                         
         MVC   BYTE,FULL+0                                                      
         L     R1,AFACTAB                                                       
         NI    BYTE,X'0F'                                                       
DJOB081  CLC   BYTE,4(R1)          FIND ADV SYSTEM                              
         JE    DJOB082                                                          
         LA    R1,8(R1)                                                         
         CLI   5(R1),X'FF'         CHECK EOT                                    
         JNE   DJOB081                                                          
         DC    H'0'                                                             
DJOB082  MVC   DUB1(4),0(R1)                                                    
         CLI   DUB1+3,C' '                                                      
         JE    *+14                                                             
         MVC   DUB1+2(1),DUB1+3                                                 
         MVI   DUB1+3,C' '                                                      
*                                                                               
         MVC   DUB1+4(2),=C'  '                                                 
         MVC   DUB1+6(1),FULL+1                                                 
         TM    FULL+0,X'F0'                                                     
         BZR   RE                                                               
         SR    R1,R1                                                            
         IC    R1,FULL+0                                                        
         SRL   R1,4                                                             
         LA    R1,X'C0'(R1)                                                     
         STC   R1,DUB1+3                                                        
         BR    RE                                                               
*                                                                               
DJOB085  SR    R3,R3               SET TO DATASPACE                             
         USING DMDSHDR,R3                                                       
         LAM   AR3,AR3,MYALET                                                   
         L     R3,DHAADVS          SET R3 TO ADV SYS BLOCK                      
         LA    R0,DSJOBMXQ                                                      
DJOB090  CLI   0(R3),0             EMPTY ADV ENTRY                              
         JE    DJOB100                                                          
*                                                                               
         CLC   FULL(2),12(R3)      MATCH ON ASID                                
         JNE   DJOB100                                                          
         MVC   DUB1,0(R3)          MOVE IN JOBNAME                              
         MVI   DUB2,C'J'                                                        
         EDIT  (B2,8(R3)),(5,DUB2+1)                                            
*                                                                               
DJOB100  LA    R3,DSJOBLNQ(,R3)                                                 
         JCT   R0,DJOB090          ALL JOBS DONE                                
*                                                                               
         BR    RE                                                               
         DROP  R3                                                               
                                                                                
***********************************************************************         
* FIND ASENTRY FROM BYTE=NUMBER                                       *         
***********************************************************************         
FINDNUM  NTR1                                                                   
         L     R6,=V(SELIST)       START AT BEGINING OF SELIST                  
         USING SELISTD,R6                                                       
         LH    R4,0(R6)            SET BXLE                                     
         L     R5,2(R6)                                                         
         LA    R6,6(R6)                                                         
*                                                                               
FINDN10  CLC   SESYS,BYTE          FIND SELECTED SYS                            
         JE    FINDNX                                                           
*                                                                               
         JXLE  R6,R4,FINDN10                                                    
         LTR   RB,RB               SET CC NEQ (NOT FOUND)                       
         J     EXIT                                                             
FINDNX   ST    R6,ASENTRY          SAVE ASENTRY                                 
         J     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* DATA AREAS HERE                                                     *         
***********************************************************************         
*                                                                               
$$DATA   LOCTR ,                                                                
*                                                                               
OPLEN    DS    H                                                                
*                                                                               
OPMSG    DS    0CL80  '+DDOPER+ SSSSSSSS FFFFFFFF CCCCCCCCCC                    
OPFACID  DS    CL8                                                              
         DS    CL1                                                              
OPMSGOPS DS    0CL31                                                            
OPMSGTXT DS    0CL70                                                            
OPSYSID  DS    CL8                                                              
         DS    CL1                                                              
OPFILID  DS    CL8                                                              
         DS    CL1                                                              
OPCMND   DS    CL10                                                             
         DS    CL3                                                              
OPEXTRA  DS    CL39                                                             
         ORG   OPMSG                                                            
OPTSKID  DS    CL2                 T#                                           
         DS    CL1                                                              
OPTSKWT  DS    CL2                 T# - WAITING ON TASK                         
         DS    CL1                                                              
OPTSKLU  DS    CL8                                                              
*                                                                               
***********************************************************************         
*        CONSTANTS                                                    *         
***********************************************************************         
SPACES   DC    CL64' '                                                          
ARZERO   DC    16F'0'                                                           
OPERBUSY DC    C'........ J...... IS BUSY RETRY/IGNORE  '                       
RUNRBUSY DC    C'WAITING FOR RUNNER QUEUE   RETRY/IGNORE'                       
         DS    0F                                                               
ACTTAB   DC    C'INIT    ',A(INIT)     SET UP LOCAL STATE ENTRY                 
         DC    C'DELETE  ',A(DEL)      DELETE LOCAL STATE ENTRY                 
         DC    C'LOCATE  ',A(LOCATE)   LOCATE LOCAL TABLE ADDRESS               
         DC    C'LOPEN   ',A(LOPEN)    LOCAL TABLE SET OPEN                     
         DC    C'LREAD   ',A(LREAD)    LOCAL TABLE SET READ                     
         DC    C'LCLOSE  ',A(LCLOSE)   LOCAL TABLE SET CLOSED                   
         DC    C'ENQUIRE ',A(ENQUIRE)  ENQUIRE ON AN SE                         
         DC    C'OPEN    ',A(OPEN)     SET SE TO OPEN STATE                     
         DC    C'READ    ',A(READ)     SET SE TO READ STATE                     
         DC    C'USER    ',A(USER)     SET SE TO USER STATE                     
         DC    C'CLOSE   ',A(CLOSE)    SET SE TO CLOSE STATE                    
         DC    C'XXXXXXXX',A(0)                                                 
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        SAVED AREAS                                                  *         
***********************************************************************         
         DS    0D                                                               
         DC    C'*STATE*'                                                       
INITFLG  DC    C'N'                INITIALISED FLAG                             
DSPACE   DC    C' '                DATASPACE ID                                 
MVSNAME  DC    CL8'        '       MVS JOB NAME                                 
MVSJOB   DC    CL8'JOB00000'       JOB ID                                       
MVSJNUM  DC    XL2'0000'           JOB NUMBER                                   
ASTATIC  DC    A(0)                A(MY STATIC TABLE ENTRY)                     
ALOCAL   DC    A(0)                A(MY LOCAL TABLE ENTRY)                      
MYALET   DC    A(0)                ALET FOR DMGR DATASPACE                      
MYTLET   DC    A(0)                ALET FOR TABS DATASPACE                      
ASTATE   DC    A(0)                A(STATE TABLE)                               
AGLOBAL  DC    A(0)                A(GLOBAL TABLE)                              
ALOCALS  DC    A(0)                A(LOCAL TASK ENTRIES)                        
AFACTAB  DC    A(0)                A(FACIDTAB)                                  
ECBAD    DC    F'0'                WTOR FIELDS                                  
REPLY    DC    CL4' '                                                           
         EJECT                                                                  
       ++INCLUDE FACOMTAB                                                       
       ++INCLUDE FACIDTABL                                                      
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
$$CODE   LOCTR ,                   BACK TO CODE AREA                            
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        WORKING STORAGE DSECT                                        *         
***********************************************************************         
WORKD    DSECT                                                                  
SAVERD   DS    F                                                                
SAVER1   DS    F                                                                
DUB      DS    D                                                                
DUB1     DS    D                                                                
DUB2     DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
BYTE1    DS    X                                                                
NEWSTATG DS    X                                                                
NEWSTATL DS    X                                                                
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
WORK1    DS    CL64                                                             
MYWORK   DS    CL64                                                             
SVJOBD   DS    CL32                                                             
WKSTATE  DS    H                                                                
WKLOCAL  DS    A                                                                
MYJOBNTR DS    A                                                                
ASENTRY  DS    A                                                                
ACOMMS   DS    AL4                 POINTER                                      
NCOMMS   DS    AL4                 COUNTER                                      
COMMS    DS    400AL4              LIST OF COMMANDS                             
*                                                                               
COACTN   DS    CL14                                                             
COSPACE  DS    CL64                                                             
         ORG   COSPACE                                                          
         DS    CL1                                                              
CODEST   DS    CL8                                                              
         DS    CL1                                                              
CODESTN  DS    CL8                                                              
         DS    CL1                                                              
COCOMM   DS    CL32                                                             
         ORG                                                                    
*                                                                               
WAIT     DS    X                   WAIT 1 TO 9                                  
WAITTIME DS    F                                                                
LASTWAIT DS    F                                                                
*                                                                               
RUNTIME  DS    F                                                                
*                                                                               
OPENSTAT DS    XL512               AREA FOR FASTARTS DEFAULT OPEN STATE         
*                                                                               
WRKX     EQU   *                                                                
*                                                                               
*                                                                               
SVPARMS  DSECT                                                                  
SVP1     DS    F                                                                
SVP2     DS    F                                                                
SVP3     DS    F                                                                
SVP4     DS    F                                                                
SVP5     DS    F                                                                
SVP6     DS    F                                                                
*                                                                               
*FASSB                                                                          
*FASSBOFF                                                                       
*DMDSHDR                                                                        
*DMDSYSHDR                                                                      
*FACOMTABD                                                                      
*FACIDTABD                                                                      
*FASELIST                                                                       
*DMSPACED                                                                       
       ++INCLUDE FASSB                                                          
         ORG   SSBCNTL                                                          
       ++INCLUDE FASSBOFF                                                       
       ++INCLUDE DMDSHDR                                                        
       ++INCLUDE DMDSYSHDR                                                      
       ++INCLUDE FACOMTABD                                                      
       ++INCLUDE FATABSD                                                        
       ++INCLUDE FATABSRUN                                                      
       ++INCLUDE FACIDTABD                                                      
       ++INCLUDE FASELIST                                                       
       ++INCLUDE DMSPACED                                                       
         IHAASCB LIST=YES                                                       
         PUSH ACONTROL                                                          
         ACONTROL COMPAT(NOCASE),FLAG(NOPAGE0),TYPECHECK(NOREGISTER)            
         IHAASSB LIST=YES                                                       
         POP  ACONTROL                                                          
         IAZJSAB LIST=YES                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDSTATE   10/05/16'                                      
         END                                                                    
