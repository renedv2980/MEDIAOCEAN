*          DATA SET SPLDRTYP   AT LEVEL 007 AS OF 03/10/11                      
*CATALP SPLDRTYP                                                                
         TITLE 'LDRECTY - SPOT - GET RECORD TYPE AND AGENCY CODE'               
*PARMS VIA R1                                                                   
*AL4 A(RECORD)                                                                  
*AL4 A(FILE NAME)                                                               
*AL4 A(RECORD INFO AREA)                                                        
*AL4 A(SPLDDEFN)                                                                
                                                                                
         PRINT NOGEN                                                            
LDRECTY  CSECT                                                                  
         NMOD1 0,SPLDRTYP                                                       
         LM    R3,R5,0(R1)         R3=A(REC),R4=A(NAME),R5=A(RECINFO)           
         ST    R1,APARM                                                         
         MVC   RECDATA,0(R5)       EXTRACT RECORD INFO PASSED                   
         XC    RETURN,RETURN       CLEAR RETURN AREA                            
         ZAP   RECTYVM,=P'0'       SET NO MONEY                                 
*                                                                               
         CLI   RTYPFNUM,X'21'      SPTFIL                                       
         BE    T2                                                               
         CLI   RTYPFNUM,X'23'      SPTDIR                                       
         BE    T2                                                               
         CLI   RTYPFNUM,X'32'      TRFFIL                                       
         BE    T2                                                               
         CLI   RTYPFNUM,X'33'      TRFDIR                                       
         BE    T2                                                               
         B     XIT                                                              
*                                                                               
T2       CLI   0(R3),X'10'         TEST BUY RECORD                              
         BH    TBUY                                                             
         CLI   0(R3),X'0D'         TEST SFM/DARE/NWS RECORD                     
         BNE   T20                                                              
         LA    RF,DAREA                                                         
         CLC   0(2,R3),=X'0D34'    DARE=0D34/35/36/37/38/3E                     
         BL    T4                                                               
         CLC   0(2,R3),=X'0D38'                                                 
         BNH   TYPE                                                             
         CLC   0(2,R3),=X'0D3E'                                                 
         BE    TYPE                                                             
*                                                                               
T4       LA    RF,DAREP                                                         
         CLC   0(2,R3),=X'0DB4'    DARE=0DB4/B5/B6/B7/B8                        
         BL    T6                                                               
         CLC   0(2,R3),=X'0DB8'                                                 
         BNH   TYPE                                                             
         CLC   0(2,R3),=X'0DBD'    DARE=0DBD/BE/BF                              
         BL    T6                                                               
         CLC   0(2,R3),=X'0DBF'                                                 
         BNH   TYPE                                                             
*                                                                               
T6       LA    RF,NWSA                                                          
         CLC   0(2,R3),=X'0D65'                                                 
         BL    T8                                                               
         CLC   0(2,R3),=X'0D6B'                                                 
         BNH   TYPE                                                             
*                                                                               
T8       LA    RF,NWSP                                                          
         CLC   0(2,R3),=C'0DE5'                                                 
         BL    T10                                                              
         CLC   0(2,R3),=X'0DE8'                                                 
         BNH   TYPE                                                             
*                                                                               
T10      LA    RF,ESTP                                                          
         CLC   0(2,R3),=X'0DF2'                                                 
         BE    TYPE                                                             
         LA    RF,SPILL                                                         
         CLC   0(2,R3),=X'0D13'                                                 
         BE    TYPE                                                             
         LA    RF,SFMA                                                          
         TM    1(R3),X'80'         TEST PASSIVE                                 
         BZ    TYPE                                                             
         LA    RF,SFMP                                                          
         B     TYPE                                                             
*                                                                               
T20      CLI   0(R3),X'0A'         TRAFFIC RECORD                               
         BNE   T30                                                              
         LA    RF,TRAFA                                                         
         TM    1(R3),X'80'         TEST PASSIVE                                 
         BZ    TYPE                                                             
         LA    RF,TRAFP                                                         
         B     TYPE                                                             
*                                                                               
T30      CLI   0(R3),X'02'         GOAL REC                                     
         BNE   T40                                                              
         LA    RF,GOALP                                                         
         TM    11(R3),X'80'        PASSIVE                                      
         BO    TYPE                                                             
         LA    RF,GOALA                                                         
         TM    11(R3),X'40'+X'20'+X'10'  TEST PACKAGE INFO OR HISTORY           
         BNZ   TYPE                      OR PLANNED                             
         CLI   11(R3),0            ADV GOAL                                     
         BE    TYPE                                                             
         LA    RF,GOALP                                                         
         B     TYPE                                                             
*                                                                               
T40      CLI   0(R3),X'00'         TEST IF HEADER RECORD                        
         BNE   T50                                                              
         LA    RF,CONTROL                                                       
         OC    0(5,R3),0(R3)       TEST IF FILE HEADER                          
         BE    TYPE                                                             
         LA    RF,CLTH                                                          
         OC    4(9,R3),4(R3)       CLI                                          
         BZ    TYPE                                                             
         LA    RF,PRDH                                                          
         OC    7(6,R3),7(R3)       PRD                                          
         BZ    TYPE                                                             
         LA    RF,ESTH                                                          
         OC    8(5,R3),8(R3)       EST                                          
         BZ    TYPE                                                             
         LA    RF,BILLH                                                         
         B     TYPE                MUST BE BILLREC                              
*                                                                               
T50      LA    RF,STABILL                                                       
         CLC   0(2,R3),=X'0E01'    STATION BILL                                 
         BE    TYPE                                                             
         LA    RF,STABILLP         STATION BILL PASSIVE                         
         CLI   0(R3),X'0E'                                                      
         BNE   T60                                                              
         TM    1(R3),X'80'         TEST PASSIVE                                 
         BO    TYPE                                                             
*                                                                               
T60      CLI   0(R3),X'03'         SYND                                         
         BNE   T70                                                              
         LA    RF,SYNA                                                          
         OC    8(5,R3),8(R3)       TEST PASSIVE                                 
         BZ    TYPE                                                             
         LA    RF,SYNP                                                          
         B     TYPE                                                             
*                                                                               
T70      CLI   0(R3),X'06'         AG/DP/EQ                                     
         BL    T80                                                              
         BH    T70B                                                             
T70A     CLI   RTYPDAIS,C'D'       AGY HDR FILE RECORD                          
         BNE   T70C                                                             
         BAS   RE,EXTAGY           EXTRACT AGENCY FROM RECORD                   
         B     T70C                                                             
T70B     CLI   0(R3),X'09'                                                      
         BH    T80                                                              
T70C     LA    RF,ADEHDR                                                        
         B     TYPE                                                             
*                                                                               
T80      CLI   0(R3),X'0B'         INVOICE RECORD                               
         BNE   T90                                                              
         LA    RF,INVREC                                                        
         B     TYPE                                                             
*                                                                               
T90      CLI   0(R3),X'0C'         COMPETITION RECORD                           
         BNE   T100                                                             
         LA    RF,COMPREC                                                       
         B     TYPE                                                             
*                                                                               
T100     LA    RF,UNKNOWN          UNKNOWN                                      
         B     TYPE                                                             
                                                                                
TBUY     LA    RF,CONTROL          TEST IF TRAILER                              
         CLI   0(R3),X'FF'                                                      
         BE    TYPE                                                             
         LA    RF,BUYP                                                          
         TM    0(R3),X'0C'         SPECIAL MKTGRP PASSIVES                      
         BO    TYPE                                                             
         CLI   10(R3),0            ALL OTHER BUY PASSIVES                       
         BNE   TYPE                                                             
         LA    RF,BUYA                                                          
         B     TYPE                COUNT AS ACTIVE BUY                          
*                                                                               
TYPE     ST    RF,THISRECT         RE=A(RECORD TYPE TABLE ENTRY)                
         MVC   RTYRRTYP,0(RF)                                                   
         MVC   RTYRFLAG,1(RF)                                                   
         TM    1(RF),X'01'         TEST IF AGENCY DEFINED IN KEY                
         BZ    TYPEX                                                            
         SR    R1,R1                                                            
         IC    R1,2(RF)            GET DISPLACEMENT OF AGY/MED BYTE             
         AR    R1,R3                                                            
         SR    R0,R0                                                            
         IC    R0,0(R1)            GET AGY/MED BYTE                             
         SRL   R0,4                DROP MED                                     
         STC   R0,RTYRAGCO                                                      
TYPEX    EQU   *                                                                
*                                                                               
XIT      MVC   4(4,R5),RETURN      RETURN VALUES                                
         MVC   8(8,R5),RECTYVM     RETURN MONEY                                 
         XIT1                                                                   
         EJECT                                                                  
* FIND BINARY AGENCY CODE IN AGENCY HEADER                                      
* AND STORE ALPHA AGY POSITIONALLY IN AGYTAB                                    
                                                                                
EXTAGY   NTR1                                                                   
         LA    R7,24(R3)           R7=A(FIRST ELEMENT)                          
         LR    R8,R3                                                            
         AHI   R8,6144             R8=A(END OF A MAXIMUM RECORD)                
         SR    R0,R0                                                            
*                                                                               
EXTAGY2  IC    R0,1(R7)            BUMP TO NEXT ELEMENT                         
         CHI   R0,1                                                             
         BNH   EXTAGYX             EXIT IF INVALID OR ZERO ELEMENT              
         AR    R7,R0                                                            
         CLI   0(R7),0                                                          
         BE    EXTAGYX                                                          
         CR    R7,R8               EXIT IF BEYOND MAX LENGTH RECORD             
         BH    EXTAGYX                                                          
         CLI   0(R7),X'02'         FIND A MEDIA ELEMENT                         
         BNE   EXTAGY2                                                          
         SR    RE,RE                                                            
         IC    RE,3(R7)            GET AGY/MED BYTE                             
         SRL   RE,4                DROP MEDIA                                   
         STC   RE,RTYRAGCO                                                      
         AR    RE,RE               X2                                           
         LA    RE,AGYTAB(RE)                                                    
         CLI   0(RE),0             TEST SOMETHING THERE ALREADY                 
         BNE   EXTAGYX             YES - SKIP                                   
         MVC   0(2,RE),1(R3)       MOVE AGYALPHA TO TABLE                       
EXTAGYX  XIT1                                                                   
         EJECT                                                                  
DUB      DS    D                                                                
APARM    DS    F                                                                
THISRECT DS    A                   A(RECTYP TABLE ENTRY FOR THIS REC)           
*                                                                               
THISAGY  DC    CL2' '              AGENCY ALPHA                                 
AGYTAB   DC    15XL2'00'           TABLE OF AGENCY ALPHAS                       
                                                                                
RECDATA  DS    0XL16               PASSED/RETURN DATA IN P3                     
*                                                                               
RECTYV   DS    0XL8                                                             
PASSED   DS    0XL4                PASSED DATA                                  
RTYPMODE DS    XL1                 MODE                                         
RTYPFLAG DS    XL1                 RECORD FLAGS INPUT                           
RTYPDAIS DS    CL1                 D=DAFILE,I=ISFILE                            
RTYPFNUM DS    XL1                 FILE NUMBER                                  
RETURN   DS    0XL4                                                             
RTYRAGCO DS    XL1                 AGENCY/COMPANY                               
RTYRRTYP DS    XL1                 RECORD TYPE                                  
RTYRFLAG DS    XL1                 RECORD FLAGS RETURN                          
         DS    XL1                 N/D                                          
*                                                                               
RECTYVM  DS    PL8                 RECORD TYPE MONEY RETURN FIELD               
         LTORG                                                                  
         EJECT                                                                  
* TABLE OF RECORD TYPES                                                         
* XL1 RECORD TYPE NUMBER                                                        
* XL1 FLAGS 80=PASSIVE,01=AGENCY BASED                                          
* XL1 DISPLACEMENT TO AGENCY BYTE                                               
* XL1 N/D                                                                       
         DS    0F                                                               
MAJP     DS    0CL20                                                            
CLTH     DC    X'01010100',CL16'CLIENT HEADER   '                               
PRDH     DC    X'02010100',CL16'PRODUCT HEADER  '                               
ESTH     DC    X'03010100',CL16'ESTIMATE HEADER '                               
BILLH    DC    X'04010100',CL16'BILLING HEADER  '                               
GOALA    DC    X'05010100',CL16'GOAL RECORD     '                               
SYNA     DC    X'06000000',CL16'SYNDICATION     '                               
ADEHDR   DC    X'07000000',CL16'ADV/DPT/EQ HDRS '                               
TRAFA    DC    X'08010200',CL16'TRAFFIC RECORD  '                               
INVREC   DC    X'09010100',CL16'INVOICE RECORD  '                               
COMPREC  DC    X'0A000000',CL16'COMP RECORDS    '                               
DAREA    DC    X'0B010200',CL16'DARE RECORD     '                               
NWSA     DC    X'0C010200',CL16'NWS RECORD      '                               
SFMA     DC    X'0D010200',CL16'SFM RECORD      '                               
SPILL    DC    X'0E000000',CL16'SPILL RECORDS   '                               
STABILL  DC    X'0F010200',CL16'STATION BILLS   '                               
BUYA     DC    X'10010000',CL16'BUY ACTIVE      '                               
UNKNOWN  DC    X'11000000',CL16'UNKNOWN         '                               
CONTROL  DC    X'12000000',CL16'CONTROL         '                               
*                                                                               
MINP     DS    0XL20                                                            
GOALP    DC    X'20800000',CL16'GOAL PASSIVE    '                               
SYNP     DC    X'21800000',CL16'SYND PASSIVE    '                               
TRAFP    DC    X'22800000',CL16'TRAFFIC PASSIVE '                               
DAREP    DC    X'23800000',CL16'DARE PASSIVE    '                               
NWSP     DC    X'24800000',CL16'NWS PASSIVE     '                               
SFMP     DC    X'25800000',CL16'SFM PASSIVE     '                               
STABILLP DC    X'26800000',CL16'STABILL PASSIVE '                               
BUYP     DC    X'27800000',CL16'BUY PASSIVE     '                               
ESTP     DC    X'28800000',CL16'ESTIMATE PASSIVE'                               
*                                                                               
ENDP     DC    X'FF'                                                            
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007SPLDRTYP  03/10/11'                                      
         END                                                                    
