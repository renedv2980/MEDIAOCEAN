*          DATA SET FABSAM     AT LEVEL 013 AS OF 02/24/15                      
*PROCESS USING(WARN(15))                                                        
*CATALP FABSAM                                                                  
         TITLE 'TEST BSAM I/O'                                                  
FABSAM   CSECT                                                                  
         SYSSTATE ASCENV=P                                                      
         ENTRY BSAMTAB                                                          
         NMOD1 WORKL,FABSAM**,CLEAR=YES                                         
         USING WORKD,RC                                                         
         ST    RD,SAVERD                                                        
         ST    R1,SAVER1                                                        
         LM    R8,R9,0(R1)         R8 = COMMAND, R9 = IOA OR DSN                
         LT    RF,=V(PROTOFF)                                                   
         BZ    *+6                                                              
         BASR  RE,RF                                                            
*                                                                               
         L     RF,VSSB                                                          
         USING SSBD,RF                                                          
MAIN02   L     R1,SSBBSAM                                                       
         LA    R0,1(R1)                                                         
         CS    R1,R0,SSBBSAM                                                    
         BNE   MAIN02                                                           
         DROP  RF                                                               
*                                                                               
         CHI   R8,1                GET A DCB AND OPEN THE FILE                  
         BE    GET                                                              
         CHI   R8,2                                                             
         BE    READ                                                             
         CHI   R8,3                                                             
         BE    CLOSE                                                            
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* GET A FREE DCB AND OPEN THE FILE ASSOCIATED WITH IT                 *         
***********************************************************************         
GET      MVC   DSN,0(R9)                                                        
         BRAS  RE,GETDCB                                                        
         BNE   XMOD                                                             
         BRAS  RE,DYNALLOC                                                      
         BNE   XMOD                                                             
         BRAS  RE,OPENDCB                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* READ THE NEXT (OR FIRST FOR THAT MATTER) RECORD                     *         
* NTRY: R9     = A(IOAREA)                                            *         
***********************************************************************         
READ     LTR   R9,R9                                                            
         BZ    BADAREA                                                          
         BRAS  RE,GETUTL                                                        
*                                                                               
         ICM   R2,15,ABSAM         SET BY GETUTL                                
         BZ    CANTREAD                                                         
         USING BSAMTABD,R2                                                      
         L     R3,BSAMREAD                                                      
         L     R4,BSAMADCB                                                      
*                                                                               
         READ  (R3),SF,(R4),(R9),MF=E                                           
*                                                                               
         LR    R1,R3               FIRST FULLWORD OFF R3 IS ECB                 
         ICM   RF,15,VADWAIT                                                    
         BZ    *+10                                                             
         BASR  RE,RF                                                            
         B     RDF02                                                            
*                                                                               
         WAIT  ECB=(R1)                                                         
*                                                                               
RDF02    CHECK (R3)                                                             
         B     XMOD                                                             
*                                                                               
BADAREA  DC    H'0',CL16'BAD I/O AREA ADR'                                      
CANTREAD DC    H'0',CL16'FILE NOT OPEN   '                                      
         EJECT                                                                  
***********************************************************************         
* END OF FILE CONDITION                                               *         
***********************************************************************         
EODAD    BRAS  RE,CLSEDCB                                                       
         LHI   RE,RETEOF                                                        
         ST    R0,RETCD                                                         
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* CLOSE AND FREE UTL AND DCB ENTRY                                    *         
* THIS ONLY EVER GETS CALLED BY ABEND - EODAD HANDLES NORMAL EOF      *         
***********************************************************************         
CLOSE    BRAS  RE,GETUTL                                                        
         ICM   RF,15,ABSAM                                                      
         BZ    XMOD                                                             
         MVI   INABEND,C'Y'                                                     
         BRAS  RE,CLSEDCB                                                       
         B     XMOD                                                             
         EJECT                                                                  
***********************************************************************         
* GET FREE TABLE ENTRY                                                *         
***********************************************************************         
GETDCB   NTR1  ,                                                                
         BRAS  RE,GETUTL                                                        
*                                                                               
         ICM   RF,15,ABSAM         SET BY GETUTL                                
         BZ    GDCB02                                                           
         LHI   R0,RETHAVE          YOU ALREADY HAVE A DCB                       
         ST    R0,RETCD                                                         
         B     EXITL                                                            
*                                                                               
         USING BSAMTABD,R2                                                      
GDCB02   LAY   R2,BSAMTAB          A(START OF TABLE)                            
         LHI   R4,BSAMTABL         ENTRY LENGTH                                 
         LAY   R5,BSAMTABX         END OF TABLE                                 
         L     R1,AUTLR            <- NOTE WHICH ONE!!!                         
*                                                                               
GDCB04   SLR   R0,R0               SWAP INTO A ZERO ENTRY                       
         CS    R0,R1,BSAMAUTL                                                   
         BE    GDCB06                                                           
         BXLE  R2,R4,GDCB04                                                     
         LHI   R0,RETXFREE         NO FREE DCBS                                 
         ST    R0,RETCD                                                         
         B     EXITL                                                            
*                                                                               
         USING UTLD,RA                                                          
GDCB06   L     RA,AUTLL                                                         
         MVC   BSAMLUID,TLUID      SET A(UTL)                                   
         ST    R2,ABSAM            SET A(THIS DCB TABLE ENTRY)                  
         STCM  R2,15,TBSAMDCB      SET A(ENTRY IN LOCAL UTL)                    
*                                                                               
         L     RA,AUTLR            ALSO SET A(ENTRY IN REAL UTL)                
         SAM31                                                                  
         STCM  R2,15,TBSAMDCB                                                   
         SAM24                                                                  
         B     EXITOK                                                           
         DROP  R2,RA                                                            
         EJECT                                                                  
***********************************************************************         
* DYNALLOC THE FILE INTO THE SELECTED DCB                             *         
***********************************************************************         
DYNALLOC NTR1  ,                                                                
         GOTO1 VTICTOC,DMCB,C'SSET',0                                           
*                                                                               
         L     R2,ABSAM                                                         
         USING BSAMTABD,R2                                                      
         L     R3,BSAMADCB                                                      
         GOTO1 VDYNALOC,DMCB,(X'FF',40(R3)),DSN                                 
*                                                                               
         GOTO1 VTICTOC,DMCB,C'RSET',0                                           
         B     EXITOK                                                           
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* OPEN THE DCB                                                        *         
***********************************************************************         
OPENDCB  NTR1  ,                                                                
         GOTO1 VTICTOC,DMCB,C'SSET',0                                           
*                                                                               
         L     R2,ABSAM                                                         
         USING BSAMTABD,R2                                                      
         L     R3,BSAMADCB         THIS BIT IS SINGLE THREADED                  
         OPEN  ((R3),INPUT),MF=(E,OPENFILE)                                     
*                                                                               
         GOTO1 VTICTOC,DMCB,C'RSET',0                                           
         B     EXITOK                                                           
         EJECT                                                                  
***********************************************************************         
* CLOSE THE DCB                                                       *         
***********************************************************************         
CLSEDCB  NTR1  ,                                                                
         CLI   INABEND,C'Y'        LEAVE TICTOC ALONE IF IN ABEND               
         BE    CLS02                                                            
         GOTO1 VTICTOC,DMCB,C'SSET',0                                           
*                                                                               
CLS02    L     R2,ABSAM                                                         
         USING BSAMTABD,R2                                                      
         L     R3,BSAMADCB                                                      
         CLOSE ((R3)),MF=(E,CLSEFILE)                                           
*                                                                               
         USING UTLD,RA                                                          
         ICM   RA,15,AUTLL         CLEAR LOCAL                                  
         BZ    *+10                                                             
         XC    TBSAMDCB,TBSAMDCB                                                
*                                                                               
         SAM31                                                                  
         ICM   RA,15,AUTLR         CLEAR XA                                     
         BZ    *+10                                                             
         XC    TBSAMDCB,TBSAMDCB                                                
         SAM24                                                                  
*                                                                               
         GOTO1 VDYNALOC,DMCB,(C'U',40(R3)),0,0                                  
*                                                                               
         XC    BSAMLUID,BSAMLUID   THE ORDER YOU CLEAR IS IMPORTANT -           
         XC    BSAMAUTL,BSAMAUTL   AUTL MUST BE LAST                            
*                                                                               
         CLI   INABEND,C'Y'        LEAVE TICTOC ALONE IF IN ABEND               
         BE    EXITOK                                                           
         GOTO1 VTICTOC,DMCB,C'RSET',0                                           
         B     EXITOK                                                           
         DROP  RA                                                               
         EJECT                                                                  
***********************************************************************         
* USEFUL ROUTINES                                                     *         
***********************************************************************         
EXITOK   CR    RB,RB                                                            
         J     EXIT                                                             
*                                                                               
EXITL    CLI   *,RB                                                             
         J     EXIT                                                             
*                                                                               
EXITH    CLI   *,0                                                              
         J     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
XMOD     LT    RF,=V(PROTON)                                                    
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         L     RD,SAVERD                                                        
         L     R1,SAVER1                                                        
         MVC   8(4,R1),RETCD                                                    
         CLC   =F'0',RETCD                                                      
         XMOD1 ,                                                                
*                                                                               
GETUTL   L     RF,VSSB                                                          
         ICM   RF,15,SSBTKADR-SSBD(RF)                                          
         BZ    NOTINTSK                                                         
         ICM   R0,15,TCBRUTL-TCBD(RF)                                           
         BZ    NOTINTSK                                                         
         ST    R0,AUTLR                                                         
         ICM   R0,15,TCBLUTL-TCBD(RF)                                           
         BZ    NOTINTSK                                                         
         ST    R0,AUTLL                                                         
*                                                                               
         L     RF,AUTLL                                                         
         MVC   ABSAM,TBSAMDCB-UTLD(RF)                                          
         BR    RE                                                               
*                                                                               
NOTINTSK DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
VADWAIT  DC    V(ADWAIT)                                                        
VSSB     DC    V(SSB)                                                           
VDYNALOC DC    V(DYNALLOC)                                                      
VTICTOC  DC    V(TICTOC)                                                        
*                                                                               
OPENFILE OPEN  (R3),MF=L                                                        
CLSEFILE CLOSE (R3),MF=L                                                        
         EJECT                                                                  
***********************************************************************         
* DCB ENTRY TABLE                                                     *         
***********************************************************************         
BSAMTAB  DS    0D                                                               
         DC    XL8'00',A(0,READ01,DCB01,0)                                      
         DC    XL8'00',A(0,READ02,DCB02,0)                                      
         DC    XL8'00',A(0,READ03,DCB03,0)                                      
         DC    XL8'00',A(0,READ04,DCB04,0)                                      
         DC    XL8'00',A(0,READ05,DCB05,0)                                      
         DC    XL8'00',A(0,READ06,DCB06,0)                                      
         DC    XL8'00',A(0,READ07,DCB07,0)                                      
         DC    XL8'00',A(0,READ08,DCB08,0)                                      
         DC    XL8'00',A(0,READ09,DCB09,0)                                      
         DC    XL8'00',A(0,READ0A,DCB0A,0)                                      
         DC    XL8'00',A(0,READ0B,DCB0B,0)                                      
         DC    XL8'00',A(0,READ0C,DCB0C,0)                                      
         DC    XL8'00',A(0,READ0D,DCB0D,0)                                      
         DC    XL8'00',A(0,READ0E,DCB0E,0)                                      
         DC    XL8'00',A(0,READ0F,DCB0F,0)                                      
         DC    XL8'00',A(0,READ10,DCB10,0)                                      
         DC    XL8'00',A(0,READ11,DCB11,0)                                      
         DC    XL8'00',A(0,READ12,DCB12,0)                                      
         DC    XL8'00',A(0,READ13,DCB13,0)                                      
         DC    XL8'00',A(0,READ14,DCB14,0)                                      
         DC    XL8'00',A(0,READ15,DCB15,0)                                      
         DC    XL8'00',A(0,READ16,DCB16,0)                                      
         DC    XL8'00',A(0,READ17,DCB17,0)                                      
         DC    XL8'00',A(0,READ18,DCB18,0)                                      
         DC    XL8'00',A(0,READ19,DCB19,0)                                      
         DC    XL8'00',A(0,READ1A,DCB1A,0)                                      
         DC    XL8'00',A(0,READ1B,DCB1B,0)                                      
         DC    XL8'00',A(0,READ1C,DCB1C,0)                                      
         DC    XL8'00',A(0,READ1D,DCB1D,0)                                      
         DC    XL8'00',A(0,READ1E,DCB1E,0)                                      
         DC    XL8'00',A(0,READ1F,DCB1F,0)                                      
         DC    XL8'00',A(0,READ20,DCB20,0)                                      
         DC    XL8'00',A(0,READ21,DCB21,0)                                      
         DC    XL8'00',A(0,READ22,DCB22,0)                                      
         DC    XL8'00',A(0,READ23,DCB23,0)                                      
         DC    XL8'00',A(0,READ24,DCB24,0)                                      
         DC    XL8'00',A(0,READ25,DCB25,0)                                      
         DC    XL8'00',A(0,READ26,DCB26,0)                                      
         DC    XL8'00',A(0,READ27,DCB27,0)                                      
         DC    XL8'00',A(0,READ28,DCB28,0)                                      
         DC    XL8'00',A(0,READ29,DCB29,0)                                      
         DC    XL8'00',A(0,READ2A,DCB2A,0)                                      
         DC    XL8'00',A(0,READ2B,DCB2B,0)                                      
         DC    XL8'00',A(0,READ2C,DCB2C,0)                                      
         DC    XL8'00',A(0,READ2D,DCB2D,0)                                      
         DC    XL8'00',A(0,READ2E,DCB2E,0)                                      
         DC    XL8'00',A(0,READ2F,DCB2F,0)                                      
BSAMTABX DC    XL8'00',A(0,READ30,DCB30,0)                                      
         DC    16X'FF'                                                          
*                                                                               
BSAMTABD DSECT                                                                  
BSAMLUID DS    CL8                 LUID AND UTL MUST BE TOGETHER                
BSAMAUTL DS    A                                                                
BSAMREAD DS    A                                                                
BSAMADCB DS    A                                                                
         DS    A                                                                
BSAMTABL EQU   *-BSAMTABD                                                       
*                                                                               
FABSAM   CSECT                                                                  
         EJECT                                                                  
***********************************************************************         
* DCB POOL                                                            *         
***********************************************************************         
READ01   READ  ECB01,SF,,MF=L                                                   
DCB01    DCB   DDNAME=FILE01,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ02   READ  ECB02,SF,,MF=L                                                   
DCB02    DCB   DDNAME=FILE02,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ03   READ  ECB03,SF,,MF=L                                                   
DCB03    DCB   DDNAME=FILE03,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ04   READ  ECB04,SF,,MF=L                                                   
DCB04    DCB   DDNAME=FILE04,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ05   READ  ECB05,SF,,MF=L                                                   
DCB05    DCB   DDNAME=FILE05,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ06   READ  ECB06,SF,,MF=L                                                   
DCB06    DCB   DDNAME=FILE06,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ07   READ  ECB07,SF,,MF=L                                                   
DCB07    DCB   DDNAME=FILE07,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ08   READ  ECB08,SF,,MF=L                                                   
DCB08    DCB   DDNAME=FILE08,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ09   READ  ECB09,SF,,MF=L                                                   
DCB09    DCB   DDNAME=FILE09,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ0A   READ  ECB0A,SF,,MF=L                                                   
DCB0A    DCB   DDNAME=FILE0A,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ0B   READ  ECB0B,SF,,MF=L                                                   
DCB0B    DCB   DDNAME=FILE0B,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ0C   READ  ECB0C,SF,,MF=L                                                   
DCB0C    DCB   DDNAME=FILE0C,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ0D   READ  ECB0D,SF,,MF=L                                                   
DCB0D    DCB   DDNAME=FILE0D,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ0E   READ  ECB0E,SF,,MF=L                                                   
DCB0E    DCB   DDNAME=FILE0E,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ0F   READ  ECB0F,SF,,MF=L                                                   
DCB0F    DCB   DDNAME=FILE0F,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ10   READ  ECB10,SF,,MF=L                                                   
DCB10    DCB   DDNAME=FILE10,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ11   READ  ECB11,SF,,MF=L                                                   
DCB11    DCB   DDNAME=FILE11,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ12   READ  ECB12,SF,,MF=L                                                   
DCB12    DCB   DDNAME=FILE12,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ13   READ  ECB13,SF,,MF=L                                                   
DCB13    DCB   DDNAME=FILE13,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ14   READ  ECB14,SF,,MF=L                                                   
DCB14    DCB   DDNAME=FILE14,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ15   READ  ECB15,SF,,MF=L                                                   
DCB15    DCB   DDNAME=FILE15,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ16   READ  ECB16,SF,,MF=L                                                   
DCB16    DCB   DDNAME=FILE16,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ17   READ  ECB17,SF,,MF=L                                                   
DCB17    DCB   DDNAME=FILE17,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ18   READ  ECB18,SF,,MF=L                                                   
DCB18    DCB   DDNAME=FILE18,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ19   READ  ECB19,SF,,MF=L                                                   
DCB19    DCB   DDNAME=FILE19,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ1A   READ  ECB1A,SF,,MF=L                                                   
DCB1A    DCB   DDNAME=FILE1A,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ1B   READ  ECB1B,SF,,MF=L                                                   
DCB1B    DCB   DDNAME=FILE1B,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ1C   READ  ECB1C,SF,,MF=L                                                   
DCB1C    DCB   DDNAME=FILE1C,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ1D   READ  ECB1D,SF,,MF=L                                                   
DCB1D    DCB   DDNAME=FILE1D,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ1E   READ  ECB1E,SF,,MF=L                                                   
DCB1E    DCB   DDNAME=FILE1E,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ1F   READ  ECB1F,SF,,MF=L                                                   
DCB1F    DCB   DDNAME=FILE1F,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ20   READ  ECB20,SF,,MF=L                                                   
DCB20    DCB   DDNAME=FILE20,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ21   READ  ECB21,SF,,MF=L                                                   
DCB21    DCB   DDNAME=FILE21,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ22   READ  ECB22,SF,,MF=L                                                   
DCB22    DCB   DDNAME=FILE22,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ23   READ  ECB23,SF,,MF=L                                                   
DCB23    DCB   DDNAME=FILE23,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ24   READ  ECB24,SF,,MF=L                                                   
DCB24    DCB   DDNAME=FILE24,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ25   READ  ECB25,SF,,MF=L                                                   
DCB25    DCB   DDNAME=FILE25,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ26   READ  ECB26,SF,,MF=L                                                   
DCB26    DCB   DDNAME=FILE26,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ27   READ  ECB27,SF,,MF=L                                                   
DCB27    DCB   DDNAME=FILE27,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ28   READ  ECB28,SF,,MF=L                                                   
DCB28    DCB   DDNAME=FILE28,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ29   READ  ECB29,SF,,MF=L                                                   
DCB29    DCB   DDNAME=FILE29,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ2A   READ  ECB2A,SF,,MF=L                                                   
DCB2A    DCB   DDNAME=FILE2A,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ2B   READ  ECB2B,SF,,MF=L                                                   
DCB2B    DCB   DDNAME=FILE2B,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ2C   READ  ECB2C,SF,,MF=L                                                   
DCB2C    DCB   DDNAME=FILE2C,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ2D   READ  ECB2D,SF,,MF=L                                                   
DCB2D    DCB   DDNAME=FILE2D,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ2E   READ  ECB2E,SF,,MF=L                                                   
DCB2E    DCB   DDNAME=FILE2E,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ2F   READ  ECB2F,SF,,MF=L                                                   
DCB2F    DCB   DDNAME=FILE2F,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
READ30   READ  ECB30,SF,,MF=L                                                   
DCB30    DCB   DDNAME=FILE30,DSORG=PS,MACRF=(R),RECFM=V,EODAD=EODAD             
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVER1   DS    F                                                                
SAVERD   DS    F                                                                
*                                                                               
RETCD    DS    F                                                                
RETXFREE EQU   1                   NO FREE DCBS                                 
RETHAVE  EQU   2                   YOU HAVE A DCB ALREADY                       
RETEOF   EQU   3                   EOF ON READ                                  
*                                                                               
AUTLL    DS    F                                                                
AUTLR    DS    F                                                                
ABSAM    DS    F                                                                
DMCB     DS    6F                                                               
INABEND  DS    C                                                                
DSN      DS    CL35                                                             
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* FATCB                                                                         
       ++INCLUDE FATCB                                                          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'013FABSAM    02/24/15'                                      
         END                                                                    
