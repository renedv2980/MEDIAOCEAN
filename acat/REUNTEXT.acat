*          DATA SET REUNTEXT   AT LEVEL 014 AS OF 05/01/02                      
*CATALP UNTEXT                                                                  
         TITLE 'UNSCAN REP TEXT ELEMENTS'                                       
*              PARA 1              BYTE 1-3  A(FIRST FIELD HEADER)              
*              PARA 2              BYTE 1-3  A(RECORD)                          
         SPACE 1                                                                
UNTEXT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 10,UNTXT                                                         
         USING UNTXTD,RC                                                        
         L     R2,0(R1)                                                         
         L     R3,4(R1)                                                         
         XR    R4,R4                                                            
         LA    R3,34(R3)           1ST ELEMENT                                  
         SPACE 1                                                                
         XR    R5,R5                                                            
         IC    R5,0(R2)                                                         
         SH    R5,=H'8'            R5 FIELD LENGTH                              
         TM    1(R2),X'02'         TEST EXTENDED HEADER                         
         BZ    *+8                                                              
         SH    R5,=H'8'                                                         
         LA    R6,8(R2)                                                         
         MVI   LAST,32                                                          
         EJECT                                                                  
UNTXT2   CLI   0(R3),0             END OF RECORD                                
         BE    EXIT                                                             
         CLI   0(R3),8                                                          
         BE    UNTXT4              TEXT ELEMENT                                 
         SPACE 1                                                                
UNTXT3   SR    R4,R4                                                            
         IC    R4,1(R3)                                                         
         AR    R3,R4                                                            
         B     UNTXT2                                                           
         SPACE 1                                                                
UNTXT4   MVC   THIS,2(R3)          SEQUENCE NUMBER                              
         NI    THIS,X'E0'                                                       
         CLC   THIS,LAST           IS IT SAME LINE                              
         BE    *+8                                                              
         BAS   R8,NXTFLD           NO, GET NEXT FILED HEADER                    
         SPACE 1                                                                
         MVC   LAST,THIS           SAVE CURRENT NUMBER                          
         MVC   THIS,2(R3)                                                       
         NI    THIS,X'1F'                                                       
         CLI   THIS,1              FIRST ON LINE                                
         BE    *+12                YES                                          
         MVI   0(R6),C','                                                       
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
         CLI   3(R3),C'M'          MANUAL                                       
         BNE   UNSPACE                                                          
         SPACE 1                                                                
         XR    R1,R1                                                            
         IC    R1,1(R3)                                                         
         SH    R1,=H'5'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),4(R3)       DATA TO OUTPUT FIELD                         
         SPACE 1                                                                
         LA    R6,1(R1,R6)                                                      
         B     UNTXT3                                                           
         EJECT                                                                  
UNSPACE  MVC   HALF,4(R3)          NUMBER                                       
         LH    R9,HALF                                                          
         CLI   3(R3),C'S'                                                       
         BNE   *+8                                                              
         SRL   R9,8                SPACE= 1 BYTE                                
         SPACE 1                                                                
         LTR   R9,R9               VALUE OF ZERO MEANS 'ALL'                    
         BNZ   UNSP4                                                            
         MVC   DUB(3),=C'ALL'                                                   
         LA    R9,DUB                                                           
         LA    RE,3                SET DATA LENGTH                              
         B     UNSP6                                                            
         SPACE                                                                  
UNSP4    CVD   R9,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DUB(5),DUB+5(3)                                                  
         XR    R1,R1                                                            
         SPACE 1                                                                
         LA    R9,DUB(R1)          R9 TO NON ZERO                               
         CLI   0(R9),C'0'                                                       
         BNE   *+12                                                             
         LA    R1,1(R1)                                                         
         B     *-16                                                             
         SPACE 1                                                                
         LA    RE,5                                                             
         SR    RE,R1               LENGTH OF NUMBER                             
         SPACE 1                                                                
UNSP6    CLI   3(R3),C'X'          INTERCEPT STATION AND MARKET                 
         BE    UNSP7               KEYWORDS                                     
         CLI   3(R3),C'Y'                                                       
         BNE   UNSP8                                                            
         MVC   0(3,R6),=C'MKT'                                                  
         B     *+10                                                             
UNSP7    MVC   0(3,R6),=C'STA'                                                  
         MVI   3(R6),C'='                                                       
         LA    R6,4(R6)                                                         
         B     UNSP10                                                           
         SPACE                                                                  
UNSP8    MVC   0(1,R6),3(R3)                                                    
         MVI   1(R6),C'='                                                       
         LA    R6,2(R6)                                                         
         SPACE 1                                                                
UNSP10   BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R6),0(R9)       NUMBER                                       
         LA    R6,1(RE,R6)                                                      
         B     UNTXT3                                                           
         EJECT                                                                  
NXTFLD   XR    R5,R5               NEXT FIELD                                   
         IC    R5,0(R2)                                                         
         AR    R2,R5                                                            
         TM    1(R2),X'02'         TEST EXTENDED HEADER                         
         BZ    *+16                NO                                           
         CLI   0(R2),X'4C'         FIELD + HEADER + EXTENSION = 76              
         BNE   EXIT                                                             
         B     *+12                                                             
         CLI   0(R2),X'44'         FIELD + HEADER = 68                          
         BNE   EXIT                                                             
         LA    R5,60               FIELD IS 60                                  
         LA    R6,8(R2)                                                         
         BR    R8                                                               
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         EJECT                                                                  
UNTXTD   DSECT                                                                  
LAST     DS    CL1                                                              
THIS     DS    CL1                                                              
HALF     DS    H                                                                
DUB      DS    D                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014REUNTEXT  05/01/02'                                      
         END                                                                    
