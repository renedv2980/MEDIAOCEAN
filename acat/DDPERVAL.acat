*          DATA SET DDPERVAL   AT LEVEL 010 AS OF 07/18/19                      
*CATALP PERVAL                                                                  
***********************************************************************         
* INPUT  P1    XL1 LOW ORDER 6 BITS LENGTH OF INPUT STRING (1-63)     *         
*                  X'80'=EXTENDED PERVAL BLOCK PASSED                 *         
*                  X'40'=VALIDATE FOR DD.MM RATHER THAN MM.YY         *         
*              AL3 A(INPUT STRING)                                    *         
*                                                                     *         
*        P2    XL1 LOW ORDER FOUR BITS LANGUAGE CODE (1-15)           *         
*                  X'80'=TODAYS DATE SET IN OUTPUT BLOCK              *         
*                  X'40'=SINGLE DATE INPUT REQUIRED                   *         
*                  X'20'=RETURN SINGLE DATE AS SINGLE                 *         
*                  X'10'=RETURN ADJUSTED EBD DATE ONE DAY LESS        *         
*              AL3 A(OUTPUT CONTROL BLOCK)                            *         
* OUTPUT P2    XL1 RETURN CODE                                        *         
*              AL3 A(OUTPUT CONTROL BLOCK)                            *         
***********************************************************************         
         TITLE 'DDPERVAL - VALIDATE VARIABLE PERIOD RANGE'                      
***********************************************************************         
* SAVE AND EXTRACT PARAMETERS, GET TODAY'S DATE, INITIALISE W/S       *         
***********************************************************************         
         PRINT NOGEN                                                            
PERVAL   CSECT                                                                  
         NMOD1 WORKX-WORKD,**PVAL**,R9,RR=RE                                    
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         ST    R1,R1SAVE                                                        
         SR    RA,RA                                                            
         ICM   RA,7,5(R1)                                                       
         USING PERVALD,RA          RA=A(OUTPUT CONTROL BLOCK)                   
*                                                                               
PERVAL1  MVC   INOPT1,0(R1)        DECODE 1ST PARAMETER                         
         MVI   MINYR,64            1964                                         
         MVI   MAXYR,27            2027                                         
         TM    INOPT1,PVINXTND                                                  
         BZ    PERVAL1A                                                         
         TM    PVALIND1,PVALDDMM   TEST VALIDATE FOR DDMM IN EXTENSION          
         BZ    *+8                 NO                                           
         OI    INOPT1,PVINDDMM     YES SET IN INOPT1 AS WELL                    
         TM    PVALIND1,PVAL6459   WANT NEW DATE RANGE                          
         BZ    PERVAL1A            NO                                           
         OI    INOPT1,PVIN6459     YES SET IN INOPT1 AS WELL                    
         MVI   MAXYR,59            2059                                         
*                                                                               
PERVAL1A MVC   ILEN,0(R1)                                                       
         NI    ILEN,X'3F'                                                       
         MVI   INPST,0                                                          
         MVC   INPST+1(3),1(R1)    SAVE INPUT START ADDRESS                     
*                                                                               
PERVAL2  MVC   INOPT2,4(R1)        DECODE 2ND PARAMETER                         
         MVC   LANG,4(R1)                                                       
         NI    LANG,X'0F'                                                       
         CLI   LANG,0                                                           
         BE    *+12                                                             
         CLI   LANG,7                                                           
         BNH   *+10                                                             
         MVC   LANG,DEFLANG        SET DEFAULT UK=1 OR US=2                     
         MVC   OLANG,LANG          SET OUTPUT LANGUAGE                          
*                                                                               
INIT     SR    R3,R3               SET TABLES FOR APPROPRIATE LANGUAGE          
         IC    R3,LANG                                                          
         MHI   R3,ATABADSL                                                      
         LA    R2,ATABADS(R3)                                                   
         MVC   TABLSTAD,0(R2)                                                   
         LA    R3,TABLSTAD                                                      
         LA    R2,ATABADSL/4                                                    
*                                                                               
INIT1    L     R4,0(R3)            RELOCATE A-TYPES - RE HAS RELO               
         AR    R4,RE                                                            
         ST    R4,0(R3)                                                         
         LA    R3,4(R3)                                                         
         BCT   R2,INIT1                                                         
*                                                                               
INIT2    LA    R6,TADJWK           SET TODAYS DATE INFO                         
         USING DFLDINFD,R6                                                      
         TM    INOPT2,X'80'        TODAY'S DATE SUPPLIED                        
         BO    INIT5                                                            
*                                                                               
                                                                                
INIT3    LA    R2,100              TRY 100 TIMES TO GET JULIAN DATE             
INIT3A   XC    DUB(16),DUB                                                      
         TIME  DEC,DUB,DATETYPE=YYYYDDD,LINKAGE=SYSTEM,MF=(E,TIMEPL)            
         LTR   RF,RF                                                            
         BZ    INIT3B                                                           
         BCT   R2,INIT3A                                                        
         DC    H'0'                                                             
*                                                                               
INIT3B   MVO   DUB+12(4),DUB+8(4)  DATE AT +8(4)=P'0YYYYDDD'                    
         OI    DUB+15,X'0F'                                                     
         CLI   DUB+12,X'20'                                                     
         BNE   *+12                                                             
         MVI   DUB+12,1                                                         
         B     *+8                                                              
         MVI   DUB+12,0                                                         
         L     R1,DUB+12                                                        
         ST    R1,JULIAN           P'0CYYDDDF' WHERE C=1 FOR YEAR 2000+         
*                                                                               
INIT4    MVI   TCENT,19            SET CENTURY                                  
         CLI   JULIAN,1                                                         
         BNE   *+8                                                              
         MVI   TCENT,20                                                         
         ZAP   DUB,JULIAN+2(2)     SET ADJ FACTOR TO JULIAN DAY-1               
         CVB   R2,DUB                                                           
         BCTR  R2,0                                                             
         STH   R2,TADJWK           SET NUMBER OF DAYS TO ADJUST                 
         MVI   TADJTP,C'D'         DAY TYPE ADJUSTMENT                          
         SLL   R1,8                SHIFT OUT CENTURY                            
         SRL   R1,24               SHIFT OUT DAYS                               
         SLL   R1,4                LEAVE GAP FOR SIGN                           
         LA    R1,15(R1)           ADD SIGN                                     
         STH   R1,DUB1             GET IT OUT OF R1                             
         ZAP   DUB,DUB1(2)                                                      
         CVB   R2,DUB                                                           
         STC   R2,TYEAR            SET YEAR IN BINARY                           
         MVI   TDAY,1              SET DAY TO 1                                 
         MVI   TMONTH,1            SET MONTH TO 1                               
         BAS   R8,ADJUSTER         GET TODAY'S DATE                             
         B     INIT10                                                           
*                                                                               
INIT5    OC    PVALBSTA,PVALBSTA   BINARY TODAY SUPPLIED                        
         BZ    INIT6                                                            
         LA    R1,PVALBSTA                                                      
         MVI   FPER,PVALBVP                                                     
         B     INIT9                                                            
*                                                                               
INIT6    OC    PVALCSTA,PVALCSTA   CMPRSD OLD TODAY SUPPLIED                    
         BZ    INIT7                                                            
         LA    R1,PVALCSTA                                                      
         MVI   FPER,PVALCVP                                                     
         B     INIT9                                                            
*                                                                               
INIT7    OC    PVALPSTA,PVALPSTA   PWOS TODAY SUPPLIED                          
         BZ    INIT8                                                            
         LA    R1,PVALPSTA                                                      
         MVI   FPER,PVALPVP                                                     
         B     INIT9                                                            
*                                                                               
INIT8    OC    PVALESTA,PVALESTA   EBCDIC YYMMDD TODAY SUPPLIED                 
         BZ    INIT8A                                                           
         CLC   PVALESTA,SPACES                                                  
         BE    INIT8A                                                           
         LA    R1,PVALESTA                                                      
         MVI   FPER,PVALEBC                                                     
         B     INIT9                                                            
*                                                                               
INIT8A   OC    PVALNSTA,PVALNSTA   CMPRSD NEW TODAY SUPPLIED                    
         BZ    INIT3               NO USE MACHINE DATE                          
         LA    R1,PVALNSTA                                                      
         MVI   FPER,PVALNVP                                                     
*                                                                               
INIT9    L     RF,=A(CNVDATE)      DATE CONVERSION ROUTINE                      
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         MVC   TCENT(4),FULL       SET TODAYS DATE                              
*                                                                               
INIT10   MVI   UPASS,0             SET ONLY ONE PASS NEEDED                     
         XC    SPER(8),SPER        SET NO PERIOD PASSED                         
         MVC   UCENT(4),TCENT      SET USE DATE TO TODAYS DATE                  
*                                                                               
INIT10A  TM    INOPT1,PVINXTND     TEST EXTENDED BLOCK PASSED                   
         BZ    INIT20                                                           
         TM    PVALIND1,PVALPVP+PVALCVP+PVALBVP+PVALEBC+PVALNVP                 
         BZ    INIT20                                                           
         MVC   FPER,PVALIND1                                                    
         NI    FPER,PVALPVP+PVALCVP+PVALBVP+PVALEBC+PVALNVP                     
INIT10B  LA    R1,PVALPERD         CONVERT START DATE                           
         L     RF,=A(CNVDATE)                                                   
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         MVC   SPER(4),FULL        SET START OF PERIOD IN BINARY                
INIT10C  LA    R1,PVALPERD+3       CONVERT END DATE                             
         TM    PVALIND1,PVALCVP+PVALNVP                                         
         BZ    *+8                                                              
         LA    R1,PVALPERD+2                                                    
         L     RF,=A(CNVDATE)                                                   
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
         MVC   EPER(4),FULL        SET END OF PERIOD IN BINARY                  
*                                                                               
INIT11   MVI   UPASS,1             SET FIRST PASS                               
         MVC   UCENT(4),SPER       SET USE DATE TO START OF PERIOD              
*                                                                               
INIT20   MVI   RETCODE,0           INITIALISE WORKING STORAGE FIELDS            
         MVI   PROGSW,0                                                         
         MVI   FPER,0                                                           
         MVC   PVALCPER,SPACES                                                  
         MVC   PVALESTA(12),SPACES                                              
         MVC   SDATEHDR(L'SDATEFLD+2),SPACES                                    
         MVC   EDATEHDR(L'EDATEFLD+2),SPACES                                    
         MVC   SFLDINF(8),=X'FFFFFFFFFFFFFFFF'                                  
         XC    SFLDINF+8(16),SFLDINF+8                                          
         MVC   EFLDINF(8),=X'FFFFFFFFFFFFFFFF'                                  
         XC    EFLDINF+8(16),EFLDINF+8                                          
         EJECT                                                                  
***********************************************************************         
* SPLIT INPUT INTO START AND END FIELDS OR FIND A SINGLE DATE FIELD   *         
* ALL BLANK CHARACTERS IN THE INPUT STRING ARE IGNORED                *         
* VALID SEPARATORS ARE HYPHEN,COMMA AND WORDS 'THRU' AND 'TO'         *         
***********************************************************************         
SPLIT    SR    R2,R2               R2=LENGTH OF INPUT FDATA                     
         ICM   R2,1,ILEN                                                        
         BZ    SPLIT0B                                                          
         CHI   R2,(L'SDATEFLD*2)                                                
         BH    ERRPARM             SILLY INPUT LENGTH                           
         L     R3,INPST            R3=A(START OF DATA)                          
         AR    R2,R3                                                            
         BCTR  R2,0                R2=A(LAST CHR IN INPUT AREA)                 
*                                                                               
SPLIT0   CLI   0(R2),C' '          FIND SIGNIFICANT DATA LENGTH                 
         BH    SPLIT0A                                                          
         BCTR  R2,0                                                             
         CR    R2,R3                                                            
         BNE   SPLIT0                                                           
         CLI   0(R2),C' '          TEST EMPTY OR SINGLE CHR FIELD               
         BNH   *+8                                                              
SPLIT0A  LA    R2,1(R2)                                                         
         SR    R2,R3               R2=SIGNIFICANT DATA LENGTH                   
SPLIT0B  STC   R2,INPUTLEN                                                      
         LTR   R2,R2                                                            
         BZ    SPLIT10                                                          
         LA    R7,SDATEFLD         POINT TO START DATE                          
         ST    R7,IDATSTR                                                       
         XC    FINFO,FINFO         CLEAR FIELD INFO                             
         MVI   FADJD,X'FF'                                                      
*                                                                               
SPLIT1   CLI   0(R3),C' '          SKIP BLANK CHAR IN STRING                    
         BE    SPLIT3                                                           
SPLIT1A  LA    R5,1                SET LENGTH OF SEPARATOR IN R5                
         CLI   0(R3),C'-'          TEST HYPHEN IN STRING                        
         BNE   SPLIT1B                                                          
         LR    R4,R3                                                            
         BCTR  R4,0                IS IT PRECEDED BY A LEFT BRACKET             
         CLI   0(R4),C'('                                                       
         BNE   SPLIT4              NO - MUST BE DATE SEPARATOR                  
SPLIT1B  CLI   0(R3),C','          TEST COMMA IN STRING                         
         BE    SPLIT4              YES - DATE SEPARATOR FOUND                   
         BAS   R8,FINDSEP          CHECK POSSIBLE SEPARATORS                    
         LTR   R6,R6                                                            
         BNZ   SPLIT4              SEPARATOR WORD FOUND                         
*                                                                               
SPLIT2   TM    FTFLAG,X'01'        TEST ADJUSTMENT                              
         BO    SPLIT2F                                                          
         MVI   FTFLAG,0            INITIALISE THIS CHARACTER TYPE FLAGS         
         CLI   0(R3),C'0'                                                       
         BL    *+20                                                             
         CLI   0(R3),C'9'                                                       
         BH    ERR1                                                             
         OI    FTFLAG,X'80'        SET NUMERIC                                  
         B     SPLIT2B                                                          
         CLI   0(R3),C'A'                                                       
         BL    *+12                                                             
         OI    FTFLAG,X'40'        SET ALPHABETIC                               
         B     SPLIT2B                                                          
         CLI   0(R3),C'('                                                       
         BNE   SPLIT2A                                                          
         OI    FTFLAG,X'01'        SET START OF ADJUSTMENT                      
         LR    RE,R7                                                            
         S     RE,IDATSTR                                                       
         STC   RE,FADJD            SET DISPLACEMENT TO ADJUSTMENT               
         B     SPLIT2C                                                          
SPLIT2A  OI    FTFLAG,X'20'        SET SPECIAL CHARACTER                        
         CLI   0(R3),C'.'                                                       
         BE    SPLIT2A1                                                         
         CLI   0(R3),C'/'                                                       
         BE    SPLIT2A1                                                         
         B     ERR1                                                             
SPLIT2A1 CLC   FTFLAG,FLFLAG       ONLY SINGLE SPECIAL ALLOWED                  
         BE    ERR1                                                             
         OC    FCOUNT,FCOUNT       CANT BE FIRST CHR IN STRING                  
         BZ    ERR1                                                             
SPLIT2B  CLC   FTFLAG,FLFLAG       TEST IF SAME AS LAST TYPE                    
         BE    SPLIT2F             YES                                          
*                                                                               
SPLIT2C  TM    FTFLAG,X'21'        TEST IF THIS IS SEPARATOR                    
         BZ    SPLIT2D             NO                                           
         SR    RF,RF                                                            
         ICM   RF,3,FCOUNT         YES POINT TO LAST FIELD                      
         BZ    SPLIT2E                                                          
         SLL   RF,2                                                             
         LA    RF,FFLD1D-4(RF)                                                  
         LR    RE,R7                                                            
         S     RE,IDATSTR          RE IS DISP TO SEPARATOR                      
         SR    R1,R1                                                            
         IC    R1,0(RF)            R1 IS DISP TO START OF FIELD                 
         SR    RE,R1                                                            
         CLI   1(RF),0             TEST IF LENGTH ALREADY SET                   
         BNE   SPLIT2E                                                          
         STC   RE,1(RF)            SET LENGTH OF FIELD                          
         B     SPLIT2E                                                          
*                                                                               
SPLIT2D  LH    RF,FCOUNT           THIS IS THE START OF A FIELD                 
         LA    RF,1(RF)                                                         
         STH   RF,FCOUNT           BUMP NUMBER OF FIELDS IN DATE                
         CHI   RF,3                                                             
         BH    ERR1                                                             
         SLL   RF,2                                                             
         LA    RF,FFLD1D-4(RF)     INDEX INTO FIELD DISPLACEMENTS               
         LR    RE,R7                                                            
         S     RE,IDATSTR          RE HAS DISPLACEMENT TO FIELDSTART            
         STC   RE,0(RF)                                                         
         CLC   FCOUNT,=H'1'        SET LENGTH OF PREVIOUS FIELD                 
         BE    SPLIT2E                                                          
         SHI   RF,4                BACK TO PREV FIELD                           
         SR    R1,R1                                                            
         IC    R1,0(RF)            GET DISP TO START OF PREV FIELD              
         SR    RE,R1                                                            
         CLI   1(RF),0             TEST IF LENGTH ALREADY SET                   
         BNE   *+8                                                              
         STC   RE,1(RF)                                                         
SPLIT2E  MVC   FLFLAG,FTFLAG       SAVE LAST CHARACTER TYPE                     
SPLIT2F  MVC   0(1,R7),0(R3)       MOVE DATE CHARACTER                          
         LA    R7,1(R7)                                                         
*                                                                               
SPLIT3   LA    R3,1(R3)            POINT TO NEXT INPUT CHARACTER                
         BCT   R2,SPLIT1                                                        
         SR    R5,R5               INDICATE NO SEPARATOR                        
*                                                                               
SPLIT4   S     R7,IDATSTR          R5=LENGTH OF SEPARATOR FIELD                 
         STC   R7,SLENGTH          R7=LENGTH OF DATE FIELD                      
         BZ    SPLIT4B                                                          
         OI    PROGSW,SWSTAIN      SET START DATE EXISTS                        
         LH    RF,FCOUNT                                                        
         SLL   RF,2                                                             
         LA    RF,FFLD1D-4(RF)     INDEX INTO FIELD DISPLACEMENTS               
         CLI   1(RF),0                                                          
         BNE   SPLIT4A             LENGTH ALREADY SET                           
         SR    R1,R1                                                            
         IC    R1,0(RF)                                                         
         SR    R7,R1                                                            
         STC   R7,1(RF)            SET LENGTH OF LAST START FIELD               
SPLIT4A  BAS   RE,SETFTV           SET FIELD TYPE/VALUE                         
         TM    FFLAGS,X'01'                                                     
         BO    ERR1                                                             
         MVC   SADJD(3),FADJD      SET START FIELD INFO                         
         MVC   SFIELDS,FCOUNT+1                                                 
         MVC   SFLD1D(12),FFLD1D                                                
         LTR   R2,R2                                                            
         BZ    SPLIT6              END OF INPUT AND NO END DATE                 
SPLIT4B  SR    R2,R5               POINT BEYOND SEPARATOR                       
         BZ    SPLIT6              END OF INPUT STRING                          
         AR    R3,R5               BUMP PAST SEPARATOR                          
*                                                                               
SPLIT5   CLI   0(R3),C' '          SKIP BLANKS AROUND SEPARATOR                 
         BNE   SPLIT6                                                           
         LA    R3,1(R3)                                                         
         BCT   R2,SPLIT5                                                        
*                                                                               
SPLIT6   STC   R2,ELENGTH          MOVE OUT END DATE                            
         LTR   R2,R2                                                            
         BNP   SPLITX                                                           
         LA    R7,EDATEFLD         POINT TO END DATE                            
         ST    R7,IDATSTR                                                       
         XC    FINFO,FINFO         CLEAR FIELD INFO                             
         MVI   FADJD,X'FF'                                                      
*                                                                               
SPLIT7   CLI   0(R3),C' '          MOVE NON BLANK END DATE CHRS                 
         BE    SPLIT8                                                           
         TM    FTFLAG,X'01'        TEST ADJUSTMENT                              
         BO    SPLIT7F                                                          
         MVI   FTFLAG,0            INITIALISE THIS CHARACTER TYPE FLAGS         
         CLI   0(R3),C'0'                                                       
         BL    *+20                                                             
         CLI   0(R3),C'9'                                                       
         BH    ERR2                                                             
         OI    FTFLAG,X'80'        SET NUMERIC                                  
         B     SPLIT7B                                                          
         CLI   0(R3),C'A'                                                       
         BL    *+12                                                             
         OI    FTFLAG,X'40'        SET ALPHABETIC                               
         B     SPLIT7B                                                          
         CLI   0(R3),C'('                                                       
         BNE   SPLIT7A                                                          
         OI    FTFLAG,X'01'        SET START OF ADJUSTMENT                      
         LR    RE,R7                                                            
         S     RE,IDATSTR                                                       
         STC   RE,FADJD            SET DISPLACEMENT TO ADJUSTMENT               
         B     SPLIT7C                                                          
SPLIT7A  OI    FTFLAG,X'20'        SET SPECIAL CHARACTER                        
         CLI   0(R3),C'.'                                                       
         BE    SPLIT7A1                                                         
         CLI   0(R3),C'/'                                                       
         BE    SPLIT7A1                                                         
         B     ERR2                                                             
SPLIT7A1 CLC   FTFLAG,FLFLAG       ONLY SINGLE SPECIAL ALLOWED                  
         BE    ERR2                                                             
         OC    FCOUNT,FCOUNT       CANT BE FIRST CHR IN STRING                  
         BZ    ERR2                                                             
SPLIT7B  CLC   FTFLAG,FLFLAG       TEST IF SAME AS LAST TYPE                    
         BE    SPLIT7F             YES                                          
SPLIT7C  TM    FTFLAG,X'21'        TEST IF THIS IS SEPARATOR                    
         BZ    SPLIT7D             NO                                           
         SR    RF,RF                                                            
         ICM   RF,3,FCOUNT         YES POINT TO LAST FIELD                      
         BZ    SPLIT7E                                                          
         SLL   RF,2                                                             
         LA    RF,FFLD1D-4(RF)                                                  
         LR    RE,R7                                                            
         S     RE,IDATSTR          RE IS DISP TO SEPARATOR                      
         SR    R1,R1                                                            
         IC    R1,0(RF)            R1 IS DISP TO START OF FIELD                 
         SR    RE,R1                                                            
         CLI   1(RF),0             TEST IF LENGTH ALREADY SET                   
         BNE   SPLIT7E                                                          
         STC   RE,1(RF)            SET LENGTH OF FIELD                          
         B     SPLIT7E                                                          
SPLIT7D  LH    RF,FCOUNT           THIS IS THE START OF A FIELD                 
         LA    RF,1(RF)                                                         
         STH   RF,FCOUNT           BUMP NUMBER OF FIELDS IN DATE                
         CHI   RF,3                                                             
         BH    ERR2                                                             
         SLL   RF,2                                                             
         LA    RF,FFLD1D-4(RF)     INDEX INTO FIELD DISPLACEMENTS               
         LR    RE,R7                                                            
         S     RE,IDATSTR          RE HAS DISPLACEMENT TO FIELDSTART            
         STC   RE,0(RF)                                                         
         CLC   FCOUNT,=H'1'        SET LENGTH OF PREVIOUS FIELD                 
         BE    SPLIT7E                                                          
         SHI   RF,4                BACK TO PREV FIELD                           
         SR    R1,R1                                                            
         IC    R1,0(RF)            GET DISP TO START OF PREV FIELD              
         SR    RE,R1                                                            
         CLI   1(RF),0             TEST IF LENGTH ALREADY SET                   
         BNE   *+8                                                              
         STC   RE,1(RF)                                                         
SPLIT7E  MVC   FLFLAG,FTFLAG       SAVE LAST CHARACTER TYPE                     
SPLIT7F  MVC   0(1,R7),0(R3)       MOVE DATE CHARACTER                          
         LA    R7,1(R7)                                                         
*                                                                               
SPLIT8   LA    R3,1(R3)            BUMP TO NEXT END FIELD CHR                   
         BCT   R2,SPLIT7                                                        
         S     R7,IDATSTR          CALCULATE LENGTH OF END DATE                 
         STC   R7,ELENGTH                                                       
         BZ    SPLITX                                                           
         OI    PROGSW,SWENDIN      SET END DATE EXISTS                          
         SR    RF,RF                                                            
         ICM   RF,3,FCOUNT                                                      
         BZ    SPLIT8A                                                          
         SLL   RF,2                                                             
         LA    RF,FFLD1D-4(RF)     INDEX INTO FIELD DISPLACEMENTS               
         CLI   1(RF),0                                                          
         BNE   SPLIT8A             LENGTH ALREADY SET                           
         SR    R1,R1                                                            
         IC    R1,0(RF)                                                         
         SR    R7,R1                                                            
         STC   R7,1(RF)            SET LENGTH OF LAST END FIELD                 
SPLIT8A  BAS   RE,SETFTV           SET FIELD TYPE/VALUE                         
         TM    FFLAGS,X'01'                                                     
         BO    ERR2                                                             
         MVC   EADJD(3),FADJD      SET END FIELD INFO                           
         MVC   EFIELDS,FCOUNT+1                                                 
         MVC   EFLD1D(12),FFLD1D                                                
*                                                                               
SPLIT9   TM    PROGSW,SWSTAIN+SWENDIN TEST START/END COMBINATIONS               
         BM    SPLITX              START OR END EXISTS                          
         BZ    SPLIT10             NEITHER EXISTS                               
         TM    INOPT2,PVINSGLO     BOTH EXIST                                   
         BO    ERR2                AND SHOULDNT                                 
         B     SPLITX                                                           
*                                                                               
SPLIT10  MVC   SCENT(4),TCENT      MOVE IN TODAY'S DATE AS A DEFAULT            
         OI    PROGSW,SWNOINP      AND SET NO INPUT FLAG                        
         B     XPND                                                             
SPLITX   EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* PARSE AN INPUT DATE FIELD AND TRY TO FIGURE OUT WHAT WAS INPUT      *         
***********************************************************************         
PARSE    LA    R6,SADJWK           POINT TO START DATE FIELDS                   
         LA    R7,SDATEFLD                                                      
         ST    R7,IDATSTR          SET A(DATE FIELD)                            
         MVC   IDATLEN,SLENGTH     SET L'DATE FIELD                             
*                                                                               
PARS0    CLI   DFIELDS,0           NO FIELDS PRESENT                            
         BE    PARSA                                                            
*                                                                               
PARS1    CLI   DFIELDS,1           TEST ONE DATE FIELD                          
         BNE   PARS2                                                            
*                                                                               
PARS1A   TM    DFLAGS,X'40'        TEST ALPHA MONTH                             
         BZ    PARS1B                                                           
         MVC   DMONTH,DFLD1V                                                    
         B     PARSA                                                            
*                                                                               
PARS1B   TM    DFLAGS,X'20'        TEST YESTERDAY/TODAY/TOMORROW                
         BZ    PARS1C                                                           
         SR    RF,RF                                                            
         IC    RF,DFLD1V                                                        
         SHI   RF,2                YESTERDAY=-1,TODAY=0,TOMORROW=1              
         STH   RF,DADJWK                                                        
         MVC   DCENT(4),TCENT      STORE TODAYS DATE                            
         MVI   DADJTP,C'D'         INDICATE ADJUST BY DAYS                      
         BAS   R8,ADJUSTER         ADJUST TODAYS DATE                           
         B     PARSA                                                            
*                                                                               
PARS1C   TM    DFLAGS,X'80'        TEST NUMERIC FIELD                           
         BZ    PARS1X                                                           
         LA    RF,DFLD1D           POINT TO FIRST FIELD DEFINITION              
         CLI   1(RF),2             MUST BE 1 OR 2 DIGIT NUMBER                  
         BH    PARS1C0                                                          
         BAS   R8,VALYEAR          ASSUMED TO BE YEAR                           
         BNE   ERR1                                                             
         B     PARSA                                                            
PARS1C0  CLI   1(RF),8             TEST POSSIBLE DDMMYYYY FORMAT                
         BH    ERR1                                                             
         BL    PARS1C1                                                          
         LA    R4,4                4 PAIRS OF DIGITS                            
*&&UK                                                                           
         CLC   4(2,R7),=C'12'      TEST LOOKS LIKE ISO DATE YYYYMMDD            
         BH    PARS1C2                                                          
         MVC   DUB,0(R7)                                                        
         MVC   0(2,R7),DUB+6       CONVERT TO DDMMYYYY                          
         MVC   2(2,R7),DUB+4                                                    
         MVC   4(4,R7),DUB+0                                                    
         B     PARS1C2                                                          
*&&                                                                             
*&&US                                                                           
         CLC   0(2,R7),=C'12'      TEST LOOKS LIKE ISO DATE YYYYMMDD            
         BNH   PARS1C2                                                          
         MVC   DUB,0(R7)                                                        
         MVC   0(2,R7),DUB+4       CONVERT TO MMDDYYYY                          
         MVC   2(2,R7),DUB+6                                                    
         MVC   4(4,R7),DUB+0                                                    
         B     PARS1C2                                                          
*&&                                                                             
PARS1C1  CLI   1(RF),6             TEST POSSIBLE DDMMYY FORMAT                  
         BNE   PARS1D                                                           
         LA    R4,3                3 PAIRS OF DIGITS                            
PARS1C2  LA    R3,DDAY             POINT TO DAY FIELD                           
PARS1C3  PACK  DUB,0(2,R7)         CONVERT DDMMYY(YY)                           
         CVB   R2,DUB                                                           
         STC   R2,0(R3)                                                         
         LA    R7,2(R7)            BUMP TO NEXT PAIR                            
         BCTR  R3,0                                                             
         BCT   R4,PARS1C3                                                       
         CLI   1(RF),6                                                          
         BNE   PARS1C4                                                          
         BAS   R8,SETCENT          SET DEFAULT CENTURY FOR DDMMYY               
         B     PARS1C6                                                          
PARS1C4  XC    DYEAR,DCENT         SWAP YEAR AND CENTURY FOR DDMMYYYY           
         XC    DCENT,DYEAR                                                      
         XC    DYEAR,DCENT                                                      
         CLI   DCENT,19            TEST MINIMUM YEAR 1964                       
         BNE   PARS1C5                                                          
         CLC   DYEAR,MINYR                                                      
         BL    ERR1                                                             
         B     PARS1C6                                                          
*                                                                               
PARS1C5  CLI   DCENT,20            TEST MAXIMUM YEAR 2059                       
         BNE   ERR1                                                             
         CLC   DYEAR,MAXYR                                                      
         BH    ERR1                                                             
PARS1C6  CLI   LANG,USA            SWAP DAY AND MONTH IF USA                    
         BNE   PARSA                                                            
         XC    DDAY,DMONTH                                                      
         XC    DMONTH,DDAY                                                      
         XC    DDAY,DMONTH                                                      
         B     PARSA               GO AND LOOK FOR ADJUSTMENT                   
*                                                                               
PARS1D   CLI   1(RF),4             FOUR DIGITS YYYY OR MMYY OR DDMM             
         BNE   ERR1                                                             
         TM    INOPT1,PVINDDMM     TEST VALIDATE FOR DDMM                       
         BO    PARS1F                                                           
         CLC   0(4,R7),=C'1900'    TEST MINIMUM 1900                            
         BL    PARS1F              MAY BE DDMM                                  
         BAS   R8,VALYEAR                                                       
         BNE   ERR1                                                             
         B     PARSA               CHECK FOR ADJUSTMENT                         
*                                                                               
PARS1F   PACK  DUB,0(2,R7)         DDMM OR MMYY                                 
         CVB   R2,DUB                                                           
         LTR   R2,R2                                                            
         BZ    ERR1                ZEROS NOT ALLOWED FOR DD OR MM               
         PACK  DUB,2(2,R7)                                                      
         CVB   R3,DUB                                                           
         CLI   LANG,USA                                                         
         BE    PARS1F3                                                          
*                                                                               
PARS1F1  TM    INOPT1,PVINDDMM     TEST VALIDATE FOR DDMM (UK)                  
         BO    PARS1F2             YES                                          
         CHI   R2,12                                                            
         BH    ERR1                                                             
         STC   R2,DMONTH           DEFAULT IS MMYY                              
         STC   R3,DYEAR                                                         
         BAS   R8,SETCENT          SET CENTURY BASED ON YEAR                    
         B     PARSA                                                            
PARS1F2  CHI   R2,31               REQUESTED DDMM                               
         BH    ERR1                                                             
         STC   R2,DDAY                                                          
         LTR   R3,R3                                                            
         BZ    ERR1                                                             
         CHI   R3,12                                                            
         BH    ERR1                                                             
         STC   R3,DMONTH                                                        
         B     PARSA                                                            
*                                                                               
PARS1F3  TM    INOPT1,PVINDDMM     TEST VALIDATE FOR MMDD (USA)                 
         BO    PARS1F4             YES                                          
         CHI   R2,12                                                            
         BH    ERR1                                                             
         STC   R2,DMONTH           DEFAULT IS MMYY                              
         STC   R3,DYEAR                                                         
         BAS   R8,SETCENT          SET CENTURY BASED ON YEAR                    
         B     PARSA                                                            
PARS1F4  CHI   R2,12               REQUESTED MMDD                               
         BH    ERR1                                                             
         STC   R2,DMONTH                                                        
         LTR   R3,R3                                                            
         BZ    ERR1                                                             
         CHI   R3,31                                                            
         BH    ERR1                                                             
         STC   R3,DDAY                                                          
         B     PARSA                                                            
*                                                                               
PARS1X   DC    H'0'                UNSPECIFIED FIELD                            
                                                                                
PARS2    CLI   DFIELDS,2           TEST TWO DATE FIELDS                         
         BNE   PARS3                                                            
*                                                                               
PARS2A   TM    DFLAGS,X'40'        TEST ALPHA MONTH PRESENT                     
         BZ    PARS2B                                                           
         CLI   LANG,USA                                                         
         BE    PARS2A3                                                          
*                                                                               
PARS2A1  CLI   DALPMN,1            UK - MUST BE MMMYY OR DDMMM                  
         BNE   PARS2A2                                                          
         MVC   DMONTH,DFLD1V       MONTH IN 1ST FIELD - MUST BE MMMYY           
         LA    RF,DFLD2D                                                        
         BAS   R8,VALYEAR                                                       
         BNE   ERR1                                                             
         B     PARSA                                                            
PARS2A2  CLI   DFLD1V,0            MONTH IN 2ND FIELD - MUST BE DDMMM           
         BE    ERR1                                                             
         CLI   DFLD1V,31                                                        
         BH    ERR1                                                             
         MVC   DDAY,DFLD1V                                                      
         MVC   DMONTH,DFLD2V                                                    
         B     PARSA                                                            
*                                                                               
PARS2A3  CLI   DALPMN,1            US - MUST BE MMMDD OR MMM/YY                 
         BNE   ERR1                                                             
         MVC   DMONTH,DFLD1V                                                    
         SR    R7,R7                                                            
         IC    R7,DFLD2D                                                        
         A     R7,IDATSTR                                                       
         BCTR  R7,0                POINT TO CHR BEFORE THE 2ND FIELD            
         CLI   0(R7),C'/'          YEAR ALWAYS PRECEEDED BY /                   
         BE    PARS2A4                                                          
PARS2A3A CLI   DFLD2L,4            INPUT IS MMMNNNN - NNNN MAY BE YEAR          
         BNE   PARS2A3D                                                         
         LA    R7,1(R7)            POINT TO START OF 4 DIGIT STRING             
         CLC   0(4,R7),=C'1964'    TEST MMM1964 THRU MMM2059                    
         BL    *+14                                                             
         CLC   0(4,R7),=C'2059'                                                 
         BNH   PARS2A4                                                          
PARS2A3B PACK  DUB,0(2,R7)         MAY BE MMMDDYY                               
         CVB   R2,DUB                                                           
         LTR   R2,R2               RANGE CHECK POSSIBLE DAY NUMBER              
         BZ    ERR1                                                             
         CHI   R2,31                                                            
         BH    ERR1                                                             
         PACK  DUB,2(2,R7)                                                      
         CVB   R3,DUB                                                           
         STC   R2,DDAY                                                          
         STC   R3,DYEAR                                                         
         BAS   R8,SETCENT          SET CENTURY BASED ON YEAR                    
         B     PARSA                                                            
PARS2A3D CLI   DFLD2L,2            INPUT IS MMMNN - NN MAY BE A YEAR            
         BNE   PARS2A3X                                                         
         CLI   DFLD2V,90           TEST IF IT IS OBVIOUSLY A YEAR               
         BNL   PARS2A4                                                          
PARS2A3X EQU   *                   NOW ASSUME THAT ITS MMMDD                    
         CLI   DFLD2V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD2V,31                                                        
         BH    ERR1                                                             
         MVC   DDAY,DFLD2V         MMMDD                                        
         B     PARSA                                                            
PARS2A4  LA    RF,DFLD2D                                                        
         BAS   R8,VALYEAR          MMM/YY                                       
         BNE   ERR1                                                             
         B     PARSA                                                            
*                                                                               
PARS2B   TM    DFLAGS,X'80'        MUST BE TWO INTEGER FIELDS                   
         BZ    PARS2X                                                           
         CLI   LANG,USA                                                         
         BE    PARS2B3                                                          
*                                                                               
PARS2B1  TM    INOPT1,PVINDDMM     TEST VALIDATE FOR DD.MM (UK)                 
         BO    PARS2B2             YES                                          
         CLI   DFLD1V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD1V,12                                                        
         BH    ERR1                                                             
         MVC   DMONTH,DFLD1V       DEFAULT IS MM.YY                             
         LA    RF,DFLD2D                                                        
         BAS   R8,VALYEAR                                                       
         BNE   ERR1                                                             
         B     PARSA                                                            
PARS2B2  CLI   DFLD1V,0            REQUESTED DD.MM                              
         BE    ERR1                                                             
         CLI   DFLD1V,31                                                        
         BH    ERR1                                                             
         MVC   DDAY,DFLD1V         REQUESTED DD.MM                              
         CLI   DFLD2V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD2V,12                                                        
         BH    ERR1                                                             
         MVC   DMONTH,DFLD2V                                                    
         B     PARSA                                                            
*                                                                               
PARS2B3  TM    INOPT1,PVINDDMM     TEST VALIDATE FOR MM.DD (USA)                
         BO    PARS2B4             YES                                          
         CLI   DFLD1V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD1V,12                                                        
         BH    ERR1                                                             
         MVC   DMONTH,DFLD1V       DEFAULT IS MM.YY                             
         LA    RF,DFLD2D                                                        
         BAS   R8,VALYEAR                                                       
         BNE   ERR1                                                             
         B     PARSA                                                            
PARS2B4  CLI   DFLD1V,0            REQUESTED MM.DD                              
         BE    ERR1                                                             
         CLI   DFLD1V,12                                                        
         BH    ERR1                                                             
         MVC   DMONTH,DFLD1V                                                    
         CLI   DFLD2V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD2V,31                                                        
         BH    ERR1                                                             
         MVC   DDAY,DFLD2V                                                      
         B     PARSA                                                            
*                                                                               
PARS2X   DC    H'0'                UNSPECIFIED FIELD                            
                                                                                
PARS3    CLI   DFIELDS,3           TEST THREE DATE FIELDS                       
         BE    *+6                                                              
         DC    H'0'                FUCK ME OL' BOOTS GUV'NER                    
         CLI   LANG,USA                                                         
         BE    PARS3B                                                           
         CLI   DALPMN,1            TEST ALPHA MONTH IN FIRST FIELD              
         BNE   PARS3A                                                           
         MVI   OLANG,USA           SET TO OUTPUT USA DATES MMMDD/YY             
         B     PARS3B                                                           
*                                                                               
PARS3A   CLI   DFLD1V,0            DD.MM.YY FOR UK                              
         BE    ERR1                                                             
         CLI   DFLD1V,31                                                        
         BH    ERR1                                                             
         MVC   DDAY,DFLD1V                                                      
         CLI   DFLD2V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD2V,12                                                        
         BH    ERR1                                                             
         MVC   DMONTH,DFLD2V                                                    
         LA    RF,DFLD3D                                                        
         BAS   R8,VALYEAR                                                       
         BNE   ERR1                                                             
         B     PARSA                                                            
*                                                                               
PARS3B   CLI   DFLD1V,0            MM.DD.YY FOR US                              
         BE    ERR1                                                             
         CLI   DFLD1V,12                                                        
         BH    ERR1                                                             
         MVC   DMONTH,DFLD1V                                                    
         CLI   DFLD2V,0                                                         
         BE    ERR1                                                             
         CLI   DFLD2V,31                                                        
         BH    ERR1                                                             
         MVC   DDAY,DFLD2V                                                      
         LA    RF,DFLD3D                                                        
         BAS   R8,VALYEAR                                                       
         BNE   ERR1                                                             
         B     PARSA                                                            
                                                                                
PARSA    CLI   DADJD,X'FF'         ANY ADJUSTMENT FOUND                         
         BE    PARSB               NO                                           
         SR    R7,R7               YES POINT TO ADJUSTMENT FIELD                
         IC    R7,DADJD                                                         
         A     R7,IDATSTR                                                       
*                                                                               
PARSA1   CLI   0(R7),C'('          ADJUSTMENT MUST BE IN PARENS                 
         BNE   ERR1                                                             
         TM    PROGSW,SWSCND       IF THIS IS END                               
         BZ    *+8                                                              
         OI    PROGSW,SWENDAD      SET WE HAVE END ADJUSTMENT                   
         LA    R7,1(R7)            POINT PAST BRACKET                           
         NI    PROGSW,255-SWNADJ   DEFAULT TO POSITIVE ADJUSTMENT               
         CLI   0(R7),C'+'          TEST IF SIGN SPECIFIED                       
         BE    PARSA1A                                                          
         CLI   0(R7),C'-'                                                       
         BNE   PARSA2                                                           
         OI    PROGSW,SWNADJ       SET NEGATIVE ADJUSTMENT                      
PARSA1A  LA    R7,1(R7)            BUMP PAST SIGN                               
*                                                                               
PARSA2   LR    RF,R7               GET ADJUSTMENT VALUE                         
         LA    R1,4                MAXIMUM THREE DIGITS                         
PARSA3   CLI   0(RF),C'0'                                                       
         BL    PARSA4                                                           
         CLI   0(RF),C'9'                                                       
         BH    PARSA4                                                           
         LA    RF,1(RF)                                                         
         BCT   R1,PARSA3                                                        
         B     ERR1                                                             
PARSA4   SR    RF,R7               RF=NUMBER OF DIGITS                          
         BNP   ERR1                                                             
         LR    R2,RF                                                            
         BCTR  R2,0                                                             
         EX    R2,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,R7)                                                      
         CVB   R3,DUB                                                           
         TM    PROGSW,SWNADJ       TEST NEGATIVE ADJUSTMENT                     
         BZ    PARSA5                                                           
         LNR   R3,R3                                                            
*                                                                               
PARSA5   STH   R3,DADJWK           SAVE ADJUSTMENT VALUE                        
         LA    R7,1(R2,R7)                                                      
         BAS   R8,FINDADJ          LOOK FOR D,W,M                               
         CLI   0(R7),C')'                                                       
         BNE   ERR1                ADJUSTMENT TERMINATED WITH )                 
*                                                                               
PARSB    TM    PROGSW,SWSCND       TEST IF VALIDATING SECOND DATE               
         BO    XPND                YES                                          
         OI    PROGSW,SWSCND       NO SET TO VALIDATE SECOND DATE               
         LA    R7,EDATEFLD                                                      
         LA    R6,EADJWK                                                        
         ST    R7,IDATSTR          SET A(DATE FIELD)                            
         MVC   IDATLEN,ELENGTH     SET L'DATE FIELD                             
         B     PARS0                                                            
         EJECT                                                                  
***********************************************************************         
* EXPAND EXTRACTED DATE(S) INTO VARIOUS FORMATS                       *         
***********************************************************************         
XPND     CLI   SFIELDS,1           TEST IF TWO SINGLE FIELD DATES               
         BNE   XPND0                                                            
         CLI   EFIELDS,1                                                        
         BNE   XPND0                                                            
         CLC   SFLAGS,EFLAGS       START AND END DATES MUST BE SIMILAR          
         BNE   ERR2                                                             
*                                                                               
XPND0    CLI   SFIELDS,1           TEST START DATE IS SINGLE NUM FLD            
         BNE   XPND1                                                            
         TM    SFLD1T,X'80'                                                     
         BZ    XPND1                                                            
         CLI   SFLD1L,2                                                         
         BH    XPND1                                                            
         CLI   EDAY,X'FF'          IS DAY SPECIFIED IN THE END FIELD            
         BE    XPND0A              NO                                           
         CLI   SYEAR,0             YES TREAT YEAR AS A DAY FIELD ALSO           
         BE    ERR1                                                             
         CLI   SYEAR,31                                                         
         BH    ERR1                                                             
         MVC   SDAY,SYEAR          ASSUME START FLD IS ALSO DAY                 
         MVI   SCENT,X'FF'                                                      
         MVI   SYEAR,X'FF'                                                      
         B     XPND2                                                            
XPND0A   CLI   EMONTH,X'FF'        IS MONTH SPECIFIED IN THE END FIELD          
         BE    XPND2               NO                                           
         TM    EFLAGS,X'40'        IS END MONTH AN ALPHA MONTH                  
         BO    ERR1                                                             
         CLI   SYEAR,0             TREAT YEAR AS A MONTH FIELD ALSO             
         BE    ERR1                                                             
         CLI   SYEAR,12                                                         
         BH    ERR1                                                             
         MVC   SMONTH,SYEAR        ASSUME START FLD IS ALSO MONTH               
         MVI   SCENT,X'FF'                                                      
         MVI   SYEAR,X'FF'                                                      
         B     XPND2                                                            
*                                                                               
XPND1    CLI   EFIELDS,1           TEST END DATE IS SINGLE NUMERIC FLD          
         BNE   XPND2                                                            
         TM    EFLD1T,X'80'                                                     
         BZ    XPND2                                                            
         CLI   EFLD1L,2                                                         
         BH    XPND2                                                            
         CLI   SFIELDS,1           AMBIGUOUS IF START DATE IS MULTI FLD         
         BH    ERR2                                                             
*                                                                               
XPND2    LA    R2,EYEAR            CHECK END DATE FIRST                         
         SR    R3,R3               BUILD ACTSW HERE                             
         LA    R5,2                PASSES THROUGH LOOP                          
XPND3    LA    R4,3                SIZE OF LOOP                                 
*                                                                               
XPND4    CLI   0(R2),X'FF'         SET BIT ON IF FIELD PRESENT                  
         BE    XPND5                                                            
         LA    R3,1(R3)                                                         
*                                                                               
XPND5    SLL   R3,1                BUMP TO NEXT FIELD                           
         LA    R2,1(R2)                                                         
         BCT   R4,XPND4            IF THERE IS ONE                              
         BCT   R5,XPND6                                                         
         SRL   R3,1                ALL DONE - RESET FINAL SHUNT                 
         B     XPND7                                                            
*                                                                               
XPND6    LA    R2,SYEAR            CHECK OUT START DATE                         
         SLL   R3,1                HOB OF NIBBLE ALWAYS 0                       
         B     XPND3                                                            
*                                                                               
XPND7    TM    PROGSW,SWAFSET      TEST/SET FIRST TIME THROUGH                  
         BO    XPND7A                                                           
         OI    PROGSW,SWAFSET                                                   
         STC   R3,PVALASSM         RETURN ASSUMPTION FLAG                       
         XI    PVALASSM,X'77'      FLIP BITS                                    
*                                                                               
XPND7A   SR    R2,R2               USED FOR COMPARISON                          
         LA    R4,ACTTABLN         R4=NUM OF ENTRIES IN TABLE                   
         LA    R5,ACTTAB           R5=A(ACTION TABLE ENTRY)                     
*                                                                               
XPND8    IC    R2,0(R5)            TEST BIT PATTERN MATCHES                     
         CR    R2,R3                                                            
         BE    XPND9                                                            
         LA    R5,4(R5)            BUMP TO NEXT ENTRY                           
         BCT   R4,XPND8                                                         
         DC    H'0'                                                             
*                                                                               
XPND9    ICM   R2,7,1(R5)          LOAD ADDRESS OF ACTION                       
         A     R2,RELO                                                          
         BR    R2                  GO DO IT                                     
                                                                                
***********************************************************************         
* EXPAND ACTIONS & MAKE ASSUMPTIONS ABOUT MISSING PARTS OF DATES      *         
***********************************************************************         
ACT01    MVC   EMONTH,SMONTH       EMONTH=SMONTH                                
         B     XPND2                                                            
*                                                                               
ACT02    MVC   SMONTH,EMONTH       SMONTH=EMONTH                                
         B     XPND2                                                            
*                                                                               
ACT03    MVC   SCENT(2),UCENT      SYEAR=UYEAR                                  
         B     XPND2                                                            
*                                                                               
ACT04    CLC   SMONTH,EMONTH       IF SMONTH>EMONTH                             
         BH    ACT27               THEN SYEAR=EYEAR-1                           
         B     ACT08               ELSE SYEAR=EYEAR                             
*                                                                               
ACT05    CLC   EMONTH,SMONTH       IF EMONTH<SMONTH                             
         BL    ACT26               THEN EYEAR=SYEAR+1                           
         B     ACT07               ELSE EYEAR=SYEAR                             
*                                                                               
ACT06    SR    R2,R2               SMONTH=EMONTH+1                              
         IC    R2,EMONTH                                                        
         LA    R2,1(R2)                                                         
         CHI   R2,12                                                            
         BNH   ACT06A                                                           
         LA    R2,1                                                             
ACT06A   STC   R2,SMONTH                                                        
         B     XPND2                                                            
*                                                                               
ACT07    MVC   ECENT(2),SCENT      EYEAR=SYEAR                                  
         B     XPND2                                                            
*                                                                               
ACT08    MVC   SCENT(2),ECENT      SYEAR=EYEAR                                  
         B     XPND2                                                            
*                                                                               
ACT09    MVI   SMONTH,X'01'        SMONTH=JAN                                   
         B     XPND2                                                            
*                                                                               
ACT10    SR    R2,R2               EMONTH=SMONTH-1                              
         IC    R2,SMONTH                                                        
         BCT   R2,ACT10A                                                        
         LA    R2,12                                                            
ACT10A   STC   R2,EMONTH                                                        
         B     XPND2                                                            
*                                                                               
ACT11    CLI   SMONTH,X'01'        IF START MONTH JANUARY                       
         BE    ACT08               THEN SYEAR=EYEAR                             
         B     ACT27               ELSE SYEAR=EYEAR-1                           
*                                                                               
ACT12    MVI   SDAY,X'01'          START DAY IS FIRST                           
         B     XPND2                                                            
*                                                                               
ACT13    SR    R2,R2               EDAY=LAST IN MONTH                           
         IC    R2,EYEAR                                                         
         SR    R3,R3                                                            
         IC    R3,EMONTH                                                        
         BAS   RE,LEAP                                                          
         STC   R1,EDAY                                                          
         B     XPND2                                                            
*                                                                               
ACT14    MVC   SMONTH,UMONTH       SMONTH=UMONTH                                
         B     XPND2                                                            
*                                                                               
ACT15    SR    R2,R2               SMONTH=EMONTH-1                              
         IC    R2,EMONTH                                                        
         BCT   R2,ACT15A                                                        
         LA    R2,12                                                            
ACT15A   STC   R2,SMONTH                                                        
         B     XPND2                                                            
*                                                                               
ACT16    MVC   EMONTH,UMONTH       EMONTH=UMONTH                                
         B     XPND2                                                            
*                                                                               
ACT17    CLI   EMONTH,12           IF EMONTH=DEC                                
         BE    ACT07               THEN EYEAR=SYEAR                             
         B     ACT26               ELSE EYEAR=SYEAR+1                           
*                                                                               
ACT18    CLC   SCENT(2),ECENT      IF SYEAR=EYEAR                               
         BE    ACT09               THEN SMONTH=JAN                              
         B     ACT06               ELSE SMONTH=EMONTH+1                         
*                                                                               
ACT19    MVC   ECENT(2),UCENT      EYEAR=UYEAR                                  
         B     XPND2                                                            
*                                                                               
ACT20    MVC   SDAY,EDAY           SDAY=EDAY                                    
         CLI   EADJTP,X'FF'        ANY ADJUSTMENT FOR END                       
         BE    XPND2               NO                                           
         CLI   SADJTP,X'FF'        ANY ADJUSTMENT FOR START                     
         BNE   XPND2               YES                                          
         MVC   SADJWK(3),EADJWK    USE END ADJUSTMENT                           
         B     XPND2                                                            
*                                                                               
ACT21    CLC   SDAY,EDAY           IF SDAY>EDAY                                 
         BH    ACT15               THEN SMONTH=EMONTH-1                         
         B     ACT02               ELSE SMONTH=EMONTH                           
*                                                                               
ACT22    CLC   EMONTH,SMONTH       IF EMONTH<SMONTH                             
         BL    ACT26               THEN EYEAR=SYEAR+1                           
         BH    ACT07               IF EMONTH>SMONTH THEN EYEAR=SYEAR            
         CLC   EDAY,SDAY           IF EMONTH=SMONTH & EDAY<SDAY                 
         BL    ACT26               THEN EYEAR=SYEAR+1                           
         B     ACT07               ELSE EYEAR=SYEAR                             
*                                                                               
ACT23    CLC   EDAY,SDAY           IF EDAY<SDAY                                 
         BL    ACT28               THEN EMONTH=SMONTH+1                         
         B     ACT01               ELSE EMONTH=SMONTH                           
*                                                                               
ACT24    MVC   SDAY,UDAY           SDAY=UDAY                                    
         B     XPND2                                                            
*                                                                               
ACT25    MVC   EDAY,SDAY           EDAY=SDAY                                    
         CLI   SADJTP,X'FF'        ANY ADJUSTMENT FOR START                     
         BE    XPND2               NO                                           
         CLI   EADJTP,X'FF'        ANY ADJUSTMENT FOR END                       
         BNE   XPND2               YES                                          
         MVC   EADJWK(3),SADJWK    USE START ADJUSTMENT                         
         B     XPND2                                                            
*                                                                               
ACT26    SR    R2,R2               EYEAR=SYEAR+1                                
         IC    R2,SYEAR                                                         
         MVC   ECENT,SCENT                                                      
         LA    R2,1(R2)                                                         
         CHI   R2,100                                                           
         BL    ACT26A                                                           
         SHI   R2,100                                                           
         MVI   ECENT,20                                                         
ACT26A   STC   R2,EYEAR                                                         
         B     XPND2                                                            
*                                                                               
ACT27    SR    R2,R2               SYEAR=EYEAR-1                                
         IC    R2,EYEAR                                                         
         MVC   SCENT,ECENT                                                      
         SHI   R2,1                                                             
         BNM   ACT27A                                                           
         AHI   R2,100                                                           
         MVI   SCENT,19                                                         
ACT27A   STC   R2,SYEAR                                                         
         B     XPND2                                                            
*                                                                               
ACT28    SR    R2,R2               EMONTH=SMONTH+1                              
         IC    R2,SMONTH                                                        
         LA    R2,1(R2)                                                         
         CHI   R2,12                                                            
         BNH   ACT28A                                                           
         SHI   R2,12                                                            
ACT28A   STC   R2,EMONTH                                                        
         B     XPND2                                                            
*                                                                               
ACT29    CLC   SMONTH,EMONTH       IF SMONTH>EMONTH                             
         BH    ACT27               THEN SYEAR=EYEAR-1                           
         BL    ACT08               IF SMONTH<EMONTH THEN SYEAR=EYEAR            
         CLC   SDAY,EDAY           IF SMONTH=EMONTH & SDAY>EDAY                 
         BH    ACT27               THEN SYEAR=EYEAR-1                           
         B     ACT08               ELSE SYEAR=EYEAR                             
*                                                                               
ACT30    MVI   EMONTH,12           EMONTH=DEC                                   
         B     XPND2                                                            
*                                                                               
ACT31    CLC   SCENT(2),ECENT      IF SYEAR=EYEAR                               
         BE    ACT30               THEN EMONTH=DEC                              
         B     ACT10               ELSE EMONTH=SMONTH-1                         
*                                                                               
ACT32    NI    PROGSW,255-SWSCND   INDICATE ERROR 1                             
         B     ERR1                REJECT IT                                    
*                                                                               
ACT33    OI    PROGSW,SWSCND       INDICATE ERROR 2                             
         B     ERR1                REJECT IT                                    
         EJECT                                                                  
***********************************************************************         
* ADJUST IF NECESSARY AND CHECK START AND END DATES VALID             *         
* IF INVALID ON FIRST PASS OF VALIDATION THEN SET UP FOR SECOND PASS  *         
***********************************************************************         
CHKOK    NI    PROGSW,255-SWSCND   SET VALIDATING 1ST DATE                      
         LA    R6,SADJWK                                                        
*                                                                               
CHK2     SR    R2,R2               R2=YEAR                                      
         IC    R2,DYEAR                                                         
         SR    R3,R3               R3=MONTH                                     
         ICM   R3,1,DMONTH                                                      
         BZ    ERR1                                                             
         CHI   R3,12                                                            
         BH    ERR1                                                             
         SR    R4,R4               R4=DAY                                       
         ICM   R4,1,DDAY                                                        
         BZ    ERR1                                                             
         BAS   RE,LEAP                                                          
         CR    R4,R1                                                            
         BNH   CHK2A                                                            
         CLC   DMONTH(2),=X'021D'  TEST IF 29TH FEB                             
         BNE   ERR1                NO DEFINATE ERROR                            
         CLI   UPASS,1             YES ALLOWED ON 1ST PASS ONLY                 
         BNE   ERR1                                                             
         B     CHK7                                                             
*                                                                               
CHK2A    BAS   R8,ADJUSTER         FINAL ADJUSTMENT                             
         CLC   DCENT(2),=X'1300'   MIN YEAR 1900                                
         BL    ERR1                                                             
         CLC   DCENT(2),=X'143B'   MAX YEAR 2059                                
         BH    ERR1                                                             
*                                                                               
         TM    PROGSW,SWSCND       FINISHED IF SECOND DATE                      
         BO    CHK3                                                             
         TM    RETCODE,PVRCONE     TEST IF ONLY ONE DATE ENTERED                
         BO    CHK3                YES                                          
         OI    PROGSW,SWSCND       SET UP TO VALIDATE END DATE                  
         LA    R6,EADJWK                                                        
         B     CHK2                                                             
*                                                                               
CHK3     TM    INOPT2,PVIN1DYL     TEST RETURN 1 DAY LESS                       
         BZ    CHK4                                                             
         TM    PROGSW,SWENDAD      ONLY IF END DATE WAS ADJUSTED                
         BZ    CHK4                                                             
         MVI   DADJTP,C'D'         SET TO SUBTRACT ONE DAY FROM END             
         BAS   R8,ADJUSTER                                                      
         CLC   DCENT(2),=X'1340'   MIN YEAR 1964                                
         BL    ERR2                                                             
         CLC   DCENT(2),=X'143B'   MAX YEAR 2059                                
         BH    ERR2                                                             
*                                                                               
CHK4     CLC   SCENT(4),ECENT      START DATE MUST BE LE END DATE               
         BL    CHK6                                                             
         BH    ERR2                                                             
*                                                                               
CHK5     TM    PVALASSM,X'07'      DATES EQUAL - CHECK ONLY ONE INPUT           
         BO    *+12                                                             
         TM    PVALASSM,X'70'      SINGLE DATE ONLY                             
         BNO   CHK6                                                             
         OI    RETCODE,PVRCONE     SET ONLY ONE DATE INPUT                      
*                                                                               
CHK6     CLI   UPASS,0             TEST IF VALIDATION PERIOD PASSED             
         BE    CHKX                                                             
         CLC   SCENT(4),SPER       TEST START DATE (BINARY)                     
         BL    CHK7                                                             
         CLC   SCENT(4),EPER                                                    
         BH    CHK7                                                             
         TM    INOPT2,PVINSGLO     TEST SINGLE DATE                             
         BO    CHKX                                                             
         TM    PROGSW,SWENDIN                                                   
         BZ    CHKX                                                             
         CLC   ECENT(4),EPER       TEST END DATE                                
         BH    CHK7                                                             
         B     CHKX                                                             
*                                                                               
CHK7     TM    PVALASSM,X'44'      PERIOD FAILURE - TEST YEAR INPUT             
         BNO   CHK7X                                                            
         CLI   UPASS,1             NO TEST 1ST OR 2ND PASS FOR PERIOD           
         BNE   CHK7X                                                            
         CLC   SPER(2),EPER        IF ONLY ONE YEAR ONLY ONE PASS               
         BNL   CHK7X                                                            
         MVI   UPASS,2             SET SECOND PASS                              
         MVC   UCENT(4),EPER       SET USE DATE TO END OF PERIOD                
         B     INIT20                                                           
CHK7X    OI    PROGSW,SWINVP       SET DATE(S) NOT IN PERIOD                    
*                                                                               
CHKX     EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* RETURN DATES IN VARIOUS FORMATS AND WORK OUT PERIOD DATA            *         
***********************************************************************         
RETOK    TM    RETCODE,PVRCONE     TEST ONLY ONE DATE INPUT                     
         BZ    RETOK01                                                          
         XC    PVALBSTA(16),PVALBSTA                                            
         B     RETOK02             PROCESS START DATE INFO                      
*                                                                               
RETOK01  SR    R3,R3               CONVERT END YEAR                             
         IC    R3,EYEAR                                                         
         CVD   R3,DUB                                                           
         ZAP   PWK,DUB             SAVE FOR PWOS                                
         MP    PWK,=P'100'                                                      
         UNPK  PVALCPER+15(2),DUB                                               
         OI    PVALCPER+16,X'F0'                                                
         MVC   PVALEEND,PVALCPER+15                                             
         CLI   ECENT,20            SET FUNNY YEAR                               
         BNE   *+16                                                             
         IC    R3,PVALEEND                                                      
         LA    R3,10(R3)                                                        
         STC   R3,PVALEEND                                                      
*                                                                               
RETOK01A IC    R3,EMONTH           CONVERT END MONTH                            
         CVD   R3,DUB                                                           
         AP    PWK,DUB             SAVE FOR PWOS                                
         MP    PWK,=P'100'                                                      
         UNPK  PVALCPER+12(2),DUB                                               
         OI    PVALCPER+13,X'F0'                                                
         MVC   PVALEEND+2(2),PVALCPER+12                                        
         CLI   OLANG,USA           IF NOT UK/US DONT USE ALPHA MONTH            
         BH    RETOK01B                                                         
         BAS   R8,NAMER            FIND MONTH IN TABLE                          
         MVC   PVALCPER+12(3),0(R4)                                             
*                                                                               
RETOK01B IC    R3,EDAY             CONVERT END DAY                              
         CVD   R3,DUB                                                           
         AP    PWK,DUB             SAVE FOR PWOS                                
         UNPK  PVALCPER+9(2),DUB                                                
         OI    PVALCPER+10,X'F0'                                                
         MVC   PVALEEND+4(2),PVALCPER+9                                         
         MVI   PVALCPER+8,C'-'                                                  
         MVO   DUB(4),PWK+1(4)     REMOVE SIGN                                  
         MVC   PVALPEND,DUB        STORE PWOS END                               
         CLI   ECENT,20                                                         
         BNE   *+16                                                             
         IC    R2,PVALPEND                                                      
         LA    R2,160(R2)                                                       
         STC   R2,PVALPEND                                                      
*                                                                               
         MVC   PVALBEND,EYEAR      STORE BIN END                                
         CLI   ECENT,20                                                         
         BNE   *+16                                                             
         IC    R2,PVALBEND                                                      
         LA    R2,100(R2)                                                       
         STC   R2,PVALBEND                                                      
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,7,PVALBEND       CONVERT TO TWO BYTE CMPRSD NEW               
         SRDL  R2,5                                                             
         SRL   R2,3                                                             
         SRDL  R2,4                                                             
         SRL   R2,4                                                             
         AHI   R2,-64              BASE YEAR IS 1964                            
         SLDL  R2,9                                                             
         STCM  R2,3,PVALNEND       STORE NEW CMPRSD END DATE                    
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,7,PVALBEND       CONVERT TO TWO BYTE CMPRSD OLD               
         SRDL  R2,5                                                             
         SRL   R2,3                                                             
         SRDL  R2,4                                                             
         SRL   R2,4                                                             
         CHI   R2,127              MAX YEAR FOR CMPRSD OLD IS 2027              
         BH    RETOK01C                                                         
         SLDL  R2,9                                                             
         STCM  R2,3,PVALCEND       STORE CMPRSD OLD END DATE                    
         B     RETOK02                                                          
RETOK01C XC    PVALCEND,PVALCEND   CLEAR CMPRSD OLD END DATE                    
*                                                                               
RETOK02  SR    R3,R3               CONVERT START YEAR                           
         IC    R3,SYEAR                                                         
         CVD   R3,DUB                                                           
         ZAP   PWK,DUB             SAVE FOR PWOS                                
         MP    PWK,=P'100'                                                      
         UNPK  PVALCPER+6(2),DUB                                                
         OI    PVALCPER+7,X'F0'                                                 
         MVC   PVALESTA(2),PVALCPER+6                                           
         CLI   SCENT,20            SET FUNNY YEAR                               
         BNE   *+16                                                             
         IC    R3,PVALESTA                                                      
         LA    R3,10(R3)                                                        
         STC   R3,PVALESTA                                                      
*                                                                               
RETOK02A IC    R3,SMONTH           CONVERT START MONTH                          
         CVD   R3,DUB                                                           
         AP    PWK,DUB             SAVE FOR PWOS                                
         MP    PWK,=P'100'                                                      
         UNPK  PVALCPER+3(2),DUB                                                
         OI    PVALCPER+4,X'F0'                                                 
         MVC   PVALESTA+2(2),PVALCPER+3                                         
         CLI   OLANG,USA           IF NOT UK/US DONT USE MONTH NAME             
         BH    RETOK02B                                                         
         BAS   R8,NAMER            GET MONTH NAME FROM TABLE                    
         MVC   PVALCPER+3(3),0(R4)                                              
*                                                                               
RETOK02B IC    R3,SDAY             CONVERT START DAY                            
         CVD   R3,DUB                                                           
         AP    PWK,DUB             SAVE FOR PWOS                                
         UNPK  PVALCPER(2),DUB                                                  
         OI    PVALCPER+1,X'F0'                                                 
         MVC   PVALESTA+4(2),PVALCPER                                           
         MVO   DUB(4),PWK+1(4)     REMOVE SIGN                                  
*                                                                               
         MVC   PVALPSTA,DUB        STORE PWOS START                             
         CLI   SCENT,20                                                         
         BNE   *+16                                                             
         IC    R2,PVALPSTA                                                      
         LA    R2,160(R2)                                                       
         STC   R2,PVALPSTA                                                      
*                                                                               
         MVC   PVALBSTA,SYEAR      STORE BINARY YMD START                       
         CLI   SCENT,20                                                         
         BNE   *+16                                                             
         IC    R2,PVALBSTA                                                      
         LA    R2,100(R2)                                                       
         STC   R2,PVALBSTA                                                      
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,7,PVALBSTA       CONVERT TO TWO BYTE CMPRSD NEW               
         SRDL  R2,5                                                             
         SRL   R2,3                                                             
         SRDL  R2,4                                                             
         SRL   R2,4                                                             
         AHI   R2,-64              BASE YEAR IS 1964                            
         SLDL  R2,9                                                             
         STCM  R2,3,PVALNSTA                                                    
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,7,PVALBSTA       CONVERT TO TWO BYTE CMPRSD OLD               
         SRDL  R2,5                                                             
         SRL   R2,3                                                             
         SRDL  R2,4                                                             
         SRL   R2,4                                                             
         CHI   R2,127              MAX YEAR FOR CMPRSD OLD IS 2027              
         BH    RETOK02C                                                         
         SLDL  R2,9                                                             
         STCM  R2,3,PVALCSTA       STORE CMPRSD OLD DATE                        
         B     RETOK03                                                          
RETOK02C XC    PVALCSTA,PVALCSTA   CLEAR CMPRSD OLD DATE                        
*                                                                               
RETOK03  CLI   OLANG,USA           UK STYLE DATES DDMMMYY-DDMMMYY               
         BH    RETOK03A                                                         
         MVC   PVALCPER+2(14),PVALCPER+3  MANUAL SQUASH                         
         MVI   PVALCPER+16,C' '                                                 
         MVC   PVALCPER+10(6),PVALCPER+11                                       
         B     RETOK04                                                          
RETOK03A MVI   PVALCPER+2,C'.'     CONTINENTAL DATES DD.MM.YY-DD.MM.YY          
         MVI   PVALCPER+5,C'.'                                                  
         CLC   PVALCPER+9(8),SPACES                                             
         BE    RETOK05                                                          
         MVI   PVALCPER+11,C'.'                                                 
         MVI   PVALCPER+14,C'.'                                                 
         B     RETOK05                                                          
*                                                                               
RETOK04  CLI   OLANG,USA           US STYLE DATES MMMDD/YY-MMMDD/YY             
         BNE   RETOK05                                                          
         MVC   DATEWK(15),PVALCPER                                              
         MVC   PVALCPER(3),DATEWK+2                                             
         MVC   PVALCPER+3(2),DATEWK                                             
         MVI   PVALCPER+5,C'/'                                                  
         MVC   PVALCPER+6(2),DATEWK+5                                           
         TM    RETCODE,PVRCONE     TEST IF ONLY ONE DATE INPUT                  
         BO    RETOK05                                                          
         MVI   PVALCPER+8,C'-'                                                  
         MVC   PVALCPER+9(3),DATEWK+10                                          
         MVC   PVALCPER+12(2),DATEWK+8                                          
         MVI   PVALCPER+14,C'/'                                                 
         MVC   PVALCPER+15(2),DATEWK+13                                         
*                                                                               
RETOK05  EQU   *                                                                
*                                                                               
RETOK10  TM    INOPT2,PVINSGLO     CALC COUNTS IF TWO DATES                     
         BNO   RETOK10A                                                         
         MVI   PVALNDYS+1,1        SKIP D/W/M CALCS                             
         MVI   PVALNWKS+1,1                                                     
         MVI   PVALNMNS+1,1                                                     
         B     RETOK20                                                          
RETOK10A SR    R2,R2               CURRENT START YEAR                           
         IC    R2,SYEAR                                                         
         SR    R3,R3               CURRENT START MONTH                          
         IC    R3,SMONTH                                                        
         SR    R4,R4               COUNT OF DAYS                                
         SR    R5,R5               WORK REG                                     
         SR    R7,R7               COUNT OF MONTHS                              
         CLC   SDAY,EDAY           START DAY GT END DAY                         
         BNH   RETOK11             NO                                           
         BAS   RE,LEAP                                                          
         LR    R4,R1               DAYS IN START MONTH                          
         LA    R3,1(R3)            INCR START MONTH                             
         CHI   R3,13                                                            
         BNE   RETOK11                                                          
         LA    R3,1                START MONTH = 1                              
         LA    R2,1(R2)            INCR START YEAR                              
         CHI   R2,100                                                           
         BL    *+12                                                             
         SHI   R2,100                                                           
         MVI   SCENT,20                                                         
*                                                                               
RETOK11  STC   R2,SYEAR                                                         
         STC   R3,SMONTH                                                        
         IC    R5,EDAY             ADD END DAY                                  
         AR    R4,R5                                                            
         IC    R5,SDAY             SUB START DAY                                
         SR    R4,R5                                                            
         LA    R4,1(R4)            ADD 1 FOR INCLUSIVE                          
*                                                                               
RETOK12  CLC   SMONTH,EMONTH       START MONTH EQ END MONTH                     
         BNE   RETOK13             NO                                           
         CLC   SCENT(2),ECENT      START YEAR EQ END YEAR                       
         BE    RETOK15             YES - CONTINUE                               
*                                                                               
RETOK13  IC    R2,SYEAR                                                         
         IC    R3,SMONTH                                                        
         BAS   RE,LEAP                                                          
         AR    R4,R1               ADD DAYS IN START MONTH                      
         LA    R3,1(R3)            START MONTH + 1                              
         LA    R7,1(R7)            INCR NUM OF MONTHS                           
         CHI   R3,13                                                            
         BNE   RETOK14                                                          
         LA    R3,1                START MONTH = 1                              
         LA    R2,1(R2)            START YEAR + 1                               
         CHI   R2,100                                                           
         BL    *+12                                                             
         SHI   R2,100                                                           
         MVI   SCENT,20                                                         
*                                                                               
RETOK14  STC   R2,SYEAR                                                         
         STC   R3,SMONTH                                                        
         B     RETOK12                                                          
*                                                                               
RETOK15  LR    R5,R4                                                            
         SR    R4,R4                                                            
         STH   R5,PVALNDYS         SAVE NUM OF DAYS                             
         D     R4,=F'7'                                                         
         LTR   R4,R4                                                            
         BZ    RETOK16                                                          
         LA    R5,1(R5)            ROUND UP WEEKS IF REMAINDER                  
*                                                                               
RETOK16  STH   R5,PVALNWKS         SAVE NUM OF WEEKS                            
         LA    R7,1(R7)            ROUND UP MONTHS                              
         CLC   SDAY,EDAY           IF START DAY > END DAY                       
         BNH   *+8                                                              
         LA    R7,1(R7)            ROUND UP AGAIN FOR CALENDAR MONTHS           
         STH   R7,PVALNMNS         SAVE NUM OF MONTHS                           
*                                                                               
RETOK20  TM    PROGSW,SWNOINP      SET SPECIAL RETURN CODES                     
         BZ    *+12                                                             
         MVI   RETCODE,PVRCMISS    TELL CALLER NO INPUT FOUND                   
         B     RETOK20A                                                         
         TM    PROGSW,SWINVP                                                    
         BZ    *+8                                                              
         OI    RETCODE,PVRINVP     TELL CALLER DATE(S) OUTSIDE PERIOD           
RETOK20A TM    RETCODE,PVRCONE+PVRCMISS                                         
         BZ    RETOKX              IF NOT SINGLE DATE                           
         TM    INOPT2,PVINSGLO+PVINSGLS                                         
         BNZ   RETOKX              SINGLE DATE REQUIRED                         
         MVC   PVALBEND,PVALBSTA                                                
         MVC   PVALCEND,PVALCSTA                                                
         MVC   PVALPEND,PVALPSTA                                                
         MVC   PVALEEND,PVALESTA                                                
         MVC   PVALNEND,PVALNSTA                                                
         CLI   OLANG,UK                                                         
         BH    RETOK20B                                                         
         MVC   PVALCPER+8(7),PVALCPER                                           
         MVI   PVALCPER+7,C'-'                                                  
         B     RETOKX                                                           
RETOK20B MVC   PVALCPER+9(8),PVALCPER                                           
         MVI   PVALCPER+8,C'-'                                                  
*                                                                               
RETOKX   L     R1,R1SAVE           SET RETURN CODE IN P2                        
         MVC   4(1,R1),RETCODE                                                  
*                                                                               
PERVALX  CLI   4(R1),0                                                          
         XMOD1 1                                                                
ERRPARM  L     R1,R1SAVE           ERROR IN INPUT PARMS                         
         MVI   4(R1),PVRCINV1                                                   
         B     ERRGEN                                                           
*                                                                               
ERR1     TM    PROGSW,SWSCND       TEST START OR END BEING VALIDATED            
         BO    ERR2                                                             
         L     R1,R1SAVE                                                        
         MVI   4(R1),PVRCINV1                                                   
         B     ERRGEN                                                           
*                                                                               
ERR2     L     R1,R1SAVE           SET END DATE INVALID                         
         MVI   4(R1),PVRCINV2                                                   
         B     ERRGEN                                                           
*                                                                               
ERRGEN   XC    PVALOUTB,PVALOUTB   SET OUTPUT BLOCK TO NULLS AND SPACES         
         MVC   PVALCPER,SPACES                                                  
         MVC   PVALESTA(12),SPACES                                              
         B     PERVALX                                                          
         EJECT                                                                  
***********************************************************************         
* SET TYPE AND VALUE OF EACH INPUT FIELD                              *         
***********************************************************************         
SETFTV   NTR1                                                                   
         SR    R0,R0               R0=NUMBER OF INPUT FIELDS                    
         ICM   R0,3,FCOUNT                                                      
         BZ    SETFTVX                                                          
         LA    RF,FFLD1D           RF=A(FIELD DEFN DIS/LEN/TYP/VAL)             
*                                                                               
SETFTV1  SR    RE,RE               POINT TO START OF INPUT FIELD                
         IC    RE,0(RF)                                                         
         A     RE,IDATSTR          RE=A(START OF FIELD)                         
         SR    R1,R1                                                            
         ICM   R1,1,1(RF)                                                       
         BZ    SETFTVE             R1=LENGTH OF FIELD                           
         CLI   0(RE),C'0'                                                       
         BL    SETFTV4                                                          
*                                                                               
SETFTV2  OI    FFLAGS,X'80'        SET NUMERIC FIELD                            
         OI    2(RF),X'80'                                                      
         CHI   R1,2                TEST IF 1 THRU 2 CHRS NUMERIC                
         BNH   SETFTV3                                                          
         TM    1(RF),X'01'         MUST BE EVEN NUMBER OF DIGITS                
         BO    SETFTVE                                                          
         B     SETFTV9                                                          
SETFTV3  BCTR  R1,0                CONVERT 1/2 DIGIT FIELD TO DECIMAL           
         EX    R1,*+8                                                           
         B     *+10                                                             
         PACK  DUB,0(0,RE)                                                      
         CVB   R1,DUB                                                           
         STC   R1,3(RF)            SET BINARY VALUE OF FIELD                    
         B     SETFTV9                                                          
*                                                                               
SETFTV4  CLI   FCOUNT+1,1          TEST SINGLE FIELD                            
         BNE   SETFTV5                                                          
         L     R3,ANOW                                                          
         BAS   R8,SETFTA           SEARCH ALPHA FOR NOW FIELDS                  
         BZ    SETFTV5                                                          
         OI    FFLAGS,X'20'        SET ALPHA YESTERDAY/TODAY/TOMORROW           
         OI    2(RF),X'20'                                                      
         STC   R3,3(RF)            SET VALUE 1/2/3                              
         B     SETFTVX                                                          
*                                                                               
SETFTV5  L     R3,AMON             TEST FOR ALPHA MONTTH                        
         BAS   R8,SETFTA           SEARCH ALPHA FOR MONTH FIELDS                
         BZ    SETFTVE                                                          
         TM    FFLAGS,X'40'        CAN ONLY BE ONE MONTH FIELD                  
         BO    SETFTVE                                                          
         OI    FFLAGS,X'40'        SET ALPHA MONTH FIELD                        
         OI    2(RF),X'40'                                                      
         STC   R3,3(RF)            SET MONTH NUMBER                             
         LH    R2,FCOUNT                                                        
         SR    R2,R0                                                            
         LA    R2,1(R2)                                                         
         STC   R2,FALPMN           SET FIELD NUMBER                             
         B     SETFTV9                                                          
*                                                                               
SETFTVE  OI    FFLAGS,X'01'        SET ERROR IN FIELDS                          
         B     SETFTVX                                                          
*                                                                               
SETFTV9  LA    RF,4(RF)            BUMP TO NEXT INPUT FIELD                     
         BCT   R0,SETFTV1                                                       
SETFTVX  XIT1                                                                   
                                                                                
SETFTA   SR    R4,R4               RE=A(STRING)/R1=L'STRING                     
         IC    R4,0(R3)            R4=WIDTH OF TABLE ENTRY                      
         LA    R3,1(R3)            R3=A(NEXT TABLE ENTRY)                       
         LR    R5,R1                                                            
         BCTR  R5,0                R5=L'STRING-1                                
*                                                                               
SETFTA1  CLI   0(R3),X'FF'         END OF TABLE                                 
         BE    SETFTA4                                                          
         CLM   R1,1,0(R3)          TEST MAX AND MIN LENGTHS                     
         BH    SETFTA3                                                          
         CLM   R1,1,1(R3)                                                       
         BL    SETFTA3                                                          
SETFTA2  EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),2(R3)                                                    
         BE    SETFTA6                                                          
SETFTA3  AR    R3,R4               BUMP TO NEXT ENTRY                           
         B     SETFTA1                                                          
*                                                                               
SETFTA4  SR    R3,R3               RETURN NOT FOUND                             
         BR    R8                                                               
*                                                                               
SETFTA6  LA    R4,0(R3,R4)         R3=A(MATCHING ENTRY)                         
         BCTR  R4,0                POINT TO LAST BYTE OF FOUND ENTRY            
         SR    R3,R3                                                            
         ICM   R3,1,0(R4)          SET NUMERIC VALUE IN LAST BYTE               
         BR    R8                                                               
         EJECT                                                                  
**********************************************************************          
* VALIDATE AND SET YEAR - RF=A(YEAR FIELD DEFINITION)                *          
* YEAR 00 THRU 59 WILL BE ASSUMED TO BE 2000 THRU 2059               *          
* YEAR 64 THRU 99 WILL BE ASSUMED TO BE 1964 THRU 1999               *          
* RETURN WITH CC EQUAL IF VALID YEAR                                 *          
**********************************************************************          
VALYEAR  CLI   1(RF),2             TEST ONE OR TWO BYTE YEAR FIELD              
         BH    VALYEAR1            NO                                           
         MVC   DYEAR,3(RF)         YES BINARY VALUE ALREADY SET                 
         MVI   DCENT,20                                                         
         CLC   DYEAR,MAXYR         MAX YEAR SUPPORTED IS 2059                   
         BNH   VALYEARX                                                         
         MVI   DCENT,19                                                         
         CLC   DYEAR,MINYR         MIN YEAR SUPPORTED IS 1964                   
         BL    VALYEAR9                                                         
         B     VALYEARX                                                         
*                                                                               
VALYEAR1 CLI   1(RF),4             POINT TO 4 DIGIT YEAR STRING                 
         BNE   VALYEAR9                                                         
         SR    R7,R7                                                            
         IC    R7,0(RF)                                                         
         A     R7,IDATSTR                                                       
         CLC   0(2,R7),=C'19'      CAN BE 1964 THRU 1999                        
         BNE   VALYEAR2                                                         
         MVI   DCENT,19                                                         
         PACK  DUB,2(2,R7)                                                      
         CVB   R2,DUB                                                           
         CLM   R2,1,MINYR                                                       
         BL    VALYEAR9                                                         
         STC   R2,DYEAR                                                         
         B     VALYEARX                                                         
*                                                                               
VALYEAR2 CLC   0(2,R7),=C'20'      CAN BE 2000 THRU 2059                        
         BNE   VALYEAR9                                                         
         MVI   DCENT,20                                                         
         PACK  DUB,2(2,R7)                                                      
         CVB   R2,DUB                                                           
         CLM   R2,1,MAXYR                                                       
         BH    VALYEAR9                                                         
         STC   R2,DYEAR                                                         
         B     VALYEARX                                                         
*                                                                               
VALYEAR9 LTR   R8,R8               EXIT WITH CC NEQ IF ERROR                    
         B     *+6                                                              
VALYEARX CR    R8,R8               EXIT WITH CC EQL IF VALID YEAR               
         BR    R8                                                               
                                                                                
SETCENT  MVI   DCENT,20            SET CENTURY BASED ON YEAR VALUE              
         CLC   DYEAR,MAXYR         00-59 ASSUMED 20NN                           
         BNHR  R8                                                               
         MVI   DCENT,19            64-99 ASSUMED 19NN                           
         BR    R8                                                               
         EJECT                                                                  
**********************************************************************          
* LOOK FOR VALID SEPERATOR WORD                                      *          
**********************************************************************          
FINDSEP  L     R4,ASEP             SEARCH TABLE OF VALID SEP WORDS              
         SR    R5,R5                                                            
         LR    R6,R5                                                            
         IC    R6,0(R4)            GET LENGTH OF ENTRY                          
         LA    R4,1(R4)                                                         
*                                                                               
FSEP1    CLI   0(R4),X'FF'         END OF TABLE                                 
         BE    FSEP4                                                            
         IC    R5,0(R4)            GET LENGTH OF WORD                           
         BCTR  R5,0                                                             
         EX    R5,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R3),1(R4)                                                    
         BE    FSEP2                                                            
         AR    R4,R6               LOOK AT NEXT                                 
         B     FSEP1                                                            
*                                                                               
FSEP2    C     R3,INPST            1ST CHAR POSITION                            
         BNE   FSEP3                                                            
         CLC   0(2,R3),=C'TO'      TO NOT A SEPARATOR HERE                      
         BE    FSEP4                                                            
FSEP3    LR    R4,R3                                                            
         BCTR  R4,0                BACKTRACK                                    
         BCTR  R4,0                                                             
         CLC   0(3,R4),=C'OCT'     TO AS IN OCTOBER                             
         BNE   FSEP6                                                            
FSEP4    SR    R6,R6               NO VALID SEPARATOR FOUND                     
*                                                                               
FSEP6    LA    R5,1(R5)                                                         
         BR    R8                                                               
                                                                                
**********************************************************************          
* LOOK FOR DAY, WEEK, OR MONTH LETTER FOR ADJUSTER                   *          
**********************************************************************          
FINDADJ  L     R4,AADJ             POINT TO ABBREVS                             
         LA    R5,3                NO OF ENTRIES PER LANG                       
*                                                                               
FADJ1    CLC   0(1,R7),0(R4)       TEST MATCH                                   
         BE    FADJ2                                                            
         LA    R4,1(R4)            NO TRY NEXT                                  
         BCT   R5,FADJ1                                                         
         MVI   DADJTP,C'D'         NO MATCH - ASSUME DAYS                       
         BR    R8                                                               
*                                                                               
FADJ2    MVC   DADJTP,3(R4)        GETS D,W,OR M                                
         LA    R7,1(R7)            SET UP R7                                    
         BR    R8                                                               
         EJECT                                                                  
***********************************************************************         
* ADJUST DATE BASED UPON VALUE AND TYPE OF ADJUSTMENT D/M/Y           *         
***********************************************************************         
ADJUSTER CLI   DADJTP,X'FF'        EXIT IF NO AJUSTMENT                         
         BER   R8                                                               
         STM   R2,R6,R2SAVE                                                     
         SR    R2,R2               R2=YEAR                                      
         IC    R2,DYEAR                                                         
         SR    R3,R3               R3=MONTH                                     
         IC    R3,DMONTH                                                        
         SR    R4,R4               R4=DAY                                       
         IC    R4,DDAY                                                          
         LH    R5,DADJWK           EXIT IF ZERO ADJUSTMENT                      
         LTR   R5,R5                                                            
         BZ    ADJUSTEX                                                         
         CLI   DADJTP,C'W'         WEEKS INVOLVED                               
         BNE   ADJUST01            NO                                           
         MHI   R5,7                CONVERT TO DAYS                              
         B     ADJUST02                                                         
*                                                                               
ADJUST01 CLI   DADJTP,C'M'         MONTHS INVOLVED                              
         BE    ADJUST20            YES                                          
*                                                                               
ADJUST02 LTR   R5,R5               TEST NEGATIVE ADJUSTMENT                     
         BM    ADJUST14                                                         
         AR    R5,R4               ADD DAYS THIS MONTH                          
*                                                                               
ADJUST11 BAS   RE,LEAP             ADJUST FOR POSS L/YEAR                       
         SR    R5,R1                                                            
         BNP   ADJUST13            FINISHED                                     
         LA    R3,1(R3)            MONTH + 1                                    
         CHI   R3,13                                                            
         BNE   ADJUST11                                                         
         LA    R2,1(R2)            YEAR + 1                                     
         CHI   R2,99                                                            
         BNH   *+10                                                             
         SR    R2,R2                                                            
         MVI   DCENT,20                                                         
         LA    R3,1                JAN                                          
         B     ADJUST11                                                         
*                                                                               
ADJUST13 AR    R5,R1               RESET DAYS THIS MONTH                        
         LR    R4,R5               SET UP FOR EXIT                              
         B     ADJUSTEX            ALL DONE                                     
*                                                                               
ADJUST14 AR    R5,R4               ADD DAYS THIS MONTH                          
         BP    ADJUST18            NOW >0 - CHECK NO MORE                       
*                                                                               
ADJUST16 BCT   R3,ADJUST17         MONTH - 1                                    
         SHI   R2,1                YEAR - 1                                     
         BNM   *+12                                                             
         LHI   R2,99                                                            
         MVI   DCENT,19                                                         
         LA    R3,12               DEC                                          
*                                                                               
ADJUST17 BAS   RE,LEAP             CHECK FOR POSS L/YEAR                        
         AR    R5,R1                                                            
         BNP   ADJUST16            MORE TO DO                                   
*                                                                               
ADJUST18 LR    R4,R5               SET UP FOR EXIT                              
         B     ADJUSTEX            END IT                                       
*                                                                               
ADJUST20 AR    R3,R5               ADJUST MONTHS                                
         LTR   R5,R5               TEST NEGATIVE                                
         BM    ADJUST30            YES                                          
*                                                                               
ADJUST21 CHI   R3,12               IS MONTH VALID                               
         BNH   ADJUST31            YES                                          
         LA    R2,1(R2)            YEAR + 1                                     
         CHI   R2,99                                                            
         BNH   *+10                                                             
         SR    R2,R2                                                            
         MVI   DCENT,20                                                         
         SHI   R3,12               MONTH - 12                                   
         B     ADJUST21            GO CHECK AGAIN                               
*                                                                               
ADJUST30 LTR   R3,R3               IS MONTH VALID                               
         BP    ADJUST31            YES                                          
         SHI   R2,1                YEAR - 1                                     
         BNM   *+12                                                             
         LHI   R2,99                                                            
         MVI   DCENT,19                                                         
         AHI   R3,12               MONTH + 12                                   
         B     ADJUST30            GO CHECK AGAIN                               
*                                                                               
ADJUST31 BAS   RE,LEAP             ENSURE MAX DAYS IN MONTH                     
         CR    R4,R1               NOT EXCEEDED                                 
         BNH   ADJUSTEX                                                         
         LR    R4,R1                                                            
*                                                                               
ADJUSTEX L     R6,R6SAVE                                                        
         STC   R2,DYEAR                                                         
         STC   R3,DMONTH                                                        
         STC   R4,DDAY                                                          
         MVC   DADJWK(3),=X'FFFFFF' DONT DO IT AGAIN                            
         LM    R2,R5,R2SAVE                                                     
         BR    R8                                                               
         EJECT                                                                  
**********************************************************************          
* RETURN POINTER TO MONTH NAME                                       *          
* INPUT R3=BINARY MONTH NUMBER OUTPUT R4=A(MONTH NAME)               *          
**********************************************************************          
NAMER    L     R6,AMON                                                          
         STC   R3,FULL                                                          
         SR    R5,R5                                                            
         IC    R5,0(R6)            ENTRY LENGTH                                 
NAME1    LA    R4,0(R5,R6)         LAST BYTE OF ENTRY                           
         CLC   FULL(1),0(R4)                                                    
         BE    NAME2                                                            
         CLI   1(R4),X'FF'         NEXT ENTRY                                   
         JE    *+2                                                              
         AR    R6,R5                                                            
         B     NAME1                                                            
*                                                                               
NAME2    LA    R4,3(R6)            POINT TO NAME                                
         BR    R8                                                               
                                                                                
***********************************************************************         
* SET R1 TO CONTAIN DAYS IN CURRENT MONTH INCLUDING LEAP YEAR         *         
***********************************************************************         
LEAP     SR    R1,R1               INDEX IN TO MNUMTAB                          
         IC    R1,MNUMTAB(R3)                                                   
         CHI   R3,2                EXIT IF NOT FEBRUARY                         
         BNER  RE                                                               
         STC   R2,DUB                                                           
         TM    DUB,X'03'           LEAP YEAR                                    
         BNZR  RE                  NO                                           
         LA    R1,1(R1)            YES ADD 1 TO FEB DAYS                        
         BR    RE                                                               
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
DEFLANG  DS    0C                                                               
*&&UK*&& DC    AL1(1)                                                           
*&&US*&& DC    AL1(2)                                                           
SPACES   DC    CL80' '                                                          
*                                                                               
ATABADSL EQU   ATABADS1-ATABADS                                                 
ATABADS  DC    A(ENGMON,ENGNOW,ENGSEP,ENGADJ)  0                                
ATABADS1 DC    A(ENGMON,ENGNOW,ENGSEP,ENGADJ)  1                                
         DC    A(ENGMON,ENGNOW,ENGSEP,ENGADJ)  2                                
         DC    A(GERMON,GERNOW,GERSEP,GERADJ)  3                                
         DC    A(FREMON,FRENOW,FRESEP,FREADJ)  4                                
         DC    A(SPAMON,SPANOW,SPASEP,SPAADJ)  5                                
         DC    A(ITAMON,ITANOW,ITASEP,ITAADJ)  6                                
         DC    A(DUTMON,DUTNOW,DUTSEP,DUTADJ)  7                                
         EJECT                                                                  
***********************************************************************         
* AL1 MAXIMUM LENGTH FOR DATA                                         *         
* AL1 MINIMUM LENGTH FOR DATA (OPTIONAL)                              *         
* CLN STRING FOR COMPARISON                                           *         
* AL1 MONTH NUMBER OR 0                                               *         
* ENTRIES ARE IN DESCENDING ORDER OF MINIMUM LENGTH, EXCEPT THAT AN   *         
* ABBREVIATION OF A NAME WHICH REQUIRES A SEPARATE ENTRY (EG  'MRZ'   *         
* FOR MAERZ) MUST ALWAYS APPEAR AFTER THE FULL NAME, NOT BEFORE       *         
***********************************************************************         
ENGMON   DC    AL1(12)                                                          
         DC    AL1(4),AL1(3),CL9'JULY',AL1(7)                                   
         DC    AL1(3),AL1(3),CL9'MAY',AL1(5)                                    
         DC    AL1(6),AL1(2),CL9'AUGUST',AL1(8)                                 
         DC    AL1(4),AL1(2),CL9'JUNE',AL1(6)                                   
         DC    AL1(9),AL1(1),CL9'SEPTEMBER',AL1(9)                              
         DC    AL1(8),AL1(1),CL9'DECEMBER',AL1(12)                              
         DC    AL1(8),AL1(1),CL9'NOVEMBER',AL1(11)                              
         DC    AL1(8),AL1(1),CL9'FEBRUARY',AL1(2)                               
         DC    AL1(7),AL1(1),CL9'OCTOBER',AL1(10)                               
         DC    AL1(7),AL1(1),CL9'JANUARY',AL1(1)                                
         DC    AL1(5),AL1(1),CL9'APRIL',AL1(4)                                  
         DC    AL1(5),AL1(1),CL9'MARCH',AL1(3)                                  
         DC    X'FF'                                                            
*                                                                               
GERMON   DC    AL1(12)                                                          
         DC    AL1(4),AL1(3),CL9'JULI',AL1(7)                                   
         DC    AL1(3),AL1(3),CL9'MAI',AL1(5)                                    
         DC    AL1(6),AL1(2),CL9'AUGUST',AL1(8)                                 
         DC    AL1(4),AL1(2),CL9'JUNI',AL1(6)                                   
         DC    AL1(9),AL1(1),CL9'SEPTEMBER',AL1(9)                              
         DC    AL1(8),AL1(1),CL9'DEZEMBER',AL1(12)                              
         DC    AL1(8),AL1(1),CL9'NOVEMBER',AL1(11)                              
         DC    AL1(7),AL1(1),CL9'OKTOBER',AL1(10)                               
         DC    AL1(7),AL1(1),CL9'FEBRUAR',AL1(2)                                
         DC    AL1(6),AL1(1),CL9'JANUAR',AL1(1)                                 
         DC    AL1(5),AL1(1),CL9'APRIL',AL1(4)                                  
         DC    AL1(5),AL1(2),CL9'MAERZ',AL1(3)                                  
         DC    AL1(3),AL1(3),CL9'MRZ',AL1(3)                                    
         DC    X'FF'                                                            
*                                                                               
FREMON   DC    AL1(12)                                                          
         DC    AL1(7),AL1(4),CL9'JUILLET',AL1(7)                                
         DC    AL1(3),AL1(3),CL9'MAI',AL1(5)                                    
         DC    AL1(4),AL1(2),CL9'AOUT',AL1(8)                                   
         DC    AL1(4),AL1(2),CL9'JUIN',AL1(6)                                   
         DC    AL1(5),AL1(1),CL9'AVRIL',AL1(4)                                  
         DC    AL1(8),AL1(1),CL9'DECEMBRE',AL1(12)                              
         DC    AL1(7),AL1(1),CL9'FEVRIER',AL1(2)                                
         DC    AL1(7),AL1(1),CL9'JANVIER',AL1(1)                                
         DC    AL1(4),AL1(1),CL9'MARS',AL1(3)                                   
         DC    AL1(8),AL1(1),CL9'NOVEMBRE',AL1(11)                              
         DC    AL1(7),AL1(1),CL9'OCTOBRE',AL1(10)                               
         DC    AL1(9),AL1(1),CL9'SEPTEMBRE',AL1(9)                              
         DC    X'FF'                                                            
*                                                                               
SPAMON   DC    AL1(12)                                                          
         DC    AL1(4),AL1(3),CL9'MAYO',AL1(5)                                   
         DC    AL1(5),AL1(3),CL9'JULIO',AL1(7)                                  
         DC    AL1(6),AL1(2),CL9'AGOSTO',AL1(8)                                 
         DC    AL1(5),AL1(1),CL9'ABRIL',AL1(4)                                  
         DC    AL1(9),AL1(1),CL9'DICIEMBRE',AL1(12)                             
         DC    AL1(5),AL1(1),CL9'ENERO',AL1(1)                                  
         DC    AL1(7),AL1(1),CL9'FEBRERO',AL1(2)                                
         DC    AL1(5),AL1(1),CL9'JUNIO',AL1(6)                                  
         DC    AL1(5),AL1(1),CL9'MARZO',AL1(3)                                  
         DC    AL1(9),AL1(1),CL9'NOVIEMBRE',AL1(11)                             
         DC    AL1(7),AL1(1),CL9'OCTUBRE',AL1(10)                               
         DC    AL1(9),AL1(1),CL9'SEPTIEMBR',AL1(9)    DROPPED LAST E            
         DC    X'FF'                                                            
*                                                                               
ITAMON   DC    AL1(12)                                                          
         DC    AL1(6),AL1(3),CL9'MAGGIO',AL1(5)                                 
         DC    AL1(6),AL1(2),CL9'AGOSTO',AL1(8)                                 
         DC    AL1(6),AL1(1),CL9'APRILE',AL1(4)                                 
         DC    AL1(8),AL1(1),CL9'DICEMBRE',AL1(12)                              
         DC    AL1(8),AL1(1),CL9'FEBBRAIO',AL1(2)                               
         DC    AL1(7),AL1(1),CL9'GENNAIO',AL1(1)                                
         DC    AL1(5),AL1(1),CL9'JUNIO',AL1(6)                                  
         DC    AL1(6),AL1(1),CL9'LUGLIO',AL1(7)                                 
         DC    AL1(5),AL1(1),CL9'MARZO',AL1(3)                                  
         DC    AL1(8),AL1(1),CL9'NOVEMBRE',AL1(11)                              
         DC    AL1(7),AL1(1),CL9'OTTOBRE',AL1(10)                               
         DC    AL1(9),AL1(1),CL9'SETTEMBRE',AL1(9)                              
         DC    X'FF'                                                            
*                                                                               
DUTMON   DC    AL1(12)                                                          
         DC    AL1(4),AL1(3),CL9'JULI',AL1(7)                                   
         DC    AL1(5),AL1(2),CL9'APRIL',AL1(4)                                  
         DC    AL1(3),AL1(2),CL9'MEI',AL1(5)                                    
         DC    AL1(4),AL1(2),CL9'JUNI',AL1(6)                                   
         DC    AL1(8),AL1(1),CL9'AUGUSTUS',AL1(8)                               
         DC    AL1(8),AL1(1),CL9'DECEMBER',AL1(12)                              
         DC    AL1(8),AL1(1),CL9'FEBRUARI',AL1(2)                               
         DC    AL1(7),AL1(1),CL9'JANUARI',AL1(1)                                
         DC    AL1(5),AL1(1),CL9'MAART',AL1(3)                                  
         DC    AL1(8),AL1(1),CL9'NOVEMBER',AL1(11)                              
         DC    AL1(7),AL1(1),CL9'OKTOBER',AL1(10)                               
         DC    AL1(9),AL1(1),CL9'SEPTEMBER',AL1(9)                              
         DC    X'FF'                                                            
                                                                                
ENGNOW   DC    AL1(12)                                                          
         DC    AL1(8),AL1(3),CL9'TOMORROW',AL1(3)                               
         DC    AL1(5),AL1(1),CL9'TODAY',AL1(2)                                  
         DC    AL1(9),AL1(1),CL9'YESTERDAY',AL1(1)                              
         DC    X'FF'                                                            
*                                                                               
GERNOW   DC    AL1(10)                                                          
         DC    AL1(6),AL1(2),CL7'MORGEN',AL1(3)                                 
         DC    AL1(5),AL1(1),CL7'HEUTE',AL1(2)                                  
         DC    AL1(7),AL1(1),CL7'GESTERN',AL1(1)                                
         DC    X'FF'                                                            
*                                                                               
FRENOW   DC    AL1(14)                                                          
         DC    AL1(6),AL1(3),CL11'DEMAIN',AL1(3)                                
         DC    AL1(12),AL1(3),CL11'AUJOURD''HUI',AL1(2)                         
         DC    AL1(4),AL1(2),CL11'HIER',AL1(1)                                  
         DC    X'FF'                                                            
*                                                                               
SPANOW   DC    AL1(12)                                                          
         DC    AL1(6),AL1(1),CL9'MANANA',AL1(3)                                 
         DC    AL1(3),AL1(1),CL9'HOY',AL1(2)                                    
         DC    AL1(4),AL1(1),CL9'AYER',AL1(1)                                   
         DC    X'FF'                                                            
*                                                                               
ITANOW   DC    AL1(12)                                                          
         DC    AL1(6),AL1(1),CL9'DOMANI',AL1(3)                                 
         DC    AL1(4),AL1(1),CL9'OGGI',AL1(2)                                   
         DC    AL1(4),AL1(1),CL9'IERI',AL1(1)                                   
         DC    X'FF'                                                            
*                                                                               
DUTNOW   DC    AL1(12)                                                          
         DC    AL1(6),AL1(2),CL9'MORGEN',AL1(3)                                 
         DC    AL1(7),AL1(1),CL9'VANDAAG',AL1(2)                                
         DC    AL1(8),AL1(1),CL9'GISTEREN',AL1(1)                               
         DC    X'FF'                                                            
                                                                                
ENGSEP   DC    AL1(5)                                                           
         DC    AL1(4),CL4'THRU'                                                 
         DC    AL1(2),CL4'TO'                                                   
         DC    X'FF'                                                            
*                                                                               
GERSEP   DC    AL1(4)                                                           
         DC    AL1(3),CL7'BIS'                                                  
         DC    X'FF'                                                            
*                                                                               
FRESEP   DC    AL1(8)                                                           
         DC    AL1(2),CL7'EN'                                                   
         DC    AL1(7),CL7'JUSQU''A'                                             
         DC    X'FF'                                                            
*                                                                               
SPASEP   DC    AL1(6)                                                           
         DC    AL1(5),CL5'HASTA'                                                
         DC    AL1(1),CL5'A'                                                    
         DC    X'FF'                                                            
*                                                                               
ITASEP   DC    AL1(5)                                                           
         DC    AL1(4),CL4'FINO'                                                 
         DC    AL1(1),CL4'A'                                                    
         DC    X'FF'                                                            
*                                                                               
DUTSEP   DC    AL1(4)                                                           
         DC    AL1(3),CL3'T/M'                                                  
**NOP**  DC    AL1(3),CL3'TOT'                                                  
         DC    X'FF'                                                            
                                                                                
ENGADJ   DC    CL3'DWM',CL3'DWM'   DAY/WEEK/MONTH LETTERS                       
GERADJ   DC    CL3'TWM',CL3'DWM'                                                
FREADJ   DC    CL3'JSM',CL3'DWM'                                                
SPAADJ   DC    CL3'DSM',CL3'DWM'                                                
ITAADJ   DC    CL3'GSM',CL3'DWM'                                                
DUTADJ   DC    CL3'DWM',CL3'DWM'                                                
                                                                                
***********************************************************************         
* TABLE CONTAINING NUMBER OF DAYS IN EACH MONTH                       *         
***********************************************************************         
MNUMTAB  DC    X'FF'                                                            
         DC    AL1(31),AL1(28),AL1(31),AL1(30)                                  
         DC    AL1(31),AL1(30),AL1(31),AL1(31)                                  
         DC    AL1(30),AL1(31),AL1(30),AL1(31)                                  
                                                                                
***********************************************************************         
* THE FOLLOWING TABLE EQUATES ACTIONS WITH BIT SETTINGS WHICH INDICATE*         
* WHICH START AND END FIELDS ARE PRESENT                              *         
***********************************************************************         
ACTTAB   EQU   *                                                                
         DC    B'01110111',AL3(CHKOK)                                           
         DC    B'01110110',AL3(ACT12)                                           
         DC    B'01110100',AL3(ACT18)                                           
         DC    B'01110011',AL3(ACT29)                                           
         DC    B'01110010',AL3(ACT12)                                           
         DC    B'01110001',AL3(ACT21)                                           
         DC    B'01110000',AL3(ACT20)                                           
*                                                                               
         DC    B'01100111',AL3(ACT13)                                           
         DC    B'01100110',AL3(ACT12)                                           
         DC    B'01100100',AL3(ACT18)                                           
         DC    B'01100011',AL3(ACT13)                                           
         DC    B'01100010',AL3(ACT04)                                           
         DC    B'01100001',AL3(ACT13)                                           
         DC    B'01100000',AL3(ACT02)                                           
*                                                                               
         DC    B'01000111',AL3(ACT31)                                           
         DC    B'01000110',AL3(ACT31)                                           
         DC    B'01000100',AL3(ACT09)                                           
         DC    B'01000011',AL3(ACT11)                                           
         DC    B'01000010',AL3(ACT11)                                           
         DC    B'01000001',AL3(ACT14)                                           
         DC    B'01000000',AL3(ACT08)                                           
*                                                                               
         DC    B'00110111',AL3(ACT22)                                           
         DC    B'00110110',AL3(ACT12)                                           
         DC    B'00110100',AL3(ACT17)                                           
         DC    B'00110011',AL3(ACT03)                                           
         DC    B'00110010',AL3(ACT12)                                           
         DC    B'00110001',AL3(ACT21)                                           
         DC    B'00110000',AL3(ACT19)                                           
*                                                                               
         DC    B'00100111',AL3(ACT05)                                           
         DC    B'00100110',AL3(ACT05)                                           
         DC    B'00100100',AL3(ACT06)                                           
         DC    B'00100011',AL3(ACT03)                                           
         DC    B'00100010',AL3(ACT03)                                           
         DC    B'00100001',AL3(ACT02)                                           
         DC    B'00100000',AL3(ACT02)                                           
*                                                                               
         DC    B'00010111',AL3(ACT23)                                           
         DC    B'00010110',AL3(ACT12)                                           
         DC    B'00010100',AL3(ACT16)                                           
         DC    B'00010011',AL3(ACT01)                                           
         DC    B'00010010',AL3(ACT12)                                           
         DC    B'00010001',AL3(ACT14)                                           
         DC    B'00010000',AL3(ACT33)                                           
*                                                                               
         DC    B'00000111',AL3(ACT25)                                           
         DC    B'00000110',AL3(ACT01)                                           
         DC    B'00000100',AL3(ACT07)                                           
         DC    B'00000011',AL3(ACT03)                                           
         DC    B'00000010',AL3(ACT01)                                           
         DC    B'00000001',AL3(ACT32)                                           
         DC    B'00000000',AL3(ACT32)                                           
ACTTABLN EQU   (*-ACTTAB)/4                                                     
         EJECT                                                                  
**********************************************************************          
* ROUTINES TO DECODE INPUT DATES AND SET BINARY CYMD IN FULL         *          
* R1 POINTS TO DATE AND FPER HAS ITS TYPE                            *          
**********************************************************************          
CNVDATE  NTR1  BASE=*                                                           
         TM    FPER,PVALBVP                                                     
         BO    CNVBIN                                                           
         TM    FPER,PVALCVP                                                     
         BO    CNVCMO                                                           
         TM    FPER,PVALNVP                                                     
         BO    CNVCMN                                                           
         TM    FPER,PVALPVP                                                     
         BO    CNVPWOS                                                          
         TM    FPER,PVALEBC                                                     
         BO    CNVEBC                                                           
         DC    H'0'                                                             
*                                                                               
CNVBIN   MVC   FULL+1(3),0(R1)     R1=A(3 BYTE BINARY DATE)                     
         MVI   FULL,19                                                          
         CLI   FULL,100                                                         
         BL    CNVXIT                                                           
         MVI   FULL,20                                                          
         SR    R2,R2                                                            
         IC    R2,FULL+1                                                        
         SHI   R2,10                                                            
         STC   R2,FULL+1                                                        
         B     CNVXIT                                                           
*                                                                               
CNVCMN   ICM   R3,12,0(R1)         R1=A(2 BYTE CMPRSD NEW DATE)                 
         SR    R2,R2                                                            
         SLDL  R2,7                                                             
         AHI   R2,64               BASE YEAR IS 1964                            
         MVI   FULL,19                                                          
         CHI   R2,100                                                           
         BL    *+12                                                             
         MVI   FULL,20                                                          
         AHI   R2,-100                                                          
         STC   R2,FULL+1                                                        
         SR    R2,R2                                                            
         SLDL  R2,4                                                             
         STC   R2,FULL+2                                                        
         SR    R2,R2                                                            
         SLDL  R2,5                                                             
         STC   R2,FULL+3                                                        
         B     CNVXIT                                                           
*                                                                               
CNVCMO   ICM   R3,12,0(R1)         R1=A(2 BYTE CMPRSD OLD DATE)                 
         SR    R2,R2                                                            
         SLDL  R2,7                                                             
         MVI   FULL,19                                                          
         CHI   R2,100                                                           
         BL    *+12                                                             
         MVI   FULL,20                                                          
         AHI   R2,-100                                                          
         STC   R2,FULL+1                                                        
         SR    R2,R2                                                            
         SLDL  R2,4                                                             
         STC   R2,FULL+2                                                        
         SR    R2,R2                                                            
         SLDL  R2,5                                                             
         STC   R2,FULL+3                                                        
         B     CNVXIT                                                           
*                                                                               
CNVPWOS  MVC   DUB1(3),0(R1)       R1=A(3 BYTE PWOS DATE)                       
         ZAP   DUB,=P'0'                                                        
         LA    R2,DUB1                                                          
         LA    R3,FULL+1                                                        
         LA    R0,3                                                             
         MVI   FULL,19                                                          
         CLI   DUB1,X'99'          TEST FUNNY PACKED YEAR NUMBER                
         BNH   CNVPWOS1                                                         
         MVI   FULL,20                                                          
         SR    RF,RF                                                            
         IC    RF,DUB1                                                          
         SHI   RF,160                                                           
         STC   RF,DUB1                                                          
CNVPWOS1 MVO   DUB+6(2),0(1,R2)                                                 
         CVB   RF,DUB                                                           
         STC   RF,0(R3)                                                         
         LA    R2,1(R2)                                                         
         LA    R3,1(R3)                                                         
         BCT   R0,CNVPWOS1                                                      
         B     CNVXIT                                                           
*                                                                               
CNVEBC   MVC   DUB1(6),0(R1)       R1=A(6 BYTE EBCDIC DATE)                     
         CLI   DUB1,C'9'           TEST FUNNY EBCDIC FIRST YEAR CHR             
         BNH   *+16                                                             
         IC    R2,DUB1                                                          
         SHI   R2,10                                                            
         STC   R2,DUB1                                                          
         PACK  DUB,DUB1(6)                                                      
         MVO   PWK,DUB+4(4)        PWK+1(3) IS PWOS DATE                        
         CLI   0(R1),C'9'                                                       
         BNH   CNVEBC1                                                          
         IC    R2,0(R1)                                                         
         SLL   R2,4                                                             
         STC   R2,DUB1                                                          
         NI    PWK+1,X'0F'                                                      
         OC    PWK+1(1),DUB1                                                    
CNVEBC1  LA    R1,PWK+1            POINT TO PWOS INTERMEDIATE VERSION           
         B     CNVPWOS                                                          
*                                                                               
CNVXIT   XIT1                                                                   
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
FULL     DS    F                                                                
RELO     DS    F                                                                
INPST    DS    A                   STORED ADDRESS OF INPUT START                
JULIAN   DS    F                   TODAYS DATE JULIAN P'0CYYDDDF'               
*                                                                               
R1SAVE   DS    F                                                                
R2SAVE   DS    F                                                                
R3SAVE   DS    F                                                                
R4SAVE   DS    F                                                                
R5SAVE   DS    F                                                                
R6SAVE   DS    F                                                                
*                                                                               
TABLSTAD DS    0XL(ATABADSL)                                                    
AMON     DS    A                                                                
ANOW     DS    A                                                                
ASEP     DS    A                                                                
AADJ     DS    A                                                                
*                                                                               
LANGMOD  DS    H                   OFFSET FOR LANG TABLES                       
ENTLEN   DS    H                   ENTRY LENGTH FOR LANG TABLES                 
*                                                                               
TIMEPL   DS    0XL28               PARAM LIST FOR IBM TIME MACRO                
*                                                                               
SFLDINF  DS    0XL24               START DATE FIELD INFO                        
SADJWK   DS    H                   START ADJ NUMBER                             
SADJTP   DS    CL1                 START ADJ TYPE                               
SCENT    DS    XL1                 START CENTURY                                
SYEAR    DS    XL1                 START YEAR                                   
SMONTH   DS    XL1                 START MONTH                                  
SDAY     DS    XL1                 START DAY                                    
SADJD    DS    XL1                 START DISPLM TO ADJUSTMENT FIELD             
SFLAGS   DS    XL1                 START FLAGS                                  
SALPMN   DS    XL1                 START NUMBER OF ALPHA MONTH FIELD            
SLENGTH  DS    XL1                 START LENGTH                                 
SFIELDS  DS    XL1                 START NUMBER OF FIELDS                       
SFLD1D   DS    XL1                 START DISPLM TO 1ST FIELD                    
SFLD1L   DS    XL1                 START LENGTH OF 1ST FIELD                    
SFLD1T   DS    XL1                 START TYPE   OF 1ST FIELD                    
SFLD1V   DS    XL1                 START VALUE  OF 1ST FIELD                    
SFLD2D   DS    XL1                 START DISPLM TO 2ND FIELD                    
SFLD2L   DS    XL1                 START LENGTH OF 2ND FIELD                    
SFLD2T   DS    XL1                 START TYPE   OF 2ND FIELD                    
SFLD2V   DS    XL1                 START VALUE  OF 2ND FIELD                    
SFLD3D   DS    XL1                 START DISPLM TO 3RD FIELD                    
SFLD3L   DS    XL1                 START LENGTH OF 3RD FIELD                    
SFLD3T   DS    XL1                 START TYPE   OF 3RD FIELD                    
SFLD3V   DS    XL1                 START VALUE  OF 3RD FIELD                    
*                                                                               
EFLDINF  DS    0XL24               END DATE FIELD INFO                          
EADJWK   DS    H                   END ADJ NUMBER                               
EADJTP   DS    XL1                 END ADJ TYPE                                 
ECENT    DS    XL1                 END CENTURY                                  
EYEAR    DS    XL1                 END YEAR                                     
EMONTH   DS    XL1                 END MONTH                                    
EDAY     DS    XL1                 END DAY                                      
EADJD    DS    XL1                 END DISPLM TO ADJUSTMENT FIELD               
EFLAGS   DS    XL1                 END FLAGS                                    
EALPMN   DS    XL1                 END NUMBER OF ALPHA MONTH FIELD              
ELENGTH  DS    XL1                 END LENGTH                                   
EFIELDS  DS    XL1                 END NUMBER OF FIELDS                         
EFLD1D   DS    XL1                 END DISPLM TO 1ST FIELD                      
EFLD1L   DS    XL1                 END LENGTH OF 1ST FIELD                      
EFLD1T   DS    XL1                 END TYPE   OF 1ST FIELD                      
EFLD1V   DS    XL1                 END VALUE  OF 1ST FIELD                      
EFLD2D   DS    XL1                 END DISPLM TO 2ND FIELD                      
EFLD2L   DS    XL1                 END LENGTH OF 2ND FIELD                      
EFLD2T   DS    XL1                 END TYPE   OF 2ND FIELD                      
EFLD2V   DS    XL1                 END VALUE  OF 2ND FIELD                      
EFLD3D   DS    XL1                 END DISPLM TO 3RD FIELD                      
EFLD3L   DS    XL1                 END LENGTH OF 3RD FIELD                      
EFLD3T   DS    XL1                 END TYPE   OF 3RD FIELD                      
EFLD3V   DS    XL1                 END VALUE  OF 3RD FIELD                      
*                                                                               
TFLDINF  DS    0XL24               TODAY DATE FIELD INFO                        
TADJWK   DS    H                   TODAY ADJ NUMBER                             
TADJTP   DS    XL1                 TODAY ADJ TYPE                               
TCENT    DS    XL1                 TODAY CENTURY                                
TYEAR    DS    XL1                 TODAY YEAR                                   
TMONTH   DS    XL1                 TODAY MONTH                                  
TDAY     DS    XL1                 TODAY DAY                                    
         DS    XL5                 N/D                                          
UCENT    DS    XL1                 USE CENTURY                                  
UYEAR    DS    XL1                 USE YEAR                                     
UMONTH   DS    XL1                 USE MONTH                                    
UDAY     DS    XL1                 USE DAY                                      
UPASS    DS    XL1                 USE PASS NUMBER                              
         DS    XL7                 N/D                                          
*                                                                               
         DS    0F                                                               
FINFO    DS    0XL24               FIELD INFO                                   
FTFLAG   DS    X                   THIS CHR FLAGS                               
FLFLAG   DS    X                   LAST CHR FLAG                                
FADJD    DS    XL1                 DISPLM TO ADJUSTMENT FIELD (....)            
FFLAGS   DS    XL1                 FLAGS                                        
FALPMN   DS    XL1                 NUMBER OF ALPHA MONTH FIELD                  
         DS    XL5                                                              
FCOUNT   DS    H                   NUMBER OF FIELDS                             
FFLD1D   DS    XL1                 DISPLM TO 1ST FIELD                          
FFLD1L   DS    XL1                 LENGTH OF 1ST FIELD                          
FFLD1T   DS    XL1                 TYPE   OF 1ST FIELD                          
FFLD1V   DS    XL1                 VALUE  OF 1ST FIELD                          
FFLD2D   DS    XL1                 DISPLM TO 2ND FIELD                          
FFLD2L   DS    XL1                 LENGTH OF 2ND FIELD                          
FFLD2T   DS    XL1                 TYPE   OF 2ND FIELD                          
FFLD2V   DS    XL1                 VALUE  OF 2ND FIELD                          
FFLD3D   DS    XL1                 DISPLM TO 3RD FIELD                          
FFLD3L   DS    XL1                 LENGTH OF 3RD FIELD                          
FFLD3T   DS    XL1                 TYPE   OF 3RD FIELD                          
FFLD3V   DS    XL1                 VALUE  OF 3RD FIELD                          
*                                                                               
IDATSTR  DS    A                   A(START DATE FIELD)                          
IDATLEN  DS    XL1                 L'START DATE FIELD                           
INPUTLEN DS    XL1                 ACTUAL INPUT DATA LENGTH                     
ILEN     DS    XL1                 INPUT STRING LENGTH                          
FPER     DS    XL1                 FLAG FOR PERIOD VALIDATION                   
SPER     DS    XL4                 START PERIOD PASSED                          
EPER     DS    XL4                 END PERIOD PASSED                            
RETCODE  DS    XL1                 RETCODE                                      
PWK      DS    PL5                 ACCUM FOR PWOS DATE                          
*                                                                               
PROGSW   DS    XL1                 PROGRAM SWITCH                               
SWNOINP  EQU   X'80'               NO INPUT DATA                                
SWENDAD  EQU   X'40'               END FIELD ADJUSTMENT EXISTS                  
SWENDIN  EQU   X'20'               END FIELD INPUT EXISTS                       
SWSTAIN  EQU   X'10'               START FIELD INPUT EXISTS                     
SWAFSET  EQU   X'08'               ASSUMPTION FLAG HAS BEEN SET                 
SWNADJ   EQU   X'04'               NEGATIVE ADJUSTMENT ENTERED                  
SWSCND   EQU   X'02'               SECOND PASS THROUGH SCAN                     
SWINVP   EQU   X'01'               DATE(S) FAILED PERIOD VALIDATION             
*                                                                               
LANG     DS    XL1                 INPUT LANGUAGE CODE                          
OLANG    DS    XL1                 OUTPUT LANGUAGE CODE                         
UK       EQU   1                   UK LANGUAGE VALUE                            
USA      EQU   2                   USA LANGUAGE VALUE                           
*                                                                               
SDATEHDR DS    CL1                                                              
SDATEFLD DS    CL20                INPUT START DATE                             
SDATETRL DS    CL1                                                              
*                                                                               
EDATEHDR DS    CL1                                                              
EDATEFLD DS    CL20                INPUT END DATE                               
EDATETRL DS    CL1                                                              
*                                                                               
DATEWK   DS    CL15                WORK AREA FOR US DATE OUTPUT                 
TMPCHAR  DS    CL1                 WORK BYTE                                    
INOPT1   DS    CL1                 SAVED HIGH ORDER BYTE OF PARM1               
INOPT2   DS    CL1                 SAVED HIGH ORDER BYTE OF PARM2               
*                                                                               
MINYR    DS    X                   MINYR NEW 1964 WAS 1928                      
MAXYR    DS    X                   MAXYR OLD 2027, MAXYR NEW 2059               
*                                                                               
WORKX    DS    0H                                                               
*                                                                               
DFLDINFD DSECT                                                                  
DFLDINF  DS    0XL24               DATE FIELD INFO                              
DADJWK   DS    H                   ADJUSTMENT NUMBER                            
DADJTP   DS    CL1                 ADJUSTMENT TYPE                              
DCENT    DS    XL1                 CENTURY                                      
DYEAR    DS    XL1                 YEAR                                         
DMONTH   DS    XL1                 MONTH                                        
DDAY     DS    XL1                 DAY                                          
DADJD    DS    XL1                 DISPLM TO ADJUSTMENT FIELD (....)            
DFLAGS   DS    XL1                 FLAGS                                        
DALPMN   DS    XL1                 NUMBER OF ALPHA MONTH FIELD                  
DLENGTH  DS    XL1                 FIELD LENGTH                                 
DFIELDS  DS    XL1                 NUMBER OF FIELDS                             
DFLD1D   DS    XL1                 DISPLM TO 1ST FIELD                          
DFLD1L   DS    XL1                 LENGTH OF 1ST FIELD                          
DFLD1T   DS    XL1                 TYPE   OF 1ST FIELD                          
DFLD1V   DS    XL1                 VALUE  OF 1ST FIELD                          
DFLD2D   DS    XL1                 DISPLM TO 2ND FIELD                          
DFLD2L   DS    XL1                 LENGTH OF 2ND FIELD                          
DFLD2T   DS    XL1                 TYPE   OF 2ND FIELD                          
DFLD2V   DS    XL1                 VALUE  OF 2ND FIELD                          
DFLD3D   DS    XL1                 DISPLM TO 3RD FIELD                          
DFLD3L   DS    XL1                 LENGTH OF 3RD FIELD                          
DFLD3T   DS    XL1                 TYPE   OF 3RD FIELD                          
DFLD3V   DS    XL1                 VALUE  OF 3RD FIELD                          
         EJECT                                                                  
       ++INCLUDE DDPERVALD                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DDPERVAL  07/18/19'                                      
         END                                                                    
