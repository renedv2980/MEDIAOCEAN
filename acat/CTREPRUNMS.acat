*          DATA SET CTREPRUNMS AT LEVEL 008 AS OF 05/01/02                      
*CATALP CTRUNMST                                                                
         TITLE 'MODULE TO CONTROL CONMAN RUN'                                   
CTRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,CTRUNCON,=V(CTSAVE)                                            
         L     RA,=V(CTWORKC)                                                   
         USING CTWORKD,RA                                                       
         L     RE,=V(SSB)                                                       
         XC    0(2,RE),0(RE)       INDICATE OFF-LINE                            
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R9,=V(MASTC)                                                     
         USING MASTD,R9                                                         
         EJECT                                                                  
*              INITIAL CONTROL CARDS                                            
         SPACE 2                                                                
READCARD MVC   MCAPHAS2,APPLIC                                                  
         MVC   MCAPHAS1,SPECS                                                   
         ST    RA,MCAWORK                                                       
         GOTO1 =V(RUNSTART)                                                     
*&&OS*&& MVC   APPLIC,MCAPHAS2                                                  
*&&OS*&& MVC   SPECS,MCAPHAS1                                                   
         MVC   RCTRACE,MCTRACE     TRANSFER MASTER VALUES                       
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVI   RCFLONLY,C'Y'       GO TO EXECUTE I/O AND RUN SPECS              
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,C'N'                                                    
         EJECT                                                                  
*              OPEN FILE ROUTINES                                               
         SPACE 3                                                                
OPEN2    L     R2,FILEC                                                         
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'CONTROL',FILELIST,(R2)                  
         B     OPEN4                                                            
         SPACE 2                                                                
FILELIST DC    CL8'NCTFILE'                                                     
*        DC    CL8'NCTUSER'                                                     
         DC    C'X '                                                            
         SPACE 2                                                                
OPEN4    GOTO1 =V(CTOPEN)                                                       
         EJECT                                                                  
*                   CONTROL DIVIDERS AND REQUESTS                               
         SPACE 3                                                                
         CLI   RCREQSUM,C'Y'       IS A DIVIDER WANTED                          
         BNE   GOREQS                                                           
         CLC   LOGO1,SPACES                                                     
         BNE   DIV                                                              
         MVC   LOGO1,=C'DONOVAN'                                                
         MVC   LOGO2,=C'NEWYORK'                                                
         MVC   LOGONAME,=CL33'DONOVAN DATA SYSTEMS INC.,'                       
         MVC   LOGOADD,=CL33'666 FIFTH AVENUE,'                                 
         MVC   LOGOADD2,=CL33'NEW YORK CITY'                                    
         MVC   LOGOADD3,=CL33'NY 10019'                                         
         CLC   RCOUNTRY,=C'UK'                                                  
         BNE   DIV                                                              
         MVC   LOGOADD,=CL33'8 HILL STREET,'                                    
         MVC   LOGOADD2,=CL33'LONDON W.1'                                       
         MVC   LOGOADD3,SPACES                                                  
         SPACE 2                                                                
DIV      GOTO1 LOGO,DMCB,LOGOC                                                  
         SPACE 2                                                                
GOREQS   BAS   RE,ANYLINUP                                                      
         GOTO1 FILCON,DMCB,1     GO AND HANDLE REQUESTS                         
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
         SPACE 2                                                                
FINALISE CLI   RCREQSUM,C'Y'                                                    
         BNE   CLOSE1                                                           
**NOP**  GOTO1 REQSUM,DMCB,(R2)  SUMMARISE REQUESTS                             
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC                                                  
         EJECT                                                                  
*                   CLOSE FILES AND END JOB                                     
         SPACE 3                                                                
CLOSE1   EQU   *                                                                
         L     R3,FILEC                                                         
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'CONTROL',,(R3)                        
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
         LTR   R2,R2                                                            
         BZ    NORMEND                                                          
*&&DO*&& CANCEL                                                                 
*&&OS*&& ABEND 99,DUMP                                                          
         SPACE 2                                                                
NORMEND  DS    0H                                                               
         XBASE                                                                  
         EJECT                                                                  
*                   LINE-UP PATTERN CONTROL                                     
         SPACE 3                                                                
ANYLINUP NTR                                                                    
         CLI   RCLINEUP,C'Y'                                                    
         BNE   LINEND                                                           
         MVC   P,SPACES                                                         
         GOTO1 PRINT,DMCB,P,=C'BC01'                                            
         GOTO1 PRINT,DMCB,P,=C'BL09'                                            
         DATE  DMCB,DATE=NO                                                     
         CLC   DMCB+6(2),=C'US'                                                 
         BE    USLINUP                                                          
         LA    R2,2                                                             
         SPACE 2                                                                
LINLOOP  MVI   P+1,C'X'            PRINT 109 X'S ON HEADLINES 10 & 11           
         MVC   P+2(108),P+1                                                     
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,LINLOOP                                                       
         B     LINEND                                                           
         SPACE 2                                                                
USLINUP  LA    R4,3                DO IT THREE TIMES                            
USLINUP2 LA    R2,3                                                             
         SPACE 2                                                                
USLINUP4 MVI   P,C'X'              PRINT 86 X'S ON HEADS 10,11,12               
         MVC   P+1(85),P                                                        
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,USLINUP4                                                      
         MVC   P,SPACES                                                         
         GOTO1 (RF),(R1),,=C'BC01'                                              
         GOTO1 (RF),(R1),,=C'BL09'                                              
         BCT   R4,USLINUP2                                                      
         MVC   P,SPACES                                                         
         SPACE 2                                                                
         SPACE 2                                                                
         SPACE 2                                                                
LINEND   XIT                                                                    
         SPACE 2                                                                
TESTOPT1 DC    C' '                                                             
TESTOPT2 DC    C' '                                                             
       ++INCLUDE DDREMOTED                                                      
         PRINT OFF                                                              
       ++INCLUDE DDLOGOD                                                        
         EJECT                                                                  
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE CTREPWORKD                                                     
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008CTREPRUNMS05/01/02'                                      
         END                                                                    
