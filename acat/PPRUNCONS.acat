*          DATA SET PPRUNCONS  AT LEVEL 004 AS OF 05/01/02                      
*CATALP PPRUNCON                                                                
         ENTRY EMLPUT                                                           
*******************************************************************             
*                                                                 *             
* THIS IS AN OS COMPATIBLE VERSION.                               *             
*                                                                 *             
*******************************************************************             
*                                                                               
*      CHANGE LOG                                                               
*                                                                               
*   BPLA  10/00   COPY OF PPRUNCON MADE 10/30/00                                
*                 LEVEL RESET TO 1                                              
*                                                                               
*   BPLA 12/97    CHANGE TO USE CORE-RESIDENT GETINS                            
*                                                                               
*   BPLA 7/16/96  CHANGE FOR EXPANDED SSB                                       
*                                                                               
*   BPLA 2/7/94 CHANGE TO CLOSE SECOND PRINT                                    
*                                                                               
         TITLE 'MODULE TO CONTROL PPG RUN'                                      
PPRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,PPRUNCON,=V(PPSAVE)                                            
         L     RA,=V(PPWRKC)                                                    
         USING PPWORKD,RA                                                       
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R9,=V(MASTC)                                                     
         USING MASTD,R9                                                         
         EJECT                                                                  
* INITIAL CONTROL CARDS *                                                       
         SPACE 2                                                                
*&&DO*&& MVC   MCAPHAS1,PPSPECS                                                 
*&&DO*&& MVC   MCAPHAS2,PPAPPLIC                                                
         ST    RA,MCAWORK                                                       
         GOTO1 =V(RUNSTART)                                                     
*                            GET CORE-RESIDENT VERSION OF GETINS                
         MVI   BYTE,QGETINS                                                     
         BAS   RE,SPLOAD                                                        
         MVC   GETINS,4(R1)                                                     
*                                                                               
*&&OS*&& MVC   PPSPECS,MCAPHAS1                                                 
*&&OS*&& MVC   PPAPPLIC,MCAPHAS2                                                
         MVC   RCTRACE,MCTRACE                                                  
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVC   RCORIGID,MCORIGID                                                
         MVC   ORIGNAME,MCORIGIN                                                
         MVC   ORIGADDR,MCORIGAD                                                
         MVC   RCRECOVR,MCRECOVR                                                
         MVC   RCRQFILT,MCREQCOD                                                
         CLC   MCREQCOD,SPACES                                                  
         BNE   *+10                                                             
         MVC   RCRQFILT,MCPROG                                                  
         CLC   MCREQCOD,SPACES                                                  
         BNE   *+10                                                             
         MVC   RCRQFILT,MCPROG                                                  
         MVC   RCMEDFLT,MCMEDIA                                                 
         MVC   RCAGYFLT,MCUSER                                                  
**NEW 10/31/89                                                                  
         MVC   RCLANG,MCLANG           LANGUAGE                                 
         MVC   RCCTRY,MCCTRY           COUNTRY                                  
*                                  DO FILE SPECS                                
         MVI   RCFLONLY,C'Y'                                                    
         MVI   FORCEHED,C'Y'                                                    
         MVI   RCSUBPRG,0                                                       
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,C'N'                                                    
*                                                                               
         CLI   RCWRITE,C'N'                                                     
         BE    OPEN2                                                            
         CLC   FCUPPRTD(4),=C'NNNN'                                             
         BE    OPEN2                                                            
         MVC   FILELIST+00(1),FCUPPRTD                                          
         MVC   FILELIST+08(1),FCUPPRTF                                          
         MVC   FILELIST+16(1),FCUPPUBD                                          
         MVC   FILELIST+24(1),FCUPPUBF                                          
*                                                                               
*                                  CHANGE Y TO U                                
         LA    R2,FILELIST                                                      
         L     R3,=V(PPFILEC)                                                   
         LA    R0,4                                                             
OPEN1    DS    0H                                                               
         CLI   0(R2),C'Y'                                                       
         BNE   *+8                                                              
         MVI   0(R2),C'U'                                                       
         LA    R2,8(R2)                                                         
         BCT   R0,OPEN1                                                         
*                                                                               
         CLI   RCRECOVR,C'N'                                                    
         BE    OPEN2                                                            
*                                                                               
         MVC   FILELIST+40(10),=C'UPRECV1 X '  SET FOR RECOVERY                 
         L     RF,=V(SSB)                                                       
         XC    0(4,RF),0(RF)       SETS RECOVERY ON                             
         MVI   2(RF),X'FF'         RESET EXPANDED SSB INDICATOR                 
*                                                                               
         SPACE 2                                                                
OPEN2    DS    0H                                                               
         MVC   WORK(2),RCAGYFLT                                                 
         BAS   RE,GETAGINF         SET FILE NO IN UTL                           
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'PRINT',FILELIST,(R3)                    
*&&DO                                                                           
         COMRG                                                                  
         MVC   RCUPSI,23(R1)                                                    
         MVC   RCJOB,24(R1)                                                     
*&&                                                                             
*&&OS                                                                           
         LA    R2,DUB                                                           
         EXTRACT (R2),FIELDS=TIOT                                               
         MVC   RCJOB,0(R2)                                                      
         MVC   RCUPSI,MCUPSI                                                    
*&&                                                                             
         B     OPEN4                                                            
         SPACE 2                                                                
FILELIST DC    CL8'NPRTDIR'                                                     
         DC    CL8'NPRTFILE'                                                    
         DC    CL8'NPUBDIR'                                                     
         DC    CL8'NPUBFILE'                                                    
         DC    CL8'NCTFILE'                                                     
         DC    CL10'X'                                                          
         SPACE 2                                                                
OPEN4    DS    0H                                                               
*                                  LINE-UP PATTERN                              
         CLI   RCLINEUP,C'Y'                                                    
         BNE   RC1B                                                             
**NEW 5/5/89                                                                    
         L     RF,=V(REMOTEC)                                                   
         USING REMOTED,RF                                                       
         OC    REMOTKEY,REMOTKEY        TEST DIRECT TO PRTQUE                   
         BNZ   RC1B                                                             
         DROP  RF                                                               
*****    L     RF,=V(MASTC)                                                     
*****    USING MASTD,RF                                                         
         OC    MCREMPQK,MCREMPQK        TEST SOON JOB                           
         BNZ   RC1B                                                             
*****    DROP  RF                                                               
**NEW 5/5/89                                                                    
         MVI   FORCEHED,C'Y'                                                    
         MVI   SKIPSPEC,C'Y'                                                    
         LA    R2,7                                                             
RC0D     DS    0H                                                               
         MVI   P,C'X'                                                           
         MVC   P+1(109),P          BACK TO 110                                  
         GOTO1 REPORT                                                           
         BCT   R2,RC0D                                                          
         B     RC1B                                                             
*                                                                               
RC1      DS    0H                                                               
RC1B     DS    0H                                                               
         EJECT                                                                  
START    DS    0H                                                               
*                   CONTROL DIVIDERS AND REQUESTS                               
         SPACE 2                                                                
         CLI   LOGONAME,C' '       IF NO DESTINATION NAME                       
         BNH   GOREQS              NO DIVIDERS                                  
         GOTO1 LOGO,DMCB,LOGOC     START DIVIDER                                
         SPACE 2                                                                
GOREQS   DS    0H                                                               
         GOTO1 VFILCON                                                          
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
         SPACE 2                                                                
FINALISE DS    0H                                                               
         CLI   LOGONAME,C' '                                                    
         BNH   GOEND                                                            
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC                                                  
         SPACE 2                                                                
GOEND    DS    0H                                                               
*                                                                               
         L     RF,=A(EMLFLAG)      EMAIL FILE OPEN?                             
         CLI   0(RF),C'Y'                                                       
         BNE   CLOSE3              NO                                           
         L     RF,=A(EMLCLSE)      CLOSE IT                                     
         BASR  RE,RF                                                            
*                                                                               
CLOSE3   DS    0H                                                               
*                                                                               
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
         CLI   MCREPTS,2                                                        
         BNE   GOEND2                                                           
         GOTO1 =V(PRINTS2),DMCB,=C'CLOSE'                                       
*                                                                               
GOEND2   DS    0H                                                               
         CP    MCNDUMPS,=P'0'                                                   
         BE    NORMEND                                                          
*&&DO*&& CANCEL                                                                 
*&&OS*&& ABEND 999                                                              
         SPACE 2                                                                
NORMEND  DS    0H                                                               
         XBASE                                                                  
         SPACE 2                                                                
*                             ROUTINE TO EXTRACT FILE NUNBER FROM AGY           
GETAGINF NTR1                                                                   
         L     R3,PPFILEC                                                       
         USING CND,R3                                                           
         XC    CNLEN,CNLEN                                                      
         MVC   CNAGY,WORK                                                       
         CLC   RCORIGID,=X'002B'   IF TCH1 (OR NO ORIG ID GIVEN)                
         BE    GETAG3              SKIP SET OF ORIGIN ID                        
         CLC   RCORIGID,=X'005A'   IF SJR?   ORIG=90                            
         BE    GETAG3              SKIP SET OF ORIGIN ID                        
         MVC   CNID,RCORIGID                                                    
GETAG3   GOTO1 =V(CONFID),DMCB,CND,FULL                                         
         OC    FULL,FULL                                                        
         BNZ   *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     R5,UTL                                                           
         MVC   4(1,R5),CNPSE                                                    
         MVC   PRNTSE,CNPSE                                                     
         MVC   SPOTSE,CNSSE                                                     
         MVC   ACCTSE,CNASE                                                     
         MVC   SPOTCD,CNSCD                                                     
         MVC   ACCTCD,CNACD                                                     
*                                                                               
*                                                                               
         LA    RF,PRINTSES                                                      
GETAG5   DS    0H                                                               
         CLC   1(1,RF),PRNTSE                                                   
         BE    GETAG6                                                           
         LA    RF,2(RF)                                                         
         B     GETAG5                                                           
*                                                                               
GETAG6   DS    0H                                                               
         MVC   FILENUM,0(RF)                                                    
*                                                                               
         XIT1                                                                   
*                                                                               
       ++INCLUDE PRINTSES                                                       
         EJECT                                                                  
*                                                                               
*                                                                               
SPLOAD   LR    R0,RE                                                            
         MVC   DUB,SPACES                                                       
         MVC   DUB(4),=C'T00A'                                                  
         GOTO1 HEXOUT,DMCB,BYTE,DUB+4,1,0                                       
         SPACE 1                                                                
*=========================================================*                     
* SEE IF WE NEED TO LOAD  TEST VERSION OF THIS PHASE      *                     
*=========================================================*                     
         SPACE 1                                                                
         L     R1,MCTSTPL          GET ADDRESS OF CESSPOOL                      
*                                                                               
SPLOAD2  CLI   0(R1),0                                                          
         BE    SPLOAD10                                                         
         CLC   DUB(6),5(R1)        MATCH TEST=T000.. FOR 6                      
         BE    SPLOAD4             NOTE PHASE NAME IS AFTER TEST= !             
         LA    R1,L'MCREQREC(R1)                                                
         B     SPLOAD2                                                          
*                                                                               
SPLOAD4  MVC   DUB(7),5(R1)        ADD VERSION TO PHASE NAME                    
*                                                                               
SPLOAD10 GOTO1 LOADER,DMCB,DUB,0                                                
         BAS   RE,LOADCHEK                                                      
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
         SPACE 2                                                                
LOADCHEK OC    DMCB+4(4),DMCB+4                                                 
         BCR   7,RE                                                             
         MVC   WORK(16),=C'CANT FIND PHASE '                                    
         MVC   WORK+16(8),DUB                                                   
         GOTO1 LOGIO,DMCB,1,(24,WORK)                                           
         GOTO1 (RF),(R1),,=C'PPG IS GIVING UP'                                  
*&&DO*&& CANCEL                                                                 
*&&OS*&& ABEND 999                                                              
         EJECT                                                                  
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* PUT A PRINT LINE TO AN EXTRA OUTPUT FILE FOR LATER PROCESSING       *         
* NTRY:  P1    = A(PRINT LINE)                                        *         
*        P2    = A(PRINT CONTROL CHARACTERS)                          *         
***********************************************************************         
         SPACE 1                                                                
EMLPUT   NTR1  BASE=*,LABEL=*                                                   
         LR    R2,R1                                                            
*                                                                               
         L     RF,=A(EMLFLAG)                                                   
         CLI   0(RF),C'Y'          FILE OPEN ALREADY?                           
         BE    *+8                                                              
         BRAS  RE,EMLOPEN          OPEN IT                                      
*                                                                               
         L     R4,=A(EMLLINE)                                                   
         L     RE,4(R2)                                                         
         MVC   0(4,R4),0(RE)       PRINTER CONTROL CHARACTERS                   
         L     RE,0(R2)                                                         
         MVC   4(132,R4),0(RE)     PRINT LINE                                   
*                                                                               
         L     R3,=A(EMLFILE)                                                   
         PUT   (R3),0(R4)          PUT RECORD TO FILE                           
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* OPEN EMLFILE FOR OUTPUT                                             *         
***********************************************************************         
         SPACE 1                                                                
EMLOPEN  NTR1  BASE=*,LABEL=*                                                   
         L     R3,=A(EMLFILE)                                                   
         OPEN  ((R3),OUTPUT)                                                    
         LTR   RF,RF                                                            
         BZ    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,=A(EMLFLAG)                                                   
         MVI   0(RF),C'Y'          FILE OPEN ALREADY?                           
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
* CLOSE EMLFILE                                                       *         
***********************************************************************         
         SPACE 1                                                                
EMLCLSE  NTR1  BASE=*,LABEL=*                                                   
         L     R3,=A(EMLFILE)                                                   
         CLOSE (R3)                                                             
*                                                                               
         L     RF,=A(EMLFLAG)                                                   
         MVI   0(RF),C'N'          SET FILE CLOSED                              
         XIT1  ,                                                                
*                                                                               
         LTORG                                                                  
*                                                                               
EMLFLAG  DC    C'N'                                                             
*                                                                               
EMLLINE  DC    136C' '                                                          
EMLFILE  DCB   DSORG=PS,MACRF=PM,DDNAME=EMLFILE,RECFM=FB,LRECL=(136)            
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
*                                                                               
       ++INCLUDE DDCNTRL                                                        
*                                                                               
         EJECT                                                                  
       ++INCLUDE DDREMOTED                                                      
         EJECT                                                                  
         PRINT OFF                                                              
       ++INCLUDE DDCOREQUS                                                      
       ++INCLUDE DDREPMASTD                                                     
       ++INCLUDE DDLOGOD                                                        
       ++INCLUDE PPREPWORK                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004PPRUNCONS 05/01/02'                                      
         END                                                                    
