*          DATA SET FAREPORT   AT LEVEL 002 AS OF 03/11/14                      
*CATALP FAREPORT                                                                
         TITLE 'FAREPORT - FACPAK REPORT WRITER'                                
REPORT   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 REPWRKX-REPWRKD,**FAREP,RA,RR=RE,CLEAR=YES                       
         USING REPWRKD,RC          RC=A(LOCAL WS)                               
*                                                                               
         ST    RE,RELO                                                          
         MVC   REGS,4(RD)                                                       
         LR    R9,R1                                                            
         USING REPBLK,R9           R9=A(REPORT CONTROL BLOCK)                   
         L     R1,REPACOM                                                       
         USING COMFACSD,R1         EXTRACT ADDRESSES FROM COMFACS               
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VDATCON,CDATCON                                                  
         MVC   VDICTATE,CDICTATE                                                
         MVC   VPQPROF,CPQPROF                                                  
         MVI   REPERRS,0           RESET ERROR RETURN BYTE                      
*                                                                               
HEADCHK  CLI   REPFHEAD,C'Y'       CHECK IF OLD STYLE HEADLINE FORCE            
         BNE   HEADCHKX                                                         
         MVI   REPFHEAD,C'N'       RESET INDICATOR                              
         OI    REPHEADI,REPHFRCE   FORCE HEADLINE PRINTING                      
         CLI   REPHEADH,0          FORCE HEAD HOOK NUMBER IF MISSING            
         BNE   *+8                                                              
         MVI   REPHEADH,1          HOOK IS 1 BY DEFAULT                         
HEADCHKX DS    0H                                                               
*                                                                               
MIDSCHK  CLI   REPFMIDS,C'Y'       CHECK IF OLD STYLE HEADLINE FORCE            
         BNE   MIDSCHKX                                                         
         MVI   REPFMIDS,C'N'       RESET INDICATOR                              
         OI    REPMIDSI,REPMFRCE   FORCE MID LINE PRINTING                      
         CLI   REPMIDSH,0          FORCE MIDS HOOK NUMBER IF MISSING            
         BNE   *+8                                                              
         MVI   REPMIDSH,2          HOOK IS 2 BY DEFAULT                         
MIDSCHKX DS    0H                                                               
*                                                                               
FOOTCHK  CLI   REPFFOOT,C'Y'       CHECK IF OLD STYLE FOOTING FORCE             
         BNE   FOOTCHKX                                                         
         MVI   REPFFOOT,C'N'       RESET INDICATOR                              
         OI    REPFOOTI,REPFFRCE   FORCE FOOTING  PRINTING                      
         CLI   REPFOOTH,0          FORCE FOOT HOOK NUMBER IF MISSING            
         BNE   *+8                                                              
         MVI   REPFOOTH,2          HOOK IS 3 BY DEFAULT                         
FOOTCHKX DS    0H                                                               
*                                                                               
SETLANG  LLC   RF,REPLANG          INDEX INTO LANGUAGE TABLE                    
         SLL   RF,2                                                             
         L     RF,LANGNDX(RF)                                                   
         A     RF,RELO                                                          
         TM    REPIND2,REPILOW     TEST LOWER CASE REQUESTED                    
         BZ    *+8                                                              
         LA    RF,LPHRASEL(RF)                                                  
         MVC   LPHRASES(LPHRASEL),0(RF)                                         
*                                                                               
SETLANGX LLC   RF,REPACTN          GO TO ACTION ROUTINE                         
         SLL   RF,2                                                             
         B     *+0(RF)                                                          
         B     INI                                                              
         B     OPN                                                              
         B     CLR                                                              
         B     PUT                                                              
         B     CLO                                                              
         B     CLOP                                                             
         B     CLOE                                                             
         B     CLOU                                                             
         B     ROP                                                              
         B     EXIT                                                             
         B     EXIT                                                             
         B     EXIT                                                             
         B     EXIT                                                             
         B     EXIT                                                             
         B     EXIT                                                             
*                                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALIZE REPBLK                                        *         
***********************************************************************         
INI      MVI   REPIND1,0                                                        
         CLI   REPWIDTH,0          SET DEFAULT REPORT WIDTH                     
         BNE   *+8                                                              
         MVI   REPWIDTH,REPWREGQ                                                
         CLI   REPMAXL,0           SET DEFAULT MAXLINES VALUE                   
         BNE   *+8                                                              
         MVI   REPMAXL,REPMAXLQ                                                 
         MVC   REPLINE,REPMAXL                                                  
         MVC   REPPAGE,=H'1'       RESET LINE & PAGE NUMBERS                    
*                                                                               
         ICM   R2,15,REPAMST       TEST MASTC RESOLVED                          
         BNZ   INI2                                                             
         OI    REPIND1,REPIONL+REPIREM                                          
         B     INI10                                                            
*                                                                               
         USING MASTD,R2                                                         
INI2     OI    REPIND1,REPIOFF     SET OFFLINE PROCESSING                       
         GOTOR VDATCON,DMCB,(4,MCDATE),(3,REPDATE)                              
         MVC   REPUSRN,MCORIGIN                                                 
         MVC   REPUSRA,MCORIGAD                                                 
         MVC   REPSYSID,MCSYSTEM                                                
         MVC   REPPRGID,MCPROG                                                  
         MVC   REPUSRID,MCORIGID                                                
         MVC   REPABOX,MCBXAREA                                                 
         L     R1,MCVLOGOC                                                      
         USING LOGOD,R1                                                         
         MVC   REPAPRT,LOGOPRNT                                                 
         OC    MCREMPQK,MCREMPQK   TEST SOON REPORT                             
         BZ    INI4                                                             
         MVC   REPUSRID(L'MCREMPQK),MCREMPQK                                    
         OI    REPIND1,REPINOT     SET NOTIFY PENDING (SOON JOB)                
*                                                                               
INI4     L     R1,MCVREMOT                                                      
         USING REMOTED,R1                                                       
         MVC   REPAPQB,REMOTABF                                                 
         OC    REMOTKEY,REMOTKEY                                                
         BZ    *+12                                                             
         OI    REPIND1,REPIREM     SET REPORT TO PRINT QUEUE                    
         B     *+8                                                              
         OI    REPIND1,REPILOC     SET REPORT PRINTING AT DDS                   
         ICM   R1,15,REPABOX                                                    
         BZ    INI10                                                            
         USING BOXD,R1                                                          
         MVC   BOXWIDTH+3(1),REPWIDTH                                           
         OI    BOXDDCTL,BOXDDREQ                                                
         MVC   BOXSYS,MCSYSTEM                                                  
         MVC   BOXCTRY,MCCTRY                                                   
         MVC   BOXLANG,MCLANG                                                   
         DROP  R1,R2                                                            
*                                                                               
INI10    LA    R2,REPHEADN         SET ADDRESS VALUES                           
         L     R3,REPABUF                                                       
         LA    R0,4                R0=LINE TYPE COUNT                           
         SR    R1,R1               R1=TOTAL NUMBER OF LINES                     
INI12    XC    4(4,R2),4(R2)                                                    
         SR    RE,RE                                                            
         ICM   RE,1,0(R2)                                                       
         BZ    INI14                                                            
         AR    R1,RE                                                            
         ST    R3,4(R2)            SET A(FIRST LINE)                            
         LLC   RF,REPWIDTH                                                      
         MR    RE,RE                                                            
         AR    R3,RF                                                            
INI14    LA    R2,8(R2)                                                         
         BCT   R0,INI12                                                         
         STC   R1,REPTOTSN         SET TOTAL NUMBER OF LINES                    
         LTR   R1,R1                                                            
         BZ    INI16                                                            
*                                                                               
         LLC   R0,REPWIDTH         CLEAR ALL LINES TO SPACES                    
         MR    R0,R0                                                            
         L     R0,REPABUF                                                       
         SR    RF,RF                                                            
         ICM   RF,8,SPACES                                                      
         MVCL  R0,RE                                                            
*&&DO                                                                           
INI16    LA    R0,REPRLHQ1         SET DEFAULT RETENTION IF NOT SET             
         LA    R1,REPRDHQ1                                                      
         TM    REPIND1,REPIONL                                                  
         BNZ   *+12                                                             
         LA    R0,REPRLHQ2                                                      
         LA    R1,REPRDHQ2                                                      
         OC    REPRLH,REPRLH                                                    
         BNZ   *+8                                                              
         STCM  R0,3,REPRLH                                                      
         OC    REPRDH,REPRDH                                                    
         BNZ   *+8                                                              
         STCM  R1,3,REPRDH                                                      
*&&                                                                             
INI16    OI    REPHEADI,REPHFRCE   FORCE HEAD LINE PRINTING NEXT                
         OI    REPIND1,REPINIT     SET REPBLK INITIALIZED                       
*                                                                               
INI18    OC    REPAGCOP(4),REPAGCOP SET CTRY/LANG INFO IF NOT SET               
         BNZ   INI18X                                                           
         CTRY                                                                   
         STCM  R0,15,REPAGCOP                                                   
INI18X   EQU   *                                                                
*                                                                               
INIX     B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* OPEN PRINT QUEUE REPORT                                             *         
***********************************************************************         
OPN      LA    RE,EXIT             DUMMY ENTRY                                  
*                                                                               
OPNPQ    NTR1                                                                   
         TM    REPIND1,REPIOFF     TEST OFFLINE PROCESSING                      
         BNZ   OPNPQY                                                           
         TM    REPIND1,REPIREM     TEST GOING TO PRINT QUEUE                    
         BZ    OPNPQY                                                           
         TM    REPIND1,REPIOPN                                                  
         JNZ   *+2                                                              
         LA    R2,P                                                             
         USING PQPLD,R2            R2=A(PRINT QUEUE OPEN INFO)                  
         XC    PQPLD(QLSOFEND-PQPLD),PQPLD                                      
         MVI   QLEXTRA,FF          SET EXTENDED OPEN LIST                       
         MVI   QLFLAG,QLFLDD                                                    
         MVC   QLSRCID,REPUSRID                                                 
         MVC   QLSUBID,REPSUBID                                                 
         MVC   QLCLASS,REPCLASS                                                 
         MVC   QLTYPE,REPTYPE                                                   
         MVC   QLATTB,REPATTB                                                   
         MVC   QLSTAT,REPSTAT                                                   
         MVC   QLDESC,REPDESC                                                   
         MVC   QLFORMS,REPFORM                                                  
         MVC   QLCHARS,REPCHAR                                                  
         MVC   QLCOPIES,REPCOPY                                                 
         MVC   QLPSWD,REPPSWD                                                   
         TM    REPSECF1,REPSNONO   TEST IF VALID SECURITY FLAG                  
         BZ    *+16                YES                                          
         MVI   QLSECF1,0           NO CLEAR SECURITY FLAGS                      
         MVI   QLSECF2,0                                                        
         B     *+16                                                             
         MVC   QLSECF1,REPSECF1                                                 
         MVC   QLSECF2,REPSECF2                                                 
         MVC   QLRETNL,REPRLH                                                   
         MVC   QLRETND,REPRDH                                                   
         MVC   QLMAKER,REPMAKER                                                 
         MVI   QLLINET,X'C0'                                                    
         MVC   QLLINEW,REPWIDTH                                                 
         MVC   QLLPP,REPMAXL                                                    
         MVC   QLREPTY,REPRTY                                                   
         MVC   QLARC,REPARC                                                     
         MVC   QLTYP1,REPTYP1                                                   
         CLI   REPARC,C'A'         SET ARCHIVABLE IF VALID ARC CLASS            
         BL    *+16                                                             
         TM    QLTYP1,QLTYAE+QLTYAR+QLTYAD                                      
         BNZ   *+8                                                              
         OI    QLTYP1,QLTYAR                                                    
*                                                                               
OPNPQ1   OC    QLMAKER(3),QLMAKER  TEST IF SPP SET BY CALLER                    
         BNZ   OPNPQ2                                                           
         CLI   REPSYSID,C' '       TEST IF SYSTEM CODE SET                      
         BNH   OPNPQ3              NO                                           
         MVC   QLSYS,REPSYSID      YES SET SYSTEM CODE 1ST CHR                  
         CLC   REPSYSID,=C'MP'     ADJUST IF 1ST CHR NOT THE SAME               
         BNE   *+8                                                              
         MVI   QLSYS,C'L'                                                       
         CLC   REPSYSID,=C'MB'                                                  
         BNE   *+8                                                              
         MVI   QLSYS,C'B'                                                       
         CLC   REPSYSID,=C'PE'                                                  
         BNE   *+8                                                              
         MVI   QLSYS,C'E'                                                       
         MVC   QLPRG,REPPRGID                                                   
         CLI   QLPRG,C' '          TEST IF PROGRAM CODE SET                     
         BH    OPNPQ2              YES USE IT                                   
         MVC   QLPRG,=C'XX'        NO SET DEFAULT UNKNOWN PROGRAM CODE          
         B     OPNPQ3                                                           
OPNPQ2   OC    QLMAKER+3(2),QLMAKER+3                                           
         BZ    OPNPQ2A                                                          
         TM    QLTYPE,QLTYSQL                                                   
         BZ    OPNPQ3                                                           
         CLC   REPSQL,SPACES                                                    
         BH    OPNPQ3                                                           
OPNPQ2A  ICM   RF,15,VPQPROF                                                    
         BZ    OPNPQ3                                                           
         MVC   DMCB+8(4),REPACOM   EXTRACT REPORT PROFILE                       
         ST    R2,DMCB+4                                                        
         MVI   DMCB+4,1            PASS PQLD FORMAT                             
         LA    R1,DMCB+16                                                       
         ST    R1,DMCB                                                          
         XC    0(8,R1),0(R1)       SET PROFILE KEY SPPUU                        
         MVC   0(3,R1),QLMAKER                                                  
         MVC   3(2,R1),REPUSRID                                                 
         LA    R1,DMCB                                                          
         BASR  RE,RF                                                            
         MVI   GOTPROF,1           SET PROFILE READ                             
*                                                                               
OPNPQ3   TM    QLTYPE,QLTYSQL      SET SPECIAL SQL DESCRIPTION                  
         BZ    OPNPQ3A                                                          
         MVC   QLDESC,SPACES                                                    
         MVC   QLDESC(3),QLMAKER                                                
         CLC   REPSQL,SPACES                                                    
         BNH   OPNPQ4                                                           
         MVI   QLDESC+5,C'@'       SET SQL TRANFORM CODE IN DESCRIPTION         
         MVC   QLDESC+6(5),REPSQL                                               
         B     OPNPQ4                                                           
OPNPQ3A  OC    QLDESC,QLDESC       SET DEFAULT REPORT DESCRIPTION               
         BNZ   OPNPQ4                                                           
         MVC   QLDESC,SPACES                                                    
         MVC   QLDESC(3),QLMAKER                                                
*                                                                               
OPNPQ4   GOTOR VDATAMGR,DMCB,OPEN,PRTQUE,0,PQPLD,REPAPQB                        
         MVC   REPERRS,8(R1)                                                    
         CLI   REPERRS,0                                                        
         BNE   OPNPQN                                                           
         MVC   REPREPNO,QLREPRNO                                                
         MVC   REPREPCI,QLREPRCI   TTTT OF 0TTTTT                               
         MVC   REPREPCF,QLREPRCI-1 0T OF 0TTTTT                                 
*                                                                               
OPNPQY   OI    REPIND1,REPIOPN     SET REPORT HAS BEEN OPENED                   
         CLI   *+1,0               SET CC=EQUAL IF NO ERRORS                    
         B     EXIT                                                             
*                                                                               
OPNPQN   CLI   *+0,0               SET CC=NOT EQUAL ON ERROR                    
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* RE-OPEN PRINT QUEUE REPORT                                          *         
***********************************************************************         
ROP      LA    RE,EXIT             DUMMY ENTRY                                  
*                                                                               
ROPPQ    NTR1                                                                   
         TM    REPIND1,REPIOFF     TEST OFFLINE PROCESSING                      
         BNZ   ROPPQY                                                           
         TM    REPIND1,REPIREM     TEST GOING TO PRINT QUEUE                    
         BZ    ROPPQY                                                           
         TM    REPIND1,REPIOPN     TEST ALREADY OPEN                            
         JNZ   *+2                                                              
         OC    REPUSRID,REPUSRID   MUST PASS USERID NUMBER                      
         JZ    *+2                                                              
*                                                                               
ROPPQ2   MVI   FLAG1,QLFLCIDA      SET PASSING CI DISK ADDRESS                  
         XC    FULL1,FULL1                                                      
         XC    FULL2,FULL2                                                      
         MVC   FULL1+1(1),REPREPCF                                              
         MVC   FULL1+2(2),REPREPCI                                              
         OC    FULL1,FULL1         FULL1=REPORT CIADDR                          
         BNZ   ROPPQ4                                                           
         MVI   FLAG1,QLFLKEY       SET PASSING REPORT NUMBER                    
         MVC   FULL2+2(2),REPREPNO                                              
         OC    FULL2,FULL2         FULL2=REPORT NUMBER                          
         JZ    *+2                 MUST PASS CIADDR OR REPORT NUMBER            
*                                                                               
ROPPQ4   LA    R2,P                                                             
         USING PQPLD,R2            R2=A(PRINT QUEUE OPEN INFO)                  
         XC    PQPLD(QLSOFEND-PQPLD),PQPLD                                      
         MVI   QLEXTRA,FF          SET EXTENDED OPEN LIST                       
         MVI   QLFLAG,QLFLRALL+QLFLRDT+QLFLRRE+QLFLDD                           
         OC    QLFLAG,FLAG1        SET PASSING DISKADDR/REPORTID                
         MVC   QLSRCID,REPUSRID                                                 
         MVC   QLREPRCI-1(3),FULL1+1                                            
         MVC   QLREPNO,FULL2+2                                                  
         GOTOR VDATAMGR,DMCB,OPEN,PRTQUE,0,PQPLD,REPAPQB                        
         MVC   REPERRS,8(R1)                                                    
         CLI   REPERRS,0                                                        
         BNE   ROPPQN                                                           
         MVC   REPREPNO,QLREPRNO   EXTRACT VALUES INTO REPBLK                   
         MVC   REPUSRID,QLSRCID                                                 
         MVC   REPSUBID,QLSUBID                                                 
         MVC   REPCLASS,QLCLASS                                                 
         MVC   REPTYPE,QLTYPE                                                   
         MVC   REPATTB,QLATTB                                                   
         MVC   REPSTAT,QLSTAT                                                   
         MVC   REPDESC,QLDESC                                                   
         MVC   REPFORM,QLFORMS                                                  
         MVC   REPCHAR,QLCHARS                                                  
         MVC   REPCOPY,QLCOPIES                                                 
         MVC   REPPSWD,QLPSWD                                                   
         MVC   REPSECF1,QLSECF1                                                 
         MVC   REPSECF2,QLSECF2                                                 
         MVC   REPRTY,QLREPTY                                                   
         MVC   REPARC,QLARC                                                     
         MVC   REPTYP1,QLTYP1                                                   
*                                                                               
ROPPQY   OI    REPIND1,REPIOPN     SET REPORT HAS BEEN (RE)OPENED               
         CLI   *+1,0               SET CC=EQUAL IF NO ERRORS                    
         B     EXIT                                                             
*                                                                               
ROPPQN   CLI   *+0,0               SET CC=NOT EQUAL ON ERROR                    
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* CLOSE PRINT QUEUE REPORT                                            *         
***********************************************************************         
CLO      TM    REPIND2,REPIBOX+REPIBOXI                                         
         BNO   CLO02                                                            
         MVI   CONTROL,CONTDONE                                                 
         LA    R1,SPACES                                                        
         GOTOR PRT                                                              
CLO02    LA    R0,CLOSE                                                         
         B     CLOPQ                                                            
CLOP     LA    R0,CLOPUR                                                        
         B     CLOPQ                                                            
CLOE     LA    R0,CLOERR                                                        
         B     CLOPQ                                                            
CLOU     LA    R0,CLOUSR                                                        
         B     CLOPQ                                                            
*                                                                               
CLOPQ    TM    REPIND1,REPIREM     TEST REPORT TO PRINT QUEUE                   
         BZ    CLOPQX                                                           
         TM    REPIND1,REPIOFF     TEST OFFLINE                                 
         BZ    CLOPQ2                                                           
         TM    REPIND1,REPIPUT     TEST ANY LINES OUTPUT                        
         BNZ   CLOPQX                                                           
         MVI   PC,CHANNEL1         PUT SKIP TO CHANNEL 1                        
         MVC   P,SPACES                                                         
         GOTOR VDATAMGR,DMCB,PRINT,PRTQUE,0,PC,REPAPQB                          
         OI    REPIND1,REPIPUT                                                  
         B     CLOPQX                                                           
*                                                                               
CLOPQ2   TM    REPIND1,REPIPUT     TEST ANY LINES PUT                           
         BZ    CLOPQX                                                           
*                                                                               
CLOPQ4   LA    R2,P                                                             
         USING UKRECD,R2                                                        
         MVC   UKUSRINF,REPUSER                                                 
         GOTOR VDATAMGR,DMCB,(R0),PRTQUE,UKRECD,P,REPAPQB                       
         MVC   REPERRS,8(R1)                                                    
         CLI   REPERRS,0                                                        
         BNE   CLOPQX                                                           
         L     R2,REPAPQB                                                       
         USING PQRECD,R2                                                        
         MVC   REPLINES,PQLINES                                                 
         MVC   REPPAGES,PQPAGES                                                 
         TM    PQTYPE,PQTYNEW                                                   
         BO    *+16                                                             
         MVC   REPLINES,OQLINES                                                 
         MVC   REPPAGES,OQPAGES                                                 
*                                                                               
CLOPQX   NI    REPIND1,255-REPIOPN SET REPORT NOT OPEN                          
         CLI   REPERRS,0           SET CC FOR CALLER                            
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
***********************************************************************         
* CLEAR ALL PRINT LINES IN BUFFER                                     *         
***********************************************************************         
CLR      SR    R1,R1                                                            
         ICM   R1,1,REPTOTSN                                                    
         BZ    CLRX                                                             
         LLC   R0,REPWIDTH                                                      
         MR    R0,R0                                                            
         L     R0,REPABUF                                                       
         SR    RF,RF                                                            
         ICM   RF,8,SPACES                                                      
         MVCL  R0,RE                                                            
CLRX     B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* PUT A LINE (AND PROCESS SPECS)                                      *         
***********************************************************************         
PUT      TM    REPIND1,REPIOPN     TEST REPORT HAS BEEN OPENED                  
         BNZ   *+12                                                             
         GOTOR OPNPQ               NO - OPEN IT                                 
         BNE   PUTX                                                             
         TM    REPIND2,REPISKP     TEST SKIP SPEC PROCESSING                    
         BNZ   PUTH                                                             
                                                                                
***********************************************************************         
* HANDLE FOOTLINE PRINTING                                            *         
***********************************************************************         
PUTF     TM    REPFOOTI,REPFFRCE   TEST FORCE FOOT LINES                        
         BZ    FITS                                                             
         NI    REPFOOTI,255-REPFFRCE                                            
         CLI   REPFOOTN,0          TEST FOOT LINES SET                          
         BE    PUTFX                                                            
         LLC   R1,REPMAXL                                                       
         LLC   R0,REPFOOTN                                                      
         SR    R1,R0                                                            
         LA    R1,1(R1)                                                         
         IC    R0,REPLINE                                                       
         SR    R1,R0                                                            
         BNP   PUTFX                                                            
         MVI   CONTROL,CONTLINE                                                 
         STC   R1,CONTROL+1                                                     
         MVC   P,SPACES                                                         
         LA    R1,P                                                             
         GOTOR PRT                 ADVANCE TO FOOT LINES                        
*                                                                               
         MVI   CONTROL,CONTLINE                                                 
         MVI   CONTROL+1,1                                                      
         MVC   HOOKN,REPFOOTH                                                   
         MVC   FLAG,REPFOOTI                                                    
         MVI   SPACING,0                                                        
         IC    R0,REPFOOTN         R0=NUMBER OF FOOT LINES                      
         ICM   R1,15,REPAFOOT      R1=A(FOOT LINES)                             
         BNZ   PUTF2                                                            
*                                                                               
         CLI   HOOKN,0             TEST FOOT LINE HOOK SET                      
         BE    PUTFX                                                            
         LA    R1,P                POINT TO FOOTLINE RETURN AREA                
         GOTOR HOOK                CALL USER HOOK                               
         GOTOR PRT                                                              
         BCT   R0,*-4              DO FOR NUMBER OF FOOT LINES                  
         B     PUTFX                                                            
*                                                                               
PUTF2    CLI   HOOKN,0             TEST FOOT LINE HOOK SET                      
         BE    *+8                                                              
         GOTOR HOOK                CALL USER HOOK                               
         GOTOR PRTB                                                             
*                                                                               
PUTFX    MVI   REPFFOOT,C'N'       RESET INDICATOR - DRIVER SETS IT ON          
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* CHECK IF LINES FIT ON THIS PAGE                                     *         
***********************************************************************         
FITS     TM    REPHEADI,REPHFRCE                                                
         BNZ   SPEC                                                             
         TM    REPMIDSI,REPMFRCE                                                
         BNZ   SPEC                                                             
         LLC   R1,REPLINE          CHECK LINE+ALLOW+FEET FITS                   
         LLC   R0,REPALLN                                                       
         AR    R1,R0                                                            
         ICM   R0,1,REPFOOTN                                                    
         BZ    *+12                                                             
         LA    R1,1(R1)            ALLOW FOR AT LEAST ONE SPACE TO FEET         
         B     *+8                                                              
         LA    R0,1                                                             
         AR    R1,R0                                                            
         IC    R0,REPMAXL                                                       
         CR    R1,R0                                                            
         BNL   FITS6                                                            
         ICM   R1,15,REPAPRNT      CHECK PRINT LINES WILL FIT                   
         BNZ   *+12                                                             
         IC    R0,REPPRNTN                                                      
         B     FITS4                                                            
         SR    RE,RE               CALCULATE NUMBER OF PRINT LINES              
         ICM   RE,1,REPPRNTN                                                    
         JZ    *+2                                                              
         BCTR  RE,0                RE=N'PRINT LINES-1                           
         LLC   RF,REPWIDTH                                                      
         MR    RE,RE                                                            
         AR    R1,RF               R1=A(LAST PRINT LINE)                        
         IC    RE,REPWIDTH                                                      
         LR    RF,RE               RF=LINE WIDTH                                
         BCTR  RE,0                RE=LINE WIDTH-1                              
         IC    R0,REPPRNTN         R0=N'PRINT LINES                             
*                                                                               
FITS2    EX    RE,*+8                                                           
         BNE   FITS4                                                            
         CLC   0(0,R1),SPACES                                                   
         SR    R1,RF               YES - BACK UP TO PREVIOUS LINE               
         BCT   R0,FITS2                                                         
         LA    R0,1                                                             
*                                                                               
FITS4    LLC   R1,REPLINE                                                       
         AR    R0,R1                                                            
         ICM   R1,1,REPFOOTN                                                    
         BNZ   *+8                                                              
         LA    R1,1                                                             
         AR    R0,R1                                                            
         ICM   R1,1,REPPRNSA       ADD ON SPACING                               
         BNZ   *+8                                                              
         LA    R1,1                                                             
         BCTR  R1,0                R1=SPACING-1                                 
         AR    R0,R1                                                            
         CLM   R0,1,REPMAXL                                                     
         BNH   PUTP                                                             
*                                                                               
FITS6    CLI   REPEOPH,0                                                        
         BE    FITSX                                                            
         MVC   HOOKN,REPEOPH                                                    
         GOTOR HOOK                CALL USER END-OF-PAGE HOOK                   
*                                                                               
FITSX    OI    REPHEADI,REPHFRCE   SET FORCE HEADLINE PRINTING                  
         EJECT                                                                  
***********************************************************************         
* PROCESS USER SPEC POOL                                              *         
***********************************************************************         
SPEC     ICM   R2,15,REPAPHS       PROCESS SPEC POOL                            
         BZ    PUTH                                                             
         MVI   SWITCH,1                                                         
         CLI   REPSUBPG,0                                                       
         BE    *+8                                                              
         MVI   SWITCH,0            SET SUB-PROGRAM SET                          
         SR    RF,RF                                                            
*                                                                               
SPEC2    ICM   RF,1,0(R2)          GET SPEC INDEX VALUE                         
         BZ    PUTH                                                             
         SLL   RF,2                                                             
         L     RF,SPECTAB-4(RF)    GET SPEC ROUTINE ADDRESS                     
         A     RF,RELO                                                          
         BASR  RE,RF                                                            
*                                                                               
SPEC4    LLC   RF,1(R2)            BUMP TO NEXT SPEC                            
         AR    R2,RF                                                            
         B     SPEC2                                                            
*                                                                               
SPECTAB  DS    0F                  ** TABLE OF SPEC ROUTINES **                 
         DC    A(RESET)            1                                            
         DC    A(SPROG1)           2                                            
         DC    A(KEYWORD)          3                                            
         DC    A(LITERAL)          4                                            
         DC    A(ACTION)           5                                            
         DC    A(0)                6                                            
         DC    A(0)                7                                            
         DC    A(SPROG2)           8                                            
         DC    A(0)                9                                            
         DC    A(0)                10                                           
         DC    A(0)                11                                           
         DC    A(0)                12                                           
         DC    A(0)                13                                           
         DC    A(0)                14                                           
         DC    A(0)                15                                           
         DC    A(0)                16                                           
         EJECT                                                                  
***********************************************************************         
* SPEC SUB-ROUTINES                                                   *         
***********************************************************************         
RESET    MVI   SWITCH,0            RESET SUB-PROGRAM                            
         BR    RE                                                               
*                                                                               
SPROG1   CLC   REPSUBPG,2(R2)      MATCH SUB-PROGRAM                            
         BNER  RE                                                               
         MVI   SWITCH,1                                                         
         BR    RE                                                               
*                                                                               
SPROG2   CLC   REPSUBPG,2(R2)      SPROG N THRU M                               
         BLR   RE                                                               
         CLC   REPSUBPG,3(R2)                                                   
         BHR   RE                                                               
         MVI   SWITCH,1                                                         
         BR    RE                                                               
*                                                                               
ACTION   CLI   SWITCH,1                                                         
         BNER  RE                                                               
         CLI   2(R2),16            TEST IF A HOOK                               
         BH    ACTION2                                                          
         LR    R0,RE                                                            
         MVC   HOOKN,2(R2)         SET HOOK NUMBER                              
         GOTOR HOOK                                                             
         LR    RE,R0                                                            
         BR    RE                                                               
ACTION2  DC    H'0'                                                             
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* HANDLE LITERALS AND KEYWORDS                                        *         
***********************************************************************         
LITERAL  DS    0H                                                               
KEYWORD  CLI   SWITCH,1                                                         
         BNER  RE                                                               
         NTR1                                                                   
         SR    RE,RE                                                            
         ICM   RE,1,2(R2)                                                       
         SRDL  RE,4                RE=LINE TYPE                                 
         SRL   RF,32-4             RF=LINE NUMBER                               
         SLL   RE,3                                                             
         LA    RE,REPDEFN-8(RE)                                                 
         OC    4(4,RE),4(RE)       TEST LINE BLOCK ADDRESS SET                  
         BZ    KEYWORDX                                                         
         CLM   RF,1,0(RE)          TEST LINE NUMBER WITHIN BLOCK                
         BH    KEYWORDX                                                         
         L     R1,4(RE)            R1=A(LINE BLOCK)                             
         LLC   RE,REPWIDTH                                                      
         LR    R0,RE                                                            
         BCTR  RF,0                RF=LINE NUMBER-1                             
         MR    RE,RE                                                            
         AR    R1,RF               R1=A(DESTINATION PRINT LINE)                 
         AR    R0,R1               R0=A(NEXT PRINT LINE)                        
         LLC   RF,3(R2)                                                         
         BCTR  RF,0                RF=COLUMN NUMBER-1                           
         AR    R1,RF               R1=A(OUTPUT)                                 
         CLI   0(R2),4             TEST LITERAL VALUE                           
         BNE   KEYWORD4                                                         
         LLC   RE,1(R2)                                                         
         AHI   RE,-4               RE=L'LITERAL                                 
*                                                                               
KEYWORD2 BCTR  RE,0                                                             
         LA    RF,0(RE,R1)         RF=A(END OF LITERAL IN LINE)                 
         CR    RF,R0               TEST LITERAL IS WITHIN PRINT LINE            
         BNL   KEYWORD2            NO - TRUNCATE LITERAL TO FIT                 
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),4(R2)                                                    
         CLI   0(R1),ESCLOQ        TEST FOR ESCAPE SEQUENCE                     
         BL    KEYWORDX                                                         
         CLI   0(R1),ESCHIQ                                                     
         BH    KEYWORDX                                                         
         LR    RF,R1               YES - CALL DICTATE TO TRANSLATE              
         ICM   R0,15,=C'SU  '                                                   
         TM    REPIND2,REPILOW                                                  
         BZ    *+8                                                              
         ICM   R0,4,=C'L'                                                       
         TM    REPIND2,REPIBOX     TEST BOX PRINTING                            
         BZ    *+8                                                              
         ICM   R0,1,=X'80'         YES - USE LASER UNDERSCORE                   
         GOTOR VDICTATE,DMCB,(R0),(RF),0                                        
         B     KEYWORDX                                                         
*                                                                               
KEYWORD4 LLC   RF,4(R2)            RF=KEYWORD NUMBER                            
         SLL   RF,2                                                             
         L     RF,KEYTAB-4(RF)                                                  
         LTR   RF,RF               TEST KEYWORD RESOLVED                        
         BZ    KEYWORDX                                                         
         A     RF,RELO                                                          
         BASR  RE,RF               CALL KEYWORD SUB-ROUTINE                     
*                                                                               
KEYWORDX B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* HANDLE SPEC KEYWORDS                                                *         
***********************************************************************         
KEYTAB   DS    0F                  ** TABLE OF KEYWORD ROUTINES **              
         DC    A(KSPACE)                                                        
         DC    A(KRUN)                                                          
         DC    A(KREPORT)                                                       
         DC    A(KPAGE)                                                         
         DC    A(KND)                                                           
         DC    A(KRQSTR)                                                        
         DC    A(KRUN)                                                          
         DC    A(KAGYNAM)                                                       
         DC    A(KAGYADD)                                                       
         DC    A(KRQSTR)                                                        
         DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(0)                                                             
         DC    A(0)                                                             
*                                                                               
KSPACE   LLC   RF,REPWIDTH                                                      
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),SPACES                                                   
         BR    RE                                                               
*                                                                               
KRUN     NTR1                                                                   
         LR    R3,R1                                                            
         LLC   RF,LRUNDATE                                                      
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),LRUNDATE+1                                               
         LA    RF,1(R3,RF)                                                      
         GOTOR VDATCON,DMCB,(X'03',REPDATE),(X'11',(RF))                        
*                                                                               
         LA    R1,18                                                            
         CLI   4(R2),2             TEST RUN KEYWORD                             
         BNE   *+8                                                              
         IC    R1,REPWIDTH         YES - TIME ON LINE 2                         
         AR    R3,R1                                                            
         LLC   RF,LRUNTIME                                                      
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),LRUNTIME+1                                               
         LA    R3,1(R3,RF)                                                      
         THMS                                                                   
         SRL   R1,8                                                             
         ST    R1,DUB+4                                                         
         XC    DUB(4),DUB                                                       
         OI    DUB+7,X'0F'                                                      
         CVB   R0,DUB                                                           
         EDIT  (R0),(5,(R3)),2,FILL=0                                           
         MVI   2(R3),C':'                                                       
KRUNX    B     EXIT                                                             
*                                                                               
KREPORT  LLC   RF,LREPORT                                                       
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),LREPORT+1                                                
         LA    R1,1(R1,RF)                                                      
         MVC   0(L'REPSYSID,R1),REPSYSID                                        
         MVC   L'REPSYSID(L'REPPRGID,R1),REPPRGID                               
         BR    RE                                                               
*                                                                               
KPAGE    LLC   RF,LPAGE                                                         
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),LPAGE+1                                                  
         LA    R1,1(R1,RF)                                                      
         EDIT  (B2,REPPAGE),(5,(R1)),ALIGN=LEFT                                 
         BR    RE                                                               
*                                                                               
KND      BR    RE                  USED TO BE KPERIOD - N/D NOW                 
*                                                                               
KRQSTR   LLC   RF,LRQSTR                                                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),LRQSTR+1                                                 
         LA    R1,1(R1,RF)                                                      
         MVC   0(L'REPSUBID,R1),REPSUBID                                        
         CLI   4(R2),10            TEST REQUESTOR NAME ONLY                     
         BER   RE                                                               
         OC    REPREPNO,REPREPNO                                                
         BZR   RE                                                               
         MVI   3(R1),C','                                                       
         SR    R0,R0                                                            
         ICM   R0,3,REPREPNO                                                    
         EDIT  (R0),(5,4(R1)),ALIGN=LEFT                                        
         BR    RE                                                               
*                                                                               
KAGYNAM  MVC   0(L'REPUSRN,R1),REPUSRN                                          
         BR    RE                                                               
*                                                                               
KAGYADD  MVC   0(L'REPUSRA,R1),REPUSRA                                          
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
* HANDLE HEADLINE PRINTING                                            *         
***********************************************************************         
PUTH     TM    REPHEADI,REPHFRCE   TEST FORCE HEADLINE PRINTING                 
         BZ    PUTM                                                             
         NI    REPHEADI,255-REPHFRCE                                            
*                                                                               
PUTH2    OI    REPMIDSI,REPMFRCE   SET FORCE MIDLINE PRINTING                   
         CLI   REPFOOTN,0          TEST ANY FOOTLINES TO PRINT                  
         BE    PUTH4                                                            
         OI    REPFOOTI,REPFFRCE   FORCE FOOTLINE PRINTING                      
         LA    RF,REPORT                                                        
         LA    R1,REPBLKD                                                       
         BASR  RE,RF               RECURSIVE CALL TO PRINT FOOT LINES           
*                                                                               
PUTH4    MVC   P,SPACES                                                         
         MVI   CONTROL,CONTCHAN                                                 
         MVI   CONTROL+1,1                                                      
         LA    R1,P                                                             
         GOTOR PRT                                                              
         OC    REPMAXP,REPMAXP                                                  
         BZ    PUTH6                                                            
         CLC   REPPAGE,REPMAXP                                                  
         JH    *+2                 TOO MANY PAGES PRINTED                       
*                                                                               
PUTH6    MVI   CONTROL,CONTLINE                                                 
         CLI   REPTOPSA,0          PRINT SPACE LINES AT TOP OF PAGE             
         BE    PUTH7                                                            
         MVC   CONTROL+1(1),REPTOPSA                                            
         MVC   P,SPACES                                                         
         LA    R1,P                                                             
         GOTOR PRT                                                              
*                                                                               
PUTH7    MVI   CONTROL+1,1                                                      
         MVC   HOOKN,REPHEADH                                                   
         MVC   FLAG,REPHEADI                                                    
         MVI   SPACING,1                                                        
         SR    R0,R0                                                            
         ICM   R0,1,REPHEADN       R0=NUMBER OF HEAD LINES                      
         BZ    PUTM                                                             
         ICM   R1,15,REPAHEAD      R1=A(HEAD LINES)                             
         BNZ   PUTH8                                                            
*                                                                               
         CLI   HOOKN,0             TEST HEAD LINE HOOK SET                      
         BE    PUTFX                                                            
         LA    R1,P                POINT TO HEADLINE RETURN AREA                
         GOTOR HOOK                CALL USER HOOK                               
         GOTOR PRT                                                              
         BCT   R0,*-4              DO FOR NUMBER OF HEAD LINES                  
         B     PUTHX                                                            
*                                                                               
PUTH8    CLI   HOOKN,0             TEST HEAD LINE HOOK SET                      
         BE    *+8                                                              
         GOTOR HOOK                CALL USER HOOK                               
         GOTOR PRTB                PRINT HEADLINE BLOCK                         
*                                                                               
PUTHX    MVI   REPFHEAD,C'N'       RESET INDICATORS - DRIVER SETS IT ON         
         EJECT                                                                  
***********************************************************************         
* HANDLE MIDLINE PRINTING                                             *         
***********************************************************************         
PUTM     TM    REPMIDSI,REPMFRCE   TEST FORCE MIDLINE PRINTING                  
         BZ    PUTP                                                             
         NI    REPMIDSI,255-REPMFRCE                                            
         MVI   CONTROL,CONTLINE                                                 
         MVI   CONTROL+1,1                                                      
         MVC   HOOKN,REPMIDSH                                                   
         MVC   FLAG,REPMIDSI                                                    
         MVI   SPACING,1                                                        
         SR    R0,R0                                                            
         ICM   R0,1,REPMIDSN       R0=NUMBER OF MID LINES                       
         BZ    PUTP                                                             
         ICM   R1,15,REPAMIDS      R1=A(MID LINES)                              
         BNZ   PUTM2                                                            
*                                                                               
         CLI   HOOKN,0             TEST HEAD LINE HOOK SET                      
         BE    PUTP                                                             
         LA    R1,P                POINT TO HEADLINE RETURN AREA                
         GOTOR HOOK                CALL USER HOOK                               
         GOTOR PRT                                                              
         BCT   R0,*-4              DO FOR NUMBER OF MID LINES                   
         B     PUTMX                                                            
*                                                                               
PUTM2    CLI   HOOKN,0             TEST MID LINE HOOK SET                       
         BE    *+8                                                              
         GOTOR HOOK                CALL USER HOOK                               
         GOTOR PRTB                PRINT MIDLINE BLOCK                          
         B     PUTMX                                                            
*                                                                               
PUTMX    MVI   REPFMIDS,C'N'       RESET INDICATOR - DRIVER SETS IT ON          
         EJECT                                                                  
***********************************************************************         
* HANDLE USER PRINT LINES                                             *         
***********************************************************************         
PUTP     MVI   CONTROL,CONTLINE                                                 
         MVI   CONTROL+1,1                                                      
         SR    R0,R0                                                            
         ICM   R0,1,REPPRNTN                                                    
         BZ    PUTX                                                             
         TM    REPIND2,REPIBOX+REPIBOXI+REPIBOXF                                
         BNO   PUTP02                                                           
         LLC   R1,REPLINE                                                       
         L     RF,REPABOX                                                       
         LA    R3,BOXROWS-1-BOXD(R1,RF)                                         
         ICM   R3,8,0(R3)          SAVE EXISTING CHARACTER                      
         MVI   0(R3),BOXRMID       SET BOX MIDLINE                              
         LA    R1,SPACES                                                        
         GOTOR PRT                 PRINT BOX MIDLINE                            
         STCM  R3,8,0(R3)          RESTORE SAVED CHARACTER                      
*                                                                               
PUTP02   ICM   R1,15,REPAPRNT                                                   
         BZ    PUTX                                                             
         MVC   FLAG,REPPRNTI                                                    
         MVI   SPACING,0                                                        
         LLC   RE,REPPRNSA                                                      
         AHI   RE,-1                                                            
         BNP   *+12                                                             
         STC   RE,SPACING          SET SPACE AFTER VALUE                        
         OI    FLAG,REPHSPAC       SET SPACING ACTIVE                           
         OI    FLAG,REPPCLRA       PRINTS ARE ALWAYS CLEARED                    
         GOTOR PRTB                                                             
         MVI   REPPRNSA,0          RESET SPACE AFTER VALUE                      
*                                                                               
PUTX     B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PRINT A LINE                                             *         
*                                                                     *         
* NTRY - CONTROL CONTAINS SPACING                                     *         
*        R1=A(PRINT LINE TO BE PRINTED)                               *         
*        FLAG CONTAINS EXTRA CONTROL INFORMATION                      *         
*                                                                     *         
* NOTE - REPLINE & REPPAGE ARE UPDATED BY THIS ROUTINE                *         
***********************************************************************         
PRT      NTR1  WORK=(R8,PRTWRKX-PRTWRKD)                                        
         USING PRTWRKD,R8                                                       
         LR    R2,R1               R2=A(PRINT LINE)                             
         MVC   PRTP,SPACES         EXTRACT PRINT LINE INTO LOCAL W/S            
         LLC   R1,REPWIDTH                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   PRTP(0),0(R2)                                                    
*                                                                               
         TM    REPIND1,REPIONL     TEST LOCAL PRINTING AT DDS                   
         BNZ   PRT02                                                            
         MVI   PRTCON,C'B'         BUILD PRINT CONTROL                          
         MVC   PRTCON+1(1),CONTROL                                              
         LLC   R0,CONTROL+1                                                     
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PRTCON+2(2),DUB                                                  
         GOTOR REPAPRT,DMCB,PRTPC,PRTCON                                        
         B     PRT30                                                            
*                                                                               
PRT02    TM    REPIND1,REPIREM     TEST REMOTE PRINTING                         
         JZ    *+2                                                              
         TM    REPIND2,REPIBOX     TEST PRINTING BOXES                          
         BNZ   PRT10               YES - HANDLE SPECIALLY                       
*                                                                               
         MVI   PRTPC,CHANNEL1      SKIP TO CHANNEL 1                            
         CLI   CONTROL,CONTCHAN    TEST CHANNEL CONTROL                         
         BNE   *+12                                                             
         GOTOR REMPRT              YES - EXECUTE                                
         B     PRT30                                                            
*                                                                               
         LLC   R3,CONTROL+1        R3=SPACING VALUE                             
         LA    R4,LINETAB          PRINT & SPACE TABLE                          
         LA    R5,3                                                             
         CR    R3,R5               TEST NOT MORE THAN 3 LINES TO PRINT          
         BNH   PRT06                                                            
         MVC   PRTPC,LINETAB+2     PRINT IMMEDIATE & SPACE 3                    
         GOTOR REMPRT                                                           
         MVC   PRTPC,SPACTAB+2     SPACE 3 IMMEDIATE                            
         SR    R3,R5                                                            
         LA    R4,SPACTAB          SPACE IMMEDIATE TABLE                        
*                                                                               
PRT04    CR    R3,R5               CONTINUE SPACING DOWN                        
         BNH   PRT06                                                            
         GOTOR REMPRT                                                           
         SR    R3,R5                                                            
         BP    PRT04                                                            
*                                                                               
PRT06    LTR   R3,R3               TEST ANYTHING LEFT TO DO                     
         BZ    PRT30                                                            
         AR    R4,R3                                                            
         BCTR  R4,0                R4=A(PRINT OR SPACE COMMAND)                 
         MVC   PRTPC,0(R4)                                                      
         GOTOR REMPRT                                                           
         B     PRT30                                                            
*                                                                               
PRT10    TM    REPIND2,REPIBOXI    TEST BOXES INITIALISED                       
         BNZ   *+8                                                              
         GOTOR SETBOX              NO - INITIALISE NOW                          
*                                                                               
         CLI   CONTROL,CONTDONE    TEST CLOSING REPORT                          
         BE    *+12                                                             
         CLI   CONTROL,CONTCHAN    TEST CHANNEL CONTROL                         
         BNE   PRT16                                                            
         TM    REPIND1,REPIPUT                                                  
         BZ    PRT12                                                            
         MVC   REPLINE,REPMAXL                                                  
         MVC   PRTP,SPACES                                                      
         MVC   PRTPC,LINETAB                                                    
         GOTOR BLDBOX,PRTPC        BUILD BOX BOTTOM LINE                        
         GOTOR REMPRT              YES - EXECUTE                                
*                                                                               
PRT12    CLI   CONTROL,CONTDONE    TEST CLOSING REPORT                          
         BE    PRTX                YES - ALL DONE                               
*                                                                               
PRT14    MVI   PRTPC,CHANNEL1      SKIP TO CHANNEL 1                            
         GOTOR REMPRT              YES - EXECUTE                                
         B     PRT30                                                            
*                                                                               
PRT16    SR    R3,R3                                                            
         ICM   R3,1,CONTROL+1                                                   
         JZ    *+2                                                              
         MVC   PRTPC,LINETAB       PRINT IMMEDIATE & SPACE                      
*                                                                               
PRT18    GOTOR BLDBOX,PRTPC        BUILD BOX LINE                               
         GOTOR REMPRT              PUT TO PRINT QUEUE                           
         IC    R1,REPLINE          BUMP LINE COUNT                              
         LA    R1,1(R1)                                                         
         STC   R1,REPLINE                                                       
         BCT   R3,PRT18            DO FOR NUMBER OF LINES                       
*                                                                               
PRT30    CLI   CONTROL,CONTCHAN    TEST CHANNEL COMMAND                         
         BNE   PRT32                                                            
         SR    R1,R1                                                            
         ICM   R1,3,REPPAGE        YES - UPDATE PAGE AND SET LINE               
         LA    R1,1(R1)                                                         
         STCM  R1,3,REPPAGE                                                     
         MVI   REPLINE,1                                                        
         B     PRT34                                                            
*                                                                               
PRT32    TM    REPIND2,REPIBOX     LINE ALREADY INCREMENTED FOR BOXES           
         BNZ   PRT34                                                            
         LLC   R0,CONTROL+1        BUMP LINE COUNT                              
         LLC   R1,REPLINE                                                       
         AR    R1,R0                                                            
         STC   R1,REPLINE                                                       
*                                                                               
PRT34    OI    REPIND1,REPIPUT     SET LINE PRINTED                             
         TM    FLAG,REPHCLRA       TEST CLEAR AFTER PRINTING                    
         BZ    PRTX                                                             
         LLC   R1,REPWIDTH                                                      
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),SPACES      YES - CLEAR LINE TO SPACES                   
*                                                                               
PRTX     B     EXIT                                                             
*                                                                               
REMPRT   LR    R0,RE                                                            
         GOTOR VDATAMGR,DMCB,PRINT,PRTQUE,0,PRTPC,REPAPQB                       
         CLI   8(R1),0                                                          
         BE    REMPRT1                                                          
         TM    8(R1),X'80'                                                      
         BO    *+6                                                              
         DC    H'0'                ERROR ON PRINT QUEUE                         
         DC    H'0',C'$PQFULL '    END-OF-FILE ON PRINT QUEUE                   
*                                                                               
REMPRT1  MVC   PRTP,SPACES                                                      
         LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R8                                                               
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO PRINT A BLOCK OF LINES                                   *         
*                                                                     *         
* NTRY - R1=A(FIRST LINE TO PRINT)                                    *         
*        R0=NUMBER OF LINES IN BLOCK                                  *         
***********************************************************************         
PRTB     NTR1                                                                   
         LLC   RF,REPWIDTH                                                      
         BCTR  RF,0                RF=LINE WIDTH-1                              
         LA    R2,1(RF)            FIND HIGHEST LINE IN BLOCK TO PRINT          
         LR    R3,R0               R3=NUMBER OF LINES IN BLOCK                  
         BCTR  R3,0                                                             
         MR    R2,R2                                                            
         LA    R2,0(R3,R1)         R2=A(LAST PRINT LINE)                        
         LA    RE,1(RF)            RE=LINE WIDTH                                
*                                                                               
PRTB2    EX    RF,*+8              LOCATE LAST NON SPACE LINE                   
         BNE   PRTB4                                                            
         CLC   0(0,R2),SPACES      TEST SPACE LINE                              
         SR    R2,RE                                                            
         BCT   R0,PRTB2                                                         
         LA    R0,1                R0=LAST NON-SPACE LINE NUMBER                
*                                                                               
PRTB4    LR    R2,R1               POINT TO CURRENT BLOCK LINE                  
         SR    R3,R3               R3=SPACING CONTROL                           
PRTB6    LA    R2,1(R2,RF)         LOCATE NEXT NON-SPACE LINE                   
         LA    R3,1(R3)            BUMP SPACING CONTROL                         
         BCT   R0,PRTB8            DO FOR NUMBER OF LINES                       
         TM    FLAG,REPHSPAC       TEST SPACE AFTER LAST PRINT LINE             
         BZ    PRTB10                                                           
         IC    R0,SPACING                                                       
         AR    R3,R0               ADD SPACING LINES                            
         SR    R0,R0               RESET R0 TO ZERO                             
         B     PRTB10                                                           
PRTB8    EX    RF,*+8              TEST THIS LINE IS SPACES                     
         BE    PRTB6                                                            
         CLC   0(0,R2),SPACES                                                   
*                                                                               
PRTB10   MVI   CONTROL,CONTLINE                                                 
         STC   R3,CONTROL+1                                                     
         GOTOR PRT                 PRINT THIS LINE                              
         LR    R1,R2               POINT TO NEXT NON-SPACE LINE                 
         LTR   R0,R0               TEST ANY MORE LINES IN BLOCK                 
         BNZ   PRTB4                                                            
         TM    FLAG,REPHMLIN+REPHOPEN  DONE IF NO MID-LINE, OPEN BOXES          
         BZ    PRTBX                                                            
         L     R1,REPABOX                                                       
         MVI   BOXREQ-BOXD(R1),C'B'  FORCE MID-LINE                             
         TM    FLAG,REPHOPEN CHECK IF BOX OPEN TO BE FORCED                     
         BNO   *+8                                                              
         MVI   BOXREQ-BOXD(R1),C'O'  FORCE BOX OPEN                             
         MVI   CONTROL+1,1         PRINT ONE LINE                               
         MVC   P,SPACES            CLEAR PRINT LINE                             
         LA    R1,P                POINT TO PRINT LINE                          
         GOTOR PRT                 PRINT MID-LINE                               
*                                                                               
PRTBX    B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO CALL USER'S HOOK ROUTINE                                 *         
*                                                                     *         
* ON ENTRY TO USER HOOK THE FOLLOWING VALUES ARE SET -                *         
*                                                                     *         
*        RF =  HIGH ORDER NIBBLE CONTAINS HOOK NUMBER                 *         
*        R1 =  A(PRINT LINE TO RETURN VALUES IN)                      *         
*                                                                     *         
* REGISTERS 2 THRU 12 ARE RESET TO VALUES BEFORE CALLING REPORT       *         
***********************************************************************         
HOOK     NTR1                                                                   
         ICM   RF,15,REPAUSR                                                    
         BZ    HOOKX                                                            
         ICM   RF,8,HOOKN                                                       
         MVC   P,SPACES                                                         
         L     RE,REGS                                                          
         LM    R2,RC,28(RE)                                                     
         BASR  RE,RF                                                            
HOOKX    XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO INITIALISE FOR ONLINE BOXES                              *         
***********************************************************************         
SETBOX   NTR1                                                                   
         TM    REPIND2,REPIBOX     TEST ONLINE BOXES REQUIRED                   
         BZ    SETBOXN                                                          
         SR    R2,R2                                                            
         ICM   R2,15,REPABOX       R2=A(BOXAREA)                                
         BZ    SETBOXN                                                          
         USING BOXD,R2                                                          
         XC    BOXCOORD,BOXCOORD   CLEAR COORDINATES                            
         LA    RE,BOXCOLS          SET LOW/HIGH BOX COLUMNS                     
         LLC   R0,REPWIDTH                                                      
*                                                                               
SETBOX02 CLI   0(RE),BOXCLFT                                                    
         BE    SETBOX04                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SETBOX02                                                      
         B     SETBOXN                                                          
*                                                                               
SETBOX04 LA    RF,BOXCOLS-1                                                     
         SR    RE,RF                                                            
         STC   RE,BOXLCOL                                                       
         AR    RE,RF                                                            
*                                                                               
SETBOX06 CLI   0(RE),BOXCRGT                                                    
         BE    SETBOX08                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SETBOX06                                                      
         B     SETBOXN                                                          
*                                                                               
SETBOX08 LA    RF,BOXCOLS-1                                                     
         SR    RE,RF                                                            
         STC   RE,BOXRCOL                                                       
         LA    RE,BOXROWS                                                       
         LLC   R0,REPMAXL                                                       
*                                                                               
SETBOX10 CLI   0(RE),BOXRTOP       SET LOW/HIGH BOX ROWS                        
         BE    SETBOX12                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SETBOX10                                                      
         B     SETBOXN                                                          
*                                                                               
SETBOX12 LA    RF,BOXROWS-1                                                     
         SR    RE,RF                                                            
         STC   RE,BOXTROW                                                       
         AR    RE,RF                                                            
*                                                                               
SETBOX14 CLI   0(RE),BOXRBOT                                                    
         BE    SETBOX16                                                         
         LA    RE,1(RE)                                                         
         BCT   R0,SETBOX14                                                      
         B     SETBOXN                                                          
*                                                                               
SETBOX16 LA    RF,BOXROWS-1                                                     
         SR    RE,RF                                                            
         STC   RE,BOXBROW                                                       
         MVI   BOXSTAT,0           RESET BOX STATUS BYTE                        
         OI    REPIND2,REPIBOXI    SET BOXES INITIALISED                        
         B     SETBOXX                                                          
*                                                                               
SETBOXN  NI    REPIND2,FF-(REPIBOX)                                             
*                                                                               
SETBOXX  B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* BUILD BOXES                                                         *         
*                                                                     *         
* NTRY - R1=A(PRINT CONTROL CHARACTER),CLNNN'PRINT LINE'              *         
***********************************************************************         
BLDBOX   NTR1                                                                   
         L     R2,REPABOX                                                       
         CLC   REPLINE,BOXTROW     TEST BEFORE BOX TOP                          
         BL    BLDBOXX                                                          
         CLC   REPLINE,BOXTROW     TEST BOX TOP LINE                            
         BNE   BLDBOX02                                                         
         MVI   BOXSTAT,BOXSOPEN    SET BOX IS OPEN                              
         LA    RE,BOXTOPC                                                       
         B     BLDBOX12                                                         
*                                                                               
BLDBOX02 CLC   REPLINE,BOXBROW     TEST BOX BOTTOM LINE                         
         BE    *+14                                                             
         CLC   REPLINE,REPMAXL                                                  
         BNE   BLDBOX04                                                         
         TM    BOXSTAT,BOXSOPEN    TEST BOX IS OPEN                             
         BZ    BLDBOXX                                                          
         NI    BOXSTAT,FF-(BOXSOPEN)                                            
         LA    RE,BOXBOTC                                                       
         B     BLDBOX12                                                         
*                                                                               
BLDBOX04 TM    BOXSTAT,BOXSOPEN    TEST BOX IS OPEN                             
         BZ    BLDBOXX                                                          
         LLC   RF,REPLINE                                                       
         LA    RF,BOXROWS-1(RF)                                                 
         LA    RE,BOXMIDC                                                       
         CLI   0(RF),BOXRMID       TEST BOX MIDDLE LINE                         
         BE    BLDBOX12                                                         
         LA    RE,BOXPRNT          NO - THIS IS A PRINT LINE                    
*                                                                               
BLDBOX12 LA    R1,1(R1)            BUMP PAST PRINT CONTROL CHARACTER            
         LA    R0,1                R0=COLUMN NUMBER (ONE BASED)                 
*                                                                               
BLDBOX14 CLM   R0,1,BOXLCOL        TEST REACHED LHS OF BOX                      
         BL    BLDBOX28            NOT YET - BUMP                               
         BH    BLDBOX16            HIGHER - CONTINUATION                        
         MVC   0(1,R1),0(RE)       EQUAL - SET LEFT SIDE CHARACTER              
         B     BLDBOX18                                                         
*                                                                               
BLDBOX16 CLM   R0,1,BOXRCOL        TEST REACHED RHS OF BOX                      
         BE    BLDBOX30                                                         
         LR    RF,R0                                                            
         LA    RF,BOXCOLS-1(RF)    NO - POINT TO BOX COLUMN CHARACTER           
         CLI   0(RF),BOXCCOL       TEST THIS IS A COLUMN                        
         BNE   *+14                                                             
         MVC   0(1,R1),1(RE)                                                    
         B     BLDBOX18                                                         
         CLI   2(RE),0             TEST FILL CHARACTER PRESENT                  
         BE    BLDBOX18                                                         
         MVC   0(1,R1),2(RE)       NO - SET FILL CHARACTER                      
*                                                                               
BLDBOX18 TM    REPIND2,REPIBOXJ    TEST AUTO-JOIN FEATURE ON                    
         BZ    BLDBOX28                                                         
         CLI   0(R1),BOX_VE        TEST MOVED VERTICAL IN                       
         BNE   BLDBOX28                                                         
*                                                                               
         CLM   R0,1,BOXLCOL        HANDLE BOX LEFT                              
         BNE   BLDBOX20                                                         
         CLI   1(R1),BOX_HO        TEST FOLLOWED BY HORIZONTAL OR CROSS         
         BE    *+12                                                             
         CLI   1(R1),BOX_CR                                                     
         BNE   BLDBOX28                                                         
         MVI   0(R1),BOX_LT        YES - SET LEFT TEE                           
         B     BLDBOX28                                                         
*                                                                               
BLDBOX20 LR    RF,R1                                                            
         BCTR  RF,0                                                             
         CLI   0(RF),BOX_HO        TEST PREVIOUS IS A HORIZONTAL                
         BE    *+12                                                             
         CLI   0(RF),BOX_CR                                                     
         BNE   BLDBOX22                                                         
         CLI   1(R1),BOX_HO        TEST NEXT IS HORIZONTAL TOO                  
         BE    *+12                                                             
         CLI   1(R1),BOX_CR                                                     
         BNE   *+12                                                             
         MVI   0(R1),BOX_CR        YES - MOVE IN CROSS                          
         B     BLDBOX28                                                         
         MVI   0(R1),BOX_RT        NO - SET RIGHT TEE                           
         B     BLDBOX28                                                         
*                                                                               
BLDBOX22 CLI   1(R1),BOX_HO        PREVIOUS WASN'T - TEST NEXT IS               
         BE    *+12                                                             
         CLI   1(R1),BOX_CR                                                     
         BNE   BLDBOX28                                                         
         MVI   0(R1),BOX_LT        YES - SET LEFT TEE                           
         B     BLDBOX28                                                         
*                                                                               
BLDBOX28 AHI   R0,1                INCREMENT COLUMN NUMBER                      
         LA    R1,1(R1)            BUMP TO NEXT PRINT LINE CHARACTER            
         B     BLDBOX14                                                         
*                                                                               
BLDBOX30 MVC   0(1,R1),3(RE)       SET RIGHT SIDE CHARACTER                     
         TM    REPIND2,REPIBOXJ    TEST AUTO-JOIN FEATURE ON                    
         BZ    BLDBOXX                                                          
*                                                                               
         CLI   0(R1),BOX_VE        TEST MOVED VERTICAL IN                       
         BNE   BLDBOXX                                                          
         LR    RE,R1                                                            
         BCTR  RE,0                                                             
         CLI   0(RE),BOX_HO        TEST PREVIOUS IS HORIZONTAL                  
         BE    *+12                                                             
         CLI   0(RE),BOX_CR                                                     
         BNE   BLDBOXX                                                          
         MVI   0(R1),BOX_RT                                                     
*                                                                               
BLDBOXX  B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
         LTORG                                                                  
*                                                                               
BOXSOPEN EQU   X'80'               BOXSTAT STATUS - BOX IS OPEN                 
CHANNEL1 EQU   X'89'                                                            
FF       EQU   X'FF'                                                            
ESCLOQ   EQU   X'20'               ESCAPE SEQUENCE LOW VALUE                    
ESCHIQ   EQU   X'2F'               ESCAPE SEQUENCE HIGH VALUE                   
*                                                                               
OPEN     DC    C'OPEN   '                                                       
PRINT    DC    C'DMPRINT'                                                       
CLOSE    DC    C'CLOSE  '                                                       
CLOPUR   DC    C'CLO/PUR'                                                       
CLOERR   DC    C'CLO/ERR'                                                       
CLOUSR   DC    C'CLO/USR'                                                       
PRTQUE   DC    C'PRTQUE '                                                       
*                                                                               
LINETAB  DC    X'091119'                                                        
SPACTAB  DC    X'0B131B'                                                        
*                                                                               
SPACES   DC    CL256' '                                                         
*                                  ROW EQUATES                                  
BOXRTOP  EQU   C'T'                TOP OF BOX                                   
BOXRMID  EQU   C'M'                MIDLINE RULE                                 
BOXRBOT  EQU   C'B'                BOTTOM OF BOX                                
BOXRCNT  EQU   C' '                (INNER) CONTINUE                             
*                                  COLUMN EQUATES                               
BOXCLFT  EQU   C'L'                LEFT SIDE OF BOX                             
BOXCCOL  EQU   C'C'                COLUMN RULE                                  
BOXCRGT  EQU   C'R'                RIGHT SIDE OF BOX                            
BOXCCNT  EQU   C' '                (INNER) CONTINUE                             
*                                                                               
BOXTOPC  DC    AL1(BOX_TL,BOX_TT,BOX_HO,BOX_TR)                                 
BOXMIDC  DC    AL1(BOX_LT,BOX_CR,BOX_HO,BOX_RT)                                 
BOXBOTC  DC    AL1(BOX_BL,BOX_BT,BOX_HO,BOX_BR)                                 
BOXPRNT  DC    AL1(BOX_VE,BOX_VE,000000,BOX_VE)                                 
*                                                                               
LANGNDX  DC    A(ENG),A(ENG),A(ENG),A(GER),A(FRE),A(ENG),A(ENG),A(DUT)          
*                                                                               
ENG      DC    AL1(08),CL15'RUN DATE       ' 00                                 
         DC    AL1(08),CL15'RUN TIME       ' 01                                 
         DC    AL1(06),CL15'REPORT         ' 02                                 
         DC    AL1(04),CL15'PAGE           ' 03                                 
         DC    AL1(00),CL15'N/D            ' 04                                 
         DC    AL1(09),CL15'REQUESTOR      ' 05                                 
*                                                                               
ENGLC    DC    AL1(08),CL15'Run date       ' 00                                 
         DC    AL1(08),CL15'Run time       ' 01                                 
         DC    AL1(06),CL15'Report         ' 02                                 
         DC    AL1(04),CL15'Page           ' 03                                 
         DC    AL1(00),CL15'n/d            ' 04                                 
         DC    AL1(09),CL15'Requestor      ' 05                                 
*                                                                               
GER      DC    AL1(05),CL15'DATUM          ' 00                                 
         DC    AL1(05),CL15'ZEIT           ' 01                                 
         DC    AL1(06),CL15'REPORT         ' 02                                 
         DC    AL1(05),CL15'SEITE          ' 03                                 
         DC    AL1(00),CL15'N/D            ' 04                                 
         DC    AL1(10),CL15'ANFORDERER     ' 05                                 
*                                                                               
GERLC    DC    AL1(05),CL15'Datum          ' 00                                 
         DC    AL1(05),CL15'Zeit           ' 01                                 
         DC    AL1(06),CL15'Report         ' 02                                 
         DC    AL1(05),CL15'Seite          ' 03                                 
         DC    AL1(00),CL15'n/d            ' 04                                 
         DC    AL1(10),CL15'Anforderer     ' 05                                 
*                                                                               
FRE      DC    AL1(08),CL15'DATE IMP       ' 00                                 
         DC    AL1(09),CL15'HEURE IMP      ' 01                                 
         DC    AL1(04),CL15'ETAT           ' 02                                 
         DC    AL1(04),CL15'PAGE           ' 03                                 
         DC    AL1(00),CL15'N/D            ' 04                                 
         DC    AL1(11),CL15'DEMANDE PAR    ' 05                                 
*                                                                               
FRELC    DC    AL1(08),CL15'Date imp       ' 00                                 
         DC    AL1(09),CL15'Heure imp      ' 01                                 
         DC    AL1(04),CL15'Etat           ' 02                                 
         DC    AL1(04),CL15'Page           ' 03                                 
         DC    AL1(00),CL15'n/d            ' 04                                 
         DC    AL1(11),CL15'Demand{ par    ' 05                                 
*                                                                               
DUT      DC    AL1(08),CL15'RUNDATUM       ' 00                                 
         DC    AL1(07),CL15'RUNTIJD        ' 01                                 
         DC    AL1(07),CL15'RAPPORT        ' 02                                 
         DC    AL1(06),CL15'PAGINA         ' 03                                 
         DC    AL1(00),CL15'N/D            ' 04                                 
         DC    AL1(08),CL15'OPVRAGER       ' 05                                 
*                                                                               
DUTLC    DC    AL1(08),CL15'Rundatum       ' 00                                 
         DC    AL1(07),CL15'Runtijd        ' 01                                 
         DC    AL1(07),CL15'Rapport        ' 02                                 
         DC    AL1(06),CL15'Pagina         ' 03                                 
         DC    AL1(00),CL15'n/d            ' 04                                 
         DC    AL1(08),CL15'Opvrager       ' 05                                 
         EJECT                                                                  
* FAREPBLK                                                                      
REPBLKD  DSECT                                                                  
       ++INCLUDE FAREPBLK                                                       
         EJECT                                                                  
REPWRKD  DSECT                     ** REPORT LOCAL W/S **                       
DUB      DS    D                                                                
FULL1    DS    F                                                                
FULL2    DS    F                                                                
RELO     DS    A                                                                
REGS     DS    A                                                                
VDATAMGR DS    V                                                                
VDATCON  DS    V                                                                
VDICTATE DS    V                                                                
VPQPROF  DS    V                                                                
DMCB     DS    6F                                                               
CONTROL  DS    CL2                                                              
CONTCHAN EQU   C'C'                                                             
CONTLINE EQU   C'L'                                                             
CONTDONE EQU   C'X'                                                             
HOOKN    DS    X                                                                
SWITCH   DS    X                                                                
FLAG     DS    X                                                                
FLAG1    DS    X                                                                
SPACING  DS    X                                                                
GOTPROF  DS    X                                                                
WORK     DS    XL64                                                             
*                                                                               
PC       DS    X                                                                
P        DS    CL256                                                            
*                                                                               
LPHRASES DS    0X                  ** LANGUAGE DEPENDENT PHRASES **             
LRUNDATE DS    XL16                RUN DATE                                     
LRUNTIME DS    XL16                RUN TIME                                     
LREPORT  DS    XL16                REPORT                                       
LPAGE    DS    XL16                PAGE                                         
LND      DS    XL16                N/D                                          
LRQSTR   DS    XL16                REQUESTOR                                    
LPHRASEL EQU   *-LPHRASES                                                       
REPWRKX  EQU   *                                                                
                                                                                
PRTWRKD  DSECT                     ** PRT S/R LOCAL W/S **                      
PRTCON   DS    CL4                                                              
PRTPC    DS    XL1                                                              
PRTP     DS    XL256                                                            
PRTWRKX  EQU   *                                                                
         EJECT                                                                  
* DNPRTQD                                                                       
       ++INCLUDE DNPRTQD                                                        
* DMPRTQL                                                                       
       ++INCLUDE DMPRTQL                                                        
* DMPRTQK                                                                       
       ++INCLUDE DMPRTQK                                                        
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
* DDMASTD                                                                       
       ++INCLUDE DDMASTD                                                        
* DDLOGOD                                                                       
       ++INCLUDE DDLOGOD                                                        
* DDREMOTED                                                                     
       ++INCLUDE DDREMOTED                                                      
* DDBIGBOX                                                                      
       ++INCLUDE DDBIGBOX                                                       
         ORG   BOXAWIDE                                                         
BOXCOORD DS    0XL4                BOX COORDINATES                              
BOXLCOL  DS    XL1                 LEFTMOST COLUMN                              
BOXRCOL  DS    XL1                 RIGHTMOST BOX COLUMN                         
BOXTROW  DS    XL1                 BOX TOP ROW NUMBER                           
BOXBROW  DS    XL1                 BOX BOTTOM ROW NUMBER                        
                                                                                
* DDBOXEQUS                                                                     
       ++INCLUDE DDBOXEQUS                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002FAREPORT  03/11/14'                                      
         END                                                                    
