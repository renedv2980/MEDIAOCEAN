*          DATA SET ACLABEL3   AT LEVEL 034 AS OF 12/03/03                      
*CATALP ACLABEL3                                                                
         TITLE 'LABELS3 - LABELS (ALL PURPOSE 7 LINES, 3 ACROSS).'              
         PRINT NOGEN                                                            
ACLABEL3 CSECT                                                                  
         SPACE 1                                                                
         NMOD1 0,**LAB3**                                                       
         L     R9,0(,R1)                                                        
         CLC   =C'$OPEN',0(R9)                                                  
         BE    FRSTTEST                                                         
         CLC   =C'$CLOS',0(R9)                                                  
         BNE   R9LD                                                             
         MVC   CLOSE,0(R9)                                                      
         MVI   FIRST,C'L'                                                       
         CP    ACROSS,=P'0'        IS ARRAY EMPTY                               
         BE    NDT                 YES, DON'T FLUSH - JUST END                  
         B     PRINTIT                                                          
         EJECT ,                                                                
***********************************************************************         
         USING LINESD,R9                                                        
R9LD     L     R2,NOMBRE                                                        
NOTFRST  L     R5,SVPAD            LOAD PRINT ADDR                              
         LA    R6,LINE1                                                         
         LA    R7,7                                                             
MVLN     MVC   0(39,R5),0(R6)                                                   
         LA    R5,121(R5)                                                       
         LA    R6,39(R6)                                                        
         BCT   R7,MVLN                                                          
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'3'        ARE WE 3 ACROSS YET                          
         BNL   PRINTIT             YES                                          
         L     R5,SVPAD                                                         
         LA    R5,41(R5)                                                        
         ST    R5,SVPAD                                                         
         B     NDT                 NO                                           
         EJECT ,                                                                
***********************************************************************         
PRINTIT  ZAP   ACROSS,=P'0'        ZERO OUT ACROSS COUNTER                      
         AP    PRINTED,=P'3'                                                    
         LA    R5,ARRAY                                                         
LOOPRX   MVC   PCTL,=C'BL01'       SETUP SINGLE SPACE                           
LOOPR    MVC   P+1(121),0(R5)                                                   
         BAS   RE,PRT                PRINT A LINE                               
         LA    R5,121(R5)                                                       
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'7'        ALL DONE                                     
         BL    LOOPR               NO                                           
         MVI   PCTL+3,C'2'                                                      
         MVI   P,C' '              YES, GO TO NEXT LABEL SET                    
         MVC   P+1(L'P-1),P                                                     
         BAS   RE,PRT                                                           
         CLI   SWS,C'8'                                                         
         BNE   CLF                                                              
*&&US                                                                           
         MVI   PCTL+3,C'3'                                                      
         BAS   RE,PRT                                                           
*&&                                                                             
CLF      CLI   FIRST,C'F'                                                       
         BE    NOCLR                                                            
         MVC   ARRAY(121),SPACES                                                
         MVC   ARRAY2(242),ARRAY                                                
         MVC   ARRAY4(242),ARRAY                                                
         MVC   ARRAY6(242),ARRAY                                                
NOCLR    LA    R5,ARRAY                                                         
         ST    R5,SVPAD            BACK TO START FOR NEXT                       
         ZAP   ACROSS,=P'0'        DITTO ACROSS COUNTER.                        
         CLI   FIRST,C'F'          FIRST TIME                                   
         BNE   NDT                                                              
         SP    CTLN,=P'1'                                                       
         BZ    *+8                                                              
         B     LOOPRX                                                           
         EJECT ,                                                                
NOLINUP  MVC   ARRAY(121),SPACES                                                
         MVC   ARRAY2(242),ARRAY                                                
         MVC   ARRAY4(242),ARRAY                                                
         MVC   ARRAY6(242),ARRAY                                                
         MVI   FIRST,C'N'          YES,NOT ANY MORE                             
         B     XIT                                                              
         EJECT ,                                                                
***********************************************************************         
FRSTTEST ZAP   PRINTED,=P'0'                                                    
         CLI   FIRST,C'F'          WERE WE CLOSED                               
         BE    FRSTM               YES, START NEW ARRAY, ETC.                   
         B     SPCL                OTHERWISE GO TO SPECIAL START.               
                                                                                
FRSTM    DS    0H                                                               
         MVC   PRINT,4(R1)                                                      
         MVC   SPCLUP,20(R1)                                                    
         ICM   RE,15,SPCLUP        USE LOGO DATA LINE UP                        
         BZ    FRSTM10                                                          
         MVC   LABLK,0(RE)                                                      
         MVI   LINSW,C'F'                                                       
         BAS   RE,LINUP                                                         
         B     SPCL                                                             
                                                                                
FRSTM10  MVI   ARRAY,C'*'          DO LINEUP WITH STARS                         
         MVC   ARRAY+1(38),ARRAY                                                
         MVC   ARRAY+41(39),ARRAY                                               
         MVC   ARRAY+82(39),ARRAY                                               
         MVC   ARRAY2(121),ARRAY                                                
         MVC   ARRAY3(121),ARRAY                                                
         MVC   ARRAY4(121),ARRAY                                                
         MVC   ARRAY5(121),ARRAY                                                
         MVC   ARRAY6(242),ARRAY                                                
                                                                                
SPCL     L     R2,8(R1)            HOW MANY OF EACH                             
         LTR   R2,R2                                                            
         BNZ   *+8                                                              
         LHI   R2,1                                                             
         ST    R2,NOMBRE                                                        
         CLI   FIRST,C'F'          IF NOT CLOSED, DON'T REFRESH PRINT           
         BNE   XIT                 POSITION ADDRESS, DO LINEUPS, OR             
*                                  CHANGE SPACING OPTION.                       
         L     R2,16(R1)                                                        
         CHI   R2,8                                                             
         BNE   *+12                                                             
         MVI   SWS,C'8'                                                         
         B     *+8                                                              
         MVI   SWS,C'6'                                                         
         LA    R5,ARRAY                                                         
         ST    R5,SVPAD                                                         
         L     R2,12(R1)                                                        
         LTR   R2,R2                                                            
         BZ    NOLINUP                                                          
         CVD   R2,DOUBLE                                                        
         ZAP   CTLN,DOUBLE                                                      
         B     PRINTIT                                                          
         EJECT ,                                                                
***********************************************************************         
*                                                                               
***********************************************************************         
NDT      CLI   FIRST,C'L'          WAS IT A CLOSE                               
         BNE   CHKLIM                                                           
*&&UK                                                                           
         MVI   LINSW,C'L'                                                       
         BAS   RE,LINUP            DO LAST TIME DIVIDERS                        
         ZAP   ACROSS,=P'0'        ZERO OUT ACROSS COUNTER                      
         LA    R5,ARRAY                                                         
         MVC   PCTL,=C'BL01'       SETUP SINGLE SPACE                           
NDT2     MVC   P+1(121),0(R5)                                                   
         BAS   RE,PRT              PRINT A LINE                                 
         LA    R5,121(R5)                                                       
         AP    ACROSS,=P'1'                                                     
         CP    ACROSS,=P'7'        ALL DONE                                     
         BL    NDT2                NO                                           
*&&                                                                             
         CLI   CLOSE+5,C'P'        FORCE PRINT CLOSE (CLOSP PASSED)             
         BNE   MVIF                                                             
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
MVIF     MVI   FIRST,C'F'          RESET FOR NEXT REQUEST                       
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 1                                                                
CHKLIM   BCT   R2,NOTFRST                                                       
         B     XIT                                                              
         SPACE 2                                                                
PRT      NTR1                                                                   
         GOTO1 PRINT,DMCB,P,PCTL                                                
         B     XIT                                                              
         EJECT ,                                                                
***********************************************************************         
*                                                                               
***********************************************************************         
         USING LBBLKD,R2                                                        
LINUP    NTR1                                                                   
         LA    R2,LABLK            DO FIRST/LAST LABELS                         
         MVC   LINEUP03+2(34),SPACES                                            
         MVC   LINEUP04+2(34),SPACES                                            
         MVC   LINEUP05+2(34),SPACES                                            
         MVC   LINEUP06+2(34),SPACES                                            
         MVC   LINEUP03,LINEUPNN                                                
         MVC   LINEUP05+02(L'LBLOGNM),LBLOGNM                                   
         MVC   LINEUP06+02(L'LBLOGAD),LBLOGAD                                   
         CLI   LINSW,C'F'                                                       
         BNE   LIN2                                                             
*&&UK*&& MVC   LINEUP04+02(16),=C'POST PROCESSING='                             
*&&UK*&& MVC   LINEUP04+18(L'LBLOG2),LBLOG2                                     
*&&US*&& MVC   LINEUP04+02(9),=C'ORIGIN ID='     '                              
*&&US*&& MVC   LINEUP04+12(L'LBORGNID),LBORGNID                                 
         MVC   LINEUP03+2(12),=C'START OF JOB'                                  
         LA    RE,LINEUP03+15                                                   
         B     LIN4                                                             
                                                                                
LIN2     MVC   LINEUP03+2(10),=C'END OF JOB'                                    
         MVC   LINEUP04,LINEUPNN                                                
         LA    R3,LINEUP04+2                                                    
         EDIT  PRINTED,(6,0(R3)),ALIGN=LEFT                                     
         LA    RE,LINEUP04+3                                                    
         AR    RE,R0                                                            
         MVC   0(14,RE),=C'LABELS PRINTED'                                      
         LA    RE,LINEUP03+13                                                   
LIN4     MVC   0(L'LBJNM,RE),LBJNM                                              
         LA    RE,L'LBJNM-1(RE)                                                 
         CLI   0(RE),C' '                                                       
         BH    *+8                                                              
         BCT   RE,*-8                                                           
         CLI   LBSTPNM,C' '                                                     
         BNH   LIN6                                                             
         MVI   1(RE),C'/'                                                       
         MVC   2(L'LBSTPNM,RE),LBSTPNM                                          
*                                                                               
LIN6     LA    R2,LINEUP01                                                      
         LA    R3,ARRAY                                                         
         LA    RF,7                                                             
LIN10    LA    RE,3                                                             
LIN20    MVC   0(38,R3),0(R2)                                                   
         LA    R3,41(R3)                                                        
         BCT   RE,LIN20                                                         
         LA    R2,38(R2)                                                        
         SHI   R3,2                                                             
         BCT   RF,LIN10                                                         
         B     XIT                                                              
         EJECT                                                                  
***********************************************************************         
* VARIABLE DEFINITIONS                                                          
***********************************************************************         
DUB      DS    D                                                                
DOUBLE   DS    D                                                                
SVPAD    DS    F                                                                
PRINT    DS    F                                                                
SPCLUP   DS    A                                                                
DMCB     DS    6F                                                               
NOMBRE   DS    F                                                                
FIRST    DC    C'F'                                                             
WORK     DS    CL80                                                             
CLOSE    DS    CL6                                                              
P        DC    CL132' '                                                         
SPACES   DC    CL132' '                                                         
CTLN     DS    PL1                                                              
PCTL     DC    CL4'BC01'                                                        
ACROSS   DC    PL1'0'                                                           
SWS      DS    CL1                                                              
PRINTED  DS    PL4                                                              
LINSW    DS    CL1                                                              
LABLK    DS    CL(LBBLKLNQ)                                                     
LINEUPNN DC    C'**                                  **'                        
LINEUP01 DC    C'**************************************'                        
LINEUP02 DC    C'**************************************'                        
LINEUP03 DC    C'**                                  **'                        
LINEUP04 DC    C'**                                  **'                        
LINEUP05 DC    C'**                                  **'                        
LINEUP06 DC    C'**                                  **'                        
LINEUP07 DC    C'**************************************'                        
         SPACE 2                                                                
ARRAY    DS    CL41                LINE 1 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
ARRAY2   DS    CL41                LINE 2 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
ARRAY3   DS    CL41                LINE 3 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
ARRAY4   DS    CL41                LINE 4 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
ARRAY5   DS    CL41                LINE 5 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
ARRAY6   DS    CL41                LINE 6 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
ARRAY7   DS    CL41                LINE 7 ARRAY                                 
         DS    CL41                                                             
         DS    CL39                                                             
         SPACE 3                                                                
         LTORG                                                                  
         SPACE 2                                                                
       ++INCLUDE ACLABELD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'034ACLABEL3  12/03/03'                                      
         END                                                                    
