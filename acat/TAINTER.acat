*          DATA SET TAINTER    AT LEVEL 021 AS OF 02/10/12                      
*CATALP TAINTER                                                                 
*                                                                               
*        PARAMETERS                                                             
*        ----------                                                             
*                                                                               
*        P1 = A(PARAMETER BLOCK)                                                
*                                                                               
         TITLE 'TAINTER - BUILD WORK-CODE BLOCK'                                
TAINTER  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,TAINTER                                                        
         L     RA,0(R1)            RA=A(PARAMETER BLOCK)                        
         USING TND,RA                                                           
         L     RC,TNRC             RC=A(CONTROLLER STORAGE AREA)                
         USING GEND,RC                                                          
         L     R9,ASTARTSV         R9=A(GLOBAL SAVED VALUES)                    
         USING SYSWORKD,R9                                                      
         L     R8,ASPOOLD          R8=A(SPOOL STORAGE AREA)                     
         USING SPOOLD,R8                                                        
         L     R7,TNASYCOM         R7=A(COMMON FACILITIES FOR SYSTEM)           
         USING SYSCOMMD,R7                                                      
         EJECT                                                                  
*              MAIN CONTROL                                                     
         SPACE 1                                                                
         BAS   RE,SETWCS           ESTABLISH WORK-CODES                         
         SPACE 1                                                                
         BAS   RE,POST             POST AMOUNTS TO WORK-CODE TABLE              
         SPACE 1                                                                
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO ESTABLISH WORK-CODES                                  
         SPACE 1                                                                
SETWCS   NTR1                                                                   
         MVC   TNWCS,SPACES        INITIALIZE TO SPACES                         
         SPACE 1                                                                
         L     R4,TNAIFEL          R4=A(INTERFACE ELEMENT)                      
         USING TAIFD,R4                                                         
         SPACE 1                                                                
SWC2     ZIC   R0,TAIFNWCS         R0=N'SUB-ELS.                                
         LTR   R0,R0                                                            
         BZ    SWCX                                                             
         LA    R4,TAIFWCS          R4=A(FIRST SUB-ELEMENT)                      
         MVI   BYTE,0              SET OK TO USE WORK-CODES                     
         SPACE 1                                                                
SWC4     LA    R3,IFWCTAB          LOOK UP SUB-ELS. IN WORK-CODE TBL            
         SPACE 1                                                                
SWC6     CLI   0(R3),X'FF'                                                      
         BNE   *+6                                                              
         DC    H'0'                UNDEFINED SUB-ELEMENT                        
         CLC   0(1,R3),0(R4)                                                    
         BE    *+12                                                             
         LA    R3,L'IFWCTAB(R3)                                                 
         B     SWC6                                                             
         SPACE 1                                                                
         TM    BYTE,X'80'          HAVE WE FOUND A BAD MATCH                    
         BZ    SWC8                                                             
         TM    0(R3),X'80'         YES - IS THIS A WORK-CODE ROUTINE            
         BZ    *+12                                                             
         OI    BYTE,X'40'          YES, SET WE'VE REACHED WORK-CODES            
         B     SWC22                                                            
         TM    BYTE,X'40'          THIS IS A VAL RTN - HAVE WE SEEN W/C         
         BZ    SWC22                                                            
         MVI   BYTE,0              YES, SO RESET                                
         SPACE 1                                                                
SWC8     LH    RF,2(R3)            DISPLACEMENT TO ROUTINE                      
         AR    RF,RB                                                            
         LA    RE,SWC20            SET RE FOR COMMON NTR1                       
         NTR1                                                                   
         BR    RF                  ** GO OFF TO ROUTINE **                      
         SPACE 1                                                                
SWC20    BE    *+8                                                              
         OI    BYTE,X'80'          SKIP TO NEXT WORK-CODE GROUP                 
         SPACE 1                                                                
SWC22    ZIC   R1,1(R3)            L'THIS SUB-EL                                
         AR    R4,R1               BUMP TO NEXT SUB-EL                          
         BCT   R0,SWC4             AND PROCESS IT                               
         SPACE 1                                                                
SWCX     DS    0H                  INSURE ALL WORK-CODES DEFINED                
         LA    RE,2                                                             
         XR    RF,RF                                                            
         SPACE 1                                                                
SWCX1    LA    R1,TNWCS                                                         
         LA    R0,L'TNWCS/2                                                     
         SPACE 1                                                                
SWCX2    CLC   0(2,R1),SPACES      IS W/C PRESENT                               
         BE    *+10                                                             
         LR    RF,R1               YES, SAVE ITS ADDRESS                        
         B     *+16                                                             
         LTR   RF,RF               NO, DO WE HAVE A GOOD ONE YET                
         BZ    *+10                                                             
         MVC   0(2,R1),0(RF)       YES, COPY IT TO THIS ONE                     
         LA    R1,2(R1)                                                         
         BCT   R0,SWCX2                                                         
         BCT   RE,SWCX1            PROCESS TWICE TO BE SURE                     
         SPACE 1                                                                
SWCXX    B     XIT                                                              
         EJECT                                                                  
*              ROUTINES TO HANDLE WORK-CODE DEFINITION FILTERING                
         SPACE 1                                                                
WCMUS    DS    0H                                                               
         BAS   RE,ISITAFM          MUSICIANS                                    
         B     XIT                                                              
         SPACE 1                                                                
WCSING   DS    0H                                                               
         BAS   RE,ISITSING         SINGERS                                      
         B     XIT                                                              
         SPACE 1                                                                
WCEXT    DS    0H                                                               
         BAS   RE,ISITEXT          EXTRAS                                       
         B     XIT                                                              
         SPACE 1                                                                
WCOTH    DS    0H                  ALL OTHERS                                   
         BAS   RE,ISITAFM                                                       
         BE    NO                                                               
         BAS   RE,ISITSING                                                      
         BE    NO                                                               
         BAS   RE,ISITEXT                                                       
         BE    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
WCTV     DS    0H                                                               
         TM    TGMEEQU,LIKETV      TV,INTERNET, OR NEW MEDIA                    
         BNZ   YES                                                              
         B     NO                                                               
         SPACE 1                                                                
WCRAD    DS    0H                                                               
         TM    TGMEEQU,RADIO       RADIO                                        
         BO    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
WCON     DS    0H                                                               
         CLI   TGONOF+1,C'N'       'ON' CAMERA                                  
         B     XIT                                                              
         SPACE 1                                                                
WCOFF    DS    0H                                                               
         CLI   TGONOF+1,C'F'       'OFF' CAMERA                                 
         B     XIT                                                              
         SPACE 1                                                                
WCSESS   DS    0H                                                               
         BAS   RE,ISITSESS         SESSION PAYMENTS                             
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
WCRES    DS    0H                                                               
         BAS   RE,ISITSESS         RESIDUAL PAYMENTS                            
         BE    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
WCCAT    DS    0H                  CATEGORY                                     
         CLC   TGCAEQU,1(R4)                                                    
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
WCUSE    DS    0H                  USE                                          
         CLC   TGUSEQU,1(R4)                                                    
         BE    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
WCCLB    DS    0H                  CELEBRITY                                    
         TM    TNCASTA2,TACASCLB                                                
         BO    YES                                                              
         B     NO                                                               
         SPACE 1                                                                
ISITAFM  DS    0H                                                               
*        TM    TGUNEQU,AFM         MATCH ON UNION AFM                           
         LR    R0,RE                                                            
         GOTOR UNITEST,DMCB,TGUNEQUS,AFM,0,0,0                                  
         LR    RE,R0                                                            
         BO    YESR                                                             
         B     NOR                                                              
         SPACE 1                                                                
ISITSING DS    0H                                                               
         TM    TGCASTAT,SINGER     TEST IF SINGER                               
         BO    YESR                                                             
         B     NOR                                                              
         SPACE 1                                                                
ISITEXT  DS    0H                                                               
         TM    TGCATYPE,EXTRA      TEST IF EXTRA                                
         BO    YESR                                                             
         B     NOR                                                              
         SPACE 1                                                                
ISITSESS DS    0H                                                               
         TM    TGUSSTAT,SESSION                                                 
         BO    YESR                                                             
         B     NOR                                                              
         SPACE 1                                                                
         EJECT                                                                  
*              ROUTINES HANDLE SETTING WORK-CODES                               
         SPACE 1                                                                
WCALL    DS    0H                  SET ALL WORK-CODES TO THIS ONE               
         MVC   TNWCS(2),1(R4)                                                   
         MVC   TNWCS+2(L'TNWCS-2),TNWCS                                         
         B     YES                                                              
         SPACE 1                                                                
WCFEE    DS    0H                  SET FEES WORK-CODE                           
         MVC   TNFEEWC,1(R4)                                                    
         B     YES                                                              
         SPACE 1                                                                
WCPNH    DS    0H                  SET P&H WORK-CODE                            
         MVC   TNPNHWC,1(R4)                                                    
         B     YES                                                              
         SPACE 1                                                                
WCHNW    DS    0H                  SET H&W WORK-CODE                            
         MVC   TNHNWWC,1(R4)                                                    
         B     YES                                                              
         SPACE 1                                                                
WCTNH    DS    0H                  SET T&H WORK-CODE                            
         MVC   TNTNHWC,1(R4)                                                    
         OI    TNSTAT,TNHCOMB                                                   
         B     YES                                                              
         SPACE 1                                                                
WCTAX    DS    0H                  SET TAX WORK-CODE                            
         MVC   TNTAXWC,1(R4)                                                    
         B     YES                                                              
         SPACE 1                                                                
WCHND    DS    0H                  SET HANDLING WORK-CODE                       
         MVC   TNHANDWC,1(R4)                                                   
         B     YES                                                              
         SPACE 1                                                                
WCCSF    DS    0H                  SET CSF WORK-CODE                            
         MVC   TNCSFWC,1(R4)                                                    
         B     YES                                                              
         SPACE 1                                                                
WCCOM    DS    0H                  SET COMMISSION WORK-CODE                     
         MVC   TNCOMMWC,1(R4)                                                   
         B     YES                                                              
*                                                                               
WCGST    DS    0H                  SET GST WORK-CODE                            
         MVC   TNGSTWC,1(R4)                                                    
         B     YES                                                              
         EJECT                                                                  
*              ROUTINE TO POST AMOUNTS TO WORK-CODE TABLE                       
         SPACE 1                                                                
POST     NTR1                                                                   
         LA    R3,TNFEEWC          FEES                                         
         L     R4,TNPAY                                                         
         A     R4,TNREXP                                                        
*                                                                               
         CLC   TNGSTWC,SPACES      IF INTERFACE GST WC EXISTS                   
         BH    PST10               DON'T ADD TO FEES                            
         TM    TNSTAT,TNSGSTU      IF GST IS US$ FOR ACTRA PAYMENTS             
         BZ    *+8                                                              
         A     R4,TNGST            ADD TO FEES                                  
*                                                                               
PST10    BAS   RE,ADDIT                                                         
         SPACE 1                                                                
         LA    R3,TNPNHWC          P&H                                          
         L     R4,TNPNH                                                         
         BAS   RE,ADDIT                                                         
         SPACE 1                                                                
         LA    R3,TNCSFWC          COMMERCIAL SERVICE FEE                       
         L     R4,TNCSF                                                         
         BAS   RE,ADDIT                                                         
*                                                                               
PST15    LA    R3,TNCOMMWC         COMMISSION                                   
         L     R4,TNCOMM                                                        
         LTR   R4,R4                                                            
         BZ    *+8                                                              
         BAS   RE,ADDIT                                                         
         SPACE 1                                                                
         TM    TNSTAT,TNHCOMB      TEST POST TAX & HANDLING TOGETHER            
         BZ    PST30                                                            
         LA    R3,TNTNHWC                                                       
         L     R4,TNTAX                                                         
         A     R4,TNHAND                                                        
         A     R4,TNFICR                                                        
*                                                                               
         CLC   TNGSTWC,SPACES                                                   
         BH    PST20                                                            
         TM    TNSTAT,TNSGSTU      IF GST IS US$ FOR ACTRA PAYMENTS             
         BO    *+8                 DON'T ADD TO TAX & HANDLING                  
         A     R4,TNGST                                                         
*                                                                               
PST20    BAS   RE,ADDIT                                                         
         B     PSTX                                                             
         SPACE 1                                                                
PST30    LA    R3,TNTAXWC          ELSE POST PAYROLL TAXES                      
         L     R4,TNTAX                                                         
         A     R4,TNFICR                                                        
         BAS   RE,ADDIT                                                         
         SPACE 1                                                                
         LA    R3,TNHANDWC         AND HANDLING SEPARATELY                      
         L     R4,TNHAND                                                        
*                                                                               
         CLC   TNGSTWC,SPACES                                                   
         BH    PST35                                                            
         TM    TNSTAT,TNSGSTU      IF GST IS US$ FOR ACTRA PAYMENTS             
         BO    *+8                 DON'T ADD TO TAX & HANDLING                  
         A     R4,TNGST                                                         
*                                                                               
PST35    BAS   RE,ADDIT                                                         
*                                                                               
PSTX     CLC   TNGSTWC,SPACES                                                   
         BNH   PSTZ                                                             
         LA    R3,TNGSTWC          GST                                          
         L     R4,TNGST                                                         
         BAS   RE,ADDIT                                                         
         SPACE 1                                                                
PSTZ     B     XIT                                                              
         EJECT                                                                  
*              ROUTINE ADDS CURRENT AMOUNT TO WORK-CODE TABLE                   
         SPACE 1                                                                
*                                  R3=A(CURRENT WORK-CODE)                      
*                                  R4=CURRENT AMOUNT                            
ADDIT    NTR1                                                                   
         L     R2,TNAWCTAB         R2=A(WORK-CODE TABLE)                        
         USING WCD,R2                                                           
         SPACE 1                                                                
ADD2     OC    WCD(WCLNQ),WCD      LOOK FOR NEW ENTRY                           
         BZ    ADD4                                                             
         OC    TNINV,TNINV         OR IF INVOICE FILTER PRESENT                 
         BZ    *+14                                                             
         CLC   TNINV,WCINV         MATCH ON INVOICE NUMBER                      
         BNE   *+14                                                             
         CLC   WCCODE,0(R3)        MATCH AGAINST CURRENT WORK-CODE              
         BE    ADD6                                                             
         OC    TNINV,TNINV         IF INVOICE FILTER PRESENT                    
         BZ    *+12                                                             
         LA    R2,WCNEXT2          TABLE SIZE IS BIGGER                         
         B     *+8                                                              
         LA    R2,WCNEXT           BUMP TO NEXT ENTRY                           
         B     ADD2                LOOP                                         
         SPACE 1                                                                
ADD4     MVC   WCCODE,0(R3)        SAVE WORK-CODE                               
         OC    TNINV,TNINV         IF INVOICE FILTER PRESENT                    
         BZ    *+10                                                             
         MVC   WCINV,TNINV         SAVE IT                                      
         SPACE 1                                                                
ADD6     A     R4,WCAMT            ADD IN AMOUNT                                
         ST    R4,WCAMT                                                         
         B     XIT                                                              
         EJECT                                                                  
*              EXITS, ETC.                                                      
         SPACE 2                                                                
YESR     CR    RE,RE               SET CONDITION CODE                           
         BR    RE                                                               
NOR      LTR   RE,RE                                                            
         BR    RE                                                               
         SPACE 2                                                                
YES      SR    RC,RC               SET CONDITION CODE                           
NO       LTR   RC,RC                                                            
XIT      XIT1                                                                   
         SPACE 3                                                                
         GETEL R4,DATADISP,ELCODE                                               
         EJECT                                                                  
*              CONSTANTS, ETC.                                                  
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
*              TABLE TRANSLATES INTERFACE EL. TO CURRENT WC'S                   
         SPACE 1                                                                
IFWCTAB  DS    0F                                                               
         DC    AL1(01,1),AL2(WCMUS-TAINTER) GROUPINGS START HERE                
         DC    AL1(02,1),AL2(WCSING-TAINTER)                                    
         DC    AL1(03,1),AL2(WCEXT-TAINTER)                                     
         DC    AL1(04,1),AL2(WCOTH-TAINTER)                                     
         DC    AL1(05,1),AL2(WCTV-TAINTER)                                      
         DC    AL1(06,1),AL2(WCRAD-TAINTER)                                     
         DC    AL1(07,1),AL2(WCON-TAINTER)                                      
         DC    AL1(08,1),AL2(WCOFF-TAINTER)                                     
         DC    AL1(09,1),AL2(WCSESS-TAINTER)                                    
         DC    AL1(10,1),AL2(WCRES-TAINTER)                                     
         DC    AL1(11,2),AL2(WCCAT-TAINTER)                                     
         DC    AL1(12,2),AL2(WCUSE-TAINTER)                                     
         DC    AL1(13,1),AL2(WCCLB-TAINTER)                                     
         SPACE 1                                                                
         DC    AL1(129,3),AL2(WCALL-TAINTER) WORK-CODES START HERE              
         DC    AL1(130,3),AL2(WCFEE-TAINTER)            (X'80' BIT ON)          
         DC    AL1(131,3),AL2(WCPNH-TAINTER)                                    
         DC    AL1(132,3),AL2(WCHNW-TAINTER)                                    
         DC    AL1(133,3),AL2(WCTNH-TAINTER)                                    
         DC    AL1(134,3),AL2(WCCSF-TAINTER)                                    
         DC    AL1(135,3),AL2(WCHND-TAINTER)                                    
         DC    AL1(136,3),AL2(WCTAX-TAINTER)                                    
         DC    AL1(137,3),AL2(WCCOM-TAINTER)                                    
         DC    AL1(138,3),AL2(WCGST-TAINTER)                                    
         DC    X'FF'                                                            
         EJECT                                                                  
UNITEST  NTR1  BASE=*,LABEL=*                                                   
       ++INCLUDE TAUNITEST                                                      
         XIT1                                                                   
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE TAINTERD                                                       
         EJECT                                                                  
* TASYSVALD                                                                     
* TASYSWORKD                                                                    
* TASYSDSECT                                                                    
* TASYSEQUS                                                                     
* DDSPLWORKD                                                                    
* DDSPOOLD                                                                      
* TAGENFILE                                                                     
* DDGENTWA (TWAD)                                                               
         PRINT OFF                                                              
SYSCOMMD DSECT                                                                  
       ++INCLUDE TASYSVALD                                                      
SYSWORKD DSECT                                                                  
       ++INCLUDE TASYSWORKD                                                     
       ++INCLUDE TASYSDSECT                                                     
       ++INCLUDE TASYSEQUS                                                      
       ++INCLUDE DDSPLWORKD                                                     
       ++INCLUDE DDSPOOLD                                                       
       ++INCLUDE TAGENFILE                                                      
TWAD     DSECT                                                                  
         DS    CL64                                                             
CONHEADH EQU   *                                                                
       ++INCLUDE DDGENTWA                                                       
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'021TAINTER   02/10/12'                                      
         END                                                                    
