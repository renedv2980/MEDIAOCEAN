*          DATA SET CTREPRUNM  AT LEVEL 002 AS OF 02/13/15                      
*CATALP CTRUNMST                                                                
         TITLE 'MODULE TO CONTROL CONMAN RUN'                                   
CTRUNCON CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,CTRUNCON,=V(CTSAVE)                                            
         L     RA,=V(CTWORKC)                                                   
         USING CTWORKD,RA                                                       
         L     RE,=V(SSB)                                                       
         XC    0(2,RE),0(RE)       SET OFF-LINE                                 
         MVI   2(RE),X'FF'         SET EXTENDED SSB                             
         L     R8,LOGOC                                                         
         USING LOGOD,R8                                                         
         L     R9,=V(MASTC)                                                     
         USING MASTD,R9                                                         
         EJECT                                                                  
* INITIAL CONTROL CARDS                                                         
*                                                                               
READCARD MVC   MCAPHAS2,APPLIC                                                  
         MVC   MCAPHAS1,SPECS                                                   
         ST    RA,MCAWORK                                                       
         GOTO1 =V(RUNSTART)                                                     
         MVC   APPLIC,MCAPHAS2                                                  
         MVC   SPECS,MCAPHAS1                                                   
         MVC   RCTRACE,MCTRACE     TRANSFER MASTER VALUES                       
         MVC   RCDUMP,MCDUMP                                                    
         MVC   RCWRITE,MCWRITE                                                  
         MVC   RCDATE,MCDATE                                                    
         MVC   RCSPECNO,MCNUMBER                                                
         MVC   RCPROG,MCPROG                                                    
         MVI   RCFLONLY,C'Y'       GO TO EXECUTE I/O AND RUN SPECS              
         GOTO1 REPORT                                                           
         MVI   RCFLONLY,C'N'                                                    
         EJECT                                                                  
* OPEN FILE ROUTINES                                                            
*                                                                               
OPEN2    L     R2,FILEC                                                         
         GOTO1 DATAMGR,DMCB,=C'OPEN',=C'CONTROL',FILELIST,(R2)                  
         GOTO1 =V(CTOPEN)                                                       
*&&US                                                                           
         MVI   BYTE,QDEMAND                                                     
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMAND-COMFACSD(4,RF),4(R1)                                     
*                                                                               
         MVI   BYTE,QDEMOUT                                                     
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMOUT-COMFACSD(4,RF),4(R1)                                     
*                                                                               
         MVI   BYTE,QDEMADDR                                                    
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMADDR-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMAINT                                                    
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMAINT-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMAINT                                                    
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMAINT-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMEL                                                      
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMEL-COMFACSD(4,RF),4(R1)                                      
*                                                                               
         MVI   BYTE,QDEMOVAL                                                    
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMOVAL-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMOMTH                                                    
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMOMTH-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMTABS                                                    
         BAS   RE,CTLOAD                                                        
         L     RF,ACOMFACS                                                      
         MVC   CDEMTABS-COMFACSD(4,RF),4(R1)                                    
*                                                                               
         MVI   BYTE,QDEMOCON                                                    
         BAS   RE,CTLOAD                                                        
         MVC   DEMOCON,4(R1)                                                    
*&&                                                                             
* CONTROL DIVIDERS AND REQUESTS                                                 
*                                                                               
         CLI   RCREQSUM,C'Y'       IS A DIVIDER WANTED                          
         BNE   GOREQS                                                           
         CLC   LOGO1,SPACES                                                     
         BNE   DIV                                                              
         MVC   LOGO1,=CL7' MEDIA '                                              
         MVC   LOGO2,=CL7' OCEAN '                                              
         MVC   LOGONAME,=CL33'MEDIAOCEAN'                                       
         MVC   LOGOADD,=CL33'45 WEST 18TH STREET'                               
         MVC   LOGOADD2,=CL33'NEW YORK, NY 10011'                               
         MVC   LOGOADD3,SPACES                                                  
         CLC   RCOUNTRY,=C'UK'                                                  
         BNE   DIV                                                              
         MVC   LOGOADD,=CL33'110 SOUTHWARK STREET'                              
         MVC   LOGOADD2,=CL33'LONDON SE1 0TA'                                   
         MVC   LOGOADD3,SPACES                                                  
*                                                                               
DIV      GOTO1 LOGO,DMCB,LOGOC                                                  
*                                                                               
GOREQS   BAS   RE,ANYLINUP                                                      
         GOTO1 FILCON,DMCB,1       GO AND HANDLE REQUESTS                       
         SR    R2,R2               SET FOR NORMAL TERMINATION                   
*                                                                               
FINALISE CLI   RCREQSUM,C'Y'                                                    
         BNE   CLOSE1                                                           
*NOP*    GOTO1 REQSUM,DMCB,(R2)    SUMMARISE REQUESTS                           
         MVI   LOGOTYPE,C'E'                                                    
         ZAP   LOGOREQS,RCRQVAL                                                 
         GOTO1 LOGO,DMCB,LOGOC                                                  
         EJECT                                                                  
* CLOSE FILES AND END JOB                                                       
*                                                                               
CLOSE1   L     R3,FILEC                                                         
         GOTO1 DATAMGR,DMCB,=C'DMCLSE',=C'CONTROL',,(R3)                        
         GOTO1 PRINT,DMCB,=C'CLOSE'                                             
         LTR   R2,R2                                                            
         BZ    NORMEND                                                          
         ABEND 99,DUMP                                                          
*                                                                               
NORMEND  XBASE                                                                  
         EJECT                                                                  
* LINE-UP PATTERN CONTROL                                                       
*                                                                               
ANYLINUP NTR1                                                                   
         CLI   RCLINEUP,C'Y'                                                    
         BNE   LINEND                                                           
         MVC   P,SPACES                                                         
         GOTO1 PRINT,DMCB,P,=C'BC01'                                            
         GOTO1 PRINT,DMCB,P,=C'BL09'                                            
         DATE  DMCB,DATE=NO                                                     
         CLC   DMCB+6(2),=C'US'                                                 
         BE    USLINUP                                                          
         LA    R2,2                                                             
*                                                                               
LINLOOP  MVI   P+1,C'X'            PRINT 109 X'S ON HEADLINES 10 & 11           
         MVC   P+2(108),P+1                                                     
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,LINLOOP                                                       
         B     LINEND                                                           
*                                                                               
USLINUP  LA    R4,3                DO IT THREE TIMES                            
USLINUP2 LA    R2,3                                                             
*                                                                               
USLINUP4 MVI   P,C'X'              PRINT 86 X'S ON HEADS 10,11,12               
         MVC   P+1(85),P                                                        
         GOTO1 PRINT,DMCB,P,=C'BL01'                                            
         BCT   R2,USLINUP4                                                      
         MVC   P,SPACES                                                         
         GOTO1 (RF),(R1),,=C'BC01'                                              
         GOTO1 (RF),(R1),,=C'BL09'                                              
         BCT   R4,USLINUP2                                                      
         MVC   P,SPACES                                                         
*                                                                               
LINEND   XIT1                                                                   
                                                                                
* LOAD CORE RESIDENT PHASE                                                      
*                                                                               
CTLOAD   LR    R0,RE                                                            
         MVC   MCDUB,SPACES                                                     
         MVC   MCDUB(4),=C'T00A'                                                
         GOTO1 MCVHEXOU,DMCB,BYTE,MCDUB+4,1,0                                   
         GOTO1 MCVLOADM,DMCB,0                                                  
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
* CONSTANTS AND LITERALS                                                        
*                                                                               
FILELIST DC    CL8'NCTFILE'                                                     
         DC    C'X '                                                            
ATABLES  DC    X'D4',3X'00',X'FF'                                               
TESTOPT1 DC    C' '                                                             
TESTOPT2 DC    C' '                                                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
* CTREPWORKD                                                                    
       ++INCLUDE CTREPWORKD                                                     
* DDCOMFACS                                                                     
       ++INCLUDE DDCOMFACS                                                      
* DDCOREEQUS                                                                    
       ++INCLUDE DDCOREQUS                                                      
* DDLOGOD                                                                       
       ++INCLUDE DDLOGOD                                                        
* DDMASTD                                                                       
       ++INCLUDE DDMASTD                                                        
* DDREMOTED                                                                     
       ++INCLUDE DDREMOTED                                                      
* FASSBOFF                                                                      
       ++INCLUDE FASSBOFF                                                       
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002CTREPRUNM 02/13/15'                                      
         END                                                                    
