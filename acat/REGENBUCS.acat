*          DATA SET REGENBUCS  AT LEVEL 095 AS OF 11/07/00                      
*CATALP REGENBUC                                                                
         TITLE 'REGENBUC - REPPAK BUCKET BUILD ROUTINE'                         
         ENTRY REGENBUC                                                         
         ENTRY GETMONTH                                                         
***********************************************************************         
*    HISTORY OF CHANGES                                                         
*    FEB25/00  (BU ) --- GENERATE TRADE BUCKETS OPTIONALLY                      
*                        BOTH REGULAR AND ALTERNATE CALENDAR BUCKETS            
*                                                                               
*    NOV02/00  RHV)  --- SPORTS BUY HANDLING                                    
*                                                                               
*                                                                               
***********************************************************************         
*              THIS ROUTINE BUILDS CONTRACT RECORD BUCKETS FROM BUYREC          
*                                                                               
*              PARAMETER 1 =       BYTE 0     X'FF' THEN AMOUNTS ARE            
*                                              COMPLEMENTED FOR OLD OR          
*                                              DELETED RECS                     
*                                  BYTE 1-3   A(BUYREC)                         
*                                                                               
*              PARAMETER 2 =       BYTE 0     X'F-' USE ALTERNATE               
*                                               CALENDAR TO BUILD               
*                                               ALTERNATE BUCKETS               
*                                                                               
*                                             X'F0' FORCE STATION               
*                                               ALTERNATE CALENDAR              
*                                                                               
*                                             X'F1' FORCE REP                   
*                                               ALTERNATE CALENDAR              
*                                                                               
*                                  BYTE 1-3   A(OUTPUT AREA) XL200              
*                                                WHERE 1ST 2 BYTES ARE          
*                                                OUTPUT LENGTH                  
*                                                                               
*              PARAMETER 3 =       BYTE 1-3   A(ROUTINE ADDRESS BLOCK)          
*                                                WORD 1=A(GETBROAD)             
*                                                WORD 2=A(GETDAY)               
*                                                WORD 3=A(ADDAY)                
*                                                WORD 4=A(DATCON)               
*                                                WORD 5=A(DATAMGR)              
*                                                                               
* EXTRA PARAMETERS FOR ALTERNATE CALENDAR CALL:                                 
*                                                                               
*              PARAMETER 4 =       BYTE 2-3   REP AGENCY ALPHA CODE             
*                                                                               
*              PARAMETER 5 =       BYTE 1-3   A(STATION CALL LETTERS)           
*                                                                               
*---------------------------------------------------------------------*         
* NOTES:                                                                        
*      AFTER MAKING AN ALTERNATE CALENDAR CALL THE CALLING MODULE WILL          
*   NEED TO RESTORE THE IO SEQUENCE.                                            
*                                                                               
*      ALTERNATE CALENDAR CALLS RETURN X'80' IN THE HIGH ORDER BYTE             
* OF THE OUTPUT LENGTH IF THE STATION CALENDAR WAS USED                         
*                                                                               
***********************************************************************         
REGENBUC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKLQ,REGENBUC,CLEAR=YES                                        
         USING WORKD,RC                                                         
         ST    R1,SAVER1                                                        
         L     RA,0(R1)            BUYREC                                       
         USING BUYREC,RA                                                        
         L     RE,8(R1)                                                         
         MVC   CAL,4(R1)           BUCKET FLAG                                  
         L     R9,4(R1)            OUTPUT AREA                                  
         XC    0(200,R9),0(R9)                                                  
         SH    R9,=H'12'           BUCKET LENGTH-2 (1ST TIME)                   
         MVC   VGETBRD(20),0(RE)   ROUTINES                                     
         MVC   AGENCY,12+2(R1)                                                  
         L     RE,16(R1)                                                        
         MVC   STATION,0(RE)                                                    
         SPACE 3                                                                
*---------------------------------*                                             
* INITIALIZE FOR ACL READING                                                    
*---------------------------------*                                             
         TM    CAL,X'F0'           READING ACL?                                 
         BNO   MAIN06              NO                                           
*                                                                               
***<<<   TM    RBUYFLG2,X'02'      ACL - TRADE BUYLINE?                         
***<<<   BO    EXITEQ              YES - DON'T PROCESS                          
*                                                                               
         CLI   CAL,X'F1'           REP CALENDAR FORCED?                         
         BE    MAIN04              YES                                          
*                                                                               
         CLC   STATION,=CL5' '                                                  
         BH    *+6                                                              
         DC    H'0'                SOMEONE FORGOT A PARAMETER                   
*                                                                               
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING RACLKEY,R5                                                       
         MVI   RACLKTYP,X'20'                                                   
         MVC   RACLKREP,AGENCY                                                  
         MVC   RACLKNAM,STATION                                                 
         CLI   RACLKNAM+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   RACLKNAM+4,C' '                                                  
         XC    RACLKYR,RACLKYR                                                  
         DROP  R5                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(0,=C'DMRDHI'),=C'REPDIR',KEY,KEY                  
         CLI   DMCB+8,0                                                         
         BNE   MAIN02              NO ACL REC                                   
*                                                                               
         CLC   KEY(RACLKYR-RACLKEY),KEYSAVE                                     
         BNE   MAIN02              NO ACL REC                                   
*                                                                               
         OI    GACLFLGS,GACLSTAQ   SET TO USE STATION CALENDAR                  
         MVI   CAL,X'F0'           FORCE STATION ON GETMONTH                    
         B     MAIN06                                                           
*                                                                               
MAIN02   DS    0H                  NO STATION CALENDARS FOUND                   
         CLI   CAL,X'F0'           STATION CALENDAR FORCED?                     
         BE    EXITNEQ             YES                                          
*                                                                               
MAIN04   DS    0H                  TRY FOR REP CALENDAR                         
         XC    KEY,KEY                                                          
         LA    R5,KEY                                                           
         USING RACLKEY,R5                                                       
         MVI   RACLKTYP,X'20'                                                   
         MVC   RACLKREP,AGENCY                                                  
         DROP  R5                                                               
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(0,=C'DMRDHI'),=C'REPDIR',KEY,KEY                  
         CLI   DMCB+8,0                                                         
         BNE   EXITNEQ             NO ACL REC                                   
*                                                                               
         CLC   KEY(RACLKYR-RACLKEY),KEYSAVE                                     
         BNE   EXITNEQ             NO ACL REC                                   
         SPACE 3                                                                
*---------------------------------*                                             
* GET MONDAY DATE OF CURRENT WEEK         - 2 BYTE FORMAT                       
*---------------------------------*                                             
MAIN06   DS    0H                                                               
         GOTO1 VDATCON,DMCB,(5,0),DUB           GET TODAY'S DATE                
         GOTO1 VGETDAY,DMCB,DUB,FULL            GET DAY OF WEEK                 
         CLC   FULL(3),=3C' '                                                   
         BNE   *+6                                                              
         DC    H'0'                             HAD BETTER BE VALID             
         ZIC   R6,DMCB                          DAY OF WEEK                     
         BCTR  R6,0                                                             
         LNR   R6,R6                            BACK UP TO MONDAY               
         GOTO1 VADDAY,DMCB,DUB,DMCB+12,(R6)                                     
         GOTO1 VDATCON,DMCB,DMCB+12,(2,DATE)                                    
         SPACE 3                                                                
*-------------------*                                                           
* GET COST PER WEEK                                                             
*-------------------*                                                           
         XC    TOTSPOTS(12),TOTSPOTS    TOTALS                                  
         MVC   FULL,RBUYCOS                                                     
         L     R7,FULL                                                          
         CLC   RBUYKPLN,=3X'FF'    PLAN?                                        
         BE    MAIN08              YES                                          
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BE    MAIN08              YES                                          
         SR    R7,R7               CLEAR $'S                                    
*                                                                               
MAIN08   L     R1,SAVER1                                                        
         CLI   0(R1),X'FF'         COMPLEMENT?                                  
         BNE   MAIN10              NO                                           
         LCR   R7,R7                                                            
         TM    RBUYSFG,X'40'       SPORTS BUY                                   
         BZ    MAIN10              NO                                           
         ICM   RF,15,RBUYTCOS      YES - COMPLEMENT BUY TOT ALSO                
         LCR   RF,RF                                                            
         STCM  RF,15,RBUYTCOS                                                   
*                                                                               
MAIN10   DS    0H                                                               
         ST    R7,RATE             STORE $'S                                    
         SPACE 3                                                                
*-----------------------------------------------------------*                   
         XC    LSTDATES,LSTDATES   1ST BUCKET INDICATOR                         
*                                                                               
         MVC   FULL(2),RBUYLEN     REC LEN                                      
         LH    R5,FULL                                                          
         LA    R5,RBUYREC(R5)      REC END                                      
         SH    R5,=H'2'                                                         
         LA    R3,RBUYELEM         FIRST ELEM                                   
*                                                                               
MAIN20   DS    0H                  NEXT ELEMENT                                 
         CLI   0(R3),3             DATE ELEM?                                   
         BNE   *+16                NO                                           
         BAS   RE,BUCKET           RETURNS R9!!                                 
         BNE   EXITNEQ                                                          
         B     MAIN30                                                           
*                                                                               
         CLI   0(R3),6             MISSED ELEM?                                 
         BNE   *+16                NO                                           
         BAS   RE,MISSED                                                        
         BNE   EXITNEQ                                                          
         B     MAIN30                                                           
*                                                                               
MAIN30   ZIC   R4,1(R3)            ELEM LEN                                     
         BXLE  R3,R4,MAIN20                                                     
         SPACE 3                                                                
*--------------------------------------------*                                  
* LAST ELEMENT PROCESSED, FINISH UP AND EXIT                                    
*--------------------------------------------*                                  
         TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BZ    MAIN37              NO                                           
         ICM   R1,15,RBUYTCOS                                                   
         S     R1,TOTCOST          =ROUNDING DIFFERENCE                         
         MVC   FULL,6(R9)          $ OF LAST BUCKET                             
         A     R1,FULL                                                          
         STCM  R1,15,6(R9)         PUT ROUNDING DIFF IN LAST BUCKET             
*                                                                               
         MVC   RBUYTSPT,=H'1'      1 SPOT/BUY ON SPORTS BUYS                    
*                                                                               
MAIN37   DS    0H                                                               
         L     R1,SAVER1                                                        
         L     R1,4(R1)            ADDRESS OF OUPUT AREA                        
         LA    R9,14(R9)           NEXT ELEM                                    
         SR    R9,R1               GET OUTPUT LENGTH                            
         STH   R9,FULL                                                          
         MVC   0(2,R1),FULL        STORE OUTPUT LENGTH                          
*                                                                               
         TM    CAL,X'F0'           ALTERNATE WEEKS?                             
         BNO   MAIN40              NO                                           
*                                                                               
         TM    GACLFLGS,GACLSTAQ   STATION CALENDAR USED?                       
         BNO   *+8                                                              
         OI    0(R1),X'80'         YES - SET OUTPUT FLAG                        
         B     EXITEQ              DON'T KEEP TOTALS                            
*                                                                               
MAIN40   DS    0H                                                               
         MVC   RBUYTWKS,TOTWKS+3   TOTAL WEEKS                                  
         TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BO    EXITEQ              YES - TOTAL/SPOTS ALREADY HANDLED            
         MVC   RBUYTCOS,TOTCOST    TOTAL COST                                   
         MVC   RBUYTSPT,TOTSPOTS+2 TOTAL SPOTS                                  
*                                                                               
EXITEQ   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITNEQ  TM    CAL,X'F0'           EXIT NOT EQUAL                               
         BO    *+6                                                              
         DC    H'0'                ONLY VAILD ON ALTERNATE CALL                 
*                                                                               
         LTR   RB,RB                                                            
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*********************************************************************           
* PROCESS DATE ELEMENT ---- RETURNS R9 TO MAIN PROGRAM LOOP ----                
*********************************************************************           
BUCKET   NTR1                                                                   
*-------------------------------*                                               
* CONVERT DATES IN DATE ELEMENT                                                 
*-------------------------------*                                               
         GOTO1 VDATCON,DMCB,(3,2(R3)),STARTDAT                                  
         CLI   RBUYKLIN,255                       PLAN REC?                     
         BNE   BUCKET02                           NO                            
*                                                 YES - NO END DATE IN          
         MVC   ENDDATE,STARTDAT                       PLAN DATE ELEM            
         B     BUCKET04                                                         
*                                                                               
BUCKET02 DS    0H                                                               
         GOTO1 VDATCON,DMCB,(3,5(R3)),ENDDATE                                   
BUCKET04 DS    0H                                                               
         SPACE 3                                                                
*------------------------------*                                                
* GET WEEKLY RATE & SPOTS/WEEK                                                  
*-------------------------------*                                               
         CLI   RBUYKLIN,255        PLAN REC?                                    
         BNE   BUCKET10            NO                                           
*                                                                               
         MVC   WEEKSPOT,=F'1'      PLAN GETS 1 SPOT PER WEEK                    
         MVC   WEEKRATE,RATE        FIXED RATE                                  
         B     BUCKET20                                                         
*                                                                               
BUCKET10 DS    0H                                                               
         ZIC   RF,9(R3)            NO. SPOTS PER WEEK                           
         ST    RF,WEEKSPOT                                                      
*                                                                               
         LA    R1,RBUYELEM                                                      
BUCKET12 ZIC   R4,1(R1)                                                         
         AR    R1,R4                                                            
         CLI   0(R1),0             EOR?                                         
         BE    BUCKET14            YES                                          
         CLI   0(R1),4             COMMENT ELEMENT?                             
         BNE   BUCKET12            NO                                           
*                                                                               
         CLC   2(3,R1),=C'CR='     CREDIT?                                      
         BNE   BUCKET14            NO                                           
*                                                                               
         LCR   RF,RF               MINUS NUMBER OF SPOTS                        
         ST    RF,WEEKSPOT                                                      
         LCR   RF,RF                                                            
         B     BUCKET16                                                         
*                                                                               
BUCKET14 TM    RBUYCOS,X'80'       MINUS BUY?                                   
         BZ    *+10                                                             
         XC    WEEKSPOT,WEEKSPOT   YES - NO SPOTS                               
*                                                                               
BUCKET16 DS    0H                  COMPUTE WEEKLY RATE                          
         SR    RE,RE                                                            
         M     RE,RATE                                                          
         ST    RF,WEEKRATE                                                      
*                                                                               
BUCKET20 DS    0H                                                               
         SPACE 3                                                                
*-------------------*                                                           
* BUCKET GENERATION                                                             
*-------------------*                                                           
GENBUC00 DS    0H                                                               
         SR    R0,R0                                                            
         ICM   R0,3,AGENCY                                                      
         GOTO1 =A(GETMONTH),DMCB,(CAL,STARTDAT),BRDDATES,              +        
               PARMLST,(R0),STATION,RR=Y                                        
         CLI   DMCB,X'FF'                                                       
         BE    EXITNEQ                                                          
*                                                                               
         CLC   BRDDATES,LSTDATES   NEW BROADCAST MONTH?                         
         BE    GBUC0060            NO                                           
*-------------------*                                                           
* CREATE NEW BUCKET                                                             
*-------------------*                                                           
         MVC   LSTDATES,BRDDATES   SAVE LAST DATES                              
*                                                                               
         LA    R9,14(R9)           NEXT OUTPUT ELEMENT                          
         MVC   0(2,R9),=X'030E'    BUCKET ELEM CODE + LEN                       
*                                                                               
         TM    RBUYFLG2,X'02'      TRADE BUYLINE?                               
         BNO   GBUC0020            NO                                           
         MVI   0(R9),X'63'         YES - SET TRADE ELEMENT CODE                 
         TM    CAL,X'F0'           ALTERNATE BUCKETS?                           
         BNO   *+8                                                              
         MVI   0(R9),X'83'         YES - SET ALT CAL TRADE ELT CODE             
*                                                                               
         B     GBUC0040                                                         
GBUC0020 EQU   *                                                                
         TM    CAL,X'F0'           ALTERNATE BUCKETS?                           
         BNO   *+8                                                              
         MVI   0(R9),X'53'         YES - SET ALTERNATE ELEMENT CODE             
*                                                                               
GBUC0040 EQU   *                                                                
         MVC   LASTDATE,BRDDATES+14                                             
*                                                                               
         MVC   2(2,R9),LASTDATE    BUCKET YEAR AND MONTH                        
         MVC   4(2,R9),DATE        CURRENT MONDAY DATE                          
         XC    6(9,R9),6(R9)       AMOUNT + LAST ELEMENT INDICATOR              
*                                                                               
GBUC0060 DS    0H                                                               
         SPACE 3                                                                
*---------------------------*                                                   
* ADD WEEKLY AMOUNT & SPOTS                                                     
*---------------------------*                                                   
         ICM   R6,15,6(R9)         BUCKET AMOUNT                                
         A     R6,WEEKRATE                                                      
         STCM  R6,15,6(R9)                                                      
*                                                                               
         LM    R6,R8,TOTSPOTS      TOTALS                                       
         CLI   RBUYKLIN,255        PLAN?                                        
         BE    GBUC0080            YES - NO SPOTS FOR PLAN REC                  
*                                                                               
         ICM   RE,15,10(R9)        SPOTS/MONTH                                  
         A     RE,WEEKSPOT                                                      
         STCM  RE,15,10(R9)                                                     
*                                                                               
         TM    RBUYSFG,X'40'       SPORTS BUY?                                  
         BZ    *+10                NO                                           
         XC    10(4,R9),10(R9)     YES - DON'T COUNT SPOTS                      
*                                                                               
GBUC0080 A     R6,WEEKSPOT                                                      
         A     R7,WEEKRATE                                                      
         LA    R8,1(R8)            WEEK CTR                                     
         STM   R6,R8,TOTSPOTS                                                   
         SPACE 3                                                                
*-------------------*                                                           
* GET NEXT BUY WEEK                                                             
*-------------------*                                                           
         LA    R6,7                                                             
         TM    8(R3),X'40'                       ALTERNATE WEEKS?               
         BZ    *+8                               NO                             
         LA    R6,14                                                            
         GOTO1 VADDAY,DMCB,STARTDAT,STARTDAT,(R6)                               
*                                                                               
         CLC   STARTDAT,ENDDATE    FINISHED WITH BUY?                           
         BNH   GENBUC00            NO                                           
*                                                                               
         CR    RB,RB                                                            
         XIT1  REGS=(R9)           RETURN R9!!!                                 
         EJECT                                                                  
***********************************************************************         
* MISSED ELEM - SUBTRACT AMOUNT FROM APPROPRIATE MONTH BUCKET                   
***********************************************************************         
MISSED   NTR1                                                                   
         L     R1,SAVER1                                                        
         L     R9,4(R1)            OUTPUT                                       
         LA    R9,2(R9)            FIRST BUCKET                                 
*                                                                               
* GET BROADCAST MONTH OF MISSED SPOT                                            
*                                                                               
         GOTO1 VDATCON,DMCB,(3,2(R3)),STARTDAT                                  
         SR    R0,R0                                                            
         ICM   R0,3,AGENCY                                                      
         GOTO1 =A(GETMONTH),DMCB,(CAL,STARTDAT),BRDDATES,              +        
               PARMLST,(R0),STATION,RR=Y                                        
         CLI   DMCB,X'FF'                                                       
         BE    EXITNEQ                                                          
*                                                                               
         MVC   LASTDATE,BRDDATES+14                                             
*                                                                               
MISSED02 CLI   0(R9),0             LAST?                                        
         BNE   MISSED04                                                         
         SH    R9,=H'14'           YES - USE PREVIOUS MONTH BY                  
         B     MISSED06              BACKING UP ONE MONTH IN TABLE              
*                                                                               
MISSED04 CLC   2(2,R9),LASTDATE    SAME MONTH?                                  
         BE    MISSED06            YES                                          
         ZIC   R4,1(R9)            NO - NEXT BUCKET                             
         AR    R9,R4                                                            
         B     MISSED02                                                         
*                                                                               
MISSED06 DS    0H                  SUBTRACT MISSED SPOT                         
         ICM   R6,15,6(R9)         AMOUNT                                       
         SR    RE,RE                                                            
         ZIC   RF,6(R3)            NO. OF MISSED SPOTS                          
         LR    R1,RF                                                            
         SR    RE,RE                                                            
         M     RE,RATE                                                          
         SR    R6,RF                                                            
         STCM  R6,15,6(R9)                                                      
*                                                                               
         LM    R6,R7,TOTSPOTS                                                   
         TM    RBUYCOS,X'80'       CREDIT BUY?                                  
         BZ    *+6                                                              
         SR    R1,R1                                                            
*                                                                               
         SR    R6,R1               TOTAL SPOTS                                  
         SR    R7,RF               TOTAL AMOUNT                                 
         STM   R6,R7,TOTSPOTS                                                   
*                                                                               
         ICM   R6,15,10(R9)        SPOTS IN MONTH                               
         SR    R6,R1                                                            
         STCM  R6,15,10(R9)                                                     
*                                                                               
         B     EXITEQ                                                           
         DROP  RC                                                               
         EJECT                                                                  
         TITLE 'GETMONTH - GETBROAD REPLACEMENT ROUTINE'                        
***********************************************************************         
* GETMONTH  - GETBROAD REPLACEMTN ROUTINE                                       
*                                                                               
*  INPUT:                                                                       
*              PARAMETER 1 =       BYTE 0     CALENDAR OPTIONS                  
*                                               X'F-' USE ACL RECORD TO         
*                                                DETERMIN MONTH OTHER-          
*                                                WISE JUST CALL                 
*                                                GETBROAD                       
*                                               X'F0' FORCE TO STATION          
*                                                ALTERNATE CALENDAR             
*                                               X'F1' FORCE TO REP              
*                                                ALTERNATE CALENDAR             
*                                                                               
*                                  BYTE 1-3    A(INPUT DATE)                    
*                                               YYMMDD EBCDIC                   
*                                                                               
*              PARAMETER 2 =       BYTE 1-3    A(16-BYTE OUTPUT AREA)           
*                                                                               
*                                                                               
* EXTRA PARAMETERS FOR ALTERNATE CALENDAR CALL:                                 
*                                                                               
*              PARAMETER 3 =       BYTE 1-3   A(ROUTINE ADDRESS BLOCK)          
*                                                WORD 1=A(GETBROAD)             
*                                                WORD 2=A(GETDAY)               
*                                                WORD 3=A(ADDAY)                
*                                                WORD 4=A(DATCON)               
*                                                WORD 5=A(DATAMGR)              
*                                                                               
*              PARAMETER 4 =       BYTE 2-3   REP AGENCY ALPHA CODE             
*                                                                               
*              PARAMETER 5 =       BYTE 1-3   A(STATION CALL LETTERS)           
*                                                                               
*  OUTPUT :                                                                     
*    PARM 1        BYTE 0      X'FF' INVALID DATE                               
*                                                                               
*    OUTPUT AREA   CL6         START DATE OF MONTH YYMMDD EBCDIC                
*                  CL6         END DATE OF MONTH YYMMDD EBCDIC                  
*                  XL2         BINARY # WEEKS IN MONTH                          
*                  CL2         YM OF CALENDAR MONTH(BINARY)                     
*                                                                               
***********************************************************************         
GETMONTH DS    0D                                                               
         NMOD1 GMWORKLQ,GETMONTH,CLEAR=YES                                      
         USING GMWORKD,RC                                                       
         ST    R1,GMSAVER1         SAVE A(PARMS)                                
         LM    R2,R3,0(R1)                                                      
         L     RE,8(R1)                                                         
         MVC   GGETBRD(20),0(RE)   ROUTINES                                     
         MVC   GMAGENCY,12+2(R1)                                                
         L     RE,16(R1)                                                        
         MVC   GMSTA,0(RE)                                                      
*                                                                               
         TM    0(R1),X'F0'         ALTERNATE REQUEST?                           
         BO    GETACL              YES                                          
*--------------------------------------*                                        
* CALL GETBROAD, SETUP RETURN AND EXIT *                                        
*--------------------------------------*                                        
         GOTO1 GGETBRD,GMDMCB,(1,(R2)),(R3),GGETDAY,GADDAY                      
         CLI   0(R1),X'FF'         INVALID?                                     
         BE    GETMONEX            NO                                           
*                                                                               
         MVC   12(2,R3),=H'4'                                                   
         CLI   0(R1),X'05'                                                      
         BNE   *+10                                                             
         MVC   12(2,R3),=H'5'                                                   
*                                                   STORE YM                    
         GOTO1 GDATCON,GMDMCB,6(R3),(3,GMFULL)                                  
         MVC   14(2,R3),GMFULL                                                  
*                                                                               
         L     R1,GMSAVER1                                                      
         MVI   0(R1),0                                                          
         B     GETMONX                                                          
         EJECT                                                                  
*---------------------------------------------------*                           
* READ ACLREC, DETERMINE MONTH, SETUP RETURN & EXIT                             
*---------------------------------------------------*                           
GETACL   DS    0H                                                               
         GOTO1 GDATCON,GMDMCB,(0,(R2)),(3,GMFULL2)                              
*                                                                               
         XC    GMKEY,GMKEY         READ 1ST KEY                                 
         LA    R5,GMKEY                                                         
         USING RACLKEY,R5                                                       
         MVI   RACLKTYP,X'20'                                                   
         MVC   RACLKREP,GMAGENCY                                                
         L     R1,GMSAVER1                                                      
         CLI   0(R1),X'F0'         STATION TO BE USED?                          
         BNE   GACL002             NO                                           
*                                                                               
         CLC   GMSTA,=CL5' '                                                    
         BH    *+6                                                              
         DC    H'0'                SOMEONE FORGOT A PARAMETER                   
*                                                                               
         MVC   RACLKNAM,GMSTA                                                   
         CLI   RACLKNAM+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   RACLKNAM+4,C' '                                                  
*                                                                               
GACL002  DS    0H                                                               
         MVC   GMKEYSV,GMKEY                                                    
         GOTO1 GDATAMGR,GMDMCB,(0,=C'DMRDHI'),=C'REPDIR',GMKEY,GMKEY            
         CLI   GMDMCB+8,0                                                       
         BNE   GETMONEX            NO ACL REC                                   
*                                                                               
         CLC   GMKEY(RACLKYR-RACLKEY),GMKEYSV                                   
         BNE   GETMONEX            NO ACL REC                                   
*                                                                               
         MVC   GM1STKEY,GMKEY      SET 1ST KEY                                  
*                                                                               
GACL010  DS    0H                                                               
         XC    GMKEY,GMKEY                                                      
         LA    R5,GMKEY                                                         
         USING RACLKEY,R5                                                       
         MVI   RACLKTYP,X'20'                                                   
         MVC   RACLKREP,GMAGENCY                                                
         L     R1,GMSAVER1                                                      
         CLI   0(R1),X'F0'         STATION TO BE USED?                          
         BNE   GACL012             NO                                           
*                                                                               
         CLC   GMSTA,=CL5' '                                                    
         BH    *+6                                                              
         DC    H'0'                SOMEONE FORGOT A PARAMETER                   
*                                                                               
         MVC   RACLKNAM,GMSTA                                                   
         CLI   RACLKNAM+4,C'T'                                                  
         BNE   *+8                                                              
         MVI   RACLKNAM+4,C' '                                                  
*                                                                               
GACL012  DS    0H                                                               
         MVC   RACLKYR,GMFULL2                                                  
         DROP  R5                                                               
*                                                                               
         MVC   GMKEYSV,GMKEY                                                    
         GOTO1 GDATAMGR,GMDMCB,(0,=C'DMRDHI'),=C'REPDIR',GMKEY,GMKEY            
         CLI   GMDMCB+8,0                                                       
         BNE   GETMONEX            NO ACL REC                                   
*                                                                               
         CLC   GMKEY(L'RACLKEY),GM1STKEY                                        
         BNE   *+8                                                              
         OI    GMFLAGS,X'20'                                                    
*                                                                               
         CLC   GMKEY(RACLKYR-RACLKEY),GMKEYSV                                   
         BE    GACL016                                                          
*                                                                               
         TM    GMFLAGS,X'40'       LAST OP AN INCREMENT?                        
         BO    GETMONEX            YES - NO ACL REC                             
         B     GACL021                                                          
*                                                                               
GACL016  DS    0H                                                               
         GOTO1 GDATAMGR,GMDMCB,(0,=C'GETREC'),=C'REPFIL',GMKEY+28,     +        
               GMIOAREA,GMDMWORK                                                
         CLI   GMDMCB+8,0           DISK ERROR CHECK                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GACL020  DS    0H                                                               
         GOTO1 GDATCON,GMDMCB,(0,(R2)),(2,GMFULL)                               
*                                                                               
         LA    R6,GMIOAREA        GET END DATE ELEMENT                          
         MVI   GMELCODE,X'10'                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                 REQ'D                                        
         DC    H'0'                                                             
         LR    R5,R6                                                            
         LA    R5,RACLDATE-RACLELEM(R5)                                         
*                                                                               
         LA    R6,GMIOAREA         GET WEEKS ELEMENT                            
         MVI   GMELCODE,X'20'                                                   
         BAS   RE,GETEL                                                         
         BE    *+6                 REQ'D                                        
         DC    H'0'                                                             
         LA    R6,RACLWKPM-RACLWKS(R6)                                          
*                                                                               
         LA    RF,L'RACLDATE-2(R5) LAST DAY OF YEAR                             
*                                                                               
         CLC   GMFULL(2),0(R5)     FIRST DAY OF YEAR                            
         BNL   GACL022             NOT IN PREVIOUS YEAR                         
*                                                                               
GACL021  DS    0H                                                               
         TM    GMFLAGS,X'20'       FIRST KEY ENCOUNTERED                        
         BNZ   GETMONEX            YES - EXIT                                   
*                                                                               
         GOTO1 GDATCON,GMDMCB,(3,GMFULL2),(0,GMDUB)                             
         GOTO1 GADDAY,GMDMCB,(C'Y',GMDUB),GMDUB,-1                              
         GOTO1 GDATCON,GMDMCB,(0,GMDUB),(3,GMFULL2)                             
*                                                                               
         NI    GMFLAGS,X'FF'-X'40'                                              
         OI    GMFLAGS,X'80'                                                    
         B     GACL010                                                          
*                                                                               
GACL022  DS    0H                                                               
         LA    R5,2(R5)                                                         
         CLC   GMFULL(2),0(RF)     LAST DAY OF YEAR                             
         BNH   GACL024             NOT IN NEXT YEAR                             
*                                                                               
         MVC   GMFULL2(1),GMKEY+(RACLKYR-RACLKEY)                               
*                                                                               
         GOTO1 GDATCON,GMDMCB,(3,GMFULL2),(0,GMDUB)                             
         GOTO1 GADDAY,GMDMCB,(C'Y',GMDUB),GMDUB,1                               
         GOTO1 GDATCON,GMDMCB,(0,GMDUB),(3,GMFULL2)                             
*                                                                               
         NI    GMFLAGS,X'FF'-X'80'                                              
         OI    GMFLAGS,X'40'                                                    
         B     GACL010                                                          
*                                                                               
GACL024  DS    0H                                                               
         LA    R1,1                COUNT MONTHS                                 
*                                                                               
GACL026  DS    0H                                                               
         CLC   GMFULL(2),0(R5)     CHECK AGAINST END DATE OF MONTH              
         BNH   GACL030             ITS IN THIS MONTH                            
         LA    R1,1(R1)                                                         
         LA    R5,2(R5)            NEXT MONTH                                   
         LA    R6,2(R6)            # WEEKS NEXT MONTH                           
         CR    R5,RF                                                            
         BL    GACL026                                                          
*                                                                               
GACL030  DS    0H                                                               
         STC   R1,GMELCODE                        KEEP MONTH BYTE               
*                                                                               
         GOTO1 GDATCON,GMDMCB,(2,(R5)),(0,6(R3))                                
         SH    R5,=H'2'                           GET START OF MONTH            
         GOTO1 GDATCON,GMDMCB,(2,(R5)),(0,(R3))                                 
         GOTO1 GADDAY,GMDMCB,(0,(R3)),(R3),1                                    
         MVC   12(2,R3),0(R6)                     # OF WEEKS IN MONTH           
*                                                STORE YM                       
         MVC   14(1,R3),GMIOAREA+(RACLKYR-RACLKEY)                              
         MVC   15(1,R3),GMELCODE                                                
*                                                                               
         L     R1,GMSAVER1                                                      
         MVI   0(R1),0                                                          
         B     GETMONX                                                          
         SPACE 3                                                                
*---------------------------------------------------------------------          
GETMONEX DS    0H                                                               
         L     R1,GMSAVER1                                                      
         MVI   0(R1),X'FF'                                                      
         B     GETMONX                                                          
*                                                                               
GETMONX  DS    0H                                                               
         CR    RB,RB                                                            
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
         GETEL R6,=Y(RACLDESC-RACLREC),GMELCODE                                 
*                                                                               
         LTORG                                                                  
*                                                                               
***>>>   FOR TESTING PURPOSES, KEEP THE SIZE OF THIS MODULE THE SAME            
         ORG   REGENBUC+(((*-REGENBUC)/2048)+1)*2048                            
*                                                                               
***********************************************************************         
* GETMONTH WORKING STORAGE                                                      
***********************************************************************         
GMWORKD  DSECT                                                                  
GMDMWORK DS    12D                                                              
GMDUB    DS    D                                                                
GMDMCB   DS    6F                                                               
*                                  ADDRESS PARAMETER BLOCK                      
GGETBRD  DS    A                     A(GETBROAD)                                
GGETDAY  DS    A                     A(GETDAY)                                  
GADDAY   DS    A                     A(ADDAY)                                   
GDATCON  DS    A                     A(DATCON)                                  
GDATAMGR DS    A                     A(DATAMGR)                                 
*                                                                               
GMSAVER1 DS    F                   R1                                           
*                                                                               
GMFULL   DS    F                                                                
GMFULL2  DS    F                                                                
*                                                                               
GMAGENCY DS    CL2                 REP AGENCY ALPHA CODE                        
GMELCODE DS    X                                                                
GMFLAGS  DS    X                   CONTROL FLAGS                                
*                                   - X'80' LAST OP WAS A DECREMENT             
*                                   - X'40' LAST OP WAS AN INCREMENT            
*                                   - X'20' FIRST KEY ENCOUNTERED               
*                                                                               
GMSTA    DS    CL5                 STATION CALL LETTERS                         
*                                                                               
GM1STKEY DS    CL48                                                             
GMKEY    DS    CL48                                                             
GMKEYSV  DS    CL48                                                             
GMIOAREA DS    XL2000                                                           
GMWORKLQ EQU   *-GMWORKD                                                        
         EJECT                                                                  
***********************************************************************         
* REGENBUC WORKING STORAGE                                                      
***********************************************************************         
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
DUB      DS    D                                                                
DMCB     DS    6F                                                               
*                                  ADDRESS PARAMETER BLOCK                      
PARMLST  DS    0XL(4*5)                                                         
VGETBRD  DS    A                     A(GETBROAD)                                
VGETDAY  DS    A                     A(GETDAY)                                  
VADDAY   DS    A                     A(ADDAY)                                   
VDATCON  DS    A                     A(DATCON)                                  
VDATAMGR DS    A                     A(DATAMGR)                                 
*                                                                               
SAVER1   DS    F                   R1                                           
*                                                                               
FULL     DS    F                                                                
RATE     DS    F                                                                
WEEKRATE DS    F                   RATE FOR WEEK                                
WEEKSPOT DS    F                   SPOTS FOR WEEK                               
TOTSPOTS DS    F                   TOTAL SPOTS                                  
TOTCOST  DS    F                   TOTAL COST                                   
TOTWKS   DS    F                   TOTAL WEEKS                                  
BRDDATES DS    CL16                BROADCAST MONTH DATES                        
LSTDATES DS    CL16                LAST BROADCAST MONTH DATES                   
DATE     DS    CL2                 CURRENT WEEK                                 
*                                                                               
GACLFLGS DS    X                   FLAGS USED FOR GETTING ACL RECORDS           
GACLSTAQ EQU   X'80'                - USE STATION CALENDARS                     
*                                                                               
CAL      DS    X                   INDICATES WHICH CALENDAR/BUCKETS             
AGENCY   DS    CL2                 REP AGENCY ALPHA CODE                        
STATION  DS    CL5                 STATION CALL LETTERS                         
STARTDAT DS    CL6                 BUY START DATE                               
ENDDATE  DS    CL6                 BUY END DATE                                 
LASTDATE DS    XL2                 LAST DAY OF BROADCAST MONTH                  
KEY      DS    CL48                                                             
KEYSAVE  DS    CL48                                                             
IOAREA   DS    XL2000                                                           
WORKLQ   EQU   *-WORKD                                                          
         EJECT                                                                  
BUYREC   DSECT                                                                  
       ++INCLUDE REGENBUY                                                       
         EJECT                                                                  
ACLREC   DSECT                                                                  
       ++INCLUDE REGENACL                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'095REGENBUCS 11/07/00'                                      
         END                                                                    
