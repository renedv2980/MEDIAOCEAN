*          DATA SET CTKEYOUT   AT LEVEL 006 AS OF 05/01/02                      
*CTKEYOUT   AT LEVEL 004 AS OF 19/07/78 <======== FUCK ME OLD BOOTS !!          
*CATALP CTKEYOUT                                                                
         TITLE 'OBJECT TO SOURCE KEY CONVERSION'                                
* PARAMETER 1  BYTE 0    SET TO X'FF' IF INVALID RECORD                         
*                   1-3  A(RECORD)                                              
*           2            A(OUTPUT) 132 BYTE PRINT LINE                          
*                                                                               
         PRINT NOGEN                                                            
CTKEYOUT CSECT                                                                  
         NMOD1 004,**KEYOUT                                                     
         USING WRKD,RC                                                          
         LR    R2,R1               R2=A(PARMS)                                  
         LM    R3,R4,0(R2)         R3=A(KEY)                                    
         RELOC RELO                R4=A(OUTPUT)                                 
         MVI   0(R4),C' '                                                       
         MVC   1(131,R4),0(R4)                                                  
         LA    R5,RECLST                                                        
         MVI   0(R2),0             RESET ERROR BYTE                             
         EJECT                                                                  
*              SEARCH RECORD LIST FOR RECORD TYPE                               
*                                                                               
CTK2     CLI   0(R5),X'FF'                                                      
         BNE   *+12                                                             
         MVI   0(R2),X'FF'         RETURN ERROR                                 
         B     EXIT                                                             
         CLC   0(1,R5),0(R3)       MATCH KEY WITH LIST                          
         BE    *+12                                                             
         LA    R5,L'RECLST(R5)                                                  
         B     CTK2                                                             
         MVC   WORK(14),4(R5)      SAVE RECORD-TYPE (NAME)                      
         MVC   0(12,R4),=C'RECORD TYPE='                                        
         MVC   12(14,R4),WORK                                                   
         LA    R4,26(R4)                                                        
         BAS   RE,CHAIN            CHAIN TO NEXT OUTPUT FIELD                   
         MVC   DUB(4),0(R5)        GO TO KEY ROUTINE                            
         L     R5,DUB                                                           
         A     R5,RELO                                                          
         BR    R5                                                               
*                                            COMMENT                            
*                                                                               
CTKEYC   DS    0H                                                               
         USING CTCREC,R3                                                        
         MVC   WORK(2),CTCKUSER                                                 
         BAS   RE,CTUSERN                                                       
         BAS   RE,CHAIN                                                         
         MVC   WORK(1),CTCKSYS                                                  
         BAS   RE,CTSYSA                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(10),CTCKID                                                  
         BAS   RE,CTIDN                                                         
         B     EXIT                                                             
*                                            DEMOGRAPHIC                        
*                                                                               
*TKEYD   DS    0H                                                               
*        USING CTDRECD,R3                                                       
*        MVC   WORK(2),CTDKAGY                                                  
*        BAS   RE,CTAGY                                                         
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(1),CTDKMED                                                  
*        BAS   RE,CTMED2                                                        
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(2),CTDKCLI                                                  
*        BAS   RE,CTCLI                                                         
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(1),CTDKSRV                                                  
*        BAS   RE,CTSERV                                                        
*        B     EXIT                                                             
*                                            ERROR                              
*                                                                               
CTKEYE   DS    0H                                                               
         USING CTEREC,R3                                                        
         MVC   WORK(1),CTEKSYS                                                  
         BAS   RE,CTSYSN                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(1),CTEKNUM                                                  
         BAS   RE,CTNUM1                                                        
         B     EXIT                                                             
*                                            FILE CONTROL                       
*                                                                               
*TKEYF   DS    0H                                                               
*        USING CTFREC,R3                                                        
*        MVC   WORK(1),CTFKSYS                                                  
*        BAS   RE,CTSYSA                                                        
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(2),CTFKUSER                                                 
*        BAS   RE,CTUSERN                                                       
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(1),CTFKMED                                                  
*        BAS   RE,CTMED                                                         
*        B     EXIT                                                             
*                                            GROUP REQUESTS                     
*                                                                               
*TKEYG   DS    0H                                                               
*        USING CTGREC,R3                                                        
*        MVC   WORK(2),CTGKUSER                                                 
*        BAS   RE,CTUSERN                                                       
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(1),CTGKSYS                                                  
*        BAS   RE,CTSYSA                                                        
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(10),CTGKID                                                  
*        BAS   RE,CTIDN                                                         
*        B     EXIT                                                             
*                                            IDENTIFICATION (ALPHA)             
*                                                                               
CTKEYI   DS    0H                                                               
         USING CTIREC,R3                                                        
         OC    CTIKID(8),CTIKID                                                 
         BZ    CTKEYIN                                                          
         MVC   WORK(10),CTIKID                                                  
         BAS   RE,CTIDN                                                         
         B     EXIT                                                             
*                                            IDENTIFICATION (NUMERIC)           
*                                                                               
CTKEYIN  OC    CTIKID+8(2),CTIKID+8                                             
         BNZ   *+14                                                             
         MVC   0(15,R4),=C'USER ID#=MASTER'                                     
         B     EXIT                                                             
         MVC   WORK(2),CTIKID+8                                                 
         BAS   RE,CTUSERN                                                       
         B     EXIT                                                             
*                                            JCL/LIBRARY                        
*                                                                               
CTKEYJ   DS    0H                                                               
CTKEYL   DS    0H                                                               
         USING CTJREC,R3                                                        
         MVC   WORK(10),CTJKID                                                  
         BAS   RE,CTIDN                                                         
         B     EXIT                                                             
*                                            OUTPUT TYPE                        
*                                                                               
CTKEYO   DS    0H                                                               
         USING CTOREC,R3                                                        
         MVC   WORK(10),CTOKID                                                  
         BAS   RE,CTIDN                                                         
         B     EXIT                                                             
*                                            PROFILE                            
*                                                                               
CTKEYP   DS    0H                                                               
         USING CTPREC,R3                                                        
         MVC   WORK(1),CTPKSYS                                                  
         BAS   RE,CTSYSA                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(2),CTPKPROG                                                 
         BAS   RE,CTPROG2                                                       
         BAS   RE,CHAIN                                                         
         MVC   WORK(2),CTPKORIG                                                 
         BAS   RE,CTUSERN                                                       
         B     EXIT                                                             
*                                            PROTOTYPE REQUEST                  
*                                                                               
*TKEYQ   DS    0H                                                               
*        USING CTQREC,R3                                                        
*        MVC   WORK(1),CTQKSYS                                                  
*        BAS   RE,CTSYSA                                                        
*        BAS   RE,CHAIN                                                         
*        MVI   WORK,C' '                                                        
*        MVC   WORK+1(9),WORK                                                   
*        MVC   WORK(4),CTQKNAME                                                 
*        BAS   RE,CTIDN                                                         
*        B     EXIT                                                             
*                                            REQUEST PROCESSOR                  
*                                                                               
*TKEYR   DS    0H                                                               
*        USING CTRREC,R3                                                        
*        MVC   WORK(2),CTRKSYS                                                  
*        BAS   RE,CTSYSA                                                        
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(2),CTRKNUM                                                  
*        BAS   RE,CTPROG2                                                       
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(2),CTRKUID                                                  
*        BAS   RE,CTUSERN                                                       
*        B     EXIT                                                             
*                                            SCREEN GENERATOR                   
*                                                                               
*TKEYS   DS    0H                                                               
*        USING CTSREC,R3                                                        
*        MVC   WORK(1),CTSKSYS                                                  
*        BAS   RE,CTSYSA                                                        
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(3),CTSKPRG                                                  
*        BAS   RE,CTPROG3                                                       
*        BAS   RE,CHAIN                                                         
*        MVC   WORK(10),CTSKSCID                                                
*        BAS   RE,CTIDN                                                         
*        B     EXIT                                                             
*                                            TERMINAL                           
*                                                                               
CTKEYT   DS    0H                                                               
         USING CTTREC,R3                                                        
         OC    CTTKLINE(8),CTTKLINE                                             
         BZ    CTKEYTN                                                          
         MVC   WORK(4),CTTKLINE                                                 
         BAS   RE,CTLINE                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(4),CTTKADDR                                                 
         BAS   RE,CTADDR                                                        
         OC    CTTKPASS,CTTKPASS                                                
         BZ    EXIT                                                             
         BAS   RE,CHAIN                                                         
         MVC   WORK(10),CTTKPASS                                                
         BAS   RE,CTPASS                                                        
         B     EXIT                                                             
*                                            PASSWORD NUMBER                    
*                                                                               
CTKEYTN  OC    CTTKPASS+8(2),CTTKPASS+8                                         
         BNZ   *+14                                                             
         MVC   0(15,R4),=C'PASSWRD#=MASTER'                                     
         B     EXIT                                                             
         MVC   0(09,R4),=C'PASSWRD#='                                           
         MVC   WORK(2),CTTKPASS+8                                               
         BAS   RE,CTUSERN+6                                                     
         B     EXIT                                                             
*                                            EXTRACT RULES                      
*                                                                               
CTKEYX   DS    0H                                                               
         USING CTXREC,R3                                                        
         MVC   WORK(2),CTXKAGY                                                  
         BAS   RE,CTAGY2                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(3),CTXKCLT                                                  
         BAS   RE,CTCLI3                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(1),CTXKPRD                                                  
         BAS   RE,CTPRO                                                         
         BAS   RE,CHAIN                                                         
         MVC   WORK(1),CTXKEST                                                  
         BAS   RE,CTEST                                                         
         BAS   RE,CHAIN                                                         
         OC    CTXKSTRT,CTXKSTRT                                                
         BZ    *+18                                                             
         MVC   WORK(3),CTXKSTRT                                                 
         BAS   RE,CTSDTP                                                        
         BAS   RE,CHAIN                                                         
         OC    CTXKEND,CTXKEND                                                  
         BZ    *+18                                                             
         MVC   WORK(3),CTXKEND                                                  
         BAS   RE,CTEDTP                                                        
         BAS   RE,CHAIN                                                         
         B     EXIT                                                             
*                                            USER PROFILE                       
*                                                                               
CTKEYU   DS    0H                                                               
         USING CTUREC,R3                                                        
         MVC   WORK(1),CTUKSYS                                                  
         BAS   RE,CTSYSA                                                        
         BAS   RE,CHAIN                                                         
         MVC   WORK(3),CTUKPROG                                                 
         CLI   WORK,0                                                           
         BE    *+12                                                             
         BAS   RE,CTPROG3                                                       
         B     *+14                                                             
         MVC   WORK(2),CTUKPROG+1                                               
         BAS   RE,CTPROG2                                                       
         OC    CTUKAGY,CTUKAGY                                                  
         BZ    EXIT                                                             
         BAS   RE,CHAIN                                                         
         MVC   WORK(2),CTUKAGY                                                  
         BAS   RE,CTAGY                                                         
         BAS   RE,CHAIN                                                         
         MVC   WORK(1),CTUKMED                                                  
         BAS   RE,CTMED                                                         
         BAS   RE,CHAIN                                                         
         MVC   WORK(3),CTUKCLT                                                  
         BAS   RE,CTCLI3                                                        
         B     EXIT                                                             
         EJECT                                                                  
*                                            FIELD CHAIN                        
*                                                                               
CHAIN    CLI   0(R4),C' '                                                       
         BNE   *+10                                                             
         BCTR  R4,0                                                             
         B     CHAIN                                                            
         MVI   1(R4),C','                                                       
         LA    R4,2(R4)                                                         
         BR    RE                                                               
*                                            USER ID NUMBER                     
*                                                                               
CTUSERN  MVC   0(09,R4),=C'USER ID#='                                           
         MVC   DUB(2),WORK                                                      
         LH    R0,DUB                                                           
         LTR   R0,R0                                                            
         BNZ   *+16                                                             
         MVC   9(3,R4),=C'ALL'                                                  
         LA    R4,14(R4)                                                        
         BR    RE                                                               
         EDIT  (R0),(4,9(R4)),ALIGN=LEFT                                        
         AR    R4,R0                                                            
         LA    R4,10(R4)                                                        
         BR    RE                                                               
*                                            SYSTEM (ALPHA)                     
*                                                                               
CTSYSA   LA    R7,SYSLST                                                        
CTSYSA2  CLI   0(R7),X'FF'                                                      
         BE    CTSYSA4                                                          
         CLC   1(1,R7),WORK                                                     
         BE    CTSYSA4                                                          
         LA    R7,L'SYSLST(R7)                                                  
         B     CTSYSA2                                                          
CTSYSA4  MVC   0(07,R4),=C'SYSTEM='                                             
         MVC   7(8,R4),2(R7)                                                    
         LA    R4,16(R4)                                                        
         BR    RE                                                               
*                                            SYSTEM (HEX)                       
*                                                                               
CTSYSN   LA    R7,SYSLST                                                        
CTSYSN2  CLI   0(R7),X'FF'                                                      
         BE    CTSYSA4                                                          
         CLC   0(1,R7),WORK                                                     
         BE    CTSYSA4                                                          
         LA    R7,L'SYSLST(R7)                                                  
         B     CTSYSN2                                                          
*                                            ID NAME                            
*                                                                               
CTIDN    MVC   0(03,R4),=C'ID='                                                 
         MVC   3(10,R4),WORK                                                    
         LA    R4,14(R4)                                                        
         BR    RE                                                               
*                                            NUMBER                             
*                                                                               
CTNUM1   SR    R0,R0                                                            
         IC    R0,WORK                                                          
         MVC   0(07,R4),=C'NUMBER='                                             
         EDIT  (R0),(3,7(R4)),ALIGN=LEFT,ZERO=NOBLANK                           
         LA    R4,11(R4)                                                        
         BR    RE                                                               
*                                            MEDIA                              
*                                                                               
CTMED    MVC   0(06,R4),=C'MEDIA='                                              
         MVC   6(1,R4),WORK                                                     
         LA    R4,6(R4)                                                         
         CLI   WORK,0                                                           
         BNER  RE                                                               
         MVC   0(3,R4),=C'ALL'                                                  
         LA    R4,3(R4)                                                         
         BR    RE                                                               
*                                            MEDIA (DEMO)                       
*                                                                               
CTMED2   MVC   0(06,R4),=C'MEDIA='                                              
         MVC   6(1,R4),WORK                                                     
         LA    R4,6(R4)                                                         
         CLI   WORK,C'Z'                                                        
         BNER  RE                                                               
         MVC   0(3,R4),=C'ALL'                                                  
         LA    R4,3(R4)                                                         
         BR    RE                                                               
*                                            CLIENT                             
*                                                                               
CTCLI    MVC   0(07,R4),=C'CLIENT='                                             
         MVC   7(2,R4),WORK                                                     
         CLC   WORK(2),=C'ZZ'                                                   
         BE    *+10                                                             
         LA    R4,8(R4)                                                         
         BR    RE                                                               
         MVC   7(3,R4),=C'ALL'                                                  
         LA    R4,9(R4)                                                         
         BR    RE                                                               
*                                            CLIENT (3 BYTE)                    
*                                                                               
CTCLI3   MVC   0(07,R4),=C'CLIENT='                                             
         MVC   7(3,R4),WORK                                                     
         OC    WORK(3),WORK                                                     
         BNZ   *+10                                                             
         MVC   7(3,R4),=C'ALL'                                                  
         LA    R4,10(R4)                                                        
*                                            PRODUCT (1 BYTE)                   
*                                                                               
CTPRO    MVC   0(08,R4),=C'PRODUCT='                                            
         SR    R1,R1                                                            
         IC    R1,WORK                                                          
         EDIT  (R1),(3,8(R4)),ALIGN=LEFT                                        
         CLI   8(R4),C' '                                                       
         BNE   *+10                                                             
         MVC   8(3,R4),=C'ALL'                                                  
         LA    R4,12(R4)                                                        
         BR    RE                                                               
*                                            ESTIMATE (1 BYTE)                  
*                                                                               
CTEST    MVC   0(09,R4),=C'ESTIMATE='                                           
         SR    R1,R1                                                            
         IC    R1,WORK                                                          
         EDIT  (R1),(3,9(R4)),ALIGN=LEFT                                        
         CLI   9(R4),C' '                                                       
         BNE   *+10                                                             
         MVC   9(3,R4),=C'ALL'                                                  
         LA    R4,13(R4)                                                        
         BR    RE                                                               
*                                            AGENCY                             
*                                                                               
CTAGY2   MVC   0(07,R4),=C'AGENCY='                                             
         MVC   7(2,R4),WORK                                                     
         BR    RE                                                               
*                                            START & END DATE (PWOS)            
*                                                                               
CTSDTP   MVC   0(11,R4),=C'START DATE='                                         
         LA    R4,11(R4)                                                        
         B     CTDTP                                                            
*                                                                               
CTEDTP   MVC   0(09,R4),=C'END DATE='                                           
         LA    R4,9(R4)                                                         
*                                                                               
CTDTP    NTR1                                                                   
         L     R8,RELO                                                          
         GOTO1 =V(DATCON),DUB,(1,WORK),(8,0(R4)),RR=R8                          
         LA    R4,10(R4)                                                        
         XIT1                                                                   
*                                            AGENCY                             
*                                                                               
CTAGY    MVC   0(07,R4),=C'AGENCY='                                             
         MVC   7(2,R4),WORK                                                     
         CLC   WORK(2),=C'ZZ'                                                   
         BE    *+10                                                             
         LA    R4,8(R4)                                                         
         BR    RE                                                               
         MVC   7(3,R4),=C'ALL'                                                  
         LA    R4,9(R4)                                                         
         BR    RE                                                               
*                                            SERVICE                            
*                                                                               
CTSERV   MVC   0(08,R4),=C'SERVICE='                                            
         MVC   8(1,R4),WORK                                                     
         LA    R4,8(R4)                                                         
         BR    RE                                                               
*                                            2/3 BYTE PROGRAM                   
*                                                                               
CTPROG2  LA    R7,2                                                             
         B     CTPROGM                                                          
CTPROG3  LA    R7,3                                                             
CTPROGM  MVC   0(08,R4),=C'PROGRAM='                                            
         BCTR  R7,0                                                             
         EX    R7,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R4),WORK                                                     
         LA    R4,10(R4,R7)                                                     
         BR    RE                                                               
*                                            LINE ID                            
*                                                                               
CTLINE   MVC   0(08,R4),=C'LINE ID='                                            
         MVC   8(4,R4),WORK                                                     
         LA    R4,13(R4)                                                        
         BR    RE                                                               
*                                            TERMINAL ADDRESS                   
*                                                                               
CTADDR   MVC   0(08,R4),=C'ADDRESS='                                            
         MVC   8(4,R4),WORK                                                     
         LA    R4,13(R4)                                                        
         BR    RE                                                               
*                                            PASSWORD                           
*                                                                               
CTPASS   MVC   0(09,R4),=C'PASSWORD='                                           
         MVC   9(10,R4),WORK                                                    
         LA    R4,20(R4)                                                        
         BR    RE                                                               
*                                                                               
*                                                                               
EXIT     XMOD1 1                                                                
         EJECT                                                                  
*              LIST OF VALID SYSTEMS                                            
*                                                                               
SYSLST   DS    0CL10                                                            
         DC    X'00',C' ',CL8'ALL'                                              
         DC    X'02',C'S',CL8'SPOT'                                             
         DC    X'04',C'M',CL8'MEDIA'                                            
         DC    X'04',C'P',CL8'PRINT'                                            
         DC    X'06',C'A',CL8'ACCOUNT'                                          
         DC    X'08',C'R',CL8'REP'                                              
         DC    X'0A',C'C',CL8'CONTROL'                                          
         DC    X'0B',C'G',CL8'GAMES'                                            
         DC    X'E3',C'T',CL8'ONLINE'                                           
         DC    X'FF',X'FF',CL8'UNKNOWN'                                         
*                                                                               
*              LIST OF VALID RECORD TYPES                                       
*                                                                               
RECLST   DS    0CL18                                                            
         DC    C'C',AL3(CTKEYC),CL14'COMMENT       '                            
*NOP*    DC    C'D',AL3(CTKEYD),CL14'DEMO CONTROL  '                            
         DC    C'E',AL3(CTKEYE),CL14'ERROR         '                            
*NOP*    DC    C'F',AL3(CTKEYF),CL14'FILCON        '                            
*NOP*    DC    C'G',AL3(CTKEYG),CL14'GROUP         '                            
         DC    C'I',AL3(CTKEYI),CL14'ID            '                            
         DC    C'J',AL3(CTKEYJ),CL14'JCL           '                            
         DC    C'L',AL3(CTKEYL),CL14'LIBRARY       '                            
         DC    C'O',AL3(CTKEYO),CL14'OUTPUT        '                            
         DC    C'P',AL3(CTKEYP),CL14'PROFILE       '                            
*NOP*    DC    C'Q',AL3(CTKEYQ),CL14'PROTOTYPE     '                            
*NOP*    DC    C'R',AL3(CTKEYR),CL14'REQUEST       '                            
*NOP*    DC    C'S',AL3(CTKEYS),CL14'SCREEN        '                            
         DC    C'T',AL3(CTKEYT),CL14'TERMINAL      '                            
         DC    C'U',AL3(CTKEYU),CL14'USER PROFILE  '                            
         DC    X'FF'                                                            
         EJECT                                                                  
*              LITERALS                                                         
*                                                                               
         LTORG                                                                  
*              W/S                                                              
*                                                                               
WRKD     DSECT                                                                  
DUB      DS    D                                                                
RELO     DS    F                                                                
WORK     DS    CL20                                                             
         EJECT                                                                  
*                                                                               
*                                                                               
         PRINT OFF                                                              
       ++INCLUDE CTGENFILE                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006CTKEYOUT  05/01/02'                                      
         END                                                                    
