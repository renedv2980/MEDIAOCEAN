*          DATA SET DDCUREDIT  AT LEVEL 010 AS OF 01/08/98                      
*CATALP CUREDIT                                                                 
         TITLE 'EDITS CASH VALUES IN ANY CURRENCY'                              
CUREDIT  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**CURE**                                             
         USING WORKD,RC            RC=A(W/S)                                    
         LR    RA,R1                                                            
         USING CURPARMD,RA         RA=A(PARAMETER LIST)                         
         CTRY                                                                   
         STCM  R0,15,AGCOPT        GET AGCOPT/AGCTRY/LANG/CTRY                  
         MVI   DECPT,C'.'                                                       
         MVI   COMMA,C','                                                       
         CLI   LANG,2              TEST EUROPEAN LANGUAGE                       
         BNH   CUR04               NO                                           
         TM    CURPEDT2,CURPDEC    SUPPRESS SWITCH                              
         BO    CUR04                                                            
         MVI   DECPT,C','          YES SWAP DEC POINT AND COMMA                 
         MVI   COMMA,C'.'                                                       
         CLI   CTRY,8              CANADA?                                      
         BNE   CUR04                                                            
         MVI   COMMA,C' '                                                       
         TM    CURPEDT1,CURPMINY   MINUS=YES?                                   
         BZ    CUR04               NO                                           
         NI    CURPEDT1,X'FF'-CURPMINY                                          
         OI    CURPEDT2,CURPFLON   YES -- BUT DO FLOAT=- INSTEAD                
*                                                                               
CUR04    ICM   R1,15,CURPIVAL      GET INPUT VALUE OR ADDRESS IN R1             
         SR    R0,R0               CLEAR ALIGN=LEFT RETURNED LENGTH             
         B     CUR0                                                             
         EJECT                                                                  
* CONVERT INPUT TO PACKED VALUE IN DUB                                          
*                                                                               
CUR0     TM    CURPINPT,CURPISET   BINARY VALUE PASSED IN PLIST                 
         BZ    CUR2                                                             
         CVD   R1,DUB                                                           
         B     CUR6                                                             
*                                                                               
CUR2     MVC   DUB(1),CURPINPT     EXTRACT INPUT FIELD LENGTH                   
         NI    DUB,CURPILEN                                                     
         ZIC   RE,DUB                                                           
*                                                                               
         TM    CURPINPT,CURPIBIN   BINARY INPUT VALUE                           
         BZ    CUR4                                                             
         XC    DUB(4),DUB                                                       
         TM    0(R1),X'80'         TEST IF VALUE IS NEGATIVE                    
         BZ    *+10                                                             
         MVC   DUB(4),=F'-1'                                                    
         LA    RF,4                                                             
         SR    RF,RE                                                            
         LA    RF,DUB(RF)                                                       
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),0(R1)       MODE INPUT TO DUB                            
         L     R1,DUB                                                           
         CVD   R1,DUB                                                           
         B     CUR6                                                             
*                                                                               
CUR4     TM    CURPINPT,CURPIPAK   PACKED INPUT VALUE                           
         BO    *+6                                                              
         DC    H'0'                                                             
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         ZAP   DUB,0(0,R1)                                                      
         B     CUR6                                                             
*                                                                               
CUR6     TM    CURPEDT1,CURPDECN   TEST DECIMAL PLACES REQUIRED                 
         BZ    CUR8                                                             
         SR    R1,R1                                                            
         ICM   R1,1,CURPCDEC       R1=N'DECIMAL PLACES TO SHIFT OFF             
         BZ    CUR8                                                             
         LNR   R1,R1               GET TW0'S COMPLEMENT                         
         TM    CURPEDT2,CURPDECR   TEST IF ROUNDING REQUIRED                    
         BZ    *+14                                                             
         SRP   DUB,0(R1),5         SHIFT AND ROUND                              
         B     *+10                                                             
         SRP   DUB,0(R1),0         SHIFT WITHOUT ROUND                          
         B     CUR8                                                             
         EJECT                                                                  
* BUILD EDIT MASK                                                               
*                                                                               
CUR8     SR    R2,R2                                                            
         ICM   R2,7,CURPOADD       R2=A(OUTPUT)                                 
         SR    R3,R3                                                            
         ICM   R3,1,CURPOLEN       R3=L'OUTPUT                                  
         BZ    CUREXIT                                                          
         MVI   0(R2),C' '          CLEAR OUTPUT TO SPACES                       
         LR    RE,R3                                                            
         SH    RE,=H'2'                                                         
         BM    CUR10                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R2),0(R2)                                                    
*                                                                               
CUR10    TM    CURPEDT1,CURPZERB   DEAL WITH ZERO=BLANK                         
         BZ    *+14                                                             
         CP    DUB,=P'0'                                                        
         BE    CUREXIT                                                          
*                                                                               
         XC    MASK,MASK           BUILD VIRGIN MASK                            
         LA    R4,MASK+L'MASK-1    POINT R4 TO END OF MASK                      
         LA    R5,L'MASK           R5=L'MASK                                    
*                                                                               
         TM    CURPEDT1,CURPMINY+CURPCRDY                                       
         BZ    CUR14                                                            
         TM    CURPEDT1,CURPCRDY                                                
         BZ    CUR12                                                            
*                                                                               
         SH    R4,=H'2'            BACK-UP 2 BYTES                              
         SH    R5,=H'2'                                                         
         MVC   1(2,R4),=C'CR'                                                   
         B     CUR14                                                            
*                                                                               
CUR12    BCTR  R4,0                BACK-UP 1 BYTE                               
         BCTR  R5,0                                                             
         MVI   1(R4),C'-'                                                       
*                                                                               
CUR14    TM    CURPEDT1,CURPDECN                                                
         BO    CUR16                                                            
         SR    R1,R1                                                            
         ICM   R1,1,CURPCDEC                                                    
         BZ    CUR16                                                            
         LA    R1,1(R1)                                                         
         SR    R4,R1               BACK-UP N BYTES                              
         SR    R5,R1                                                            
         MVC   1(1,R4),DECPT       SET DECIMAL POINT CHR                        
*&&US                                                                           
         CLI   LANG,2              IF EUROPEAN LANGUAGE IN AMERICA              
         BNH   CUR16                                                            
         CLI   CURPCDEC,1          AND ONLY ONE DECIMAL PLACE                   
         BNE   CUR16                                                            
         MVI   1(R4),C'.'          YES-THIS IS NOT MONEY, SO FORCE A .          
*&&                                                                             
*                                                                               
CUR16    BCTR  R4,0                BACK-UP 1 BYTE                               
         BCTR  R5,0                                                             
         OI    0(R4),X'01'         SET SIGNIFICANCE INDICATOR                   
         LA    R1,1(R4)            PRESET R1 FOR EDMK                           
*                                                                               
         TM    CURPEDT1,CURPCOMY                                                
         BZ    CUR20                                                            
         AH    R4,=H'2'            GO FORWARD TWO BYTES                         
         AH    R5,=H'2'                                                         
*                                                                               
CUR18    SH    R5,=H'4'            INSERT COMMAS                                
         BNP   CUR20                                                            
         SH    R4,=H'4'                                                         
         MVC   0(1,R4),COMMA       SET COMMA CHR                                
         B     CUR18                                                            
*                                                                               
CUR20    LA    R0,L'DUB*2-1        R0=DIGIT COUNT                               
         LA    RF,MASK+L'MASK      RF=A(END OF MASK+1)                          
         LA    RE,MASK-1           RE=A(MASK-1)                                 
*                                                                               
CUR22    BCTR  RF,0                                                             
         TM    0(RF),X'F0'                                                      
         BNZ   CUR22                                                            
         OI    0(RF),X'20'                                                      
         BCT   R0,CUR22                                                         
         BCTR  RF,0                                                             
*                                                                               
         CR    RF,RE                                                            
         BE    *+12                                                             
         MVI   0(RF),C' '                                                       
         BCT   RF,*-10                                                          
*                                                                               
         MVC   NUMBER,DUB          SAVE PACKED NUMBER                           
         EDMK  MASK,DUB                                                         
*                                                                               
CUR23    LR    R4,R1               SAVE A(SIGNIFICANT DIGIT) IN R4              
         TM    CURPEDT1,CURPSYMN   TEST CURRENCY SYMBOL REQUIRED                
         BO    CUR25                                                            
         TM    CURPEDT2,CURPSYMS   IF SHORT SYMBOL FORCE LENGTH TO 1            
         BNO   *+12                                                             
         LA    RE,1                                                             
         B     CUR23A                                                           
         MVC   DUB+4(1),CURPCIND                                                
         NI    DUB+4,CURPCLEN      TURN OFF INDICATORS                          
         BZ    CUR25               EXIT IF ZERO LENGTH SYMBOL                   
         ZIC   RE,DUB+4                                                         
CUR23A   SR    R1,RE               BACK-UP BY SYMBOL LENGTH                     
         BCTR  RE,0                                                             
*                                                                               
         TM    CURPCIND,CURPCPFX   TEST PREFIX OR SUFFIX                        
         BNZ   CUR24                                                            
         LA    RF,MASK+L'MASK-1                                                 
         SR    RF,R4                                                            
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(R4)                                                    
         LA    R1,1(RF,R1)         POINT TO END                                 
*                                                                               
CUR24    EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),CURPCSYM    SET PREFIX/SUFFIX                            
         SR    R4,RE               BACK-UP BY SYMBOL LENGTH                     
         BCTR  R4,0                                                             
*                                                                               
CUR25    TM    CURPEDT2,CURPFLON   DEAL WITH FLOAT=-                            
         BZ    CUR26                                                            
         CP    NUMBER,=P'0'        TEST IF VALUE IS NEGATIVE                    
         BNL   CUR26                                                            
         BCTR  R4,0                                                             
         MVI   0(R4),C'-'          PREFIX NEGATIVE SIGN                         
*                                                                               
CUR26    TM    CURPEDT1,CURPBRAY   DEAL WITH BRACKET=YES                        
         BZ    CUR28                                                            
         LA    RF,MASK+L'MASK-1    MAKE ROOM FOR RIGHT BRACKET                  
         SR    RF,R4                                                            
         BCTR  R4,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),1(R4)                                                    
         BCTR  R4,0                                                             
         MVI   0(R4),C'('          MOVE THEM IN                                 
         LA    RF,MASK+L'MASK-2                                                 
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         MVI   1(RF),C')'                                                       
*                                                                               
CUR28    LA    RF,MASK+L'MASK      HANDLE LEFT OR RIGHT-ALIGN                   
         SR    RF,R4               RF=SIGNIFICANT LENGTH                        
         ZIC   RE,CURPOLEN         RE=OUTPUT LENGTH                             
         CR    RF,RE                                                            
         BNH   *+6                                                              
         LR    RF,RE               SIGNIFICANT LEN GTR THAN OUTPUT LEN          
         SR    R1,R1               SO USE OUTPUT LEN                            
         ICM   R1,7,CURPOADD                                                    
         TM    CURPEDT1,CURPALNL                                                
         BO    CUR30                                                            
         AR    R1,RE               ADJUST O/P POINTER FOR RIGHT ALIGN           
         SR    R1,RF                                                            
         CR    RF,RE               IF SIGNIF LEN GTR THAN OUTPUT LEN            
         BL    CUR32               CHOP LEFT DIGITS IF RIGHT-ALIGN              
         LA    R4,MASK+L'MASK                                                   
         SR    R4,RF                                                            
         B     CUR32                                                            
*                                                                               
CUR30    LR    R0,RF               RETURN SIGNFCNT LENGTH IF LEFT-ALIGN         
         LA    RE,MASK+L'MASK-1                                                 
         CLI   0(RE),C' '                                                       
         BH    CUR32                                                            
         BCTR  R0,0                                                             
         BCT   RE,*-10                                                          
*                                                                               
CUR32    BCTR  RF,0                MOVE TO OUTPUT AREA                          
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(R4)                                                    
*                                                                               
CUREXIT  TM    CURPEDT1,CURPALNL   RETURN SIGNIFICANT LENGTH IN R0              
         BNO   CUREXIT2            IF ALIGN=LEFT                                
         XIT1  REGS=(R0)                                                        
*                                                                               
CUREXIT2 XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
NUMBER   DS    D                                                                
*                                                                               
AGCOPT   DS    X                   AGENCY COUNTRY OPTIONS                       
AGCTRY   DS    X                   AGENCY COUNTRY                               
CTRY     DS    X                   ACTUAL COUNTRY                               
LANG     DS    X                   LANGUAGE                                     
*                                                                               
DECPT    DS    C                                                                
COMMA    DS    C                                                                
MASK     DS    CL30                                                             
WORK     DS    CL30                                                             
WORKX    EQU   *                                                                
         EJECT                                                                  
* DDCUREDITD                                                                    
       ++INCLUDE DDCUREDITD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DDCUREDIT 01/08/98'                                      
         END                                                                    
