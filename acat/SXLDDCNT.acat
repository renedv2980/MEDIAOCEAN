*          DATA SET SXLDDCNT   AT LEVEL 004 AS OF 06/04/20                      
*CATALP SXLDDCNT                                                                
         TITLE 'LDCOUNT - XSPDIR DIRECTORY REC TYPE COUNT/PRINT'                
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'FF'=PRINT                            
*        AL3   A(RECORD)                                                        
*        AL4   A(FILE NAME)                                                     
*                                                                               
         PRINT NOGEN                                                            
LDCOUNT  CSECT                                                                  
         NMOD1 0,SXLDDCNT,R8,R9                                                 
         SR    R3,R3               R3=A(DIRECTORY RECORD)                       
         ICM   R3,7,1(R1)                                                       
         CLI   0(R1),1             TEST COUNT ACTION                            
         BE    TALLYHO                                                          
         CLI   0(R1),X'FF'         TEST PRINT ACTION                            
         BE    PRINT                                                            
         B     XIT                                                              
*                                                                               
TDELETED LA    R4,DELETED                                                       
         USING RECTABD,R4                                                       
         AP    RECCOUNT,=P'1'                                                   
         J     CNT10                                                            
*                                                                               
TALLYHO  TM    32(R3),X'80'        DELETED?                                     
         JNZ   TDELETED                                                         
*                                                                               
CNT10    LA    R4,RECTAB           R4 = A(RECORD ENTRIES)                       
         LHI   RE,L'RECTAB         RE = LENGTH OF EACH ENTRY                    
         LA    RF,RECTABX          RF = END OF RECORD ENTRIES                   
*                                                                               
CNT20    CLC   0(2,R3),0(R4)                                                    
         JE    CNT50                                                            
         CLI   1(R4),0             TEST DO ONLY 1-BYTE COMPARE                  
         JNE   CNT25                                                            
         CLC   0(1,R3),0(R4)                                                    
         JE    CNT50                                                            
CNT25    BXLE  R4,RE,CNT20                                                      
                                                                                
         CLI   0(R3),0             TEST HEADER                                  
         BE    *+12                                                             
         CLI   0(R3),X'FF'          OR  TRAILER                                 
         BNE   CNT50                                                            
         LA    R4,CONTROL          POST TO CONTROL                              
********                                                                        
CNT50    ICM   RF,15,RECSPCL       TEST SPECIAL ROUTINE                         
         BZ    *+6                                                              
         BASR  RE,RF                                                            
         AP    RECCOUNT,=P'1'      BUMP COUNTER                                 
*                                                                               
         LA    R0,UNKNOWN                                                       
         CR    R0,R4               ARE WE POINTING AT UNKNOWN ?                 
         BNE   XIT                                                              
*                                                                               
         CLC   LASTUNK,0(R3)       SAME TYPE AS LAST UNKNOWN                    
         BE    XIT                                                              
*                                                                               
         MVC   LASTUNK,0(R3)       ELSE SAVE IT NOW AND PRINT IT                
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   P(16),=CL16'UNKNOWN XSPDIR'                                      
         MVI   P+16,C' '                                                        
         MVC   P+17(4),=C'KEY='                                                 
         GOTO1 =V(HEXOUT),DMCB,(R3),P+21,32                                     
         GOTO1 =V(PRINTER)                                                      
         DROP  RA                                                               
         B     XIT                                                              
LASTUNK  DC    H'0'                                                             
*                                                                               
CNTSLK   TM    SLKKIND-SLKRECD(R3),SLKKIPSV    SLK PASSIVE TEST                 
         BZR   RE                                                               
         LA    R4,PSSV0D73                                                      
         BR    RE                                                               
*NOP                                                                            
*NTTRF   CLC   =X'0A61',0(R3)          TEST PATTERN                             
******   BNE   CNTTRF2                                                          
CNTTRF   CLI   NPTXPSSV-NPTXKEY(R3),0  TEST PASSIVE                             
         BNE   CNTTRF4                 NON-ZERO IS PASSIVE                      
*                                                                               
CNTTRF2  TM    1(R3),X'80'         TEST TRAFFIC PASSIVE                         
         BZR   RE                                                               
*                                                                               
CNTTRF4  LA    R4,PSSVTRF                                                       
         BR    RE                                                               
*                                                                               
CNT0D06  CLI   DDMKSPL-DDMRECD(R3),0  CANADIAN ORDER DEMOS SPILL                
         BNE   CNT0D06A               YES - PASSIVE                             
         TM    DDMKAGMD-DDMRECD(R3),X'03'   TEST NETWORK                        
         BNOR  RE                           NO - THEN IT'S ACTIVE               
         LA    R1,DDMKMKT-DDMRECD(R3)       POINT TO MARKET                     
         OC    0(2,R1),0(R1)                TEST MARKET 0                       
         BZR   RE                           ZERO IS ACTIVE                      
CNT0D06A LA    R4,PSSV0D06                                                      
         BR    RE                                                               
*                                                                               
XIT      XIT1                                                                   
         EJECT                                                                  
PRINT    L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         L     RE,4(R1)                                                         
         MVC   HLINE+8(7),0(RE)                                                 
         MVC   P(26),HLINE                                                      
         GOTO1 =V(PRINTER)                                                      
         MVC   P(26),ULINE                                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         MVC   P(16),=CL16'MAJOR POINTERS'                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    R4,FRSTMAJ                                                       
         LHI   R6,L'RECTAB         HEADING                                      
         LA    R7,LASTMAJ                                                       
*                                                                               
         ZAP   COUNT,=P'0'                                                      
         BAS   RE,PRTSET           PRINT MAJOR POINTER SET                      
         ZAP   MAJCOUNT,COUNT                                                   
*                                                                               
         MVC   P(16),=CL16'MAJOR PTR TOTAL'                                     
         OI    COUNT+5,X'0F'                                                    
         UNPK  P+16(10),COUNT                                                   
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)         SKIP A LINE                                  
*                                                                               
         MVC   P(16),=CL16'MINOR POINTERS'                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    R4,FRSTMIN          FIRST MINOR POINTER                          
         LHI   R6,L'RECTAB         HEADING                                      
         LA    R7,LASTMIN                                                       
*                                                                               
         ZAP   COUNT,=P'0'                                                      
         BAS   RE,PRTSET           PRINT MINOR POINTER SET                      
         ZAP   MINCOUNT,COUNT                                                   
*                                                                               
         MVC   P(16),=CL16'MINOR PTR TOTAL'                                     
         OI    COUNT+5,X'0F'                                                    
         UNPK  P+16(10),COUNT                                                   
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)         SKIP A LINE                                  
*                                                                               
         MVC   P(16),=CL16'OTHER POINTERS'                                      
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         LA    R4,FRSTOTH          FIRST MINOR POINTER                          
         LHI   R6,L'RECTAB         HEADING                                      
         LA    R7,LASTOTH                                                       
*                                                                               
         ZAP   COUNT,=P'0'                                                      
         BAS   RE,PRTSET           PRINT MINOR POINTER SET                      
         GOTO1 =V(PRINTER)         SKIP A LINE                                  
*                                                                               
         MVC   P(16),=CL16'TOTAL POINTERS'                                      
         ZAP   COUNT,MAJCOUNT                                                   
         AP    COUNT,MINCOUNT                                                   
         LA    R4,UNKNOWN                                                       
         AP    COUNT,RECCOUNT                                                   
         LA    R4,CONTROL                                                       
         AP    COUNT,RECCOUNT                                                   
         OI    COUNT+5,X'0F'                                                    
         UNPK  P+16(10),COUNT                                                   
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         GOTO1 =V(PRINTER)                                                      
         B     XIT                                                              
*                                                                               
PRTSET   NTR1                                                                   
*                                                                               
PRTSET2  MVC   P(16),RECDESC       MAX 16 CHRS                                  
         AP    COUNT,RECCOUNT                                                   
         OI    RECCOUNT+5,X'0F'                                                 
         UNPK  P+16(10),RECCOUNT   MAX 10 DIGITS                                
         CLI   P+16,C'0'                                                        
         BNE   *+8                                                              
         MVI   P+16,C' '                                                        
         MVC   P+27(L'RECPBOOK),RECPBOOK                                        
         MVC   P+39(L'RECDSECT),RECDSECT                                        
         GOTO1 =V(PRINTER)                                                      
         BXLE  R4,R6,PRTSET2                                                    
         B     XIT                                                              
         EJECT                                                                  
*                   0         1         2         3         4                   
*                   0123456789012345678901234567890123456789012345678           
HLINE    DC    CL49'REC TYP XXXXXXX  COUNT     PANBOOK     DSECT     '          
ULINE    DC    CL49'---------------  --------- ----------- ----------'          
*                                                                               
         DS    0D                                                               
         DC    CL8'*RECTAB*'                                                    
RECTAB   DS    0XL(RECTABLQ)                                                    
FRSTMAJ  EQU   *                                                                
*****    DC    X'0200',PL6'0',CL20'GOALS               ',AL4(0)                 
*****    DC    CL10'          ',CL10'          '                                
* NET TRAFFIC                                                                   
         DC    X'0A35',PL6'0',CL20'NET COMTEXT RECORD  ',AL4(0)                 
         DC    CL10'SPTRCMLTXT',CL10'CMTRECD   '                                
         DC    X'0A61',PL6'0',CL20'NET PATTERN RECORD  ',AL4(CNTTRF)            
         DC    CL10'SPTRNPAT  ',CL10'NPTRECD   '                                
         DC    X'0A1D',PL6'0',CL20'NET REVISION RECORD ',AL4(0)                 
         DC    CL10'SPTRNREV  ',CL10'REVRECD   '                                
         DC    X'0A2D',PL6'0',CL20'NET TRAF COMMENT REC',AL4(0)                 
         DC    CL10'SPTRDTXT  ',CL10'DTXRECD   '                                
         DC    X'0D01',PL6'0',CL20'CAN ORD CAMPAIGN    ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'CMPRECD   '                                
         DC    X'0D02',PL6'0',CL20'CAN ORD CAMP STA-EST',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'CSERECD   '                                
         DC    X'0D03',PL6'0',CL20'CAN ORD CAMP STALIST',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'CSLRECD   '                                
         DC    X'0D05',PL6'0',CL20'CAN ORDER PROGRAM   ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DPRRECD   '                                
         DC    X'0D06',PL6'0',CL20'CAN ORDER DEMOS     ',AL4(CNT0D06)           
         DC    CL10'SPGENCDORD',CL10'DDMRECD   '                                
CDORD    DC    X'0D07',PL6'0',CL20'CAN ORDER           ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'CORRECD   '                                
         DC    X'0D08',PL6'0',CL20'CAN ORD GOAL        ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DGLRECD   '                                
         DC    X'0D0A',PL6'0',CL20'CAN ORD CONTACT     ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DCTRECD   '                                
         DC    X'0D0B',PL6'0',CL20'HISTORY             ',AL4(0)                 
         DC    CL10'SPGENHIST ',CL10'HISTRECD  '                                
         DC    X'0D0C',PL6'0',CL20'I2 COMMENT          ',AL4(0)                 
         DC    CL10'SPGENXCOM ',CL10'XCOMKEY   '                                
         DC    X'0D0D',PL6'0',CL20'MARKET MAPPING      ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DMPRECD   '                                
         DC    X'0D0E',PL6'0',CL20'STATION MAPPING     ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DMPKSTSQ  '                                
         DC    X'0D0F',PL6'0',CL20'DEMO CAT MAPPING    ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DMPKSTDQ  '                                
         DC    X'0D1B',PL6'0',CL20'ORDER HISTORY       ',AL4(0)                 
         DC    CL10'SPGENORHIS',CL10'OHISRECD  '                                
         DC    X'0D36',PL6'0',CL20'DARE CBL MKGD NOTICE',AL4(0)                 
         DC    CL10'SPGENDRMKN',CL10'MNXKEY    '                                
         DC    X'0D37',PL6'0',CL20'DARE CBL MKGD OFFER ',AL4(0)                 
         DC    CL10'SPGENDRMKO',CL10'MOXKEY    '                                
         DC    X'0D39',PL6'0',CL20'SUPERDESK AUTH      ',AL4(0)                 
         DC    CL10'SPGENAUTH ',CL10'AUTRECD   '                                
         DC    X'0D3A',PL6'0',CL20'SPT AUTOPAY XAPY    ',AL4(0)                 
         DC    CL10'SPGENXAPY ',CL10'APXRECD   '                                
         DC    X'0D3B',PL6'0',CL20'DARE MKGD REJ CMMNTS',AL4(0)                 
         DC    CL10'SPGENDRMRC',CL10'DAREMGCD  '                                
         DC    X'0D3F',PL6'0',CL20'NETWORK AUTOPAY     ',AL4(0)                 
         DC    CL10'NEGENAPY  ',CL10'NAPRECD   '                                
         DC    X'0D73',PL6'0',CL20'STATION LOCKIN      ',AL4(CNTSLK)            
         DC    CL10'SPGENXLK  ',CL10'SLKRECD   '                                
         DC    X'0D74',PL6'0',CL20'SPOT EXTRACT        ',AL4(0)                 
         DC    CL10'SPGENXS   ',CL10'XSRECD    '                                
*                                                                               
         DC    X'0E03',PL6'0',CL20'STATION INVOICE     ',AL4(0)                 
         DC    CL10'SPGENSNV  ',CL10'SNVKEYD   '                                
         DC    X'0E04',PL6'0',CL20'MATCHING STATUS     ',AL4(0)                 
         DC    CL10'SPGENMSR  ',CL10'MSRKEYD   '                                
         DC    X'0E05',PL6'0',CL20'BUYING GDLN STAT    ',AL4(0)                 
         DC    CL10'SPGENBGR  ',CL10'BGRKEYD   '                                
         DC    X'0E06',PL6'0',CL20'NET UNIT BILL       ',AL4(0)                 
         DC    CL10'NEGENUBILL',CL10'NUBKEY    '                                
         DC    X'0E07',PL6'0',CL20'SPT BILLING HOLD    ',AL4(0)                 
         DC    CL10'SPGENBHOLD',CL10'BHLDRECD  '                                
         DC    X'0E08',PL6'0',CL20'SPOT/NET UNBILL     ',AL4(0)                 
         DC    CL10'SPGENUNBIL',CL10'UNBRECD   '                                
         DC    X'0E0A',PL6'0',CL20'NET UNIT BILL       ',AL4(0)                 
         DC    CL10'NEGENUBILL',CL10'NUBRECD   '                                
         DC    X'0E0B',PL6'0',CL20'NET BILLING HOLD    ',AL4(0)                 
         DC    CL10'NEGENBHOLD',CL10'BHLNRECD  '                                
         DC    X'0E0C',PL6'0',CL20'WB FLIGHT           ',AL4(0)                 
         DC    CL10'SPGENWBFLT',CL10'WFLIGHTD  '                                
         DC    X'0E0D',PL6'0',CL20'BILLING PERCENT     ',AL4(0)                 
         DC    CL10'SPGENBPCT ',CL10'BPCRECD   '                                
         DC    X'0E10',PL6'0',CL20'SPT DESKTOP REV     ',AL4(0)                 
         DC    CL10'SPGENDREV ',CL10'DRVRECD   '                                
         DC    X'0E11',PL6'0',CL20'SPT DESKTOP WORK    ',AL4(0)                 
         DC    CL10'SPGENDREV ',CL10'DWKKSUBQ  '                                
         DC    X'0E12',PL6'0',CL20'SPT DESKTOP PROP    ',AL4(0)                 
         DC    CL10'SPGENDREV ',CL10'DPRKSUBQ  '                                
         DC    X'0E13',PL6'0',CL20'SPT DEVIATED WEEKS  ',AL4(0)                 
         DC    CL10'SPGENDDEV ',CL10'DDVRECD   '                                
         DC    X'0E15',PL6'0',CL20'EMAIL               ',AL4(0)                 
         DC    CL10'SPGENEMAIL',CL10'EMLRECD   '                                
LASTMAJ  EQU   *-1                                                              
*                                                                               
FRSTMIN  EQU   *                                                                
PSSVTRF  DC    X'0A00',PL6'0',CL20'PAT PASSIVE FOR LIST',AL4(CNTTRF)            
         DC    CL10'SPTRNPAT  ',CL10'NPTXPSSV  '                                
         DC    X'0AE1',PL6'0',CL20'NET PATTERN PASSIVE ',AL4(0)                 
         DC    CL10'SPTRNPAT  ',CL10'NPTXPSSV  '                                
PSSV0D06 DC    X'0D06',PL6'0',CL20'CAN ORD DEM         ',AL4(CNT0D06)           
         DC    CL10'SPGENCDORD',CL10'DDMRECD   '                                
PSSV0D73 DC    X'0D73',PL6'0',CL20'STATION LOCKIN      ',AL4(0)                 
         DC    CL10'SPGENXLK  ',CL10'SLKRECD   '                                
         DC    X'0E83',PL6'0',CL20'SPOT INV MATCHMAKER ',AL4(0)                 
         DC    CL10'SPGENSNV  ',CL10'SNVPKEY   '                                
         DC    X'0D87',PL6'0',CL20'CAN ORD             ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'CODKTYPE  '                                
         DC    X'0E8C',PL6'0',CL20'WB FLIGHT           ',AL4(0)                 
         DC    CL10'SPGENWBFLT',CL10'WP1KEY    '                                
         DC    X'0D8D',PL6'0',CL20'CAN MKT NAME        ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DMPPKSMQ  '                                
         DC    X'0D8E',PL6'0',CL20'CAN STA CALL        ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DMPPKSSQ  '                                
         DC    X'0D8F',PL6'0',CL20'CAN DEMO CAT        ',AL4(0)                 
         DC    CL10'SPGENCDORD',CL10'DMPPKSDQ  '                                
         DC    X'0E93',PL6'0',CL20'NET CBL MATCHMAKER  ',AL4(0)                 
         DC    CL10'SPGENSNV  ',CL10'SNVNKEY   '                                
         DC    X'0E95',PL6'0',CL20'EMAIL               ',AL4(0)                 
         DC    CL10'SPGENEMAIL',CL10'EMLPKEY   '                                
         DC    X'0E9C',PL6'0',CL20'WB FLIGHT           ',AL4(0)                 
         DC    CL10'SPGENWBFLT',CL10'WP2KEY    '                                
         DC    X'0EA3',PL6'0',CL20'REP INVOICES        ',AL4(0)                 
         DC    CL10'SPGENSNV  ',CL10'SNVRKEY   '                                
         DC    X'0EB3',PL6'0',CL20'MORE REP INVOICE    ',AL4(0)                 
         DC    CL10'SPGENSNV  ',CL10'SNVLKEY   '                                
         DC    X'0DB9',PL6'0',CL20'SDESK AUTH DUE      ',AL4(0)                 
         DC    CL10'SPGENAUTH ',CL10'AUPKEY    '                                
         DC    X'0DBC',PL6'0',CL20'DARE ORDER DATE TIME',AL4(0)                 
         DC    CL10'SPGENDRORD',CL10'DODKTYPE  '                                
         DC    X'0DC9',PL6'0',CL20'SDESK AUTH STRT     ',AL4(0)                 
         DC    CL10'SPGENAUTH ',CL10'AUSKEY    '                                
LASTMIN  EQU   *-1                                                              
RECTABX  EQU   *-1                                                              
*                                                                               
FRSTOTH  EQU   *                                                                
UNKNOWN  DC    X'0000',PL6'0',CL20'UNKNOWN TYPES       ',AL4(0)                 
         DC    CL10'          ',CL10'          '                                
CONTROL  DC    X'0000',PL6'0',CL20'HEADER/TRAILER      ',AL4(0)                 
         DC    CL10'          ',CL10'          '                                
DELETED  DC    X'0000',PL6'0',CL20'DELETED POINTERS    ',AL4(0)                 
         DC    CL10'          ',CL10'          '                                
LASTOTH  EQU   *-1                                                              
*                                                                               
COUNT    DC    PL6'0'                                                           
MAJCOUNT DC    PL6'0'                                                           
MINCOUNT DC    PL6'0'                                                           
*                                                                               
DMCB     DS    6A                                                               
         LTORG                                                                  
*                                                                               
RECTABD  DSECT                                                                  
RECTYPE  DS    XL2                                                              
RECCOUNT DS    PL6                                                              
RECDESC  DS    CL20                                                             
RECSPCL  DS    AL4                 SPECIAL ROUTINE ADDRESS                      
RECPBOOK DS    CL10                PANBOOK NAME                                 
RECDSECT DS    CL10                DSECT NAME                                   
RECTABLQ EQU   *-RECTABD                                                        
         PRINT OFF                                                              
*DDDPRINT                                                                       
       ++INCLUDE DDDPRINT                                                       
*SPGENXLK                                                                       
       ++INCLUDE SPGENXLK                                                       
       ++INCLUDE SPTRNPAT                                                       
       ++INCLUDE SPGENCDORD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004SXLDDCNT  06/04/20'                                      
         END                                                                    
