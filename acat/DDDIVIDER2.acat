*          DATA SET DDDIVIDER2 AT LEVEL 014 AS OF 05/01/02                      
*CATALP DDDIVIDER2                                                              
         TITLE 'MODULE TO CONTROL PRINTING OF DIVIDER PAGES'                    
DIVIDER  CSECT                                                                  
         ENTRY DIVIDER2                                                         
DIVIDER2 DS    0H                                                               
         NMOD1 20,**RUNDIV                                                      
         LM    R2,R3,12(R1)                                                     
         ST    R2,PRINT                                                         
         ST    R3,PPLOZ                                                         
         USING RUNDIVD,RC                                                       
         EJECT                                                                  
*                   INITIALIZATION                                              
         SPACE 2                                                                
         MVC   STFILL,SPACES                                                    
         MVC   STEND,=C'START'                                                  
         CLI   0(R1),0             (0=START, 1=END)                             
         BZ    *+10                                                             
         MVC   STEND,=C'END  '                                                  
         MVC   BIGONE,SPACES                                                    
         MVC   BIGTWO,SPACES                                                    
         LM    R2,R4,0(R1)         (A(LOGO) A(COMPNAME) A(COMPADDR))            
         MVC   BIGONE(7),0(R2)                                                  
         MVC   COMPNAME,0(R3)                                                   
         MVC   COMPADD,0(R4)                                                    
         MVC   COMPNAME+33(3),SPACES                                            
         MVC   COMPADD+33(3),SPACES                                             
*&&DO                                                                           
         COMRG                                                                  
         MVC   BIGTWO(7),24(R1)    FIRST 7 OF JOB NAME                          
*&&                                                                             
*&&OS                                                                           
         EXTRACT DUB,FIELDS=TIOT                                                
         L     R1,DUB                                                           
         MVC   BIGJOB(7),0(R1)                                                  
*&&                                                                             
         MVC   P,SPACES            SKIP TO CHANNEL 1 (LINE 5)                   
         BAS   RE,SKIP                                                          
         MVC   RCDATE,0(R1)                                                     
         EJECT                                                                  
*                   SET UP FOR THREE CIRCUITS                                   
         SPACE 3                                                                
         LA    R2,3                                                             
         B     DV4                                                              
         SPACE 2                                                                
DV2      LA    R3,5                SIX LINES OF XXX                             
         MVI   P,C'X'                                                           
         MVC   P+1(109),P                                                       
         BAS   RE,SPACE1                                                        
         BCT   R3,*-14                                                          
         MVI   P,C'X'                                                           
         MVC   P+1(109),P                                                       
         BAS   RE,SPACE2                                                        
         SPACE 2                                                                
DV4      BAS   RE,DVAGY                                                         
         BAS   RE,SPACE2                                                        
         GOTO1 PPLOZ,DIVPARA,STEND,PRINT                                        
         BAS   RE,SPACE1                                                        
         GOTO1 PPLOZ,DIVPARA,BIGONE,PRINT                                       
         SPACE 1                                                                
         BAS   RE,SPACE1                                                        
         GOTO1 PPLOZ,DIVPARA,BIGTWO,PRINT                                       
         BAS   RE,SPACE1                                                        
         CH    R2,=H'1'                                                         
         BE    *+8                                                              
         BAS   RE,DVAGY                                                         
         BAS   RE,SPACE2                                                        
         BCT   R2,DV2                                                           
         XMOD1 1                                                                
         EJECT                                                                  
*                   ROUTINE TO PRINT AGENCY DETAILS                             
         SPACE 3                                                                
DVAGY    NTR                                                                    
         LA    R4,2                                                             
         SPACE 2                                                                
DV6      MVC   P(5),STEND                                                       
         MVC   P+6(36),COMPNAME                                                 
         MVC   P+44(8),RCDATE                                                   
         MVC   P+55(55),P                                                       
         BAS   RE,SPACE1                                                        
         MVC   P+6(36),COMPADD                                                  
         MVC   P+55(55),P                                                       
         BAS   RE,SPACE2                                                        
         BCT   R4,DV6                                                           
         XIT                                                                    
         EJECT                                                                  
*                   PRINTING AIDS                                               
         SPACE 2                                                                
SKIP     MVC   DIVCON,=C'BC01'                                                  
         B     GOPRINT                                                          
         SPACE 2                                                                
SPACE1   MVC   DIVCON,=C'BL01'                                                  
         B     GOPRINT                                                          
         SPACE 2                                                                
SPACE2   MVC   DIVCON,=C'BL02'                                                  
         B     GOPRINT                                                          
         SPACE 2                                                                
GOPRINT  NTR                                                                    
         GOTO1 PRINT,DIVPARA,P,DIVCON                                           
         MVC   P,SPACES                                                         
         XIT                                                                    
         EJECT                                                                  
RCDATE   DC    CL8' '                                                           
         DS    CL1                                                              
P        DC    CL133' '                                                         
PRINT    DS    V                                                                
PPLOZ    DS    V                                                                
SPACES   DC    CL133' '                                                         
         EJECT                                                                  
*                   DSECT FOR PPRUNDIV                                          
         SPACE 3                                                                
RUNDIVD  DSECT                                                                  
DIVPARA  DS    4F                                                               
DUB      DS    D                                                                
DIVCON   DS    CL4                                                              
BIGONE   DS    D                                                                
BIGTWO   DS    D                                                                
STEND    DS    CL5                                                              
STFILL   DS    CL3                                                              
COMPADD  DS    CL36                                                             
COMPNAME DS    CL36                                                             
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014DDDIVIDER205/01/02'                                      
         END                                                                    
