*          DATA SET DMEOFCHK   AT LEVEL 001 AS OF 08/08/13                      
*CATALP DMEOFCHK                                                                
         TITLE 'DMEOFCHK - CHECK EOF POINTER IS CORRECT'                        
                                                                                
***********************************************************************         
*R1 POINTS TO DMCB SO THIS CAN BE CALLED VIA DMDMGR                   *         
*DMCBW1  A(C'EOFCHK' DATAMGR COMMAND)                                 *         
*DMCBW2  A(FILE DDNAME)                                               *         
*DMCBW3  A(FILE INFO TABLE) RETURNED TO CALLER                        *         
***********************************************************************         
         PRINT NOGEN                                                            
DMEOFCHK CSECT                                                                  
         NMOD1 WRKX-WRKD,DMEOFCHK,CLEAR=YES                                     
         USING WRKD,RC                                                          
         SAM24                                                                  
         LR    R2,R1               R2=A(PARAM LIST)                             
         SR    RE,RE                                                            
         ICM   RE,7,5(R2)          DMCBW2=A(DDNAME)                             
         MVC   DDNADDN,0(RE)                                                    
         LA    RF,DDNADATA                                                      
         ST    RF,8(R2)            DMCBW3=A(FILE INFO TABLE)                    
         EJECT                                                                  
***********************************************************************         
*SEARCH SYSFLES FOR MATCH ON DD NAME AT DTF+22                        *         
***********************************************************************         
FNDFIL   ICM   R3,15,=V(SYSFLES)   R3=A(SYSFLES ENTRY FOR FILE)                 
         BZ    FNDFILX                                                          
         USING SYSFLSTD,R3                                                      
FNDFIL1  CLI   0(R3),X'FF'         TEST END OF SYSFLES LIST                     
         BE    FNDFILX                                                          
         MVC   DDNASENO,SYSFSYS#   SAVE SENUM                                   
         MVC   DDNASEF1,SYSFSTAT   SAVE SEFLAGS                                 
         ST    R3,DDNAASFL                                                      
         SR    R0,R0                                                            
         IC    R0,SYSF#FLS+1       GET NUMBER OF FILES                          
         LA    R3,SYSFLIST         POINT TO FIRST                               
         SR    R4,R4                                                            
*                                                                               
FNDFIL2  TM    SYSFIND2,SFALIAS    IGNORE ALIAS FILES                           
         BO    FNDFIL3                                                          
         ICM   R4,7,SYSFADTF       R4=A(DTF)                                    
         USING DTFPHD,R4                                                        
         TM    DTFTYPE,DTFTEMU     IGNORE EMULATED FILES                        
         BO    FNDFIL3                                                          
FNDFIL2A TM    DTFTYPE,DTFTIS      TEST IF INDEX SEQ FILE                       
         BO    FNDFIL2B                                                         
         TM    DTFFLAG,DTFRODTF    IGNORE ADD=NO DA FILES                       
         BZ    FNDFIL2C                                                         
         B     FNDFIL3                                                          
         USING ISDTF,R4                                                         
FNDFIL2B TM    ISCMPRSW,X'40'      TEST IF ADD=NO IS FILE                       
         BZ    FNDFIL2C                                                         
*&&US*&& CLI   DDNASENO,X'0C'      US DEMOS CAN HAVE ADD=NO FILES               
*&&US*&& BE    FNDFIL2C                                                         
         B     FNDFIL3                                                          
FNDFIL2C CLC   DDNADDN,22(R4)      MATCH ON DDNAME IN DTF                       
         BE    FNDFIL4                                                          
*                                                                               
FNDFIL3  LA    R3,SYSFLNQ(R3)      LOOP FOR EACH FILE                           
         BCT   R0,FNDFIL2                                                       
         B     FNDFIL1             TRY NEXT SYSTEM                              
*                                                                               
FNDFIL4  MVC   DDNAFINO,SYSFILE#   SET FILE NUMBER                              
         MVC   DDNAFIF1(2),SYSFIND1 SET FILE FLAGS                              
         ST    R4,DDNAADTF         SET A(DTF)                                   
*                                                                               
FNDFILX  CLI   DDNAFINO,0          TEST IF FILE FOUND                           
         BE    ERR1                                                             
         DROP  R3,R4                                                            
         EJECT                                                                  
***********************************************************************         
*EXTRACT FILE DATA AND OPEN FILE IF NOT OPEN.                         *         
*SEARCH FOR EOF IF DNEXT NOT ALREADY SET.                             *         
***********************************************************************         
         USING DTFPHD,R4                                                        
SETFIL   MVC   FILFMT,DTFTYPE      SET FORMAT 0/1/2/3 FOR 16/18/20/22           
         NI    FILFMT,X'03'                                                     
         LLC   RE,FILFMT                                                        
         SLL   RE,2                                                             
         LA    RE,FILDATA(RE)                                                   
         MVC   FDNEXT,0(RE)        SET FIRST DNEXT FOR EMPTY FILE               
*                                                                               
SETFIL1  TM    DTFTYPE,DTFTIS      TEST IF INDEX SEQ FILE                       
         BO    ERR2                                                             
*                                                                               
SETFIL2  TM    DTFOPEN,DTF_OPN     OPEN FILE IF NOT ALREADY OPEN                
         BO    SETFIL3                                                          
         MVC   P1,=AL4(DAOPEN)     A(DAOPEN)                                    
         LA    RE,IOA                                                           
         ST    RE,P2               A(IOAREA)                                    
         XC    P3,P3                                                            
         ST    R4,P4               A(DTF)                                       
         MVC   DSKADR,FDNEXT                                                    
         LA    RE,DSKADR                                                        
         ST    RE,P5               A(DSKADR)                                    
         GOTO1 =V(DADDS),P1                                                     
         OC    P3(2),P3            TEST FOR ERRORS                              
         BNZ   ERR3                CANT OPEN FILE                               
         OI    FILFLG,X'01'                                                     
*                                                                               
SETFIL3  OC    DNEXT,DNEXT         FIND EOF IF NOT ALREADY SET                  
         BNZ   SETFIL4                                                          
         MVC   P1,=AL4(FNDEOF)     A(FNDEOF)                                    
         LA    RE,IOA                                                           
         ST    RE,P2               A(IOAREA)                                    
         XC    P3,P3                                                            
         ST    R4,P4               A(DTF)                                       
         MVC   DSKADR,FDNEXT                                                    
         LA    RE,DSKADR                                                        
         ST    RE,P5               A(DSKADR)                                    
         GOTO1 =V(DADDS),P1                                                     
         OC    P3(2),P3            TEST FOR ERRORS                              
         BNZ   ERR4                CANT FIND DNEXT                              
         OI    FILFLG,X'02'                                                     
*                                                                               
SETFIL4  MVC   DDNADSNI(4),DNEXT   RETURN DNEXT                                 
         EJECT                                                                  
***********************************************************************         
*CALL DADDS TO READ THE LAST RECORD ON FILE GIVEN BY DNEXT            *         
***********************************************************************         
DAEOF    MVC   EOFAD1,DNEXT                                                     
         OC    EOFAD1,EOFAD1       SET TO FIRST RECORD IF DNEXT NOT SET         
         BNZ   *+10                                                             
         MVC   EOFAD1,FDNEXT                                                    
*                                                                               
         MVC   P1,=AL4(RDID)       A(RDID)                                      
         LA    RE,IOA                                                           
         ST    RE,P2               A(IOAREA)                                    
         LA    RE,L'IOA                                                         
         MVC   P3(2),=C'L='                                                     
         STH   RE,P3+2             L'IOAREA                                     
         ST    R4,P4               A(DTF)                                       
         MVC   DSKADR,EOFAD1                                                    
         LA    RE,DSKADR                                                        
         ST    RE,P5               A(DSKADR)                                    
*                                                                               
DAEOF1   XC    IOA,IOA             READ CURRENT DNEXT RECORD                    
         GOTO1 =V(DADDS),P1                                                     
         OC    P3(2),P3            TEST FOR ERRORS                              
         BNZ   ERR5                ERROR IF FAIL TO READ RECORD                 
         EJECT                                                                  
***********************************************************************         
*CALL DADDS TO READ THE NEXT RECORD ON THE SAME TRACK AS DNEXT        *         
***********************************************************************         
DAEOF2   MVC   EOFAD2,EOFAD1       SET EOFAD2 TO NEXT REC ON SAME TRACK         
         TM    DTFTYPE,DTFTBIGF                                                 
         BO    DAEOF2B                                                          
         TM    DTFTYPE,DTFTBIG                                                  
         BZ    DAEOF2D                                                          
*                                                                               
DAEOF2A  TM    EOFAD2+2,X'3F'      18-BIT - MAX 63 RECS/TRK                     
         BO    DAEOF2X                                                          
         IC    RE,EOFAD2+2                                                      
         SRDL  RE,6                                                             
         SRL   RF,26                                                            
         LA    RF,1(RF)                                                         
         SLL   RF,26                                                            
         SLDL  RE,6                                                             
         STC   RE,EOFAD2+2                                                      
         B     DAEOF2R                                                          
*                                                                               
DAEOF2B  TM    EOFAD2+3,X'FF'      20-BIT AND 22-BIT - MAX 255 RECS/TRK         
         BO    DAEOF2X                                                          
         IC    RE,EOFAD2+3                                                      
         LA    RE,1(RE)                                                         
         STC   RE,EOFAD2+3                                                      
         B     DAEOF2R                                                          
*                                                                               
DAEOF2D  TM    EOFAD2+2,X'FF'      16-BIT - MAX 255 RECS/TRK                    
         BO    DAEOF2X                                                          
         IC    RE,EOFAD2+2                                                      
         LA    RE,1(RE)                                                         
         STC   RE,EOFAD2+2                                                      
         B     DAEOF2R                                                          
*                                                                               
DAEOF2R  MVC   P1,=AL4(RDID)       A(RDID)                                      
         LA    RE,IOA                                                           
         ST    RE,P2               A(IOAREA)                                    
         LA    RE,L'IOA                                                         
         MVC   P3(2),=C'L='                                                     
         STH   RE,P3+2             L'IOAREA                                     
         ST    R4,P4               A(DTF)                                       
         MVC   DSKADR,EOFAD2                                                    
         LA    RE,DSKADR                                                        
         ST    RE,P5               A(DSKADR)                                    
         XC    IOA,IOA                                                          
         GOTO1 =V(DADDS),P1                                                     
         OC    P3(2),P3            TEST FOR ERRORS                              
         BZ    ERR6                ERROR IF RECORD FOUND                        
*                                                                               
DAEOF2X  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
*CALL DADDS TO READ THE FIRST RECORD ON THE NEXT TRACK                *         
***********************************************************************         
DAEOF3   MVC   EOFAD3,EOFAD1       SET EOFAD3 TO FIRST REC ON NEXT TRK          
         TM    DTFTYPE,DTFTBIGF                                                 
         BO    DAEOF3B                                                          
         TM    DTFTYPE,DTFTBIG                                                  
         BZ    DAEOF3D                                                          
*                                                                               
DAEOF3A  ICM   RE,7,EOFAD3         18-BIT                                       
         SRL   RE,6                                                             
         LA    RE,1(RE)                                                         
         SLL   RE,6                                                             
         STCM  RE,7,EOFAD3                                                      
         OI    EOFAD3+2,X'01'                                                   
         B     DAEOF3R                                                          
*                                                                               
DAEOF3B  TM    DTFTYPE,DTFTBIG     TEST IF 20 OR 22 BIT FILE                    
         BO    DAEOF3C                                                          
         ICM   RE,7,EOFAD3         20-BIT                                       
         SRL   RE,4                                                             
         LA    RE,1(RE)                                                         
         SLL   RE,4                                                             
         STCM  RE,7,EOFAD3                                                      
         MVI   EOFAD3+3,1                                                       
         B     DAEOF3R                                                          
*                                                                               
DAEOF3C  ICM   RE,7,EOFAD3         22-BIT                                       
         SRL   RE,2                                                             
         LA    RE,1(RE)                                                         
         SLL   RE,2                                                             
         STCM  RE,7,EOFAD3                                                      
         MVI   EOFAD3+3,1                                                       
         B     DAEOF3R                                                          
*                                                                               
DAEOF3D  SR    RE,RE               16-BIT                                       
         ICM   RE,3,EOFAD3                                                      
         LA    RE,1(RE)                                                         
         STCM  RE,3,EOFAD3                                                      
         MVI   EOFAD3+2,1                                                       
         B     DAEOF3R                                                          
*                                                                               
DAEOF3R  MVC   P1,=AL4(RDID)       A(RDID)                                      
         LA    RE,IOA                                                           
         ST    RE,P2               A(IOAREA)                                    
         LA    RE,L'IOA                                                         
         MVC   P3(2),=C'L='                                                     
         STH   RE,P3+2             L'IOAREA                                     
         ST    R4,P4               A(DTF)                                       
         MVC   DSKADR,EOFAD3                                                    
         LA    RE,DSKADR                                                        
         ST    RE,P5               A(DSKADR)                                    
         XC    IOA,IOA                                                          
         GOTO1 =V(DADDS),P1                                                     
         OC    P3(2),P3            TEST FOR ERRORS                              
         BZ    ERR7                ERROR IF RECORD FOUND                        
*                                                                               
DAEOF3X  B     RETN                                                             
         EJECT                                                                  
***********************************************************************         
*ERROR RETURNS                                                        *         
***********************************************************************         
ERR1     MVI   DDNAFINF,01         FILE NOT FOUND                               
         B     ERRX                                                             
ERR2     MVI   DDNAFINF,02         FILE NOT DIRECT ACCESS                       
         B     ERRX                                                             
ERR3     MVI   DDNAFINF,03         CANT OPEN FILE                               
         B     ERRX                                                             
ERR4     MVI   DDNAFINF,04         CANT FIND DNEXT                              
         B     ERRX                                                             
ERR5     MVI   DDNAFINF,05         CANT READ DNEXT RECORD                       
         B     ERRX                                                             
ERR6     MVI   DDNAFINF,06         RECORD EXISTS AT DNEXT PLUS ONE REC          
         B     ERRX                                                             
ERR7     MVI   DDNAFINF,07         RECORD EXISTS AT DNEXT PLUS ONE TRK          
         B     ERRX                                                             
*                                                                               
ERRX     OI    8(R2),X'10'         SET DMCB ERROR RETURN                        
*                                                                               
RETN     XMOD1 1                                                                
*                                                                               
FILDATA  DC    X'00010000'         FIRST DNEXT FOR 16/18/20/22 BIT              
         DC    X'00004000'                                                      
         DC    X'00000100'                                                      
         DC    X'00000400'                                                      
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
***********************************************************************         
*THIS WORKING STORAGE AREA IS RETURNED TO CALLER AT DMCBW3            *         
*FIELDS DEFINED BY DMDDNAMED PAN BOOK                                 *         
***********************************************************************         
WRKD     DSECT                                                                  
*DMDDNAMED                                                                      
       ++INCLUDE DMDDNAMED                                                      
*                                                                               
P1       DS    F                   DADDS PARAM LIST                             
P2       DS    F                                                                
P3       DS    F                                                                
P4       DS    F                                                                
P5       DS    F                                                                
P6       DS    F                                                                
*                                                                               
FILFMT   DS    X                   0=16-BIT,1=18-BIT,2=20-BIT,3=22-BIT          
FILFLG   DS    X                   1=DAOPEN,2=FNDEOF                            
         DS    XL2                                                              
FDNEXT   DS    F                   FIRST DNEXT FOR EMPTY FILE                   
*                                                                               
EOFAD1   DS    F                   EOF DSK ADR GIVEN BY DNEXT                   
EOFAD2   DS    F                   EOD DSK ADR PLUS ONE REC ON SAME TRK         
EOFAD3   DS    F                   EOF DSK REC FIRST REC ON NEXT TRK            
DSKADR   DS    F                   CURRENT DSK ADR                              
*                                                                               
IOA      DS    CL8                 CURRENT RECORD AT DSK ADR                    
WRKX     DS    0C                                                               
                                                                                
*DMDTFPH                                                                        
       ++INCLUDE DMDTFPH                                                        
                                                                                
*DMDTFIS                                                                        
       ++INCLUDE DMDTFIS                                                        
                                                                                
*DMSYSFD                                                                        
       ++INCLUDE DMSYSFD                                                        
                                                                                
*DMGREQUS                                                                       
       ++INCLUDE DMGREQUS                                                       
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001DMEOFCHK  08/08/13'                                      
         END                                                                    
