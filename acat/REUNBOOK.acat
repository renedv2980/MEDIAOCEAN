*          DATA SET REUNBOOK   AT LEVEL 018 AS OF 03/07/89                      
*CATALP UNBOOK                                                                  
         TITLE 'UNSCAN A STRING OF RATING BOOKS'                                
         SPACE 1                                                                
*              PARA 1              BYTE 0    MAX. NUMBER FIELDS                 
*                                  BYTE 1-3  A(3-BYTE BOOK FIELDS)              
*              PARA 2              BYTE 0    C'$' - USE $, NOT -                
*                                  BYTE 1-3  A(OUTPUT FIELD HEADER)             
*              PARA 3              ZERO OR                                      
*                                  BYTE 0    C'L' - LABEL OPTION                
*                                  BYTE 1-3  A(BOOK LABEL ELEMENT)              
*              PARA 4              ZERO OR                                      
*                                  BYTE 0    C'+' - FILTER OPTION               
*                                  BYTE 1-3  A(BOOK FILTERS)                    
*                                  NOTE:  IF FILTERS ARE USED, SOURCE           
*                                         INFORMATION IS NOT RETURNED           
         SPACE 2                                                                
UNBOOK   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 UNBKX-UNBKD,**UNBK                                               
         USING UNBKD,RC                                                         
         LR    R2,R1               ADDRESS OF PARAMETER LIST                    
         SPACE 1                                                                
         MVI   TAB,0               TABLE INDEX                                  
         EJECT                                                                  
UNBOOK10 MVI   WORK,C' '                                                        
         MVC   WORK+1(L'WORK-1),WORK                                            
         LA    R6,WORK             R6 POINTS TO OUTPUT                          
         MVI   LASTSRC,0                                                        
         MVC   SPACES,WORK                                                      
         MVC   LABELS,WORK                                                      
         MVC   FILTERS,WORK                                                     
         SPACE 1                                                                
         L     R7,4(R2)            FIELD HEADER                                 
         ZIC   R8,0(R7)                                                         
         SH    R8,=H'9'                                                         
         TM    1(R7),X'02'         EXTENDED?                                    
         BZ    *+8                                                              
         SH    R8,=H'8'                                                         
         EX    R8,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R7),WORK        SPACES TO OUTPUT                             
         SPACE 1                                                                
         ZIC   R3,0(R2)            NUMBER OF FIELDS                             
         L     R4,0(R2)            ADDRESS OF INPUT FIELDS                      
         SPACE                                                                  
         LA    R5,LABELS           R5 POINTS TO LABELS                          
         LA    RE,0(R8,R6)         CALCULATE ADDRESS OF LAST BYTE               
         ST    RE,FLDEND           IN FIELD.                                    
         CLI   8(R2),C'L'          LABEL OPTION                                 
         BNE   UNBOOK15            NO                                           
         L     RE,8(R2)            YES-RE POINTS TO BOOK LABEL EL               
         ZIC   R1,1(RE)            ELEMENT LENGTH                               
         SH    R1,=H'3'            LENGTH OF LABELS LESS 1 FOR EX               
         EX    R1,*+8                                                           
         B     UNBOOK15                                                         
         MVC   LABELS(0),2(RE)                                                  
         SPACE                                                                  
UNBOOK15 LA    R9,FILTERS          R9 POINTS TO FILTERS                         
         CLI   12(R2),C'+'         FILTER OPTION                                
         BNE   UNBOOK20            NO                                           
         L     RE,12(R2)           YES-RE POINTS TO BOOK FILTERS                
         LR    RF,R3                                                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     UNBOOK20                                                         
         MVC   FILTERS(0),0(RE)                                                 
         SPACE                                                                  
UNBOOK20 CLI   12(R2),C'+'         FILTER OPTION                                
         BE    UNBOOK35            YES -- DON'T FILL IN SOURCE                  
         MVI   0(R6),C'S'          SOURCE=SRC                                   
         TM    0(R4),X'41'                                                      
         BO    UNBOOK30                                                         
         MVI   0(R6),C'N'          NSI                                          
         TM    0(R4),X'40'                                                      
         BO    *+8                                                              
         MVI   0(R6),C'A'          ARB                                          
UNBOOK30 CLC   LASTSRC,0(R6)                                                    
         BE    *+22                                                             
         MVC   LASTSRC,0(R6)       PUT THE SOURCE OUT                           
         MVI   1(R6),COMMA                                                      
         LA    R6,2(R6)                                                         
         B     *+8                                                              
         MVI   0(R6),SPACE         ERASE SOURCE IF SAME AS LAST                 
         SPACE 1                                                                
UNBOOK35 CLI   0(R9),C' '          ANY FILTERS FOR THIS BOOK?                   
         BE    *+14                NO                                           
         MVC   0(1,R6),0(R9)                                                    
         LA    R6,1(R6)            BUMP OUTPUT POINTER                          
         SPACE                                                                  
         CLM   R7,B'1000',DOLLAR   DO THEY WANT DOLLAR SIGNS?                   
         BE    UNBOOK40            YES                                          
         SPACE 1                                                                
         TM    0(R4),X'80'                                                      
         BNO   UNBOOK50                                                         
         MVI   0(R6),C'-'          SUPPRESS CPP/CPM                             
         LA    R6,1(R6)                                                         
         B     UNBOOK50                                                         
         SPACE 1                                                                
UNBOOK40 TM    0(R4),X'80'                                                      
         BO    *+12                                                             
         MVI   0(R6),C'$'          DON'T SUPPRESS CPP/CPM                       
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
UNBOOK50 TM    0(R4),X'20'                                                      
         BNO   *+12                                                             
         MVI   0(R6),C'E'          ESTIMATED                                    
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
         TM    0(R4),X'08'         TIME PERIOD                                  
         BZ    *+12                                                             
         MVI   0(R6),C'T'                                                       
         LA    R6,1(R6)                                                         
         SPACE                                                                  
         TM    0(R4),X'04'         PROJECTED BOOK                               
         BZ    *+12                                                             
         MVI   0(R6),C'P'                                                       
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
         TM    0(R4),X'02'         SPECIAL SURVEY                               
         BZ    *+12                                                             
         MVI   0(R6),C'S'                                                       
         LA    R6,1(R6)                                                         
         SPACE 1                                                                
         ZIC   RE,2(R4)            MONTH                                        
         MH    RE,=H'3'                                                         
         LA    RE,MONTH(RE)                                                     
         SPACE 1                                                                
         ZIC   RF,TAB              TABLE NUMBER                                 
         MH    RF,=H'39'                                                        
         AR    RE,RF                                                            
         MVC   0(3,R6),0(RE)                                                    
         SPACE 1                                                                
         LA    R6,1(R6)                                                         
         CLI   0(R6),C' '                                                       
         BNE   *-8                                                              
         SPACE 1                                                                
         ZIC   RE,1(R4)            YEAR                                         
         CVD   RE,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  DUB(3),DUB+6(2)                                                  
         MVC   0(2,R6),DUB+1                                                    
         LA    R6,2(R6)            BUMP OUTPUT POINTER                          
         SPACE 1                                                                
         CLC   0(5,R5),SPACES      ANY LABELS TO ATTACH                         
         BE    UNBOOK60            NO                                           
         MVI   0(R6),C'('          YES                                          
         MVC   1(5,R6),0(R5)                                                    
         MVI   6(R6),C')'                                                       
         LA    R6,7(R6)            BUMP OUTPUT POINTER                          
         SPACE                                                                  
UNBOOK60 MVI   0(R6),COMMA         END AN EXPRESSION W A COMMA                  
         LA    R6,1(R6)                                                         
         LA    R4,3(R4)                                                         
         LA    R5,5(R5)            BUMP LABEL POINTER                           
         LA    R9,1(R9)            BUMP FILTER POINTER                          
         OC    0(3,R4),0(R4)       NO MORE FIELDS                               
         BZ    *+8                                                              
         BCT   R3,UNBOOK20                                                      
         SPACE 1                                                                
         BCTR  R6,0                BACK UP TO LAST BYTE IN STRING               
         CLI   0(R6),COMMA                                                      
         BNE   *+10                                                             
         MVI   0(R6),SPACE         ERASE STRING ENDING COMMA                    
         BCTR  R6,0                                                             
         C     R6,FLDEND                                                        
         BNH   EXIT                IT FITS                                      
         ZIC   RE,TAB                                                           
         LA    RE,1(RE)            NEXT TABLE                                   
         STC   RE,TAB                                                           
         CLI   TAB,2               IT DOESN'T FIT                               
         BH    EXIT                BUT CAN'T BE COMPRESSED ANY MORE             
         B     UNBOOK10            START OVER                                   
         SPACE 1                                                                
EXIT     EQU   *                                                                
         L     R4,4(R2)            FIELD HEADER                                 
         EX    R8,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R4),WORK        OUTPUT TO FIELD                              
         XMOD1 1                                                                
         EJECT                                                                  
MONTH    DC    C'ST JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'                       
         DC    C'ST JA FE MARAP MA JUNJULAU SE OC NO DE '                       
         DC    C'ST JA F  MARAP M  JUNJ  AU S  O  N  D  '                       
         SPACE 2                                                                
DOLLAR   DC    C'$'                                                             
         SPACE 2                                                                
COMMA    EQU   C','                                                             
SPACE    EQU   C' '                                                             
         SPACE 2                                                                
UNBKD    DSECT                                                                  
WORK     DS    CL200                                                            
LABELS   DS    CL60                                                             
SPACES   DS    CL5                                                              
FILTERS  DS    CL6                                                              
FLDEND   DS    A                                                                
         SPACE 1                                                                
DUB      DS    D                                                                
TAB      DS    CL1                                                              
LASTSRC  DS    CL1                                                              
UNBKX    EQU   *                                                                
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'018REUNBOOK  03/07/89'                                      
         END                                                                    
