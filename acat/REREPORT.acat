*          DATA SET REREPORT   AT LEVEL 058 AS OF 01/07/14                      
*CATALP REREPORT                                                                
         TITLE 'REREPORT - REP MOD EXECUTES SPECS + CONTROLS PRINTING'          
*                                                                               
*******************************************************************             
*                                                                 *             
*  REREPORT --- EXECUTE SPECS AND CONTROL PRINTING                *             
*                                                                 *             
*  CATALOG DATA SET: REREPORT                                     *             
*                                                                 *             
* --------------------------------------------------------------- *             
*                                                                 *             
* >>>>>>>>>>>>>PROGRAM NOTE: IMPORTANT<<<<<<<<<<<<<<<<<<<<<<<<<<  *             
*                                                                 *             
* THIS MODULE USES AN ENTRY FOR REQTAB WHICH IS PASSED IN VXREQTAB*             
* WHICH IS COVERED BY REXADDRD AND POINTED TO BY VXADDR.  THIS    *             
* MODULE NEEDS TO BE KEPT IN SYNC WITH REREPRQRP WHICH HOLDS      *             
* THE REQTAB TABLES.                                              *             
*                                                                 *             
* --------------------------------------------------------------- *             
*                                                                 *             
*  MOD LOG                                                        *             
*  -------                                                        *             
*  -----/-- (---) --- HISTORY LOST                                *             
*                                                                 *             
*  SEP06/90 (MRR) --- >CHANGE REQTAB PROCESSING TO MATCH NEW TAB  *             
*                                                                 *             
*  SEP11/91 (MRR) --- >CHECK FOR LOCALREP CALLING TO SET REGS     *             
*                       ONE MORE LAYER BACK                       *             
*                                                                 *             
*  DEC20/91 (MRR) --- >USE ADDR OF REQTAB IN VXREQTAB             *             
*                     >USE FOOTNOTE FLAG AS ADDED TO REQTAB       *             
*                     >FIX PRINTING OF '*N' CONTRACT TYPE FILTERS *             
*                                                                 *             
*  SEP09/94 (SKU) --- ADD REP1 = REP W/O LITERAL 'REPRESENTATIVE' *             
*                                                                 *             
*  APR06/95 (BU ) --- CHANGE REGENALL-->REGENALL1                 *             
*                                                                 *             
*  FEB18/98 (BU ) --- CHANGE REGENALL1-->REGENALL2                *             
*                                                                 *             
*                     ***  END TOMBSTONE  ***                     *             
*******************************************************************             
*                                                                               
         PRINT NOGEN                                                            
REREPORT CSECT                                                                  
         NMOD1 128,**REPT**,R8                                                  
         USING REPD,RC                                                          
         L     RA,=V(REWORKC)                                                   
         USING WORKD,RA                                                         
         L     R9,FILEC                                                         
         USING FILED,R9                                                         
         MVC   USERRD,4(RD)        SAVE CALLER'S SAVE AREA ADDR                 
         CLI   LCREPFLG,C'Y'       CALLED FROM 'LOCALREP'?                      
         BNE   PC0                 NO                                           
         L     RF,4(RD)            YES - UNWIND STACK ONE MORE LEVEL            
         MVC   USERRD,4(RF)                                                     
         MVI   LCREPFLG,X'00'      CLEAR OUT FLAG                               
PC0      DS    0H                                                               
         EJECT                                                                  
*                   OPTION JUST TO CLEAR ALL PRINT LINES                        
         SPACE 2                                                                
         CLI   FORCECLR,C'N'                                                    
         BE    PC4                                                              
         MVI   FORCECLR,C'N'                                                    
         SPACE 2                                                                
PC1      LA    R2,HEAD1                                                         
         LA    R3,SPACES                                                        
         LA    R4,12                                                            
         SPACE 2                                                                
PC2      MVC   0(132,R2),0(R3)                                                  
         LA    R2,132(R2)                                                       
         BCT   R4,PC2                                                           
         MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
PC3      MVC   P,SPACES                                                         
         MVC   PSECOND,SPACES                                                   
         MVC   PTHIRD,SPACES                                                    
         MVC   PFOURTH,SPACES                                                   
         MVC   FOOT1,SPACES                                                     
         MVC   FOOT2,SPACES                                                     
         B     PCEXT                                                            
         SPACE 2                                                                
*                   CONTROL SPEC PROCESSING                                     
         SPACE 2                                                                
PC4      CLI   SKIPSPEC,C'Y'                                                    
         BE    PC6                                                              
         CLI   RCFLONLY,C'Y'                                                    
         BE    PC5                                                              
         CLI   FORCEHED,C'Y'       DONT EXECUTE SPECS IF JUST                   
         BE    PC5                 HANDLING PLINES                              
         CLI   FORCEMID,C'Y'                                                    
         BE    PC5                                                              
         CLI   FORCEFUT,C'Y'                                                    
         BE    PC5                                                              
         CLC   LINE,MAXLINES                                                    
         BL    PC14                                                             
         SPACE 2                                                                
PC5      EQU   *                                                                
         BAS   RE,PROCSPEC                                                      
         CLI   RCFLONLY,C'Y'                                                    
         BNE   PC6                                                              
         MVI   RCFLONLY,C'N'                                                    
         B     PCEXT                                                            
         EJECT                                                                  
*                   CONTROL OF HEADLINE PRINTING                                
         SPACE 2                                                                
PC6      CLI   FORCEHED,C'N'                                                    
         BE    PC8                                                              
         MVI   FORCEHED,C'N'                                                    
         B     PC10                                                             
         SPACE 2                                                                
PC8      CLC   LINE,MAXLINES                                                    
         BL    PC12                                                             
         SPACE 2                                                                
PC10     MVI   FORCEMID,C'Y'                                                    
         L     RF,ADCONLST                                                      
         ICM   RF,15,FOOTHOOK-ADCONSD(RF)                                       
         BZ    PC10A                                                            
         CLM   RF,8,=X'FF'         TEST FOR DISABLED FOOTHOOK                   
         BE    PC11                                                             
         BAS   RE,GOHOOK           EXECUTE USER'S FOOTHOOK                      
*                                                                               
PC10A    CLC   FOOT1(132),SPACES   AUTO-PRINT FOOTLINES IF NOT                  
         BE    PC11                FIRST FOR REQUEST                            
         CP    RCRQTOT,MYTOT                                                    
         BNE   PC11                                                             
         GOTO1 PRINT,P1,SPACES-1,=C'BC09'                                       
         LA    R2,FOOT1                                                         
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         B     PC11                                                             
         SPACE 2                                                                
MYTOT    DC    PL6'0'                                                           
         SPACE 2                                                                
PC11     ZAP   MYTOT,RCRQTOT                                                    
         GOTO1 PRINT,P1,SPACES-1,=C'BC01'                                       
         ICM   RF,15,HEADHOOK                                                   
         BZ    PC11A                                                            
         BAS   RE,GOHOOK           EXECUTE USER'S HEADHOOK                      
         SPACE                                                                  
PC11A    MVI   LINE,1                                                           
         LA    R2,HEAD1                                                         
         LA    R3,12                                                            
         BAS   RE,PRINCON                                                       
         SR    R4,R4                                                            
         MVC   REPDUB(4),PAGE      ADD 1 TO PAGE AND SUBPAGE                    
         LH    R4,REPDUB                                                        
         LA    R4,1(R4)                                                         
         STH   R4,REPDUB                                                        
         LH    R4,REPDUB+2                                                      
         LA    R4,1(R4)                                                         
         STH   R4,REPDUB+2                                                      
         MVC   PAGE(4),REPDUB                                                   
         EJECT                                                                  
*                   CONTROL OF MIDLINE PRINTING                                 
         SPACE 2                                                                
PC12     CLI   FORCEMID,C'N'                                                    
         BE    PC14                                                             
         MVI   FORCEMID,C'N'                                                    
         LA    R2,MID1                                                          
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         CLC   LINE,MAXLINES                                                    
         BNL   PC10                                                             
         SPACE 2                                                                
*                   CONTROL OF PRINT LINES                                      
         SPACE 2                                                                
PC14     LA    R2,P                                                             
         LA    R3,4                                                             
         BAS   RE,PRINP                                                         
         MVI   SPACING,1                                                        
         SPACE 2                                                                
*                   CONTROL OF FOOTLINE PRINTING                                
         SPACE 2                                                                
         CLI   FORCEFUT,C'N'                                                    
         BE    PC15                                                             
         MVI   FORCEFUT,C'N'                                                    
         GOTO1 PRINT,P1,SPACES-1,=C'BC09'                                       
         LA    R2,FOOT1                                                         
         LA    R3,2                                                             
         BAS   RE,PRINCON                                                       
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
PC15     CLI   CLEARHED,C'N'       TEST OPTION NOT TO CLEAR HLS                 
         BE    PC3                                                              
         B     PC1                                                              
         EJECT                                                                  
*                   CONTROL PRINTING OF A BLOCK                                 
         SPACE 2                                                                
PRINCON  NTR1                                                                   
         MVC   REPWORK,SPACES                                                   
         LR    R4,R2                                                            
         LR    R5,R3                                                            
         LA    R6,REPWORK                                                       
         SPACE 2                                                                
PC20     CLC   0(132,R4),SPACES    BUILD A TABLE OF PRINT LINE BYTES            
         BE    *+8                                                              
         MVI   0(R6),C'P'                                                       
         LA    R4,132(R4)                                                       
         LA    R6,1(R6)                                                         
         BCT   R5,PC20                                                          
         SPACE 2                                                                
*                                  NOW CONVERT TO PRINTING NUMBERS              
         LA    R6,REPWORK                                                       
         SPACE 2                                                                
PC22     CLC   0(16,R6),SPACES     ANY LEFT                                     
         BE    PC30                NO                                           
         SPACE 2                                                                
PC26     LA    R8,2                                                             
         CLC   1(15,R6),SPACES     IS THIS THE LAST                             
         BE    PC28                                                             
         CLC   1(2,R6),=C' P'                                                   
         BE    PC28                                                             
         LA    R8,1                                                             
         CLI   1(R6),C'P'                                                       
         BE    PC28                                                             
         LA    R8,3                                                             
         SPACE 2                                                                
PC28     STC   R8,0(R6)                                                         
         AR    R6,R8                                                            
         SR    R7,R7                                                            
         IC    R7,LINE                                                          
         AR    R7,R8                                                            
         STC   R7,LINE                                                          
         B     PC22                                                             
         SPACE 2                                                                
PC30     LA    R6,REPWORK          NOW PRINT R2 = DATA                          
*                                            R3 = N'LINES                       
*                                            R6 = SKIP NUMBERS                  
PC32     CLI   0(R6),C' '                                                       
         BE    PC34                                                             
         MVC   CONTROL(3),=C'BL0'                                               
         MVC   CONTROL+3(1),0(R6)                                               
         OI    CONTROL+3,X'F0'                                                  
         BCTR  R2,0                                                             
         GOTO1 PRINT,P1,(R2),CONTROL                                            
         LA    R2,1(R2)                                                         
         SPACE 2                                                                
PC34     LA    R2,132(R2)                                                       
         LA    R6,1(R6)                                                         
         BCT   R3,PC32                                                          
         XIT1                                                                   
         EJECT                                                                  
*                   SPECIAL ROUTINE FOR PRINT LINES                             
         SPACE 2                                                                
PRINP    NTR1                                                                   
         BCTR  R3,R0                                                            
         SPACE 2                                                                
PRINP2   CLC   132(132,R2),SPACES  SINGLE PRINT UNTIL LAST                      
         BE    PC36                                                             
         BCTR  R2,0                                                             
         GOTO1 PRINT,P1,(R2),=C'BL01'                                           
         LA    R2,1(R2)                                                         
         SR    R1,R1                                                            
         IC    R1,LINE                                                          
         LA    R1,1(R1)                                                         
         STC   R1,LINE                                                          
         LA    R2,132(R2)                                                       
         BCT   R3,PRINP2                                                        
         SPACE 2                                                                
PC36     MVC   CONTROL(3),=C'BL0'  USERS SPACING FOR ONE OR 2/2                 
         MVC   CONTROL+3(1),SPACING                                             
         OI    CONTROL+3,X'F0'                                                  
         BCTR  R2,0                                                             
         GOTO1 PRINT,P1,(R2),CONTROL                                            
         LA    R2,1(R2)                                                         
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         IC    R1,SPACING                                                       
         IC    R0,LINE                                                          
         AR    R1,R0                                                            
         STC   R1,LINE                                                          
         XIT1                                                                   
         SPACE 2                                                                
PCEXT    MVI   SKIPSPEC,C'N'                                                    
         XMOD1 1                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
*                   EXECUTE THE SPEC POOL                                       
         SPACE 2                                                                
PROCSPEC NTR1                                                                   
         L     R2,SPECS            POINT TO SPEC POOL                           
         SR    R3,R3                                                            
         MVI   PROGSW,C'Y'         PRESET SWITCHES                              
         MVI   FILTSW,C'Y'                                                      
         CLI   RCSUBPRG,0                                                       
         BE    EX1                                                              
         MVI   PROGSW,C'N'                                                      
         SPACE 2                                                                
EX1      CLI   RCFLONLY,C'Y'       PRESET I/O VALUES TO N                       
         BNE   EX4                                                              
         MVC   FCRDADV(32),=32C'N'                                              
         B     EX4                                                              
         SPACE 2                                                                
         SPACE 2                                                                
EX2      IC    R3,1(R2)                                                         
         AR    R2,R3               GET NEXT SPEC                                
         SPACE 2                                                                
EX4      SR    R4,R4                                                            
         IC    R4,0(R2)                                                         
         SLL   R4,2                                                             
         L     R4,INBRTAB(R4)      GO TO ROUTINE DEPENDENT                      
         BR    R4                  ON THE VALUE OF ELEMENT                      
         SPACE 2                                                                
EX6      XIT1                                                                   
         SPACE 2                                                                
EX8      MVI   PROGSW,C'N'                                                      
         MVI   FILTSW,C'Y'         RESET FOR SUB-PROGRAM FILTERING              
         B     EX2                                                              
         SPACE 2                                                                
EX10     CLC   RCSUBPRG(1),2(R2)   CHECK MATCH OF SUB-PROGRAM                   
         BNE   EX2                                                              
         MVI   PROGSW,C'Y'                                                      
         B     EX2                                                              
         EJECT                                                                  
*              REP/OFFICE FILTERS                                               
         SPACE 2                                                                
EX12     MVI   FILTSW,C'N'         FILTER RESET                                 
         B     EX2                                                              
         SPACE 2                                                                
EX14     CLC   2(2,R3),RREPKREP    REP FILTER                                   
         BNE   EX2                                                              
         B     EX20                                                             
         SPACE 2                                                                
EX16     CLC   2(2,R3),RREPKREP    REP/OFFICE FILTER                            
         CLC   4(2,R3),ROFFKOFF                                                 
         BNE   EX2                                                              
         SPACE 2                                                                
EX20     MVI   FILTSW,C'Y'                                                      
         B     EX2                                                              
         EJECT                                                                  
*                   FILE AND RUN CONTROL SPECS                                  
         SPACE 2                                                                
EX26     CLI   RCFLONLY,C'Y'                                                    
         BNE   EX2                                                              
         CLI   FILTSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         SPACE 1                                                                
         SR    R4,R4                                                            
         IC    R4,2(R2)                                                         
         BCTR  R4,R0                                                            
         LA    R4,FCRDADV(R4)                                                   
         MVI   0(R4),C'Y'                                                       
         B     EX2                                                              
         SPACE 2                                                                
EX28     CLI   RCFLONLY,C'Y'                                                    
         BNE   EX2                                                              
         CLI   FILTSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   2(R2),2                                                          
         BH    EX32                                                             
         BE    EX30                                                             
         MVI   SPACING,1                                                        
         B     EX2                                                              
         SPACE 2                                                                
EX30     MVI   SPACING,2                                                        
         B     EX2                                                              
         SPACE 2                                                                
EX32     CLI   2(R2),4                                                          
         BH    EX36                                                             
         BE    EX34                                                             
         MVI   SPACING,3                                                        
         B     EX2                                                              
         SPACE 2                                                                
EX34     MVI   RCLINEUP,C'Y'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX36     CLI   2(R2),6                                                          
         BH    EX40                                                             
         BE    EX38                                                             
         MVI   RCREQREP,C'Y'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX38     MVI   RCREQREP,C'N'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX40     CLI   2(R2),8                                                          
         BH    EX44                                                             
         BE    EX42                                                             
         MVI   RCREQSUM,C'Y'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX42     MVI   RCREQSUM,C'N'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX44     CLI   2(R2),9                                                          
         BH    EX45                                                             
         SR    R4,R4                                                            
         IC    R4,3(R2)                                                         
         CVD   R4,REPDUB                                                        
         UNPK  RCPROG,REPDUB                                                    
         OI    RCPROG+1,X'F0'                                                   
         B     EX2                                                              
         SPACE 2                                                                
EX45     CLI   2(R2),10                                                         
         BH    EX45A                                                            
         MVI   RCFILTER,C'N'                                                    
         B     EX2                                                              
         SPACE 2                                                                
EX45A    MVC   MAXLINES,2(R2)                                                   
         B     EX2                                                              
         EJECT                                                                  
*                   PRINT SPEC CONTROL AND LITERALS                             
         SPACE 2                                                                
EX46     CLI   RCFLONLY,C'Y'                                                    
         BE    EX2                                                              
         CLI   PROGSW,C'Y'                                                      
         BNE   EX2                                                              
         CLI   FILTSW,C'Y'                                                      
         BNE   EX2                                                              
         MVC   REPDUB(2),2(R2)     WORK OUT DISLACEMENT                         
         LH    R5,REPDUB                                                        
         LA    R5,HEAD1(R5)                                                     
         CLI   0(R2),13                                                         
         BE    EX48                                                             
         SR    R4,R4               WORK OUT BRANCH BY INSTRUCTION NUM           
         IC    R4,4(R2)                                                         
         SLL   R4,2                                                             
         L     R4,PRBRTAB(R4)                                                   
         BR    R4                                                               
         SPACE 2                                                                
EX48     SR    R4,R4               HANDLE LITERALS                              
         IC    R4,1(R2)                                                         
         SH    R4,=H'5'            LENGTH IS L'ELEMENT - 4                      
         EX    R4,MUVLIT                                                        
         B     EX2                                                              
         SPACE 2                                                                
MUVLIT   MVC   0(0,R5),4(R2)                                                    
         EJECT                                                                  
*                   TABLE OF INTERNAL BRANCH VALUES                             
         SPACE 2                                                                
         DS    0F                                                               
INBRTAB  DC    AL4(EX6)     0      END OF SPECS                                 
         DC    AL4(EX8)     1      PROGRAM RESET                                
         DC    AL4(EX10)    2              FILTER                               
         DC    AL4(EX12)    3      FILTER  RESET                                
         DC    AL4(EX2)     4                                                   
         DC    AL4(EX14)    5      REP     FILTER                               
         DC    AL4(EX2)     6                                                   
         DC    AL4(EX16)    7      REP/OFF FILTER                               
         DC    AL4(EX2)     8                                                   
         DC    AL4(EX2)     9                                                   
         DC    AL4(EX26)   10      FILE CONTROL SPEC                            
         DC    AL4(EX28)   11      RUN CONTROL SPEC                             
         DC    AL4(EX46)   12      PRINT KEYWORD SPEC                           
         DC    AL4(EX46)   13      PRINT LITERAL SPEC                           
         EJECT                                                                  
*                   ROUTINES FOR PRINT SPECS (0-3)                              
         SPACE 2                                                                
EX50     MVC   0(110,R5),SPACES    SPACES                                       
         CLI   SKIPSPEC,C'T'                                                    
         BE    EX2                                                              
         MVC   0(132,R5),SPACES                                                 
         B     EX2                                                              
         SPACE 2                                                                
EX52     MVC   0(7,R5),=C'RUN ON ' RUN                                          
         GOTO1 DATCON,DMCB,(4,RCDATE),(5,7(R5))                                 
         MVC   16(4,R5),=C' AT '                                                
*&&DO*&& GETIME STANDARD                                                        
*&&OS                                                                           
         TIME                                                                   
         SRL   R0,4                                                             
         LR    R1,R0                                                            
*&&                                                                             
         ST    R1,REPWORK                                                       
         ZAP   REPDUB,=P'0'                                                     
         MVC   REPDUB+5(3),REPWORK                                              
         OI    REPDUB+7,X'0F'                                                   
         CVB   R6,REPDUB                                                        
         EDIT  (R6),(5,20(R5)),2                                                
         B     EX2                                                              
         SPACE 2                                                                
EX54     MVC   0(09,R5),=C'REPORT RE'                                           
         MVC   9(2,R5),RCPROG                                                   
         B     EX2                                                              
         SPACE 2                                                                
EX56     MVC   0(5,R5),=C'PAGE '                                                
         EDIT  PAGE,(4,5(R5)),ALIGN=LEFT                                        
         B     EX2                                                              
         SPACE 2                                                                
EX57     MVC   0(8,R5),=C'SUBPAGE '                                             
         EDIT  SUBPAGE,(4,8(R5)),ALIGN=LEFT                                     
         B     EX2                                                              
         EJECT                                                                  
*              PERIOD AND REQUESTOR                                             
         SPACE 2                                                                
EX58     CLC   QEND,SPACES                                                      
         BE    EX60                                                             
         CLC   QSTART,SPACES                                                    
         BE    EX62                                                             
         MVC   0(7,R5),=C'PERIOD '                                              
         LA    R6,QSTART                                                        
         LA    R7,7(R5)                                                         
         BAS   RE,MONCONV                                                       
         CLC   QSTART+4(2),SPACES                                               
         BE    *+8                                                              
         LA    R5,2(R5)                                                         
         MVI   14(R5),C'-'                                                      
         LA    R6,QEND                                                          
         LA    R7,16(R5)                                                        
         BAS   RE,MONCONV                                                       
         B     EX2                                                              
         SPACE 2                                                                
EX60     MVC   0(16,R5),=C'FOR THE MONTH OF'                                    
         LA    R7,17(R5)                                                        
         LA    R6,QSTART                                                        
         BAS   RE,MONCONV                                                       
         B     EX2                                                              
         SPACE 2                                                                
EX62     MVC   0(15,R5),=C'PERIOD UP UNTIL'                                     
         LA    R7,17(R5)                                                        
         LA    R6,QEND                                                          
         BAS   RE,MONCONV                                                       
         B     EX2                                                              
         SPACE 2                                                                
MONCONV  PACK  REPDUB,2(2,R6)                                                   
         CVB   R1,REPDUB                                                        
         BCTR  R1,0                                                             
         MH    R1,=H'3'                                                         
         LA    R1,MONTHS(R1)                                                    
         MVC   0(3,R7),0(R1)                                                    
         CLC   4(2,R6),SPACES      POP IN DAYS                                  
         BE    *+14                                                             
         MVC   3(2,R7),4(R6)                                                    
         LA    R7,2(R7)                                                         
         MVI   3(R7),C'/'                                                       
         MVC   4(2,R7),0(R6)                                                    
         BR    RE                                                               
         SPACE 2                                                                
EX64     CLC   QUESTOR,SPACES                                                   
         BE    EX2                                                              
         MVC   0(09,R5),=C'REQUESTOR'                                           
         MVC   10(12,R5),QUESTOR                                                
         B     EX2                                                              
         EJECT                                                                  
*              NAMES AND BASIS                                                  
         SPACE 2                                                                
EX66     MVC   0(14,R5),=C'REPRESENTATIVE'                                      
         MVC   15(20,R5),RREPSHRT                                               
         B     EX2                                                              
         SPACE 2                                                                
EX67     MVC   0(20,R5),RREPSHRT                                                
         B     EX2                                                              
         SPACE 2                                                                
EX68     MVC   0(23,R5),=C'(BROADCAST MONTH BASIS)'                             
         CLI   QBASIS,C' '                                                      
         BE    EX2                                                              
         CLI   QBASIS,C'B'                                                      
         BE    EX2                                                              
         MVC   0(10,R5),=C'(FOUR-WEEK '                                         
         CLI   QBASIS,C'4'                                                      
         BE    EX2                                                              
         MVC   0(5,R5),=C'(FIVE'                                                
         CLI   QBASIS,C'5'                                                      
         BE    EX2                                                              
         MVC   0(10,R5),=C'(CALENDAR '                                          
         B     EX2                                                              
         SPACE 2                                                                
EX70     CLI   RTEMKTEM+1,C' '                                                  
         BE    EX2                                                              
         MVC   0(4,R5),=C'TEAM'                                                 
         MVC   5(1,R5),RTEMKTEM+1                                               
         MVI   6(R5),C'-'                                                       
         MVC   7(10,R5),RTEMNAME                                                
         B     EX2                                                              
         SPACE 2                                                                
EX71     MVC   0(8,R5),=C'DIVISION'                                             
         MVC   9(1,R5),RTEMKTEM                                                 
         MVI   10(R5),C'-'                                                      
         MVC   11(10,R5),RTEMDVNM                                               
         B     EX2                                                              
         SPACE 2                                                                
EX72     MVC   0(6,R5),=C'OFFICE'                                               
         MVC   7(2,R5),ROFFKOFF                                 HK 7507         
         MVI   9(R5),C'-'                                       HK 7507         
         MVC   10(20,R5),ROFFNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX74     MVC   0(11,R5),=C'SALESPERSON'                                         
         MVC   12(3,R5),RSALKSAL                                HK 7507         
         CLI   14(R5),C' '                                      HK 7507         
         BNE   *+8                                              HK 7507         
         MVI   14(R5),C'-'                                      HK 7507         
         MVI   15(R5),C'-'                                      HK 7507         
         MVC   16(20,R5),RSALNAME                                               
         B     EX2                                                              
         SPACE 2                                                                
EX75     MVC   0(7,R5),=C'STATION'                                              
         MVC   8(4,R5),RSTAKSTA                                                 
         LA    R5,11(R5)                                                        
         CLI   0(R5),C' '                                                       
         BE    *+8                                                              
         LA    R5,1(R5)                                                         
         MVC   0(3,R5),=C'-TV'                                                  
         CLI   RSTAKSTA+4,C' '                                                  
         BE    EX2                                                              
         MVC   1(1,R5),RSTAKSTA+4                                               
         MVI   2(R5),C'M'                                                       
         B     EX2                                                              
         SPACE 2                                                                
EX76     MVC   0(5,R5),=C'AS AT'                                                
         GOTO1 DATCON,DMCB,(0,QASAT),(5,6(R5))                                  
         B     EX2                                                              
         SPACE 2                                                                
EX77     CLI   RGRPKGRP+1,C' '                                                  
         BE    EX2                                                              
         MVC   0(8,R5),=C'SUBGROUP'                                             
         MVC   9(1,R5),RGRPKGRP+1                                               
         MVI   10(R5),C'-'                                                      
         MVC   11(10,R5),RGRPSBNM                                               
         B     EX2                                                              
         SPACE 2                                                                
EX78     MVC   0(5,R5),=C'GROUP'                                                
         MVC   6(1,R5),RGRPKGRP                                                 
         MVI   7(R5),C'-'                                                       
         MVC   8(10,R5),RGRPNAME                                                
         B     EX2                                                              
         EJECT                                                                  
*              REQUEST DETAILS                                                  
         SPACE 2                                                                
EX80     MVI   AREA,C' '                                                        
         MVC   AREA+1(249),AREA                                                 
         MVC   AREA(16),=C'(REQUEST DETAILS'                                    
         LA    R7,AREA+16                                                       
         L     RF,VXADDR                                                        
         USING VXADDRD,RF                                                       
         L     R6,VXREQTAB                                                      
         DROP  RF                                                               
         LTR   R6,R6                                                            
         BZ    EX2                                                              
         L     RF,=V(RQSTNUM)                                                   
         CLI   0(RF),2                                                          
         BL    EX80A                                                            
         L     RF,=V(RQSTCARD)                                                  
         B     EX80B                                                            
EX80A    EQU   *                                                                
         LA    RF,QRECORD                                                       
EX80B    EQU   *                                                                
         ST    RF,ACARDS                                                        
         SPACE 2                                                                
EX82     CLC   0(3,R6),=C'END'     END OF LIST                                  
         BNE   EX84                                                             
         MVI   0(R7),C')'          CLOSE BRACKETS                               
         CLI   AREA+16,C')'        TEST FOR ANY DETAILS                         
         BE    EX2                                                              
         GOTO1 CHOPPER,P1,(250,AREA),(108,(R5)),(C'P',2)                        
         B     EX2                                                              
         SPACE 2                                                                
EX84     CLI   6(R6),C'F'          IS THIS A FOOTNOTE ITEM?                     
         BNE   EX88                NO, SKIP IT                                  
         PACK  REPDUB,0(3,R6)      WORK OUT LENGTH AND DISPLACEMENT             
         CVB   RE,REPDUB                                                        
         BCTR  RE,0                                                             
         PACK  REPDUB,3(2,R6)                                                   
         CVB   R1,REPDUB                                                        
         BCTR  R1,0                                                             
         A     RE,ACARDS                                                        
         EX    R1,*+8                                                           
         B     *+10                                                             
         CLC   0(0,RE),SPACES      CHECK FOR SIGNIFICANT DATA                   
         BE    EX88                                                             
         MVC   1(20,R7),7(R6)      MOVE MAXIMUM DESCRIPTION                     
         MVI   21(R7),C'='                                                      
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   22(0,R7),0(RE)                                                   
         CLI   5(R6),C'L'          POSSIBLE EXCLUDE FILTER (LOWER CASE)         
         BNE   EX84L                                                            
         TM    0(RE),X'40'         IF UPPER CASE, IT'S INCLUDE FILTER           
         BO    EX84L                                                            
         EX    R1,*+8              ELSE,IT'S EXCLUDE FILTER                     
         B     *+10                                                             
         MVC   23(0,R7),0(RE)      PRINT AS *FILTER                             
         OI    23(R7),X'40'         AND MAKE IT PRINT IN UPPER CASE             
         MVI   22(R7),C'*'                                                      
*                                                                               
EX84L    LA    R1,20(R7)                                                        
         SPACE 2                                                                
EX85     CLI   0(R1),C' '          NOW SHUFFLE DATA BACK TO MEET                
         BNE   EX86                FIELD DESCRIPTION                            
         MVC   0(20,R1),1(R1)                                                   
         BCT   R1,EX85                                                          
         SPACE 2                                                                
EX86     LA    R1,20(R1)                                                        
         SPACE 2                                                                
EX87     LA    R7,1(R1)            NOW LOOK FOR THE SPACE (R7) THAT             
         CLI   0(R1),C' '          FOLLOWS THE LAST CHARACTER (R1)              
         BNE   EX88                                                             
         BCT   R1,EX87                                                          
         SPACE 2                                                                
EX88     LA    R6,27(R6)                                                        
         B     EX82                                                             
         EJECT                                                                  
GOHOOK   NTR1                                                                   
*                                                                               
*        EXECUTES USER'S HEADHOOK ROUTINE                                       
*                                                                               
         L     RE,USERRD                                                        
         LM    R0,RC,20(RE)                                                     
         BASR  RE,RF                                                            
         XIT1                                                                   
         EJECT                                                                  
*                   BRANCH TABLE FOR PRINT SPECS                                
         SPACE 2                                                                
         DS    0F                                                               
PRBRTAB  DC    AL4(EX50)    0      SPACES                                       
         DC    AL4(EX52)    1      RUN                                          
         DC    AL4(EX54)    2      REPORT                                       
         DC    AL4(EX56)    3      PAGE                                         
         DC    AL4(EX58)    4      PERIOD                                       
         DC    AL4(EX64)    5      REQUESTOR                                    
         DC    AL4(EX2)     6                                                   
         DC    AL4(EX2)     7                                                   
         DC    AL4(EX2)     8                                                   
         DC    AL4(EX2)     9                                                   
         DC    AL4(EX2)    10                                                   
         DC    AL4(EX2)    11                                                   
         DC    AL4(EX2)    12                                                   
         DC    AL4(EX54)   13      REPORT                                       
         DC    AL4(EX66)   14      REP                                          
         DC    AL4(EX68)   15      BASIS                                        
         DC    AL4(EX70)   16      TEAM                                         
         DC    AL4(EX72)   17      OFFICE                                       
         DC    AL4(EX74)   18      SALESMAN                                     
         DC    AL4(EX76)   19      AS AT DATE                                   
         DC    AL4(EX78)   20      GROUP                                        
         DC    AL4(EX80)   21      REQUEST DETAILS                              
         DC    AL4(EX77)    22     SUBGROUP                                     
         DC    AL4(EX75)   23      STATION                                      
         DC    AL4(EX71)   24      DIVISION                                     
         DC    AL4(EX2)    25                                                   
         DC    AL4(EX2)    26                                                   
         DC    AL4(EX67)   27      REP W/O LIT REP                              
         EJECT                                                                  
*                   DSECT FOR THIS MODULE                                       
         SPACE 2                                                                
REPD     DSECT                                                                  
REPDUB   DS    D                                                                
USERRD   DS    F                                                                
ACARDS   DS    F                                                                
REPWORK  DS    CL40                                                             
CONTROL  DS    CL4                                                              
PROGSW   DS    CL1                                                              
FILTSW   DS    CL1                                                              
AREA     DS    640C                                                             
         EJECT                                                                  
       ++INCLUDE REREPWORKD                                                     
         EJECT                                                                  
       ++INCLUDE REGENALL2                                                      
         EJECT                                                                  
       ++INCLUDE REXADDRD                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'058REREPORT  01/07/14'                                      
         END                                                                    
