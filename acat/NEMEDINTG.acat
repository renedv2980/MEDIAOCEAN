*          DATA SET NEMEDINTG  AT LEVEL 046 AS OF 01/28/05                      
*CATALP NETINTG                                                                 
         TITLE 'NEMEDINTG - NETWORK INTEGRATION REC MODULE'                     
NETINTG  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 0,**NINT**                                                       
         L     R9,0(R1)                                                         
         USING NIBLKD,R9                                                        
         L     RA,NIBNETB                                                       
         USING NETBLOCK,RA                                                      
*                                                                               
*        REMEMBER THAT  NETINTG RECS HAVE EFFECITVE DATE                        
*        IN COMPLEMENT FORM. NETINTG BUILDS KEY AND DOES A                      
*        READ HIGH WITH END DATE AND SINCE RECS ARE IN COMPLEMENT               
*        FORM THIS GIVES THE REQUIRED REC.                                      
*                                                                               
         XC    INTKEND,INTKEND                                                  
         XC    INTKMID,INTKMID                                                  
         TM    NIBCNTL,X'80'                                                    
         BO    IN5                                                              
         B     PASSRATE                                                         
         EJECT                                                                  
*                                                                               
IN5      DS    0H                * CONVERT 2 BYTE START/END DATE                
*                                  TO 3 BYTE COMPLEMENT DATE                    
         XC    FULL,FULL                                                        
         GOTO1 NBDATCON,DMCB,(2,NIBSTRT),(3,FULL+1)                             
         L     R1,FULL             CONVERT 2 BYTE DATE                          
         LCR   R1,R1               TO 3 BYTE DATE                               
         ST    R1,FULL             AND COMPLEMENT IT                            
         MVC   COMPSTRT,FULL+1                                                  
         XC    FULL,FULL                                                        
         GOTO1 NBDATCON,DMCB,(2,NIBEND),(3,FULL+1)                              
         L     R1,FULL             CONVERT 2 BYTE DATE                          
         LCR   R1,R1               TO 3 BYTE DATE                               
         ST    R1,FULL             AND COMPLEMENT IT                            
         MVC   COMPEND,FULL+1                                                   
*                                                                               
*                              ***  READ FOR INTG RECORDS                       
         LA    R4,KEY                                                           
         USING NIKEY,R4                                                         
         XC    KEY,KEY                                                          
         MVI   KEY,X'0A'                                                        
         MVC   NIKAM,NIBAM                                                      
         MVC   NIKNET,NIBNTWK                                                   
         MVC   NIKEFFDT,COMPEND      START READING WITH END DATE                
         LA    R1,INTKEND         SET UP TO READ FOR MID INTG RECS              
         LA    R2,10              MAX OF 10 MID RANGE RECS                      
         BAS   RE,HIGH                                                          
         B     *+8                                                              
INSEQ    BAS   RE,SEQ                                                           
         CLC   KEY(11),KEYSAVE                                                  
         BNE   NOREC                                                            
         CLC   NIKEFFDT,COMPSTRT    CHK EFF DATE OF KEY AND REQ START           
         BNL   IN7                                                              
         MVC   0(3,R1),NIKEFFDT                                                 
         MVC   3(4,R1),KEY+21                                                   
         AH    R1,=H'-7'                                                        
         BCT   R2,INSEQ                                                         
         DC    H'0'                                                             
IN7      MVC   3(4,R1),KEY+21                                                   
         MVC   0(3,R1),NIKEFFDT                                                 
*                                                                               
         LA    R2,INTKMID         MOVE DATES/DISK ADDS TO NIBAIO+1500           
         L     R1,NIBAIO2                                                       
         MVC   0(70,R1),0(R2)                                                   
         B     INX                  PASS BACK DATE AND DISK ADDRESS             
         EJECT                                                                  
*                                                                               
PASSRATE DS    0H                                                               
*                                * CONVERT 2 BYTE START DATE                    
*                                  TO 3 BYTE COMPLEMENT DATE                    
         XC    FULL,FULL                                                        
         GOTO1 NBDATCON,DMCB,(2,NIBSTRT),(3,FULL+1)                             
         L     R1,FULL             CONVERT 2 BYTE DATE                          
         LCR   R1,R1               TO 3 BYTE DATE                               
         ST    R1,FULL             AND COMPLEMENT IT                            
         MVC   COMPSTRT,FULL+1                                                  
*                                                                               
         L     R1,NIBAIO2                                                       
         A     R1,=F'63'                                                        
IN8      CLC   0(3,R1),=3X'00'                                                  
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   COMPSTRT,0(R1)                                                   
         BNH   GOTIT                                                            
         AH    R1,=H'-7'                                                        
         B     IN8                                                              
*                                                                               
GOTIT    DS    0H                                                               
         CLC   NIBDSKSV,3(R1)      HAVE WE ALREADY READ THE REC                 
         BNE   IN10                                                             
         L     RF,NIBAIO           YES/CHK IF IN IO AREA                        
         CLI   0(RF),X'0A'                                                      
         BE    IN12                                                             
IN10     MVC   KEY+21(4),3(R1)     NO/GET IT                                    
         MVC   NIBDSKSV,3(R1)                                                   
         BAS   RE,GETREC                                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
*                                ***  MATCH ON PROGRM CODE                      
IN12     DS    0H                                                               
         L     R6,NIBAIO                                                        
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         USING NIEL,R6                                                          
IN15     CLI   1(R6),X'17'         TEST LENGTH                                  
         BNH   IN18                                                             
         ZIC   R1,1(R6)            YES/PROG CODES                               
         SH    R1,=H'23'               SUBTRCT ELEM LENGTH                      
         M     R0,=F'1'                AND DIVIDE BY 6 TO GET                   
         D     R0,=F'6'                NUM OF PROG CODES                        
         LA    R4,NIELPROG                                                      
IN17     CLC   NIBPROG,0(R4)                                                    
         BE    MATCH                                                            
         LA    R4,6(R4)           BUMP TO NXT PROG NAME IN ELEM                 
         BCT   R1,IN17                                                          
IN18     BAS   RE,NEXTEL           ANY MORE ELEMS                               
         BE    IN15                                                             
*                                                                               
         EJECT                                                                  
*                                    ***  MATCH ON DY/TIME                      
         L     R6,NIBAIO                                                        
         MVI   ELCODE,X'03'                                                     
         BAS   RE,GETEL                                                         
         USING NIEL,R6                                                          
IN19     CLI   1(R6),X'17'         TEST LENGTH                                  
         BNE   IN25                                                             
         CLI   NIELDAY,0           DAY                                          
         BE    IN20                                                             
         MVC   BYTE1,NIELDAY                                                    
         NC    BYTE1,NIBDAY                                                     
         BZ    IN25                                                             
IN20     CLI   NIELDPT,0           DAYPRT                                       
         BE    IN22                                                             
         CLC   NIELDPT,NIBDPT                                                   
         BNE   IN25                                                             
IN22     OC    NIELTIME,NIELTIME                                                
         BZ    MATCH                                                            
*                                                                               
         CLC   NIELTIME(2),=H'600'    IF TIME LESS THAN 0600                    
         BNL   IN22A                  ADD 2400 / AVOIDS PROBLEM                 
         LA    R1,2400                                                          
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),NIELTIME          OF 11P-6A RANGE VALIDATIONS          
         A     R1,FULL                                                          
         STCM  R1,3,NIELTIME                                                    
IN22A    CLC   NIELTIME+2(2),=H'600'                                            
         BNL   IN22B                                                            
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),NIELTIME+2                                             
         LA    R1,2400                                                          
         A     R1,FULL                                                          
         STCM  R1,3,NIELTIME+2                                                  
IN22B    CLC   NIBTIME(2),=H'600'                                               
         BNL   IN22C                                                            
         XC    FULL,FULL                                                        
         MVC   FULL+2(2),NIBTIME                                                
         LA    R1,2400                                                          
         A     R1,FULL                                                          
         STCM  R1,3,NIBTIME                                                     
*                                                                               
IN22C    CLC   NIBTIME(2),NIELTIME+2     IS START HIGH THAN INTGREC END         
         BNL   IN25                                                             
         CLC   NIBTIME(2),NIELTIME       IS START BEFORE INTG REC START         
         BL    IN25                                                             
         B     MATCH                                                            
*                                                                               
IN25     BAS   RE,NEXTEL           IS THERE ANOTHER ELEMENT                     
         BE    IN19                YES/TEST FOR A MATCH                         
         DC    H'0'                                                             
*                                                                               
MATCH    DS    0H                                                               
         MVC   NIBRATE,NIELRATE    SET IN RATE                                  
         B     INX                                                              
*                                                                               
NOREC    MVC   NIBDDTE(6),=6X'FF'  NO INTG REC FOUND                            
         LTR   RE,RE                                                            
         B     INXX                                                             
*                                                                               
INX      DS    0H                                                               
         SR    R1,R1                                                            
         LTR   R1,R1                                                            
INXX     XIT1                                                                   
         EJECT                                                                  
*              DATAMGR INTERFACE                                                
         SPACE 3                                                                
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRDHI'          HANDLE COMMANDS                    
         B     DIRALL                                                           
         SPACE 1                                                                
SEQ      NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         MVC   COMMAND,=CL8'DMRSEQ'                                             
         B     DIRALL                                                           
         SPACE 1                                                                
READ     NTR1                                                                   
         MVC   COMMAND,=CL8'DMREAD'                                             
         SPACE 1                                                                
DIRALL   MVC   FILE(8),=C'UNTDIR  '         DIRECTORIES                         
         LA    R2,WORK                                                          
         GOTO1 NBDM,DMCB,COMMAND,FILE,KEY,(R2),0                                
         BAS   RE,DMCHECK                                                       
         MVC   KEY,0(R2)                                                        
         B     YES                                                              
         SPACE 1                                                                
GETREC   NTR1                                                                   
         LA    R3,KEY+21                                                        
         MVC   FILE(8),=C'UNTFILE '     FILE                                    
         MVC   NBDTADSP,=H'27'                                                  
         SPACE 1                                                                
GETREC2  L     R2,NIBAIO                                                        
         GOTO1 NBDM,DMCB,(X'00',=C'GETREC'),FILE,(R3),(R2),DMWORK               
         BAS   RE,DMCHECK                                                       
         B     XIT                                                              
         SPACE 1                                                                
DMCHECK  CLI   8(R1),0                                                          
         BER   RE                                                               
         TM    8(R1),X'90'                                                      
         BM    NO                                                               
         DC    H'0'                                                             
         SPACE 1                                                                
         GETEL (R6),NBDTADSP,ELCODE                                             
         SPACE 1                                                                
NO       SR    R1,R1                                                            
         B     *+8                                                              
         SPACE 1                                                                
YES      LA    R1,1                                                             
         LTR   R1,R1                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              DSECTS ETC                                                       
         SPACE 3                                                                
*****************    PART OF MY PROGRAM *************                           
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARAS    DS    0F                                                               
DMCB     DS    6F                                                               
USERRD   DS    F                                                                
FULL     DS    F                                                                
RELO     DS    A                                                                
KEY      DS    CL27                                                             
KEYSAVE  DS    CL27                                                             
DMWORK   DS    CL96                                                             
ELCODE   DS    CL1                                                              
COMMAND  DS    CL8                                                              
FILE     DS    CL8                                                              
COMPSTRT DS    CL3                                                              
COMPEND  DS    CL3                                                              
BYTE1    DS    CL1                                                              
INTKMID  DS    CL63                                                             
INTKEND  DS    CL7                                                              
*                                                                               
         SPACE 3                                                                
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
** NETBLOCKD                                                                    
         SPACE 2                                                                
**                                                                              
         PRINT OFF                                                              
       ++INCLUDE NETBLOCKD                                                      
         PRINT ON                                                               
       ++INCLUDE NEINTGBLK                                                      
         EJECT                                                                  
       ++INCLUDE NEGENINTG                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'046NEMEDINTG 01/28/05'                                      
         END                                                                    
