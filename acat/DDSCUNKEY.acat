*          DATA SET DDSCUNKEY  AT LEVEL 005 AS OF 03/31/88                      
*CATALP SCUNKEY                                                                 
         TITLE 'UNSCAN A KEY FIELD INTO MULTIPLE TWA FIELDS'                    
         PRINT NOGEN                                                            
SCUNKEY  CSECT                                                                  
         NMOD1 65,**SKEY**                                                      
         USING WRKD,RC             RC=A(W/S)                                    
         LR    R2,R1                                                            
         LM    R3,R4,0(R2)                                                      
         LA    R3,0(R3)            R3=A(TWA KEY FIELD)                          
         LA    R4,0(R4)            R4=A(FIRST TWA FIELD)                        
         MVC   SCANCHR,0(R2)       SET SCAN CHARACTER                           
         CLI   0(R2),0                                                          
         BNE   *+8                                                              
         MVI   SCANCHR,C'/'                                                     
         MVI   SCANCHR1,X'FF'                                                   
         CLI   SCANCHR,X'FF'       TEST IF TWO SCAN CHRS PASSED                 
         BNE   *+10                                                             
         MVC   SCANCHR(2),8(R2)    GET TWO CHRS FROM PARAM3                     
         MVC   FIELD0,SCANCHR                                                   
         LA    R1,X'FF'            SET MAXIMUM KEY FIELDS                       
         CLI   4(R2),0                                                          
         BE    *+8                                                              
         IC    R1,4(R2)                                                         
         ST    R1,MAXFLD                                                        
*                                                                               
         MVI   0(R2),0             SET RETURN                                   
         MVI   4(R2),0                                                          
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
         MVC   FIELD,SPACES                                                     
         SR    R7,R7               R7 USED FOR COUNTING OUTPUT FIELDS           
*                                                                               
         IC    R1,5(R3)            EXTRACT KEY FIELD                            
         LTR   R1,R1               SOMETHING MUST BE THERE                      
         BZ    ERRX                                                             
         BCTR  R1,0                                                             
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   FIELD(0),8(R3)                                                   
         CLC   FIELD,SPACES                                                     
         BE    ERRX                                                             
         LA    R1,FIELD+1(R1)      FIND REAL LENGTH (EXCLUDING SPACES)          
         CLI   0(R1),C' '                                                       
         BNE   *+8                                                              
         BCT   R1,*-8                                                           
         MVC   1(1,R1),SCANCHR     SET SCAN CHR IN FIELD END+1                  
*                                                                               
         LA    R8,FIELD            R8=A(LAST FIELD)                             
         LR    R3,R8                                                            
         LA    R6,1(R1)                                                         
         SR    R6,R8                                                            
         LA    R6,1(R6)            R6=L'INPUT+1                                 
         MVI   FIELDIND,ALLVALID                                                
         EJECT                                                                  
*                                                                               
SCUN2    CLC   0(1,R3),SCANCHR                                                  
         BE    SCUN6                                                            
         CLI   SCANCHR1,X'FF'      CONVERT SCAN CHARACTERS                      
         BE    *+20                                                             
         CLC   0(1,R3),SCANCHR1                                                 
         BNE   *+10                                                             
         MVC   0(1,R3),SCANCHR                                                  
*                                  SET VALIDITY BITS                            
         TM    FIELDIND,ALPHA+NUMERIC+HEX                                       
         BZ    SCUN3                                                            
         CLI   0(R3),C'A'                                                       
         BNL   *+12                                                             
         NI    FIELDIND,ALLVALID-ALPHA-NUMERIC-HEX                              
         B     SCUN3                                                            
         CLI   0(R3),C'Z'                                                       
         BNH   *+12                                                             
         NI    FIELDIND,ALLVALID-ALPHA                                          
         B     SCUN3                                                            
         NI    FIELDIND,ALLVALID-NUMERIC                                        
         CLI   0(R3),C'F'                                                       
         BNH   *+8                                                              
         NI    FIELDIND,ALLVALID-HEX                                            
SCUN3    LA    R3,1(R3)                                                         
*                                                                               
SCUN4    BCT   R6,SCUN2                                                         
         STC   R7,4(R2)            RETURN # OF FIELDS OUTPUT                    
         B     EXIT                                                             
*                                                                               
SCUN6    L     R4,4(R2)            BUMP TO NEXT TWA FIELD                       
         SR    R1,R1                                                            
SCUN7    CLI   0(R4),0                                                          
         BE    ERRX                                                             
         TM    1(R4),X'20'         SKIP PROTECTED FIELDS                        
         BZ    *+14                                                             
         IC    R1,0(R4)                                                         
         AR    R4,R1                                                            
         B     SCUN7                                                            
         ST    R4,4(R2)                                                         
         LA    R7,1(R7)            BUMP OUTPUT FIELD COUNT AND SEE IF           
         C     R7,MAXFLD           IT EXCEEDS THE MAXIMUM ALLOWED               
         BH    ERRX                                                             
         IC    R1,0(R4)                                                         
         SH    R1,=H'9'                                                         
         TM    1(R4),X'02'         TEST EXTENDED FIELD HEADER                   
         BZ    *+8                                                              
         SH    R1,=H'8'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    8(0,R4),8(R4)       CLEAR FIELD                                  
         XC    4(2,R4),4(R4)       CLEAR LENGTH & INDICATORS                    
         OI    4(R4),X'80'         INPUT THIS TIME                              
         OI    6(R4),X'80'         TRANSMIT                                     
         NI    6(R4),X'FF'-X'40'   TURN OFF INSERT CURSOR                       
         LR    R9,R3                                                            
         BCTR  R9,0                                                             
         CLC   0(1,R9),SCANCHR     IF PREVIOUS CHR IN STRING WAS A              
         BE    SCUN10              DELIMITER GET NEXT                           
SCUN8    CLI   0(R9),C' '          TEST FOR TRAILING SPACES                     
         BH    SCUN9                                                            
         BCTR  R9,0                                                             
         CR    R9,R8                                                            
         BL    SCUN10                                                           
         B     SCUN8                                                            
SCUN9    SR    R9,R8                                                            
         BM    SCUN10                                                           
         CR    R1,R9               IF INPUT WON'T FIT INTO TWA FIELD            
         BNL   *+6                 SET LENGTH TO LENGTH OF OUTPUT               
         LR    R9,R1                                                            
         EX    R9,*+8                                                           
         B     *+10                                                             
         MVC   8(0,R4),0(R8)       MOVE IN DATA                                 
         LA    R9,1(R9)                                                         
         STC   R9,5(R4)            SET LENGTH                                   
         OC    4(1,R4),FIELDIND                                                 
*                                                                               
SCUN10   L     R4,4(R2)            BUMP TO NEXT TWA FIELD                       
         SR    R1,R1                                                            
         IC    R1,0(R4)                                                         
         AR    R4,R1                                                            
         ST    R4,4(R2)                                                         
*                                                                               
         LA    R3,1(R3)            BUMP TO NEXT CHARACTER                       
         LR    R8,R3                                                            
         MVI   FIELDIND,ALLVALID                                                
         B     SCUN4                                                            
         EJECT                                                                  
*                                                                               
ERRX     STC   R7,0(R2)            RETURN # OF FIELDS                           
EXIT     XMOD1 1                                                                
*                                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
WRKD     DSECT                                                                  
MAXFLD   DS    F                                                                
SCANCHR  DS    C                                                                
SCANCHR1 DS    C                                                                
FIELDIND DS    X                                                                
FIELD0   DS    C                                                                
FIELD    DS    CL256                                                            
SPACES   DS    CL256                                                            
*                                                                               
ALLVALID EQU   X'0E'                                                            
NUMERIC  EQU   X'08'                                                            
ALPHA    EQU   X'04'                                                            
HEX      EQU   X'02'                                                            
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005DDSCUNKEY 03/31/88'                                      
         END                                                                    
