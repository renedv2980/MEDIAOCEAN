*          DATA SET PZMOD      AT LEVEL 127 AS OF 05/01/02                      
*CATALP PZMOD                                                                   
         TITLE 'PZ - CENTRAL INVOICE PROCESSING MODULE'                         
*                                                                               
***********************************************************************         
*                                                                     *         
*        THE MODULE IS PASSED THE INDEX OF A WORKER FILE              *         
*        ENTRY CONTAINING AN EASI SYSTEM 'BATCH' - I.E. A             *         
*        SET OF INVOICES FOR THE SAME AGENCY/OFFICE AND               *         
*        STATION.                                                     *         
*                                                                     *         
*        IT SCANS THE BATCH, LOOKS UP                                 *         
*        ADVERTISER, PRODUCT AND COMMERCIAL INFORMATION               *         
*        AND CODES, HANDLES THE DATA FIELDS THAT REQUIRE              *         
*        EDITING AND/OR CONVERSION AND SETS THE DATA IN               *         
*        A FIXED FORMAT IN EZBLOCKD.                                  *         
*                                                                     *         
*        ALL THE DATA IS PRESERVED IN ITS ORIGINAL UNEDITED           *         
*        FORMAT, BUT MANY FIELDS ARE ALSO CONVERTED TO                *         
*        MORE USEFUL, STANDARD FORMATS.                               *         
*                                                                     *         
*        CONTROL IS PASSED TO THE CALLER'S PROCESSING ROUTINE         *         
*        AT EZHOOK. THE MODE SETTINGS INDICATE WHAT LEVEL OF          *         
*        DATA IS TO BE PROCESSED AND WHAT PARTS OF THE                *         
*        DSECT HAVE BEEN FILLED OUT. FOR EXAMPLE, EZSPTP              *         
*        WILL BE PASSED FOR EACH INDIVIDUAL SPOT DETAIL               *         
*        AND DATA AT THE INVOICE, SCHEDULE LINE AND SPOT              *         
*        DETAIL LEVELS WILL BE THERE                                  *         
*                                                                     *         
*        ERRORS ARE RETURNED TO THE CALLER'S ERROR ROUTINE            *         
*        AT EZERRHK (WHICH MAY BE THE SAME AS EZHOOK) WITH            *         
*        THE MODE EZERRP. THE ERROR CODE, LEVEL, RECORD AND           *         
*        FIELD NAME AND ERROR MESSAGE ARE SET.                        *         
*                                                                     *         
***********************************************************************         
*                                                                     *         
*    !!  LINK WITH LKPPZ502 AND LKPPEZF10                                       
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
***********************************************************************         
*        NOTE- WORK NEEDS TO BE DONE ON THE REGIONAL COMBO            *         
*              OPTION.                                                *         
*        NOTE- WORK NEEDS TO BE DONE ON EDITIONS LIKE DAILY, SUNDAY,  *         
*              ETC. MAYBE BASE PARTLY ON INSERTION DATE. OR USE       *         
*              EDITION FIELD.                                                   
*        NOTE- THERE IS COME CODE ABOUT A "PASS 2" THRU THE DETAILS             
*              ACTIVATED BY BY EZL2PASS. IT DOESN'T WORK SO DON'T               
*              USE IT! MAYBE WE'LL WANT IT LATER.                               
*                                                                               
***********************************************************************         
         EJECT                                                                  
         PRINT NOGEN                                                            
PZMOD    CSECT                                                                  
         NMOD1 EZMODDL,**PZMOD,RR=R2,CLEAR=YES                                  
         LA    RA,COMSUBS                                                       
         USING COMSUBS,RA                                                       
         USING EZMODD,RC                                                        
         ST    R2,RELO                                                          
         L     R9,0(R1)                                                         
         LR    R8,R9                                                            
         AH    R8,=H'4096'                                                      
         USING EZBLOCKD,R9,R8      2ND BASE FOR WORK AREA                       
         ST    RC,SAVWORK          SAVE WORK AREA ADDRESS                       
EZBREG   EQU   R9                  NAME FOR EZBLOCKD REGISTER                   
*                                  SET ADDRESSES                                
         L     RF,EZCOMFCS         A(COMFACS)                                   
         USING COMFACSD,RF                                                      
         MVC   DATAMGR,CDATAMGR                                                 
         MVC   CASHVAL,CCASHVAL                                                 
         MVC   GETPROF,CGETPROF                                                 
         MVC   DATVAL,CDATVAL                                                   
         MVC   DATCON,CDATCON                                                   
         MVC   HEXOUT,CHEXOUT                                                   
         DROP  RF                                                               
*                                                                               
EZM08    DS    0H                                                               
         OC    GETPROF,GETPROF     GETPROF IN COMFACS                           
         BNZ   *+16                                                             
         L     RF,=V(GETPROF)                                                   
         A     RF,RELO                                                          
         ST    RF,GETPROF                                                       
*                                                                               
         OC    DATCON,DATCON       DATCON IN COMFACS                            
         BNZ   *+16                                                             
         L     RF,=V(DATCON)                                                    
         A     RF,RELO                                                          
         ST    RF,DATCON                                                        
*                                                                               
         OC    HEXOUT,HEXOUT       HEXOUT IN COMFACS                            
         BNZ   *+16                                                             
         L     RF,=V(HEXOUT)                                                    
         A     RF,RELO                                                          
         ST    RF,HEXOUT                                                        
*                                                                               
         OC    CASHVAL,CASHVAL     TEST CASHVAL IN COMFACS                      
         BNZ   *+16                                                             
         L     RF,=V(CASHVAL)                                                   
         A     RF,RELO                                                          
         ST    RF,CASHVAL                                                       
*                                                                               
         OC    DATVAL,DATVAL       TEST DATVAL IN COMFACS                       
         BNZ   *+16                                                             
         L     RF,=V(DATVAL)                                                    
         A     RF,RELO                                                          
         ST    RF,DATVAL                                                        
*                                                                               
         L     RF,=V(NUMVAL)                                                    
         A     RF,RELO                                                          
         ST    RF,NUMVAL                                                        
         L     RF,=V(BINSRCH)                                                   
         LTR   RF,RF                                                            
         BZ    *+8                                                              
         A     RF,RELO                                                          
         ST    RF,BINSRCH                                                       
*                                                                               
         OC    EZARECTB,EZARECTB   TEST USER SET REC TABLE                      
         BNZ   *+16                                                             
         L     RF,=V(EZRECTAB)                                                  
         A     RF,RELO                                                          
         ST    RF,EZARECTB                                                      
*                                                                               
         L     RF,=A(FLDCVT)                                                    
         A     RF,RELO                                                          
         ST    RF,AFLDCVT                                                       
         L     RF,=A(ERRTAB)                                                    
         A     RF,RELO                                                          
         ST    RF,AERRTAB                                                       
*                                                                               
         MVI   PM1,C' '                                                         
         MVI   SPACES,C' '                                                      
         MVC   SPACES+1(L'SPACES-1),SPACES                                      
*                                                                               
         XC    SVPLTENT,SVPLTENT                                                
*                                                                               
         MVC   CALLRD,4(RD)        SAVE BACK NMOD LINK                          
         SPACE                                                                  
         B     EZM10             * SKIP MPLDIR/FIL OPEN FOR NOW *               
         SPACE                                                                  
         TM    EZLOOKSW,X'C0'      UNLESS DOING ADV OR PRD LOOKS                
         BO    EZM10               SKIP MPLDIR/FIL OPEN                         
*                                                                               
         L     RF,EZUTL                                                         
         LTR   RF,RF               IF NO UTL, NO OPEN                           
         BZ    EZM10                                                            
         MVC   SAVSYS,4(RF)        SAVE CURRENT SYSTEM                          
*                                                                               
*                                  OPEN MPLDIR/MPLFIL                           
         CLI   MPLOPEN,C'Y'                                                     
         BE    EZM10                                                            
         CLI   EZMPLOPN,C'Y'       OPENED PREVIOUSLY                            
         BE    EZM10                                                            
*                                                                               
         L     RF,EZUTL                                                         
         MVC   4(1,RF),EZCONSE                                                  
         L     R4,EZAREC           USE AS WORK AREA FOR OPEN                    
         GOTO1 DATAMGR,DMCB,=C'OPEN',=CL7'MPL',FILELIST,(R4)                    
         MVI   MPLOPEN,C'Y'                                                     
         L     RF,EZUTL            RESTORE SE NUM                               
         MVC   4(1,RF),SAVSYS                                                   
         B     EZM10                                                            
*                                                                               
FILELIST DC    CL8'UMPLDIR'                                                     
         DC    CL8'UMPLFIL'                                                     
         DC    CL10'X'                                                          
*                                                                               
EZM10    DS    0H                                                               
         XC    WORK,WORK                                                        
         MVI   FDELIM,X'5E'        FIELD DELIM - SEMI-COLON                     
         MVI   RDELIM,X'15'        RECORD DELIM - COLON                         
         MVI   DMIBTS,0           SET DATAMGR CONTROLS                          
         MVI   DMOBTS,X'FF'                                                     
*                                                                               
         CLC   EZWKRIND,SVWKRIND   TEST SAME INDEX                              
         BNE   EZM11               NO- CLEAR CONTROLS                           
         OC    RFCURPOS,RFCURPOS   YES-TEST A CONTINUATION                      
         BNZ   EZM20               YES- SKIP SOME INITIALIZATION                
*                                                                               
EZM11    DS    0H                  SET BATCH DATA                               
         MVC   SVWKRIND,EZWKRIND                                                
         LA    RF,EZWKRIND                                                      
         USING EZWKRIXD,RF                                                      
         MVC   EZBTSTN(4),EZWISTN  STATION                                      
         DROP  RF                                                               
*                                                                               
         L     RF,EZWKRBUF         64 BYTE REC 0 IN BUFFER                      
         USING W_RECD,RF                                                        
         MVC   EZBTBRTM,W_AGERT TIME                                            
         GOTO1 DATCON,DMCB,(2,W_AGELD),(3,EZBTBRDT)   RECEIVED DATE             
         DROP  RF                                                               
*                                                                               
         MVI   RFFLDNO,0                                                        
         MVI   RFCNTL,0                                                         
         MVI   RFCNTL2,0                                                        
         MVC   RECSWS,SPACES                                                    
*                                                                               
         L     RF,EZWKRREC                                                      
         LA    RE,4                                                             
         ST    RE,RFCURPOS         SET STARTING POS IN FIRST REC                
         LH    RE,0(RF)                                                         
         ST    RE,ENDWFREC         SET EOR                                      
         BAS   RE,TRCWKR           TRACE WORKER FILE RECORDS                    
*                                                                               
         LA    R6,EZWKRIND         SET STATION FOR WKR FILE KEY                 
         USING EZWKRIXD,R6                                                      
         MVC   WFSTATN(4),EZWISTN  STATION                                      
         MVC   WFSTATN+4(1),EZWIMED  MEDIA                                      
*                                                                               
         CLC   EZWIMED,EZMED       ON CHANGE OF MEDIA                           
         BE    EZM12                                                            
*                                                                               
         MVC   EZMED,EZWIMED        SAVE MEDIA                                  
         DROP  R6                                                               
         XC    SADVDATA,SADVDATA   CLEAR SAVES                                  
         XC    SPRDDATA,SPRDDATA                                                
*                                  GET EZ PROFILE FOR AGENCY/MED                
         XC    EZPROF,EZPROF       (FOR CLIENT IN LKADV)                        
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'P0EZ'                                                 
         MVC   WORK+4(2),EZAGY                                                  
         MVC   WORK+6(1),EZMED                                                  
         GOTO1 GETPROF,DMCB,WORK,EZPROF,DATAMGR                                 
*                                                                               
EZM12    DS    0H                                                               
         SR    R0,R0               SET BINSRCH PARS FOR PRODUCT TABLE           
         L     R1,EZPRTAB                                                       
         SR    R2,R2                                                            
         LA    R3,PRTABDL                                                       
         LA    R4,PRKEYL                                                        
         SR    RE,RE               CALCULATE MAX ENTRIES                        
         LH    RF,EZPRTABL         = TABLE LENGTH                               
         D     RE,=A(PRTABDL)      / ENTRY LENGTH                               
         LR    R5,RF                                                            
         STM   R0,R5,BSPPARS                                                    
*                                                                               
         MVC   EZRECSEQ,=H'1'      RESET REC COUNT                              
         XC    EZINVSEQ,EZINVSEQ   AND INVOICE COUNT                            
         MVI   EZRNFRPT,C'N'       CLEAR REPEAT SW                              
         MVI   PASSNO,1            START AT PASS 1                              
         XC    SVIRNO,SVIRNO                                                    
*                                                                               
EZM20    DS    0H                                                               
         BAS   RE,GETFLD                                                        
         BAS   RE,TRCRET           TRACE RETURNS                                
*                                                                               
         TM    RFCNTL,X'80'        TEST EOFILE                                  
         BNZ   EZM70                                                            
*                                                                               
         TM    RFCNTL,X'40'        TEST END OF A RECORD                         
         BZ    EZM24                                                            
*                                                                               
         CLI   BADRCD,C'Y'         YES- TEST VALID RECORD                       
         BE    EZM20                                                            
         CLI   SKIPSW,C'Y'         TEST SKIPPING OVER INVOICE                   
         BE    EZM20                                                            
*                                                                               
         GOTO1 AFLDCVT             NO-DO FIELD CONVERTS AND LOOKUPS             
         CLI   BADRCD,C'Y'         FLDCVT MAY REJECT RECORD                     
         BE    EZM20                                                            
         BAS   RE,SETPROC          SET PROC MODES                               
         B     EZM20                                                            
*                                                                               
*                                  HANDLE FIELD                                 
*                                  ------------                                 
*                                                                               
EZM24    DS    0H                                                               
         CLI   RFFLDNO,1           FLD 1 IS RECORD TYPE                         
         BNE   EZM30                                                            
*                                                                               
         TM    EZLOOKSW,EZL2PASS   IF DOING 2 PASSES                            
         BZ    EZM25                                                            
         L     RF,EZWKRREC                                                      
         CLC   4(2,RF),=C'31'      IS IT INVOICE HEADER?                        
         BNE   EZM25                                                            
         OC    SVIRNO,SVIRNO       TIME TO DO REPEAT NOW?                       
         BZ    EZM24F                                                           
*                                                                               
         MVI   PASSNO,2            YES, PASS 2                                  
         MVI   RFCNTL,X'10'        SET TO DO RANDOM (RE-READ OF INV)            
         BAS   RE,GETFLD                                                        
         BAS   RE,TRCRET           TRACE RETURNS                                
         B     EZM25                                                            
*                                                                               
EZM24F   DS    0H                                                               
         MVC   SVIRNO,EZRECSEQ                                                  
         MVI   PASSNO,1            NO, PASS 1 NOW                               
*                                                                               
EZM25    DS    0H                                                               
         BAS   RE,SETRECQ          SET INTERNAL RECORD CODE                     
         BAS   RE,SETLAST          SET LAST MODES                               
         BAS   RE,CLRFLDS          CLEAR APPROPRIATE FIELDS                     
         B     EZM20                                                            
*                                                                               
EZM30    DS    0H                                                               
         CLI   SKIPSW,C'Y'         IF BYPASSING INVOICE                         
         BE    EZM20               SKIP FIELD                                   
         CLI   BADRCD,C'Y'         TEST HAVE VALID RECORD                       
         BE    EZM20                                                            
         BAS   RE,PROCFLD          PROCESS A REAL FIELD                         
         B     EZM20                                                            
*                                                                               
EZM70    DS    0H                  EOFILE                                       
         MVI   RECTYPE,X'FF'                                                    
         BAS   RE,SETLAST          SET LAST MODES                               
*                                                                               
         TM    EZTRACE,X'08'       PDUMP?                                       
         BZ    EZM72                                                            
*                                                                               
         XC    X(8),X                                                           
         STM   R0,RF,X+8                                                        
         GOTO1 =V(PDUMPER),DMCB,X,EZMODD,EZMODD+EZMODDL                         
         GOTO1 (RF),(R1),X,EZBLOCKD,EZBLOCKD+EZBLOCKL                           
*                                                                               
EZM72    DS    0H                                                               
EXIT     XIT1                                                                   
         EJECT                                                                  
*        GETFLD- RETURN A SINGLE FIELD                                          
*                                                                               
GETFLD   NTR1                                                                   
         XC    RFDATA,RFDATA                                                    
         MVI   RFLEN,0                                                          
*                                                                               
         TM    RFCNTL,X'10'        TEST TO DO "RANDOM" READ                     
         BNZ   GETF62                                                           
*                                                                               
         TM    RFCNTL2,X'01'       TEST NEED TO SEND EOR                        
         BZ    GETF10                                                           
         NI    RFCNTL2,X'FE'                                                    
         OI    RFCNTL,X'40'        SET EOR                                      
         B     GETFX               RETURN                                       
*                                                                               
GETF10   DS    0H                                                               
         TM    RFCNTL2,X'02'       TEST NEED TO SEND END OF FILE                
         BZ    GETF14                                                           
         NI    RFCNTL2,X'FD'                                                    
         OI    RFCNTL,X'80'        SET END OF FILE                              
         B     GETFX               RETURN                                       
*                                                                               
GETF14   DS    0H                                                               
         TM    RFCNTL,X'40'        AFTER EOR                                    
         BZ    *+8                                                              
         MVI   RFFLDNO,0           RESET FIELD NUMBER                           
*                                                                               
         TM    RFCNTL2,X'04'       TEST NEED NEW WKF REC                        
         BNZ   GETF60              YES- GET IT                                  
*                                                                               
GETF16   DS    0H                                                               
         L     R3,RFCURPOS                                                      
         A     R3,EZWKRREC                                                      
         SR    R2,R2               BYTE COUNTER                                 
*                                                                               
GETF20   DS    0H                                                               
         L     RF,EZWKRREC GET END OF REC                                       
         A     RF,ENDWFREC         RELOCATE                                     
         CR    R3,RF               TEST AT END OF PHYS REC                      
         BL    GETF26                                                           
*                                  YES                                          
         LTR   R2,R2               ANY FIELD TO HANDLE                          
         BZ    GETF24                                                           
         OI    RFCNTL2,X'05'       YES- SET DO EOR AND NEED NEW REC             
         B     GETF44              AND RETURN THIS FIELD                        
*                                                                               
GETF24   DS    0H                  NO FIELD TO HANDLE                           
         CLI   RFFLDNO,0           HAVE THERE BEEN ANY FLDS                     
         BE    GETF60              NO- JUST GET NEW RECORD                      
*                                                                               
         TM    RFCNTL,X'40'        TEST HAVE DONE EOR                           
         BNZ   GETF60              YES - JUST GET NEW RECORD                    
*                                                                               
         OI    RFCNTL,X'40'        SEND EOR NOW                                 
         OI    RFCNTL2,X'04'       AND SET TO GET NEW REC                       
         B     GETFX                                                            
*                                                                               
GETF26   DS    0H                                                               
         CLC   RDELIM,0(R3)           TEST HAVE RECORD DELIM                    
         BNE   GETF30                                                           
*                                                                               
         LA    R3,L'RDELIM(R3)      BUMP PAST RECORD DELIMITER                  
         ST    R3,RFCURPOS                                                      
         LR    RF,R3                                                            
         S     RF,EZWKRREC                                                      
         ST    RF,RFCURPOS         SET RELOCATBLE VALUE                         
*                                                                               
         LTR   R2,R2               TEST HAVE FIELD TO HANDLE                    
         BZ    GETF28                                                           
*                                                                               
         OI    RFCNTL2,X'01'       SET TO DO EOR LATER                          
         B     GETF44              RETURN FIELD                                 
*                                                                               
GETF28   DS    0H                  NO FIELD TO HANDLE                           
         CLI   RFFLDNO,0           HAVE THERE BEEN ANY FLDS                     
         BE    GETF20              NO- IGNORE                                   
*                                                                               
         OI    RFCNTL,X'40'        SEND EOR                                     
         B     GETFX                                                            
*                                                                               
GETF30   DS    0H                                                               
         CLC   FDELIM,0(R3)           TEST HAVE FIELD DELIM                     
         BE    GETF40                                                           
*                                  NO DELIMITER, CONTINUE                       
*                                                                               
* SPECIAL CODE FOR OVERRIDE CODES FIELD IN '31' INVOICE HEADER *                
* AND '51' INSERT DETAIL *                                                      
*                                                                               
         CLI   RFFLDNO,1           JUST DO FIELD 1 (REALLY 2,                   
         BNE   GETF34                              BUMPED LATER )               
         CLI   RECTYPE,EZIHEQ      THIS AN INVOICE HEADER                       
         BNE   GETF32                                                           
*                                                                               
         LA    R2,20               SET LENGTH                                   
         LR    R4,R3               SET START OF FIELD                           
*                                                                               
         LA    R3,20+L'FDELIM(,R3)                                              
         LR    RF,R3                                                            
         S     RF,EZWKRREC                                                      
         ST    RF,RFCURPOS         SET RELOCATBLE VALUE                         
         B     GETF54                                                           
*                                                                               
GETF32   DS    0H                                                               
         CLI   RECTYPE,EZSPEQ      INSERT DETAIL REC                            
         BNE   GETF34                                                           
*                                                                               
         LA    R2,10               SET LENGTH                                   
         LR    R4,R3               SET START OF FIELD                           
*                                                                               
         LA    R3,10+L'FDELIM(,R3)                                              
         LR    RF,R3                                                            
         S     RF,EZWKRREC                                                      
         ST    RF,RFCURPOS         SET RELOCATBLE VALUE                         
         B     GETF54                                                           
*                                                                               
GETF34   LTR   R2,R2               IF HAVE NO DATA YET                          
         BP    GETF36                                                           
         CLI   0(R3),C' '          IGNORE LEADING BLANKS                        
         BNH   GETF38                                                           
         LR    R4,R3               SAVE A(FIRST NON-BLANK)                      
*                                                                               
GETF36   DS    0H                                                               
         LA    R2,1(R2)            BUMP FIELD LENGTH                            
GETF38   DS    0H                                                               
         LA    R3,1(R3)            NEXT POS                                     
         B     GETF20                                                           
*                                                                               
GETF40   DS    0H                                                               
         LA    R3,L'FDELIM(R3)      BUMP PAST FIELD DELIM                       
         LR    RF,R3                                                            
         S     RF,EZWKRREC                                                      
         ST    RF,RFCURPOS         SET RELOCATBLE VALUE                         
*                                                                               
GETF44   DS    0H                                                               
         LTR   R2,R2                                                            
         BNP   GETF54                                                           
*                                  STRIP TRAILING BLANKS                        
         LA    R5,0(R2,R4)                                                      
         BCTR  R5,R0               R5 AT LAST BYTE                              
*                                                                               
GETF46   DS    0H                                                               
         CLI   0(R5),C' '                                                       
         BH    GETF50                                                           
         BCTR  R2,R0               DECR LENGTH                                  
         BCT   R5,GETF46           PREVIOUS BYTE                                
*                                                                               
GETF50   DS    0H                                                               
         CH    R2,=H'255'                                                       
         BNH   GETF54                                                           
         OI    RFCNTL,X'20'        SET FIELD ERROR                              
         LA    R2,255                                                           
*                                                                               
GETF54   DS    0H                                                               
         NI    RFCNTL,X'3F'        CLEAR END CONTROLS                           
         ZIC   RF,RFFLDNO          BUMP FIELD NUMBER                            
         LA    RF,1(RF)                                                         
         STC   RF,RFFLDNO                                                       
         STC   R2,RFLEN            SET FIELD LENGTH                             
*                                                                               
         LTR   R2,R2                                                            
         BNP   GETF56                                                           
         BCTR  R2,R0                                                            
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   RFDATA(0),0(R4)                                                  
*                                                                               
GETF56   DS    0H                                                               
         B     GETFX                                                            
*                                                                               
GETF60   DS    0H                  GET NEXT WKF RECORD                          
         NI    RFCNTL2,X'FB'       SET OFF NEED REC SWITCH                      
         GOTO1 DATAMGR,DMCB,=C'READ',EZWKRFIL,EZWKRIND,EZWKRREC,       X        
               EZWKRBUF                                                         
*                                                                               
         LH    RF,EZRECSEQ         BUMP RECORD COUNT                            
         LA    RF,1(RF)                                                         
         STH   RF,EZRECSEQ                                                      
*                                                                               
         B     GETF63                                                           
*                                                                               
GETF62   DS    0H                  GET PREVIOUS INVOICE HEADER                  
         NI    RFCNTL,X'EB'        SET OFF RANDOM SW AND NEED REC SW            
         L     RF,EZWKRREC                                                      
         XC    0(12,RF),0(RF)                                                   
         MVC   2(2,RF),SVIRNO      RECORD NUMBER FOR 'RANDOM'                   
         MVC   4(4,RF),=C'REC '                                                 
*                                                                               
         GOTO1 DATAMGR,DMCB,(0,=C'RANDOM'),EZWKRFIL,EZWKRIND,EZWKRREC, X        
               EZWKRBUF                                                         
         MVC   EZRECSEQ,SVIRNO     RESET RECORD SEQUENCE #                      
         XC    SVIRNO,SVIRNO                                                    
         MVI   RFFLDNO,0                                                        
         BAS   RE,PATCH1                                                        
*                                                                               
GETF63   DS    0H                                                               
         TM    DMCB+8,X'80'        TEST EOF                                     
         BNZ   GETF64                                                           
         CLI   DMCB+8,0            OTHER ERROR                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         L     RF,EZWKRREC         SET STARTING POS                             
         CLI   0(RF),X'FF'         TEST AT LAST RECORD                          
         BE    GETF64                                                           
         LA    RE,4                                                             
         ST    RE,RFCURPOS                                                      
         LH    RE,0(RF)            GET EOR                                      
         ST    RE,ENDWFREC                                                      
         BAS   RE,TRCWKR           TRACE WORKER FILE RECORD                     
         B     GETF16                                                           
*                                                                               
GETF64   DS    0H                  END OF FILE                                  
         TM    RFCNTL,X'40'        TEST HAVE SENT EOR                           
         BNZ   GETF66                                                           
         OI    RFCNTL,X'40'        SET EOR NOW                                  
         OI    RFCNTL2,X'02'       AND DO EOFILE AFTER                          
         B     GETFX                                                            
*                                                                               
GETF66   DS    0H                                                               
         OI    RFCNTL,X'80'        SET EOFILE                                   
*                                                                               
GETFX    DS    0H                                                               
         XIT1                                                                   
*                                                                               
PATCH1   DS    0H                                                               
         BR    RE                                                               
         DC    64X'00'                                                          
         EJECT                                                                  
*        CLRFLDS - CLEAR APPROPRIATE FIELDS AT NEW RECORD                       
         SPACE 2                                                                
CLRFLDS  NTR1                                                                   
         CLI   RECTYPE,EZSNEQ      STATION                                      
         BE    CLF20                                                            
         CLI   RECTYPE,EZPYEQ      PAYEE                                        
         BE    CLF24                                                            
         CLI   RECTYPE,EZAGEQ      AGENCY                                       
         BE    CLF30                                                            
         CLI   RECTYPE,EZSTEQ      STANDARD COMMENT TOP                         
         BE    CLF50                                                            
         CLI   RECTYPE,EZSBEQ      STANDARD COMMENT BOTTOM                      
         BE    CLF60                                                            
         CLI   RECTYPE,EZIHEQ      INVOICE                                      
         BNE   CLF10                                                            
         TM    EZLOOKSW,EZL2PASS   IF 2 PASS                                    
         BZ    *+12                                                             
         CLI   PASSNO,2            BUMP ONLY FOR PASS 2                         
         BNE   CLF9                                                             
         LH    RF,EZINVSEQ         BUMP INVOICE COUNT                           
         LA    RF,1(RF)                                                         
         STH   RF,EZINVSEQ                                                      
CLF9     DS    0H                                                               
         B     CLF34                                                            
*                                                                               
CLF10    DS    0H                                                               
         CLI   SKIPSW,C'Y'         IF BYPASSING INVOICE                         
         BE    CLFX                SKIP LOWER LEVELS                            
         CLI   RECTYPE,EZSPEQ      SPOT DETAIL                                  
         BE    CLF44                                                            
         CLI   RECTYPE,EZRREQ      RECONCILIATION                               
         BE    CLF46                                                            
         B     CLFX                                                             
*                                                                               
CLF20    XC    EZSNFRST(EZSNDLEN),EZSNFRST             STATION                  
         B     CLF32                                                            
CLF24    XC    EZPYFRST(EZPYDLEN),EZPYFRST             PAYEE                    
         B     CLF34                                                            
         SPACE                                                                  
CLF30    XC    EZAGFRST(EZAGDLEN),EZAGFRST             AGENCY                   
         SPACE                                                                  
* CLEAR TOP AND BOTTOM COMMENTS FOR STATION AND AGENCY *                        
         SPACE                                                                  
CLF32    LA    RE,EZSTFRST         CLEAR TOP STANDARD CMMNTS                    
         LA    RF,EZSTDLEN                                                      
         XCEF                                                                   
         LA    RE,EZSBFRST         CLEAR BOTTOM STANDARD CMMNTS                 
         LA    RF,EZSBDLEN                                                      
         XCEF                                                                   
         SPACE                                                                  
CLF34    LA    RE,EZIHFRST                             INVOICE                  
         LA    RF,EZIHDLEN                                                      
         XCEF                                                                   
         SPACE                                                                  
         MVI   SCTOPSW,C'Y'        SET HAVE HAD INVOICE SINCE                   
         MVI   SCBOTSW,C'Y'        LAST STANDARD COMMENTS                       
         XC    EZICFRST(EZICDLEN),EZICFRST             INV CMNT TOP             
         XC    EZIBFRST(EZIBDLEN),EZIBFRST             INV CMNT BOTTOM          
         XC    EZITFRST(EZITDLEN),EZITFRST             INV TOTALS               
         MVI   SKIPSW,C'N'                             RESET SKIP SW            
         SPACE                                                                  
CLF44    LA    RE,EZSPFRST                             SPOT DETAIL              
         LA    RF,EZSPDLEN                                                      
         XCEF                                                                   
         SPACE                                                                  
CLF46    XC    EZRRFRST(EZRRDLEN),EZRRFRST             RECON                    
         B     CLFX                                                             
*                                                                               
CLF50    DS    0H                  TOP STANDARD CMMNTS                          
         CLI   SCTOPSW,C'Y'        TEST HAVE HAD INVOICE SINCE LAST             
         BNE   CLFX                                                             
         MVI   SCTOPSW,C'N'                                                     
         LA    RE,EZSTFRST         CLEAR TOP STANDARD CMMNTS                    
         LA    RF,EZSTDLEN                                                      
         XCEF                                                                   
         XC    SCTOPPTR,SCTOPPTR   RESET LINE POINTER                           
         B     CLFX                                                             
*                                                                               
CLF60    DS    0H                  BOTTOM STANDARD CMMNTS                       
         CLI   SCBOTSW,C'Y'        TEST HAVE HAD INVOICE SINCE LAST             
         BNE   CLFX                                                             
         MVI   SCBOTSW,C'N'                                                     
         LA    RE,EZSBFRST         CLEAR BOTTOM STANDARD CMMNTS                 
         LA    RF,EZSBDLEN                                                      
         XCEF                                                                   
         XC    SCBOTPTR,SCBOTPTR   RESET LINE POINTER                           
         B     CLFX                                                             
*                                                                               
CLFX     DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        SETRECQ - SET INTERNAL 1-BYTE RECORD CODE                              
         SPACE 2                                                                
SETRECQ  NTR1                                                                   
         MVI   RECTYPE,C' '                                                     
         CLI   RFLEN,2             LENGTH MUST BE 2                             
         BNE   SRQ90                                                            
*                                                                               
         L     R4,EZARECTB         TABLE OF RECORDS                             
         USING EZRDD,R4                                                         
*                                                                               
SRQ2     DS    0H                                                               
         CLI   EZRDRCD,X'FF'       EOL                                          
         BE    SRQ90                                                            
         CLC   EZRDRCD,RFDATA                                                   
         BE    SRQ4                                                             
         LA    R4,EZRDDL(R4)       NEXT ENTRY                                   
         B     SRQ2                                                             
*                                                                               
SRQ4     DS    0H                                                               
         MVI   FLDHDR,0                                                         
         MVC   FLDHDR+1(3),EZRDFRST    SET FIRST FIELD ADDRESS                  
         L     RF,FLDHDR               MUST RELOCATE                            
         A     RF,RELO                                                          
         ST    RF,FLDHDR                                                        
         MVC   FLDFRST,FLDHDR          SAVE FIRST FIELD                         
         MVC   RECTYPE,EZRDRCQ         SET INTERNAL REC CODE                    
         MVI   BADRCD,C'N'                                                      
         B     SRQX                                                             
*                                                                               
SRQ90    DS    0H                                                               
         MVI   BADRCD,C'Y'                                                      
         MVI   EZERRCD,EZERRRTP                                                 
         BAS   RE,SETERR                                                        
*                                                                               
SRQX     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
*        SETPROC - SET PROCESS MODES FOR CALLER                                 
*                  (SET AT EOR)                                                 
         SPACE 2                                                                
SETPROC  NTR1                                                                   
         B     SETP2                                                            
         DC    XL64'00'            PATCH                                        
SETP2    DS    0H                                                               
         MVI   EZMODE,EZSPTP       PROC SPOT DETAIL                             
         CLI   RECTYPE,EZSPEQ                                                   
         BE    SETP4                                                            
         MVI   EZMODE,EZINVP       PROC INV                                     
         CLI   RECTYPE,EZIHEQ                                                   
         BE    SETP4                                                            
         MVI   EZMODE,EZICCOMP     PROC INVOICE COMMENT (TOP)                   
         CLI   RECTYPE,EZICEQ                                                   
         BE    SETP4                                                            
         MVI   EZMODE,EZAAP        PROC ADDITIONAL ADJUSTMENT                   
         CLI   RECTYPE,EZAAEQ                                                   
         BE    SETP4                                                            
         MVI   EZMODE,EZIBCOMP     PROC INVOICE COMMENT (BOTTOM)                
         CLI   RECTYPE,EZIBEQ                                                   
         BE    SETP4                                                            
         MVI   EZMODE,EZRECP       PROC RECONCILIATION                          
         CLI   RECTYPE,EZRREQ                                                   
         BE    SETP4                                                            
         B     SETPX                                                            
*                                                                               
SETP4    DS    0H                                                               
         BAS   RE,HOOK                                                          
*                                                                               
SETPX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        SETLAST - SET LAST MODES FOR CALLER                                    
*                  (SET AT NEW REC)                                             
         SPACE 2                                                                
SETLAST  NTR1                                                                   
         B     SETL2                                                            
         DC    XL64'00'            PATCH                                        
SETL2    DS    0H                                                               
         CLI   RECTYPE,EZSNEQ      STATION RECORD                               
         BE    SETL4                                                            
         CLI   RECTYPE,EZPYEQ      PAYEE RECORD                                 
         BE    SETL4                                                            
         CLI   RECTYPE,EZAGEQ      AGENCY RECORD                                
         BE    SETL4                                                            
         CLI   RECTYPE,X'FF'       END OF FILE                                  
         BNE   SETL20                                                           
*                                                                               
SETL4    DS    0H                                                               
         CLI   SPRSW,C'Y'          TEST HAVE HAD SPOT                           
         BNE   SETL6                                                            
         MVI   SPRSW,C'N'          SET OFF                                      
         MVI   EZMODE,EZSPTL       LAST FOR SPOT                                
         BAS   RE,HOOK                                                          
*                                                                               
SETL6    DS    0H                                                               
         CLI   IHRSW,C'Y'          TEST HAVE HAD INVOICE                        
         BNE   SETL10                                                           
         MVI   IHRSW,C'N'          SET OFF                                      
         MVI   EZMODE,EZINVL       LAST FOR INVOICE                             
         BAS   RE,HOOK                                                          
*                                                                               
SETL10   DS    0H                                                               
         CLI   RECTYPE,X'FF'       IF AT END OF FILE                            
         BNE   SETLX                                                            
         MVI   EZMODE,EZBATL       LAST FOR BATCH                               
         BAS   RE,HOOK                                                          
         B     SETLX                                                            
*                                                                               
SETL20   DS    0H                                                               
         CLI   RECTYPE,EZIHEQ      HAVE NEW INVOICE                             
         BNE   SETL40                                                           
*                                                                               
         CLI   SPRSW,C'Y'          TEST HAVE HAD SPOT                           
         BNE   SETL26                                                           
         MVI   SPRSW,C'N'          SET OFF                                      
         MVI   EZMODE,EZSPTL       LAST FOR SPOT                                
         BAS   RE,HOOK                                                          
*                                                                               
SETL26   DS    0H                                                               
         CLI   IHRSW,C'Y'          TEST HAVE HAD INVOICE                        
         BNE   SETL30                                                           
         MVI   EZMODE,EZINVL       LAST FOR INVOICE                             
         BAS   RE,HOOK                                                          
*                                                                               
SETL30   DS    0H                                                               
         MVI   IHRSW,C'Y'          SET HAVE INVOICE                             
         B     SETLX                                                            
*                                                                               
SETL40   DS    0H                                                               
         CLI   RECTYPE,EZIBEQ      OR INVOICE COMMENT (BOTTOM)                  
         BNE   SETL60                                                           
*                                                                               
SETL40B  DS    0H                                                               
         CLI   SPRSW,C'Y'          TEST HAVE HAD SPOT                           
         BNE   SETL46                                                           
         MVI   SPRSW,C'N'          SET OFF                                      
         MVI   EZMODE,EZSPTL       LAST FOR SPOT                                
         BAS   RE,HOOK                                                          
*                                                                               
SETL46   DS    0H                                                               
         B     SETLX                                                            
*                                                                               
SETL60   DS    0H                                                               
         CLI   RECTYPE,EZSPEQ      HAVE SPOT                                    
         BNE   SETLX                                                            
*                                                                               
         CLI   SPRSW,C'Y'          TEST HAVE HAD SPOT                           
         BNE   SETL66                                                           
         MVI   EZMODE,EZSPTL       LAST FOR SPOT                                
         BAS   RE,HOOK                                                          
*                                                                               
SETL66   DS    0H                                                               
         MVI   SPRSW,C'Y'          SET HAVE SPOT                                
*                                                                               
SETLX    XIT1                                                                   
         EJECT                                                                  
*        PROCFLD - PROCESS A FIELD                                              
         SPACE 2                                                                
PROCFLD  NTR1                                                                   
         L     R4,FLDHDR                                                        
         USING EZFDD,R4                                                         
*                                                                               
         CLI   EZFDD,X'FF'         TEST EXCESS FIELD                            
         BE    PRFX                YES - IGNORE                                 
*                                                                               
         SR    RF,RF                                                            
         ICM   RF,3,EZFDDSP        DISP INTO EZBLOCK                            
         LA    RF,EZBLOCKD(RF)     POINT TO FIELD SPACE                         
*                                                                               
         CLI   RECTYPE,EZSTEQ      TEST HAVE TOP STANDARD CMMNT                 
         BNE   PRF4                                                             
         LH    RE,SCTOPPTR         ADD TO LINE POINTER                          
         CH    RE,=Y(EZSTDLEN)     TEST TOO MANY                                
         BL    PRF3                                                             
*                                                                               
PRF2     DS    0H                                                               
         MVI   EZERRCD,EZERRSCO    STANDARD COMMENT OVERFLOW                    
         BAS   RE,SETERR                                                        
         B     PRF6                                                             
*                                                                               
PRF3     DS    0H                                                               
         AR    RF,RE                                                            
         LA    RE,L'EZSTCOM(RE)    BUMP LINE POINTER                            
         STH   RE,SCTOPPTR                                                      
         B     PRF5                                                             
*                                                                               
PRF4     DS    0H                                                               
         CLI   RECTYPE,EZSBEQ      TEST HAVE BOTTOM STANDARD CMMNT              
         BNE   PRF5                                                             
         LH    RE,SCBOTPTR         ADD TO LINE POINTER                          
         CH    RE,=Y(EZSBDLEN)     TEST TOO MANY                                
         BNL   PRF2                                                             
         AR    RF,RE                                                            
         LA    RE,L'EZSBCOM(RE)    BUMP LINE POINTER                            
         STH   RE,SCBOTPTR                                                      
*                                                                               
PRF5     DS    0H                                                               
         ZIC   R1,EZFDLEN          CLEAR FIELD                                  
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         XC    0(0,RF),0(RF)                                                    
*                                                                               
         CLI   RFLEN,0             TEST ANY DATA                                
         BE    PRF6                NO - SKIP MOVE                               
*                                                                               
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),SPACES                                                   
         IC    R1,RFLEN            USE DATA LENGTH                              
         CLC   RFLEN,EZFDLEN       UNLESS TOO LONG                              
         BNH   *+8                                                              
         IC    R1,EZFDLEN          THEN USE FIELD LENGTH                        
*                                                                               
         BCTR  R1,R0                                                            
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,RF),RFDATA                                                   
*                                                                               
         CLC   RFLEN,EZFDLEN       TEST FIELD TOO LONG                          
         BNH   PRF6                                                             
         SR    R2,R2                                                            
         ICM   R2,3,EZFDDSP       SET R2 TO FIELD FOR ERROR ROUT                
         AR    R2,EZBREG                                                        
         MVI   EZERRCD,EZERRFLN                                                 
         BAS   RE,SETERR                                                        
*                                                                               
PRF6     DS    0H                                                               
         LA    R4,EZFDDL(R4)       BUMP TO NEXT FIELD HEADER                    
         ST    R4,FLDHDR                                                        
*                                                                               
PRFX     DS    0H                                                               
         XIT1                                                                   
         DROP  RB,R4                                                            
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
*        COMSUBS - CODE AND DATA ADDRESSABLE FROM ALL CSECTS                    
         SPACE 2                                                                
COMSUBS  DS    0H                                                               
*                                  COMMON DATA                                  
GETREC   DC    CL8'GETREC'                                                      
PUTREC   DC    CL8'PUTREC'                                                      
ADDREC   DC    CL8'ADDREC'                                                      
DMREAD   DC    CL8'DMREAD'                                                      
DMADD    DC    CL8'DMADD'                                                       
DMWRT    DC    CL8'DMWRT'                                                       
DMRDHI   DC    CL8'DMRDHI'                                                      
DMRSEQ   DC    CL8'DMRSEQ'                                                      
*                                                                               
MPLDIR   DC    CL8'MPLDIR'                                                      
MPLFIL   DC    CL8'MPLFIL'                                                      
PRTDIR   DC    CL8'PRTDIR'                                                      
PRTFIL   DC    CL8'PRTFIL'                                                      
PUBDIR   DC    CL8'PUBDIR'                                                      
PUBFIL   DC    CL8'PUBFIL'                                                      
         SPACE 2                                                                
*        TSTANY - TEST ANYTHING IN FIELD AT R2                                  
*                  CC NON-ZERO = MEANS ERROR                                    
*                                                                               
TSTANY   DS    0H                                                               
         CLI   0(R2),C' '                                                       
         BNH   TSTANY4                                                          
*                                                                               
         CR    RE,RE               HAVE DATA- RETURN CC =                       
         BR    RE                                                               
*                                                                               
TSTANY4  DS    0H                                                               
         MVI   EZERRCD,EZERRMIS    MISSING FIELD ERROR                          
         SPACE 2                                                                
SETERR   NTR1                                                                   
SETERRB  DS    0H                  NON-NTR'D ENTRY POINT                        
         BAS   RE,GETERRF          GET FIELD NAME                               
         BAS   RE,GETERRM          AND MESSGE                                   
         MVI   EZMODE,EZERRP       SET ERROR MODE                               
         BAS   RE,HOOK             TELL USER'S ERROR PROC                       
         LTR   RE,RE               RETURN CC NOT =                              
         XIT1                                                                   
         EJECT                                                                  
*        GETERRF - GET ERROR FIELD NAME AND RECORD NAME                         
         SPACE 2                                                                
GETERRF  NTR1                                                                   
         MVC   EZERRREC,SPACES                                                  
         MVC   EZERRFLD,SPACES                                                  
         MVC   EZERRDTA,SPACES                                                  
         CLI   EZERRCD,EZERRRTP    FOR BAD RECORD TYPE                          
         BE    GERFX               SKIP FIELD NAME                              
         STCM  R2,15,EZERRFAD      SET A(FIELD IN ERROR)                        
         SR    R2,EZBREG           GET FIELD DISP                               
         STH   R2,ERRFLDD                                                       
         AR    R2,EZBREG           RESTORE DATA ADDRESS                         
*                                  GET RECORD DESCRIPTOR                        
         L     R3,EZARECTB                                                      
         USING EZRDD,R3                                                         
*                                                                               
GERF2    DS    0H                                                               
         CLI   0(R3),X'FF'         EOL                                          
         BNE   *+6                                                              
         DC    H'0'                                                             
         CLC   EZRDRCQ,RECTYPE                                                  
         BE    GERF4                                                            
         LA    R3,EZRDDL(R3)                                                    
         B     GERF2                                                            
*                                                                               
GERF4    DS    0H                                                               
         MVC   EZERRREC,EZRDNAM   SET RECORD NAME                               
         SPACE                                                                  
**NOOP   CLI   EZERRCD,EZERRFLN   **NOOP                                        
*        BNE   GERFX                                                            
         SPACE                                                                  
*                                  NOW SEARCH FOR FIELD                         
         ICM   R3,7,EZRDFRST       A(FIRST FIELD)                               
         A     R3,RELO             MUST RELOCATE                                
         USING EZFDD,R3                                                         
*                                                                               
GERF6    DS    0H                                                               
         CLI   0(R3),X'FF'         EOL                                          
         BE    GERFX                                                            
         LA    R4,EZFDDL(R3)                                                    
         CLC   ERRFLDD,EZFDDSP-EZFDD(R4)  IS IT BEFORE NEXT FIELD?              
         BL    GERF8                      NO, WE ARE ON RIGHT ONE               
*                                                                               
GERF7    DS    0H                                                               
         LA    R3,EZFDDL(R3)       NEXT FIELD                                   
         B     GERF6                                                            
*                                                                               
GERF8    DS    0H                                                               
         MVC   EZERRFLD,EZFDNAM    SET FIELD NAME                               
*                                                                               
         MVC   EZERRDTA,SPACES                                                  
         ZIC   RF,EZFDLEN          DATA LENGTH                                  
         LA    R0,L'EZERRDTA                                                    
         CR    RF,R0                                                            
         BNH   *+6                                                              
         LR    RF,R0                                                            
         BCTR  RF,R0                                                            
         EX    RF,GERFMVC                                                       
*                                                                               
GERFX    XIT1                                                                   
*                                                                               
GERFMVC  MVC   EZERRDTA(0),0(R2)   SET ERROR DATA                               
         DROP  R3                                                               
         EJECT                                                                  
*        GETERRM - GET ERROR MESSAGE AND LEVEL                                  
         SPACE 2                                                                
GETERRM  NTR1                                                                   
         L     R3,AERRTAB                                                       
         MVC   EZERRMSG,SPACES                                                  
         MVI   EZERRLEV,0                                                       
*                                                                               
GERM2    DS    0H                                                               
         CLI   0(R3),X'FF'         EOL                                          
         BE    GERMX                                                            
*                                                                               
         CLC   EZERRCD,0(R3)                                                    
         BE    GERM4                                                            
         LA    R3,ERRTABL(R3)                                                   
         B     GERM2                                                            
*                                                                               
GERM4    DS    0H                                                               
         MVC   EZERRLEV,1(R3)                                                   
         MVC   EZERRMSG,2(R3)                                                   
         OC    EZERRLVR,1(R3)      'OR' UP ERROR - RECORD                       
         OC    EZERRLVI,1(R3)                    - INVOICE                      
         OC    EZERRLVB,1(R3)                    - BATCH                        
*                                                                               
GERMX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        HOOK - CALL USER ROUTINE                                               
         SPACE 2                                                                
HOOK     DS    0H                                                               
         CLI   SKIPSW,C'Y'         BYPASS IF SKIPPING OVER INVOICE              
         BER   RE                                                               
*                                                                               
         NTR1                                                                   
         MVC   SVEZMODE,EZMODE     SAVE MODE                                    
*                                                                               
         MVC   SVIHLPRC,EZIHLPRC   SAVE HDR PRD CODE                            
         CLC   EZIHLPRC,=C'**2'    INTERNAL PZMOD CONVENTION                    
         BNE   *+10                (MEANS SOME MISSING PRD FOR INV)             
         MVC   EZIHLPRC,SPACES     RETURN BLANKS INSTEAD                        
*                                                                               
         BAS   RE,TRCHOOK                                                       
         L     RF,EZHOOK           A(HOOK)                                      
         L     RE,CALLRD           BACK NMOD LINK                               
         LR    R0,RE               SET R0 SO USER CAN EXIT                      
         LM    R1,RC,24(RE)        CALLER'S R1-RC                               
         BASR  RE,RF                                                            
*                                                                               
         L     RD,4(RD)                                                         
         LM    RE,RC,12(RD)                                                     
*                                                                               
         MVC   EZIHLPRC,SVIHLPRC   RESTORE HDR PRD CODE                         
         CLC   SVEZMODE,EZMODE     TEST CALLER CHANGED MODE                     
         BER   RE                  NO- RETURN                                   
         CLI   EZMODE,EZINVL       DID HE ASK TO SKIP INVOICE                   
         BNER  RE                  NO- RETURN                                   
         MVI   SKIPSW,C'Y'         YES- SET SKIP SWITCH                         
         BR    RE                                                               
         EJECT                                                                  
*        TRCRET- ROUTINE TO TRACE GETFLD RETURNS                                
*                                                                               
TRCRET   DS    0H                                                               
         TM    EZTRACE,X'80'       TEST TRACING FIELDS                          
         BZR   RE                                                               
*                                                                               
TRCRETN  NTR1                                                                   
         MVC   P,SPACES                                                         
         GOTO1 HEXOUT,DMCB,RFCNTL,P,1,=C'N'                                     
         GOTO1 HEXOUT,DMCB,RFCNTL2,P+3,1,=C'N'                                  
         EDIT  (B1,RFFLDNO),(3,P+6),FILL=0                                      
         EDIT  (B1,RFLEN),(3,P+10),FILL=0                                       
         MVC   P+14(100),RFDATA                                                 
         GOTO1 EZPRINT,DMCB,PM1,=C'BL01'                                        
         MVC   P,SPACES                                                         
*                                                                               
         XIT1                                                                   
         SPACE 2                                                                
*        TRCWKR- ROUTINE TO TRACE WORKER FILE READS                             
*                                                                               
TRCWKR   TM    EZTRACE,X'04'       TEST TRACING RECORDS                         
         BZR   RE                                                               
*                                                                               
TRCWKRN  NTR1                                                                   
         MVC   P,SPACES                                                         
         GOTO1 EZPRINT,DMCB,PM1,=C'BL01'                                        
         EDIT  (B2,EZRECSEQ),(4,P),FILL=0                                       
         L     R6,EZWKRREC                                                      
         LH    R2,0(R6)            LENGTH                                       
         EDIT  (R2),(4,P+5),FILL=0                                              
         LA    R3,0(R6,R2)         EOR                                          
         LA    R6,2(R6)                                                         
*                                                                               
TW6      DS    0H                                                               
         LR    R4,R3                                                            
         SR    R4,R6                                                            
         BNP   TW8                                                              
         CH    R4,=H'100'                                                       
         BNH   *+8                                                              
         LA    R4,100                                                           
*                                                                               
         BCTR  R4,R0                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   P+10(0),0(R6)                                                    
         LA    R4,1(R4)                                                         
         GOTO1 EZPRINT,DMCB,PM1,=C'BL01'                                        
         MVC   P,SPACES                                                         
         LA    R6,0(R6,R4)                                                      
         B     TW6                                                              
*                                                                               
TW8      DS    0H                                                               
         GOTO1 EZPRINT,DMCB,PM1,=C'BL01'                                        
         MVC   P,SPACES                                                         
         XIT1                                                                   
         EJECT                                                                  
*        TRCHOOK- ROUTINE TO TRACE CALLS TO USER                                
         SPACE                                                                  
TRCHOOK  DS    0H                                                               
         CLI   EZMODE,EZERRP       TEST ERROR MODE                              
         BNE   TRCH1D                                                           
         TM    EZTRACE,X'20'       TEST TRACING ERRORS                          
         BZR   RE                                                               
         B     TRCHOOKN                                                         
*                                                                               
TRCH1D   DS    0H                                                               
         TM    EZTRACE,X'40'       TEST TRACING REGULAR CALLS                   
         BZR   RE                                                               
*                                                                               
TRCHOOKN NTR1                                                                   
         MVC   P,SPACES                                                         
         MVC   P(17),=C'**HOOK- MODE XX ='                                      
         GOTO1 HEXOUT,DMCB,EZMODE,P+13,1,=C'N'                                  
         LA    RF,MODETAB                                                       
*                                                                               
TRCH2    DS    0H                                                               
         CLI   0(RF),X'FF'         EOL, **UNKNOWN**                             
         BE    TRCH4                                                            
         CLC   EZMODE,0(RF)                                                     
         BE    TRCH4                                                            
         LA    RF,11(RF)                                                        
         B     TRCH2                                                            
*                                                                               
TRCH4    DS    0H                                                               
         MVC   P+19(10),1(RF)                                                   
         CLI   EZMODE,EZERRP                                                    
         BE    TRCH5                                                            
*                                                                               
         TM    EZLOOKSW,EZL2PASS   IF DOING 2 PASSES                            
         BZ    TRCH4C                                                           
         MVC   P+32(6),=C'PASS=X'                                               
         MVC   P+37(1),PASSNO                                                   
         OI    P+37,X'F0'                                                       
*                                                                               
TRCH4C   DS    0H                                                               
         CLI   EZMODE,EZSPTP       FOR DETAILS                                  
         BE    TRCH4D              SHOW IOR #                                   
         CLI   EZMODE,EZSPTL                                                    
         BE    TRCH4D                                                           
*                                                                               
         MVC   P+40(4),=C'INV='    ELSE INV#                                    
         MVC   P+44(10),EZIHINV                                                 
         B     TRCH6                                                            
*                                                                               
TRCH4D   DS    0H                                                               
         MVC   P+40(4),=C'IOR='                                                 
         MVC   P+44(17),EZSPIORD                                                
         B     TRCH6                                                            
*                                                                               
TRCH5    DS    0H                                                               
         GOTO1 HEXOUT,DMCB,EZERRCD,P+32,1,=C'N'                                 
         GOTO1 HEXOUT,DMCB,EZERRLEV,P+35,1,=C'N'                                
         MVC   P+38(20),EZERRREC   RECORD NAME                                  
         MVC   P+60(20),EZERRFLD   AND FIELD                                    
         MVC   P+82(40),EZERRMSG   AND MESSAGE                                  
         GOTO1 EZPRINT,DMCB,PM1,=C'BL01'                                        
         MVC   P,SPACES                                                         
         MVC   P+82(30),EZERRDTA  AND DATA                                      
*                                                                               
TRCH6    DS    0H                                                               
         GOTO1 EZPRINT,DMCB,PM1,=C'BL01'                                        
         MVC   P,SPACES                                                         
*                                                                               
         XIT1                                                                   
         EJECT                                                                  
MODETAB  DS    0X                                                               
         DC    AL1(EZERRP),CL10'PROC ERROR'                                     
         DC    AL1(EZINVP),CL10'PROC INV'                                       
         DC    AL1(EZICCOMP),CL10'PROC IC-T'                                    
         DC    AL1(EZSPTP),CL10'PROC INSRT'                                     
         DC    AL1(EZRECP),CL10'PROC RECON'                                     
         DC    AL1(EZAAP),CL10'ADD''L ADJ'                                      
         DC    AL1(EZIBCOMP),CL10'PROC IC-B'                                    
         DC    AL1(EZSPTL),CL10'INSRT LAST'                                     
         DC    AL1(EZINVL),CL10'INV LAST'                                       
         DC    AL1(EZBATL),CL10'BATCH LAST'                                     
         DC    X'FF',CL10'*UNKNOWN*'                                            
         EJECT                                                                  
*        DATAMGR INTERFACE                                                      
*                                                                               
MPLHI    DS    0H                                                               
         LR    R0,RE                                                            
         L     RF,EZUTL                                                         
         LTR   RF,RF               IF NO UTL, NO OPEN                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         SPACE                                                                  
         MVC   4(1,RF),EZCONSE       SET CONTROL SE                             
         MVC   MPLKEYSV,MPLKEY                                                  
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,DMRDHI),MPLDIR,MPLKEY,MPLKEY                
         B     MPLDMCK                                                          
         SPACE 2                                                                
MPLGET   DS    0H                                                               
         LR    R0,RE                                                            
         L     RF,EZUTL                                                         
         LTR   RF,RF               IF NO UTL, NO OPEN                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   4(1,RF),EZCONSE       SET CONTROL SE                             
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,GETREC),MPLFIL,MPLKEY+36,          X        
               EZAREC,DMWORK                                                    
         B     MPLDMCK                                                          
         SPACE 2                                                                
MPLADD   DS    0H                                                               
         LR    R0,RE                                                            
         CLI   EZWRITE,C'Y'                                                     
         BNE   DMCK4                                                            
         L     RF,EZUTL                                                         
         MVC   4(1,RF),EZCONSE       SET CONTROL SE                             
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,ADDREC),MPLFIL,MPLKEY,             X        
               EZAREC,DMWORK                                                    
         B     MPLDMCK                                                          
         SPACE 2                                                                
HIGH     DS    0H                                                               
         LR    R0,RE                                                            
         MVC   KEYSAVE,KEY                                                      
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,DMRDHI),PRTDIR,KEY,KEY                      
         B     DMCK                                                             
         SPACE 2                                                                
PUBHIGH  DS    0H                                                               
         LR    R0,RE                                                            
         MVC   KEYSAVE,KEY                                                      
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,DMRDHI),PUBDIR,KEY,KEY                      
         B     DMCK                                                             
         SPACE 2                                                                
SEQ      DS    0H                                                               
         LR    R0,RE                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,DMRSEQ),PRTDIR,KEY,KEY                      
         B     DMCK                                                             
         SPACE 2                                                                
ADDIR    DS    0H                                                               
         LR    R0,RE                                                            
         CLI   EZWRITE,C'Y'                                                     
         BNE   DMCK4                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,DMADD),PRTDIR,KEY,KEY                       
         B     DMCK                                                             
         SPACE 2                                                                
WRITE    DS    0H                                                               
         LR    R0,RE                                                            
         CLI   EZWRITE,C'Y'                                                     
         BNE   DMCK4                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,DMWRT),PRTDIR,KEY,KEY                       
         B     DMCK                                                             
         SPACE 2                                                                
GET      DS    0H                                                               
         LR    R0,RE                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,GETREC),PRTFIL,KEY+27,             X        
               EZAREC,DMWORK                                                    
         B     DMCK                                                             
         SPACE 2                                                                
PUBGET   DS    0H                                                               
         LR    R0,RE                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,GETREC),PUBFIL,KEY+27,             X        
               EZAREC,DMWORK                                                    
         B     DMCK                                                             
         SPACE 2                                                                
PUT      DS    0H                                                               
         LR    R0,RE                                                            
         CLI   EZWRITE,C'Y'                                                     
         BNE   DMCK4                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,PUTREC),PRTFIL,KEY+27,             X        
               EZAREC,DMWORK                                                    
         B     DMCK                                                             
         SPACE 2                                                                
ADD      DS    0H                                                               
         LR    R0,RE                                                            
         CLI   EZWRITE,C'Y'                                                     
         BNE   DMCK4                                                            
         SPACE                                                                  
         GOTO1 DATAMGR,DMCB,(DMIBTS,ADDREC),PRTFIL,KEY,                X        
               EZAREC,DMWORK                                                    
         B     DMCK                                                             
*                                                                               
MPLDMCK  DS    0H                                                               
         L     RF,EZUTL                                                         
         LTR   RF,RF               IF NO UTL, NO OPEN                           
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   4(1,RF),SAVSYS      RESTORE SE NUM                               
*                                                                               
DMCK     DS    0H                                                               
         MVC   BYTE,DMOBTS                                                      
         NC    BYTE,DMCB+8                                                      
         BZ    DMCK4                                                            
*                                                                               
         DC    H'0'                DATAMGR ERROR- FOR NOW JUST ABEND            
         LR    RE,R0                                                            
         LTR   RE,RE               RETURN CC NOT = IF ERROR                     
         BR    RE                                                               
*                                                                               
DMCK4    DS    0H                                                               
         LR    RE,R0                                                            
         CR    RE,RE               RETURN CC = IF NO ERROR                      
         BR    RE                                                               
         SPACE 2                                                                
         EJECT                                                                  
*                                  ERROR TABLE                                  
*                                  CODE(1),LEVEL(1),MESSAGE(40)                 
*                                                                               
ERRTAB   DS    0C                                                               
         DC    AL1(EZERRMIS)       MISSING FIELD                                
         DC    AL1(0)              LEVEL                                        
         DC    CL40'MISSING FIELD'                                              
*                                                                               
         DC    AL1(EZERRINV)       INVALID DATA                                 
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INVALID DATA'                                               
*                                                                               
         DC    AL1(EZERRRNF)       RECORD NOT FOUND                             
         DC    AL1(0)              LEVEL                                        
         DC    CL40'RECORD NOT FOUND'                                           
*                                                                               
         DC    AL1(EZERRPRD)       PRODUCT INCONSISTENCY                        
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRODUCTS INCONSISTENT'                                      
*                                                                               
         DC    AL1(EZERROMN)       OUT OF MONTH                                 
         DC    AL1(0)              LEVEL                                        
         DC    CL40'SPOT DATE OUT OF MONTH'                                     
*                                                                               
         DC    AL1(EZERRFLN)       FIELD TOO LONG                               
         DC    AL1(0)              LEVEL                                        
         DC    CL40'FIELD TOO LONG- DATA HAS BEEN TRUNCATED'                    
*                                                                               
         DC    AL1(EZERRSCO)       STANDARD COMMENT OVERFLOW                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'TOO MANY STANDARD COMMENTS'                                 
*                                                                               
         DC    AL1(EZERRRTP)       BAD RECORD TYPE                              
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INVALID RECORD TYPE'                                        
*                                                                               
         DC    AL1(EZERRSCD)       INVALID PRINTPAK CLT/PRD CODE                
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INVALID PRINTPAK CODE'                                      
*                                                                               
         DC    AL1(EZERRADV)       ADVERTISER INCONSISTENCY                     
         DC    AL1(0)              LEVEL                                        
         DC    CL40'ADVERTISER INCONSISTENCY'                                   
*                                                                               
         DC    AL1(EZERRADC)       AD CODE INCONSISTENCY                        
         DC    AL1(0)              LEVEL                                        
         DC    CL40'AD CODE INCONSISTENCY'                                      
*                                                                               
         DC    AL1(EZERRION)       IO FIELD ERROR                               
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INSERTION ORDER ERROR'                                      
*                                                                               
         DC    AL1(EZERRINF)       IO NOT FOUND                                 
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INSERTION ORDER NOT FOUND'                                  
*                                                                               
         DC    AL1(EZERRPNA)       PUB NOT FOUND FOR AGENCY                     
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PUB NOT DEFINED FOR AGECNY'                                 
*                                                                               
         DC    AL1(EZERREDT)       EDITION INCONSISTENCY                        
         DC    AL1(0)              LEVEL                                        
         DC    CL40'EDITIONS INCONSISTENT'                                      
*                                                                               
         DC    X'FFFF'                                                          
ERRTABL  EQU   42                                                               
         SPACE 2                                                                
         LTORG                                                                  
         DROP  RC                                                               
         EJECT                                                                  
*        FLDCVT - FIELD CONVERSIONS AND LOOKUPS                                 
         SPACE 2                                                                
         DS    0D                                                               
FLDCVT   NMOD1 0,FLDCVT,R7                                                      
         L     RC,SAVWORK          RESTORE WORK AREA ADDRESS                    
         USING EZMODD,RC                                                        
*                                                                               
         CLI   EZCONVSW,X'FF'      TEST TO SUPPRESS CONVERSIONS                 
         BE    FCX                                                              
*                                                                               
         CLI   RECTYPE,EZSNEQ      STATION RECORD                               
         BE    FCSN                                                             
         CLI   RECTYPE,EZIHEQ      INVOICE HEADER                               
         BE    FCIH                                                             
         CLI   RECTYPE,EZSPEQ      INSERTION DETAIL                             
         BE    FCSP                                                             
         CLI   RECTYPE,EZAAEQ      ADDITIONAL ADJUSTMENT                        
         BE    FCAA                                                             
         CLI   RECTYPE,EZITEQ      INVOICE TOTAL                                
         BE    FCIT                                                             
         B     FCX                                                              
         EJECT                                                                  
*        STATION HEADER FIELDS                                                  
*        ---------------------                                                  
*                                                                               
FCSN     DS    0H                                                               
         BAS   RE,CHKPUB                                                        
*                                                                               
FCSNX    DS    0H                                                               
         B     FCX                                                              
         SPACE 3                                                                
*        INVOICE HEADER FIELDS                                                  
*        ---------------------                                                  
*                                                                               
FCIH     DS    0H                                                               
         CLI   EZSELINV,C' '       TEST HAVE INVOICE FILTER                     
         BNH   FCIH1                NO                                          
         CLC   EZIHINV,EZSELINV    YES- INV NUMBER MUST AGREE                   
         BE    FCIH1                                                            
         MVI   SKIPSW,C'Y'         IF NOT, SKIP                                 
         B     FCX                                                              
*                                                                               
FCIH1    DS    0H                                                               
         LA    R2,EZIHCVAD         OVERRIDE ADV                                 
         CLI   0(R2),C' '          IF PRESENT                                   
         BH    *+8                 USE IT                                       
         LA    R2,EZIHAVCD         ELSE USE ADVERTISER                          
         CLI   0(R2),C' '                                                       
         BNH   FCIH6                                                            
         MVC   LADVC,0(R2)                                                      
         BAS   RE,LKADV                                                         
         BE    *+12                                                             
         BAS   RE,SETERR                                                        
         B     FCIH6                                                            
*                                                                               
         MVC   EZIHLADC,HADVCOD    SET LOOK UP VALUES                           
         MVC   EZIHLADN,HADVNAM                                                 
         OC    EZIHLKST,HADVLKST                                                
*                                                                               
         LA    R2,EZIHCVPR         OVERRIDE PRD                                 
         CLI   0(R2),C' '          IF PRESENT                                   
         BH    *+8                 USE IT                                       
         LA    R2,EZIHPRCD         ELSE USE PRODUCT                             
         CLI   0(R2),C' '                                                       
         BNH   FCIH6                                                            
         MVC   LPRDC,0(R2)                                                      
         BAS   RE,LKPRD                                                         
         BE    *+12                                                             
         BAS   RE,SETERR                                                        
         B     FCIH6                                                            
*                                                                               
         MVC   EZIHLPRN,HPRDNAM    NAME                                         
         MVC   EZIHLPRC,HPRDCOD    CODE                                         
         OC    EZIHLKST,HPRDLKST   LOOKUP STATUS                                
*                                                                               
FCIH6    DS    0H                                                               
         LA    R2,EZIHIDAT         INVOICE DATE                                 
         BAS   RE,TSTANY           TEST THERE                                   
         BNE   FCIH7                                                            
         BAS   RE,CVTDAT           YES, CONVERT DATE                            
         MVC   EZIHDIDT,DDATE      MMMDD/YY                                     
         MVC   EZIHBIDT,BDATE      BINARY 3                                     
         OC    EZIHLKST,HPRDLKST   LOOKUP STATUS                                
*                                                                               
FCIH7    DS    0H                                                               
         LA    R2,EZIHDUED         DUE DATE                                     
         BAS   RE,TSTANY           TEST THERE                                   
         BNE   FCIH8                                                            
         BAS   RE,CVTDAT           YES, CONVERT DATE                            
         MVC   EZIHDUDT,DDATE      MMMDD/YY                                     
         MVC   EZIHBUDT,BDATE      BINARY 3                                     
*                                                                               
FCIH8    DS    0H                                                               
         OC    EZIHBEST,EZIHCVES   ANY OVERRIDE ESTIMATE?                       
         BNZ   FCIH9               YES, USE IT                                  
         LA    R2,EZIHEST          AGENCY ESTIMATE                              
         LA    R3,L'EZIHEST        GET LENGTH                                   
         MVI   NUMERRSW,C'N'       SET DONT CARE ABOUT ERRORS                   
*                                  (IF NOT NUMBER <1000, IGNORE)                
         XC    NUMMAX,NUMMAX                                                    
         BAS   RE,CVTNUM                                                        
         CLC   FULL,=F'999'                                                     
         BH    *+10                                                             
         MVC   EZIHBEST,FULL+2     SET VALUE                                    
*                                                                               
FCIH9    DS    0H                  EDITION                                      
         LA    R2,EZIHEDCD                                                      
         CLI   0(R2),C' '                                                       
         BNH   FCIH10                                                           
         MVC   LEDTN,EZIHEDCD                                                   
         BAS   RE,LKEDTN                                                        
         BE    *+12                                                             
         BAS   RE,SETERR                                                        
         B     FCIH10                                                           
*                                                                               
         MVC   EZIHLZON,HEDTZON                                                 
         MVC   EZIHLEDC,HEDTEDC                                                 
*                                                                               
FCIH10   DS    0H                                                               
         LA    R2,EZIHISDT         INVOICE PERIOD START                         
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   FCIH12D                                                          
         BAS   RE,CVTDAT           CONVERT DATE                                 
         MVC   EZIHDISD,DDATE      MMMDD/YY                                     
*                                                                               
FCIH12D  DS    0H                                                               
         LA    R2,EZIHIEDT         INVOICE PERIOD END                           
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   FCIH12F                                                          
         BAS   RE,CVTDAT           CONVERT DATE                                 
         MVC   EZIHDIED,DDATE      MMMDD/YY                                     
*                                                                               
FCIH12F  DS    0H                                                               
         LA    R2,EZIHCSDT         CONTRACT YEAR START                          
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   FCIH12H                                                          
         BAS   RE,CVTDAT           CONVERT DATE                                 
         MVC   EZIHDCSD,DDATE      MMMDD/YY                                     
*                                                                               
FCIH12H  DS    0H                                                               
         LA    R2,EZIHCEDT         CONTRACT YEAR END                            
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   FCIH14                                                           
         BAS   RE,CVTDAT           CONVERT DATE                                 
         MVC   EZIHDCED,DDATE      MMMDD/YY                                     
*                                                                               
FCIH14   DS    0H                                                               
         LA    R2,EZIHMON          MONTH OF SERVICE                             
         BAS   RE,TSTANY           MUST BE THERE                                
         BNZ   FCIH16                                                           
         XC    EDATE,EDATE                                                      
         MVC   EDATE(4),0(R2)                                                   
         OC    EDATE,=6C'0'                                                     
         CLC   EDATE+0(2),=C'94'   VALIDATE YEAR                                
         BL    FCIH14H                                                          
         CLC   EDATE+2(2),=C'01'   VALIDATE MONTH                               
         BL    FCIH14H                                                          
         CLC   EDATE+2(2),=C'12'                                                
         BH    FCIH14H                                                          
*                                                                               
FCIH14D  DS    0H                                                               
         GOTO1 DATCON,DMCB,(0,EDATE),(9,EZIHDMOS)                               
         GOTO1 (RF),(R1),,(3,WORK)                                              
         MVC   EZIHBMOS,WORK                                                    
         B     FCIH16                                                           
*                                                                               
FCIH14H  DS    0H                                                               
         MVI   EZERRCD,EZERRINV                                                 
         BAS   RE,SETERR                                                        
*                                                                               
FCIH16   DS    0H                                                               
         LA    R2,EZIHSTP          SALES TAX PCT                                
         LA    R3,L'EZIHSTP        GET LENGTH                                   
         GOTO1 CASHVAL,DMCB,(2,EZIHSTP),(R3)                                    
         CLI   DMCB,X'FF'          FIELD OK                                     
         BE    *+12                                                             
         MVI   EZERRCD,EZERRINV                                                 
         B     SETERRB                                                          
         SPACE                                                                  
         MVC   EZIHBSTP,DMCB+4     SET VALUE                                    
*                                                                               
         B     FCIH20                                                           
         DC    XL64'00'            PATCH                                        
FCIH20   DS    0H                                                               
         B     FCX                                                              
         EJECT                                                                  
*        INSERTION DETAIL FIELDS                                                
*        -----------------------                                                
*                                                                               
FCSP     DS    0H                  REGIONAL COMBO TEST                          
         TM    EZLOOKSW,EZLWRGOP   TEST TO IGNORE WSJ REG PROFILE               
         BNZ   FCSP4B              BYTE (SO Z9 WILL SHOW ALL INSERTS)           
*                                                                               
         CLC   EZSNSTA(3),=C'WSJ'  FOR VARIOUS WSJ'S                            
         BNE   FCSP4B                                                           
*                                  DO SPECIAL REGIONAL COMBO STUFF              
*                                                                               
         CLC   EZSPRCOM(3),=C'N00' ALWAYS ALLOW N00                             
         BE    FCSP4B                                                           
*                                                                               
         CLI   EZSPRCOM,C'Y'       REGIONAL COMBO SUMMARY?                      
         BNE   FCSP4                                                            
*                                                                               
         CLI   EZRCSOPT,C'Y'       YES, TEST TO PROCESS                         
         BE    FCSP4B                                                           
         MVI   BADRCD,C'Y'         NO, REJECT                                   
         B     FCSPX                                                            
*                                                                               
FCSP4    DS    0H                  NOT A REG COMBO SUMMARY                      
         CLI   EZRCSOPT,C'Y'       TEST TO PROCESS                              
         BNE   FCSP4B                                                           
         MVI   BADRCD,C'Y'         NO, REJECT                                   
         B     FCSPX                                                            
*                                                                               
*              NOTE- PRODUCT AND EDITN CHECK DONE BEFORE INS ORDER              
*                                                                               
FCSP4B   DS    0H                                                               
         L     RF,EZIHTSPN         BUMP SPOT COUNT                              
         LA    RF,1(RF)                                                         
         ST    RF,EZIHTSPN                                                      
*                                                                               
         LA    R2,EZSPCVPR         OVERRIDE PRD                                 
         CLI   0(R2),C' '          IF PRESENT                                   
         BH    *+8                 USE IT                                       
         LA    R2,EZSPPRCD         ELSE USE PRODUCT                             
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   FCSP6T                                                           
*                                                                               
         CLI   EZIHLADC,C' '       BUT IF DON'T HAVE CLIENT, POSTPONE           
         BNH   FCSP6T              (PRD OVRD BUT NO CLIENT OVRD)                
*                                                                               
         MVC   LPRDC,0(R2)                                                      
         BAS   RE,LKPRD                                                         
         BE    *+12                                                             
         BAS   RE,SETERR                                                        
         B     FCSP6T                                                           
         MVC   EZSPLPRN,HPRDNAM    NAME                                         
         MVC   EZSPLPRC,HPRDCOD    CODE                                         
         OC    EZIHLKST,HPRDLKST   STATUS                                       
*                                                                               
FCSP6T   DS    0H                                                               
*                                                                               
FCSP7    DS    0H                  EDITION                                      
         LA    R2,EZSPEDCD                                                      
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   FCSP7B                                                           
         MVC   LEDTN,EZSPEDCD                                                   
         BAS   RE,LKEDTN                                                        
         BE    *+12                                                             
         BAS   RE,SETERR                                                        
         B     FCSP7T                                                           
         MVC   EZSPLZON,HEDTZON    ZONE                                         
         MVC   EZSPLEDC,HEDTEDC    EDITION                                      
*                                                                               
FCSP7B   DS    0H                                                               
         OC    EZIHLZON(2),EZIHLZON  IF HAVE INV LEVEL EDITION                  
         BZ    FCSP7T                                                           
         CLC   EZSPLZON(2),EZIHLZON   IT MUST AGREE                             
         BE    FCSP7T                                                           
         LA    R2,EZSPEDCD                                                      
         MVI   EZERRCD,EZERREDT    INCONSISTENT                                 
         BAS   RE,SETERR                                                        
*                                                                               
FCSP7T   DS    0H                  INSERTION ORDER                              
         LA    R2,EZSPIORD                                                      
         CLI   EZSPIORD,C' '       IF WE HAVE INSERTION ORDER                   
         BNH   FCSP7V                                                           
         BAS   RE,LKIORD           GET CLT/PRD FROM INS ORD                     
         BE    *+8                                                              
         BAS   RE,SETERR                                                        
*                                                                               
FCSP7V   DS    0H                                                               
         BAS   RE,SETHLPRC         SET EZIHLPRC                                 
*                                                                               
         CLI   EZSPLPRC,C' '       MUST HAVE INSERT LEVEL PROD                  
         BH    FCSP8                                                            
         LA    R2,EZSPPRCD                                                      
         MVI   EZERRCD,EZERRMIS    MISSING FIELD                                
         BAS   RE,SETERR                                                        
*                                                                               
FCSP8    DS    0H                                                               
         OC    EZSPBEST,EZSPCVES   ANY OVERRIDE ESTIMATE?                       
         BNE   FCSP8H              YES, USE IT                                  
         LA    R2,EZIHEST          AGENCY ESTIMATE                              
         LA    R3,L'EZSPEST        GET LENGTH                                   
         MVI   NUMERRSW,C'N'       SET DONT CARE ABOUT ERRORS                   
*                                  (IF NOT NUMBER <1000, IGNORE)                
         XC    NUMMAX,NUMMAX                                                    
         BAS   RE,CVTNUM                                                        
         CLC   FULL,=F'999'                                                     
         BH    *+10                                                             
         MVC   EZSPBEST,FULL+2     SET VALUE                                    
*                                                                               
FCSP8H   DS    0H                                                               
         LA    R2,EZSPDATE         DATE                                         
         BAS   RE,CVTDAT                                                        
         BNE   FCSP04              ERROR, ON TO NEXT FIELD                      
         MVC   EZSPDDT,DDATE                                                    
         MVC   EZSPCDT,CDATE                                                    
*                                                                               
         CLC   EZSPDATE(4),EZIHMON  TEST WITHIN INVOICE MONTH                   
         BL    FCSP03                                                           
         CLC   EZSPDATE(4),EZIHMON                                              
         BNH   FCSP04                                                           
*                                                                               
FCSP03   DS    0H                                                               
         MVI   EZERRCD,EZERROMN    OUT OF MONTH ERROR                           
         BAS   RE,SETERR                                                        
*                                                                               
FCSP04   DS    0H                                                               
FCSP09   DS    0H                                                               
         LA    R2,EZSPRATE         RATE                                         
         LA    R3,L'EZSPRATE       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBRAT,FULL                                                    
*                                                                               
         LA    R2,EZSPLINS         LINES                                        
         LA    R3,L'EZSPLINS       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBLNS,FULL                                                    
*                                                                               
         LA    R2,EZSPCLMS         COLUMNS                                      
         LA    R3,L'EZSPCLMS       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBCLS,FULL                                                    
*                                                                               
         LA    R2,EZSPICHS         INCHES                                       
         LA    R3,L'EZSPICHS       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBICS,FULL                                                    
*                                                                               
         LA    R2,EZSPPRM1         PREM 1                                       
         LA    R3,L'EZSPPRM1       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBPM1,FULL                                                    
*                                                                               
         LA    R2,EZSPPRM2         PREM 2                                       
         LA    R3,L'EZSPPRM2       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBPM2,FULL                                                    
*                                                                               
         LA    R2,EZSPPRM3         PREM 3                                       
         LA    R3,L'EZSPPRM3       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBPM3,FULL                                                    
*                                                                               
         LA    R2,EZSPCLAM         COLOR PREMIUM AMOUNT                         
         LA    R3,L'EZSPCLAM       LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBCLA,FULL                                                    
*                                                                               
         LA    R2,EZSPGRS          GROSS                                        
         LA    R3,L'EZSPGRS        LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBGRS,FULL                                                    
*                                                                               
         LA    R2,EZSPNET          NET                                          
         LA    R3,L'EZSPNET        LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBNET,FULL                                                    
*                                                                               
         LA    R2,EZSPCD           CASH DISC                                    
         LA    R3,L'EZSPCD         LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBCD,FULL                                                     
*                                                                               
         L     RF,EZIHTSPC         BUMP SPOT COST TOTAL                         
         A     RF,FULL                                                          
         ST    RF,EZIHTSPC                                                      
*                                  ADJUSTMENTS                                  
FCSP14   DS    0H                                                               
         LA    R2,EZSPADR          DEBIT                                        
         LA    R3,L'EZSPADR        LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZSPBDR,FULL                                                     
*                                                                               
         L     RF,EZIHTDR          BUMP DEBIT TOTALS                            
         A     RF,FULL                                                          
         ST    RF,EZIHTDR                                                       
*                                                                               
         LA    R2,EZSPACR          CREDIT                                       
         LA    R3,L'EZSPACR        LENGTH                                       
         BAS   RE,CVTAMT                                                        
         L     RF,FULL             COMPLEMENT CR AMOUNT                         
         LTR   RF,RF                                                            
         BNP   *+10                IF NOT ALREADY NEGATIVE                      
         LCR   RF,RF                                                            
         ST    RF,FULL                                                          
         MVC   EZSPBCR,FULL                                                     
*                                                                               
         L     RF,EZIHTCR          BUMP CREDIT TOTALS                           
         A     RF,FULL                                                          
         ST    RF,EZIHTCR                                                       
*                                                                               
*                                                                               
FCSPX    DS    0H                                                               
*                                                                               
         B     FCSP20                                                           
         DC    XL64'00'            PATCH                                        
FCSP20   DS    0H                                                               
         B     FCX                                                              
         EJECT                                                                  
*        ADDITIONAL ADJUSTMENT                                                  
*        ---------------------                                                  
*                                                                               
FCAA     DS    0H                                                               
         LA    R2,EZAAGRS          GROSS                                        
         LA    R3,L'EZAAGRS        LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZAABGRS,FULL                                                    
*                                                                               
         LA    R2,EZAANET          NET                                          
         LA    R3,L'EZAANET        LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZAABNET,FULL                                                    
*                                                                               
         LA    R2,EZAACD           CASH DISC                                    
         LA    R3,L'EZAACD         LENGTH                                       
         BAS   RE,CVTAMT                                                        
         MVC   EZAABCD,FULL                                                     
*                                                                               
FCAAX    DS    0H                                                               
         B     FCX                                                              
         EJECT                                                                  
*        INVOICE TOTAL RECORD FIELDS                                            
*        ---------------------------                                            
*                                                                               
FCIT     DS    0H                                                               
         LA    R2,EZITSCH                                                       
         LA    R3,L'EZITSCH                                                     
         LA    R4,EZITBSCH                                                      
         LA    R5,12               12 FIELDS OF 11 BYTES                        
*                                                                               
FCIT20   DS    0H                                                               
         BAS   RE,CVTAMT                                                        
         MVC   0(4,R4),FULL                                                     
*                                                                               
         AR    R2,R3               NEXT FIELD                                   
         LA    R4,4(R4)                                                         
         BCT   R5,FCIT20                                                        
*                                                                               
         LA    R2,EZITSPC                                                       
         LA    R3,L'EZITSPC                                                     
         LA    R4,EZITBSPC                                                      
         LA    R5,1                1 FIELD - SPOTS - 5 BYTES                    
*                                                                               
FCIT40   DS    0H                                                               
         BAS   RE,CVTAMT                                                        
         MVC   0(4,R4),FULL                                                     
*                                                                               
         AR    R2,R3               NEXT FIELD                                   
         LA    R4,4(R4)                                                         
         BCT   R5,FCIT40                                                        
*                                                                               
         LA    R2,EZITGST                                                       
         LA    R3,L'EZITGST                                                     
         LA    R4,EZITBGST                                                      
         LA    R5,2                2 FIELDS - GST,QST - 11 BYTES                
*                                                                               
FCIT60   DS    0H                                                               
         BAS   RE,CVTAMT                                                        
         MVC   0(4,R4),FULL                                                     
*                                                                               
         AR    R2,R3               NEXT FIELD                                   
         LA    R4,4(R4)                                                         
         BCT   R5,FCIT60                                                        
*                                                                               
         ICM   RF,15,EZITBRCR      COMPLEMENT CR AMOUNT                         
         BNP   *+10                IF NOT ALREADY NEGATIVE                      
         LCR   RF,RF                                                            
         STCM  RF,15,EZITBRCR                                                   
*                                                                               
         B     FCX                                                              
*                                                                               
FCX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
*        DATE CONVERSION ROUTINE                                                
*                                                                               
CVTDAT   DS    0H                                                               
         MVC   EDATE,0(R2)                                                      
*                                                                               
CVTDAT2  NTR1                      2ND ENTRY (EDATE SET BY CALLER)              
         XC    BDATE,BDATE                                                      
         MVC   DDATE,EDATE            SET DISPLAY IN CASE OF ERROR              
*                                                                               
         OC    EDATE,=6C'0'                                                     
         CLC   EDATE+0(2),=C'94'      VALIDATE YEAR                             
         BL    CVTDAT8                                                          
         CLC   EDATE+2(2),=C'01'      VALIDATE MONTH                            
         BL    CVTDAT8                                                          
         CLC   EDATE+2(2),=C'12'                                                
         BH    CVTDAT8                                                          
         CLC   EDATE+4(2),=C'01'      VALIDATE DAY                              
         BL    CVTDAT8                                                          
         CLC   EDATE+4(2),=C'31'                                                
         BH    CVTDAT8                                                          
*                                                                               
         GOTO1 DATCON,DMCB,(0,EDATE),(5,DDATE)                                  
         GOTO1 (RF),(R1),,(3,BDATE)                                             
         GOTO1 (RF),(R1),,(2,CDATE)                                             
         B     CVTDATX                                                          
*                                                                               
CVTDAT8  DS    0H                                                               
         MVI   EZERRCD,EZERRINV                                                 
         B     SETERRB             RETURNS CC NOT =                             
*                                                                               
CVTDATX  DS    0H                                                               
         CR    RE,RE               OK EXIT WITH CC =                            
         XIT1                                                                   
         SPACE 3                                                                
*        CVTNUM - CONVERT NUMERIC FIELD                                         
*                                                                               
CVTNUM   NTR1                                                                   
         XC    FULL,FULL                                                        
         CLI   0(R2),C' '          TEST THERE                                   
         BNH   CVTNX                                                            
         LA    R3,0(R2,R3)         GET END                                      
         BCTR  R3,R0                                                            
         CLI   0(R3),C' '                                                       
         BH    *+8                                                              
         BCT   R3,*-8                                                           
*                                                                               
         SR    R3,R2               SET LENGTH                                   
         LA    R3,1(R3)                                                         
         GOTO1 NUMVAL,DMCB,(0,(R2)),(R3)                                        
         CLI   DMCB,X'FF'          TEST NUMERIC                                 
         BNE   CVTN4                                                            
         CLI   NUMERRSW,C'Y'       TEST WORRIED ABOUT ERROR                     
         BNE   CVTNX                                                            
CVTN3    DS    0H                                                               
         MVI   EZERRCD,EZERRINV                                                 
         B     SETERRB                                                          
*                                                                               
CVTN4    DS    0H                                                               
         OC    NUMMAX,NUMMAX       TEST ANY MAXIMUM                             
         BZ    *+14                                                             
         CLC   DMCB+4(4),NUMMAX                                                 
         BH    CVTN3                                                            
*                                                                               
         STC   R3,BYTE             SET ACTUAL LENGTH                            
         MVC   FULL,DMCB+4                                                      
*                                                                               
CVTNX    CR    RE,RE               OK, EXIT WITH CC =                           
         XIT1                                                                   
         SPACE 2                                                                
*        CVTAMT - CONVERT $ AMOUNT FIELD                                        
*                 **NOTE - SAME AS CVTNUM  **                                   
*                                                                               
CVTAMT   DS    0H                                                               
         MVI   NUMERRSW,C'Y'       SET TO SHOW ERRORS                           
         XC    NUMMAX,NUMMAX                                                    
         B     CVTNUM                                                           
         SPACE 3                                                                
*        CHKPUB - FIND PUB IN PUB TRANSLATE TABLE                               
*                                                                               
CHKPUB   NTR1                                                                   
         L     RF,=V(PTRTAB)                                                    
         A     RF,RELO                                                          
         USING PTRTABD,RF                                                       
*                                                                               
CKP2     DS    0H                                                               
         CLI   0(RF),X'FF'         EOL                                          
         BE    CKP10                                                            
         CLC   PTRUSCOD,EZAGY      RIGHT AGENCY?                                
         BNE   CKP6                                                             
         CLC   EZSNSTA,PTRPZPUB    AND PUB (EXTERNAL CODE)                      
         BNE   CKP6                                                             
         MVC   EZSNMED,PTRMED                                                   
         MVC   EZSNPUB(6),PTRPUB                                                
         MVC   EZSNETAB,PTRETAB    A(EDITION TABLE)                             
         B     CKP20                                                            
*                                                                               
CKP6     DS    0H                                                               
         LA    RF,PTRTABL(RF)                                                   
         B     CKP2                                                             
*                                                                               
CKP10    DS    0H                  PUB NOT FOUND FOR AGENCY                     
         MVI   EZERRCD,EZERRPNA                                                 
         B     SETERRB                                                          
*                                  NOW FIND PRINTPAK PUB NAME                   
CKP20    DS    0H                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+0(1),EZSNMED                                                 
         MVC   KEY+1(6),EZSNPUB    PUB/ZONE/EDIT                                
         MVC   KEY+7(2),EZAGY                                                   
         MVI   KEY+9,X'81'                                                      
         BAS   RE,PUBHIGH                                                       
         CLC   KEY(25),KEYSAVE                                                  
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         BAS   RE,PUBGET                                                        
         L     RF,EZAREC                                                        
         MVC   EZSNANM1(40),PUBNAME-PUBREC(RF)  NAME 1 AND NAME 2               
*                                                                               
CKPX     DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
***********************************************************************         
*        SETHLPRC - SET EZIHLPRC AFTER INSERT ORDER TESTING                     
***********************************************************************         
*                                                                               
SETHLPRC NTR1                                                                   
         CLI   EZIHLPRC,C' '       NO PRD YET (FIRST TIME)                      
         BNH   SHLP20                                                           
         CLC   EZIHLPRC,=C'***'    MIXED VALID PRODS                            
         BE    SHLP30                                                           
         CLC   EZIHLPRC,=C'**2'    VALID PROD(S) AND MISSING                    
         BE    SHLP40              OR ALL MISSING                               
         B     SHLP50              SINGLE VALID PROD                            
*                                                                               
SHLP20   DS    0H                  NO HEADER PROD YET (1ST TIME)                
         CLI   EZSPLPRC,C' '       DETAIL PROD BLANK (MISSING)                  
         BH    SHLP24                                                           
*                                                                               
         MVC   EZIHLPRC,=C'**2'    SET SOME PROD MISSING                        
         B     SHLP80                                                           
*                                                                               
SHLP24   DS    0H                  HAVE VALID PROD FOR DETAIL                   
         MVC   EZIHLPRC,EZSPLPRC   PROVISIONALY SET IN HEADER                   
         MVC   EZIHLPRN,EZSPLPRN   NAME TOO                                     
         B     SHLP80                                                           
*                                                                               
SHLP30   DS    0H                  MIXED PRODS (***)                            
         CLI   EZSPLPRC,C' '       DETAIL PROD BLANK (MISSING)                  
         BH    SHLP34                                                           
*                                                                               
         MVC   EZIHLPRC,=C'**2'    SET SOME PROD MISSING                        
         B     SHLP80                                                           
*                                                                               
SHLP34   DS    0H                  HAVE VALID PROD FOR DETAIL                   
         B     SHLP80              LEAVE AS ***                                 
*                                                                               
SHLP40   DS    0H                  PROD AND MISSING(**2)                        
         CLI   EZSPLPRC,C' '       DETAIL PROD BLANK (MISSING)                  
         BH    SHLP44                                                           
*                                                                               
         B     SHLP80              LEAVE AS **2                                 
*                                                                               
SHLP44   DS    0H                  HAVE VALID PROD FOR DETAIL                   
         B     SHLP80              LEAVE AS **2                                 
*                                                                               
SHLP50   DS    0H                  HAVE SINGLE VALID PROD                       
         CLI   EZSPLPRC,C' '       DETAIL PROD BLANK (MISSING)                  
         BH    SHLP54                                                           
*                                                                               
         MVC   EZIHLPRC,=C'**2'    SET SOME PROD MISSING                        
         B     SHLP54D             BUT CHECK IF HEADER OVERRIDE                 
*                                                                               
SHLP54   DS    0H                  HAVE VALID PROD FOR DETAIL                   
         CLC   EZIHLPRC,EZSPLPRC   IS DETAIL SAME AS HEADER                     
         BE    SHLP80              YES, DONE                                    
*                                                                               
SHLP54D  DS    0H                                                               
         TM    EZIHCVST,EZIHCOVR   IF HEADER PROD IS OVERRIDE                   
         BZ    *+10                                                             
         MVC   EZSPLPRC,EZIHLPRC   SET DETAIL = HEADER                          
         B     SHLP80                                                           
*                                                                               
SHLP80   DS    0H                                                               
SHLPX    DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*        ADVERTISER LOOKUP                                                      
***********************************************************************         
*                                                                               
LKADV    NTR1                                                                   
         MVI   EZERRCD,0                                                        
         MVI   EZRNFRPT,C'N'       CLEAR REPEAT SW                              
         TM    EZLOOKSW,EZLNOADV   TEST SKIP ADV LOOKUP                         
         BNZ   LKADVX                                                           
         CLI   LADVC,C' '                                                       
         BNH   LKADVX                                                           
*                                                                               
         CLC   LADVC,SADVCOD       IS IT SAME AS LAST?                          
         BNE   LKADV10                                                          
*                                                                               
         MVC   HADVDATA,SADVDATA    USE SAVED DATA                              
         TM    HADVLKST,EZIHLANF                                                
         BZ    LKADVX                                                           
         MVI   EZERRCD,EZERRSCD       SPOTPAK CODE NOT FOUND                    
         MVI   EZRNFRPT,C'Y'          SET IS A REPEAT ERROR                     
         B     LKADVX                                                           
*                                                                               
LKADV10  DS    0H                  NEW ADVERTISER                               
         XC    SPRDDATA,SPRDDATA   NEW ADV- CLEAR PRD DATA                      
*                                                                               
         XC    BSPPARS+8(4),BSPPARS+8   CLEAR PRODUCT TABLE                     
*                                                                               
*                                  NOW FIND PRINTPAK CLIENT NAME                
*                                                                               
LKADV54  DS    0H                                                               
         MVC   SADVCOD,LADVC                                                    
         MVI   SADVLKST,0                                                       
*                                                                               
         XC    KEY,KEY                                                          
         MVC   KEY+0(2),EZAGY                                                   
         MVC   KEY+2(1),EZMED                                                   
         MVI   KEY+3,X'02'                                                      
         MVC   KEY+4(3),LADVC                                                   
         BAS   RE,HIGH                                                          
         CLC   KEY(25),KEYSAVE                                                  
         BE    LKADV60                                                          
         SPACE                                                                  
         MVI   EZERRCD,EZERRSCD    IF NOT FOUND - HAVE PROBLEM                  
         OI    SADVLKST,EZIHLANF   AND SAVE IT                                  
         B     LKADVX                                                           
*                                                                               
LKADV60  DS    0H                                                               
         BAS   RE,GET                                                           
         L     RF,EZAREC                                                        
         MVC   SADVNAM,PCLTNAME-PCLTREC(RF)                                     
         OC    SADVNAM,SPACES                                                   
         MVC   HADVDATA,SADVDATA                                                
*                                  GET PZ PROFILE FOR THIS CLIENT               
         XC    EZPROF,EZPROF       MAIN PROFILE                                 
         XC    WORK,WORK                                                        
         MVC   WORK(4),=C'P0EZ'                                                 
         MVC   WORK+4(2),EZAGY                                                  
         LA    RF,EZWKRIND                                                      
         MVC   WORK+6(1),EZMED                                                  
         MVC   WORK+7(3),LADVC                                                  
         L     RF,EZAREC                                                        
         LA    RF,PCLTOFF-PCLTREC(RF)                                           
         CLI   0(RF),C' '                                                       
         BNH   *+14                                                             
         MVI   WORK+10,C'*'                                                     
         MVC   WORK+11(1),0(RF)                                                 
         SPACE                                                                  
         GOTO1 GETPROF,DMCB,WORK,EZPROF,DATAMGR                                 
*                                                                               
LKADVX   DS    0H                                                               
         CLI   EZERRCD,0           SET CC ON EXIT                               
         XIT1                                                                   
         EJECT                                                                  
***********************************************************************         
*        PRODUCT LOOKUP                                                         
***********************************************************************         
*                                                                               
LKPRD    NTR1                                                                   
         MVI   EZERRCD,0                                                        
         MVI   EZRNFRPT,C'N'       SET OFF REPEAT SW                            
         XC    HPRDDATA,HPRDDATA   CLEAR PRD HOLD FIELDS                        
         CLI   LPRDC,C' '                                                       
         BNH   LKPRDX                                                           
*                                                                               
         TM    EZLOOKSW,EZLNOPRD   TEST SKIP PRD LOOKUP                         
         BNZ   LKPRDX                                                           
*                                                                               
         CLC   LPRDC,SPRDCOD       TEST SAME AS SAVED PROD                      
         BNE   LKPRD10                                                          
         MVC   HPRDDATA,SPRDDATA      YES - USE SAVED DATA                      
*                                                                               
         TM    HPRDLKST,EZIHLPNF   TEST SPOTPAK CODE OK                         
         BZ    LKPRDX                                                           
         MVI   EZRNFRPT,C'Y'         SET IS A REPEAT ERROR                      
         MVI   EZERRCD,EZERRSCD                                                 
         B     LKPRDX                                                           
*                                                                               
LKPRD10  DS    0H                                                               
         MVC   SPRDCOD,LPRDC                                                    
         MVC   HPRDCOD,LPRDC                                                    
         OC    BINSRCH,BINSRCH     IS THERE A BINSRCH RTN AVAIL                 
         BZ    LKPRD14              NO                                          
*                                                                               
         XC    WORK,WORK                                                        
         MVC   WORK(3),LPRDC                                                    
         GOTO1 BINSRCH,BSPPARS,(1,WORK)                                         
         OC    1(3,R1),1(R1)       TEST TABLE FULL                              
         BNZ   *+6                                                              
         DC    H'0'                TABLE FULL- FOR NOW DUMP                     
*                                                                               
         L     R5,0(R1)                                                         
         ST    R5,SVPLTENT         SAVE A(ENTRY)                                
         CLI   0(R1),1             TEST ENTRY FOUND                             
         BE    LKPRD14                                                          
*                                  YES- SET VALUES FROM TABLE                   
         USING PRTABD,R5                                                        
         MVC   HPRDNAM,PRTNAM                                                   
         MVC   HPRDCOD,PRTCOD                                                   
         TM    HPRDLKST,EZIHLPNF                                                
         BZ    LKPRD60                                                          
         MVI   EZRNFRPT,C'Y'       SET REPEAT SW FOR RNF'S                      
         MVI   EZERRCD,EZERRSCD                                                 
         B     LKPRD60                                                          
*                                                                               
LKPRD14  DS    0H                  NOT IN TABLE                                 
*                                  NOW FIND PRINTPAK NAME                       
         XC    KEY,KEY                                                          
         MVC   KEY+0(2),EZAGY                                                   
         MVC   KEY+2(1),EZMED                                                   
         MVI   KEY+3,X'06'                                                      
         MVC   KEY+4(3),EZIHLADC                                                
         MVC   KEY+7(3),LPRDC                                                   
         BAS   RE,HIGH                                                          
         CLC   KEY(13),KEYSAVE                                                  
         BE    LKPRD44                                                          
         CLC   LPRDC,=C'***'       FOR MULTIPLE PRD CODE                        
         BNE   LKPRD16                                                          
         MVC   HPRDNAM,=CL20'VARIOUS'                                           
         B     LKPRD60                                                          
*                                                                               
LKPRD16  DS    0H                                                               
         MVI   EZERRCD,EZERRSCD    IF NOT FOUND - HAVE PROBLEM                  
         OI    HPRDLKST,EZIHLPNF   SET STATUS BIT                               
         B     LKPRDX                                                           
*                                                                               
LKPRD44  DS    0H                                                               
         BAS   RE,GET                                                           
         L     RF,EZAREC                                                        
         MVC   HPRDNAM,PPRDNAME-PPRDREC(RF)                                     
         OC    HPRDNAM,SPACES                                                   
*                                                                               
LKPRD60  DS    0H                  SET SAVED DATA                               
         MVC   SPRDDATA,HPRDDATA                                                
*                                                                               
LKPRD66  OC    SVPLTENT,SVPLTENT   USING BINSRCH                                
         BZ    LKPRD68              NO                                          
         L     R5,SVPLTENT         A(TABLE ENTRY)                               
         USING PRTABD,R5                                                        
         MVC   PRTNAM,HPRDNAM                                                   
         MVC   PRTLKST,HPRDLKST                                                 
*                                                                               
LKPRD68  DS    0H                                                               
*                                                                               
LKPRDX   DS    0H                                                               
         CLI   EZERRCD,0           SET CC ON EXIT                               
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
***********************************************************************         
*        LKIORD - INSERTION ORDER DATA LOOKUP                                   
*                                                                               
*        NOTE-  MEDIA IS OPTIONAL                                               
*               HYPHENS ARE OTPIONAL                                            
*                                                                               
***********************************************************************         
*                                                                               
LKIORD   NTR1                                                                   
         MVI   EZERRCD,0                                                        
         TM    EZLOOKSW,EZLNOIOR    SKIP IO LOOKUP                              
         BNZ   LKIORDX                                                          
*                                                                               
         LA    R3,EZSPIORD                                                      
         CLI   EZSPIORD,C'0'       UNLESS NUMERIC (SO NO MEDIA)                 
         BH    LKI4                                                             
         CLC   EZSPIORD(1),EZSNMED DOES MEDIA AGREE?                            
         BE    *+12                                                             
         MVI   EZERRCD,EZERRION                                                 
         B     LKIORDX                                                          
         LA    R3,1(R3)            BUMP PAST MEIDA                              
*                                                                               
LKI4     DS    0H                                                               
         MVC   WORK(30),SPACES     STRIP OUT SPACES, HYPENS, ETC                
         MVC   WORK(L'EZSPIORD),0(R3)                                           
         LA    RF,WORK                                                          
         LA    RE,L'EZSPIORD                                                    
*                                                                               
LKI4D    DS    0H                                                               
         CLI   0(RF),C'-'                                                       
         BE    LKI4H                                                            
         CLI   0(RF),C' '                                                       
         BE    LKI4H                                                            
         CLI   0(RF),C'/'                                                       
         BE    LKI4H                                                            
         CLI   0(RF),C','                                                       
         BE    LKI4H                                                            
*                                                                               
         LA    RF,1(RF)                                                         
         BCT   RE,LKI4D                                                         
         B     LKI4J                                                            
*                                                                               
LKI4H    DS    0H                                                               
         MVC   0(L'EZSPIORD,RF),1(RF) SQEEZE LEFT                               
         BCT   RE,LKI4D                                                         
*                                                                               
LKI4J    DS    0H                  SHOULD NOW HAVE NINE DIGITS                  
         LA    RF,WORK             YMMDD PLUS NNNN                              
         LA    RE,9                                                             
*                                                                               
LKI4L    DS    0H                                                               
         CLI   0(RF),C'0'                                                       
         BL    LKI4N                                                            
         LA    RF,1(RF)                                                         
         BCT   RE,LKI4L                                                         
         B     LKI4P                                                            
*                                                                               
LKI4N    DS    0H                                                               
         MVI   EZERRCD,EZERRION      BAD IO NUMBER FORMAT                       
         B     LKIORDX                                                          
*                                                                               
LKI4P    DS    0H                                                               
         MVI   EDATE,C'9'          DATE                                         
         MVC   EDATE+1(5),WORK                                                  
         BAS   RE,CVTDAT2                                                       
         BNE   LKIORDX                                                          
         MVC   EZSPIODT,CDATE                                                   
*                                                                               
         OC    WORK+5(4),=4C'0'    IO NUM                                       
         PACK  DUB,WORK+5(4)                                                    
         CVB   R0,DUB                                                           
         STCM  R0,3,EZSPIONM                                                    
*                                                                               
LKI6     DS    0H                                                               
         XC    WORK,WORK           BUILD IO POINTER KEY                         
         MVC   WORK+00(2),EZAGY                                                 
         MVC   WORK+02(1),EZMED                                                 
         MVI   WORK+03,X'23'                                                    
         MVC   WORK+04(4),EZSNPUB    PUB                                        
         OC    WORK+8(2),EZIHLZON    ZONE/ED FROM INV HEADER                    
         BNZ   *+10                                                             
         MVC   WORK+8(2),EZSPLZON    OR INSERT DETAIL                           
         MVC   WORK+10(2),EZSPIODT   IO DATE                                    
         MVC   WORK+12(2),EZSPIONM   IO NUMBER                                  
*                                                                               
         CLC   WORK(14),SVIOKEY    TEST HAVE ALREADY TRIED                      
         BNE   LKI7                NO, READ FILE                                
         CLI   SVIOCLT,0           IF NO CLIENT, NEVER FOUND IO                 
         BE    LKI7B               IO NUM ERROR                                 
         B     LKI8                                                             
*                                                                               
LKI7     DS    0H                                                               
         OI    DMIBTS,X'08'        SET TO PASS DELETES                          
         MVI   DMOBTS,X'FD'                                                     
*                                                                               
         MVC   KEY(25),WORK                                                     
         BAS   RE,HIGH                                                          
         CLC   KEY(14),KEYSAVE   TEST THRU IO NUM                               
         BE    LKI7D                                                            
*                                  DIDN'T FIND IO FOR ZONE/ED                   
         CLI   EZEDNOPT,C'E'       IF EXACT MATCH REQUIRED                      
         BE    LKI7A               IO NOT FOUND                                 
*                                                                               
         XC    SVIOKEY,SVIOKEY     LOSE SAVED KEY                               
         MVC   KEY,KEYSAVE         READ THRU ALL                                
         XC    KEY+8(2),KEY+8      CLEAR ZONE/ED                                
         BAS   RE,HIGH                                                          
         B     LKI704B                                                          
*                                                                               
LKI704   DS    0H                                                               
         BAS   RE,SEQ                                                           
LKI704B  DS    0H                                                               
         CLC   KEY(8),KEYSAVE     TEST THRU BASE PUB                            
         BNE   LKI7A                                                            
*                                                                               
         CLI   EZEDNOPT,C'Z'       IF NEED EXACT MATCH OR EDN=0000              
         BNE   LKI704D                                                          
         OC    KEY+8(2),KEY+8      AND THIS IS NOT 0000                         
         BNZ   LKI7A               THEN IO NOT FOUND                            
*                                                                               
LKI704D  DS    0H                                                               
         CLC   KEY+10(4),KEYSAVE+10   TEST RIGHT IODATE/NUMBER                  
         BNE   LKI704                                                           
         MVC   SVIOCLT(6),KEY+14       SAVE CLT,PRD BUT NOT                     
         B     LKI8                    WHOLE KEY                                
*                                                                               
LKI7A    DS    0H                                                               
         XC    SVIOCLT(6),SVIOCLT   CLEAR CLT,PRD                               
LKI7B    DS    0H                                                               
         MVI   EZERRCD,EZERRINF    NOT FOUND                                    
         B     LKIORDX                                                          
LKI7D    DS    0H                                                               
         MVC   SVIOKEY(25),KEY    SAVE WHOLE KEY                                
*                                                                               
LKI8     DS    0H                                                               
         MVI   DMIBTS,X'00'        NO MORE DELETES                              
         MVI   DMOBTS,X'FF'                                                     
         CLI   EZIHLADC,C' '       IF HAVE ADV CODE                             
         BNH   LKI12                                                            
         TM    EZIHCVST,EZIHCOVR    AND NOT AN OVERRIDE                         
         BNZ   LKI13                                                            
         CLC   EZIHLADC,SVIOCLT     IO ADV MUST AGREE                           
         BE    LKI13                                                            
         MVI   EZERRCD,EZERRADV                                                 
         B     LKIORDX                                                          
*                                                                               
LKI12    DS    0H                  DIDN'T HAVE ADV YET                          
         MVC   LADVC,SVIOCLT       SO LOOK IT UP                                
         BAS   RE,LKADV                                                         
         BNE   LKIORDX                                                          
*                                                                               
         MVC   EZIHLADC,HADVCOD                                                 
         MVC   EZIHLADN,HADVNAM                                                 
         OC    EZIHLKST,HADVLKST                                                
*                                                                               
LKI13    DS    0H                                                               
**********************************************************************          
*        NOOP HEADER PRODUCT LOGIC HERE                                         
*        (LOOK AT FCSP7V)                                                       
*                                                                               
         B     LKI15                                                            
         MVC   LPRDC,SVIOPRD                                                    
         CLI   EZIHLPRC,C' '       ALREADY HAVE PRD AT INV LEVEL?               
         BNH   LKI14                                                            
         TM    EZIHCVST,EZIHCOVR    AND NOT AN OVERRIDE                         
         BNZ   LKI15                                                            
         CLC   EZIHLPRC,=C'***'    MULTIPLE PRODUCTS                            
         BE    LKI15                                                            
         CLC   EZIHLPRC,SVIOPRD                                                 
         BE    LKI15                                                            
         MVC   LPRDC,=C'***'                                                    
*                                                                               
LKI14    DS    0H                  DONT HAVE PRODUCT                            
         BAS   RE,LKPRD            SO LOOK IT UP                                
         BNE   LKIORDX                                                          
*                                                                               
         MVC   EZIHLPRC,HPRDCOD                                                 
         MVC   EZIHLPRN,HPRDNAM                                                 
         OC    EZIHLKST,HPRDLKST                                                
********************************************************************            
*                                                                               
LKI15    DS    0H                                                               
         CLI   EZSPLPRC,C' '       IF HAVE PROD CODE AT INSERT LEVEL            
         BNH   LKI16                                                            
         TM    EZSPCVST,EZSPCOVR    AND NOT AN OVERRIDE                         
         BNZ   LKI17                                                            
         CLC   EZSPLPRC,SVIOPRD      MUST AGREE                                 
         BE    LKI17                                                            
         MVI   EZERRCD,EZERRPRD                                                 
         B     LKIORDX                                                          
*                                                                               
LKI16    DS    0H                  DONT HAVE PRODUCT                            
         MVC   LPRDC,SVIOPRD       SO LOOK IT UP                                
         BAS   RE,LKPRD                                                         
         BNE   LKIORDX                                                          
*                                                                               
         MVC   EZSPLPRC,HPRDCOD                                                 
         MVC   EZSPLPRN,HPRDNAM                                                 
         OC    EZIHLKST,HPRDLKST                                                
*                                                                               
LKI17    DS    0H                                                               
*                                                                               
LKI90    DS    0H                                                               
LKIORDX  DS    0H                                                               
         B     LKI98                                                            
         DC    XL64'00'            PATCH                                        
LKI98    DS    0H                                                               
         CLI   EZERRCD,0           SET CC ON EXIT                               
         XIT1                                                                   
         SPACE 1                                                                
         DROP  R5                                                               
         EJECT                                                                  
***********************************************************************         
*        EDITION LOOKUP                                                         
***********************************************************************         
*                                                                               
LKEDTN   NTR1                                                                   
         MVI   EZERRCD,0                                                        
         XC    HEDTDATA,HEDTDATA                                                
         SR    RF,RF                                                            
         ICM   RF,7,EZSNETAB       A(EDITION TABLE FOR PUB)                     
         BZ    LKE5                MAY NOT BE THERE (ERROR)                     
         A     RF,RELO                                                          
         USING ETRTABD,RF                                                       
*                                                                               
LKE4     DS    0H                                                               
         CLI   0(RF),X'FF'         EOL                                          
         BNE   LKE6                                                             
LKE5     DS    0H                                                               
         MVI   EZERRCD,EZERRINV                                                 
         B     LKEDTNX                                                          
*                                                                               
LKE6     DS    0H                                                               
         CLC   LEDTN,ETRPZEDT      CODE                                         
         BNE   LKE8                                                             
         MVC   HEDTZON,ETRZON                                                   
         MVC   HEDTEDC,ETREDT                                                   
         B     LKEDTNX                                                          
*                                                                               
LKE8     DS    0H                                                               
         LA    RF,ETRTABL(RF)                                                   
         B     LKE4                                                             
*                                                                               
LKEDTNX  DS    0H                                                               
         CLI   EZERRCD,0           SET CC ON EXIT                               
         XIT1                                                                   
         DROP  RF                                                               
         SPACE 2                                                                
         LTORG                                                                  
         SPACE 2                                                                
PRTABD   DSECT                     PRODUCT TABLE DSECT                          
PRTCOD   DS    CL3                                                              
PRKEYL   EQU   *-PRTABD                                                         
*                                                                               
PRTNAM   DS    CL20                LOOKED UP NAME                               
PRTLKST  DS    XL1                 LOOK UP STATUS                               
PRTABDL  EQU   *-PRTABD                                                         
         EJECT                                                                  
EZMODD   DSECT                                                                  
RELO     DS    F                                                                
WORK     DS    XL64                                                             
X        DS    XL100                                                            
DUB      DS    D                                                                
DMCB     DS    6F                                                               
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
*                                                                               
SPACES   DS    CL132                                                            
PM1      DS    C                                                                
P        DS    CL132                                                            
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
*                                                                               
GETPROF  DS    A                                                                
NUMVAL   DS    A                                                                
CASHVAL  DS    A                                                                
DATVAL   DS    A                                                                
DATCON   DS    A                                                                
DATAMGR  DS    A                                                                
HEXOUT   DS    A                                                                
BINSRCH  DS    A                                                                
*                                                                               
AERRTAB  DS    A                                                                
AFLDCVT  DS    A                                                                
*                                                                               
CALLRD   DS    F                                                                
FLDHDR   DS    A                                                                
FLDFRST  DS    A                                                                
RECTYPE  DS    CL1                                                              
BADRCD   DS    CL1                                                              
NEWCMLSW DS    CL1                                                              
*                                                                               
SVCMTENT DS    A                                                                
SVPLTENT DS    A                                                                
*                                                                               
*                                  FIELDS SET BY GETFLD                         
ANXTPRD  DS    A                                                                
ANXTCML  DS    A                                                                
*                                                                               
RFDATA   DS    XL256                                                            
RFLEN    DS    XL1                                                              
*                                                                               
RDELIM   DS    CL1                 RECORD DELIMITER                             
FDELIM   DS    CL1                 FIELD DELIMITER                              
*                                                                               
ERRFLDD  DS    S                                                                
EDATE    DS    CL6                 YYMMDD                                       
DDATE    DS    CL8                 MMMDD/YY                                     
BDATE    DS    XL3                 BINARY 3                                     
CDATE    DS    XL2                 BINARY 2                                     
*                                                                               
HPRDDATA DS    0CL24               HOLD PRODUCT DATA                            
HPRDCOD  DS    CL3                                                              
HPRDNAM  DS    CL20                                                             
HPRDLKST DS    XL1                 LOOKUP STATUS                                
*                                                                               
HADVDATA DS    0CL24               HOLD ADVERTISER DATA                         
HADVCOD  DS    CL3                                                              
HADVNAM  DS    CL20                                                             
HADVLKST DS    XL1                 LOOKUP STATUS                                
*                                                                               
HEDTDATA DS    0XL2                HOLD EDITION DATA                            
HEDTZON  DS    XL1                 ZONE                                         
HEDTEDC  DS    XL1                 EDITION                                      
*                                                                               
WFSTATN  DS    CL5                 STATION FROM WKR FILE KEY                    
ELSEL    DS    XL1                                                              
*                                                                               
LADVC    DS    CL3                                                              
LPRDC    DS    CL3                                                              
LEDTN    DS    CL8                                                              
*                                                                               
         DS    0D                                                               
MPLKEY   DS    CL64                MPLDIR/FIL KEY                               
MPLKEYSV DS    CL64                                                             
SAVSYS   DS    XL1                                                              
DMIBTS   DS    XL1                                                              
DMOBTS   DS    XL1                                                              
SCTOPSW  DS    CL1                                                              
SCBOTSW  DS    CL1                                                              
SCTOPPTR DS    H                                                                
SCBOTPTR DS    H                                                                
NUMERRSW DS    CL1                                                              
NUMMAX   DS    F                                                                
*                                                                               
ENDWFREC DS    A                                                                
*                                                                               
         DS    0D                                                               
DMWORK   DS    XL96                                                             
EZMODDL  EQU   *-EZMODD                                                         
*                                                                               
         EJECT                                                                  
       ++INCLUDE PZBLOCK                                                        
*                                                                               
EZBLOCKD DSECT                                                                  
         ORG   EZWKA               SAVED WORK AREA                              
SAVWORK  DS    F                                                                
SVEZMODE DS    X                                                                
SKIPSW   DS    CL1                                                              
MPLOPEN  DS    CL1                                                              
SVWKRIND DS    XL24                                                             
*                                                                               
RFCNTL   DS    XL1                 X'80'=EOF, X'40'=EOR,                        
*                                  X'20'=FIELD LENGTH ERROR                     
*                                  X'10'=DO RANDOM READ                         
RFFLDNO  DS    XL1                                                              
RFCNTL2  DS    XL1                 (GETFLD INTERNAL CONTROL)                    
RFCURPOS DS    A                                                                
*                                                                               
RECSWS   DS    0CL20               RECORD SWITCHES                              
SNRSW    DS    CL1                                                              
PYRSW    DS    CL1                                                              
AGRSW    DS    CL1                                                              
STRSW    DS    CL1                                                              
SBRSW    DS    CL1                                                              
IHRSW    DS    CL1                                                              
ICRSW    DS    CL1                                                              
SPRSW    DS    CL1                                                              
RRRSW    DS    CL1                                                              
AARSW    DS    CL1                                                              
IBRSW    DS    CL1                                                              
ITRSW    DS    CL1                                                              
BTRSW    DS    CL1                                                              
         DS    CL7                                                              
*                                                                               
PASSNO   DS    XL1                 1 = SPECIAL 1ST PASS THRU DETS               
*                                  2 = 2ND PASS (ALSO IF ONLY ONE)              
SVIRNO   DS    XL2                 SAVED INVOICE HDR RECORD NUMBER              
BSPPARS  DS    6F                  BINSRCH PARS FOR PRD TAB                     
*                                                                               
SADVDATA DS    0CL24               ADV SAVE DATA                                
SADVCOD  DS    CL3                                                              
SADVNAM  DS    CL20                LOOKED UP NAME                               
SADVLKST DS    XL1                 STATUS                                       
*                                                                               
SPRDDATA DS    0CL24               PRODUCT SAVE AREA                            
SPRDCOD  DS    CL3                                                              
SPRDNAM  DS    CL20                LOOKED UP NAME                               
SPRDLKST DS    XL1                 STATUS                                       
*                                                                               
SVIOKEY  DS    XL32                                                             
         ORG   SVIOKEY+14                                                       
SVIOCLT  DS    CL3                                                              
SVIOPRD  DS    CL3                                                              
SVIHLPRC DS    CL3                                                              
         ORG                                                                    
         EJECT                                                                  
       ++INCLUDE PPGENPZ                                                        
*                                                                               
       ++INCLUDE DMWRKFK                                                        
*++INCLUDE DDCOMFACS/PCLTREC/PUBREC/PUBNAMEL/PPRDREC/DMWRKFD                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
       ++INCLUDE PCLTREC                                                        
       ++INCLUDE PUBREC                                                         
       ++INCLUDE PUBNAMEL                                                       
       ++INCLUDE PPRDREC                                                        
       ++INCLUDE DMWRKFD                                                        
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'127PZMOD     05/01/02'                                      
         END                                                                    
