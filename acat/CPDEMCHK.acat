*          DATA SET CPDEMCHK   AT LEVEL 035 AS OF 05/01/02                      
*CATALP CPDEMCHK                                                                
         TITLE 'DEMO CONVERT FOR CPP FILE '                                     
DEMCNV   CSECT                                                                  
         PRINT NOGEN                                                            
         NBASE 0,**CPDC**,=A(COPSAVE),R9,R8                                     
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         XC    SAVEKEY,SAVEKEY                                                  
         MVC   TITLE(34),=C'CPFILE DEMO/CONVERSION DIAGNOSTICS'                 
*              INITIALIZATION                                                   
         SPACE 3                                                                
         GOTO1 =V(STXITER),PARA,DUMPLIST                                        
         SPACE 2                                                                
         EJECT                                                                  
*              CONTROL READING OF TAPES                                         
         SPACE 3                                                                
INTIN    ZAP   LINE,=P'75'                                                      
         MVI   TAPEOPT,C'Y'        FORCE TAPE OUTPUT                            
         MVI   LOADOPT,C'N'                                                     
         EJECT                                                                  
*              ROUTINES TO READ INPUT MASTER FILES                              
         SPACE 3                                                                
MASTIN   L     R7,=A(OUTDATA)                                                   
         LA    R7,4(R7)                                                         
         USING CPKEYD,R7                                                        
         OPEN  (CPTIN,(INPUT))                                                  
         EJECT                                                                  
*              READ MASTER FILE, MERGE IN SORT, WRITE NEW MASTER                
         SPACE 3                                                                
MAST     CLI   TAPEOPT,C'Y'        OPEN TAPE IF NEEDED                          
         BNE   MAST2                                                            
         OPEN  (CPTOUT,(OUTPUT))                                                
         SPACE 1                                                                
MAST2    CLI   LOADOPT,C'Y'        AND DISK                                     
         BNE   MAST4                                                            
         GOTO1 =V(ISDDS),PARA,V(ISOPEN),,,CPFILE,0,0                            
         SPACE 2                                                                
MAST4    L     R6,=A(INDATA)                                                    
         L     R7,=A(OUTDATA)                                                   
         BAS   RE,GETMAST                                                       
         SPACE 1                                                                
MASTEOF  MVC   P(17),=C'INPUT MASTER FILE'                                      
         MVC   0(32,R7),EOFREC                                                  
         EDIT  (P4,INTRECS),(7,P+41)                                            
         GOTO1 =V(PRINTER)                                                      
         MVC   P(18),=C'OUTPUT MASTER FILE'                                     
         EDIT  (P4,OUTRECS),(7,P+41)                                            
         GOTO1 =V(PRINTER)                                                      
         MVC   P(18),=C'DUPLICATES ON FILE'                                     
         EDIT  (P4,DUPCNT),(7,P+41)                                             
         GOTO1 =V(PRINTER)                                                      
         CLI   TAPEOPT,C'Y'                                                     
         BNE   MASTEOF2                                                         
         CLOSE (CPTOUT,)                                                        
         SPACE 1                                                                
MASTEOF2 DS    0H'0'                                                            
         XBASE                                                                  
         EJECT                                                                  
*              ROUTINES TO GET RECORDS FROM MASTER FILE                         
         SPACE 3                                                                
GETMAST  NTR1                                                                   
         SPACE 1                                                                
GETMASTB L     R2,=A(OUTDATA)                                                   
         GET   CPTIN,(R2)                                                       
         AP    INTRECS,=P'1'                                                    
         LA    R7,4(R2)                                                         
         SPACE 1                                                                
CHKKEY   OC    SAVEKEY,SAVEKEY                                                  
         BNZ   *+10                                                             
         MVC   SAVEKEY,0(R7)                                                    
         CLC   0(16,R7),SAVEKEY                                                 
         BNH   *+14                                                             
         MVC   SAVEKEY,0(R7)                                                    
         B     GETMASTB                                                         
         SPACE 1                                                                
         AP    DUPCNT,=P'1'                                                     
         LA    R2,16                                                            
         GOTO1 =V(PRNTBL),PARA,0,(R7),C'DUMP',(R2),=C'2D'                       
         B     GETMASTB                                                         
         SPACE 2                                                                
CPTEOF   MVI   4(R7),X'FF'                                                      
         MVC   5(15,R7),4(R7)                                                   
         B     XIT                                                              
         EJECT                                                                  
*              ROUTINE TO PUT SORT RECORDS                                      
PUTSORT  NTR1                                                                   
         GOTO1 =V(SORTER),PARA,=C'PUT',(R2)                                     
         XIT1                                                                   
         SPACE 3                                                                
*              ROUTINES TO GET SORT RECORDS                                     
         SPACE 3                                                                
GETSORT  NTR1                                                                   
         L     R6,=A(SORTDATA)                                                  
         L     R7,=A(INDATA)                                                    
         SPACE 1                                                                
GS4      GOTO1 =V(SORTER),PARA,=C'GET'                                          
         L     R2,PARA+4                                                        
         LTR   R2,R2                                                            
         BNZ   GS6                                                              
         SPACE 1                                                                
GS5      MVI   4(R7),X'FF'                                                      
         MVC   5(15,R7),4(R7)                                                   
         B     XIT                                                              
         SPACE 1                                                                
GS6      MOVE  ((R7),1000),(R2)                                                 
         BAS   RE,PUTMAST          SORT RECORDS EQUAL - ADD TOGETHER            
         B     GS4                                                              
         EJECT                                                                  
*              ROUTINES TO WRITE RECORDS                                        
         SPACE 3                                                                
PUTMAST  NTR1                                                                   
         OC    0(20,R7),0(R7)                                                   
         BZ    XIT                                                              
         LH    R2,20(R7)                                                        
         LA    R2,4(R2)                                                         
         STH   R2,0(R7)                                                         
         AP    OUTRECS,=P'1'                                                    
         CP    TRACEOPT,=P'0'                                                   
         BE    PUTMAST2                                                         
         SP    TRACEOPT,=P'1'                                                   
         GOTO1 =V(PRNTBL),PARA,0,(R7),C'DUMP',(R2),=C'2D'                       
         SPACE 2                                                                
PUTMAST2 CLI   TAPEOPT,C'Y'                                                     
         BNE   PUTMAST4                                                         
         PUT   CPTOUT,(R7)                                                      
         SPACE 2                                                                
PUTMAST4 CLI   LOADOPT,C'Y'        LOADING                                      
         BNE   XIT                                                              
         LA    R2,4(R7)            A(REC)                                       
         LH    R3,0(R7)                                                         
         SH    R3,=H'4'            LENGTH                                       
         LA    R4,CPFILE           A(DTF)                                       
         XC    DMCB(24),DMCB                                                    
         STM   R2,R4,DMCB+4                                                     
         GOTO1 =V(IVLDDS),DMCB                                                  
         B     XIT                                                              
         SPACE 2                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
       ++INCLUDE DEEQUIVOLD                                                     
*              DTFS FOR PROGRAM                                                 
         SPACE 3                                                                
CPTIN    DCB   DDNAME=CPTIN,           DOS SYS006                      X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=08196,                                            X        
               BLKSIZE=08200,          DOS BLKSIZE=08200               X        
               MACRF=GM,                                               X        
               EODAD=CPTEOF                                                     
         SPACE 2                                                                
CPTOUT   DCB   DDNAME=CPTOUT,          DOS SYS007                      X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               LRECL=08196,                                            X        
               BLKSIZE=08200,          DOS BLKSIZE=08200               X        
               MACRF=PM                                                         
         SPACE 2                                                                
CPFILE   DMIS  KEYLEN=16,RECSIZE=V,BLKSIZE=3024,                       X        
               SPARE=0,INDSIZE=500,DEVICE=3350                                  
         EJECT                                                                  
*              WORK SPACE AND CONSTANTS                                         
         SPACE 3                                                                
DUB      DS    D                                                                
WORK     DS    CL64                                                             
PARA     DS    6F                                                               
DMCB     DS    6F                                                               
C        DS    CL80                                                             
ELEMENT  DS    CL255                                                            
INPUTOPT DC    C'Y'                                                             
TRACEOPT DC    PL4'100'                                                         
LOADOPT  DC    C'Y'                                                             
BANKOPT  DC    C'N'                                                             
BANKSW   DC    AL1(0)                                                           
ANYSORT  DC    C'N'                                                             
DUMPOPT  DC    C'N'                                                             
TAPEOPT  DC    C'Y'                                                             
MESSA    DC    C'ANY INTERFACE TAPES? ENTER YES OR NO'                          
MESSB    DC    C'ANY MORE INTERFACE TAPES? ENTER YES OR NO'                     
SORTCARD DC    CL80'SORT FIELDS=(5,16,A),FORMAT=BI,WORK=1 '                     
RECCARD  DC    CL80'RECORD TYPE=V,LENGTH=(1000,,,24,100)  '                     
DUMPLIST DS    0F                                                               
         DC    A(DEMCNV)                                                        
         DC    V(ADDAY)                                                         
         ORG   *-4                                                              
         DC    X'80'                                                            
         DS    0F                                                               
INTRECS  DC    PL4'0'                                                           
OUTRECS  DC    PL4'0'                                                           
DUPCNT   DC    PL4'0'                                                           
SAVAGY   DC    CL2'  '                                                          
SAVEKEY  DS    CL16                                                             
OLDELS   DC    X'000B00'                                                        
DATADISP DC    H'19'                                                            
EOFREC   DC    H'24'                                                            
         DC    H'0'                                                             
         DC    16X'FF'                                                          
         DC    H'20'                                                            
         DC    X'0000'                                                          
         SPACE 1                                                                
         LTORG                                                                  
         EJECT                                                                  
*              STORAGE AREA FOR PROGRAM                                         
         SPACE 1                                                                
         ENTRY COPSAVE                                                          
         ENTRY REQPOOL                                                          
         ENTRY INDATA                                                           
         ENTRY SORTDATA                                                         
         ENTRY BANKPOOL                                                         
         ENTRY OUTDATA                                                          
         ENTRY SORTAREA                                                         
         ENTRY PHASE                                                            
         SPACE 1                                                                
         DS    0D                                                               
         DC    C'***SAVE STORAGE*'                                              
COPSAVE  DC    5000X'00'                                                        
         SPACE 1                                                                
         DC    C'**REQUEST POOL**'                                              
REQPOOL  DC    F'0'                                                             
         DC    F'200'                                                           
         DC    4000X'00'                                                        
         SPACE 1                                                                
         DC    C'* INPUT DATA ***'                                              
INDATA   DC    1008X'00'                                                        
         SPACE 1                                                                
         DC    C'** SORT DATA ***'                                              
SORTDATA DC    1008X'00'                                                        
         SPACE 1                                                                
         DC    C'*** BANK POOL **'                                              
BANKPOOL DC    256X'00'                                                         
         DC    C'* OUTPUT DATA **'                                              
OUTDATA  DC    1040X'00'                                                        
         SPACE 1                                                                
         DC    C'* SORT AREA ****'                                              
SORTAREA DS    42000C                                                           
         SPACE 2                                                                
         DC    C'**** PHASE *****'                                              
PHASE    BR    14                                                               
         DS    2000C                                                            
         EJECT                                                                  
*              DSECT TO COVER REQUEST POOL ITEM                                 
         SPACE 3                                                                
REQPOOLD DSECT                                                                  
RQPAGY   DS    CL2                                                              
RQPSTART DS    CL6                                                              
RQPEND   DS    CL6                                                              
RQPSTB   DS    CL3                                                              
RQPNDB   DS    CL3                                                              
         SPACE 3                                                                
       ++INCLUDE DMDTFIS                                                        
         SPACE 3                                                                
       ++INCLUDE CPGENINTER                                                     
         EJECT                                                                  
       ++INCLUDE CPGENFILE                                                      
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035CPDEMCHK  05/01/02'                                      
         END                                                                    
