*          DATA SET ACLDRCVD   AT LEVEL 001 AS OF 06/28/10                      
*CATALP ACLDRCVD                                                                
         TITLE 'LDRCVD - ACCOUNT - DIRECTORY RECOVERY DUMP ROUTINE'             
                                                                                
* PARAMS VIA R1                                                                 
* XL1    X'00'=INIT,X'01'=PROCESS,X'FF'=FINAL.                                  
*        RETURN X'FF'=DONT WRITE RECORD TO DUMP TAPE                            
*        RETURN X'FE'=DELETED REC,X'FD'=RESTORED REC                            
* AL3    A(RECOVERY FILE RECORD)                                                
                                                                                
         PRINT NOGEN                                                            
LDRCVD   CSECT                                                                  
         ENTRY LDRCVDT             TABLE OF COUNTERS AND ACTIONS                
                                                                                
         NMOD1 WORKX-WORKD,ACLDRCVD                                             
         USING WORKD,RC                                                         
         LR    RA,R1               RA=A(PARAMETER LIST)                         
         ST    R1,APARM                                                         
         SR    R3,R3                                                            
         ICM   R3,7,1(R1)          R3=A(RECOVERY HEADER)                        
         USING RCVRECD,R3                                                       
         CLI   0(R1),X'01'         TEST ACTION                                  
         BL    INIT                                                             
         BE    PROCESS                                                          
         BH    FINAL                                                            
                                                                                
INIT     LA    RE,LDRCVDT          CLEAR COUNTERS ON INIT CALL                  
INIT1    CLC   0(4,RE),=4X'FF'                                                  
         BE    EXIT                                                             
         XC    0(4,RE),0(RE)                                                    
         LA    RE,L'LDRCVDT(RE)                                                 
         B     INIT1                                                            
                                                                                
FINAL    B     EXIT                FINAL CALL                                   
                                                                                
LGCLDEL  MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     EXIT                                                             
                                                                                
LGCLRST  MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     EXIT                                                             
                                                                                
DELETE   MVI   0(RA),X'FF'         RETURN DELETE RECORD FROM DUMP TAPE          
                                                                                
EXIT     XMOD1 1                                                                
                                                                                
         EJECT                                                                  
* PROCESS A RECOVERY FILE RECORD                                                
* UPDATE TABLE OF ANY ACTIONS THAT NEED PRINTING AND ERRORS/WARNINGS            
* EXIT VIA DELETE TO REQUEST RECORD NOT WRITTEN TO RECOVERY DUMP TAPE           
                                                                                
PROCESS  CLI   RFILTY,ACCMSTQ      ACCMST RECOVERY RECORD                       
         BE    MST1                                                             
         CLI   RFILTY,ACCARCQ      ACCARC RECOVERY RECORD                       
         BE    ARC1                                                             
         CLI   RFILTY,ACCDIRQ      ACCDIR RECOVERY RECORD                       
         BNE   EXIT                                                             
                                                                                
DIR1     CLI   RRECTY,2            TEST ACCDIR CHANGE                           
         BNE   SAVE                                                             
         TM    KEYSTAT,X'80'       TEST DELETED                                 
         BZ    DIR2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),ACCDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD ACCDIR COPY                 
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRD          BUMP ACCDIR DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVDIRD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
DIR2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),ACCDIRQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    KEYSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   KEYKEY,KEYKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVDIRR          BUMP ACCDIR RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVDIRR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
MST1     CLI   RRECTY,2            TEST ACCMST CHANGE                           
         BNE   SAVE                                                             
         TM    RECSTAT,X'80'       TEST DELETED                                 
         BZ    MST2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),ACCMSTQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD ACCMST COPY                 
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVMSTD          BUMP ACCMST DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVMSTD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
MST2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),ACCMSTQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVMSTR          BUMP ACCMST RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVMSTR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
ARC1     CLI   RRECTY,2            TEST ACCARC CHANGE                           
         BNE   SAVE                                                             
         TM    RECSTAT,X'80'       TEST DELETED                                 
         BZ    ARC2                                                             
         L     RF,=A(LASTRCV)                                                   
         CLI   RFILTY-RCVHDR(RF),ACCARCQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1 TEST LAST RECORD ACCARC COPY                 
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BO    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVARCD          BUMP ACCARC DELETED RECORDS                  
         AHI   R0,1                                                             
         ST    R0,RCVARCD                                                       
         MVI   0(RA),X'FE'         RETURN RECORD IS LOGICALLY DELETED           
         B     SAVE                                                             
                                                                                
ARC2     L     RF,=A(LASTRCV)      TEST IF COPY RECORD WAS ACTIVE               
         CLI   RFILTY-RCVHDR(RF),ACCARCQ                                        
         BNE   SAVE                                                             
         CLI   RRECTY-RCVHDR(RF),1                                              
         BNE   SAVE                                                             
         TM    RECSTAT-RCVHDR(RF),X'80'                                         
         BZ    SAVE                                                             
         CLC   RECKEY,RECKEY-RCVHDR(RF)                                         
         BNE   SAVE                                                             
         L     R0,RCVARCR          BUMP ACCARC RESTORED RECORDS                 
         AHI   R0,1                                                             
         ST    R0,RCVARCR                                                       
         MVI   0(RA),X'FD'         RETURN RECORD IS LOGICALLY RESTORED          
         B     SAVE                                                             
                                                                                
SAVE     L     R0,=A(LASTRCV)      SAVE RECOVERY RECORD                         
         LHI   R1,LASTRCVL         R1=MAXIMUM RECORD LENGTH                     
         LR    RE,R3                                                            
         AHI   RE,-4                                                            
         LH    RE,0(RE)                                                         
         AHI   RE,-4               RE=ACTUAL RECORD LENGTH                      
         BM    SAVE1                                                            
         CR    RE,R1                                                            
         BNL   SAVE1                                                            
         LR    R1,RE                                                            
SAVE1    LR    RE,R3                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         B     EXIT                                                             
                                                                                
         LTORG                                                                  
                                                                                
* THIS ENTRY POINT TABLE IS PROCESSED BY RCVDMP CALLER AFTER FINAL CALL         
* EACH ENTRY HAS A COUNTER,TEXT,TYPE,AND SPARE                                  
* ENTRIES WITH A NON-ZERO COUNTER WILL BE PRINTED                               
* ERRORS/WARNINGS WILL GO TO CONSOLE AND WILL BE EMAILED                        
         DS    0F                                                               
LDRCVDT  DS    0CL24                                                            
RCVDIRD  DC    F'0',CL16'ACCDIR DELETES  ',C'I ',AL2(0)                         
RCVDIRR  DC    F'0',CL16'ACCDIR RESTORES ',C'I ',AL2(0)                         
RCVMSTD  DC    F'0',CL16'ACCMST DELETES  ',C'I ',AL2(0)                         
RCVMSTR  DC    F'0',CL16'ACCMST RESTORES ',C'I ',AL2(0)                         
RCVARCD  DC    F'0',CL16'ACCARC DELETES  ',C'I ',AL2(0)                         
RCVARCR  DC    F'0',CL16'ACCARC RESTORES ',C'I ',AL2(0)                         
RCVERR   DC    F'0',CL16'ACC ERRORS      ',C'E ',AL2(0)                         
RCVWARN  DC    F'0',CL16'ACC WARNINGS    ',C'W ',AL2(0)                         
LDRCVDTX DC    4X'FF'                                                           
                                                                                
LASTRCV  DC    2048X'00'                                                        
LASTRCVL EQU   *-LASTRCV                                                        
                                                                                
ACCDIRQ  EQU   X'69'                                                            
ACCMSTQ  EQU   X'6A'                                                            
ACCARCQ  EQU   X'6B'                                                            
                                                                                
* DSECT TO LOCAL WORKING STORAGE                                                
*                                                                               
WORKD    DSECT                                                                  
APARM    DS    A                                                                
WORKX    EQU   *                                                                
                                                                                
* DSECT TO COVER RECOVERY HEADER                                                
*                                                                               
RCVRECD  DSECT                                                                  
RCVHDR   DS    0XL24                                                            
*DMRCVRHDR                         RECOVERY HEADER                              
       ++INCLUDE DMRCVRHDR                                                      
                                                                                
RECREC   DS    0X                  RECORD                                       
RECKEY   DS    XL42                RECORD KEY                                   
RECLEN   DS    XL2                 RECORD LENGTH                                
RECSTAT  DS    XL8                 RECORD STATUS                                
RECDATA  DS    0X                  RECORD DATA                                  
                                                                                
         ORG   RECREC                                                           
KEYREC   DS    0XL54               KEY RECORD                                   
KEYKEY   DS    XL42                KEY KEY                                      
KEYSTAT  DS    XL8                 KEY STATUS                                   
KEYADDR  DS    XL4                 KEY DISK ADDRESS                             
                                                                                
*DMRCVREXT                         RECOVERY TRAILER                             
       ++INCLUDE DMRCVREXT                                                      
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'001ACLDRCVD  06/28/10'                                      
         END                                                                    
