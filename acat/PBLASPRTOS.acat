*          DATA SET PBLASPRTOS AT LEVEL 014 AS OF 05/01/02                      
*CATALP LASPRINT                                                                
         PRINT GEN                                                              
LASPRINT CSECT                                                                  
         NBASE 0,**LASP**,=V(REGSAVE),R9,R8                                     
         L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         USING POOLFORM,R2                                                      
         USING LASRTNES,R5                                                      
         SPACE 1                                                                
*                                                                               
*  PROGRAM FORMATS TEXT STORED IN PANVALET FOR IBM6670                          
*  PRINTER.  TEXT IS FORMATTED IN STANDARD BOOK FORMAT                          
*  FOR PRINTING OF FINISHED MANUALS ON STD COPY PAPER.                          
*  PROGRAM HONORS SPECIAL CONTROL LINES IN TEXT TO PROVIDE                      
*  FRONT MATTER, CHAPTERS, PAGE NUMBERING, FIGURES, SUBHEADINGS,                
*  ETC.  USER MAY ALSO SPECIFY START PAGE, RUNNING TEXT, REVISION               
*  DATES TO PRINT PAN BOOK AS REPLACEMENT PAGES FOR AN                          
*  EXISTING DOCUMENT.                                                           
*                                                                               
*  PROGRAM MAKES TWO PASSES THROUGH TEXT -- FIRST PASS TO                       
*  COMPILE CONTENTS/INDEX, SECOND PASS TO PRINT.                                
         EJECT                                                                  
*                   READ RUN-DECK CONTROL CARD(S)                               
         SPACE 1                                                                
CARD     GOTO1 =V(CARDS),PARA,C,=C'RE00'                                        
         CLC   C(2),=C'/*'                                                      
         BNE   CARD0                                                            
         SPACE 1                                                                
         CLI   PANOPEN,C'Y'                                                     
         BNE   CLOSEPR                                                          
         GOTO1 =V(PANIC),PARA,=C'CLOSE'                                         
CLOSEPR  GOTO1 =V(PRINT),PARA,=C'CLOSE'                                         
         XBASE                     CHANGED FROM EOJ                             
         SPACE 1                                                                
CARD0    NOP   CARD1                                                            
         OI    *-3,X'F0'                                                        
         SPACE 1                                                                
         MVC   TITLE,SPACES                                                     
         MVI   MODE,X'08'                                                       
         MVC   P,SPACES                                                         
         MVC   POOLLINE,=H'0'                                                   
         L     R5,=A(LASRTNES)                                                  
         MVI   SUPERSW,X'20'       RESET FOR TITLE                              
         NI    FIRSTSW,X'0F'                                                    
         MVC   REVDATE(16),SPACES  COVERS (PRINTER) FLAGS                       
         MVC   CHOSEN(22),SPACES   INCLUDES MOST FLAGS                          
         MVI   XERTEXT,C' '                                                     
         MVI   TWOLINE,C'Y'                                                     
         MVI   FILTERS,X'00'                                                    
         MVI   WPOFFSET,X'00'                                                   
         ZAP   PAGE,=P'1'                                                       
         ZAP   FIGNO,=P'0'                                                      
         ZAP   LINE,=P'0'                                                       
         MVC   P(16),=C',PRI 105_SUM 1 0'                                       
         CLI   ONESIDE,C'Y'                                                     
         BE    *+10                                                             
         MVC   P+16(6),=C'_DUP 1'                                               
         GOTO1 =V(PRINT),PARA1,P-1,=C'BL01'                                     
         TITLE 'PRINT MANUAL(S) ON 6670'                                        
CARD1    CLC   C(7),=C'ONESIDE'                                                 
         BNE   CARD2                                                            
         MVI   ONESIDE,C'Y'                                                     
         MVC   P,SPACES                                                         
         MVC   P(5),=C'DUP 0'                                                   
         GOTO1 =V(PRINT),PARA1                                                  
         B     CARD                                                             
         SPACE 1                                                                
CARDERR  MVC   P,SPACES                                                         
         MVC   P(9),=C'-CARDERR '                                               
         MVC   P+9(30),C                                                        
         GOTO1 =V(PRINT),PARA1                                                  
         B     CARD                                                             
         SPACE 1                                                                
CARD2    CLC   C(8),=C'CONTLIM='                                                
         BNE   CARD3                                                            
         MVC   CONTLIM,C+8                                                      
         B     CARD                                                             
         SPACE 1                                                                
CARD3    CLC   C(8),=C'FIGNAME='                                                
         BNE   CARD4                                                            
         CLC   C+8(8),SPACES                                                    
         BE    CARDERR                                                          
         MVC   FIGNAME(7),C+8                                                   
         LA    R1,FIGNAME+1                                                     
         LA    R2,FIGNAME-1                                                     
         NI    0(R1),X'BF'         UNCAP IT                                     
         CLI   1(R1),C' '                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     *-16                                                             
         SR    R1,R2                                                            
         STH   R1,LOFFIGNM                                                      
         B     CARD                                                             
         SPACE 1                                                                
CARD4    CLC   C(8),=C'BOOKEND='                                                
         BNE   CARD5                                                            
         CLC   C+8(10),SPACES                                                   
         BE    CARDERR                                                          
         GOTO1 =V(PANIC),PARA,=C'READ',=C'DIRECTORY',C+8,SAVEDIR                
         MVI   PANOPEN,C'Y'                                                     
         TM    PARA+8,X'10'                                                     
         BO    NOBOOK                                                           
         MVC   ENDPGNAM,C+8                                                     
         B     CARD                                                             
         EJECT                                                                  
CARD5    CLC   C(6),=C'ISSUE='        OMIT ALL BUT SELECTED CHAPTER(S)          
         BNE   CARD6                                                            
         MVC   CHOSEN,C+6                                                       
         MVC   REVDATE,C+10                                                     
         B     CARD                                                             
         SPACE 1                                                                
CARD6    CLC   C(9),=C'CHAPNAME='     REPLACE THE WORD 'CHAPTER'                
         BNE   CARD7                                                            
         SPACE 1                                                                
         CLC   C+9(8),SPACES                                                    
         BE    CARDERR                                                          
         MVC   SECTNAME(7),C+9                                                  
         LA    R1,SECTNAME+1                                                    
         LA    R2,SECTNAME-1                                                    
         NI    0(R1),X'BF'         UNCAP IT                                     
         CLI   1(R1),C' '                                                       
         BE    *+12                                                             
         LA    R1,1(R1)                                                         
         B     *-16                                                             
         SR    R1,R2                                                            
         STH   R1,LOFCHPNM                                                      
         B     CARD                                                             
         SPACE 1                                                                
CARD7    CLC   C(9),=C'WIDELINE='     BASIS FOR CENTERING WIDE PGS              
         BNE   CARD8                                                            
         SPACE 1                                                                
         CLC   C+9(3),=C'106'                                                   
         BNE   *+12                                                             
         MVI   WPOFFSET,X'0D'                                                   
         B     CARD                                                             
         SPACE 1                                                                
         CLC   C+9(3),=C'120'                                                   
         BNE   CARDERR                                                          
         SPACE 1                                                                
         MVI   WPOFFSET,X'06'                                                   
         B     CARD                                                             
         EJECT                                                                  
CARD8    CLC   C(6),=C'UPDATE'       REPLACEMENT PAGES                          
         BNE   CARD9                                                            
         SPACE 1                                                                
         MVI   BODYONLY,C'Y'                                                    
         B     CARD                                                             
         SPACE 1                                                                
CARD9    CLC   C(9),=C'ADDRLIST='    PRINT ADDRESSED COPIES USING               
         BNE   CARD10                THIS ADDRESS LIST                          
         SPACE 1                                                                
         MVC   ADLISTNM(10),C+9                                                 
         MVC   C+12(8),C                                                        
         MVC   C(12),ADLISTNM                                                   
         GOTO1 =V(PANIC),PARA,=C'READ',=C'DIRECTORY',C,SAVEDIR                  
         MVI   PANOPEN,C'Y'                                                     
         TM    PARA+8,X'10'                                                     
         BO    NOBOOK                                                           
         SPACE 1                                                                
         MVI   ADDRESS,C'Y'                                                     
         MVI   BODYONLY,C'Y'                                                    
         CLI   C+20,C' '           NO POSITION CODE                             
         BNE   *+8                                                              
         MVI   C+20,C'B'             YES - SET BOTTOM AS DEFAULT                
         MVC   ADDRPOSN,C+20       CC21                                         
         SPACE 1                                                                
         CLI   C+21,C'5'           FIVE-LINE ADDRESS                            
         BNE   CARD                  NO                                         
         MVI   TWOLINE,C'N'          YES                                        
         B     CARD                                                             
         SPACE 1                                                                
CARD10   CLC   C(2),=C'F='                                                      
         BNE   BOOKTEST                                                         
         SPACE 1                                                                
         CLI   ADDRESS,C'Y'        ADDRESS LIST SET                             
         BNE   CARD                  NO - IGNORE FILTERS                        
         SPACE 1                                                                
         MVC   FILTERS,C+2                                                      
         B     CARD                                                             
         EJECT                                                                  
*                  LOOK FOR BOOKNAME IN DIRECTORY                               
         SPACE 1                                                                
BOOKTEST TRT   C(11),TERMTRT       BLANK OR ,                                   
         BZ    NOBOOK                                                           
         CLI   0(R1),C','          KEYWORD FORMAT                               
         BNE   *+20                  NO                                         
         MVI   0(R1),C' '            YES - BLANK OUT ,                          
         LA    R3,1(R1)                                                         
         ST    R3,SAVEKEY                                                       
         MVI   SAVEKEY,C'Y'                                                     
         GOTO1 =V(PANIC),PARA,=C'READ',=C'DIRECTORY',C,SAVEDIR                  
         MVI   PANOPEN,C'Y'                                                     
         TM    PARA+8,X'10'                                                     
         BZ    OPTIONS                                                          
         SPACE 1                                                                
NOBOOK   MVC   P(16),=C'-BOOK NOT FOUND '                                       
         MVC   P+16(60),C                                                       
OTHERERR GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         MVC   P(5),=C',SEP '                                                   
         GOTO1 =V(PRINT),PARA1                                                  
         NI    CARD0+1,X'0F'                                                    
         B     CARD                                                             
         SPACE 1                                                                
OPTIONS  MVC   P,SPACES                                                         
         CLI   SAVEKEY,C'Y'                                                     
         BE    KWOPTION                                                         
         CLI   ADDRESS,C'Y'        MERGE W/ADDRESS LIST                         
         BE    DESTMSG+6             YES - IGNORE OPTIONS                       
         SPACE 1                                                                
         CLC   C+11(3),=C'ORI'                                                  
         BNE   OPTIONQ                                                          
         SPACE 1                                                                
         MVC   P(11),=C'DUP 0_QUA 2'                                            
         GOTO1 =V(PRINT),PARA1                                                  
         B     DESTMSG                                                          
         SPACE 1                                                                
OPTIONQ  CLC   C+11(4),=C'QUA '                                                 
         BNE   DESTMSG                                                          
         SPACE 1                                                                
         LA    R4,C+15                                                          
         TM    0(R4),X'F0'         NUMERIC                                      
         BNO   DESTMSG               NO                                         
         CLI   1(R4),C' '            YES - 2ND DIGIT                            
         BE    *+16                          NO                                 
         TM    1(R4),X'F0'                   YES - NUMERIC                      
         BO    *+8                                   YES                        
         MVI   1(R4),C' '                            NO  - BLANK IT             
         SPACE 1                                                                
         MVC   P(6),C+11                                                        
         GOTO1 =V(PRINT),PARA1                                                  
         EJECT                                                                  
DESTMSG  MVC   P,SPACES                                                         
         MVI   P,C'-'                                                           
         MVC   P+1(40),C+19                                                     
         GOTO1 =V(PRINT),PARA1                                                  
         B     BOOKMSG                                                          
         SPACE 1                                                                
KWOPTION L     R3,SAVEKEY                                                       
KWOPTLP  TRT   0(10,R3),INTERTRT   - OR =                                       
         BZ    BOOKMSG             NO OPTIONS                                   
         CLC   0(2,R3),=C'DE'      DEST=                                        
         BE    KWDEST                                                           
         CLC   0(2,R3),=C'OP'      OPT=                                         
         BNE   NEXTKOPT                                                         
         CLI   ADDRESS,C'Y'                                                     
         BE    NEXTKOPT            SKIP ORI OR QUA - N/A W/ADDRESSING           
         CLC   1(3,R1),=C'ORI'                                                  
         BNE   TRYQUA                                                           
         MVC   P(11),=C'DUP 0_QUA 2'                                            
         GOTO1 =V(PRINT),PARA1                                                  
         B     NEXTKOPT                                                         
         SPACE 1                                                                
TRYQUA   TM    1(R1),X'F0'         NUMERIC                                      
         BNO   NEXTKOPT              NO                                         
         MVC   P(4),=C'QUA '                                                    
         MVC   P+4(1),1(R1)                                                     
         TM    2(R1),X'F0'                                                      
         BNO   PUTQUA                                                           
         MVC   P+5(1),2(R1)                                                     
PUTQUA   GOTO1 =V(PRINT),PARA1                                                  
         SPACE 1                                                                
NEXTKOPT TRT   0(15,R3),TERMTRT                                                 
         BZ    BOOKMSG                                                          
         CLI   0(R1),C' '                                                       
         BE    BOOKMSG                                                          
         LA    R3,1(R1)                                                         
         B     KWOPTLP                                                          
         SPACE 1                                                                
KWDEST   MVC   P,SPACES                                                         
         MVI   P,C'-'                                                           
         MVC   P+1(40),1(R1)                                                    
         GOTO1 =V(PRINT),PARA1                                                  
         EJECT                                                                  
BOOKMSG  CLI   SAVEKEY,C'Y'                                                     
         BNE   BOOKMSGO                                                         
         MVI   SAVEKEY,X'00'                                                    
         L     R3,SAVEKEY                                                       
         MVC   0(9,R3),SPACES                                                   
BOOKMSGO MVC   P(12),=C'-BOOK NAME: '                                           
         MVC   P+12(10),SAVEDIR                                                 
         MVC   P+22(7),=C' LEVEL '                                              
         MVC   P+29(3),SAVEDIR+10                                               
         MVC   P+32(7),=C' AS OF '                                              
         MVC   P+39(8),SAVEDIR+26                                               
         MVC   P+47(8),=C' RUN ON '                                             
         GOTO1 =V(DATCON),PARA3,(5,0),(10,PARA3+16)                             
         LA    R1,PARA3+16                                                      
         MVC   P+55(8),0(R1)                                                    
         MVC   SAVEYR,6(R1)                                                     
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         CLI   ADDRESS,C'Y'                                                     
         BNE   DEFCODE                                                          
         SPACE 1                                                                
         MVC   P(15),=C'TAB 40 58_MERGE'                                        
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         SPACE 1                                                                
*   DEFINE CONTROL CODES FOR 6670                                               
         SPACE 1                                                                
DEFCODE  MVC   P(4),=C',DEF'                                                    
         MVC   P+4(2),=X'6DDC'     DC AS STOP CODE                              
         MVC   P+6(5),=C' STOP'                                                 
         MVC   P+11(2),=X'6D9F'    9F AS FUNCTION PREFIX                        
         MVC   P+13(9),=C' FUNCTION'                                            
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         MVC   P(13),=C',PRO_1 OUT 86'                                          
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         MVC   P(9),=C'2 OUT 193'                                               
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P(36),=C'3 TYP 224_3 MAR 2_3 FIR 6_3 LAS 68 0'                   
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         MVC   P(22),=C'4 TYP 86 193_4 OUT 86_'                                 
         MVC   P+22(18),=C'4 FIR 5_4 LAS 63 0'                                  
         MVC   P+40(9),=C'_4 MAR 10'                                            
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         MVI   SKIPSET,C'Y'        FOR ,FEED 0 COMMAND                          
         MVC   P(13),=C',FEED 0_,END '                                          
         GOTO1 =V(PRINT),PARA1                                                  
         EJECT                                                                  
*              UPDATE MODE OR ADDRESS MODE                                      
         SPACE 1                                                                
         CLI   BODYONLY,C'Y'                                                    
         BNE   MANSTART                                                         
         SPACE 1                                                                
         MVI   PHASE,2             PRINT BODY ONLY                              
         SPACE 1                                                                
         CLI   ADDRESS,C'Y'                                                     
         BNE   PANRDSET                                                         
         SPACE 1                                                                
         CLI   FILTERS,X'00'                                                    
         BE    FILTERR                                                          
         SPACE 1                                                                
         CLC   FILTERS,SPACES                                                   
         BE    FILTERR                                                          
         SPACE 1                                                                
         CLI   ADDRPOSN,C'B'       ADDRESS AT BOTTOM OF 1ST PAGE                
         BNE   ADDR1ST               NO                                         
         SPACE 1                                                                
         MVI   FIRSTPG,C'Y'          YES - SET TRAP                             
         B     PANRDSET                                                         
         SPACE 1                                                                
ADDR1ST  MVC   P,SPACES                                                         
         CLI   ADDRPOSN,C'T'       TOP-OF-PAGE ADDRESS                          
         BNE   MIDPAGE                                                          
         SPACE 1                                                                
         MVC   P(4),=X'9FD19F83'   SWITCH AND CRE                               
         GOTO1 =V(PRINT),PARA1                                                  
         CLI   TWOLINE,C'Y'        ONLY 2 LINES OF ADDRESS                      
         BE    *+14                  YES                                        
         ZAP   LINE,=P'7'            NO                                         
         B     PANRDSET                                                         
         SPACE 1                                                                
         ZAP   LINE,=P'4'                                                       
         B     PANRDSET                                                         
         SPACE 1                                                                
FILTERR  MVC   P(44),=C',MOD_QUA 0_-NO VALID FILTER AFTER ADDRLIST= '           
         MVC   P+44(22),=C'CARD. JOB ENDED._,END '                              
         B     OTHERERR                                                         
         SPACE 1                                                                
MIDPAGE  GOTO1 =V(PRINT),PARA2,SPACES-1,=C'BL24'                                
         MVC   P(6),=X'9FE39FD19FC3'    REQ TAB, SWITCH, AND REQ CRE            
         GOTO1 =V(PRINT),PARA1                                                  
         MVI   SKIPSET,C'Y'        FOR ,FEED 0 COMMAND                          
         MVC   P(13),=C',FEED 0_,END '                                          
         GOTO1 =V(PRINT),PARA1                                                  
         SPACE 1                                                                
PANRDSET GOTO1 READ,PARA,(X'80',=C'READ'),=C'PAN',C,P                           
         EJECT                                                                  
*                   MANAGE PHASES FOR MANUAL                                    
         SPACE 1                                                                
MANSTART MVI   PHASE,1                                                          
         L     R1,=V(PRINT)        FIRST TIME THROUGH PATCH TO NOPRINT          
         MVC   BORROW,0(R1)                                                     
         MVC   0(2,R1),=X'07FE'                                                 
         GOTO1 READ,PARA,(X'80',=C'READ'),=C'PAN',C,P                           
         SPACE 1                                                                
ENDREAD  NI    PARA+8,X'00'        RESET EOB RC                                 
         CLI   PHASE,1                                                          
         BL    MANEND              END MATTER DONE                              
         BH    BODYEND             BODY PRINTED - DO END MATTER                 
         SPACE 1                                                                
         MVI   PHASE,2             PRINT FRONT MATTER AND BODY                  
         L     R1,=V(PRINT)                                                     
         MVC   0(2,R1),BORROW                                                   
         NI    FIRSTSW,X'0F'                                                    
         MVC   TITLE,SPACES                                                     
         MVC   COLHEADS,SPACES                                                  
         ZAP   PAGE,=P'1'                                                       
         ZAP   FIGNO,=P'0'                                                      
         ZAP   LINE,=P'0'                                                       
         MVI   XERTEXT,C' '                                                     
         MVI   SKIPSET,C'Y'        FOR ,FEED 0 COMMAND                          
         MVI   MODE,9              START PG., SMALL-ROMAN                       
         CLI   FRONT,C'Y'          FRONT MATTER TO PRECEDE CONTENTS             
         BE    READ                YES - GET IT                                 
         L     R5,=A(LASRTNES)                                                  
         BAS   RE,CONTENTS                                                      
         B     READ                                                             
         SPACE 1                                                                
BODYEND  CLI   BODYONLY,C'Y'                                                    
         BE    MANENDB                                                          
         SPACE 1                                                                
         L     R5,=A(LASRTNES)                                                  
         BAS   RE,ANYIND           PRODUCE INDEX                                
         MVC   P(10),C                                                          
         GOTO1 =V(PRINTER)                                                      
         NI    FIRSTSW,X'0F'                                                    
         MVC   C(10),ENDPGNAM      INSIDE & BACK COVER                          
         MVI   PHASE,0                                                          
         MVI   XERTEXT,C' '                                                     
         B     READ                                                             
         SPACE 1                                                                
MANENDB  CLI   ADDRESS,C'Y'        ADDRESSING                                   
         BE    ADDROUT               YES                                        
         SPACE 1                                                                
         BAS   R7,FORCEODD                                                      
MANEND   NI    CARD0+1,X'0F'                                                    
         MVC   P,SPACES                                                         
         MVC   P(4),=C',SEP'                                                    
         GOTO1 =V(PRINT),PARA1                                                  
         B     CARD                                                             
         EJECT                                                                  
ADDROUT  CLI   FIRSTPG,C'Y'        BOTTOM ADDRESS PENDING                       
         BNE   ADDROUT1              NO                                         
         SPACE 1                                                                
         MVC   SPACING+2(2),=C'60'                                              
         GOTO1 =V(PRINTER)                                                      
         MVC   SPACING+2(2),=C'01'                                              
ADDROUT1 MVC   P(4),=X'9F839FD9'   CRE AND REPEAT                               
         GOTO1 =V(PRINT),PARA1                                                  
         XC    P,P                                                              
         MVC   P+32(2),=X'9F83'    CRE                                          
         GOTO1 FILTADDR,PARA,(X'80',=C'READ'),=C'PAN',ADLISTNM,C                
         SPACE 1                                                                
FILTADDR GOTO1 =V(PANIC),PARA                                                   
         TM    PARA+8,X'80'                                                     
         BO    MANEND                                                           
         SPACE 1                                                                
         CLI   C,C'*'              FILTER LINE                                  
         BNE   FILTADDR                                                         
         LA    R2,C+2                                                           
         LA    R3,FILTERS                                                       
         LA    R4,50                                                            
FILTTEST CLI   0(R3),C' '                                                       
         BE    BUMPEM                                                           
         CLC   0(1,R2),0(R3)       ADDR FILTER MATCH                            
         BE    PUTADDR               YES                                        
BUMPEM   LA    R2,1(R2)              NO                                         
         LA    R3,1(R3)                                                         
         BCT   R4,FILTTEST                                                      
         B     FILTADDR                                                         
         SPACE 1                                                                
PUTADDR  CLI   TWOLINE,C'Y'                                                     
         BE    PUTADDR2                                                         
         SPACE 1                                                                
         GOTO1 =V(PANIC),PARA      1ST LINE                                     
         MVC   P(32),C                                                          
         GOTO1 =V(PANIC),PARA      2ND LINE                                     
         MVC   P+34(32),C                                                       
         MVC   P+66(2),P+32        CRE                                          
         GOTO1 =V(PANIC),PARA      3RD LINE                                     
         MVC   P+68(32),C                                                       
         MVC   P+100(2),P+32       CRE                                          
         GOTO1 =V(PRINT),PARA1                                                  
         XC    P+34(98),P+34                                                    
PUTADDR2 GOTO1 =V(PANIC),PARA      4TH LINE                                     
         MVC   P(32),C                                                          
         GOTO1 =V(PANIC),PARA      5TH LINE                                     
         MVC   P+34(32),C                                                       
         MVC   P+66(4),=X'9FC39FD1'  RCR SWITCH                                 
         GOTO1 =V(PRINT),PARA1                                                  
         B     FILTADDR                                                         
         EJECT                                                                  
*                   COMMON READ ROUTINE                                         
         SPACE 1                                                                
READ     CLC   SPACES(2),=X'4040'  (TEST FOR POSSIBLE BUG - PD 1/83)            
         BE    REALREAD                                                         
         DC    X'00'            ERROR -- 'SPACES' HAS BEEN ZAPPED               
REALREAD CLI   SAVEFLAG,C'Y'                                                    
         BNE   READNEXT                                                         
         MVI   SAVEFLAG,C' '                                                    
         MVC   P(80),SAVEDIR                                                    
         B     READON                                                           
         SPACE 1                                                                
READNEXT GOTO1 =V(PANIC),PARA                                                   
         TM    PARA+8,X'80'                                                     
         BO    ENDREAD                                                          
         SPACE 1                                                                
         NOP   READON                                                           
FIRSTSW  EQU   *-3                                                              
         OI    FIRSTSW,X'F0'                                                    
         CLC   P(19),=C'*          DATA SET'                                    
         BE    READ                                                             
         SPACE 1                                                                
READON   TR    P(80),TRTABLE                                                    
         CLC   P(6),=C'NOTXER'     (OBSOLETE CONTROL LINE)                      
         BNE   *+12                                                             
         MVI   XERTEXT,C' '                                                     
         B     READ                                                             
         SPACE 1                                                                
         CLC   P(7),=C'FILTER='                                                 
         BNE   TESTFILT                                                         
         CLC   P+7(3),=C'OFF'                                                   
         BNE   READFILT                                                         
         MVI   FILTER,C'N'                                                      
         B     READ                                                             
         SPACE 1                                                                
TESTFILT CLI   FILTER,C'Y'                                                      
         BE    READ                                                             
         B     TESTPAGE                                                         
         SPACE 1                                                                
READFILT CLC   P+7(3),=C'DDS='                                                  
         BNE   *+12                                                             
         MVI   FILTER,C'Y'                                                      
         B     READ                                                             
         SPACE 1                                                                
         CLC   P+7(2),=C'US='                                                   
         BNE   *+12                                                             
         MVI   FILTER,C'Y'                                                      
         B     READ                                                             
         SPACE 1                                                                
         CLC   P+7(2),=C'UK='                                                   
         BNE   PRINTOUT                                                         
         MVI   FILTER,C'Y'                                                      
         B     READ                                                             
         EJECT                                                                  
*                                                                               
*  TEST FOR TAB SETTINGS.  IF FOUND, REPLACE TABS WITH                          
*  APPROPRIATE SPACES.                                                          
*                                                                               
TESTPAGE CLI   P,X'2B'             FMT - START OF TAB RACK                      
         BNE   TESTWIDE              NO                                         
         BAS   R6,DOTABS                                                        
         B     READ                                                             
         SPACE 1                                                                
DOTABS   CLC   P+1(2),=C'00'       XERTOPAN NULL TABRACK                        
         BNE   *+10                                                             
         MVI   XERTEXT,C'P'                                                     
         BR    R6                                                               
         SPACE 1                                                                
         MVI   XERTEXT,C'Y'          YES                                        
         CLC   P+1(2),P+3                                                       
         BER   R6                                                               
         XC    TABRACK,TABRACK                                                  
         LA    R3,P+1                                                           
         LA    R4,TABRACK                                                       
         LA    R2,18               L(TABRACK) IN H'S                            
         SPACE 1                                                                
TABRKLP  CLI   0(R3),X'2B'         END OF TAB RACK                              
         BER   R6                                                               
         SPACE 1                                                                
CHARHEX  PACK  WORK(2),0(2,R3)                                                  
         LH    R1,WORK                                                          
         SRA   R1,4                                                             
         TM    1(R3),X'F0'         LOW-ORDER NUMERIC                            
         BO    *+8                   YES                                        
         AH    R1,=H'9'                                                         
         STH   R1,0(R4)                                                         
         LA    R4,2(R4)                                                         
         LA    R3,2(R3)                                                         
         BCT   R2,TABRKLP          MORE ROOM IN RACK                            
         BR    R6                                                               
         EJECT                                                                  
*                   WIDE-PAGE TESTING                                           
         SPACE 1                                                                
TESTWIDE L     R5,=A(LASRTNES)                                                  
         CLC   P(5),=C'PAGE='                                                   
         BNE   TESTPGMD                                                         
         SPACE 1                                                                
         CLC   P+5(3),=C'END'                                                   
         BNE   *+16                                                             
         CLI   PAGEMODE,C'Y'       END BEFORE START                             
         BE    PAGEEND               NO                                         
         B     READ                                                             
         SPACE 1                                                                
         CLC   P+5(5),=C'START'                                                 
         BNE   TRYSPECL                                                         
         SPACE 1                                                                
         CLI   PAGEMODE,C'Y'       START BEFORE END                             
         BNE   PAGESTRT              NO                                         
         SPACE 1                                                                
         MVI   STARTFLG,C'Y'         YES - SET FLAG                             
         MVC   C+11(58),P+11               SAVE CAPTION                         
         B     PAGEEND                     END CURRENT WIDE PAGE                
         SPACE 1                                                                
TESTPGMD CLI   PAGEMODE,C'Y'                                                    
         BE    PAGELINE                                                         
         SPACE 1                                                                
         CLC   P(7),=C'UPDATE='    UPDATE CONTROL LINE                          
         BNE   TRYBIG                NO                                         
         SPACE 1                                                                
         L     R5,=A(LASRTNES)                                                  
         CLI   BODYONLY,C'Y'         YES - UPDATE CARD FOUND IN DECK            
         BE    UPDATING                      YES                                
         SPACE 1                                                                
         L     R1,=V(PRINT)                                                     
         MVC   0(2,R1),BORROW      RESTORE PRINTING                             
         MVC   P(42),=C',MOD_QUA 0_-UPDATE= LINE BUT NO UPDATE OR '             
         MVC   P+42(31),=C'ADDRLIST= CARD--JOB ENDED_,END '                     
         B     OTHERERR                                                         
         EJECT                                                                  
*                   BIG= SERIES                                                 
         SPACE 1                                                                
TRYBIG   CLC   P(4),=C'BIG='                                                    
         BNE   NOTBIG                                                           
         SPACE 1                                                                
         CLC   BIGWORD,SPACES                                                   
         BNE   BIG1                                                             
         MVC   BIGWORD,P+4                                                      
         BAS   R7,FORCEODD                                                      
         B     BIG2                                                             
         SPACE 1                                                                
BIG1     MVC   BIGWORD,P+4                                                      
         NI    MODE,1                                                           
BIG2     LA    R2,BIGWORD                                                       
         USING BIGTABLS,R7                                                      
         L     R7,=A(BIGTABLS)                                                  
         LA    R3,BIGWORK+81                                                    
         LA    R4,8                                                             
         CLC   BIGWORD,SPACES                                                   
         BE    BIG5                                                             
         LA    R6,BIGWORD+7                                                     
         SPACE 1                                                                
BIG3     CLI   0(R6),C' '                                                       
         BNE   BIG4                                                             
         LA    R3,5(R3)                                                         
         BCT   R6,BIG3                                                          
         SPACE 1                                                                
BIG4     GOTO1 =V(EXPAND),PARA2,(R2),(72,(R3))                                  
         LA    R2,1(R2)                                                         
         CLI   0(R2),C' '                                                       
         BE    BIG5                                                             
         LA    R3,10(R3)                                                        
         BCT   R4,BIG4                                                          
         SPACE 1                                                                
BIG5     LA    R2,BIGWORK-1        NOW PRINT AND CLEAR                          
         LA    R3,10                                                            
         MVI   SPACING+3,C'1'                                                   
         SPACE 1                                                                
BIG6     MVC   P(80),0(R2)                                                      
         TR    P(80),BOLDTBLE      CONVERT TO JAGGED BOLD                       
         GOTO1 =V(PRINTER)                                                      
         MVC   0(80,R2),SPACES                                                  
         LA    R2,80(R2)                                                        
         BCT   R3,BIG6                                                          
         SPACE 1                                                                
         OI    MODE,X'0A'                                                       
         B     READ                                                             
         SPACE 1                                                                
BIGWORD  DC    CL8' '                                                           
         EJECT                                                                  
BIGTABLS CSECT                                                                  
*                   JAGGED BOLD FOR BIG= LETTERS (101)                          
         SPACE 1                                                                
BOLDTBLE DS    0CL256                                                           
         DC    92X'40'                                                          
         DC    X'A1'     *                                                      
         DC    4X'40'                                                           
         DC    X'A1'     /                                                      
         DC    95X'40'                                                          
         DC    9X'A1'    A-I                                                    
         DC    7X'40'                                                           
         DC    9X'A1'    J-R                                                    
         DC    8X'40'                                                           
         DC    8X'A1'    S-Z                                                    
         DC    6X'40'                                                           
         DC    10X'A1'   0-9                                                    
         DC    6X'40'                                                           
         SPACE 1                                                                
BIGWORK  DC    800C' '                                                          
         SPACE 1                                                                
UPERMSGS DC    CL48'?? NO FIELD TERMINATOR ??'                                  
         DC    CL48'** NO "REP", "INS", OR "END"'                               
         DC    CL48'** INVALID KEYWORD'                                         
         DC    CL48'** INVALID NUMBER'                                          
         DC    CL48'** 1ST "REP" PAGES= NO NOT ODD'                             
         DC    CL48'** 2ND "REP" PAGES= NO NOT EVEN'                            
         DC    CL48'** 2ND "REP" PAGES= NO NOT GREATER THAN 1ST'                
         DC    CL48'** 2ND "REP" FIGURES= NO LESS THAN 1ST'                     
         DC    CL48'** NO PAGES= ENTRY IN "REP" LINE'                           
         DC    CL48'** NO RFOOT, TITLE, OR SUPER AFTER UPDATE= LINE'            
         DC    CL48'** NO AFTERPG= ENTRY IN "INS" LINE'                         
         SPACE 1                                                                
         DROP  R7                                                               
         EJECT                                                                  
LASPRINT CSECT                                                                  
NOTBIG   MVC   BIGWORD,SPACES                                                   
         CLC   P(8),=C'REVERSE='                                                
         BE    READ                                                             
         SPACE 1                                                                
         CLC   P(9),=C'WIDELINE='                                               
         BNE   TRYCOPRT                                                         
         SPACE 1                                                                
         CLC   P+9(3),=C'106'                                                   
         BNE   *+12                                                             
         MVI   WPOFFSET,X'0D'                                                   
         B     READ                                                             
         SPACE 1                                                                
         CLC   P+9(3),=C'120'                                                   
         BNE   *+12                                                             
         MVI   WPOFFSET,X'06'                                                   
         B     READ                                                             
         SPACE 1                                                                
         MVI   WPOFFSET,X'00'                                                   
         B     READ                                                             
         SPACE 1                                                                
TRYCOPRT CLC   P(9),=C'COPYRIGHT'                                               
         BNE   *+24                                                             
         CLI   PHASE,1                                                          
         BE    *+8                                                              
         BAS   R7,COPYRITE                                                      
         OI    MODE,X'0A'                                                       
         B     READ                                                             
         SPACE 1                                                                
         CLI   P,C','                                                           
         BNE   TRYCHPNM                                                         
         CLI   P+4,X'6D'                                                        
         BNE   TRYCHPNM                                                         
         SPACE 1                                                                
         CLI   PHASE,1             PRINTING NOW                                 
         BE    READ                  NO                                         
         SPACE 1                                                                
         GOTO1 =V(PRINT),PARA1                                                  
         B     READ                                                             
         EJECT                                                                  
TRYCHPNM CLC   P(8),=C'CHAPNUM='                                                
         BNE   SCAN                                                             
         SPACE 1                                                                
         TM    P+8,X'F0'           NUMERIC                                      
         BO    DOCHPNUM              YES                                        
         CLI   P+8,C'I'              NO  - A - H                                
         BNL   READ                          NO                                 
         SPACE 1                                                                
DOCHPNUM MVC   ALPHA,P+8                                                        
         BAS   R7,FORCEODD                                                      
         MVC   SECTNUM,ALPHA                                                    
         MVI   ALPHA,C' '                                                       
         ZAP   FIGNO,=P'0'                                                      
         ZAP   PAGE,=P'1'                                                       
         CLI   PHASE,2             PRINTING NOW                                 
         BNE   READ                  NO                                         
         SPACE 1                                                                
         CLC   CHOSEN,SPACES       SELECTIVE PRINTING                           
         BE    READ                  NO                                         
         SPACE 1                                                                
         L     R1,=V(PRINT)                                                     
         LA    R2,CHOSEN                                                        
         LA    R3,3                                                             
         CLC   0(1,R2),SECTNUM                                                  
         BE    NEWCHAP                                                          
         SPACE 1                                                                
         LA    R2,1(R2)                                                         
         BCT   R3,*-14                                                          
         MVC   0(2,R1),=X'07FE'    PREVENT PRINTING                             
         B     READ                                                             
         SPACE 1                                                                
NEWCHAP  MVC   0(2,R1),BORROW      START PRINTING                               
         MVI   SKIPSET,C'Y'        FOR ,FEED 0 COMMAND                          
         MVC   P(13),=C',FEED 0_,END '                                          
         GOTO1 =V(PRINT),PARA1                                                  
         B     READ                                                             
         EJECT                                                                  
*              SWITCH TO SYMBOL FONT WITHIN TEXT                                
*                                                                               
SCAN     LA    R2,P                                                             
         LA    R3,130                                                           
         SPACE 1                                                                
LOOP     CLC   0(3,R2),=C'/(/'  SWITCH TO SYMBOL                                
         BNE   NEXT                                                             
         MVC   0(3,R2),=X'DCF261'                                               
         B     GOBACK                                                           
NEXT     CLC   0(3,R2),=C'/)/'  SWITCH TO 12 PITCH                              
         BNE   GOBACK                                                           
         MVC   0(3,R2),=X'DCF161'                                               
GOBACK   LA    R2,1(R2)                                                         
         BCT   R3,LOOP                                                          
         EJECT                                                                  
*              SCAN FOR CONTROL LINE                                            
         SPACE 1                                                                
         CLC   P(8),SPACES       POSSIBLE CC-10 CONTROL LINE                    
         BNE   NOTSCAN               NO                                         
         SPACE 1                                                                
         LA    R2,P+8                                                           
RESCAN   LA    R3,3                                                             
         CLI   0(R2),C' '                                                       
         BNE   *+16                                                             
         LA    R2,1(R2)                                                         
         BCT   R3,*-12                                                          
         B     TRYSPECL                                                         
         SPACE 1                                                                
         MVC   SCANWORD(7),0(R2)                                                
         CLC   SCANWORD(6),=C'SPACE '                                           
         BE    SPACESLN                                                         
         SPACE 1                                                                
         CLC   SCANWORD(4),=C'HEAD'                                             
         BE    HEAD                                                             
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'INDEX '''                                         
         BE    DOIXLINE                                                         
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'FIGURE '                                          
         BE    FIGURE                                                           
         SPACE 1                                                                
         CLC   SCANWORD(5),=C'EJECT'                                            
         BE    EJOREND                                                          
         SPACE 1                                                                
         CLC   SCANWORD(6),=C'ENDPG '                                           
         BNE   *+12                                                             
         LA    R2,9(R2)                                                         
         B     RESCAN                                                           
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'TITLE '''                                         
         BE    TITLELN                                                          
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'SUPER '''                                         
         BE    SUPER                                                            
         SPACE 1                                                                
         CLC   SCANWORD(6),=C'PRINT '                                           
         BE    READ                IGNORE PRINT S  D  T LINE                    
         SPACE 1                                                                
         CLC   SCANWORD(6),=C'FRONT='                                           
         BE    FRONTLN                                                          
         SPACE 1                                                                
         B     TRYSPECL                                                         
         EJECT                                                                  
EJOREND  CLI   FIRSTPG,C'Y'        ADDRESSING MODE                              
         BNE   EJECT                                                            
         MVC   SPACING+2(2),=C'60'                                              
         GOTO1 =V(PRINTER)                                                      
         MVC   SPACING+2(2),=C'01'                                              
         OI    MODE,X'08'                                                       
         B     READ                                                             
         SPACE 1                                                                
EJECT    BAS   R6,ENDOLDPG                                                      
         MVI   NOSCREEN,C' '                                                    
         B     READ                                                             
         SPACE 1                                                                
SPACESLN TM    SCANWORD+6,X'F0'    NUMERIC                                      
         BNO   READ                  NO                                         
         MVI   NOSCREEN,C' '                                                    
         SPACE 1                                                                
         CLC   10(3,R2),SPACES                                                  
         BE    *+8                                                              
         MVI   NOSCREEN,C'Y'                                                    
         SPACE 1                                                                
         MVC   P(30),SPACES                                                     
         MVC   SPACING+3(1),SCANWORD+6                                          
         GOTO1 =V(PRINTER)                                                      
         MVI   SPACING+3,C'1'                                                   
         B     READ                                                             
         SPACE 1                                                                
FRONTLN  CLI   SCANWORD+6,C'S'                                                  
         BNE   TRYEND                                                           
         MVI   FRONT,C'Y'                                                       
         OI    MODE,1              START SMALL-ROMAN                            
         B     READ                                                             
         SPACE 1                                                                
TRYEND   CLI   SCANWORD+6,C'E'                                                  
         BNE   PRINTOUT                                                         
         SPACE 1                                                                
         CLI   BODYONLY,C'Y'                                                    
         BE    *+12                                                             
         CLI   PHASE,1                                                          
         BH    ENDPHS2                                                          
         SPACE 1                                                                
         BAS   R7,FORCEODD                                                      
         ZAP   FIGNO,=P'0'                                                      
         ZAP   PAGE,=P'1'                                                       
         NI    MODE,X'FE'          END SMALL-ROMAN                              
         B     READ                                                             
         SPACE 1                                                                
ENDPHS2  BAS   R7,COPYRITE                                                      
         NI    COPYRITE+1,X'0F'                                                 
         L     R5,=A(LASRTNES)                                                  
         BAS   RE,CONTENTS                                                      
         B     READ                                                             
        EJECT                                                                   
COPYRITE NOPR  R7                                                               
         SPACE 1                                                                
         OI    COPYRITE+1,X'F0'                                                 
         ZAP   WORK(2),MAXLINE                                                  
         SP    WORK(2),LINE                                                     
         CP    WORK(2),=P'2'                                                    
         BNH   DOCOPYRS                                                         
         BL    DONEWPG                                                          
         SP    WORK(2),=P'1'                                                    
         UNPK  SPACING+2(2),WORK(2)                                             
         OI    SPACING+3,X'F0'                                                  
         MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
         MVC   SPACING+2(2),=C'01'                                              
         B     DOCOPYRT                                                         
DONEWPG  BAS   R6,ENDOLDPG                                                      
         B     DOCOPYRT                                                         
DOCOPYRS MVC   P,SPACES                                                         
         GOTO1 =V(PRINTER)                                                      
DOCOPYRT MVC   P(20),=X'C39697A899898788A340DCF261D1DCF16140F1F9'               
         MVC   P+20(2),SAVEYR                                                   
         MVC   P+22(17),=X'4082A840C4969596A5819540C481A38140'                  
         MVC   P+39(13),=X'E2A8A2A38594A26B40C995834B'                          
         MVC   P+76(10),C                                                       
         GOTO1 =V(PRINTER)                                                      
         BR    R7                                                               
        EJECT                                                                   
FIGURE   CLI   7(R2),X'7D'         APOSTROPHE AFTER FIGURE                      
         BNE   TRYSPECL              NO                                         
         BAS   RE,SCANFIG            YES                                        
         CLI   ALPHA,C'F'          FIGURE FIRST ON PAGE                         
         BE    FIGPHASE              YES                                        
         MVI   SPACING+3,C'2'                                                   
         GOTO1 =V(PRINTER)                                                      
         SPACE 1                                                                
FIGPHASE CLI   PHASE,1                                                          
         BE    FIGOUT                                                           
         SPACE 1                                                                
         MVC   SAVEDIR,SPACES                                                   
         MVC   SAVEDIR(7),FIGNAME                                               
         LA    R1,SAVEDIR+1        FOR TRAILING SPACE                           
         AH    R1,LOFFIGNM                                                      
         CLI   FIGPTNUM,C'Y'                                                    
         BNE   *+18                                                             
         SPACE 1                                                                
         MVC   0(8,R1),SVFIGNO                                                  
         AH    R1,SVFNOLEN                                                      
         B     SETFIGNO                                                         
         SPACE 1                                                                
         CLI   SECTNUM,C' '        PGNO. PREFIX                                 
         BE    SETFIGNO              NO                                         
         SPACE 1                                                                
         MVI   DASHFLAG,C'Y'                                                    
         BAS   RE,DOSUPNUM                                                      
         SPACE 1                                                                
SETFIGNO EDIT  (P2,FIGNO),(3,(R1)),ALIGN=LEFT                                   
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '          FIND END OF FIGNO                            
         BNE   *-8                   NO                                         
         MVI   0(R1),C'.'            YES                                        
         MVC   3(58,R1),INDEX                                                   
         LA    R1,SAVEDIR+79                                                    
         BAS   R7,CENTERER                                                      
         SPACE 1                                                                
FIGOUT   CLI   ALPHA,C'L'          FIGURE AT PAGE BOTTOM                        
         BNE   *+12                  NO                                         
         MVI   SPACING+3,C'1'        YES                                        
         B     *+8                                                              
         MVI   SPACING+3,C'3'                                                   
         SPACE 1                                                                
         GOTO1 =V(PRINTER)                                                      
         CLI   ALPHA,C'L'          LAST ON PAGE                                 
         BNE   *+8                   NO                                         
         BAS   R6,ENDOLDPG           YES - FORCE END                            
         MVI   ALPHA,C' '                                                       
         CLI   REPFLAG,C'Y'                                                     
         BNE   READ                                                             
         CP    FIGNO,LASTFIG                                                    
         BL    READ                                                             
         LA    R7,READ                                                          
         L     R5,=A(LASRTNES)                                                  
         B     FIGNOSET+4                                                       
         EJECT                                                                  
TITLELN  BAS   RE,SCANTITL         SET TITLE                                    
         B     READ                                                             
         SPACE 3                                                                
SUPER    BAS   RE,SCANSUP          TEST AND SET SUPER                           
         CLI   SECTNUM,C' '        IS CHAPNUM GIVEN                             
         BE    DEMOTE                NO                                         
         SPACE 1                                                                
         CLI   PHASE,1                                                          
         BE    SUPSPACE            DO SPACING ONLY                              
         SPACE 1                                                                
         MVC   SAVEDIR,SPACES                                                   
         MVC   SAVEDIR(8),SECTNAME                                              
         LA    R1,SAVEDIR+1                                                     
         AH    R1,LOFCHPNM                                                      
         BAS   RE,DOSUPNUM                                                      
         BAS   R7,CENTERER                                                      
         EX    R1,UPITALL          CAP THE CENTERED TEXT                        
         OI    MODE,X'40'          CHAP ON 3RD PAGE LINE                        
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
         MVC   SAVEDIR(58),INDEX                                                
         LA    R1,SAVEDIR+59                                                    
         BAS   R7,CENTERER                                                      
         EX    R1,UPITALL          CAP THE CENTERED TEXT                        
         OI    MODE,X'80'                                                       
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
         LA    R5,INDEX                                                         
         LH    R2,LOFTITLE                                                      
         EX    R2,REQSPACE                                                      
         AR    R5,R2                                                            
         MVC   0(2,R5),=X'9FE6'    NON-TRANSP WORD USCORE                       
         AH    R2,=H'2'                                                         
         STH   R2,LOFTITLE                                                      
         SPACE 1                                                                
SUPSPACE ZAP   LINE,=P'10'                                                      
         B     TITLESET                                                         
         SPACE 1                                                                
DEMOTE   MVC   LOFTITLE,=H'0'      DEMOTE SUPER TO NEW TITLE                    
TITLESET MVC   TITLE(58),INDEX     SET SUPER TEXT FOR TITLE USAGE               
         B     READ                                                             
         SPACE 1                                                                
UPITALL  OC    0(0,R2),SPACES                                                   
         EJECT                                                                  
*              CENTERER FOR SUPER OR FIGURE                                     
         SPACE 1                                                                
CENTERER LA    R0,76               LINE WIDTH                                   
         LA    R2,P                PRINT LINE START                             
CENTLP   CLI   0(R1),C' '                                                       
         BNE   *+8                 RIGHT END FOUND                              
         BCT   R1,CENTLP                                                        
         LA    R3,SAVEDIR-1                                                     
         SR    R1,R3               LENGTH OF LINE TO CENTER                     
         SR    R0,R1               SPACES TO SPLIT EVENLY                       
         SRA   R0,1                DIVIDE BY 2                                  
         AR    R2,R0               SET START ON LINE                            
         EX    R1,CENTMOVE                                                      
         BR    R7                                                               
         SPACE 1                                                                
CENTMOVE MVC   0(0,R2),SAVEDIR                                                  
         SPACE 3                                                                
DOIXLINE BAS   RE,SCANINDX         SET INDEX ENTRY                              
         B     READ                                                             
         SPACE 3                                                                
HEAD     TM    SCANWORD+4,X'F0'    IS LEVEL NUMERIC                             
         BNO   TRYSPECL              NO                                         
         BAS   RE,SCANHEAD           YES - SET HEAD                             
         CLI   ALPHA,C'F'          FIRST LINE ON PAGE                           
         BNE   *+12                                                             
         OI    MODE,X'80'            MAKE HEAD 1ST LINE                         
         B     *+8                                                              
         OI    MODE,X'40'          PUT HEAD WITHIN PAGE BODY                    
         SR    R6,R6                                                            
         NI    SCANWORD+4,X'0F'    LEVEL NUMBER                                 
         IC    R6,SCANWORD+4                                                    
         SLA   R6,1                                                             
         LA    R3,P(R6)                                                         
         MVC   0(58,R3),INDEX                                                   
         GOTO1 =V(PRINTER)                                                      
         MVI   ALPHA,C' '                                                       
         B     READ                                                             
         EJECT                                                                  
SCANINDX ST    RE,RETADDR          INDEX ENTRY                                  
         B     SCAN3                                                            
         SPACE 3                                                                
SCANSUP  ST    RE,RETADDR          SUPER                                        
         BAS   R6,ENDOLDPG                                                      
         MVC   TITLE,SPACES                                                     
         MVC   COLHEADS,SPACES                                                  
         L     R5,=A(LASRTNES)                                                  
         MVI   SUPERSW,X'10'                                                    
         B     SCAN3               SAVE TEXT IN INDEX                           
         SPACE 3                                                                
SCANFIG  ST    RE,RETADDR          FIGURE CAPTION                               
         LA    R2,1(R2)            GET PAST LEADING '                           
         MVI   FIGSTODO,C'Y'                                                    
         MVC   COLHEADS,SPACES                                                  
         AP    FIGNO,=P'1'                                                      
         BAS   R6,TESTROOM         ROOM FOR FIGURE ON THIS PAGE                 
         CP    WORK(2),=P'3'         NO  - TEST FOR END-OF-PAGE FIT             
         MVI   ALPHA,C'L'                                                       
         BNL   SCAN3                         YES                                
         B     SCAN2A                        NO                                 
         SPACE 3                                                                
SCANHEAD ST    RE,RETADDR          PARAGRAPH HEAD                               
         MVC   COLHEADS,SPACES                                                  
         TM    MODE,X'08'          PAGE START PENDING                           
         BO    SCAN2A                YES - SKIP TEST FOR SPACE                  
         SPACE 1                                                                
         LA    R6,SCAN2A           ROOM FOR HEAD ON THIS PAGE                   
TESTROOM ZAP   WORK(2),MAXLINE                                                  
         SP    WORK(2),LINE                                                     
         CP    WORK(2),=P'7'                                                    
         BNL   SCAN3                 YES                                        
         BR    R6                                                               
         SPACE 1                                                                
SCAN2A   MVI   ALPHA,C'F'            NO                                         
         BAS   R6,ENDOLDPG         START NEW PAGE                               
         SPACE 1                                                                
SCAN3    MVC   INDEX,SPACES                                                     
         LA    R3,INDEX                                                         
         B     SCAN5                                                            
         EJECT                                                                  
SCANTITL ST    RE,RETADDR          TITLE                                        
         BAS   R6,ENDOLDPG                                                      
         MVC   TITLE,SPACES                                                     
         MVC   LOFTITLE,=H'0'                                                   
         MVC   COLHEADS,SPACES                                                  
         LA    R3,TITLE                                                         
         SPACE 1                                                                
SCAN5    LA    R2,7(R2)            SET MOVE START                               
         LA    R4,58(R2)           SET END-SEARCH START                         
         LA    R5,58               SET SEARCH LIMIT                             
SCANLP   CLI   0(R4),C' '          1ST NON-BLANK TRAILER                        
         BNE   SCAN6                 YES                                        
         BCTR  R4,0                                                             
         BCT   R5,SCANLP                                                        
         B     TRYSPECL            NO TEXT FOUND                                
         SPACE 1                                                                
SCAN6    CLI   0(R4),X'7D'         APOSTROPHE                                   
         BE    *+8                   YES                                        
         LA    R4,1(R4)              NO  - INCLUDE IT                           
         SR    R4,R2                                                            
         BNP   TRYSPECL                                                         
         CH    R4,=H'58'                                                        
         BNH   *+8                                                              
         BCT   R4,*-8                                                           
         CLC   SCANWORD,=C'SUPER '''                                            
         BNE   *+8                                                              
         STH   R4,LOFTITLE                                                      
         BCTR  R4,0                                                             
         EX    R4,SCANMOVE                                                      
         MVC   P,SPACES                                                         
         CLI   PHASE,1                                                          
         BNE   SCANXIT                                                          
         SPACE 1                                                                
         TM    MODE,1              FRONT MATTER                                 
         BO    SCANXIT               YES                                        
         SPACE 1                                                                
         BAS   R1,DOPOOLLN                                                      
         SPACE 1                                                                
SCANXIT  L     RE,RETADDR                                                       
         L     R5,=A(LASRTNES)                                                  
         BR    RE                                                               
         SPACE 1                                                                
SCANMOVE MVC   0(0,R3),0(R2)                                                    
         EJECT                                                                  
*                        PUT ENTRY INTO TITLPOOL                                
         SPACE 2                                                                
DOPOOLLN LH    R3,POOLLINE         LAST POOL LINE NUMBER                        
         CH    R3,POOLSIZE         LOWER THAN POOL DEPTH                        
         BNL   POOLFULL              NO - DO ERROR ROUTINE                      
         SPACE 1                                                                
         LA    R5,96                 YES - PROCEED                              
         MR    R4,R3               SET OFFSET FROM A(TITLPOOL)                  
         LA    R3,1(R3)            INCREMENT POOLLINE                           
         STH   R3,POOLLINE         SAVE IT                                      
         L     R2,=A(TITLPOOL)                                                  
         AR    R2,R5               POINT R2 TO NEXT POOL LINE                   
         XC    0(96,R2),0(R2)      CLEAR IT                                     
         CLC   SCANWORD,=C'TITLE '''                                            
         BNE   *+12                                                             
         LA    R5,TITLE                                                         
         B     *+8                                                              
         LA    R5,INDEX                                                         
         MVC   PLTEXT,0(R5)                                                     
         MVC   SRTTXT,0(R2)        MOVE IN SORT-KEY TEXT                        
         OC    SRTTXT,SPACES       MAKE TEXT ALL-CAPS                           
         MVC   PLCHPT,SECTNUM      PG. NO. PREFIX, IF PRESENT                   
         MVC   PLPGNO,PAGE         PG. NO.                                      
         CLC   SCANWORD(7),=C'FIGURE '                                          
         BE    *+14                                                             
         CLC   SCANWORD(7),SPACES                                               
         BNE   OTHERLN             NOT CAPTION FROM WIDE-LINE PAGE              
         OI    PLTYPE,X'40'          YES - MARK CAPTION                         
         MVC   CAPNUM,FIGNO                                                     
         BR    R1                          GET OUT                              
         SPACE 1                                                                
OTHERLN  CLC   SCANWORD,=C'TITLE '''                                            
         BNE   *+10                  NO                                         
         OI    PLTYPE,X'20'          YES - MARK IT                              
         BR    R1                          GET OUT                              
         SPACE 1                                                                
         CLC   SCANWORD,=C'SUPER '''                                            
         BNE   *+10                  NO                                         
         OI    PLTYPE,X'10'                                                     
         BR    R1                          GET OUT                              
         SPACE 1                                                                
         CLC   SCANWORD(4),=C'HEAD'                                             
         BNE   IXMARK                                                           
         CLC   SCANWORD+4(1),CONTLIM    HEADN INTO CONTENTS                     
         BH    IXMARK                    NO                                     
         MVN   PLTYPE,SCANWORD+4         YES - SAVE LEVEL NO.                   
         BR    R1                               RETURN                          
         SPACE 1                                                                
IXMARK   OI    PLTYPE,X'80'                                                     
         BR    R1                                                               
         EJECT                                                                  
*                   COLUMN HEADINGS LINE                                        
         SPACE 1                                                                
NOTSCAN  CLC   P(2),=X'6E6E'       TWO GREATER-THAN'S                           
         BNE   TRYSPECL              NO                                         
         SPACE 1                                                                
         CLI   P+2,C' '                                                         
         BL    *+14                XERSTORE LINE END                            
         CLC   P+2(78),SPACES      NULL LINE                                    
         BNE   *+14                  NO                                         
         MVC   COLHEADS,SPACES       YES                                        
         B     READ                                                             
         SPACE 1                                                                
         CLI   XERTEXT,C'Y'                                                     
         BNE   *+16                                                             
         LA    R4,P                                                             
         LA    R7,79                                                            
         BAS   RE,TRYTABRT                                                      
         MVC   COLHEADS(78),P+2    PUT TEXT INTO COLHEADS                       
         OI    MODE,X'10'          SET NEW COLHEADS BIT                         
         GOTO1 =V(PRINTER)                                                      
         B     READ                                                             
         SPACE 1                                                                
TRYSPECL CLI   XERTEXT,C'Y'                                                     
         BE    XERLINE                                                          
         EJECT                                                                  
*                   U'SCORE, SPECIALS                                           
         SPACE 1                                                                
NOTXERLN MVI   LINELEN,X'00'                                                    
         TRT   P(80),SPECIALS                                                   
         BZ    SCREENLN              NO                                         
         SPACE 1                                                                
         CLI   0(R1),X'6D'         USCORE                                       
         BNE   SPECONLY              NO                                         
         TRT   P(80),SEPUSC                                                     
         BNZ   MORESPEC                                                         
         SPACE 1                                                                
         CLI   PHASE,1             PRINTING NOW                                 
         BE    READ                  NO                                         
         SPACE 1                                                                
         MVC   U,P                   YES                                        
         LA    R4,0(R1)            P PTR TO 1ST USCORE                          
         GOTO1 =V(PANIC),PARA      GET NEXT LINE                                
         TM    PARA+8,X'80'                                                     
         BO    ENDREAD                                                          
         TR    P(80),TRTABLE                                                    
         BAS   RE,GETPLEN                                                       
         B     SETUSCOR                                                         
         SPACE 1                                                                
MORESPEC MVI   SPCUSC,X'00'                                                     
         TRT   P(80),SPECIALS                                                   
         MVI   SPCUSC,X'FF'                                                     
         BZ    SCREENLN                                                         
         B     SPECONLY                                                         
         EJECT                                                                  
*                   DO USCORE INTO V FROM P                                     
         SPACE 1                                                                
SETUSCOR MVC   V,SPACES                                                         
         LA    R3,U                U PTR                                        
         LA    R5,V                DEST                                         
         LA    R6,P                SOURCE                                       
         LA    R7,U+80             LIMIT                                        
         SR    R4,R6               DISP TO 1ST USCORE                           
         LTR   R4,R4               BLANKS IN U                                  
         BZ    GETULEN               NO                                         
         SPACE 1                                                                
MOVEREST BCTR  R4,0                  YES                                        
         EX    R4,MOVEPTOV                                                      
         LA    R4,1(R4)                                                         
         AR    R3,R4                                                            
         AR    R5,R4                                                            
         AR    R6,R4                                                            
         CR    R3,R7               LIMIT                                        
         BNL   TRYSPECU              YES                                        
         SPACE 1                                                                
GETULEN  LA    R4,1(R3)            U PTR                                        
ULENLP   CLI   0(R4),X'6D'         USCORE                                       
         BNE   DOUSCORE              NO                                         
         CR    R4,R7                 YES - LIMIT                                
         BNL   DOUSCORE                      YES                                
         LA    R4,1(R4)                      NO                                 
         B     ULENLP                                                           
         SPACE 1                                                                
DOUSCORE BCTR  R4,0                                                             
         SR    R4,R3               GET ULEN - 1                                 
         EX    R4,MOVEPTOV                                                      
         EX    R4,REQSPACE                                                      
         LA    R4,1(R4)                                                         
         AR    R3,R4                                                            
         AR    R5,R4                                                            
         AR    R6,R4                                                            
         MVC   0(2,R5),=X'9FE6'    NON-TRANSP WORD USCORE                       
         AH    R5,=H'2'                                                         
         ZIC   R1,LINELEN                                                       
         AH    R1,=H'2'                                                         
         STC   R1,LINELEN                                                       
         SPACE 1                                                                
         LR    R4,R7                                                            
         SR    R4,R3                                                            
         BNP   TRYSPECU                                                         
         BCTR  R4,0                                                             
         EX    R4,MOREUSC          MORE USCORE                                  
         BZ    MOVEREST+2                                                       
         LA    R4,0(R1)                                                         
         SR    R4,R3                                                            
         B     MOVEREST                                                         
         EJECT                                                                  
TRYSPECU TRT   V,SPECIALS                                                       
         BZ    NOUSPECS                                                         
         SPACE 1                                                                
         LA    R3,P                                                             
         LA    R5,V                                                             
         LR    R4,R1                                                            
         SR    R4,R5                                                            
         BAS   RE,USPECS                                                        
         CLI   SCRNFLAG,C'Y'                                                    
         BE    SCRNLINE                                                         
         B     PRINTOUT                                                         
         SPACE 1                                                                
GETPLEN  NTR1                      GET L(TEXT IN P) - 1                         
         CLI   SCRNFLAG,C'Y'                                                    
         BNE   GETPLENX                                                         
         LA    R2,P                                                             
         LA    R3,P+79                                                          
         CLI   0(R3),C' '          FIND TRAILING *                              
         BNE   *+12                                                             
         BCTR  R3,0                                                             
         CR    R3,R2                                                            
         BH    *-12                                                             
         SR    R3,R2                                                            
         SH    R3,=H'2'            LESS TWO *'S                                 
         STC   R3,LINELEN                                                       
GETPLENX XIT1                                                                   
         SPACE 1                                                                
MOVEPTOV MVC   0(0,R5),0(R6)                                                    
         SPACE 1                                                                
REQSPACE TR    0(0,R5),REQSPC                                                   
         SPACE 1                                                                
MOREUSC  TRT   0(0,R3),SPECIALS                                                 
         SPACE 1                                                                
MOVEVTOP MVC   0(0,R3),0(R5)                                                    
         SPACE 1                                                                
SPECCONT TRT   0(0,R5),SPECIALS                                                 
         EJECT                                                                  
SPECONLY BAS   RE,GETPLEN                                                       
         MVC   V,P                                                              
         LA    R3,P                DEST                                         
         LR    R4,R1                                                            
         SR    R4,R3               DISP TO 1ST SPEC                             
         LA    R5,V                SOURCE                                       
         BAS   RE,USPECS                                                        
         CLI   SCRNFLAG,C'Y'                                                    
         BE    SCRNLINE                                                         
         B     PRINTOUT                                                         
         SPACE 1                                                                
USPECS   LA    R6,V+119             LIMIT                                       
         SPACE 1                                                                
*                   COMMON 'SPECIAL' CHAR ROUTINE                               
         SPACE 1                                                                
MOVESOME LTR   R4,R4                                                            
         BZ    DOSPEC                                                           
         BCTR  R4,0                                                             
MOVEALL  EX    R4,MOVEVTOP                                                      
         LA    R4,1(R4)                                                         
         AR    R3,R4                                                            
         AR    R5,R4                                                            
         CR    R5,R6                                                            
         BHR   RE                                                               
DOSPEC   CLI   0(R1),X'AD'                                                      
         BE    DOSPEC1                                                          
         CLI   0(R1),X'BD'                                                      
         BE    DOSPEC1                                                          
         SPACE 1                                                                
         CLI   PAGEMODE,C'Y'                                                    
         BNE   *+12                                                             
         MVI   0(R1),C' '                                                       
         B     DOSPEC1+4                                                        
         ZIC   R1,LINELEN                                                       
         AH    R1,=H'6'                                                         
         STC   R1,LINELEN                                                       
         MVC   0(3,R3),=X'DCF261'                                               
         STC   R2,3(R3)                                                         
         MVC   4(3,R3),=X'DCF161'                                               
         AH    R3,=H'6'                                                         
         B     *+8                                                              
DOSPEC1  STC   R2,0(R3)                                                         
         LA    R3,1(R3)                                                         
         LA    R5,1(R5)                                                         
         LR    R4,R6                                                            
         SR    R4,R5                                                            
         BMR   RE                                                               
         EX    R4,SPECCONT                                                      
         BZ    MOVEALL                                                          
         LA    R4,1(R4)                                                         
         B     MOVESOME                                                         
         EJECT                                                                  
NOUSPECS MVC   P(120),V                                                         
         CLI   SCRNFLAG,C'Y'                                                    
         BE    SCRNLINE                                                         
         B     PRINTOUT                                                         
         SPACE 3                                                                
PRINTOUT CLC   P(2),=C'*-'                                                      
         BNE   *+8                                                              
         MVI   P,C'-'                                                           
         MVI   SPACING+3,C'1'                                                   
         GOTO1 =V(PRINTER)                                                      
         B     READ                                                             
         EJECT                                                                  
*                   XERSTORE'D TEXT                                             
         SPACE 1                                                                
XERLINE  TRT   P(80),LINEEND                                                    
         BNZ   WHOLELIN                                                         
         LA    R4,SAVEDIR                                                       
         ST    R4,PARA+12                                                       
         GOTO1 =V(PANIC),PARA      GET REST OF LINE                             
         TM    PARA+8,X'80'                                                     
         BO    ENDREAD                                                          
         SPACE 1                                                                
         LA    R4,P                                                             
         ST    R4,PARA+12                                                       
         TRT   SAVEDIR(10),LINEEND                                              
         BNZ   XERLINE1                                                         
         MVI   SAVEFLAG,C'Y'                                                    
         MVI   XERTEXT,C' '        NOT XER                                      
         B     NOTXERLN                                                         
XERLINE1 LA    R4,SAVEDIR                                                       
         MVC   P+80(10),SAVEDIR                                                 
         LA    R1,0(R1)                                                         
         SR    R1,R4                                                            
         EX    R1,SHY                                                           
         LA    R1,P+80(R1)                                                      
         SPACE 1                                                                
WHOLELIN CLI   PHASE,1             PRINTING NOW                                 
         BE    PRINTOUT+14           NO - SKIP FORMATTING                       
         SPACE 1                                                                
         LA    R7,0(R1)                                                         
         LA    R4,P                                                             
         SR    R7,R4                                                            
         BZ    READ                                                             
         BAS   RE,TRYTABRT                                                      
         CLI   SCRNFLAG,C'Y'                                                    
         BE    SCRNLINE                                                         
         B     SCREENLN+8                                                       
         SPACE 1                                                                
SHY      TR    P+80(0),TRTABLE                                                  
         EJECT                                                                  
*                  LOOK FOR TABS, USCORES, SPECIALS                             
         SPACE 1                                                                
TRYTABRT NTR1                                                                   
         SR    R1,R1                                                            
         ST    R4,LINESTRT                                                      
         LR    R3,R4                                                            
         SPACE 1                                                                
XUSCLP   EX    R7,TRTREST          ANY USCORES                                  
         BZ    TRYTABS                                                          
         SPACE 1                                                                
         CLI   0(R1),X'23'         EUSC OUT OF SEQUENCE                         
         BNE   *+12                                                             
USCORERR MVI   0(R1),C' '                                                       
         B     XUSCLP                                                           
         MVI   0(R1),C' '          BLANK SUSC                                   
         LA    R5,1(R1)            POINT PAST IT                                
         EX    R7,TRTREST          FIND EUSC                                    
         BZ    TRYTABS                                                          
         CLI   0(R1),X'22'                                                      
         BE    USCORERR                                                         
         LA    R4,0(R1)                                                         
         BCTR  R4,0                                                             
         SR    R4,R5                                                            
         BNP   XUSCONE                                                          
         EX    R4,REQSPACE                                                      
         LR    R3,R5                                                            
         EX    R4,USAVE1           SAVE USCORED TEXT                            
         BCTR  R3,0                                                             
         EX    R4,USTORE           OVERLAY SUSC ON RETURN                       
XUSCCOM  BCTR  R1,0                                                             
         MVC   0(2,R1),=X'9FE6'    WUSC                                         
         L     R3,LINESTRT                                                      
         B     XUSCLP                                                           
         SPACE 1                                                                
XUSCONE  BCTR  R5,0                POINT TO SUSC                                
         MVC   0(1,R5),1(R5)       OVERLAY IT W/SINGLE CHAR                     
         B     XUSCCOM                                                          
         SPACE 1                                                                
TRTREST  TRT   0(0,R3),USCORES                                                  
         SPACE 1                                                                
USAVE1   MVC   REBUILD(0),0(R3)                                                 
         SPACE 1                                                                
USTORE   MVC   0(0,R3),REBUILD                                                  
         EJECT                                                                  
TRYTABS  L     R4,LINESTRT                                                      
         MVC   USCOFFST,=H'0'                                                   
         SPACE 1                                                                
OTHERTAB EX    R7,TABTEST                                                       
         BZ    NOTABS                                                           
         SPACE 1                                                                
         BCTR  R4,0                                                             
         LR    R5,R1                                                            
         SR    R5,R4               TAB DISP + 1                                 
         SH    R5,USCOFFST                                                      
         ST    R1,SAVEREGS                                                      
         LR    R6,R1                                                            
         SH    R6,=H'4'                                                         
         CR    R6,R4                                                            
         BNH   TABLOOP0                                                         
         TRT   0(4,R6),FUNCTION                                                 
         BZ    TABLOOP0                                                         
         SPACE 1                                                                
         L     R1,SAVEREGS                                                      
         LH    R4,USCOFFST                                                      
         AH    R4,=H'2'                                                         
         STH   R4,USCOFFST                                                      
         SH    R5,=H'2'                                                         
         BP    *+6                                                              
         DC    H'0'                                                             
         SPACE 1                                                                
TABLOOP0 LA    R6,TABRACK                                                       
         LA    R3,18                                                            
TABLOOP1 CLC   0(2,R6),=H'0'                                                    
         BE    TABERROR                                                         
         CH    R5,0(R6)                                                         
         BL    TABMOVE             NEXT TAB FOUND                               
         LA    R6,2(R6)              NO - SET FOR NEXT TRY                      
         BCT   R3,TABLOOP1                                                      
         B     TABERROR                                                         
         SPACE 1                                                                
TABMOVE  L     R2,LINESTRT                                                      
         LA    R2,0(R7,R2)         PTR TO LAST TEXT CHAR                        
         SR    R2,R1               L'TEXT' AFTER TAB - 1                        
         EX    R2,TABOUT           SAVE WHAT'S AFTER TAB                        
         LA    R2,1(R2)                                                         
         EX    R2,SPACEIN          BLANK TAB AND ONWARD                         
         BCTR  R2,0                                                             
         L     R1,LINESTRT                                                      
         BCTR  R1,0                                                             
         AH    R1,0(R6)                                                         
         AH    R1,USCOFFST                                                      
         EX    R2,TABBACK          RESTORE FROM SAVE                            
         LA    R7,0(R1,R2)                                                      
         L     R4,LINESTRT                                                      
         SR    R7,R4                                                            
         B     OTHERTAB                                                         
         EJECT                                                                  
TABERROR MVI   0(R1),C' '                                                       
         EX    R7,TABTEST                                                       
         BNZ   *-8                                                              
         SPACE 1                                                                
NOTABS   CLI   SCRNFLAG,C'Y'                                                    
         BNE   TRYSPECS                                                         
         EX    R7,FINDEND                                                       
         BNZ   *+6                                                              
         DC    H'0'                                                             
         LA    R7,0(R1)                                                         
         BCTR  R7,0                                                             
         SR    R7,R4                                                            
         SH    R7,=H'2'            LESS TWO *'S                                 
         STC   R7,LINELEN                                                       
         SPACE 1                                                                
TRYSPECS L     R3,LINESTRT                                                      
         TRT   0(120,R3),SPECIALS                                               
         BZ    TABUSCEX                                                         
         SPACE 1                                                                
         MVC   REBUILD(120),0(R3)                                               
         LR    R4,R1                                                            
         SR    R4,R3               DISP TO 1ST SPEC                             
         LA    R5,REBUILD                                                       
         LA    R6,REBUILD+119                                                   
         BAS   RE,MOVESOME                                                      
         SPACE 1                                                                
TABUSCEX XIT1                                                                   
         SPACE 1                                                                
TABTEST  TRT   0(0,R4),TABCHECK                                                 
         SPACE 1                                                                
TABOUT   MVC   REBUILD(0),1(R1)    EXCLUDE TAB FROM SAVE                        
         SPACE 1                                                                
SPACEIN  MVC   0(0,R1),SPACES      BLANK TAB AND ONWARD                         
         SPACE 1                                                                
TABBACK  MVC   0(0,R1),REBUILD                                                  
         SPACE 1                                                                
FINDEND  TRT   0(0,R4),LINEEND                                                  
         EJECT                                                                  
*                   SCREEN FIELD FORMATTING                                     
         SPACE 1                                                                
SCREENLN CLI   PHASE,1             PRINTING NOW                                 
         BE    PRINTOUT+14           NO - SKIP FORMATTING                       
         CLI   NOSCREEN,C'Y'                                                    
         BE    PRINTOUT              NO - SKIP FORMATTING                       
         SPACE 1                                                                
         CLI   SCRNFLAG,C'Y'                                                    
         BE    SCRNLINE                                                         
         SPACE 1                                                                
         LA    R1,P                                                             
         LA    R3,10               LIMIT FOR 1ST *                              
         SPACE 1                                                                
         CLI   0(R1),C'*'                                                       
         BE    TWOSTARS                                                         
         LA    R1,1(R1)                                                         
         BCT   R3,*-12                                                          
         B     PRINTOUT            NO STARTING *                                
         SPACE 1                                                                
ALLASTER CLC   1(0,R1),0(R1)                                                    
         SPACE 1                                                                
TWOSTARS LA    R4,P+69                                                          
         SR    R4,R1                                                            
         EX    R4,ALLASTER         SCREEN START                                 
         BNE   PRINTOUT                                                         
         SPACE 1                                                                
         LA    R2,P+79                                                          
         CLI   0(R2),C'*'          FIND LAST *                                  
         BE    SCRNSTRT                                                         
         BCT   R2,*-8                                                           
         B     PRINTOUT              NO * IN LAST 10 COLUMNS                    
         EJECT                                                                  
SCRNSTRT MVI   SPACING+3,C'1'                                                   
         MVC   V,SPACES                                                         
         SR    R2,R1               L OF LINE WITH *S - 1                        
         SH    R2,=H'2'            L OF TEXT - 1                                
         LA    R4,1(R1)            PTR TO TEXT START                            
         ST    R4,PSCRLNST                                                      
         STC   R2,PSCRLNST         SAVE L(LINE) - 3 (LESS *'S)                  
         LA    R3,P                                                             
         SR    R1,R3               DISP OF 1ST *                                
         STC   R1,POFFSET                                                       
         LA    R3,V(R1)            START OF FRAME LINE                          
         MVC   0(3,R3),=X'DCF261'                                               
         MVI   3(R3),X'7C'         @ SIGN TO START TOP LINE                     
         MVI   4(R3),C'$'                                                       
         LA    R5,3(R3)            POINT  TO START OF LINE                      
         EX    R2,SPREADEM                                                      
         LA    R5,5(R2,R3)         POINT TO END OF LINE                         
         MVI   0(R5),C'#'          SET END                                      
         MVC   P(100),V                                                         
         GOTO1 =V(PRINTER)         ISSUE START LINE                             
         MVI   SCRNFLAG,C'Y'                                                    
         MVI   0(R3),X'5A'         VERTICAL STROKE                              
         MVC   1(3,R3),=X'DCF161'  SWITCH TO BODY TYPE                          
         LA    R3,4(R3)                                                         
         ST    R3,VSCRLNST         PTR FOR TEXT IN V                            
         LA    R1,V+119                                                         
         SR    R1,R3                                                            
         STC   R1,VSCRLNST         L(MOST OF V) - 1                             
         B     READ                                                             
         SPACE 1                                                                
SCRNLINE L     R4,PSCRLNST                                                      
         L     R3,VSCRLNST                                                      
         CLI   LINELEN,X'00'                                                    
         BE    *+22                                                             
         ZIC   R2,LINELEN                                                       
         ZIC   R1,POFFSET                                                       
         SR    R2,R1                                                            
         B     *+10                                                             
         ZIC   R2,PSCRLNST                                                      
         CLI   P+9,C'*'                                                         
         BNE   DOSCRLIN                                                         
         CLC   P+10(61),P+9        SCREEN END                                   
         BE    SCRNEND                                                          
         EJECT                                                                  
DOSCRLIN LA    R7,0(R3)                                                         
         LA    R7,1(R2,R7)                                                      
         LA    R6,V+119                                                         
         CR    R7,R6                                                            
         BNH   *+8                                                              
         LR    R2,R6                                                            
         SR    R2,R3                                                            
         EX    R2,PTOVSCRN                                                      
         LA    R5,0(R2,R3)                                                      
         MVC   1(4,R5),=X'DCF2615A' END VERTICAL                                
         SPACE 1                                                                
FIELDER  LA    R3,1(R3)                                                         
         CR    R3,R5                                                            
         BNL   PRINTSCL                                                         
         CLI   0(R3),X'7A'         START OF FIELD                               
         BNE   FIELDER                                                          
         CLI   1(R3),C'-'                                                       
         BNE   FIELDER                                                          
         OI    1(R3),X'0D'                                                      
         LA    R3,1(R3)                                                         
         B     *-16                                                             
         SPACE 1                                                                
PRINTSCL MVC   P(120),V                                                         
         GOTO1 =V(PRINTER)                                                      
         L     R3,VSCRLNST                                                      
         ZIC   R2,VSCRLNST                                                      
         EX    R2,CLEARV                                                        
         B     READ                                                             
         SPACE 1                                                                
SPREADEM MVC   2(0,R5),1(R5)       PROPAGATE $ SIGNS                            
         SPACE 1                                                                
PTOVSCRN MVC   0(0,R3),0(R4)                                                    
         SPACE 1                                                                
CLEARV   MVC   0(0,R3),SPACES                                                   
         SPACE 1                                                                
SCRNEND  MVC   V,SPACES                                                         
         SH    R3,=H'4'                                                         
         MVI   0(R3),C'*'          START BOTTOM LINE                            
         MVI   1(R3),C'$'                                                       
         LR    R5,R3                                                            
         ZIC   R2,PSCRLNST                                                      
         EX    R2,SPREADEM         FILL IT                                      
         LA    R5,2(R2,R3)                                                      
         MVI   0(R5),C'+'          END IT                                       
         MVC   1(3,R5),=X'DCF161'  SWITCH TO BODY TYPE                          
         MVC   P(100),V                                                         
         GOTO1 =V(PRINTER)                                                      
         MVI   SCRNFLAG,C' '                                                    
         B     READ                                                             
         EJECT                                                                  
*              ISSUE 'CONTENTS' AND 'FIGURES' IF PRESENT                        
         SPACE 1                                                                
LASRTNES CSECT                                                                  
CONTENTS ST    RE,RETADDR                                                       
         BAS   R7,FORCEODD                                                      
         CLC   POOLLINE,=H'0'      TITLPOOL ENTRIES                             
         BE    CLOSEOUT              NO                                         
         SPACE 1                                                                
         MVI   SKIPSET,C'Y'        FOR ,FEED 0 COMMAND                          
         MVC   P(13),=C',FEED 0_,END '                                          
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         MVC   TITLE(8),=X'C39695A38595A3A2'  CONTENTS U/LC                     
         CLI   SECTNUM,C' '                                                     
         BE    *+10                                                             
         MVC   COLHEADS(8),SECTNAME                                             
         MVC   COLHEADS+25(5),=X'E389A39385'  TITLE U/LC                        
         MVC   COLHEADS+72(4),=X'D7818785'    PAGE  U/LC                        
         OI    MODE,X'10'          NEW COLHEADS                                 
         GOTO1 =V(PRINTER)                                                      
         L     R2,=A(TITLPOOL)                                                  
         LH    R3,POOLLINE                                                      
         SPACE 1                                                                
CONT2    TM    PLTYPE,X'C0'        FIGURE OR INDEX ONLY?                        
         BM    MORECONT              YES                                        
         SPACE 1                                                                
         TM    PLTYPE,X'30'        TITLE OR SUPER                               
         BZ    CONT3                 NO                                         
         SPACE 1                                                                
         ZAP   WORK(2),MAXLINE                                                  
         SP    WORK(2),LINE                                                     
         CP    WORK(2),=P'5'       ROOM FOR IT, SKIPS, AND 2 ENTRIES            
         BNL   CONT2SK               YES - GO SKIP A LINE                       
         BAS   R6,ENDOLDPG           NO - SWITCH TO NEXT PAGE                   
         B     SUPERSW-1                                                        
         SPACE 1                                                                
CONT2SK  GOTO1 =V(PRINTER)                                                      
         SPACE 1                                                                
         TM    PLTYPE,X'20'        TITLE OR SUPER IF MASK IS X'10'              
SUPERSW  EQU   *-3                                                              
         BZ    SKIPLNE                                                          
         SPACE 1                                                                
         CLI   PLCHPT,C' '         CHAPTER NUMBER                               
         BE    SKIPLNE               NO                                         
         LA    R1,P+3                YES                                        
         BAS   RE,TPSECNO          SET IT LEFT-ADJUSTED                         
         EJECT                                                                  
         SPACE 1                                                                
SKIPLNE  LA    R1,P+9                                                           
         MVI   SPACING+3,C'2'                                                   
         MVI   ALPHA,C'C'          CAP FLAG                                     
         B     CONT4                                                            
         SPACE 1                                                                
CONT3    SR    R6,R6               INDENT HEADN LINE TEXT                       
         IC    R6,PLTYPE                                                        
         SLA   R6,1(0)                                                          
         LA    R1,P+9(R6)                                                       
         SPACE 1                                                                
CONT4    MVC   0(58,R1),0(R2)                                                   
         CLI   ALPHA,C'C'                                                       
         BNE   *+14                                                             
         MVI   ALPHA,C' '                                                       
         OC    0(58,R1),SPACES     CAP THE SUPER OR TITLE                       
         BAS   R7,CONTNTLN         ISSUE CONTENTS LINE                          
         SPACE 1                                                                
MORECONT LA    R2,96(R2)                                                        
         BCT   R3,CONT2                                                         
         SPACE 1                                                                
         B     FIGURES                                                          
         SPACE 1                                                                
*              FORMAT CONTENTS OR FIGURES LINE                                  
         SPACE 1                                                                
CONTNTLN LA    R1,P+67             FILLUP PREP                                  
         LA    R0,30                                                            
         LA    R4,2                                                             
         SPACE 1                                                                
FILLUP   CLC   0(2,R1),=C'  '                                                   
         BNE   PGNUMBER                                                         
         MVC   0(2,R1),=C' .'                                                   
         SR    R1,R4                                                            
         BCT   R0,FILLUP                                                        
         SPACE 1                                                                
PGNUMBER EDIT  (P2,PLPGNO),(3,P+73)                                             
         CLI   PLCHPT,C' '         SECTIONAL PAGE NUMBERING                     
         BE    PUTLINE               NO                                         
         SPACE 1                                                                
         LA    R1,P+73                                                          
         CLI   1(R1),C' '                                                       
         BE    *+8                                                              
         BCT   R1,*-8                                                           
         MVI   DASHFLAG,C'Y'                                                    
         BAS   RE,TPSECNO          SET CHAP NUM LEFT ADJUSTED                   
         SPACE 1                                                                
PUTLINE  GOTO1 =V(PRINTER)                                                      
         MVI   SPACING+3,C'1'                                                   
         BR    R7                                                               
         EJECT                                                                  
FIGURES  CLI   FIGSTODO,C'Y'       CAPTIONS                                     
         BNE   ANYIND6               NO                                         
         SPACE 1                                                                
         OI    MODE,X'0A'          CLOSE OUT CONTENTS                           
         GOTO1 =V(PRINTER)                                                      
         MVC   TITLE(10),SPACES                                                 
         MVC   TITLE(8),FIGNAME                                                 
         MVC   LOFTITLE,=H'0'                                                   
         MVC   COLHEADS(8),TITLE                                                
         OI    MODE,X'10'          NEW COLHEADS                                 
         LA    R1,TITLE                                                         
         AH    R1,LOFFIGNM                                                      
         MVI   0(R1),X'A2'                                                      
         GOTO1 =V(PRINTER)                                                      
         L     R2,=A(TITLPOOL)                                                  
         LH    R3,POOLLINE                                                      
         SPACE 1                                                                
FIGTEST  TM    PLTYPE,X'40'        CAPTION                                      
         BZ    NEXTLINE              NO - TRY NEXT                              
         SPACE 1                                                                
         CLI   PLCHPT,C' '         PREFIX                                       
         BNE   PREFIX                YES                                        
         SPACE 1                                                                
         EDIT  (P2,CAPNUM),(3,P+2)   NO                                         
         B     SETFIG                                                           
         SPACE 1                                                                
PREFIX   CLC   ALPHA,PLCHPT        SAME AS LAST PREFIX                          
         BE    *+16                  YES                                        
         GOTO1 =V(PRINTER)           NO  - SKIP A LINE                          
         MVC   ALPHA,PLCHPT                SAVE NEW PREFIX                      
         SPACE 1                                                                
         MVI   DASHFLAG,C'Y'                                                    
         LA    R1,P+2                                                           
         BAS   RE,TPSECNO                                                       
         EDIT  (P2,CAPNUM),(3,(R1)),ALIGN=LEFT                                  
         SPACE 1                                                                
SETFIG   MVC   P+9(58),0(R2)                                                    
         BAS   R7,CONTNTLN                                                      
         SPACE 1                                                                
NEXTLINE LA    R2,96(R2)                                                        
         BCT   R3,FIGTEST                                                       
         B     ANYIND6                                                          
         EJECT                                                                  
*              SORT AND PRINT THE INDEX                                         
         SPACE 1                                                                
ANYIND   ST    RE,RETADDR                                                       
         BAS   R7,FORCEODD                                                      
         L     R1,=V(PRINT)                                                     
         MVC   0(2,R1),BORROW      RESET SELECTIVE ISSUE                        
         CLI   SKIPSET,C'Y'                                                     
         BE    *+24                                                             
         MVI   SKIPSET,C'Y'        FOR ,FEED 0 COMMAND                          
         MVC   P(13),=C',FEED 0_,END '                                          
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         CLC   POOLLINE,=H'0'      TITLPOOL ENTRIES                             
         BE    CLOSEOUT              NO                                         
         SPACE 1                                                                
         MVC   TITLE(5),=X'C9958485A7'  INDEX U/LC                              
         CLI   SECTNUM,C' '        SECTION NUMBERING                            
         BE    ANYIND1               NO                                         
         MVI   SECTNUM,C'I'          YES                                        
         ZAP   PAGE,=P'1'                                                       
ANYIND1  L     R2,=A(TITLPOOL)                                                  
         LH    R3,POOLLINE                                                      
         GOTO1 =V(XSORT),PARA2,(R2),(R3),96,32,61                               
         B     FIRSTIND                                                         
         SPACE 1                                                                
ANYIND2  CLC   ALPHA,SRTTXT                                                     
         BE    ANYIND3                                                          
         GOTO1 =V(PRINTER)         SKIP LINE BEFORE NEW 1ST LETTER              
FIRSTIND MVC   ALPHA,SRTTXT                                                     
         SPACE 1                                                                
ANYIND3  BAS   R7,INDEXLN          ISSUE INDEX LINE                             
         LA    R2,96(R2)                                                        
         BCT   R3,ANYIND2                                                       
         SPACE 1                                                                
*                   END 'CONTENTS' AND 'FIGURES' OR 'INDEX'                     
         SPACE 1                                                                
ANYIND6  OI    MODE,X'0A'                                                       
         GOTO1 =V(PRINTER)                                                      
         MVI   ALPHA,C' '                                                       
         BAS   R7,FORCEODD                                                      
CLOSEOUT MVI   SECTNUM,C' '                                                     
         TM    MODE,1                                                           
         BZ    INDXT                                                            
         XI    MODE,1              RESET FRONT-MATTER BIT                       
         ZAP   FIGNO,=P'0'                                                      
         ZAP   PAGE,=P'1'                                                       
INDXT    L     RE,RETADDR                                                       
         BR    RE                                                               
         EJECT                                                                  
*                   FORMAT INDEX LINE                                           
         SPACE 1                                                                
INDEXLN  MVC   P+5(58),0(R2)                                                    
         LA    R4,P+65             LIMIT FOR SAME-LINE PAGE NUMBERS             
         LA    R6,58               LIMIT FOR FINDING IX-LINE LENGTH             
         LA    R1,P+62             LAST POSSIBLE IX-LINE CHAR                   
         CLI   0(R1),C' '                                                       
         BNE   *+12                END OF IX-LINE TEXT                          
         BCTR  R1,0                                                             
         BCT   R6,*-10                                                          
         BR    R7                  BLANK INDEX LINE                             
         SPACE 1                                                                
         LA    R1,3(R1)            LEAVE TWO TRAILING SPACES                    
IXPGNUM  TM    PLTYPE,X'40'        FIGURE                                       
         BZ    IXNOTFIG              NO                                         
         SPACE 1                                                                
         MVI   0(R1),C'('            YES - TAG IT                               
         MVC   1(3,R1),FIGNAME                                                  
         MVC   4(3,R1),=C'.) '                                                  
         LA    R1,7(R1)              BUMP PAST TAG                              
         SPACE 1                                                                
IXNOTFIG CLI   PLCHPT,C' '         SECTIONAL PAGE NUMBERING                     
         BE    SETIXPG               NO                                         
         BAS   RE,IXSECNO                                                       
         SPACE 1                                                                
SETIXPG  EDIT  (P2,PLPGNO),(3,0(R1)),ALIGN=LEFT                                 
         LA    R1,2(R1)                                                         
         CLI   0(R1),C' '                                                       
         BNE   *+8                                                              
         BCT   R1,*-8                                                           
         LA    R1,1(R1)            POINT PAST PAGE NO.                          
         SPACE 1                                                                
         CLC   CAPTEXT,96(R2)      SAME TEXT ON NEXT INDEX LINE                 
         BNE   PUTLINE               NO                                         
         SPACE 1                                                                
         CR    R1,R4                 YES - ROOM FOR PG. NO.                     
         BH    PUTLINE                       NO                                 
         SPACE 1                                                                
         MVI   0(R1),C','                    YES - AFFIX , FOR NEXT             
         LA    R1,1(R1)                            POINT PAST IT                
         LA    R2,96(R2)           POINT TO MATCHING IX LINE                    
         BCT   R3,IXPGNUM          REDUCE COUNTER AND LOOP                      
         SPACE 1                                                                
         BCTR  R1,0                MATCH BUT POOL EMPTIED                       
         MVI   0(R1),C' '                                                       
         LA    R7,ANYIND6                                                       
         B     PUTLINE                                                          
         EJECT                                                                  
*                   HANDLE SECTION NUMBERS                                      
         SPACE 1                                                                
LASPRINT CSECT                                                                  
DOSUPNUM ST    RE,SECNORET                                                      
         MVC   P-1(1),SECTNUM                                                   
         B     IXSECNO+14                                                       
         SPACE 1                                                                
IXSECNO  ST    RE,SECNORET                                                      
         MVC   P-1(1),PLCHPT                                                    
         MVI   DASHFLAG,C'Y'                                                    
         TM    P-1,X'F0'           NUMERIC                                      
         BO    ONEDIGIT              YES                                        
         B     SETSUPNO              NO                                         
         SPACE 1                                                                
TPSECNO  ST    RE,SECNORET                                                      
         MVC   P-1(1),PLCHPT                                                    
         TM    P-1,X'F0'           NUMERIC                                      
         BO    ONEDIGIT              YES                                        
         BCTR  R1,0                  NO - BACK POINTER BY 1                     
SETSUPNO NI    P-1,X'0F'                  BLANK ZONE BITS                       
         XR    R6,R6                      ZERO REGISTER                         
         IC    R6,P-1                                                           
         BCTR  R6,0                                                             
         SLA   R6,1                                                             
         LA    R6,DIGTAB(R6)                                                    
         MVC   0(2,R1),0(R6)                                                    
         LA    R1,2(R1)                                                         
         B     DASHED                                                           
         SPACE 1                                                                
ONEDIGIT MVC   0(1,R1),P-1                                                      
         LA    R1,1(R1)                                                         
DASHED   CLI   DASHFLAG,C'Y'                                                    
         BNE   *+16                                                             
         MVI   0(R1),C'-'                                                       
         LA    R1,1(R1)                                                         
         MVI   DASHFLAG,C' '                                                    
         L     RE,SECNORET                                                      
         BR    RE                                                               
         SPACE 1                                                                
*                   ERROR ROUTINE FOR TITLPOOL FULL                             
         SPACE 1                                                                
POOLFULL L     R1,=V(PRINT)                                                     
         MVC   0(2,R1),BORROW      RESTORE PRINTING                             
         MVC   P(45),=C',MOD_QUA 0_-            HAS TOO MANY CONTENT-'          
         MVC   P+12(10),C                                                       
         MVC   P+45(33),=C'INDEX ENTRIES -- SKIPPING IT_,END'                   
         B     OTHERERR                                                         
         EJECT                                                                  
*         WIDE-PAGE ROUTINES                                                    
         SPACE 1                                                                
LASRTNES CSECT                                                                  
STFLGTST CLI   STARTFLG,C'Y'       SAVED START                                  
         BNE   READ                                                             
         MVI   STARTFLG,C' '         YES - RESET FLAG                           
NOTSHT2  MVC   P+11(58),C+11                                                    
         SPACE 1                                                                
PAGESTRT MVI   PAGEMODE,C'Y'                                                    
         MVC   COLHEADS,SPACES                                                  
         CLC   P+11(8),SPACES      FIGURE TITLE                                 
         BE    PGSTRT0               NO                                         
         SPACE 1                                                                
         MVC   INDEX(58),P+11        YES - SAVE IT                              
         MVI   FIGFLAG,C'Y'                FLAG IT                              
         MVI   FIGSTODO,C'Y'                                                    
         AP    FIGNO,=P'1'                                                      
         SPACE 1                                                                
PGSTRT0  BAS   R6,ENDOLDPG                                                      
         CLI   PHASE,1                                                          
         BNE   PAGE0                                                            
         SPACE 1                                                                
         CLI   FIGFLAG,C'Y'                                                     
         BNE   READ                                                             
         SPACE 1                                                                
         MVI   FIGFLAG,C'N'                                                     
         MVC   SCANWORD(7),SPACES                                               
         BAS   R1,DOPOOLLN                                                      
         B     READ                                                             
         SPACE 1                                                                
PAGE0    CLI   FIGFLAG,C'Y'                                                     
         BNE   PAGE1                                                            
         SPACE 1                                                                
         LA    R1,INDEX                                                         
         LA    R2,58(R1)                                                        
         CLI   0(R2),C' '                                                       
         BH    *+8                                                              
         BCT   R2,*-8                                                           
         SR    R2,R1                                                            
         STH   R2,LOFCAPT                                                       
         SPACE 1                                                                
PAGE1    L     R2,=A(PAGEBLOC)     CLEAR PAGEBLOC                               
         LA    R3,54      54 IS SAME AS PAGELIM                                 
         XC    LENRACK,LENRACK                                                  
PGCLEAR  MVC   0(132,R2),SPACES                                                 
         LA    R2,132(R2)                                                       
         BCT   R3,PGCLEAR                                                       
         B     READ                                                             
         SPACE 1                                                                
LOFCAPT  DS    H                   CAPTION LENGTH - 1                           
         EJECT                                                                  
PAGELINE CLI   PHASE,1                                                          
         BE    READ                                                             
         SPACE 1                                                                
         MVC   WORK(2),=2X'F0'     LINE NUMBER                                  
         MVZ   WORK(2),P+1                                                      
         CLC   WORK(2),=2X'F0'                                                  
         BNE   READ                                                             
         SPACE 1                                                                
         PACK  DUB,P+1(2)                                                       
         CVB   R3,DUB                                                           
         CH    R3,PAGELIM                                                       
         BH    READ                                                             
         SPACE 1                                                                
         LTR   R3,R3                                                            
         BZ    READ                                                             
         SPACE 1                                                                
         BCTR  R3,0                                                             
         CLI   XERTEXT,C' '                                                     
         BE    *+14                                                             
         LA    R4,LENRACK(R3)                                                   
         MVC   0(1,R4),P+69                                                     
         MH    R3,=H'132'                                                       
         L     R2,=A(PAGEBLOC)                                                  
         AR    R2,R3                                                            
         CLI   P,C'R'                                                           
         BNE   *+8                                                              
         LA    R2,66(R2)                                                        
         MVC   0(66,R2),P+3                                                     
         B     READ                                                             
         SPACE 1                                                                
PAGEEND  CLI   PHASE,1             FIRST READ                                   
         BNE   *+8                                                              
         B     TESTCNTD                                                         
         SPACE 1                                                                
         MVI   MODE,X'20'          START (OR CONTINUE) WIDE PAGE                
         GOTO1 =V(PRINTER)                                                      
         CLI   SKIPSET,C'Y'                                                     
         BE    EVENODD                                                          
         GOTO1 =V(PRINT),PARA2,SPACES-1,=C'BC01'                                
EVENODD  MVI   SKIPSET,C' '                                                     
         CLI   ONESIDE,C'Y'        RECTO PGS ONLY                               
         BE    STODDWPG              YES                                        
         TM    PAGE+1,X'10'          NO  - RECTO NOW                            
         BO    STODDWPG                      YES                                
         BAS   R4,CAPTION                    NO  - CAPT OR TITLE                
         GOTO1 =V(PRINT),PARA2,P-1,=C'BL03'                                     
         B     PGENDLP0                                                         
         SPACE 1                                                                
STODDWPG GOTO1 =V(PRINT),PARA2,SPACES-1,=C'BL03'                                
         EJECT                                                                  
PGENDLP0 L     R2,=A(PAGEBLOC)                                                  
         LA    R3,0                                                             
         ZIC   R6,WPOFFSET                                                      
         MVC   P,SPACES                                                         
         LA    R6,P(R6)                                                         
         SPACE 1                                                                
PGENDLP  CLI   XERTEXT,C' '                                                     
         BE    NONXERPG                                                         
         SPACE 1                                                                
         MVC   0(132,R6),0(R2)                                                  
         LR    R4,R6                                                            
         BAS   RE,TRYTABRT                                                      
         B     PUTPGLIN                                                         
         SPACE 1                                                                
NONXERPG MVC   0(132,R6),0(R2)                                                  
PUTPGLIN GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         LA    R2,132(R2)                                                       
         LA    R3,1(R3)                                                         
         CH    R3,PAGELIM                                                       
         BL    PGENDLP                                                          
         SPACE 1                                                                
         CLI   ONESIDE,C'Y'        RECTO PGS ONLY                               
         BE    CAPFOOT               YES                                        
         TM    PAGE+1,X'10'          NO  - RECTO NOW                            
         BO    CAPFOOT                                                          
         B     TESTCNTD                      NO                                 
         SPACE 1                                                                
CAPFOOT  GOTO1 =V(PRINT),PARA2,SPACES-1,=C'BL02'                                
         BAS   R4,CAPTION                                                       
         GOTO1 =V(PRINT),PARA1                                                  
         MVC   P,SPACES                                                         
         B     TESTCNTD                                                         
         SPACE 1                                                                
MOVETOP  MVC   0(0,R6),0(R2)                                                    
         EJECT                                                                  
CAPTION  MVC   P,SPACES                                                         
         CLI   FIGFLAG,C'Y'        CAPTION AVAILABLE                            
         BNE   NOTCAPT               NO                                         
         SPACE 1                                                                
         MVC   SAVEDIR,SPACES                                                   
         LA    R1,SAVEDIR                                                       
         MVC   0(7,R1),FIGNAME                                                  
         LA    R1,1(R1)                                                         
         AH    R1,LOFFIGNM                                                      
         CLI   FIGPTNUM,C'Y'                                                    
         BNE   *+18                                                             
         SPACE 1                                                                
         MVC   0(8,R1),SVFIGNO                                                  
         AH    R1,SVFNOLEN                                                      
         B     FIGEDIT                                                          
         SPACE 1                                                                
         CLI   SECTNUM,C' '        SECTION NUMBER                               
         BE    *+12                  NO                                         
         MVI   DASHFLAG,C'Y'                                                    
         BAS   RE,DOSUPNUM                                                      
FIGEDIT  EDIT  (P2,FIGNO),(3,(R1)),ALIGN=LEFT                                   
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '          FIND END OF FIGNO                            
         BNE   *-8                   NO                                         
         CLI   CTDFLAG,C'Y'        CONTINUED WIDE PAGE                          
         BNE   *+14                  NO                                         
         MVC   1(8,R1),=X'4DC39695A37D845D'  (CONT'D) U/LC                      
         LA    R1,9(R1)                                                         
         MVI   0(R1),C'.'            YES                                        
         LA    R1,3(R1)                                                         
         LH    R3,LOFCAPT                                                       
         EX    R3,MOVECAP                                                       
         AR    R1,R3                                                            
         LA    R1,1(R1)                                                         
         LA    R0,120                                                           
         LA    R2,P                                                             
         BAS   R7,CENTLP                                                        
         B     NUMPAGE                                                          
         SPACE 1                                                                
MOVECAP  MVC   0(0,R1),INDEX                                                    
         EJECT                                                                  
NOTCAPT  MVC   P+10(62),TITLE                                                   
         SPACE 1                                                                
NUMPAGE  CLI   REPFLAG,C'Y'                                                     
         BNE   NUMCPTLN                                                         
         SPACE 1                                                                
         CP    FIGNO,LASTFIG                                                    
         BL    NUMCPTLN                                                         
         BAS   R7,FIGNOSET                                                      
NUMCPTLN EDIT  (P2,PAGE),(3,P+128)                                              
         LA    R1,P+128                                                         
         CLI   1(R1),C' '                                                       
         BE    *+8                                                              
         BCT   R1,*-8                                                           
         CLI   POINTNUM,C'Y'                                                    
         BE    PTNMPGE                                                          
         SPACE 1                                                                
         CLI   SECTNUM,C' '        SECTIONAL PG NOS                             
         BER   R4                    NO - RETURN                                
         TM    SECTNUM,X'F0'                                                    
         BO    *+6                                                              
         BCTR  R1,0                                                             
         MVI   DASHFLAG,C'Y'                                                    
         BAS   RE,DOSUPNUM                                                      
         BR    R4                                                               
         SPACE 1                                                                
PTNMPGE  MVI   1(R1),C'.'                                                       
         SH    R1,SVPNOLEN                                                      
         LA    R1,1(R1)                                                         
         LH    R2,SVPNOLEN                                                      
         BCTR  R2,0                                                             
         EX    R2,MVCAPPG                                                       
         BR    R4                                                               
         SPACE 1                                                                
MVCAPPG  MVC   0(0,R1),PGNOWORK                                                 
         SPACE 1                                                                
TESTCNTD MVI   CTDFLAG,C' '                                                     
         MVI   PAGEMODE,C'N'                                                    
         CLI   STARTFLG,C'Y'                                                    
         BE    STARTSVE                                                         
         SPACE 1                                                                
WPCNTDLP GOTO1 =V(PANIC),PARA                                                   
         TM    PARA+8,X'80'                                                     
         BO    ENDREADW                                                         
         SPACE 1                                                                
         CLI   P,X'2B'                                                          
         BNE   *+12                                                             
         BAS   R6,DOTABS                                                        
         B     WPCNTDLP                                                         
         EJECT                                                                  
         CLC   P+9(6),=C'EJECT '                                                
         BE    WPCNTDLP                                                         
         SPACE 1                                                                
         CLC   P+9(6),=C'SPACE '                                                
         BE    WPCNTDLP                                                         
         SPACE 1                                                                
         CLC   P(10),=C'PAGE=START'                                             
         BE    SHEET1                                                           
         SPACE 1                                                                
         CLI   XERTEXT,C'Y'                                                     
         BNE   NONXER                                                           
         SPACE 1                                                                
         TRT   P(2),LINEEND                                                     
         BNZ   WPCNTDLP                                                         
         B     NONXER+10                                                        
         SPACE 1                                                                
NONXER   CLC   P(80),SPACES                                                     
         BE    WPCNTDLP                                                         
         SPACE 1                                                                
         MVI   FIGFLAG,C'N'          NO                                         
         MVI   MODE,X'0C'                                                       
         GOTO1 =V(PRINTER)                                                      
         B     READON                                                           
         SPACE 1                                                                
STARTSVE MVI   STARTFLG,C' '                                                    
         MVI   MODE,X'02'                                                       
         CLC   C+11(8),SPACES      SAVED CAPTION                                
         BNE   NOTSHT2               YES                                        
         SPACE 1                                                                
SHEET2   MVI   CTDFLAG,C'Y'          NO  - DO 2ND SHEET CAPTION                 
         MVI   PAGEMODE,C'Y'                                                    
         BAS   R6,ENDOLDPG                                                      
         B     PAGE0                                                            
         SPACE 1                                                                
SHEET1   MVI   MODE,X'02'                                                       
         CLC   P+11(8),SPACES      NEW CAPTION                                  
         BNE   PAGESTRT              YES                                        
         B     SHEET2                NO                                         
         SPACE 1                                                                
ENDREADW MVI   MODE,X'0C'                                                       
         GOTO1 =V(PRINTER)                                                      
         B     ENDREAD                                                          
         EJECT                                                                  
LASPRINT CSECT                                                                  
ENDOLDPG TM    MODE,X'02'          END-PAGE PENDING                             
         BO    *+14                  YES - GO DO IT                             
         TM    MODE,X'08'            NO  - START-PAGE PENDING                   
         BOR   R6                            YES - GET OUT                      
         SPACE 1                                                                
         OI    MODE,X'0A'                    NO  - END OLD, SET START           
         GOTO1 =V(PRINTER)                                                      
         BR    R6                                                               
         SPACE 2                                                                
FORCEODD BAS   R6,ENDOLDPG                                                      
         SPACE 1                                                                
         CLI   PHASE,0             PRINTING ENDPGNAM                            
         BE    CLEANUP                                                          
         SPACE 1                                                                
         TM    PAGE+1,X'10'        EVEN-ODD                                     
         BO    CLEANUP               ODD NOW - GET OUT                          
         OI    MODE,X'02'            NO  - PRINT EVEN-PG FORM                   
         GOTO1 =V(PRINTER)                                                      
         MVI   SKIPSET,C' '                                                     
         SPACE 1                                                                
CLEANUP  MVC   TITLE,SPACES                                                     
         MVC   COLHEADS,SPACES                                                  
         MVC   LOFTITLE,=H'0'                                                   
         BR    R7                                                               
         EJECT                                                                  
*                   UPDATE-MODE SUBROUTINES                                     
         SPACE 1                                                                
LASRTNES CSECT                                                                  
ENDPRIOR ST    R6,SAVEREGS                                                      
         BAS   R7,FORCEODD                                                      
         MVI   SECTNUM,C' '                                                     
         ZAP   PAGE,=P'999'                                                     
         ZAP   SVNUM,=P'0'                                                      
         ZAP   FIGNO,=P'0'                                                      
         XC    UPERRNUM,UPERRNUM                                                
         MVI   REPFLAG,C' '                                                     
         MVI   POINTNUM,C' '                                                    
         MVI   FIGPTNUM,C' '                                                    
         ZAP   LASTFIG,=P'0'                                                    
         ZAP   LASTPG,=P'0'                                                     
         MVC   REVDATE,SPACES                                                   
         LA    R3,P+10                                                          
         LA    R4,69                                                            
         LA    R0,P+79                                                          
         EX    R4,TERMSCAN                                                      
         L     R6,SAVEREGS                                                      
         BR    R6                                                               
         SPACE 1                                                                
TERMSCAN TRT   0(0,R3),TERMTRT                                                  
         SPACE 1                                                                
TERMFIND LA    R3,1(R1)            FIND NEXT COMMA OR BLANK                     
         LR    R4,R0                                                            
         SR    R4,R3                                                            
         EX    R4,TERMSCAN                                                      
         BR    R6                                                               
         SPACE 1                                                                
INTERSET LA    R3,1(R1)            FIND NEXT = OR -                             
         LR    R4,R0                                                            
         SR    R4,R3                                                            
         EX    R4,TRTINTER                                                      
         BR    R6                                                               
         SPACE 1                                                                
TRTINTER TRT   0(0,R3),INTERTRT                                                 
         EJECT                                                                  
*                   UPDATING MAIN LINE                                          
         SPACE 1                                                                
UPDATING CLC   P+7(3),=C'REP'      REPLACE PAGES                                
         BE    REPSTART              YES                                        
         SPACE 1                                                                
         CLC   P+7(3),=C'INS'      INSERT PAGES                                 
         BE    INSSTART                                                         
         SPACE 1                                                                
         LA    R6,READ                                                          
         CLC   P+7(3),=C'END'      END OF UPDATE                                
         BE    ENDPRIOR              YES - DO SO THEN READ NEXT LINE            
         SPACE 1                                                                
         MVC   UPERRNUM,=H'1'        NO  - FLUSH UPDATE SERIES                  
         B     UPFLUSH                                                          
         SPACE 3                                                                
*                   REPLACE MAIN LINE                                           
         SPACE 1                                                                
REPSTART BAS   R6,ENDPRIOR                                                      
         BZ    UPFLUSH                                                          
         MVI   REPFLAG,C'Y'                                                     
         SPACE 1                                                                
REPSCLP  BAS   R6,INTERSET                                                      
         BZ    ENDREPST                                                         
         SPACE 1                                                                
         CLC   0(2,R3),=C'CH'      CHAP=                                        
         BE    REPCHAP                                                          
         CLC   0(2,R3),=C'PA'      PAGES=                                       
         BE    REPPGNOS                                                         
         CLC   0(2,R3),=C'FI'      FIGURES=                                     
         BE    REPFIGS                                                          
         CLC   0(2,R3),=C'LA'      LASTFIG=                                     
         BE    REPLFIG                                                          
         CLC   0(2,R3),=C'RE'      REVISED=                                     
         BE    REPREV                                                           
         MVC   UPERRNUM,=H'2'                                                   
         B     UPFLUSH                                                          
         SPACE 1                                                                
ENDREPST CP    PAGE,=P'999'                                                     
         BNE   UPCOMTST                                                         
         MVC   UPERRNUM,=H'8'                                                   
         B     UPFLUSH                                                          
         EJECT                                                                  
UPCOMTST GOTO1 =V(PANIC),PARA      READ NEXT LINE                               
         TM    PARA+8,X'80'                                                     
         BO    ENDREAD             END OF BOOK                                  
         SPACE 1                                                                
         LA    R2,P+9                                                           
         MVC   SCANWORD(7),0(R2)                                                
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'TITLE '''                                         
         BE    TITLELN                                                          
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'SUPER '''                                         
         BE    SUPER                                                            
         SPACE 1                                                                
         CLC   SCANWORD(7),=C'RFOOT '''                                         
         BE    RFOOTRTN                                                         
         SPACE 1                                                                
         MVC   UPERRNUM,=H'9'                                                   
         B     UPFLUSH                                                          
         SPACE 1                                                                
RFOOTRTN LA    RE,RFOOTEND                                                      
         ST    RE,RETADDR                                                       
         MVC   SCANWORD(5),=C'SUPER '''                                         
         LA    R3,TITLE                                                         
         B     SCAN5                                                            
         SPACE 1                                                                
RFOOTEND EX    R4,FOOTSPC                                                       
         AH    R3,LOFTITLE                                                      
         MVC   0(2,R3),=X'9FE6'    NON-TRANSP WORD USCORE                       
         LH    R3,LOFTITLE                                                      
         AH    R3,=H'2'                                                         
         STH   R3,LOFTITLE                                                      
         B     READ                                                             
         SPACE 1                                                                
FOOTSPC  TR    0(0,R3),REQSPC                                                   
         EJECT                                                                  
REPCHAP  BAS   R7,UPCHAP                                                        
         B     REPSCLP                                                          
         SPACE 1                                                                
REPPGNOS BAS   R7,TWONUMS                                                       
         TM    NUM1+1,X'10'        ODD NUMBER                                   
         BO    *+14                  YES                                        
         MVC   UPERRNUM,=H'4'        NO                                         
         B     UPFLUSH                                                          
         SPACE 1                                                                
         TM    NUM+1,X'10'                                                      
         BNO   *+14                                                             
         MVC   UPERRNUM,=H'5'                                                   
         B     UPFLUSH                                                          
         SPACE 1                                                                
         CP    NUM1,NUM                                                         
         BL    *+14                                                             
         MVC   UPERRNUM,=H'6'                                                   
         B     UPFLUSH                                                          
         SPACE 1                                                                
         ZAP   PAGE,NUM1                                                        
         ZAP   LASTPG,NUM                                                       
         B     REPSCLP                                                          
         SPACE 1                                                                
REPFIGS  BAS   R7,TWONUMS                                                       
         CP    NUM,NUM1                                                         
         BNL   *+14                                                             
         MVC   UPERRNUM,=H'7'                                                   
         B     UPFLUSH                                                          
         SPACE 1                                                                
         ZAP   FIGNO,NUM1                                                       
         SP    FIGNO,=P'1'                                                      
         ZAP   LASTFIG,NUM                                                      
         B     REPSCLP                                                          
         SPACE 1                                                                
REPLFIG  BAS   R7,UPLFIG                                                        
         B     REPSCLP                                                          
         SPACE 1                                                                
REPREV   BAS   R7,UPREV                                                         
         B     REPSCLP                                                          
         EJECT                                                                  
*                   INSERT MAIN LINE                                            
         SPACE 1                                                                
INSSTART BAS   R6,ENDPRIOR                                                      
         BZ    UPFLUSH                                                          
         SPACE 1                                                                
INSSCLP  BAS   R6,INTERSET                                                      
         BZ    ENDINSST                                                         
         SPACE 1                                                                
         CLC   0(2,R3),=C'CH'      CHAP=                                        
         BE    INSCHAP                                                          
         CLC   0(2,R3),=C'AF'      AFTERPG=                                     
         BE    INSPAGE                                                          
         CLC   0(2,R3),=C'LA'      LASTFIG=                                     
         BE    INSLFIG                                                          
         CLC   0(2,R3),=C'RE'      REVISED=                                     
         BE    INSREV                                                           
         MVC   UPERRNUM,=H'2'                                                   
         B     UPFLUSH                                                          
         SPACE 1                                                                
ENDINSST CP    PAGE,=P'999'                                                     
         BNE   *+14                                                             
         MVC   UPERRNUM,=H'10'                                                  
         B     UPFLUSH                                                          
         SPACE 1                                                                
         LA    R1,PGNOWORK                                                      
         CLI   SECTNUM,C' '        SECTION NUMBER                               
         BE    *+12                  NO                                         
         MVI   DASHFLAG,C'Y'                                                    
         BAS   RE,DOSUPNUM                                                      
         EDIT  (P2,SVNUM),(3,(R1)),ALIGN=LEFT                                   
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '                                                       
         BNE   *-8                                                              
         LA    R2,PGNOWORK                                                      
         SR    R1,R2                                                            
         STH   R1,SVPNOLEN                                                      
         MVI   POINTNUM,C'Y'                                                    
         B     UPCOMTST                                                         
         EJECT                                                                  
INSCHAP  BAS   R7,UPCHAP                                                        
         B     INSSCLP                                                          
         SPACE 1                                                                
INSPAGE  BAS   R6,TERMFIND                                                      
         BAS   R6,GETNUM                                                        
         TM    NUM+1,X'10'         EVEN                                         
         BZ    *+10                  YES                                        
         AP    NUM,=P'1'             NO - MAKE IT EVEN                          
         ZAP   PAGE,=P'1'                                                       
         MVI   POINTNUM,C'Y'                                                    
         ZAP   SVNUM,NUM                                                        
         B     INSSCLP                                                          
         SPACE 1                                                                
INSLFIG  BAS   R7,UPLFIG                                                        
         B     INSSCLP                                                          
         SPACE 1                                                                
INSREV   BAS   R7,UPREV                                                         
         B     INSSCLP                                                          
         SPACE 1                                                                
*                   PRESET .NUMBERING FOR FIGURES                               
         SPACE 1                                                                
FIGNOSET STM   R0,R6,SAVEREGS      SET FOR .NO'ED FIGURES                       
         LA    R1,SVFIGNO                                                       
         CLI   SECTNUM,C' '        SECTION NUMBER                               
         BE    *+12                  NO                                         
         MVI   DASHFLAG,C'Y'                                                    
         BAS   RE,DOSUPNUM                                                      
         EDIT  (P2,FIGNO),(3,(R1)),ALIGN=LEFT                                   
         LA    R1,1(R1)                                                         
         CLI   0(R1),C' '          FIND END OF FIGNO                            
         BNE   *-8                   NO                                         
         MVI   0(R1),C'.'                                                       
         LA    R0,SVFIGNO-1                                                     
         SR    R1,R0                                                            
         STH   R1,SVFNOLEN                                                      
         MVI   FIGPTNUM,C'Y'                                                    
         ZAP   FIGNO,=P'0'                                                      
         LM    R0,R6,SAVEREGS                                                   
         BR    R7                                                               
         EJECT                                                                  
UPCHAP   BAS   R6,TERMFIND                                                      
         TM    0(R3),X'F0'         1ST OR ONLY NUMERIC                          
         BO    UPCHAPN               YES                                        
         SPACE 1                                                                
UPCHAPX  MVC   SECTNUM,0(R3)         NO - ASSUME ALPHA A-H                      
         BR    R7                                                               
         SPACE 1                                                                
UPCHAPN  TM    1(R3),X'F0'         2ND NUMERIC                                  
         BNO   UPCHAPX               NO                                         
         NI    1(R3),X'0F'           YES                                        
         IC    R2,1(R3)                                                         
         LA    R2,1(R2)                                                         
         STC   R2,SECTNUM                                                       
         OI    SECTNUM,X'C0'                                                    
         BR    R7                                                               
         SPACE 1                                                                
TWONUMS  BAS   R6,INTERSET                                                      
         BAS   R6,GETNUM                                                        
         MVC   NUM1,NUM                                                         
         BAS   R6,TERMFIND                                                      
         BAS   R6,GETNUM                                                        
         BR    R7                                                               
         SPACE 1                                                                
UPREV    LA    R3,1(R1)                                                         
         LA    R4,10(R3)                                                        
         CLI   0(R4),C' '                                                       
         BE    REVOUT                                                           
REVLP    CLI   0(R4),C','                                                       
         BE    REVOUT                                                           
         BCTR  R4,0                                                             
         CR    R4,R3                                                            
         BH    REVLP                                                            
         B     UPFLUSH                                                          
         SPACE 1                                                                
REVOUT   LR    R1,R4                                                            
         BCTR  R4,0                                                             
         SR    R4,R3                                                            
         LA    R2,REVDATE                                                       
         EX    R4,MOVEREV                                                       
         BR    R7                                                               
         SPACE 1                                                                
MOVEREV  MVC   0(0,R2),0(R3)                                                    
         SPACE 1                                                                
UPLFIG   BAS   R6,TERMFIND                                                      
         BAS   R6,GETNUM                                                        
         ZAP   FIGNO,NUM                                                        
         B     FIGNOSET            RETURN VIA R7                                
         EJECT                                                                  
GETNUM   LA    R4,0(R3)                                                         
GETNUMLP TM    0(R4),X'F0'         NUMERIC                                      
         BO    MORENUM               YES                                        
         SPACE 1                                                                
         MVC   UPERRNUM,=H'3'        NO                                         
         B     UPFLUSH                                                          
         SPACE 1                                                                
MORENUM  LA    R4,1(R4)                                                         
         CR    R4,R1                                                            
         BL    GETNUMLP                                                         
         SPACE 1                                                                
         BCTR  R4,0                                                             
         SR    R4,R3                                                            
         EX    R4,PACKNUM                                                       
         BR    R6                                                               
         SPACE 1                                                                
PACKNUM  PACK  NUM,0(0,R3)                                                      
         SPACE 1                                                                
NUM      DC    PL2'0'                                                           
NUM1     DC    PL2'0'                                                           
SVNUM    DC    PL2'0'                                                           
         SPACE 2                                                                
UPFLUSH  MVC   SAVEDIR,P                                                        
         MVC   P(80),SPACES                                                     
         MVC   P(16),=C',MOD_QUA 0_,END '                                       
         GOTO1 =V(PRINT),PARA2,P-1,=C'BL02'                                     
         USING BIGTABLS,R7                                                      
         L     R7,=A(BIGTABLS)                                                  
         LH    R3,UPERRNUM                                                      
         MH    R3,=H'48'           L(ERRMSG)                                    
         LA    R3,UPERMSGS(R3)                                                  
         MVC   P(48),0(R3)                                                      
         MVC   P+48(30),=C' SKIPPING REST OF "UPDATE." **'                      
         GOTO1 =V(PRINTER)                                                      
         MVC   P(80),SAVEDIR                                                    
         GOTO1 =V(PRINTER)                                                      
UPFLLOOP GOTO1 =V(PANIC),PARA                                                   
         TM    PARA+8,X'80'                                                     
         BO    ENDREAD                                                          
         SPACE 1                                                                
         CLC   P(7),=C'UPDATE='                                                 
         BE    UPDATING                                                         
         B     UPFLLOOP                                                         
         EJECT                                                                  
*              WORK SPACE                                                       
         SPACE 1                                                                
LASPRINT CSECT                                                                  
DUB      DS    D                                                                
WORK     DS    CL32                                                             
SAVEREGS DS    8F                                                               
PARA     DS    4F                                                               
PARA1    DS    6F                                                               
PARA2    DS    6F                                                               
PARA3    DS    6F                                                               
RETADDR  DS    F                                                                
SAVEKEY  DS    F                                                                
PSCRLNST DS    F                                                                
VSCRLNST DS    F                                                                
U        DS    CL80                USCORE SAVE                                  
V        DS    CL120               SPECIALS SAVE                                
SECNORET DS    F                                                                
DIGTAB   DC    C'1011121314151617'                                              
BORROW   DS    CL2                                                              
FIGNO    DC    PL2'0'                                                           
SVFIGNO  DC    CL8' '    INCLUDES PERIOD                                        
SVFNOLEN DC    H'0'                                                             
C        DS    CL80                                                             
SAVEDIR  DS    CL80                                                             
INDEX    DS    CL60                                                             
FILTERS  DS    CL50                                                             
ADLISTNM DC    CL12' '                                                          
ENDPGNAM DC    CL10'ENDPAGES'                                                   
FIGNAME  DC    X'C68987A499854040' FIGURE IN U/LC                               
SECTNAME DC    X'C3888197A3859940' CHAPTER U/LC                                 
PAGELIM  DC    H'54'     LINES IN PAGEBLOC                                      
POOLLINE DC    H'0'                                                             
POOLSIZE DC    H'800'                                                           
LOFFIGNM DC    H'6'                                                             
LOFCHPNM DC    H'7'                                                             
USCOFFST DC    H'0'                                                             
LASTFIG  DC    PL2'0'                                                           
CHOSEN   DC    CL3' '    CLEARED (L22) BY CARD0                                 
SCANWORD DS    CL5                                                              
         DS    CL2                                                              
SAVEYR   DS    CL2                                                              
ALPHA    DC    C' '                                                             
FIGSTODO DC    C' '                                                             
FIGFLAG  DC    C'N'                                                             
CTDFLAG  DC    C'N'                                                             
FILTER   DC    C'N'                                                             
FRONT    DC    C'N'                                                             
BODYONLY DC    C'N'                                                             
FIGPTNUM DC    C' '                                                             
ADDRESS  DC    C' '                                                             
ADDRPOSN DC    C' '                                                             
TWOLINE  DC    C'Y'                                                             
PAGEMODE DC    C'N'      END OF CLEARING BY CARD0                               
WPOFFSET DS    X                                                                
SAVEFLAG DC    C' '                                                             
         EJECT                                                                  
PANOPEN  DC    C' '                                                             
CONTLIM  DC    C'2'                                                             
PHASE    DC    X'00'                                                            
POFFSET  DC    X'00'                                                            
NOSCREEN DC    C' '                                                             
STARTFLG DC    C' '                                                             
DASHFLAG DC    C' '                                                             
SCRNFLAG DC    C' '                                                             
LINELEN  DC    X'00'                                                            
XERTEXT  DC    C' '                                                             
LENRACK  DC    CL54' '                                                          
LINESTRT DS    F                                                                
TABRACK  DS    XL40                                                             
REBUILD  DC    CL160' '                                                         
USCORSAV DC    CL160' '                                                         
         SPACE 1                                                                
LINEEND  DS    0CL256                                                           
         DC    6X'00'                                                           
         DC    X'06'          RCR - 06                                          
         DC    23X'00'                                                          
         DC    X'1E'          CRE - 1E                                          
         DC    225X'00'                                                         
         SPACE 1                                                                
TABCHECK DS    0CL256                                                           
         DC    5X'00'                                                           
         DC    X'05'          HT  - 05  XEROX PTB                               
         DC    51X'00'                                                          
         DC    X'39'          IT  - 39  XEROX TAB                               
         DC    198X'00'                                                         
         SPACE 1                                                                
USCORES  DS    0CL256                                                           
         DC    34X'00'                                                          
         DC    X'22'          BUS - 22                                          
         DC    X'23'          WUS - 23                                          
         DC    220X'00'                                                         
         SPACE 1                                                                
TRTABLE  DS    0CL256                                                           
         DC    X'000102030405060708090A0B0C0D0E0F'                              
         DC    X'101112131415161718191A1B1C1D1E1F'                              
         DC    X'202122232425262728292A2B2C2D2E2F'                              
         DC    X'303132333435363738393A3B3C3D3E3F'                              
         DC    X'404042434445464748494A4B4C4D4E4F'                              
         DC    X'505152535455565758595A5B5C5D5E40'                              
         DC    X'60616263646566676869406B6C6D6E6F'                              
         DC    X'707172737475767778797A7B7C7D7E7F'                              
         DC    X'808182838485868788898A8B8C8D8E8F'                              
         DC    X'909192939495969798999A9B9C9D9E9F'                              
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'                              
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'                              
         DC    X'C0C1C2C3C4C5C6C7C8C960CBCCCDCECF'                              
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDE40'                              
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'                              
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'                              
         EJECT                                                                  
TERMTRT  DS    0CL256                                                           
         DC    64X'00'                                                          
         DC    X'40'     SPACE                                                  
         DC    42X'00'                                                          
         DC    X'6B'     COMMA                                                  
         DC    148X'00'                                                         
         SPACE 1                                                                
INTERTRT DS    0CL256                                                           
         DC    96X'00'                                                          
         DC    X'60'     DASH                                                   
         DC    29X'00'                                                          
         DC    X'7E'     EQUAL SIGN                                             
         DC    129X'00'                                                         
         SPACE 1                                                                
FUNCTION DS    0CL256                                                           
         DC    106X'00'                                                         
         DC    X'9F'          FUNCTION CODE FOR USCORE                          
         DC    149X'00'                                                         
         SPACE 1                                                                
*                   TRT TABLE FOR SPECIALS                                      
         SPACE 1                                                                
SPECIALS DS    0CL256                                                           
         DC    109X'00'                                                         
SPCUSC   DC    X'FF'               USCORE X'6D'                                 
         DC    29X'00'                                                          
         DC    X'C5'               LEFT BRACE -  SYM                            
         DC    15X'00'                                                          
         DC    X'E3'               RIGHT BRACE - SYM                            
         DC    17X'00'                                                          
         DC    X'4A'               LEFT BRACKET                                 
         DC    X'00'                                                            
         DC    X'7F'               BULLET      - SYM                            
         DC    13X'00'                                                          
         DC    X'5A'               RIGHT BRACKET                                
         DC    66X'00'                                                          
         SPACE 1                                                                
SEPUSC   DS    0CL256                                                           
         DC    64X'01'                                                          
         DC    X'00'          40                                                
         DC    44X'01'                                                          
         DC    X'00'          6D                                                
         DC    146X'01'                                                         
         SPACE 1                                                                
UPERRNUM DC    H'0'                                                             
         EJECT                                                                  
         LTORG                                                                  
         EJECT                                                                  
PAGEBLOC CSECT                                                                  
         DS    54CL132                                                          
** CHANGE PAGELIM WHEN REP FACTOR FOR PAGEBLOC IS CHANGED **                    
         SPACE 1                                                                
TITLPOOL CSECT                                                                  
         DS    800CL96                                                          
** CHANGE POOLSIZE WHEN L(TITLPOOL) IS CHANGED **                               
         SPACE 1                                                                
       ++INCLUDE DDDPOOLLN                                                      
         EJECT                                                                  
       ++INCLUDE DDDLASPUN                                                      
         SPACE 1                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'014PBLASPRTOS05/01/02'                                      
         END                                                                    
