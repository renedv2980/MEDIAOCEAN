*          DATA SET DDMONVAL   AT LEVEL 010 AS OF 05/01/02                      
*CATALP MONVAL                                                                  
         TITLE 'MONVAL - MONTH (PERIOD) VALIDATOR'                              
MONVAL   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 MONVALDX-MONVALD,**MNVL**,CLEAR=YES                              
         USING MONVALD,RC                                                       
         LR    R9,R1                                                            
         LM    R2,R3,0(R1)         P1 A(INPUT FIELD)                            
         MVC   PRESET,0(R3)        P2 A(4 BYTE OUTPUT FIELD)                    
*                                     THIS WILL BE SET TO YMYM BINARY           
*                                     OR ZERO IF ERROR DETECTED                 
*                                     MAY BE PRE-SET WITH YMYM.                 
*                                     THIS ALLOWS SHORT INPUT (EG APR)          
         MVC   FISCMON,8(R1)       P3 BYTE 1 FISCAL MONTH NUMBER                
         MVC   MONTYPE,9(R1)          BYTE 2 MONTH TYPE                         
         MVC   MONONLY,10(R1)         BYTE 3 C'Y'=ALLOW MMM-MMM IF              
*                                     PRESET PERIOD=ZERO                        
         MVC   CROSSYR,11(R1)         BYTE 4 C'Y'=ALLOW CROSS FISCAL            
*                                     YEAR EXPRESSIONS ON OPEN ENDED            
*                                     PLANS                                     
         MVI   OPENEND,C'N'        INITIALIZE OPEN ENDED PRESET                 
         OC    PRESET,PRESET       TEST FOR NO PRESET PERIOD                    
         BZ    *+16                                                             
         CLI   PRESET,0            TEST FOR PRESET YEAR                         
         BNE   *+8                 YES                                          
         MVI   OPENEND,C'Y'        SET OPEN ENDED SWITCH                        
         EJECT                                                                  
*              REGULAR EXPRESSIONS                                              
         SPACE 3                                                                
REG      BAS   RE,REGMON           DO WE START WITH REGULAR MONTH               
         BNE   SPECIAL             IF NOT GO AND TRY ELSEWHERE                  
         MVC   PERIOD+1(1),MONTH                                                
         CLI   0(R2),C' '          IS THAT ALL?                                 
         BH    REG2                NO                                           
         CLI   PRESET,0            YES - TEST IF YEAR PRESET                    
         BE    ERROR               NO                                           
*                                                                               
         BAS   RE,FIGYEAR          GO AND FIGURE OUT YEAR                       
         BNE   ERROR                                                            
         MVC   PERIOD(1),YEAR                                                   
*                                                                               
REG1     MVC   PERIOD+2(2),PERIOD  END=START                                    
         B     OKXIT                                                            
         SPACE 1                                                                
REG2     CLI   0(R2),C'-'          WAS MONTH FOLLOWED BY DASH                   
         BE    REG10                                                            
         CLI   0(R2),C'/'          GET PAST REDUNDANT /                         
         BNE   *+8                                                              
         LA    R2,1(R2)                                                         
         BAS   RE,REGYEAR          SHOULD NOW HAVE A YEAR                       
         BNE   ERROR                                                            
         MVC   PERIOD(1),YEAR                                                   
         CLI   0(R2),C'-'          FOLLOWED BY -                                
         BE    REG20                                                            
         CLI   0(R2),C' '          OR EOF                                       
         BH    ERROR                                                            
         OC    PRESET,PRESET       WAS PERIOD PRESET                            
         BZ    REG4                NO                                           
         CLI   OPENEND,C'Y'        TEST OPEN-ENDED PRESET                       
         BE    REG4                YES                                          
         SPACE 1                                                                
REG3     BAS   RE,CONVPER                                                       
         BAS   RE,CONVPRE                                                       
         CLC   START,PRESETST      CHECK IT FITS WITHIN PRESET YMYM             
         BL    ERROR                                                            
         CLC   END,PRESETEN                                                     
         BH    ERROR                                                            
         SPACE 1                                                                
REG4     MVC   PERIOD+2(2),PERIOD  END=START                                    
         CLI   OPENEND,C'Y'                                                     
         BNE   REG5                                                             
         CLI   CROSSYR,C'Y'        TEST CROSS FISCAL YEAR ALLOWED               
         BE    *+8                 YES                                          
         BAS   RE,FITOPEN          CHECK PERIOD FITS OPEN PRESET                
         SPACE 1                                                                
REG5     B     OKXIT                                                            
         SPACE 1                                                                
REG10    LA    R2,1(R2)            SO FAR WE HAVE MMM-                          
         BAS   RE,REGMON           THIS MUST HAVE MONTH FOLLOWING               
         BNE   ERROR                                                            
         MVC   PERIOD+3(1),MONTH                                                
         CLI   0(R2),C' '                                                       
         BH    REG30                                                            
*                                  NOW HAVE MMM-MMM                             
REG11    CLI   PRESET,0            TEST IF PRESET PERIOD GIVEN                  
         BNE   REG12               YES                                          
         CLI   MONONLY,MONTHOK     TEST IF MONTH ONLY INPUT ALLOWED             
         BNE   ERROR               NO                                           
*                                  CHECK MMM-MMM = 1 FISCAL YEAR                
         ZIC   R1,PERIOD+3         R1=END MONTH                                 
         ZIC   R0,PERIOD+1         R0=START MONTH                               
         LA    RF,12               RF=N'MONTHS IN A YEAR                        
         CLI   MONTYPE,10          TEST FOR 444 YEAR                            
         BNE   *+8                                                              
         LA    RF,13                                                            
         CR    R0,R1               TEST PERIOD FALLS IN SAME YEAR               
         BL    *+6                 YES                                          
         AR    R1,RF               NO-SO ADD A YEAR TO END MONTH                
         SR    R1,R0                                                            
         LA    R1,1(R1)            R1=N'MONTHS INCLUSIVE                        
         CR    R1,RF               TEST THAT PERIOD=1 YEAR                      
         BE    OKXIT                                                            
         B     ERROR                                                            
*                                                                               
REG12    MVC   MONTH,PERIOD+1      FIGURE OUT START YEAR                        
         BAS   RE,FIGYEAR                                                       
         BNE   ERROR                                                            
         MVC   PERIOD(1),YEAR                                                   
         MVC   MONTH,PERIOD+3      AND END YEAR                                 
         BAS   RE,FIGYEAR                                                       
         BNE   ERROR                                                            
         MVC   PERIOD+2(1),YEAR                                                 
         CLC   PERIOD(2),PERIOD+2                                               
         BH    ERROR                                                            
         B     OKXIT               ELSE WE'RE OK                                
         SPACE 1                                                                
REG20    LA    R2,1(R2)            HERE WE HAVE MMMYY-                          
         BAS   RE,REGMON           MUST BE FOLLOWED BY MMMYY                    
         BNE   ERROR                                                            
         MVC   PERIOD+3(1),MONTH                                                
         CLI   0(R2),C'/'          GET PAST REDUNDANT /                         
         BNE   *+8                                                              
         LA    R2,1(R2)                                                         
         BAS   RE,REGYEAR                                                       
         BNE   ERROR                                                            
         MVC   PERIOD+2(1),YEAR                                                 
         B     REG32                                                            
         SPACE 1                                                                
REG30    CLI   0(R2),C'/'          GET PAST REDUNDANT /                         
         BNE   *+8                                                              
         LA    R2,1(R2)                                                         
         BAS   RE,REGYEAR          HERE WE HAVE MMM-MMM??                       
         BNE   ERROR                                                            
         MVC   PERIOD(1),YEAR      SET START YEAR                               
         MVC   PERIOD+2(1),YEAR    =END YEAR                                    
         SPACE 1                                                                
REG32    CLC   PERIOD+0(2),PERIOD+2  CHECK START V END                          
         BH    ERROR                                                            
         OC    PRESET,PRESET       WAS PERIOD PRESET                            
         BZ    OKXIT               NO-ALL DONE                                  
         CLI   OPENEND,C'Y'        TEST OPEN-ENDED PRESET                       
         BE    REG34               YES                                          
         BAS   RE,CONVPER          CONVERT PERIOD TO START/END                  
         BAS   RE,CONVPRE          CONVERT PRESET TO PRESETST/EN                
         CLC   START,PRESETST      YES - CHECK IT FITS                          
         BL    ERROR                                                            
         CLC   END,PRESETEN                                                     
         BH    ERROR                                                            
         B     OKXIT                                                            
         SPACE 1                                                                
REG34    CLI   CROSSYR,C'Y'        TEST CROSS-FISCAL YEAR ALLOWED               
         BE    *+8                 YES                                          
         BAS   RE,FITOPEN          NO-TEST FOR FIT IN 1 FISCAL YEAR             
         B     OKXIT                                                            
         EJECT                                                                  
*              VALIDATE FOR A REGULAR MONTH                                     
         SPACE 3                                                                
REGMON   NTR1                                                                   
*                                  R2=A(EXPRESSION)                             
*                                  ALLOWABLE FORMATS: JAN FEB ETC.              
*                                                     01/ 12/ 1/                
*                                                     JA FE NOT JU MA           
*                                                     F S O N D                 
         LA    R1,REGLIST          SCAN AGAINST REGULAR MONTHS                  
         SR    RE,RE                                                            
         SPACE 1                                                                
REGMON2  CLI   0(R1),X'FE'         TYPE MARKER                                  
         BNE   *+14                                                             
         MVC   MARKER,1(R1)                                                     
         B     REGMON3                                                          
         SPACE 1                                                                
         IC    RE,0(R1)            L'FIELD - 1                                  
         EX    RE,*+8                                                           
         B     *+10                                                             
         CLC   0(0,R2),2(R1)                                                    
         BE    REGMON4                                                          
         SPACE 1                                                                
REGMON3  LA    R1,L'REGLIST(R1)                                                 
         CLI   0(R1),X'FF'         CHECK END OF LIST                            
         BE    NO                                                               
         B     REGMON2                                                          
         SPACE 1                                                                
REGMON4  MVC   MONTH,1(R1)         RETURN MONTH NUMBER                          
         LA    R2,1(R2,RE)                                                      
         CLI   MONTYPE,10                                                       
         BE    REGMON6                                                          
         CLI   MARKER,2            FOR REGULAR MONTH TYPES                      
         BL    YES                                                              
         BH    ERROR               P EXPRESSIONS NOT ALLOWED                    
         OC    PRESET,PRESET       REJECT 'M' EXPRESSIONS IF                    
         BZ    ERROR               NO PRESET SINCE THEY'RE RELATIVE             
         CLI   FISCMON,0                                                        
         BE    ERROR                                                            
         BAS   RE,RESMON           RESOLVE M EXPRESSIONS                        
         B     YES                                                              
         SPACE 1                                                                
REGMON6  CLI   MARKER,3            4 WEEK MONTH TYPES                           
         BNE   ERROR               ONLY USE P EXPRESSIONS                       
         CLI   FISCMON,0                                                        
         BE    YES                                                              
         ZIC   R1,MONTH                                                         
         BCTR  R1,0                                                             
         ZIC   R0,FISCMON                                                       
         AR    R1,R0                                                            
         STC   R1,MONTH                                                         
         B     YES                                                              
         EJECT                                                                  
*              TABLES FOR MONTH VALIDATION                                      
         SPACE 3                                                                
REGLIST  DS    0CL5                L'FIELD-1                                    
*                                  MONTH NUMBER                                 
*                                  MONTH EXPRESSION                             
         DC    X'FE',AL1(1,0,0,0)  TYPE 1 MARKER                                
         DC    AL1(2,01),C'JAN'                                                 
         DC    AL1(2,02),C'FEB'                                                 
         DC    AL1(2,03),C'MAR'                                                 
         DC    AL1(2,04),C'APR'                                                 
         DC    AL1(2,05),C'MAY'                                                 
         DC    AL1(2,06),C'JUN'                                                 
         DC    AL1(2,07),C'JUL'                                                 
         DC    AL1(2,08),C'AUG'                                                 
         DC    AL1(2,09),C'SEP'                                                 
         DC    AL1(2,10),C'OCT'                                                 
         DC    AL1(2,11),C'NOV'                                                 
         DC    AL1(2,12),C'DEC'                                                 
         DC    AL1(2,01),C'01/'                                                 
         DC    AL1(2,02),C'02/'                                                 
         DC    AL1(2,03),C'03/'                                                 
         DC    AL1(2,04),C'04/'                                                 
         DC    AL1(2,05),C'05/'                                                 
         DC    AL1(2,06),C'06/'                                                 
         DC    AL1(2,07),C'07/'                                                 
         DC    AL1(2,08),C'08/'                                                 
         DC    AL1(2,09),C'09/'                                                 
         DC    AL1(2,10),C'10/'                                                 
         DC    AL1(2,11),C'11/'                                                 
         DC    AL1(2,12),C'12/'                                                 
         DC    AL1(1,01),C'JA '                                                 
         DC    AL1(1,02),C'FE '                                                 
         DC    AL1(1,04),C'AP '                                                 
         DC    AL1(1,08),C'AU '                                                 
         DC    AL1(1,09),C'SE '                                                 
         DC    AL1(1,10),C'OC '                                                 
         DC    AL1(1,11),C'NO '                                                 
         DC    AL1(1,12),C'DE '                                                 
         DC    AL1(1,01),C'1/ '                                                 
         DC    AL1(1,02),C'2/ '                                                 
         DC    AL1(1,03),C'3/ '                                                 
         DC    AL1(1,04),C'4/ '                                                 
         DC    AL1(1,05),C'5/ '                                                 
         DC    AL1(1,06),C'6/ '                                                 
         DC    AL1(1,07),C'7/ '                                                 
         DC    AL1(1,08),C'8/ '                                                 
         DC    AL1(1,09),C'9/ '                                                 
         DC    AL1(0,02),C'F  '                                                 
         DC    AL1(0,09),C'S  '                                                 
         DC    AL1(0,10),C'O  '                                                 
         DC    AL1(0,11),C'N  '                                                 
         DC    AL1(0,12),C'D  '                                                 
         SPACE 1                                                                
         DC    X'FE',AL1(2,0,0,0)  TYPE 2 MARKER                                
         DC    AL1(2,01),C'M01'                                                 
         DC    AL1(2,02),C'M02'                                                 
         DC    AL1(2,03),C'M03'                                                 
         DC    AL1(2,04),C'M04'                                                 
         DC    AL1(2,05),C'M05'                                                 
         DC    AL1(2,06),C'M06'                                                 
         DC    AL1(2,07),C'M07'                                                 
         DC    AL1(2,08),C'M08'                                                 
         DC    AL1(2,09),C'M09'                                                 
         DC    AL1(2,10),C'M10'                                                 
         DC    AL1(2,11),C'M11'                                                 
         DC    AL1(2,12),C'M12'                                                 
         DC    AL1(1,01),C'M1 '                                                 
         DC    AL1(1,02),C'M2 '                                                 
         DC    AL1(1,03),C'M3 '                                                 
         DC    AL1(1,04),C'M4 '                                                 
         DC    AL1(1,05),C'M5 '                                                 
         DC    AL1(1,06),C'M6 '                                                 
         DC    AL1(1,07),C'M7 '                                                 
         DC    AL1(1,08),C'M8 '                                                 
         DC    AL1(1,09),C'M9 '                                                 
         SPACE 1                                                                
         DC    X'FE',AL1(3,0,0,0)  TYPE 3 MARKER                                
         DC    AL1(2,01),C'P01'                                                 
         DC    AL1(2,02),C'P02'                                                 
         DC    AL1(2,03),C'P03'                                                 
         DC    AL1(2,04),C'P04'                                                 
         DC    AL1(2,05),C'P05'                                                 
         DC    AL1(2,06),C'P06'                                                 
         DC    AL1(2,07),C'P07'                                                 
         DC    AL1(2,08),C'P08'                                                 
         DC    AL1(2,09),C'P09'                                                 
         DC    AL1(2,10),C'P10'                                                 
         DC    AL1(2,11),C'P11'                                                 
         DC    AL1(2,12),C'P12'                                                 
         DC    AL1(2,13),C'P13'                                                 
         DC    AL1(1,01),C'P1 '                                                 
         DC    AL1(1,02),C'P2 '                                                 
         DC    AL1(1,03),C'P3 '                                                 
         DC    AL1(1,04),C'P4 '                                                 
         DC    AL1(1,05),C'P5 '                                                 
         DC    AL1(1,06),C'P6 '                                                 
         DC    AL1(1,07),C'P7 '                                                 
         DC    AL1(1,08),C'P8 '                                                 
         DC    AL1(1,09),C'P9 '                                                 
         DC    X'FF'                                                            
         EJECT                                                                  
*              RESOLVE RELATIVE MONTH NUMBER                                    
         SPACE 3                                                                
RESMON   NTR1                                                                   
         ZIC   R1,MONTH                                                         
         BCTR  R1,0                                                             
         ZIC   R0,FISCMON                                                       
         AR    R1,R0                                                            
         CH    R1,=H'12'                                                        
         BNH   *+8                                                              
         SH    R1,=H'12'           ADJUST IF OVER 12                            
         STC   R1,MONTH                                                         
         B     XIT                                                              
         EJECT                                                                  
*              VALIDATE YEAR EXPRESSION                                         
         SPACE 3                                                                
REGYEAR  NTR1                                                                   
*                                  ALLLOWABLE YY                                
*                                  RETURN IN YEAR                               
         CLI   0(R2),C'0'                                                       
         BL    NO                                                               
         CLI   0(R2),C'9'                                                       
         BH    NO                                                               
         CLI   1(R2),C'0'                                                       
         BL    NO                                                               
         CLI   1(R2),C'9'                                                       
         BH    NO                                                               
         PACK  DUB,0(2,R2)                                                      
         CVB   R1,DUB                                                           
         CHI   R1,80               TEST YEAR IS 80 OR MORE                      
         BNL   *+8                                                              
         AHI   R1,100              NO-THEN TREAT IT AS Y2K                      
         STC   R1,YEAR                                                          
         LA    R2,2(R2)                                                         
         B     YES                                                              
         EJECT                                                                  
*              FIGURE OUT YEAR FROM MONTH OR SET ERROR                          
         SPACE 3                                                                
FIGYEAR  NTR1                                                                   
*                                  PRESET = YMYM                                
*                                  MONTH IS SET                                 
*                                  PASS BACK YEAR                               
         SPACE 1                                                                
         MVC   YEAR,PRESET         ASSUME START YEAR                            
         ZIC   R0,YEAR                                                          
         CHI   R0,80               TREAT ANY YEAR < 80 AS Y2K                   
         BNL   *+8                                                              
         AHI   R0,100                                                           
         STC   R0,YEAR                                                          
*                                  PASS BACK YEAR                               
         CLC   PRESET(1),PRESET+2  WAS PRESET START/END YEAR SAME               
         BNE   FY2                                                              
         CLC   MONTH,PRESET+1      YES - CHECK OUR MONTH FITS                   
         BL    NO                                                               
         CLC   MONTH,PRESET+3                                                   
         BH    NO                                                               
         B     YES                                                              
         SPACE 1                                                                
*                                  YEARS ARE DIFFERENT                          
FY2      CLC   MONTH,PRESET+1      CHECK AGAINST START MONTH                    
         BNL   YES                 IF G/E THEN MONTH IS FOR START YEAR          
         MVC   YEAR,PRESET+2                                                    
         ZIC   R0,YEAR                                                          
         CHI   R0,80               TREAT ANY YEAR < 80 AS Y2K                   
         BNL   *+8                                                              
         AHI   R0,100                                                           
         STC   R0,YEAR                                                          
         CLC   MONTH,PRESET+3      CHECK AGAINST END MONTH                      
         BNH   YES                 IF L/E THEN MONTH IS FOR END YEAR            
         B     NO                  ELSE WE HAVE A PROBLEM                       
         EJECT                                                                  
* SUB-ROUTINE TO CHECK FOR FIT OF PERIOD YM AGAINST OPEN PRESET                 
* PERIOD (MONTHS ONLY)                                                          
*                                                                               
FITOPEN  CLC   PERIOD+1(1),PRESET+1 TEST PERIOD ST LT PRESET ST MON             
         BL    FITOPEN2            YES                                          
         CLC   PRESET+1(1),PRESET+3 TEST PRESET ST LT PRESET END MON            
         BL    FITOPEN1            YES-MONTHS ARE WITHIN SAME YEAR              
         CLC   PERIOD+3(1),PRESET+3 TEST PERIOD END LE PRESET END               
         BH    ERROR               PAST END MONTH                               
         B     FITOPENX            PERIOD CHECKS OUT                            
*                                                                               
FITOPEN1 CLC   PERIOD(1),PERIOD+2  TEST PERIOD IS IN SAME YEAR                  
         BNE   ERROR               CONFLICTS WITH PRESET IN SAME YEAR           
         CLC   PERIOD+3(1),PRESET+3 TEST PERIOD END LE PRESET END               
         BH    ERROR               NO-GOES PAST END MONTH                       
         B     FITOPENX            YES-CHECKS OUT                               
*                                  PERIOD ST MON L.T. PRESET ST MON             
FITOPEN2 CLC   PERIOD(1),PERIOD+2  TEST PERIOD CROSSES YEARS                    
         BE    ERROR               SAME YEAR MEANS MUST BE ERROR                
         CLC   PERIOD+1(1),PRESET+3 PERIOD ST AND END MUST BE                   
         BH    ERROR               LE PRESET END                                
         CLC   PERIOD+3(1),PRESET+3                                             
         BH    ERROR                                                            
*                                                                               
FITOPENX BR    RE                                                               
         EJECT                                                                  
*              SPECIAL EXPRESSIONS                                              
         SPACE 3                                                                
SPECIAL  CLI   MONTYPE,10          NOT ALLOWED FOR TYPE 10                      
         BE    ERROR                                                            
         LA    R1,SPECLIST                                                      
         SPACE 1                                                                
SPEC2    CLC   0(2,R2),0(R1)                                                    
         BE    SPEC4                                                            
         LA    R1,L'SPECLIST(R1)                                                
         CLI   0(R1),X'FF'                                                      
         BE    ERROR                                                            
         B     SPEC2                                                            
         SPACE 1                                                                
SPECLIST DS    0CL4                EXPRESSION REL START-END                     
         DC    C'Q1',AL1(1,3)      QUARTERS                                     
         DC    C'Q2',AL1(4,6)                                                   
         DC    C'Q3',AL1(7,9)                                                   
         DC    C'Q4',AL1(10,12)                                                 
         DC    C'H1',AL1(1,6)      HALVES                                       
         DC    C'H2',AL1(7,12)                                                  
         DC    X'FF'                                                            
         SPACE 1                                                                
SPEC4    CLI   FISCMON,0           FISCAL YEAR START MONTH NEEDED               
         BE    ERROR                                                            
         CLI   2(R2),C' '          TEST IF YEAR MIGHT BE INPUT                  
         BH    SPEC6               YES                                          
         CLI   PRESET,0            NO-THEN MUST HAVE PRESET DATES               
         BE    ERROR                                                            
         MVC   MONTH,2(R1)         RESOLVE START MONTH                          
         BAS   RE,RESMON                                                        
         BAS   RE,FIGYEAR                                                       
         BNE   ERROR                                                            
         MVC   PERIOD(2),YEAR                                                   
         MVC   MONTH,3(R1)         AND END MONTH                                
         BAS   RE,RESMON                                                        
         BAS   RE,SPYEAR                                                        
         MVC   PERIOD+2(2),YEAR                                                 
         BAS   RE,CONVPER                                                       
         BAS   RE,CONVPRE                                                       
         CLC   START,PRESETST      TEST PERIOD YM FITS IN PRESET YM             
         BL    ERROR                                                            
         CLC   END,PRESETEN                                                     
         BH    ERROR                                                            
         B     OKXIT                                                            
         SPACE 1                                                                
SPEC6    CLI   OPENEND,C'Y'        TEST FOR OPEN ENDED PRESET                   
         BNE   ERROR               NO-THEN CANNOT ALLOW YEAR                    
         MVC   STMONTH(2),2(R1)    SAVE RELATIVE START/END MONTH                
         MVC   MONTH,STMONTH       BEGIN WITH START MONTH                       
         BAS   RE,RESMON           RESOLVE TO ACTUAL START MONTH                
         LA    R2,2(R2)            BUMP INPUT POINTER                           
         CLI   0(R2),C'/'          TEST FOR SLASH BEFORE YEAR                   
         BNE   *+8                                                              
         LA    R2,1(R2)            YES-BUMP PAST IT                             
         BAS   RE,REGYEAR          LOOK FOR YEAR                                
         BNE   ERROR                                                            
         MVC   PERIOD(2),YEAR      SET START YM                                 
         MVC   MONTH,ENDMONTH                                                   
         BAS   RE,RESMON                                                        
         BAS   RE,SPYEAR                                                        
         MVC   PERIOD+2(2),YEAR    SET END YM                                   
         BAS   RE,FITOPEN          TEST FOR FIT WITH OPEN PRESET                
         B     OKXIT                                                            
         SPACE 2                                                                
* SUB-ROUTINE TO WORK OUT END YEAR - AT ENTRY MONTH=END MONTH                   
*                                                                               
SPYEAR   ZIC   R1,YEAR                                                          
         CLC   MONTH,PERIOD+1      TEST END MONTH GT START                      
         BH    *+8                 MUST BE IN SAME YEAR                         
         LA    R1,1(R1)                                                         
         STC   R1,YEAR                                                          
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO DO A Y2K COMPLIANT START END PERIOD COMPARE                    
* SETS START AND END TO THREE BYTE VERSION OF YEAR/MONTH                        
*                                                                               
CONVPER  ZIC   R1,PERIOD+0                                                      
         AHI   R1,1900                                                          
         STCM  R1,3,START+0        START YEAR                                   
         MVC   START+2(1),PERIOD+1 START MONTH                                  
*                                                                               
         ZIC   R1,PERIOD+2         END YEAR                                     
         AHI   R1,1900                                                          
         STCM  R1,3,END+0                                                       
         MVC   END+2(1),PERIOD+3                                                
         BR    RE                                                               
         SPACE 2                                                                
* SUB-ROUTINE TO CONVERT PRESET YEAR/MONTH TO Y2K FORMAT                        
*                                                                               
CONVPRE  ZIC   R1,PRESET+0         GET PRESET START YEAR                        
         AHI   R1,1900                                                          
         CLI   PRESET+0,80                                                      
         BNL   *+8                                                              
         AHI   R1,100                                                           
         STCM  R1,3,PRESETST+0                                                  
         MVC   PRESETST+2(1),PRESET+1                                           
*                                                                               
         ZIC   R1,PRESET+2         GET PRESET END YEAR                          
         AHI   R1,1900                                                          
         CLI   PRESET+2,80                                                      
         BNL   *+8                                                              
         AHI   R1,100                                                           
         STCM  R1,3,PRESETEN+0                                                  
         MVC   PRESETEN+2(1),PRESET+3                                           
*                                                                               
         BR    RE                                                               
         EJECT                                                                  
*              ODDMENTS                                                         
         SPACE 3                                                                
OKXIT    MVC   4(4,R9),PERIOD      PASS BACK PERIOD EXPRESSION                  
         B     XIT                                                              
         SPACE 1                                                                
ERROR    XC    4(4,R9),4(R9)       PASS BACK ZERO RESULT                        
         B     XIT                                                              
         SPACE 1                                                                
YES      SR    R0,R0                                                            
         LTR   R0,R0                                                            
         XIT1  REGS=(R2)           PASS BACK R2 FOR YES                         
         SPACE 1                                                                
NO       LA    R0,1                                                             
         LTR   R0,R0                                                            
         SPACE 1                                                                
XIT      XIT1                                                                   
         SPACE 2                                                                
* LITERAL POOL                                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*              DSECT TO COVER MONVAL WORKING STORAGE                            
         SPACE 3                                                                
MONVALD  DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
FISCMON  DS    XL1                                                              
MONTYPE  DS    XL1                                                              
MONONLY  DS    C                                                                
CROSSYR  DS    C                                                                
PRESET   DS    XL4                                                              
YEAR     DS    XL1                                                              
MONTH    DS    XL1                                                              
PERIOD   DS    XL4                                                              
MARKER   DS    XL1                                                              
OPENEND  DS    C                   Y=OPEN ENDED YEARS ON PRESET PERIOD          
STMONTH  DS    X                   RELATIVE START MONTH-SPECIALS                
ENDMONTH DS    X                   RELATIVE END MONTH-SPECIALS                  
START    DS    XL3                                                              
END      DS    XL3                                                              
PRESETST DS    XL3                                                              
PRESETEN DS    XL3                                                              
MONVALDX EQU   *                                                                
         SPACE 1                                                                
* EQUATES                                                                       
*                                                                               
MONTHOK  EQU   C'Y'                OK TO ACCEPT MONTH ONLY EXPRESSIONS          
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'010DDMONVAL  05/01/02'                                      
         END                                                                    
