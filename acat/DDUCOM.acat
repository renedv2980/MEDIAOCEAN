*          DATA SET DDUCOM     AT LEVEL 031 AS OF 01/15/15                      
*CATALP DDUCOM                                                                  
***********************************************************************         
* PROGRAMS THAT CALL DDUCOM:                                                    
*                                                                               
*                                                                               
***********************************************************************         
         TITLE 'DDUCOM - GETS UCOMMENT DATA'                                    
DDUCOM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,*DDUCOM*,RA,RR=RE,CLEAR=YES                                
         USING WORKD,RC                                                         
         ST    RE,RELO                                                          
         ST    RD,SAVERD                                                        
         ST    R1,SAVER1                                                        
         MVC   UCOMBLK,0(R1)                                                    
         USING DDUCOMD,UCOMBLK                                                  
         MVI   UCERROR,0                                                        
*                                                                               
         BRAS  RE,INIT             INITIALISE                                   
         BRAS  RE,VALPARM          VALIDATE INPUT PARAMETERS                    
*                                                                               
         BRAS  RE,CLTUCOM          GET CLIENT LEVEL UCOM REC                    
         BRAS  RE,TITLES           GET TITLES FROM CLT LEVEL RECORD             
*                                                                               
         TM    UCOPT,UCOTTL        TITLES ONLY                                  
         BO    MAINX                                                            
*                                                                               
         TM    UCOPT,UCOPRD        DOES CALLER WANT PRD DATA                    
         BNO   MAIN20                                                           
         BRAS  RE,PRDUCOM          GET PRODUCT LEVEL UCOM REC                   
         TM    UCDATA,UCDNOPRD     SEE IF NO PRD DATA FOUND                     
         BO    MAIN20              SKIP TO ESTIMATE                             
         BRAS  RE,GETDATA          GET PRODUCT DATA                             
*                                                                               
MAIN20   TM    UCOPT,UCOEST        DOES CALLER WANT EST DATA                    
         BO    *+12                                                             
         TM    UCOPT,UCO8EST                                                    
         BNO   MAIN30                                                           
*                                                                               
         BRAS  RE,ESTUCOM          GET ESTIMATE LEVEL UCOM REC                  
         TM    UCDATA,UCDNOEST     SEE IF NO EST DATA FOUND                     
         BO    MAIN30              SKIP TO MKT                                  
         BRAS  RE,GETDATA          GET ESTIMATE DATA                            
         CLI   UCSYS,C'P'          IF PRINT DONE, NO EFF CHECK                  
         BE    MAIN30              ONLY FOR SPOT AND NET                        
*                                                                               
*                                                                               
         TM    UCOPT,UCOEFF        CHECKING EST EFFECTIVE DATE?                 
         BO    *+12                                                             
         TM    UCOPTSP,UCOEFFO     CHECKING EST EFFECTIVE DATE?                 
         BNO   *+16                                                             
*                                                                               
         BRAS  RE,CHKEDATE         CHECK EFFECTIVE DATES                        
         TM    UCDATA,UCDNOEST     SEE IF HAD TO CLEAR ALL DATA                 
         BO    MAIN30              SKIP TO MKT                                  
*                                                                               
         TM    UCOPTSP,UCOCTYP+UCOCTYO+UCOCTCL  CHECKING COST TYPE?             
         BZ    *+8                                                              
         BRAS  RE,CHKCTYP                                                       
*                                                                               
*                                  CODE FOR MAIN/COMM BILL UCOMS                
         BRAS  RE,CHKCOMM          CHECK FOR #R# OR #F#                         
         TM    UCDATA,UCDNOEST     SEE IF HAD TO CLEAR ALL DATA                 
         BO    MAIN30              SKIP TO MKT                                  
*                                                                               
MAIN30   TM    UCOPT,UCOMKT        DOES CALLER WANT MKT DATA                    
         BNO   MAIN40                                                           
         BRAS  RE,MKTUCOM          GET MARKET LEVEL UCOM REC                    
         TM    UCDATA,UCDNOMKT     SEE IF NO MKT DATA FOUND                     
         BO    MAIN40              SKIP                                         
         BRAS  RE,GETDATA          GET MARKET DATA                              
*                                                                               
MAIN40   TM    UCOPT,UCOREG        DOES CALLER WANT REG DATA                    
         BNO   MAIN50                                                           
         BRAS  RE,REGUCOM          GET REGION LEVEL UCOM REC                    
         TM    UCDATA,UCDNOREG     SEE IF NO REG DATA FOUND                     
         BO    MAIN50              SKIP                                         
         BRAS  RE,GETDATA          GET REGION DATA                              
*                                                                               
MAIN50   TM    UCOPT,UCODST        DOES CALLER WANT DST DATA                    
         BNO   MAINX                                                            
         BRAS  RE,DSTUCOM          GET DISTRICT LEVEL UCOM REC                  
         TM    UCDATA,UCDNODST     SEE IF NO DST DATA FOUND                     
         BO    MAINX               SKIP                                         
         BRAS  RE,GETDATA          GET DISTRICT DATA                            
*                                                                               
MAINX    B     EXITBACK            RETURN DATA                                  
         EJECT                                                                  
********************************************************************            
* INITIALISATION                                                   *            
********************************************************************            
         SPACE 1                                                                
INIT     NTR1  ,                                                                
         L     RF,UCACOMF                                                       
         USING COMFACSD,RF                                                      
         MVC   DMGR,CDATAMGR                                                    
         DROP  RF                                                               
*                                                                               
         MVI   UCDATA,0            CLEAR                                        
         MVI   ELEM60S,C'N'        SET TO SKIP ELEMS 60 AND OVER                
*                                  THIS WILL PROTECT OLD VERSIONS               
*                                                                               
         LA    R1,PTITLES          SET PRD AND EST ADDRESSES                    
         ST    R1,UCPTTLS                                                       
         LA    R1,ETITLES                                                       
         ST    R1,UCETTLS                                                       
         LA    R1,PDATA                                                         
         ST    R1,UCPDATA                                                       
         LA    R1,EDATA                                                         
         ST    R1,UCEDATA                                                       
*                                                                               
         XC    UCPLENS,UCPLENS     CLEAR PRD AND EST DATA LENGTHS               
         XC    UCELENS,UCELENS                                                  
         XC    UCPMXLNS,UCPMXLNS                                                
         XC    UCEMXLNS,UCEMXLNS                                                
         XC    UCPEDITS,UCPEDITS                                                
         XC    UCEEDITS,UCEEDITS                                                
*                                                                               
         TM    UCOPT,UCOMKT        DOES CALLER WANT MKT DATA                    
         BNO   INIT10                                                           
         LA    R1,MTITLES          INIT MKT FIELDS                              
         ST    R1,UCMTTLS                                                       
         LA    R1,MDATA                                                         
         ST    R1,UCMDATA                                                       
         XC    UCMMXLNS,UCMMXLNS                                                
         XC    UCMLENS,UCMLENS                                                  
         XC    UCMEDITS,UCMEDITS                                                
*                                                                               
INIT10   TM    UCOPT,UCOREG+UCODST DOES CALLER WANT REGION/DISTRICT             
         BZ    INIT20                                                           
         LA    R1,RTITLES          INIT REG FIELDS                              
         ST    R1,UCRTTLS                                                       
         LA    R1,RDATA                                                         
         ST    R1,UCRDATA                                                       
         XC    UCRMXLNS,UCRMXLNS                                                
         XC    UCRLENS,UCRLENS                                                  
         XC    UCREDITS,UCREDITS                                                
         LA    R1,DTITLES          INIT DST FIELDS                              
         ST    R1,UCDTTLS                                                       
         LA    R1,DDATA                                                         
         ST    R1,UCDDATA                                                       
         XC    UCDMXLNS,UCDMXLNS                                                
         XC    UCDLENS,UCDLENS                                                  
         XC    UCDEDITS,UCDEDITS                                                
*                                                                               
INIT20   DS    0H                                                               
         B     EXITOK                                                           
         EJECT                                                                  
********************************************************************            
* VALIDATE INPUT PARAMETERS                                        *            
********************************************************************            
         SPACE 1                                                                
VALPARM  NTR1  ,                                                                
*                                                                               
         OC    UCACOMF,UCACOMF     A(COMFACS)                                   
         BZ    ERRPARM                                                          
*                                                                               
         LA    R2,VALSYS           VALID SYSTEMS                                
VPARM05  CLC   UCSYS,0(R2)                                                      
         BE    VPARM10                                                          
         LA    R2,1(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         BE    ERRPARM                                                          
         B     VPARM05                                                          
VALSYS   DC    C'SNP',X'FF'        VALID SYSTEMS                                
*                                                                               
VPARM10  OC    UCMED,UCMED         MEDIA REQD                                   
         BZ    ERRPARM                                                          
*                                                                               
         OC    UCCLT,UCCLT         CLIENT REQD                                  
         BZ    ERRPARM                                                          
*                                                                               
         TM    UCOPT,UCOPRD        CALLER WANTS PRD DATA                        
         BNO   *+14                                                             
         CLC   UCPRD,=C'   '       THEN NEED PRODUCT                            
         BNH   ERRPARM                                                          
*                                                                               
         TM    UCOPT,UCOEST        CALLER WANTS EST DATA                        
         BNO   *+14                                                             
         OC    UCEST,UCEST         THEN NEED ESTIMATE                           
         BZ    ERRPARM                                                          
*                                                                               
         TM    UCOPT,UCOEFF        WANT TO CHECK EST EFFECTIVE DATE             
         BNO   *+14                                                             
         OC    UCMOS,UCMOS         THEN NEED MONTH OF SERVICE                   
         BZ    ERRPARM                                                          
*                                                                               
         CLI   UCSYS,C'P'          FOR PRINT SKIP THE BELOW CHECK               
         BE    *+22                                                             
         TM    UCOPTSP,UCOCTYP+UCOCTYO      WANT TO CHECK COST TYPE?            
         BZ    *+14                                                             
         OC    UCCTYP,UCCTYP       THEN NEED COST TYPE                          
         BZ    ERRPARM                                                          
*                                                                               
         TM    UCOPT,UCOMKT        CALLER WANTS MKT DATA                        
         BNO   VPARM20                                                          
         MVI   ELEM60S,C'Y'        SET TO LOOK AT ELEMS OF X60+                 
         CLI   UCSYS,C'S'          MKT ONLY FOR SPOT                            
         BNE   ERRPARM                                                          
         TM    UCOPT,UCOTTL        TITLES ONLY                                  
         BO    VPARM25             SKIP THIS VALIDATION                         
         OC    UCMKT,UCMKT         THEN NEED MARKET                             
         BZ    ERRPARM                                                          
*                                                                               
VPARM20  TM    UCOPT,UCOREG+UCODST CALLER WANTS REG/DST DATA                    
         BZ    VPARM25                                                          
         MVI   ELEM60S,C'Y'        SET TO LOOK AT ELEMS OF X60+                 
         TM    UCOPT,UCOTTL        TITLES ONLY                                  
         BO    VPARM25             SKIP THIS VALIDATION                         
*                                                                               
         OC    UCDIV,UCDIV         THEN NEED DIVISION FOR EITHER                
         BZ    ERRPARM                                                          
         OC    UCREG,UCREG         THEN NEED REGION FOR EITHER                  
         BZ    ERRPARM                                                          
         CLI   UCSYS,C'P'          ONLY FOR PRINT                               
         BNE   ERRPARM                                                          
*                                                                               
         TM    UCOPT,UCODST        DISTRICT?                                    
         BNO   VPARM25                                                          
         OC    UCDST,UCDST         THEN NEED DISTICT                            
         BZ    ERRPARM                                                          
*                                                                               
VPARM25  CLI   UCOPT,0             MUST ASK FOR SOMETHING                       
         BE    ERRPARM                                                          
*                                                                               
VPARMX   B     EXIT                                                             
         EJECT                                                                  
********************************************************************            
* GET CLIENT LEVEL UCOM RECORD                                     *            
********************************************************************            
*                                                                               
CLTUCOM  NTR1                                                                   
         CLI   UCSYS,C'S'          SPOT                                         
         BE    CLTU10                                                           
         CLI   UCSYS,C'N'          OR NET                                       
         BNE   CLTU20                                                           
*                                                                               
CLTU10   XC    KEY,KEY                                                          
         USING UCOMHDRD,R6                                                      
         LA    R6,KEY                                                           
         MVC   UCOMKTYP,=X'0D0C'   REC TYPE                                     
         MVC   UCOMKAGY,UCSAM      SPOT AGY/MED                                 
         MVI   UCOMCTYP,C'U'       UCOM TYPE COMMENT REC                        
         MVC   UCOMKCLT,UCSCLT     PACKED SPOT CLT                              
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'SPTDIR',KEY,KEY,DMWORK               
         CLC   KEY(13),KEYSAVE                                                  
         BNE   ERRNOCLT                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'SPTFIL',KEY+14,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO+24            FIRST ELEM                                   
         ST    R6,IOELEM                                                        
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
CLTU20   CLI   UCSYS,C'P'          PRINT                                        
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         XC    KEY,KEY                                                          
         USING PUCOMD,R6                                                        
         LA    R6,KEY                                                           
         MVC   PUCMKAGY,UCAGY      AGENCY                                       
         MVC   PUCMKMED,UCMED      MEDIA                                        
         MVI   PUCMKRCD,X'12'      UCOM TYPE COMMENT REC                        
         MVC   PUCMKCLT,UCCLT      CLIENT                                       
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'PRTDIR',KEY,KEY,DMWORK               
         CLC   KEY(25),KEYSAVE                                                  
         BNE   ERRNOCLT                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'PRTFIL',KEY+27,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO+33            FIRST ELEM                                   
         ST    R6,IOELEM                                                        
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
         EJECT                                                                  
********************************************************************            
* GET TITLES FROM CLIENT LEVEL UCOM RECORD                         *            
********************************************************************            
*                                                                               
TITLES   NTR1                                                                   
         L     R6,IOELEM           A(FIRST ELEM)                                
         B     TTL15                                                            
TTL10    ZIC   R1,1(R6)            NEXT ELEM                                    
         AR    R6,R1                                                            
TTL15    CLI   0(R6),0                                                          
         BE    TTLX                                                             
*                                                                               
         CLI   ELEM60S,C'Y'        PROCESS X'60' ELEMS AND HIGHER               
         BE    TTL18                                                            
         CLI   0(R6),X'60'                                                      
         BNL   TTL10                                                            
*                                                                               
TTL18    CLC   UCPROG,=C'  '                                                    
         BNH   *+12                                                             
         BRAS  RE,PROGFILT         TEST ANY PROGRAM FILTERS                     
         BNE   TTL10               SKIP                                         
*                                                                               
         TM    UCOPT,UCO8EST       NEED 8 EST UCOMS ?                           
         BNO   TTL19                                                            
         MVC   BYTE,0(R6)          ONLY LOOK AT EST ELEMS X'5N'                 
         NI    BYTE,X'F0'                                                       
         CLI   BYTE,X'50'                                                       
         BNE   TTL10                                                            
         LA    R3,ELEMTABE         TABLE FOR ESTIMTE UCOMS ONLY                 
         B     *+8                                                              
*                                                                               
         USING ELEMTABD,R3                                                      
TTL19    LA    R3,ELEMTAB          TABLE OF ELEMS AND DISPS                     
*                                                                               
TTL20    CLI   0(R3),X'FF'                                                      
         BE    TTL10                                                            
         CLC   0(1,R3),0(R6)       MATCH ON ELEM CODE                           
         BE    TTL30                                                            
         LA    R3,ELTABLNQ(R3)     NEXT IN TABLE                                
         B     TTL20                                                            
*                                                                               
TTL30    LA    R2,WORKD                                                         
         AH    R2,TTLDISP          DISP TO TITLE                                
         ZIC   R1,1(R6)            LEN ON ELEM                                  
         AHI   R1,-7               6 FOR HDR AND 1 FOR EX                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),6(R6)       FILL IN TITLE                                
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,MAXDISP          DISP TO MAX DATA LENGTH                      
         MVC   0(1,R2),3(R6)       FILL IN MAX LEN                              
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,EDITDISP         DISP TO EDIT RULE                            
         MVC   0(1,R2),2(R6)       FILL IN MAX LEN                              
         B     TTL10               NEXT ELEM                                    
*                                                                               
TTLX     MVC   UCPMXLNS,PMAXLNS    RETURN MAX LENS                              
         MVC   UCEMXLNS,EMAXLNS                                                 
         MVC   UCPEDITS,PEDITS     RETURN EDIT RULES                            
         MVC   UCEEDITS,EEDITS                                                  
*                                                                               
         TM    UCOPT,UCOMKT        CALLER WANTS MKT DATA                        
         BNO   TTLX10                                                           
         MVC   UCMMXLNS,MMAXLNS                                                 
         MVC   UCMEDITS,MEDITS                                                  
*                                                                               
TTLX10   TM    UCOPT,UCOREG+UCODST CALLER WANTS REGION/DISTRICT                 
         BZ    EXIT                                                             
         MVC   UCRMXLNS,RMAXLNS                                                 
         MVC   UCREDITS,REDITS                                                  
         MVC   UCDMXLNS,DMAXLNS                                                 
         MVC   UCDEDITS,DEDITS                                                  
*                                                                               
         B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
********************************************************************            
* FILTER ON PROGRAM  -  R6 POINTS TO UCOM ELEM                     *            
********************************************************************            
*                                                                               
PROGFILT NTR1                                                                   
         LA    R2,PROGTAB                                                       
         B     *+8                                                              
PF10     LA    R2,4(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         BE    EXITOK                                                           
*                                                                               
         CLC   UCPROG,0(R2)        MATCH ON CALLING PROGRAM                     
         BNE   PF10                                                             
         MVC   BYTE,4(R6)          ELEM PROG BIT                                
         NC    BYTE,2(R2)          AND WITH TABLE BIT                           
         CLC   BYTE,2(R2)          IS BIT STILL ON                              
         BNE   EXITL               NO, EXIT LO                                  
*                                                                               
PFX      B     EXITOK                                                           
*                                                                               
PROGTAB  DC    C'MX',X'08'         MX=MBI BIT                                   
         DC    X'FF'                                                            
         EJECT                                                                  
********************************************************************            
* GET PRODUCT LEVEL UCOM RECORD                                    *            
********************************************************************            
*                                                                               
PRDUCOM  NTR1                                                                   
         CLI   UCSYS,C'S'          SPOT                                         
         BE    PRDU10                                                           
         CLI   UCSYS,C'N'          OR NET                                       
         BNE   PRDU20                                                           
*                                                                               
         USING UCOMHDRD,R6                                                      
PRDU10   LA    R6,KEY              KEY HAS UP TO CLT                            
         MVC   UCOMKPRD,UCPRD      FILL IN PRD                                  
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'SPTDIR',KEY,KEY,DMWORK               
         CLC   KEY(13),KEYSAVE                                                  
         BNE   ERRNOPRD                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'SPTFIL',KEY+14,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
PRDU20   CLI   UCSYS,C'P'          PRINT                                        
         BNE   PRDU30                                                           
*                                                                               
         USING PUCOMD,R6                                                        
         LA    R6,KEY              KEY HAS CLIENT                               
         MVC   PUCMKPRD,UCPRD      FILL IN PRODUCT                              
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'PRTDIR',KEY,KEY,DMWORK               
         CLC   KEY(25),KEYSAVE                                                  
         BNE   ERRNOPRD                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'PRTFIL',KEY+27,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO+33            FIRST ELEM                                   
         ST    R6,IOELEM                                                        
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
PRDU30   DS    0H                                                               
         DC    H'0'                                                             
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
********************************************************************            
* GET ESTIMATE LEVEL UCOM RECORD                                   *            
********************************************************************            
*                                                                               
ESTUCOM  NTR1                                                                   
         CLI   UCSYS,C'S'          SPOT                                         
         BE    ESTU10                                                           
         CLI   UCSYS,C'N'          OR NET                                       
         BNE   ESTU20                                                           
*                                                                               
         USING UCOMHDRD,R6                                                      
ESTU10   LA    R6,KEY              KEY HAS UP TO CLT                            
         MVC   UCOMKPRD,UCPRD      MAKE SURE HAVE PRD                           
         MVC   UCOMKEST,UCSEST     AND FILL IN ESTIMATE                         
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'SPTDIR',KEY,KEY,DMWORK               
         CLC   KEY(13),KEYSAVE                                                  
         BNE   ERRNOEST                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'SPTFIL',KEY+14,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
ESTU20   CLI   UCSYS,C'P'          PRINT                                        
         BNE   ESTU30                                                           
*                                                                               
         USING PUCOMD,R6                                                        
         LA    R6,KEY              KEY HAS CLIENT                               
         MVC   PUCMKPRD,UCPRD      MAKE SURE HAVE PRODUCT                       
         MVC   PUCMKEST,UCEST      FILL IN EST                                  
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'PRTDIR',KEY,KEY,DMWORK               
         CLC   KEY(25),KEYSAVE                                                  
         BNE   ERRNOEST                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'PRTFIL',KEY+27,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO+33            FIRST ELEM                                   
         ST    R6,IOELEM                                                        
         B     EXIT                                                             
         DROP  R6                                                               
*                                                                               
ESTU30   DS    0H                                                               
         DC    H'0'                                                             
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
********************************************************************            
* GET MARKET LEVEL UCOM RECORD - SPOT ONLY                         *            
********************************************************************            
*                                                                               
MKTUCOM  NTR1                                                                   
*                                                                               
         USING UCOMHDRD,R6                                                      
         LA    R6,KEY              KEY HAS UP TO CLT                            
         MVC   UCOMKPRD,UCPRD      MAKE SURE HAVE PRD                           
         MVC   UCOMKEST,UCSEST     MAKE SURE HAVE EST                           
         MVC   UCOMKMKT,UCMKT      AND FILL IN MARKET                           
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'SPTDIR',KEY,KEY,DMWORK               
         CLC   KEY(13),KEYSAVE                                                  
         BNE   ERRNOMKT                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'SPTFIL',KEY+14,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
********************************************************************            
* GET REGION LEVEL UCOM RECORD  - FOR PRINT                        *            
********************************************************************            
*                                                                               
REGUCOM  NTR1                                                                   
*                                                                               
         USING PUCOMD,R6                                                        
         LA    R6,KEY              KEY HAS CLIENT                               
         MVC   PUCMKPRD,UCPRD      MAKE SURE HAVE OTHER FIELDS                  
         MVC   PUCMKEST,UCEST                                                   
         MVC   PUCMKDIV,UCDIV                                                   
         MVC   PUCMKREG,UCREG                                                   
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'PRTDIR',KEY,KEY,DMWORK               
         CLC   KEY(25),KEYSAVE                                                  
         BNE   ERRNOREG                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'PRTFIL',KEY+27,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO+33            FIRST ELEM                                   
         ST    R6,IOELEM                                                        
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
********************************************************************            
* GET DISTRICT LEVEL UCOM - PRINT ONLY                             *            
********************************************************************            
*                                                                               
DSTUCOM  NTR1                                                                   
*                                                                               
         USING PUCOMD,R6                                                        
         LA    R6,KEY              KEY HAS CLIENT                               
         MVC   PUCMKPRD,UCPRD      MAKE SURE HAVE OTHER FIELDS                  
         MVC   PUCMKEST,UCEST                                                   
         MVC   PUCMKDIV,UCDIV                                                   
         MVC   PUCMKREG,UCREG                                                   
         MVC   PUCMKDST,UCDST                                                   
*                                                                               
         MVC   KEYSAVE,KEY                                                      
         GOTO1 DMGR,DMCB,(0,=C'DMRDHI'),=C'PRTDIR',KEY,KEY,DMWORK               
         CLC   KEY(25),KEYSAVE                                                  
         BNE   ERRNODST                                                         
         GOTO1 DMGR,DMCB,(0,=C'GETREC'),=C'PRTFIL',KEY+27,IO,DMWORK             
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         LA    R6,IO+33            FIRST ELEM                                   
         ST    R6,IOELEM                                                        
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
********************************************************************            
* GET PRODUCT, ESTIMATE, OR MARKET LEVEL DATA FROM UCOM REC                     
********************************************************************            
*                                                                               
GETDATA  NTR1                                                                   
         L     R6,IOELEM           A(FIRST ELEM)                                
         B     GETD15                                                           
GETD10   ZIC   R1,1(R6)            NEXT ELEM                                    
         AR    R6,R1                                                            
GETD15   CLI   0(R6),0                                                          
         BE    GETDX                                                            
*                                                                               
         CLI   ELEM60S,C'Y'        PROCESS X'60' ELEMS AND HIGHER               
         BE    *+12                                                             
         CLI   0(R6),X'60'                                                      
         BNL   GETD10                                                           
*                                                                               
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   GETD18                                                           
         MVC   BYTE,0(R6)                                                       
         NI    BYTE,X'F0'                                                       
         CLI   BYTE,X'50'                                                       
         BNE   GETD10                                                           
         LA    R3,ELEMTABE         TABLE OF ELEMS AND DISPS FOR EST             
         B     *+8                                                              
*                                                                               
         USING ELEMTABD,R3                                                      
GETD18   LA    R3,ELEMTAB          TABLE OF ELEMS AND DISPS                     
*                                                                               
GETD20   CLI   0(R3),X'FF'                                                      
         BE    GETD10                                                           
         CLC   0(1,R3),0(R6)       MATCH ON ELEM CODE                           
         BNE   GETD26                                                           
         CLI   SYSTEM,0            SYSTEM SPECIFIC TABLE ENTRY?                 
         BE    GETD30              NO THEN GOT IT                               
         CLC   SYSTEM,UCSYS        RIGHT SYSTEM?                                
         BE    GETD30                                                           
GETD26   LA    R3,ELTABLNQ(R3)     NEXT IN TABLE                                
         B     GETD20                                                           
*                                                                               
GETD30   LA    R2,WORKD            FIRST CHECK IF TITLE THERE                   
         AH    R2,TTLDISP          DISP TO TITLE                                
         OC    0(20,R2),0(R2)                                                   
         BZ    GETD10              NO THEN SKIP DATA                            
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,LENDISP          DISP TO LENGTH                               
         ZIC   R1,1(R6)            LEN ON ELEM                                  
         AHI   R1,-6               6 FOR HDR                                    
         BM    GETD10              CAN'T BE MINUS                               
         STC   R1,0(R2)                                                         
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         AHI   R1,-1               1 FOR EX                                     
         BM    GETD10                                                           
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),6(R6)       FILL IN DATA                                 
         B     GETD10              NEXT ELEM                                    
*                                                                               
GETDX    MVC   UCPLENS,PLENS       RETURN LENS                                  
         MVC   UCELENS,ELENS                                                    
         TM    UCOPT,UCOMKT        DOES CALLER WANT MKT DATA                    
         BNO   *+10                                                             
         MVC   UCMLENS,MLENS                                                    
         TM    UCOPT,UCOREG+UCODST DOES CALLER WANT REGION/DISTRICT             
         BZ    *+16                                                             
         MVC   UCRLENS,RLENS                                                    
         MVC   UCDLENS,DLENS                                                    
         B     EXIT                                                             
*                                                                               
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK EFFECTIVE DATE FOR ESTIMATE UCOM                              *         
***********************************************************************         
CHKEDATE NTR1                                                                   
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   CHKED10                                                          
         LA    R3,ELEMTABE         TABLE OF ELEMS AND DISPS FOR EST             
         B     CHKED20                                                          
*                                                                               
         USING ELEMTABD,R3                                                      
CHKED10  LA    R3,ELEMTAB          TABLE OF ELEMS AND DISPS                     
*                                                                               
         LA    R3,4*ELTABLNQ(R3)   POINT TO ESTIMATE DATA                       
*                                                                               
CHKED20  LA    R2,WORKD            FIRST CHECK IF TITLE THERE                   
         AH    R2,TTLDISP          DISP TO TITLE                                
         OC    0(20,R2),0(R2)                                                   
         BZ    CHKED50             NO THEN SKIP DATA                            
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         CLI   0(R2),C'#'          SEE IF HAVE EFF DATE RANGE                   
         BNE   CHKED24                                                          
         CLI   5(R2),C'#'                                                       
         BNE   CHKED24                                                          
         CLC   =C'##',2(R2)        MAKING SURE IT'S NOT #T##R#                  
         BNE   CHKED26             TRUE EFF DATES, GOTO COMPARE RANGE           
*                                                                               
CHKED24  TM    UCOPTSP,UCOEFFO     RETURNING 'EFFECTIVE' UCOMS ONLY ?           
         BO    CHKED30             YES - NEED TO CLEAR                          
         B     CHKED50             NO, DON'T TOUCH THIS UCOM'S DATA             
*                                                                               
CHKED26  CLC   UCMOS,1(R2)         COMPARE AGAINST DATE RANGE                   
         BL    CHKED30             OUTSIDE OF RANGE -- NEED TO CLEAR            
         CLC   UCMOS,3(R2)                                                      
         BH    CHKED30             OUTSIDE OF RANGE -- NEED TO CLEAR            
         B     CHKED40             THIS UCOM IS WITHIN EF RANGE - KEEP          
*                                                                               
*                                                                               
CHKED30  XC    0(32,R2),0(R2)      CLEAR DATA                                   
         LA    R2,WORKD                                                         
         AH    R2,TTLDISP                                                       
         XC    0(20,R2),0(R2)      CLEAR TITLE                                  
         LA    R2,WORKD                                                         
         AH    R2,MAXDISP          CLEAR MAX DATA LENGTH                        
         XC    0(1,R2),0(R2)                                                    
         LA    R2,WORKD                                                         
         AH    R2,EDITDISP         CLEAR EDIT RULE                              
         XC    0(1,R2),0(R2)                                                    
         LA    R2,WORKD                                                         
         AH    R2,LENDISP          CLEAR LENGTH                                 
         XC    0(1,R2),0(R2)                                                    
         B     CHKED50             CHECK NEXT UCOM                              
*                                                                               
*                                                                               
CHKED40  MVC   WORK(32),0(R2)      TEMP                                         
         LA    R2,WORKD                                                         
         AH    R2,LENDISP                                                       
         ZIC   R1,0(R2)                                                         
         CHI   R1,7                                                             
         BL    CHKED50             NO INPUT BEYOND # ? LEAVE ALONE              
         AHI   R1,-6               GET RID OF DATE RANGE OVERHEAD               
         STC   R1,0(R2)                                                         
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         XC    0(32,R2),0(R2)      CLEAR DATA                                   
         AHI   R1,-1               1 FOR EX                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORK+6                                                   
*                                                                               
*                                                                               
CHKED50  LA    R3,ELTABLNQ(R3)     NEXT IN TABLE                                
         CLI   0(R3),X'FF'         EOT ?                                        
         BE    *+12                                                             
         CLI   0(R3),X'60'         DON'T LOOK HIGHER THEN EST (51-58)           
         BL    CHKED20                                                          
*                                                                               
         MVC   UCELENS,ELENS       REINSTATE LENS                               
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   *+10                                                             
         MVC   UCPLENS,PLENS       PRODUCT LENGTHS ALSO                         
*                                                                               
         OC    UCELENS,UCELENS     ANYTHING LEFT AFTER OUR 'CLEANING'?          
         BNZ   CHKEDX              YES, DONE                                    
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   *+14                                                             
         OC    UCPLENS,UCPLENS     ALSO CHECKING PRODUCT LENGTHS                
         BNZ   CHKEDX                                                           
*                                                                               
         OI    UCDATA,UCDNOEST                                                  
*                                                                               
CHKEDX   B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK NETWORK COST TYPE FOR ESTIMATE UCOM                                     
***********************************************************************         
CHKCTYP  NTR1                                                                   
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   CHKCT10                                                          
         LA    R3,ELEMTABE         TABLE OF ELEMS AND DISPS FOR EST             
         B     CHKCT20                                                          
*                                                                               
         USING ELEMTABD,R3                                                      
CHKCT10  LA    R3,ELEMTAB          TABLE OF ELEMS AND DISPS                     
*                                                                               
         LA    R3,4*ELTABLNQ(R3)   POINT TO ESTIMATE DATA                       
*                                                                               
CHKCT20  LA    R2,WORKD            FIRST CHECK IF TITLE THERE                   
         AH    R2,TTLDISP          DISP TO TITLE                                
         OC    0(20,R2),0(R2)                                                   
         BZ    CHKCT50             NO THEN SKIP DATA                            
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
*                                                                               
         CLC   =C'#R#',0(R2)       DON'T CONFUSE LINE ITEM W/ CTYPE             
         BE    CHKCT23                                                          
         CLC   =C'#F#',0(R2)       DON'T CONFUSE LINE ITEM W/ CTYPE             
         BE    CHKCT23                                                          
*                                                                               
         CLI   0(R2),C'#'          SEE IF HAVE COST TYPE                        
         BNE   *+20                                                             
         CLI   2(R2),C'#'                                                       
         BNE   *+12                                                             
         LA    R4,0(R2)            POINT R4 TO COST TYPE                        
         B     CHKCT25             GOTO COMPARE COST TYPE                       
*                                                                               
         CLI   6(R2),C'#'          DON'T FORGET EFFECTIVE DATE RANGE            
         BNE   *+20                                                             
         CLI   8(R2),C'#'                                                       
         BNE   *+12                                                             
         LA    R4,6(R2)            POINT R4 TO COST TYPE                        
         B     CHKCT25                                                          
*                                                                               
CHKCT23  TM    UCOPTSP,UCOCTYO     UCOMS ONLY FOR SPECIFIED COST TYPE?          
         BO    CHKCT30             YES - NEED TO CLEAR                          
         B     CHKCT50             NO, DON'T TOUCH THIS UCOM'S DATA             
*                                                                               
CHKCT25  TM    UCOPTSP,UCOCTCL     IF CTYPES TOGETHER MIGHT NEED TO             
         BO    CHKCT30             CLEAR CTYPE UCOMS                            
         CLC   UCCTYP,1(R4)        COMPARE AGAINST REQUESTED CTYPE              
         BNE   CHKCT30             NOT THE SAME -- NEED TO CLEAR                
         B     CHKCT40             RIGHT COST TYPE -- KEEP                      
*                                                                               
*                                                                               
CHKCT30  XC    0(32,R2),0(R2)      CLEAR DATA                                   
         LA    R2,WORKD                                                         
         AH    R2,TTLDISP                                                       
         XC    0(20,R2),0(R2)      CLEAR TITLE                                  
         LA    R2,WORKD                                                         
         AH    R2,MAXDISP          CLEAR MAX DATA LENGTH                        
         XC    0(1,R2),0(R2)                                                    
         LA    R2,WORKD                                                         
         AH    R2,EDITDISP         CLEAR EDIT RULE                              
         XC    0(1,R2),0(R2)                                                    
         LA    R2,WORKD                                                         
         AH    R2,LENDISP          CLEAR LENGTH                                 
         XC    0(1,R2),0(R2)                                                    
         B     CHKCT50             CHECK NEXT UCOM                              
*                                                                               
*                                                                               
CHKCT40  MVC   WORK(32),0(R2)      TEMP                                         
         CR    R2,R4               IS CTYPE AFTER EFF DATE RANGE?               
         BNE   CHKCT45             YES, CLEAR IT FROM MIDDLE OF STRING          
         LA    R2,WORKD            NO, CLEAR 1ST 3 BYTES                        
         AH    R2,LENDISP                                                       
         ZIC   R1,0(R2)                                                         
         CHI   R1,4                                                             
         BL    CHKCT50             NO INPUT BEYOND # ? LEAVE ALONE              
         AHI   R1,-3               GET RID OF DATE RANGE OVERHEAD               
         STC   R1,0(R2)                                                         
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         XC    0(32,R2),0(R2)      CLEAR DATA                                   
         AHI   R1,-1               1 FOR EX                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORK+3                                                   
         B     CHKCT50                                                          
*                                                                               
CHKCT45  DS    0H                  LEAVE THE DATE RANGE, CLEAR CTYPE            
         LA    R2,WORKD            (IN THEORY SHOULD NOT HAPPEN)                
         AH    R2,LENDISP                                                       
         ZIC   R1,0(R2)                                                         
         AHI   R1,-3               GET RID OF COST TYPE OVERHEAD                
         STC   R1,0(R2)                                                         
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         XC    0(32,R2),0(R2)      CLEAR DATA                                   
         MVC   0(6,R2),WORK        COPY EFF DATE RANGE                          
         CHI   R1,6                ONLY EFF DATE,CTYPE & NOTHING AFTER          
         BNL   *+6                                                              
         DC    H'0'                JUST CHECKING MYSELF                         
         BE    CHKCT50             YES, DONE                                    
         AHI   R1,-7               6 FOR EFF DATE + 1 FOR EX                    
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   6(0,R2),WORK+9                                                   
         B     CHKCT50                                                          
*                                                                               
CHKCT50  LA    R3,ELTABLNQ(R3)     NEXT IN TABLE                                
         CLI   0(R3),X'FF'         EOT ?                                        
         BE    *+12                                                             
         CLI   0(R3),X'60'         DON'T LOOK HIGHER THEN EST (51-58)           
         BL    CHKCT20                                                          
*                                                                               
         MVC   UCELENS,ELENS       REINSTATE LENS                               
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   *+10                                                             
         MVC   UCPLENS,PLENS       PRODUCT LENGTHS ALSO                         
*                                                                               
         OC    UCELENS,UCELENS     ANYTHING LEFT AFTER OUR 'CLEANING'?          
         BNZ   CHKCTX                                                           
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   *+14                                                             
         OC    UCPLENS,UCPLENS     ALSO CHECKING PRODUCT LENGTHS                
         BNZ   CHKCTX                                                           
*                                                                               
         OI    UCDATA,UCDNOEST                                                  
*                                                                               
CHKCTX   B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* CHECK IF COMMENT FOR MAIN BILL OR COMISSION BILL                              
***********************************************************************         
CHKCOMM  NTR1                                                                   
         MVC   FULL(1),UCCTYP                                                   
         CLI   UCSYS,C'S'                                                       
         BE    *+10                                                             
         MVC   FULL(1),UCMKT                                                    
*                                                                               
         MVI   BYTE,C'C'           CLEAR #R# OR #F# IF THERE                    
*                                                                               
         CLI   FULL,C'R'           MAIN BILL COMMENT?                           
         BE    *+12                                                             
         CLI   FULL,C'F'           COMM BILL COMMENT?                           
         BNE   *+10                                                             
*                                                                               
         XC    BYTE,BYTE                                                        
*                                                                               
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   CHKCM10                                                          
         LA    R3,ELEMTABE         TABLE OF ELEMS AND DISPS FOR EST             
         B     CHKCM20                                                          
         USING ELEMTABD,R3                                                      
CHKCM10  LA    R3,ELEMTAB          TABLE OF ELEMS AND DISPS                     
*                                                                               
         LA    R3,4*ELTABLNQ(R3)   POINT TO ESTIMATE DATA                       
*                                                                               
CHKCM20  LA    R2,WORKD            FIRST CHECK IF TITLE THERE                   
         AH    R2,TTLDISP          DISP TO TITLE                                
         OC    0(20,R2),0(R2)                                                   
         BZ    CHKCM50             NO THEN SKIP DATA                            
*                                                                               
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         CLC   =C'#R#',0(R2)       SEE IF HAVE BILL TYPE                        
         BE    *+14                YES, MAIN BILL                               
         CLC   =C'#F#',0(R2)       SEE IF HAVE BILL TYPE                        
         BNE   CHKCM50             NO, DON'T TOUCH THIS UCOM'S DATA             
*                                                                               
         CLI   BYTE,C'C'           CLEAR #R# OR #F# IF THERE                    
         BE    CHKCM40                                                          
         LA    R4,0(R2)            POINT R4 TO BILL TYPE                        
*                                                                               
         CLC   FULL(1),1(R4)       COMPARE AGAINST REQUESTED TYPE               
         BE    CHKCM40             RIGHT TYPE -- KEEP, ELSE CLEAR               
*                                                                               
         XC    0(32,R2),0(R2)      CLEAR DATA                                   
         LA    R2,WORKD                                                         
         AH    R2,TTLDISP                                                       
         XC    0(20,R2),0(R2)      CLEAR TITLE                                  
         LA    R2,WORKD                                                         
         AH    R2,MAXDISP          CLEAR MAX DATA LENGTH                        
         XC    0(1,R2),0(R2)                                                    
         LA    R2,WORKD                                                         
         AH    R2,EDITDISP         CLEAR EDIT RULE                              
         XC    0(1,R2),0(R2)                                                    
         LA    R2,WORKD                                                         
         AH    R2,LENDISP          CLEAR LENGTH                                 
         XC    0(1,R2),0(R2)                                                    
         B     CHKCM50             CHECK NEXT UCOM                              
*                                                                               
*                                                                               
CHKCM40  MVC   WORK(32),0(R2)      TEMP                                         
         LA    R2,WORKD            CLEAR 1ST 3 BYTES                            
         AH    R2,LENDISP                                                       
         ZIC   R1,0(R2)                                                         
         CHI   R1,4                                                             
         BL    CHKCM50             NO INPUT BEYOND # ? LEAVE ALONE              
         AHI   R1,-3               GET RID OF DATE RANGE OVERHEAD               
         STC   R1,0(R2)                                                         
         LA    R2,WORKD                                                         
         AH    R2,DATADISP         DISP TO DATA                                 
         XC    0(32,R2),0(R2)      CLEAR DATA                                   
         AHI   R1,-1               1 FOR EX                                     
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),WORK+3                                                   
*                                                                               
CHKCM50  LA    R3,ELTABLNQ(R3)     NEXT IN TABLE                                
         CLI   0(R3),X'FF'         EOT ?                                        
         BE    *+12                                                             
         CLI   0(R3),X'60'         DON'T LOOK HIGHER THEN EST (51-58)           
         BL    CHKCM20                                                          
*                                                                               
         MVC   UCELENS,ELENS       REINSTATE LENS                               
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   *+10                                                             
         MVC   UCPLENS,PLENS       PRODUCT LENGTHS ALSO                         
*                                                                               
         OC    UCELENS,UCELENS     ANYTHING LEFT AFTER OUR 'CLEANING'?          
         BNZ   CHKCMX                                                           
         TM    UCOPT,UCO8EST       8 EST UCOMS ?                                
         BNO   *+14                                                             
         OC    UCPLENS,UCPLENS     ALSO CHECKING PRODUCT LENGTHS                
         BNZ   CHKCMX                                                           
*                                                                               
         OI    UCDATA,UCDNOEST                                                  
*                                                                               
CHKCMX   B     EXIT                                                             
         DROP  R3                                                               
         EJECT                                                                  
***********************************************************************         
* ERROR EXITS                                                         *         
***********************************************************************         
         SPACE 1                                                                
ERRPARM  MVI   UCERROR,UCERPARM                                                 
         B     EXITBACK                                                         
ERRNOCLT OI    UCDATA,UCDNOCLT+UCDNOPRD+UCDNOEST+UCDNOMKT                       
         B     EXITBACK            RETURN THERE'S NOTHING ELSE TO DO            
ERRNOPRD OI    UCDATA,UCDNOPRD     SET NO PRD UCOM REC                          
         MVC   KEY,KEYSAVE         RESET KEY                                    
         B     EXIT                BUT CONTINUE ON TO ESTIMATE                  
ERRNOEST OI    UCDATA,UCDNOEST     SET NO EST UCOM REC                          
         MVC   KEY,KEYSAVE         RESET KEY                                    
         B     EXIT                BUT CONTINUE TO MARKET                       
ERRNOMKT OI    UCDATA,UCDNOMKT     SET NO MKT UCOM REC                          
         B     EXITBACK            RETURN THERE'S NOTHING ELSE TO DO            
ERRNOREG OI    UCDATA,UCDNOREG     SET NO REG UCOM REC                          
         MVC   KEY,KEYSAVE         RESET KEY                                    
         B     EXIT                BUT CONTINUE TO DISTRICT                     
ERRNODST OI    UCDATA,UCDNODST     SET NO DST UCOM REC                          
         B     EXITBACK            RETURN THERE'S NOTHING ELSE TO DO            
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND HANDY ROUTINES                                      *         
***********************************************************************         
         SPACE 1                                                                
EXITOK   CR    RB,RB                                                            
         B     EXIT                                                             
*                                                                               
EXITL    CLI   *,255                                                            
         B     EXIT                                                             
*                                                                               
EXIT     XIT1  ,                                                                
*                                                                               
EXITBACK L     R1,SAVER1           RETURN PARAMETER LIST                        
         MVC   0(UCOMDLNQ,R1),UCOMBLK                                           
         L     RD,SAVERD                                                        
         CLI   UCERROR,0           SET CC FOR RETURN                            
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         SPACE 1                                                                
         LTORG                                                                  
*                                                                               
* ELEM CODE / SPARE / DISP TO TITLE / TO DATA / TO DATA LEN                     
*                                                                               
         DC    0F                                                               
ELEMTAB  DC   X'41',X'00',AL2(PTTL1-WORKD),AL2(PMAXLN1-WORKD)                   
         DC   AL2(PEDIT1-WORKD),AL2(PDATA1-WORKD),AL2(PLEN1-WORKD)              
         DC   X'42',X'00',AL2(PTTL2-WORKD),AL2(PMAXLN2-WORKD)                   
         DC   AL2(PEDIT2-WORKD),AL2(PDATA2-WORKD),AL2(PLEN2-WORKD)              
         DC   X'43',X'00',AL2(PTTL3-WORKD),AL2(PMAXLN3-WORKD)                   
         DC   AL2(PEDIT3-WORKD),AL2(PDATA3-WORKD),AL2(PLEN3-WORKD)              
         DC   X'44',X'00',AL2(PTTL4-WORKD),AL2(PMAXLN4-WORKD)                   
         DC   AL2(PEDIT4-WORKD),AL2(PDATA4-WORKD),AL2(PLEN4-WORKD)              
         DC   X'51',X'00',AL2(ETTL1-WORKD),AL2(EMAXLN1-WORKD)                   
         DC   AL2(EEDIT1-WORKD),AL2(EDATA1-WORKD),AL2(ELEN1-WORKD)              
         DC   X'52',X'00',AL2(ETTL2-WORKD),AL2(EMAXLN2-WORKD)                   
         DC   AL2(EEDIT2-WORKD),AL2(EDATA2-WORKD),AL2(ELEN2-WORKD)              
         DC   X'53',X'00',AL2(ETTL3-WORKD),AL2(EMAXLN3-WORKD)                   
         DC   AL2(EEDIT3-WORKD),AL2(EDATA3-WORKD),AL2(ELEN3-WORKD)              
         DC   X'54',X'00',AL2(ETTL4-WORKD),AL2(EMAXLN4-WORKD)                   
         DC   AL2(EEDIT4-WORKD),AL2(EDATA4-WORKD),AL2(ELEN4-WORKD)              
* MKT LEVEL FOR SPOT                                                            
         DC   X'61',C'S',AL2(MTTL1-WORKD),AL2(MMAXLN1-WORKD)                    
         DC   AL2(MEDIT1-WORKD),AL2(MDATA1-WORKD),AL2(MLEN1-WORKD)              
         DC   X'62',C'S',AL2(MTTL2-WORKD),AL2(MMAXLN2-WORKD)                    
         DC   AL2(MEDIT2-WORKD),AL2(MDATA2-WORKD),AL2(MLEN2-WORKD)              
         DC   X'63',C'S',AL2(MTTL3-WORKD),AL2(MMAXLN3-WORKD)                    
         DC   AL2(MEDIT3-WORKD),AL2(MDATA3-WORKD),AL2(MLEN3-WORKD)              
         DC   X'64',C'S',AL2(MTTL4-WORKD),AL2(MMAXLN4-WORKD)                    
         DC   AL2(MEDIT4-WORKD),AL2(MDATA4-WORKD),AL2(MLEN4-WORKD)              
* REG LEVEL FOR PRINT                                                           
         DC   X'61',C'P',AL2(RTTL1-WORKD),AL2(RMAXLN1-WORKD)                    
         DC   AL2(REDIT1-WORKD),AL2(RDATA1-WORKD),AL2(RLEN1-WORKD)              
         DC   X'62',C'P',AL2(RTTL2-WORKD),AL2(RMAXLN2-WORKD)                    
         DC   AL2(REDIT2-WORKD),AL2(RDATA2-WORKD),AL2(RLEN2-WORKD)              
         DC   X'63',C'P',AL2(RTTL3-WORKD),AL2(RMAXLN3-WORKD)                    
         DC   AL2(REDIT3-WORKD),AL2(RDATA3-WORKD),AL2(RLEN3-WORKD)              
         DC   X'64',C'P',AL2(RTTL4-WORKD),AL2(RMAXLN4-WORKD)                    
         DC   AL2(REDIT4-WORKD),AL2(RDATA4-WORKD),AL2(RLEN4-WORKD)              
* DST LEVEL FOR PRINT                                                           
         DC   X'71',C'P',AL2(DTTL1-WORKD),AL2(DMAXLN1-WORKD)                    
         DC   AL2(DEDIT1-WORKD),AL2(DDATA1-WORKD),AL2(DLEN1-WORKD)              
         DC   X'72',C'P',AL2(DTTL2-WORKD),AL2(DMAXLN2-WORKD)                    
         DC   AL2(DEDIT2-WORKD),AL2(DDATA2-WORKD),AL2(DLEN2-WORKD)              
         DC   X'73',C'P',AL2(DTTL3-WORKD),AL2(DMAXLN3-WORKD)                    
         DC   AL2(DEDIT3-WORKD),AL2(DDATA3-WORKD),AL2(DLEN3-WORKD)              
         DC   X'74',C'P',AL2(DTTL4-WORKD),AL2(DMAXLN4-WORKD)                    
         DC   AL2(DEDIT4-WORKD),AL2(DDATA4-WORKD),AL2(DLEN4-WORKD)              
         DC   X'FF'                                                             
* DST LEVEL FOR 8 ESTIMATE UCOMS. PRODUCT FIELDS IN WORKING STORAGE             
* ARE BEING RE-USED HERE IN ENTRIES 55 TO 58                                    
ELEMTABE DC    0F                                                               
         DC   X'51',X'00',AL2(ETTL1-WORKD),AL2(EMAXLN1-WORKD)                   
         DC   AL2(EEDIT1-WORKD),AL2(EDATA1-WORKD),AL2(ELEN1-WORKD)              
         DC   X'52',X'00',AL2(ETTL2-WORKD),AL2(EMAXLN2-WORKD)                   
         DC   AL2(EEDIT2-WORKD),AL2(EDATA2-WORKD),AL2(ELEN2-WORKD)              
         DC   X'53',X'00',AL2(ETTL3-WORKD),AL2(EMAXLN3-WORKD)                   
         DC   AL2(EEDIT3-WORKD),AL2(EDATA3-WORKD),AL2(ELEN3-WORKD)              
         DC   X'54',X'00',AL2(ETTL4-WORKD),AL2(EMAXLN4-WORKD)                   
         DC   AL2(EEDIT4-WORKD),AL2(EDATA4-WORKD),AL2(ELEN4-WORKD)              
         DC   X'55',X'00',AL2(PTTL1-WORKD),AL2(PMAXLN1-WORKD)                   
         DC   AL2(PEDIT1-WORKD),AL2(PDATA1-WORKD),AL2(PLEN1-WORKD)              
         DC   X'56',X'00',AL2(PTTL2-WORKD),AL2(PMAXLN2-WORKD)                   
         DC   AL2(PEDIT2-WORKD),AL2(PDATA2-WORKD),AL2(PLEN2-WORKD)              
         DC   X'57',X'00',AL2(PTTL3-WORKD),AL2(PMAXLN3-WORKD)                   
         DC   AL2(PEDIT3-WORKD),AL2(PDATA3-WORKD),AL2(PLEN3-WORKD)              
         DC   X'58',X'00',AL2(PTTL4-WORKD),AL2(PMAXLN4-WORKD)                   
         DC   AL2(PEDIT4-WORKD),AL2(PDATA4-WORKD),AL2(PLEN4-WORKD)              
         DC   X'FF'                                                             
*                                                                               
ELEMTABD DSECT                                                                  
ELEMCODE DS    XL1                 ELEM CODE                                    
SYSTEM   DS    XL1                 SYSTEM FOR SPECIFIC ELEMS                    
TTLDISP  DS    XL2                 DISP TO TITLE                                
MAXDISP  DS    XL2                 DISP TO MAX DATA LENGTH                      
EDITDISP DS    XL2                 DISP TO EDIT RULE                            
DATADISP DS    XL2                 DISP TO DATA                                 
LENDISP  DS    XL2                 DISP TO LENGTH                               
ELTABLNQ EQU   *-ELEMTABD                                                       
*                                                                               
         EJECT                                                                  
***********************************************************************         
* WORKING STORAGE DSECT                                               *         
***********************************************************************         
         SPACE 1                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
RELO     DS    A                                                                
SAVER1   DS    A                                                                
SAVERD   DS    A                                                                
*                                                                               
DMGR     DS    A                                                                
IOELEM   DS    A                   A(FIRST ELEM IN REC)                         
*                                                                               
FULL     DS    F                                                                
DMCB     DS    6F                                                               
DMWORK   DS    12D                                                              
WORK     DS    CL80                                                             
BYTE     DS    XL1                                                              
ELEM60S  DS    XL1                                                              
*                                                                               
UCOMBLK  DS    XL(UCOMDLNQ)                                                     
*                                                                               
KEY      DS    XL50                                                             
KEYSAVE  DS    XL50                                                             
SVKEY    DS    XL50                                                             
*                                                                               
PTITLES  DS    CL(20*4)                                                         
         ORG   PTITLES                                                          
PTTL1    DS    CL20                                                             
PTTL2    DS    CL20                                                             
PTTL3    DS    CL20                                                             
PTTL4    DS    CL20                                                             
*                                                                               
PMAXLNS  DS    0XL4                                                             
PMAXLN1  DS    XL1                                                              
PMAXLN2  DS    XL1                                                              
PMAXLN3  DS    XL1                                                              
PMAXLN4  DS    XL1                                                              
*                                                                               
PEDITS   DS    0XL4                                                             
PEDIT1   DS    XL1                                                              
PEDIT2   DS    XL1                                                              
PEDIT3   DS    XL1                                                              
PEDIT4   DS    XL1                                                              
*                                                                               
PLENS    DS    0XL4                                                             
PLEN1    DS    XL1                                                              
PLEN2    DS    XL1                                                              
PLEN3    DS    XL1                                                              
PLEN4    DS    XL1                                                              
*                                                                               
PDATA    DS    CL(32*4)                                                         
         ORG   PDATA                                                            
PDATA1   DS    CL32                                                             
PDATA2   DS    CL32                                                             
PDATA3   DS    CL32                                                             
PDATA4   DS    CL32                                                             
*                                                                               
ETITLES  DS    CL(20*4)                                                         
         ORG   ETITLES                                                          
ETTL1    DS    CL20                                                             
ETTL2    DS    CL20                                                             
ETTL3    DS    CL20                                                             
ETTL4    DS    CL20                                                             
*                                                                               
EMAXLNS  DS    0XL4                                                             
EMAXLN1  DS    XL1                                                              
EMAXLN2  DS    XL1                                                              
EMAXLN3  DS    XL1                                                              
EMAXLN4  DS    XL1                                                              
*                                                                               
EEDITS   DS    0XL4                                                             
EEDIT1   DS    XL1                                                              
EEDIT2   DS    XL1                                                              
EEDIT3   DS    XL1                                                              
EEDIT4   DS    XL1                                                              
*                                                                               
ELENS    DS    0XL4                                                             
ELEN1    DS    XL1                                                              
ELEN2    DS    XL1                                                              
ELEN3    DS    XL1                                                              
ELEN4    DS    XL1                                                              
*                                                                               
EDATA    DS    CL(32*4)                                                         
         ORG   EDATA                                                            
EDATA1   DS    CL32                                                             
EDATA2   DS    CL32                                                             
EDATA3   DS    CL32                                                             
EDATA4   DS    CL32                                                             
*                                                                               
MTITLES  DS    CL(20*4)            MARKETS FOR SPOT                             
         ORG   MTITLES                                                          
MTTL1    DS    CL20                                                             
MTTL2    DS    CL20                                                             
MTTL3    DS    CL20                                                             
MTTL4    DS    CL20                                                             
*                                                                               
MMAXLNS  DS    0XL4                                                             
MMAXLN1  DS    XL1                                                              
MMAXLN2  DS    XL1                                                              
MMAXLN3  DS    XL1                                                              
MMAXLN4  DS    XL1                                                              
*                                                                               
MEDITS   DS    0XL4                                                             
MEDIT1   DS    XL1                                                              
MEDIT2   DS    XL1                                                              
MEDIT3   DS    XL1                                                              
MEDIT4   DS    XL1                                                              
*                                                                               
MLENS    DS    0XL4                                                             
MLEN1    DS    XL1                                                              
MLEN2    DS    XL1                                                              
MLEN3    DS    XL1                                                              
MLEN4    DS    XL1                                                              
*                                                                               
MDATA    DS    CL(32*4)                                                         
         ORG   MDATA                                                            
MDATA1   DS    CL32                                                             
MDATA2   DS    CL32                                                             
MDATA3   DS    CL32                                                             
MDATA4   DS    CL32                                                             
*                                                                               
         ORG   MTITLES             ORG PRINT REGIONS OVER SPOT MARKETS          
RTITLES  DS    CL(20*4)            REGIONS FOR PRINT                            
         ORG   RTITLES                                                          
RTTL1    DS    CL20                                                             
RTTL2    DS    CL20                                                             
RTTL3    DS    CL20                                                             
RTTL4    DS    CL20                                                             
*                                                                               
RMAXLNS  DS    0XL4                                                             
RMAXLN1  DS    XL1                                                              
RMAXLN2  DS    XL1                                                              
RMAXLN3  DS    XL1                                                              
RMAXLN4  DS    XL1                                                              
*                                                                               
REDITS   DS    0XL4                                                             
REDIT1   DS    XL1                                                              
REDIT2   DS    XL1                                                              
REDIT3   DS    XL1                                                              
REDIT4   DS    XL1                                                              
*                                                                               
RLENS    DS    0XL4                                                             
RLEN1    DS    XL1                                                              
RLEN2    DS    XL1                                                              
RLEN3    DS    XL1                                                              
RLEN4    DS    XL1                                                              
*                                                                               
RDATA    DS    CL(32*4)                                                         
         ORG   RDATA                                                            
RDATA1   DS    CL32                                                             
RDATA2   DS    CL32                                                             
RDATA3   DS    CL32                                                             
RDATA4   DS    CL32                                                             
*                                                                               
DTITLES  DS    CL(20*4)            DISTRICTS FOR PRINT                          
         ORG   DTITLES                                                          
DTTL1    DS    CL20                                                             
DTTL2    DS    CL20                                                             
DTTL3    DS    CL20                                                             
DTTL4    DS    CL20                                                             
*                                                                               
DMAXLNS  DS    0XL4                                                             
DMAXLN1  DS    XL1                                                              
DMAXLN2  DS    XL1                                                              
DMAXLN3  DS    XL1                                                              
DMAXLN4  DS    XL1                                                              
*                                                                               
DEDITS   DS    0XL4                                                             
DEDIT1   DS    XL1                                                              
DEDIT2   DS    XL1                                                              
DEDIT3   DS    XL1                                                              
DEDIT4   DS    XL1                                                              
*                                                                               
DLENS    DS    0XL4                                                             
DLEN1    DS    XL1                                                              
DLEN2    DS    XL1                                                              
DLEN3    DS    XL1                                                              
DLEN4    DS    XL1                                                              
*                                                                               
DDATA    DS    CL(32*4)                                                         
         ORG   DDATA                                                            
DDATA1   DS    CL32                                                             
DDATA2   DS    CL32                                                             
DDATA3   DS    CL32                                                             
DDATA4   DS    CL32                                                             
*                                                                               
IO       DS    CL2000                                                           
*                                                                               
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
         SPACE 1                                                                
* DDUCOMD                                                                       
       ++INCLUDE DDUCOMD                                                        
* SPGENCUOM                                                                     
UCOMHDRD DSECT                                                                  
       ++INCLUDE SPGENUCOM                                                      
* PUCOMREC                                                                      
PUCOMD   DSECT                                                                  
       ++INCLUDE PUCOMREC                                                       
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'031DDUCOM    01/15/15'                                      
         END                                                                    
