*          DATA SET DDUNFLD    AT LEVEL 015 AS OF 07/28/04                      
*CATALP DDUNFLD                                                                 
                                                                                
*=================================================================              
* TAKE DDDLFLD OUTPUT AND RETURN IN FIXED LENGTH FIELDS                         
* TEXT DATA IS LEFT ALIGNED WITH A MAX FIELD LENGTH OF 80                       
* NUMERIC DATA IS RIGHT ALIGNED IN THE FIRST 16 BYTES                           
*=================================================================              
                                                                                
DDUNFLD  TITLE 'TRANSLATE DDDLFLD OUTPUT'                                       
DDUNFLD  CSECT                                                                  
         NMOD1 WORKX-WORKD,**UNFL**                                             
         USING WORKD,RC                                                         
*                                                                               
         LR    RA,R1                                                            
         USING UNFLDD,RA                                                        
*                                                                               
         L     R7,UNFLBLK          POINT TO OUTPUT BLOCK                        
         LH    R0,UNFLRECL         GET OUTPUT RECSIZE                           
         BNZ   *+8                                                              
         LHI   R0,80                                                            
         STH   R0,UNFLRECL                                                      
*                                                                               
         L     R5,UNFLAREC         INPUT RECORD ADDRESS                         
         AHI   R5,1                SKIP FIRST CHAR                              
         CLI   0(R5),C' '          AND POSSIBLY SECOND                          
         BH    *+8                                                              
         AHI   R5,1                                                             
*                                                                               
DDUN2    ST    R5,FLDSTART                                                      
         LH    RE,UNFLRECL                                                      
         AHI   RE,-2                                                            
         MVI   0(R7),C' '                                                       
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R7),0(R7)       FILL OUTPUT WITH SPACES                      
*                                                                               
         CLI   0(R5),C'0'          TEST NUMERIC FIELD                           
         BL    DDUN10                                                           
         CLI   0(R5),C'9'                                                       
         BH    DDUN10                                                           
* NUMERIC FIELD                                                                 
         LHI   R0,C' '             FIELD WILL END WITH A SPACE                  
         LA    R6,17(R5)           MAX LEN+1                                    
*                                                                               
DDUN4    SRST  R6,R5               POINT R6 TO TERMINATOR                       
         BC    1,DDUN4                                                          
         BC    4,*+6                                                            
         DC    H'0'                MUST FIND TERMINATOR                         
*                                                                               
         LR    RF,R6                                                            
         S     RF,FLDSTART         GIVES DATA LENGTH                            
         LA    R1,16(R7)           POINT TO LAST OUTPUT POSITION                
         SR    R1,RF               THEN BACK UP BY STRING LENGTH                
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R1),0(R5)                                                    
         B     DDUN20                                                           
* TEXT FIELD                                                                    
DDUN10   CLI   0(R5),C'"'                                                       
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    R5,1(R5)            SKIP PAST "                                  
         ST    R5,FLDSTART         SET TO START ON NEXT CHAR                    
         LA    R6,81(R5)           SET BOUND ON SEARCH                          
         LHI   R0,C'"'                                                          
*                                                                               
DDUN12   SRST  R6,R5               POINT R6 TO TERMINATOR                       
         BC    1,DDUN12                                                         
         BC    4,*+6                                                            
         DC    H'0'                MUST FIND TERMINATOR                         
*                                                                               
         LR    RF,R6                                                            
         S     RF,FLDSTART         GIVES DATA LENGTH                            
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R7),0(R5)                                                    
*                                                                               
DDUN20   IC    R0,UNFLFLDS                                                      
         AHI   R0,1                                                             
         STC   R0,UNFLFLDS                                                      
*                                                                               
         AH    R7,UNFLRECL         NEXT OUTPUT FIELD                            
*                                                                               
         LA    R5,1(R6)            POINT TO CHAR AFTER DELIMITER                
         CLI   0(R5),C' '          TEST SPACE                                   
         BNE   *+8                 YES - POINTING TO NEXT FIELD                 
         LA    R5,1(R5)            ELSE SKIP OVER SPACE AFTER DELIM             
*                                                                               
         CLI   0(R5),X'5E'         SEMICOLON ENDS INPUT RECORD                  
         BNE   DDUN2                                                            
         XIT1                                                                   
         LTORG                                                                  
*                                                                               
WORKD    DSECT                                                                  
FLDSTART DS    A                                                                
WORKX    EQU   *                                                                
*                                                                               
UNFLDD   DSECT                                                                  
UNFLAREC DS    A                   ADDRESS OF INPUT RECORD                      
UNFLBLK  DS    A                   ADDRESS OF OUTPUT BLOCK                      
UNFLFLDS DS    X                   NUMBER OF FIELDS RETURNED                    
         DS    X                                                                
UNFLRECL DS    H                   MAX OUTPUT RECORD SIZE (DEFAULT 80)          
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'015DDUNFLD   07/28/04'                                      
         END                                                                    
