*          DATA SET FACHKOUT   AT LEVEL 005 AS OF 01/28/19                      
*CATALP FACHKOUT                                                                
         TITLE 'FACILITIES CHKOUT - TAKE TERMINAL/SYSTEM CHECKPOINT'            
         PRINT NOGEN                                                            
CHKOUT   CSECT                                                                  
         NMOD1 CHKWORKX-CHKWORK,**CKPT**                                        
         USING CHKWORK,RC          RC=A(W/S)                                    
         L     RA,=V(SYSFAC)                                                    
         USING SYSFACD,RA          RA=A(SYSFAC)                                 
         L     R4,VSSB                                                          
         USING SSBD,R4             R4=A(SSB)                                    
         L     R7,SSBTKADR                                                      
         USING TCBD,R7             R7=A(TCB)                                    
         L     R3,TCBUTL                                                        
         USING UTLD,R3             R3=A(UTL)                                    
         L     R6,TUTLXADR                                                      
         USING XAUTLD,R6           R6=XA UTL                                    
*                                                                               
         MVC   RECLEN,SSBTWAL      GET TEMPSTR RECORD INFO                      
         MVC   CHKDSP,=Y(CHKPTDSP)                                              
         MVC   GLODSP,=Y(CHKPTGLD)                                              
*                                                                               
         TIME  BIN                 R0=TIME(1/100SEC),R1=DATE(JULIAN)            
         ST    R1,MVSDATE                                                       
         ST    R0,MVSTIME                                                       
         EJECT                                                                  
***********************************************************************         
* SYSTEM IS CHECKPOINTED AT TWA0+CHKPTDSP (SEE FACHKPT)               *         
***********************************************************************         
CHKCLR   L     R5,TCBTWA           CLEAR FACPAK CHECKPOINT AREA IN TWA0         
         AH    R5,CHKDSP                                                        
         USING CHKPTD,R5           R5=A(CHECKPOINT AREA)                        
         LA    R0,CHUTLSV          SET A(AREA TO BE CLEARED)                    
         LA    R1,CHKPTMAX-CHUTLSV SET L'AREA TO BE CLEARED                     
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE               CLEAR REQUIRED CHECKPOINT AREA               
*                                                                               
CHKTIME  MVC   CHTDDATE(8),MVSDATE SET DATE AND TIME                            
*                                                                               
CHKUTL   MVC   CHUTLSV,TSVDATA     SAVE CONNECT UTL DATA                        
         MVC   CHUTLSV1,TSVDATA1                                                
         MVC   CHUTLSV2,TSVDATA2                                                
         MVC   CHUTLSV3,TSVDATA3                                                
         MVC   CHUTLJOB,TJOBINFO                                                
         MVC   CHUTLNUM,TNUM       SAVE STATIC UTL AND VTAM DATA                
         MVC   CHUTLTYP,TTYPE                                                   
         MVC   CHUTLSYM,TCBSYM                                                  
         MVC   CHUTLSTA,TSTAT1                                                  
         NI    CHUTLSTA,TSTATMST+TSTATRSV+TSTATWEB                              
         MVC   CHUTLOFF,TOFFCODE                                                
         MVC   CHUTLCTR,TCTRY                                                   
         MVC   CHUTLCID,TCID                                                    
*                                                                               
         MVC   CHXTINF,TCBXTINF    SAVE TEMPEST                                 
         MVC   CHUTLST8,TSTAT8                                                  
         MVC   CHUTLLDY,TLVLDRTY                                                
         MVC   CHUTLST9,TSTAT9                                                  
         MVC   CHUTLXPI,TXPINFO    SAVE EXTERNAL PROGRAM INFO                   
         MVC   CHUTLPEX,TPASSEXP                                                
         MVC   CHUTLSTB,TSTATB                                                  
         MVC   CHUTLAGP,TAGYPER                                                 
         MVC   CHUTLUPF,TUPDFAC                                                 
         MVC   CHUTLTKT,TTICKET                                                 
*                                                                               
         SAM31                                                                  
         MVC   CHUTLTKN,TTICKETN   SAVE TTICKETN FROM XA UTL                    
         SAM24                                                                  
*                                                                               
         MVC   CHLNSYS,TCBLNSYS    SAVE LOGON SYSTEM AND PROGRAM ID'S           
         MVC   CHLNPRG,TCBLNPRG                                                 
         MVC   CHBILL,TCBBILL      SAVE BILLING REFERENCE                       
         MVC   CHSRMSG,TCBSRMSG    SAVE $CT SERVICE REQ FIELD                   
*                                                                               
         CLI   TSYS,0              TEST TERMINAL CONNECTED                      
         BE    CHKUTLX             NO                                           
         SR    R0,R0               SAVE TCB SYSTEM SWITCH DATA                  
         ICM   R0,1,TCBSWNUM                                                    
         BZ    CHKUTLX             GOTO TCB CHECKPOINT DATA                     
         STC   R0,CHTCBNUM                                                      
*                                  CHECKPOINT CONNECTED SYSTEM                  
         MVC   CHUTLSV+(TSYS-TSVDATA)(1),TCBSWSYS                               
         MVC   CHUTLSV+(TAGYB-TSVDATA)(1),TCBSWAGB                              
         MVC   CHUTLSV+(TACCS-TSVDATA)(4),TCBSWACS                              
         TM    TTEST,TTESTTAC      TEST IF TACCS=TSTTAB ENTRY                   
         BZ    *+10                                                             
         MVC   CHUTLSV+(TACCS-TSVDATA)(4),TACCS                                 
         LA    RE,TCBSWTAB                                                      
         USING TCBSWTAB,RE         RE=A(TCB SYSTEM TABLE)                       
         LA    RF,CHTCBTAB                                                      
         USING CHTCBTAB,RF         RF=A(TWA CHKPT TABLE)                        
CHKUTL2  MVC   CHTCBSYS,TCBSWSYS                                                
         MVC   CHTCBSOV,TCBSWSOV                                                
         MVC   CHTCBAGB,TCBSWAGB                                                
         MVC   CHTCBACS,TCBSWACS                                                
         LA    RE,TCBSWLEN(RE)                                                  
         LA    RF,CHTCBLEN(RF)                                                  
         BCT   R0,CHKUTL2                                                       
         DROP  RE,RF                                                            
*                                                                               
CHKUTLX  EQU   *                                                                
*                                                                               
CHKSSB   L     R1,SSBSEQ           SAVE SSB DATA                                
         LA    R1,1(R1)                                                         
         ST    R1,SSBSEQ           BUMP SAVE SEQUENCE NUMBER                    
         MVC   CHSSBSIN,SSBSIN                                                  
         MVC   CHSSBTT#,SSBTTRN#   TOTAL TRANSACTION FOR ADV                    
         MVC   CHSSBSEQ,SSBSEQ                                                  
         MVC   CHSSBSID,SSBSYSID                                                
         MVC   CHSSBDMP,SSBDMPNO                                                
         MVC   CHSSBTRM,SSBTRMS                                                 
         MVC   CHSSBDSV,SSBDMPSV                                                
CHKSSBX  EQU   *                                                                
*                                                                               
CHKTCB   LA    R1,CHTCBSIN         SET LAST COMPLETED SIN FOR MY TASK           
         L     R7,VTCB                                                          
         LH    R8,0(R7)                                                         
         L     R9,2(R7)                                                         
         LA    R7,6(R7)                                                         
         MVC   0(3,R1),TCBSINL+1   CHECKPOINT LAST COMPLETED SINS               
         LA    R1,3(R1)                                                         
         BXLE  R7,R8,*-10                                                       
CHKTCBX  EQU   *                                                                
*                                                                               
CHKSEL   L     R7,VSELIST          SAVE SELIST DATA                             
         LH    R8,0(R7)                                                         
         L     R9,2(R7)                                                         
         LA    R7,6(R7)                                                         
         USING SELISTD,R7          R7=A(SELIST)                                 
         LA    R2,CHSELTAB                                                      
         USING CHSELTAB,R2         R2=A(SELIST CHECKPOINT TABLE)                
         SR    R1,R1                                                            
         LLC   RF,SSBTASKS         SET RF TO NUMBER OF ACTIVE TASKS             
*                                                                               
CHKSEL2  MVC   DMCB(4),=4X'FF'     FIND LOWEST D/A FOR SE RCVR FILE             
         LA    RE,SERCVDA                                                       
         LR    R0,RF                                                            
CHKSEL4  CLC   0(4,RE),DMCB                                                     
         BH    *+10                                                             
         MVC   DMCB(4),0(RE)                                                    
         LA    RE,4(RE)                                                         
         BCT   R0,CHKSEL4                                                       
*                                                                               
         MVC   CHSELSYS,SESYS      BUILD CHECKPOINT ENTRY FOR SE                
         MVC   CHSELIND,SEIND                                                   
         OC    SESIN,SESIN                                                      
         BZ    *+8                                                              
         OI    CHSELIND,SEIACTV                                                 
*AW      MVC   CHSELRDA,DMCB                                                    
*                                                                               
         LA    R1,1(R1)            BUMP TO NEXT SE                              
         LA    R2,CHSELLEN(R2)                                                  
         BXLE  R7,R8,CHKSEL2                                                    
         STC   R1,CHSELNUM         SET NUMBER OF CHECKPOINTED SE'S              
CHKSELX  EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* PROCESS BROADCAST PENDING                                           *         
***********************************************************************         
CHKBC    TM    TSTAT2,TSTATBCP     TEST IF NEED TO CHECK BRDCSTS                
         BO    CHKBCX                                                           
         TM    TSTAT2,TSTATBCS     HAVE WE JUST CONNECTED TO A SYSTEM           
         BZ    CHKBCX              NO                                           
         NI    TSTAT2,255-TSTATBCS                                              
*                                                                               
         L     R1,MVSTIME          CONVERT TIME TO BINARY HHMMSS                
         SR    R0,R0                                                            
         D     R0,=F'100'          CONVERT TO SECONDS                           
         SR    R0,R0                                                            
         D     R0,=F'60'           CONVERT TO MINUTES                           
         STC   R0,TIMEB+2                                                       
         SR    R0,R0                                                            
         D     R0,=F'60'           CONVERT TO HOURS                             
         STC   R0,TIMEB+1                                                       
         STC   R1,TIMEB+0                                                       
*                                                                               
CHKBC0   CLC   CHBCDATE(8),SSBSDATE TEST IF FIRST TIME TODAY                    
         BNL   CHKBC2                                                           
*                                                                               
CHKBC1   XC    CHBCDATA,CHBCDATA   INITIALISE BRDCST INFO FOR THIS TRM          
         MVC   CHBCDATE(8),MVSDATE                                              
*                                                                               
CHKBC2   L     R7,SSBABC           R7=A(BRDCST TABLE)                           
         USING BCTABD,R7                                                        
         LH    R8,0(R7)                                                         
         L     R9,2(R7)                                                         
         LA    R7,6(R7)                                                         
         MVC   SAVECNUM,BCTLCNUM   SAVE HIGH MESSAGE NUM IN BCTAB               
         OC    BCTLCNUM,BCTLCNUM   TEST IF BCTAB EMPTY                          
         BZ    CHKBCX                                                           
         CLC   CHBCCNUM,BCTLCNUM   TEST HIGH MESSAGE NUMBERS                    
         BH    CHKBC1              INITIALISE IF OUT OF SYNC                    
         LA    R7,BCTABL(R7)                                                    
         XC    BCBYTE(4),BCBYTE    SET BYTE/BIT FOR FIRST DATA ENTRY            
*                                                                               
CHKBC3   OC    BCTNUM,BCTNUM       TEST END OF BCTAB                            
         BZ    CHKBCW                                                           
         LH    RE,BCBIT                                                         
         LH    RF,BCBYTE                                                        
         LA    RE,1(RE)            BUMP TO NEXT BIT                             
         CHI   RE,7                                                             
         BNH   *+10                                                             
         SR    RE,RE                                                            
         LA    RF,1(RF)                                                         
         STH   RE,BCBIT                                                         
         STH   RF,BCBYTE                                                        
         TM    BCTFLAG,BCTFDEL     IGNORE DELETED ENTRIES IN BCTAB              
         BO    CHKBCV                                                           
*                                                                               
         IC    RE,BITMASK(RE)      RE=BIT MASK                                  
         LA    RF,CHBCPNDG(RF)     RF=A(BYTE IN BRDCST PENDING LIST)            
         EX    RE,*+8                                                           
         B     *+8                                                              
         TM    0(RF),0             TEST IF BIT ALREADY ON FOR TRM               
         BO    CHKBCV              YES WE FOUND THIS MSG PREVIOUSLY             
         STC   RE,BCMASK           NO SAVE MASK AND BYTE ADDRESS                
         ST    RF,BCADDR                                                        
*                                                                               
CHKBC4   OC    BCTSTTM,BCTSTTM     TEST IF MSG HAS START TIME                   
         BZ    CHKBC4A                                                          
         CLC   TIMEB(2),BCTSTTM                                                 
         BL    CHKBCV                                                           
CHKBC4A  OC    BCTENTM,BCTENTM     TEST IF MSG HAS END TIME                     
         BZ    CHKBC5                                                           
         CLC   TIMEB(2),BCTENTM                                                 
         BH    CHKBCV                                                           
*                                                                               
CHKBC5   CLI   BCTCTRY,X'FF'       FILTER ON COUNTRY                            
         BE    CHKBC6                                                           
         CLC   BCTCTRY,TCTRY                                                    
         BNE   CHKBCV                                                           
*                                                                               
CHKBC6   CLI   BCTOVSYS,0          FILTER ON GENERIC SYSTEM                     
         BE    CHKBC7                                                           
         CLC   BCTOVSYS,TOVSYS                                                  
         BNE   CHKBCV                                                           
         CLI   BCTPRG,0                                                         
         BE    CHKBC7                                                           
         CLC   BCTPRG,TPRG                                                      
         BNE   CHKBCV                                                           
*                                                                               
CHKBC7   CLI   BCTSYS,0            FILTER ON ACTUAL SYSTEM                      
         BE    CHKBC8                                                           
         CLC   BCTSYS,TSYS                                                      
         BNE   CHKBCV                                                           
         CLI   BCTPRG,0                                                         
         BE    CHKBC8                                                           
         CLC   BCTPRG,TPRG                                                      
         BNE   CHKBCV                                                           
*                                                                               
CHKBC8   CLI   BCTLUID,0           FILTER ON LUID                               
         BE    CHKBCA                                                           
         MVC   DUB,TSYM            SET TERMINAL LUID IN DUB                     
         TM    BCTFLAG,BCTFLST                                                  
         BO    CHKBC9                                                           
         LA    R0,8                PROCESS 8 CHRS                               
         LA    RE,BCTLUID                                                       
         LA    RF,DUB                                                           
CHKBC8A  CLI   0(RE),C'*'          ALLOW WILD CARD CHRS                         
         BE    CHKBC8B                                                          
         CLI   0(RE),C' '                                                       
         BNH   CHKBC8B                                                          
         CLC   0(1,RE),0(RF)                                                    
         BNE   CHKBCV                                                           
CHKBC8B  LA    RE,1(RE)                                                         
         LA    RF,1(RF)                                                         
         BCT   R0,CHKBC8A                                                       
         B     CHKBCA                                                           
*                                                                               
CHKBC9   EQU   *                   FILTER ON LUID LIST                          
*                                                                               
CHKBCA   EQU   *                   END OF FILTERS                               
*                                                                               
CHKBCU   L     RF,BCADDR           TURN ON BIT IN BC PENDING LIST               
         OC    0(1,RF),BCMASK                                                   
*&&UK*&& B     CHKBCV              *NOP* BROADCAST                              
         OI    TSTAT2,TSTATBCP     SET BRDCST PENDING IN UTL                    
*                                                                               
CHKBCV   BXLE  R7,R8,CHKBC3        BACK FOR NEXT BCTAB ENTRY                    
*                                                                               
CHKBCW   TM    TSTAT2,TSTATBCP     WAS A NEW BRDCST MSG PENDING FOUND           
         BZ    CHKBCX              NO                                           
         MVC   CHBCDATE(8),MVSDATE SET BRDCST DATE/TIME                         
         MVC   CHBCCNUM,SAVECNUM   SET BRDCAST HIGH MESSAGE NUMBER              
*                                                                               
CHKBCX   EQU   *                                                                
         EJECT                                                                  
***********************************************************************         
* CHECKPOINT GLOBAL DATA                                              *         
***********************************************************************         
CHKGL    L     R7,SSBTKADR         CHECKPOINT GLOBALS                           
         USING TCBD,R7                                                          
         L     R9,TCBTWA                                                        
         AH    R9,GLODSP           R9=A(DISK TWA GLOBAL AREA)                   
         USING GLOBD,R9                                                         
         L     R8,TCBWRKA                                                       
         LA    R0,8                                                             
CHKGL0   CLC   0(4,R8),=C'MNTR'    FIND MONITORS REGISTERS                      
         BE    CHKGL0A                                                          
         L     R8,8(R8)                                                         
         BCT   R0,CHKGL0                                                        
         XC    SAVEFLGS,SAVEFLGS                                                
         B     CHKGL2                                                           
CHKGL0A  MVC   SAVEFLGS,108(R8)    SAVE MONITOR'S FLAGS                         
         L     R8,104(R8)          R8=A(TASK GLOBAL WORK AREA)                  
*                                                                               
         TM    GLOBSWS-GLOBD(R8),GLOBCLR                                        
         BO    CHKGL2              GLOBALS WANT CLEARING                        
         TM    GLOBSWS-GLOBD(R8),GLOBUPDT                                       
         BO    CHKGL3              GLOBALS HAVE BEEN UPDATED                    
*                                                                               
CHKGL1   CLC   GLOBDATE(8),SSBSDATE CHECK DATE/TIME STAMP IN TWA#0              
         BNL   CHKGLX                                                           
*                                                                               
CHKGL2   LR    R0,R9               INITIALISE GLOBAL INFO FOR THIS TRM          
         LA    R1,CHKPTGLL                                                      
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
         MVC   GLOBDATE(8),MVSDATE                                              
         B     CHKGLX                                                           
*                                                                               
CHKGL3   LR    R0,R9               MOVE UPDATED GLOBALS TO TWA                  
         LA    R1,CHKPTGLL                                                      
         LR    RE,R8                                                            
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
         MVC   GLOBDATE(8),MVSDATE SET DATE/TIME OF GLOBALS                     
         MVI   GLOBSWS,0           RESET ACTIVITY SWITCHES                      
*                                                                               
CHKGLX   EQU   *                                                                
*                                                                               
CHKMISC  TM    SAVEFLGS,X'C0'      TEST IF CONNECT AND PROVER MSG               
         BNO   CHKMISCX                                                         
         L     RF,TCBTWA                                                        
         MVC   CHPROVFL,SAVEFLGS   SAVE PROVER FLAGS SET BY MONITOR             
         NI    CHPROVFL,X'E0'                                                   
         MVC   CHPROVTX,72(RF)     SAVE PROVER MESSAGE BUILT BY CONNECT         
CHKMISCX EQU   *                                                                
         EJECT                                                                  
CHKWRT   L     R7,SSBTKADR         WRITE TWA0 WITH CHECKPOINT DATA              
         USING TCBD,R7                                                          
         MVC   DMCB+20(2),=C'L='   SPECIAL PARM FOR TWA0 READ/WRITES            
         MVC   DMCB+22(2),RECLEN                                                
         NI    TSTAT5,255-TST5TCP  TURN OFF VTAM TRM CHKPNT PENDING             
         SR    R2,R2                                                            
         ICM   R2,7,TCBIOCNT       SAVE AND CLEAR TASK I/O COUNT                
         XC    TCBIOCNT,TCBIOCNT                                                
         GOTO1 VDATAMGR,DMCB,=C'DMWRT',=C'TEMPSTR',0,TCBTWA                     
         LA    R2,1(R2)                                                         
         STCM  R2,7,TCBIOCNT       RESTORE NEW TASK I/O COUNT                   
         CLI   DMCB+8,0                                                         
CHKWRTX  EQU   *                                                                
*                                                                               
CHKEXIT  XIT1                                                                   
         EJECT                                                                  
BITMASK  DC    X'8040201008040201'                                              
ARZERO   DC    16F'0'                                                           
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
GLOBD    DSECT                                                                  
GLOBDATE DS    PL4                 DATE OF LAST SAVE OF GLOBALS                 
GLOBTIME DS    CL4                 TIME OF LAST SAVE OF GLOBALS                 
GLOBXFR  DS    XL2                 DISP TO XFR CONTROL ELEMENT                  
         DS    XL1                 N/D                                          
GLOBSWS  DS    XL1                 GLOBBER SWITCHES                             
GLOBREAD EQU   X'80'               SET IF GLOBALS READ FROM DISK                
GLOBUPDT EQU   X'40'               SET IF GLOBALS UPDATED                       
GLOBCLR  EQU   X'20'               SET TO CLEAR GLOBALS                         
*                                                                               
GLOBELS  DS    XL500               GLOBAL DATA AREA                             
*                                                                               
GLOBELEN EQU   *-GLOBELS           LENGTH OF GLOBAL DATA AREA                   
                                                                                
CHKWORK  DSECT                                                                  
MVSDATE  DS    F                   DATE (JULIAN)                                
MVSTIME  DS    F                   TIME (1/100 SEC)                             
*                                                                               
DUB      DS    D                                                                
DMCB     DS    6F                                                               
SAVEREG  DS    F                                                                
FULL     DS    F                                                                
BCADDR   DS    A                                                                
BCBYTE   DS    H                                                                
BCBIT    DS    H                                                                
BCMASK   DS    X                                                                
TIMEB    DS    XL3                                                              
SAVEFLGS DS    XL4                 MONITOR'S INFO FLAGS                         
SAVECNUM DS    H                                                                
RECLEN   DS    H                   TEMPSTR RECORD LENGTH                        
CHKDSP   DS    H                   TEMPSTR DISPLACEMENT TO CHKPNT AREA          
GLODSP   DS    H                   TEMPSTR DISPLACEMENT TO GLOBAL AREA          
CHKWORKX EQU   *                                                                
         EJECT                                                                  
*FACHKPT                                                                        
       ++INCLUDE FACHKPT                                                        
                                                                                
*FABCTAB                                                                        
       ++INCLUDE FABCTAB                                                        
                                                                                
*FASSB                                                                          
       ++INCLUDE FASSB                                                          
                                                                                
*FASELIST                                                                       
       ++INCLUDE FASELIST                                                       
                                                                                
*FASYSFAC                                                                       
       ++INCLUDE FASYSFAC                                                       
                                                                                
*FATCB                                                                          
       ++INCLUDE FATCB                                                          
                                                                                
*FAUTL                                                                          
       ++INCLUDE FAUTL                                                          
                                                                                
*FATABSTMS                                                                      
       ++INCLUDE FATABSTMS                                                      
                                                                                
*FATABSD                                                                        
       ++INCLUDE FATABSD                                                        
                                                                                
*DMDSHDR                                                                        
       ++INCLUDE DMDSHDR                                                        
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005FACHKOUT  01/28/19'                                      
         END                                                                    
