*          DATA SET DDACTVAL   AT LEVEL 004 AS OF 05/01/02                      
*CATALP ACTVAL                                                                  
         SPACE 2                                                                
*PARAMETER LIST                                                                 
*                                                                               
*P1      XL1   LENGTH OF INPUT STRING                                           
*        AL3   A(INPUT STRING)                                                  
*P2      XL1   LANGUAGE CODE                                                    
*        AL3   A(OUTPUT CONTROL BLOCK)                                          
         SPACE 1                                                                
*OUTPUT CONTROL BLOCK                                                           
*                                                                               
*VALOUT  OUTPUT AREA                                                            
*ACTNUM  ACTION NUMBER 00 - 08  0=NOMATCH ELSE TABLE ENT                        
*ACTASS  FF= FULL MATCH, OR CHARS MATCHED                                       
*INPFRM  CHARS INPUT                                                            
*OUTFRM  FULL ACTION NAME                                                       
         SPACE 1                                                                
         TITLE 'DDACTVAL -VALIDATE STANDARD ACTION TYPES'                       
         PRINT NOGEN                                                            
ACTVAL   CSECT                                                                  
         NMOD1 VAREND-VARSTART,XXACTVXX                                         
         USING VARS,RC                                                          
         L     RA,4(R1)            RA=A(OUTPUT CONTROL BLOCK)                   
         LA    RA,0(RA)                                                         
         USING ACTVALD,RA                                                       
*                                                                               
         MVC   LANG,4(R1)          EXTRACT LANGUAGE CODE                        
         CLI   LANG,0                                                           
         BE    *+12                                                             
         CLI   LANG,7                                                           
         BNH   *+10                                                             
         MVC   LANG,DEFLANG                                                     
*                                                                               
         XC    LANGMOD,LANGMOD     CALC MOD FOR TABLE OFFSETS                   
         MVI   VALOUT,C' '         INITIALISE IT                                
         MVC   VALOUT+1(L'VALOUT-1),VALOUT                                      
         MVC   ILEN,0(R1)          GET L'INPUT                                  
         CLI   LANG,2              IF ENGLISH, MOD=0                            
         BNH   VALA1                                                            
         SR    R2,R2                                                            
         IC    R2,LANG                                                          
         BCTR  R2,0                ELSE MOD=(LANG-2)                            
         BCTR  R2,0                                                             
         STC   R2,LANGMOD+L'LANGMOD-1                                           
*                                                                               
*                                                                               
VALA1    EQU   *                                                                
         SR    R2,R2               COUNT FOR SCAN LOOP                          
         SR    R9,R9                                                            
         IC    R2,ILEN                                                          
         LTR   R2,R2                                                            
         BZ    VALA2               IF ZERO, ASSUME SCREEN INPUT                 
         L     R9,0(R1)            POINT TO 1ST POS OF INPUT                    
         B     VALA4                                                            
VALA2    EQU   *                                                                
         L     R2,5(R1)            GET L'INPUT FROM SCREEN HDR                  
         LTR   R2,R2                                                            
         BZ    VALERR              NO INPUT                                     
         L     R9,8(R1)            POINT TO INPUT START                         
VALA4    EQU   *                                                                
         CLI   0(R9),C' '                                                       
         BNE   VALA10              NOT A SPACE, SO VALIDATE                     
         LA    R9,1(R9)            ELSE LOOK AT NEXT                            
         BCT   R2,VALA4                                                         
         B     VALERR              ALL SPACES                                   
VALA10   EQU   *                                                                
         LA    R3,ACTTAB           POINT TO ACTION TABLE.                       
         BAS   R8,FINDER           SEARCH TABLE FOR MATCH                       
         LTR   R3,R3               CHECK RESULT                                 
         BNZ   VALARET             INPUT MATCHED                                
         CLI   LANGMOD+L'LANGMOD-1,X'00'                                        
*                                  IF TERM IS ENGLISH                           
         BNH   VALERR              RETURN WITH ERROR                            
         XC    LANGMOD,LANGMOD     ELSE, TRY ENGLISH                            
         B     VALA10                                                           
*                                                                               
*RETURN ROUTINES                                                                
*                                                                               
VALERR   EQU   *                                                                
*                                  OUTPUT ALL '0' IF INVALID INPUT              
VALARET  EQU   *                                                                
         XIT1                                                                   
         SPACE 2                                                                
         EJECT                                                                  
*                                                                               
* FIND VARIABLE DATA STRING IN TABLE                                            
*                                                                               
FINDER   EQU   *                                                                
         SR    R4,R4                                                            
         XC    ENTLEN(2),ENTLEN                                                 
         IC    R4,0(R3)            GET NO OF TABLE ENTRIES                      
         LR    R5,R4               AND AGAIN                                    
         MVC   ENTLEN+1(1),1(R3)   GET ENTRY LENGTH                             
         MH    R5,ENTLEN           GET STARTING POINT...                        
         MH    R5,LANGMOD                                                       
         LA    R3,2(R3,R5)         ...FOR TABLE SEARCH                          
FIND1    EQU   *                                                                
         SR    R5,R5                                                            
         LR    R6,R5                                                            
         IC    R5,0(R3)            MAXIMUM SEARCH.                              
         BCTR  R5,R0                                                            
         STH   R5,MAXACT           SAVE MAX LENGTH                              
         IC    R6,1(R3)            MINIMUM SEARCH.                              
         BCTR  R6,R0                                                            
FIND2    EQU   *                                                                
         EX    R5,TABCOMP                                                       
         BE    FIND3               FOUND IT.                                    
         BCTR  R5,R0                                                            
         CR    R5,R6               MINIMUM FOUND?                               
         BNL   FIND2               NO - LOOK AGAIN.                             
         AH    R3,ENTLEN           LOOK AT NEXT ENTRY IN TABLE.                 
         BCT   R4,FIND1                                                         
         XC    ACTNUM,ACTNUM       RETURN ZERO.                                 
         SR    R3,R3                                                            
         BR    R8                                                               
FIND3    EQU   *                                                                
         MVC   ACTNUM(1),2(R3)     STORE ACTION NO.                             
         MVI   ACTASS,X'FF'        ASSUME FULL MATCH                            
         CH    R5,MAXACT                                                        
         BE    FIND4               IF FULL MATCH, ELSE...                       
         LA    R5,1(R5)                                                         
         STC   R5,ACTASS           ...STORE NO. OF MATCHING CHARS               
FIND4    EQU   *                                                                
         BCTR  R2,0                                                             
         EX    R2,INPMOVE          CHARS INPUT                                  
         MVC   OUTFRM(9),3(R3)     FULL ACTION NAME                             
         BR    R8                                                               
         SPACE                                                                  
         EJECT                                                                  
*                                                                               
* GET DEFAULT LANGUAGE CODE                                                     
*                                                                               
DEFLANG  DS    0C                                                               
*&&UK*&& DC    X'01'                                                            
*&&US*&& DC    X'02'                                                            
*                                                                               
*                                                                               
* EXECUTED INSTRUCTIONS                                                         
*                                                                               
TABCOMP  CLC   0(0,R9),3(R3)                                                    
INPMOVE  MVC   INPFRM(0),0(R9)                                                  
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*TABLE OF VALID ACTION NAMES                                                    
*                                                                               
*THE FORMAT OF THE FOLLOWING TABLE IS:                                          
*        AL1   NUMBER OF ENTRIES IN TABLE                                       
*        XL1   LENGTH OF TABLE ENTRY                                            
*AND FOR EACH ENTRY:                                                            
*        XL1   MAXIMUM LENGTH FOR DATA                                          
*        XL1   MINIMUM LENGTH FOR DATA                                          
*        XL1   ACTION VALUE                                                     
*        CL9   CHARACTER DATA                                                   
         SPACE 1                                                                
ACTTAB   DC    AL1(8),X'0C'                                                     
         DC    X'03',X'02',X'01',CL9'ADD'                                       
         DC    X'07',X'02',X'02',CL9'DISPLAY'                                   
         DC    X'06',X'02',X'03',CL9'CHANGE'                                    
         DC    X'06',X'02',X'04',CL9'DELETE'                                    
         DC    X'07',X'03',X'05',CL9'RESTORE'                                   
         DC    X'04',X'02',X'06',CL9'LIST'                                      
         DC    X'06',X'02',X'07',CL9'SELECT'                                    
         DC    X'06',X'02',X'08',CL9'REPORT'                                    
         SPACE 1                                                                
GERACT   DC    X'03',X'02',X'01',CL9'NEU'                                       
         DC    X'06',X'02',X'02',CL9'ZEIGEN'                                    
         DC    X'04',X'02',X'03',CL9'AEND'                                      
         DC    X'06',X'02',X'04',CL9'LOESCH'                                    
         DC    X'06',X'02',X'05',CL9'??????'                                    
         DC    X'04',X'02',X'06',CL9'LIST'                                      
         DC    X'06',X'02',X'07',CL9'ANZEIG'                                    
         DC    X'05',X'02',X'08',CL9'DRUCK'                                     
         SPACE 1                                                                
FREACT   DC    X'07',X'02',X'01',CL9'AJOUTER'                                   
         DC    X'08',X'02',X'02',CL9'AFFICHER'                                  
         DC    X'08',X'02',X'03',CL9'MODIFIER'                                  
         DC    X'09',X'02',X'04',CL9'SUPPRIMER'                                 
         DC    X'09',X'05',X'05',CL9'RESTAURER'                                 
         DC    X'06',X'02',X'06',CL9'LISTER'                                    
         DC    X'07',X'03',X'07',CL9'CHOISIR'                                   
         DC    X'09',X'02',X'08',CL9'RAPPORTER'                                 
         EJECT                                                                  
* GENERAL DSECT                                                                 
*                                                                               
VARS     DSECT                                                                  
VARSTART DS    0H                                                               
DUB      DS    D                   DOUBLEWORD WORK AREA                         
MAXACT   DS    H                                                                
LANGMOD  DS    H                   OFFSET FOR LANG TABLES                       
ENTLEN   DS    H                   ENTRY LENGTH FOR LANG TABLES                 
ILEN     DS    CL1                 INPUT STRING LENGTH                          
LANG     DS    CL1                 TERMINAL LANG CODE                           
DATEWK   DS    CL15                WORK AREA FOR US DATE OUTPUT                 
TMPCHAR  DS    CL1                 WORK BYTE FOR DAY/MON SWAP                   
VAREND   DS    0H                                                               
         SPACE 2                                                                
* OUTPUT BLOCK DSECT                                                            
*                                                                               
ACTVALD  DSECT                                                                  
VALOUT   DS    0CL22               OUTPUT AREA                                  
ACTNUM   DS    XL1                 ACTION NUMBER 00 - 08 (SEE TOP)              
ACTASS   DS    XL1                 'FF'= FULL MATCH, OR CHARS MATCHED           
INPFRM   DS    CL10                CHARS INPUT                                  
OUTFRM   DS    CL10                FULL ACTION NAME                             
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004DDACTVAL  05/01/02'                                      
         END                                                                    
