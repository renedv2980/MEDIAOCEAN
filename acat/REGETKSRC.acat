*          DATA SET REGETKSRC  AT LEVEL 085 AS OF 12/21/10                      
*CATALP GETKSRC                                                                 
*        TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
*********************************************************************           
* HISTORY OF CHANGES:                                               *           
* JUN09/04 (BU ) --- ADD 'REGULAR LPM' CODE                         *           
* JUN28/04 (BU ) --- ADD 'HISPANIC LPM CODE                         *           
* SEP21/06 (BU ) --- ADDITIONAL CHANGES TO BOOK TYPES               *           
* JAN08/07 (BU ) --- BOOK TYPE 'J'                                  *           
* MAY01/08 (SKU) --- SUPPORT LIVE+3 BY DEFINING NON-ALPHANUMERIC    *           
*                    BOOKTYPES                                      *           
* FEB00/08 (BOB) --- SUPPORT NEW RINVKEY STRUCTURE                  *           
*                                                                   *           
*                                                                   *           
*                                                                   *           
*********************************************************************           
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF REINVKSRC FIELD'                
*********************************************************************           
*                                                                   *           
* INPUT  PARM1:  0 - C'Q' = IBLK MUST PASS: RTGSVC, QLFYR & BKTYPE  *           
*                                           USER/EXTERNAL VALUES    *           
*                                                                   *           
*                    C'B' = IBLK MUST PASS: BKVAL BITS & BKTYPE     *           
*                                                                   *           
*                    C'K' = IBLK MUST PASS: RTGSVC, QLFYR & BKTYPE  *           
*                                           INTERNAL VALUES         *           
*                                                                   *           
*              1-3 - A(IBLOCK)                                      *           
*                                                                   *           
*        PARM2:    - A(OUTPUT BLOCK)                                *           
*                                                                   *           
*        PARM3:    - A(COMFACS)                                     *           
*                                                                   *           
* ON EXIT:                                                          *           
*                                                                   *           
* THE OUTPUT BLOCK WILL BE FILLED WITH THE INTERNAL VALUES          *           
*        DMCB+4(1) = ERROR CODE  (IF AN ERROR OCCURED, OBLK='FFFFFF')           
*                                                                   *           
*                    00= NO ERRORS                                  *           
*                    01= NO MATCH ON INPUT PARMS                    *           
*                    02= UNDEFINED BKTYPE                           *           
*                                                                   *           
*FORMAT OF INPUT BLOCK:                                             *           
*                                                                   *           
*IPARMS   DS    0CL5                INPUT                           *           
*IRSVC    DS    CL1       0         RTG SVC: N=NSI, S=SRC, M=MFX    *           
*IQLF     DS    CL1       1         TRACK QUALIFIER/BK PREFIX       *           
*IBITS    DS    XL1       2         BOOKVAL BITS                    *           
*IBKTYP   DS    CL1       3         BOOKTYPE - 2CH                  *           
*                                                                   *           
*FORMAT OF OUTPUT BLOCK: - INTERNAL VALUES                          *           
*                                                                   *           
*OPARMS   DS    0CL5                INPUT                           *           
*ORSVC    DS    CL1       0         RTG SVC:                        *           
*OQLF     DS    CL1       1         TRACK QUALIFIER/BK PREFIX       *           
*OBITS    DS    XL1       2         BOOKVAL BITS                    *           
*OBKTYP   DS    XL1       3         BOOKTYPE - DEMO SYSTEM BKTYPE   *           
*         DS    XL1       4         SPARE                           *           
*                                                                   *           
*UNDER NEW SCHEME OQLF AND OBITS ARE THE SAME                       *           
*                                                                   *           
*********************************************************************           
                                                                                
                                                                                
*                                                                               
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
*********************************************************************           
*                                                                   *           
*        INITIALIZATION                                             *           
*                                                                   *           
*********************************************************************           
         SPACE 2                                                                
GETKSRC  CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,GETKSRC,RR=RE,CLEAR=YES                              
*                                                                               
         USING WORKD,RC            RC=A(W/S)                                    
*                                                                               
         ST    RE,RELO                                                          
         ST    R1,APARMS           A(PARAM) LIST                                
*                                                                               
         SR    R9,R9                                                            
         ICM   R9,7,1(R1)          P1 = A(INPUT BLOCK) ->> R9                   
         BNZ   *+6                                                              
         DC    H'0'                MISSING A(INPUT BLK)  (PARM1)                
*                                                                               
         OC    4(4,R1),4(R1)       P2 = A(OUTPUT BLOCK)                         
         BNZ   *+6                                                              
         DC    H'0'                MISSING A(OUTPUT BLK)  (PARM2)               
*                                                                               
         MVC   MODE,0(R1)          SAVE CALLING MODE                            
*                                                                               
         L     RF,8(R1)            A(COMFACS)                                   
         MVC   VDEMTABS,CDEMTABS-COMFACSD(RF)   V(DEMTABS)                      
*                                                                               
*        ANALYZE MODE                                                           
*                                                                               
         CLI   MODE,MQLFY          VALID MODES - RTGSVC, QLFYR & BKTYPE         
         BE    GETQLF                                                           
*                                                                               
         CLI   MODE,MBKVL          BKVAL BITS & BKTYPE                          
         BE    GETBKV                                                           
*                                                                               
         CLI   MODE,MKSRC          RTGSVC, QLFYR & BKTYPE INTERNAL              
         BE    GETSRC                                                           
*                                                                               
GETKDIE  DC    H'0'                INV PARM1,BYTE 0 (Q/B/K ONLY)                
*                                                                               
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
*********************************************************************           
*                                                                   *           
*        EXTERNAL VALUES OF RTGSVC/QUALIFR/BKTYPE GIVEN             *           
*                                                                   *           
*********************************************************************           
         SPACE 2                                                                
GETSRC   DS    0H                                                               
*                                                                               
         MVC   OPARMS,0(R9)        SAVE INPUT PARAMETER                         
*                                                                               
         XC    IPARMS,IPARMS       INIT OUTPUT                                  
*                                                                               
         BRAS  RE,RTGTRN           TRANSLATE INTERNAL RATING SOURCE             
*                                                                               
         BRAS  RE,QLFTRN           TRANSLATE INTERNAL BOOKVAL BITS              
*                                                                               
         BRAS  RE,BKTTRN           TRANSLATE INTERNAL BOOKTYPE                  
*                                                                               
         MVC   IBITS,IQLF          BOOKVAL BITS SAME AS QUALIFIER               
*                                                                               
         L     RF,APARMS                                                        
         L     RF,4(RF)            POINT TO OUTPUT AREA                         
         MVC   0(L'IPARMS,RF),IPARMS RETURN RESULTS                             
*                                                                               
         B     GETKSRCX                                                         
*                                                                               
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
*********************************************************************           
*                                                                   *           
*        INTERNAL VALUES OF RTGSVC/QUALIFR/BKTYPE GIVEN             *           
*                                                                   *           
*********************************************************************           
         SPACE 2                                                                
GETQLF   DS    0H                                                               
*                                                                               
         MVC   IPARMS,0(R9)        SAVE INPUT PARAMETER                         
*                                                                               
         XC    OPARMS,OPARMS       INIT OUTPUT                                  
*                                                                               
         BRAS  RE,RTGVAL           VALIDATE EXTERNAL RATING SOURCE              
*                                                                               
         BRAS  RE,QLFVAL           VALIDATE EXTERNAL QUALIFIER                  
*                                                                               
         BRAS  RE,BKTVAL           VALIDATE EXTERNAL BOOKTYPE                   
*                                                                               
         MVC   OBITS,OQLF          BOOKVAL BITS SAME AS QUALIFIER               
*                                                                               
         L     RF,APARMS                                                        
         L     RF,4(RF)            POINT TO OUTPUT AREA                         
         MVC   0(L'OPARMS,RF),OPARMS RETURN RESULTS                             
*                                                                               
         B     GETKSRCX                                                         
*                                                                               
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
*********************************************************************           
*                                                                   *           
*        INTERNAL BOOKVAL AND BOOK TYPE GIVEN                       *           
*                                                                   *           
*********************************************************************           
         SPACE 2                                                                
GETBKV   DS    0H                                                               
*                                                                               
         MVC   OPARMS,0(R9)        SAVE INPUT PARAMETER                         
*                                                                               
         XC    IPARMS,IPARMS       INIT OUTPUT                                  
*                                                                               
         BRAS  RE,BITTRN           TRANSLATE INTERNAL QUALIFIER                 
*                                                                               
         BRAS  RE,BKTTRN           TRANSLATE INTERNAL BOOKTYPE                  
*                                                                               
         MVC   IQLF,IBITS          BOOKVAL BITS SAME AS QUALIFIER               
*                                                                               
         L     RF,APARMS                                                        
         L     RF,4(RF)            POINT TO OUTPUT AREA                         
         MVC   0(L'IPARMS,RF),IPARMS RETURN RESULTS                             
*                                                                               
         B     GETKSRCX                                                         
*                                                                               
         TITLE 'REGETKSRC - TRANSLATE IN/OUT OF RTGSVC/QLF/BKTYPE'              
*********************************************************************           
*                                                                   *           
*        EXIT ROUTINES                                              *           
*                                                                   *           
*********************************************************************           
         SPACE 2                                                                
GETKSRCX DS    0H                                                               
*                                                                               
         CLI   ERRCODE,0           NO ERRORS                                    
         BE    *+10                                                             
         MVC   IPARMS,=X'FFFFFFFFFF'                                            
*                                                                               
         L     R1,APARMS           INPUT PARAM LIST                             
         MVC   4(1,R1),ERRCODE                                                  
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGC/QLF/BKTYPE - RTGVAL'           
***********************************************************************         
*                                                                     *         
*        VALIDATE EXTERNAL RATING SERVICE CODE                        *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
RTGVAL   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LAY   R1,RTGTBL           POINT TO RATING SERVICE TABLE                
         USING RTGTBLD,R1          ESTABLISH ENTRY IN TBLE                      
*                                                                               
RTGVALLP DS    0H                                                               
*                                                                               
         CLI   RTBRTGEX,X'FF'      DONE AT END OF TABLE                         
         BE    RTGVALNF                                                         
*                                                                               
         CLC   IRSVC,RTBRTGEX      MATCH OF EXTERNAL RTG SRV CODE               
         BE    RTGVALFD                                                         
*                                                                               
RTGVALCN DS    0H                                                               
*                                                                               
         LA    R1,RTGTBLLQ(R1)     BUMP TO NEXT TABLE ENTRY                     
         B     RTGVALLP                                                         
*                                                                               
RTGVALFD DS    0H                  RATING SERVICE IN TABLE                      
*                                                                               
         MVC   ORSVC,RTBRTGIN      RETURN INTERNAL RTG SVC CODE                 
*                                                                               
         B     RTGVALX                                                          
*                                                                               
RTGVALNF DS    0H                  RATING SERVICE NOT IN TABLE                  
*                                                                               
         MVI   ERRCODE,ERNTFND     CODE NOT FOUND ERROR                         
*                                                                               
         B     RTGVALX                                                          
*                                                                               
RTGVALX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGC/QLF/BKTYPE - RTGTRN'           
***********************************************************************         
*                                                                     *         
*        TRANSLATE EXTERNAL RATING SERVICE CODE                       *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
RTGTRN   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LAY   R1,RTGTBL           POINT TO RATING SERVICE TABLE                
         USING RTGTBLD,R1          ESTABLISH ENTRY IN TBLE                      
*                                                                               
RTGTRNLP DS    0H                                                               
*                                                                               
         CLI   RTBRTGEX,X'FF'      DONE AT END OF TABLE                         
         BE    RTGTRNNF                                                         
*                                                                               
         CLC   ORSVC,RTBRTGIN      MATCH OF INTERNAL RTG SRV CODE               
         BE    RTGTRNFD                                                         
*                                                                               
RTGTRNCN DS    0H                                                               
*                                                                               
         LA    R1,RTGTBLLQ(R1)     BUMP TO NEXT TABLE ENTRY                     
         B     RTGTRNLP                                                         
*                                                                               
RTGTRNFD DS    0H                  RATING SERVICE IN TABLE                      
*                                                                               
         MVC   IRSVC,RTBRTGEX      RETURN EXTERNAL RTG SVC CODE                 
*                                                                               
         B     RTGTRNX                                                          
*                                                                               
RTGTRNNF DS    0H                  RATING SERVICE NOT IN TABLE                  
*                                                                               
         MVI   ERRCODE,ERNTFND     CODE NOT FOUND ERROR                         
*                                                                               
         B     RTGTRNX                                                          
*                                                                               
RTGTRNX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGC/QLF/BKTYPE - QLFVAL'           
***********************************************************************         
*                                                                     *         
*        VALIDATE EXTERNAL QUALIFIER CODE                             *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
QLFVAL   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         LAY   R1,QLFTBL           POINT TO QUALIFIER TABLE                     
         USING QLFTBLD,R1          ESTABLISH ENTRY IN TABLE                     
*                                                                               
QLFVALLP DS    0H                                                               
*                                                                               
         CLI   QTBQLFEX,X'FF'      DONE AT END OF TABLE                         
         BE    QLFVALNF                                                         
*                                                                               
         CLC   IQLF,QTBQLFEX       MATCH OF EXTERNAL QLF CODE                   
         BE    QLFVALFD                                                         
*                                                                               
QLFVALCN DS    0H                                                               
*                                                                               
         LA    R1,QLFTBLLQ(R1)     BUMP TO NEXT TABLE ENTRY                     
         B     QLFVALLP                                                         
*                                                                               
QLFVALFD DS    0H                  QUALIFIER IN TABLE                           
*                                                                               
         MVC   OQLF,QTBQLFIN       RETURN INTERNAL QLF CODE                     
*                                                                               
         B     QLFVALX                                                          
*                                                                               
QLFVALNF DS    0H                  QUALIFIER NOT IN TABLE                       
*                                                                               
         MVI   ERRCODE,ERNTFND     CODE NOT FOUND ERROR                         
*                                                                               
         B     QLFVALX                                                          
*                                                                               
QLFVALX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGC/QLF/BKTYPE - QLFTRN'           
***********************************************************************         
*                                                                     *         
*        TRANSLATE INTERNAL QUALIFER CODE                             *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
QLFTRN   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   BYTE,OQLF           COPY BOOKVAL BITS                            
*                                                                               
         TM    BYTE,X'41'          CHECK FOR SRC RATING SOURCE                  
         BNO   *+16                                                             
         MVI   IRSVC,C'S'          SET SRC RATING ID                            
         XI    BYTE,X'41'          TURN OFF RSVC BITS                           
         B     QLFTRN1                                                          
*                                                                               
         TM    BYTE,X'40'          CHECK FOR SRC RATING SOURCE                  
         BNO   *+16                                                             
         MVI   IRSVC,C'N'          SET SRC RATING ID                            
         XI    BYTE,X'40'          TURN OFF RSVC BITS                           
         B     QLFTRN1                                                          
*                                                                               
QLFTRN1  DS    0H                                                               
*                                                                               
         LAY   R1,QLFTBL           POINT TO QUALIFIER TABLE                     
         USING QLFTBLD,R1          ESTABLISH ENTRY IN TABLE                     
*                                                                               
QLFTRNLP DS    0H                                                               
*                                                                               
         CLI   QTBQLFEX,X'FF'      DONE AT END OF TABLE                         
         BE    QLFTRNNF                                                         
*                                                                               
         CLC   BYTE,QTBQLFIN       MATCH OF INTERNAL QLF CODE                   
         BE    QLFTRNFD                                                         
*                                                                               
QLFTRNCN DS    0H                                                               
*                                                                               
         LA    R1,QLFTBLLQ(R1)     BUMP TO NEXT TABLE ENTRY                     
         B     QLFTRNLP                                                         
*                                                                               
QLFTRNFD DS    0H                  QUALIFIER IN TABLE                           
*                                                                               
         MVC   IQLF,QTBQLFEX       RETURN EXTERNAL QLF CODE                     
*                                                                               
         B     QLFTRNX                                                          
*                                                                               
QLFTRNNF DS    0H                  QUALIFIER NOT IN TABLE                       
*                                                                               
         MVI   ERRCODE,ERNTFND     CODE NOT FOUND ERROR                         
*                                                                               
         B     QLFTRNX                                                          
*                                                                               
QLFTRNX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGC/QLF/BKTYPE - BITTRN'           
***********************************************************************         
*                                                                     *         
*        TRANSLATE INTERNAL BOOKVAL BITS                              *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
BITTRN   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         MVC   BYTE,OBITS          COPY BOOKVAL BITS                            
*                                                                               
         TM    BYTE,X'41'          CHECK FOR SRC RATING SOURCE                  
         BNO   *+16                                                             
         MVI   IRSVC,C'S'          SET SRC RATING ID                            
         XI    BYTE,X'41'          TURN OFF RSVC BITS                           
         B     BITTRN1                                                          
*                                                                               
         TM    BYTE,X'40'          CHECK FOR SRC RATING SOURCE                  
         BNO   *+16                                                             
         MVI   IRSVC,C'N'          SET SRC RATING ID                            
         XI    BYTE,X'40'          TURN OFF RSVC BITS                           
         B     BITTRN1                                                          
*                                                                               
BITTRN1  DS    0H                                                               
*                                                                               
         LAY   R1,QLFTBL           POINT TO QUALIFIER TABLE                     
         USING QLFTBLD,R1          ESTABLISH ENTRY IN TABLE                     
*                                                                               
BITTRNLP DS    0H                                                               
*                                                                               
         CLI   QTBQLFEX,X'FF'      DONE AT END OF TABLE                         
         BE    BITTRNNF                                                         
*                                                                               
         CLC   BYTE,QTBQLFIN       MATCH OF INTERNAL QLF CODE                   
         BE    BITTRNFD                                                         
*                                                                               
BITTRNCN DS    0H                                                               
*                                                                               
         LA    R1,QLFTBLLQ(R1)     BUMP TO NEXT TABLE ENTRY                     
         B     BITTRNLP                                                         
*                                                                               
BITTRNFD DS    0H                  QUALIFIER IN TABLE                           
*                                                                               
         MVC   IBITS,QTBQLFEX      RETURN EXTERNAL QLF CODE                     
*                                                                               
         B     BITTRNX                                                          
*                                                                               
BITTRNNF DS    0H                  QUALIFIER NOT IN TABLE                       
*                                                                               
         MVI   ERRCODE,ERNTFND     CODE NOT FOUND ERROR                         
*                                                                               
         B     BITTRNX                                                          
*                                                                               
BITTRNX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF BKTC/QLF/BKTYPE - BKTVAL'           
***********************************************************************         
*                                                                     *         
*        VALIDATE BOOK TYPE EXTERNAL CODE                             *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
BKTVAL   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         GOTOR VDEMTABS,DMCB,(0,SPBOOKTB),0   GET A(BOOK TYPE TABLE)            
*                                                                               
         OC    DMCB(4),DMCB        ERROR IF NOT FOUND                           
         BZ    BKTVALNF                                                         
*                                                                               
         L     R1,DMCB             POINT TO BOOK TYPE TABLE                     
         USING SPBKTYPD,R1         ESTABLISH ENTRY IN TBLE                      
*                                                                               
         L     R0,DMCB+4           GET TABLE ENTRY LENGTH                       
*                                                                               
BKTVALLP DS    0H                                                               
*                                                                               
         CLI   SPBKTYPA,X'FF'      DONE AT END OF TABLE                         
         BE    BKTVALNF                                                         
*                                                                               
         CLC   IBKTYP,SPBKTYPA     MATCH OF ENTERNAL BOOK TYPE CODE             
         BNE   BKTVALFD                                                         
*                                                                               
         TM    SPBTYIND,SPBKMON    MUST BE A MONTHLY BOOK TYPE                  
         BNO   BKTVALNF                                                         
*                                                                               
         B     BKTVALFD                                                         
*                                                                               
BKTVALCN DS    0H                                                               
*                                                                               
         AR    R1,R0               BUMP TO NEXT TABLE ENTRY                     
         B     BKTVALLP                                                         
*                                                                               
BKTVALFD DS    0H                  BOOK TYPE IN TABLE                           
*                                                                               
         MVC   OBKTYP,SPBKTYPN     RETURN INTERNAL BOOK TYPE CODE               
*                                                                               
         B     BKTVALX                                                          
*                                                                               
BKTVALNF DS    0H                  BOOK TYPE NOT IN TABLE                       
*                                                                               
         MVI   ERRCODE,ERUNDEF     BOOK TYPE UNDEFINED                          
*                                                                               
         B     BKTVALX                                                          
*                                                                               
BKTVALX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF BKTC/QLF/BKTYPE - BKTTRN'           
***********************************************************************         
*                                                                     *         
*        TRANSLATE BOOK TYPE EXTERNAL CODE                            *         
*                                                                     *         
***********************************************************************         
         DS    0H                                                               
BKTTRN   NTR1  BASE=*,LABEL=*                                                   
*                                                                               
         GOTOR VDEMTABS,DMCB,(0,SPBOOKTB),0   GET A(BOOK TYPE TABLE)            
*                                                                               
         OC    DMCB(4),DMCB        ERROR IF NOT FOUND                           
         BZ    BKTTRNNF                                                         
*                                                                               
         L     R1,DMCB             POINT TO BOOK TYPE TABLE                     
         USING SPBKTYPD,R1         ESTABLISH ENTRY IN TBLE                      
*                                                                               
         L     R0,DMCB+4           GET TABLE ENTRY LENGTH                       
*                                                                               
BKTTRNLP DS    0H                                                               
*                                                                               
         CLI   SPBKTYPA,X'FF'      DONE AT END OF TABLE                         
         BE    BKTTRNNF                                                         
*                                                                               
         CLC   OBKTYP,SPBKTYPN     MATCH ON EXTERNAL BOOK TYPE CODE             
         BNE   BKTTRNCN                                                         
*                                                                               
         TM    SPBTYIND,SPBKMON    MUST BE A MONTHLY BOOK TYPE                  
         BNO   BKTTRNCN                                                         
*                                                                               
         B     BKTTRNFD                                                         
*                                                                               
BKTTRNCN DS    0H                                                               
*                                                                               
         AR    R1,R0               BUMP TO NEXT TABLE ENTRY                     
         B     BKTTRNLP                                                         
*                                                                               
BKTTRNFD DS    0H                  BOOK TYPE IN TABLE                           
*                                                                               
         MVC   IBKTYP,SPBKTYPA     RETURN EXTERNAL BOOK TYPE CODE               
*                                                                               
         B     BKTTRNX                                                          
*                                                                               
BKTTRNNF DS    0H                  BOOK TYPE NOT IN TABLE                       
*                                                                               
         MVI   ERRCODE,ERUNDEF     BOOK TYPE UNDEFINED                          
*                                                                               
         B     BKTTRNX                                                          
*                                                                               
BKTTRNX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGSVC/QLF/BKTYPE - TABLES'         
***********************************************************************         
*                                                                     *         
*        RATING SERVICE TABLE                                         *         
*              XL1'EXTERNAL VALUE'                                    *         
*              XL1'INTERNAL VALUE'                                    *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
RTGTBL   DS    0XL2                                                             
         DC    C'N',C'N'           NIELSEN                                      
*                                                                               
         DC    X'FF'               EOT                                          
*                                                                               
***********************************************************************         
*                                                                     *         
*        QUALIFIER TABLE                                              *         
*              XL1'EXTERNAL VALUE'                                    *         
*              XL1'INTERNAL VALUE'                                    *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
QLFTBL   DS    0XL2                                                             
         DC    C' ',X'00'          PROGRAM                                      
         DC    C'P',X'04'          PROJECTED                                    
         DC    C'T',X'08'          TIME PERIOD                                  
         DC    C'S',X'02'          SPECIAL SURVEY                               
         DC    C'E',X'20'          ESTIMATED                                    
*                                                                               
         DC    X'FF'               EOT                                          
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTGSVC/QLF/BKTYPE - WORKD'          
***********************************************************************         
*                                                                     *         
*        STORAGE, LTORG, DSECTS                                       *         
*                                                                     *         
***********************************************************************         
         LTORG                                                                  
         EJECT                                                                  
WORKD    DSECT                     ** REDEMLK GLOBAL W/S **                     
DUB      DS    D                                                                
WORK     DS    XL64                                                             
DMCB     DS    6F                                                               
RELO     DS    A                                                                
BYTE     DS    X                                                                
*                                                                               
MODE     DS    XL1                                                              
MQLFY    EQU   C'Q'                RATING SRVC/ QLFYR/ BOOKTYP                  
MBKVL    EQU   C'B'                BKVAL BITS / BOOKTYPE                        
MKSRC    EQU   C'K'                KSRC INPUT                                   
*                                                                               
APARMS   DS    A                                                                
ATABLE   DS    A                                                                
VDEMTABS DS    A                   A(DEMTABS)                                   
*                                                                               
IPARMS   DS    0CL5                INPUT                                        
IRSVC    DS    CL1       0         RTG SVC: N=NSI, S=SRC, M=MFX                 
IQLF     DS    CL1       1         TRACK QUALIFIER/BK PREFIX                    
IBITS    DS    XL1       2         BOOKVAL BITS                                 
IBKTYP   DS    CL2       3         BOOKTYPE                                     
*                                                                               
OPARMS   DS    0CL5                INPUT                                        
ORSVC    DS    CL1       0         RTG SVC: N=NSI, S=SRC, M=MFX                 
OQLF     DS    CL1       1         TRACK QUALIFIER/BK PREFIX                    
OBITS    DS    XL1       2         BOOKVAL BITS                                 
OBKTYP   DS    CL1       3         BOOKTYPE                                     
         DS    XL1                 SPARE                                        
*                                                                               
ERRCODE  DS    XL1                                                              
ERNTFND  EQU   1                   NOT FOUND                                    
ERUNDEF  EQU   2                   UNDEFINED BK TYPE                            
*                                                                               
WORKX    EQU   *                                                                
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTG/QLF/BKTYPE - RTGTBLD'           
***********************************************************************         
*                                                                     *         
*        DSECT RATING SERVICE TABLE                                   *         
*              XL1'EXTERNAL VALUE'                                    *         
*              XL1'INTERNAL VALUE'                                    *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
RTGTBLD  DSECT                                                                  
RTBRTGEX DS    CL1                 RATING SERVICE EXTERNAL CODE                 
RTBRTGIN DS    XL1                 RATING SERVICE INTERNAL CODE                 
*                                                                               
RTGTBLLQ EQU   *-RTGTBLD           LENGTH OF TABLE ENTRY                        
*                                                                               
         TITLE 'REGETKSRC - TRANS IN/OUT OF RTG/QLF/BKTYPE - QLFTBLD'           
***********************************************************************         
*                                                                     *         
*        DSECT QUALIFIER TABLE                                        *         
*              XL1'EXTERNAL VALUE'                                    *         
*              XL1'INTERNAL VALUE'                                    *         
*                                                                     *         
***********************************************************************         
         DS    0D                                                               
QLFTBLD  DSECT                                                                  
QTBQLFEX DS    CL1                 QUALIFIER EXTERNAL CODE                      
QTBQLFIN DS    XL1                 QUALIFIER INTERNAL CODE                      
*                                                                               
QLFTBLLQ EQU   *-QLFTBLD           LENGTH OF TABLE ENTRY                        
*                                                                               
         EJECT                                                                  
*REGENINVA                                                                      
*        PRINT OFF                                                              
       ++INCLUDE REGENINVA                                                      
       ++INCLUDE DEDEMTABD                                                      
       ++INCLUDE DDCOMFACS                                                      
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'085REGETKSRC 12/21/10'                                      
         END                                                                    
