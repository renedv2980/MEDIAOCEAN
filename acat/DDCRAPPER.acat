*          DATA SET DDCRAPPER  AT LEVEL 003 AS OF 01/05/21                      
*CATALP CRAPPER                                                                 
         TITLE 'CRAPPER DETECTOR'                                               
*&&      SET   NOP=N                                                            
                                                                                
CRAPPER  CSECT                                                                  
         ENTRY CRAPBLK                                                          
         ENTRY CRAPBLX                                                          
         NMOD1 0,CRAPPER*                                                       
         STAR  CLEAR=ARZERO,ARS=OFF                                             
         USING CRAPPERD,CRAPBLK                                                 
*                                                                               
*NOP     B     CRAPPERT            TEMP EXIT FOR DEBUGGING                      
*                                                                               
         CLI   CRTRAP,C'Y'         TRAP REQUIRED?                               
         BNE   CRAPPERX            NO                                           
         CLI   CRINIT,C'F'         FAILED INIT?                                 
         BE    CRAPPERX            YES                                          
*                                                                               
         CLI   CRINIT,C'N'         INITIALISED?                                 
         BNE   *+8                 NO                                           
         BRAS  RE,INIT             DO IT                                        
         CLI   CRINIT,C'Y'         INITIALISED OK?                              
         BNE   CRAPPERX            NO                                           
*                                                                               
         SAM31                                                                  
         LAM   AR2,AR2,CRALET      SEE IF THE CORE IS CHANGED                   
         L     R2,CRAWATCH                                                      
         L     R3,CRLWATCH                                                      
         L     RE,CRCORE                                                        
         L     RF,CRLWATCH                                                      
         SAC   512                                                              
         CLCL  R2,RE               COMPARE CORE VS BUFFER                       
         BE    CRAPPERX            ALL OK - JUST EXIT                           
         EJECT                                                                  
***********************************************************************         
* HAVE A PIECE OF CRAPPED ON CORE                                     *         
***********************************************************************         
         BRAS  RE,ARSOFF                                                        
*                                                                               
         MVI   CRHIT,C'Y'          SET HAVE A HIT                               
         LH    RF,CRCOUNT                                                       
         AHI   RF,1                                                             
         STH   RF,CRCOUNT          INCREMENT COUNT OF HITS                      
*                                                                               
         S     R2,CRAWATCH                                                      
         ST    R2,CRDSP            DISPLACEMENT TO FIRST BAD BYTE               
*                                                                               
         XR    RF,RF               NOW SEE IF CAN GO A BIT BEFORE THAT          
         CHI   R2,8                                                             
         BNH   *+8                                                              
         LHI   RF,8                                                             
*                                                                               
         L     R1,CRCORE                                                        
         A     R1,CRDSP                                                         
         SR    R1,RF                                                            
         MVC   CRBEFR,0(R1)        SAVE ORIGINAL CORE COPY                      
*                                                                               
         LAM   AR2,AR2,CRALET                                                   
         L     R2,CRAWATCH                                                      
         A     R2,CRDSP                                                         
         SR    R2,RF                                                            
         SAC   512                                                              
         MVC   CRAFTR,0(R2)        SAVE BAD CORE COPY                           
         BRAS  RE,ARSOFF                                                        
*                                                                               
         CLI   CRFIX,C'Y'          FIX DAMAGED CORE?                            
         BNE   CRAP02              NO                                           
         L     R2,CRAWATCH                                                      
         L     R3,CRLWATCH                                                      
         LAM   AR2,AR2,CRALET                                                   
         L     RE,CRCORE                                                        
         L     RF,CRLWATCH                                                      
         SAC   512                                                              
         MVCL  R2,RE               RESTORE CORE FROM XA BUFFER                  
         BRAS  RE,ARSOFF                                                        
*                                                                               
CRAP02   BRAS  RE,REPORT                                                        
*                                                                               
CRAPPERX SAC   0                                                                
         REAR  ARS=OFF                                                          
         B     CRAPXA                                                           
         EJECT                                                                  
***********************************************************************         
* CHECK XA BUFFER AREAS IF REQUIRED                                   *         
***********************************************************************         
         USING CRAPPEXD,CRAPBLX                                                 
CRAPXA   CLI   CRXTRAP,C'Y'        ANY AX TRAPS                                 
         BNE   CRAPXXIT                                                         
         USING SSBD,RF                                                          
         L     RF,ASSB                                                          
         OC    SSBTPOPC,SSBTPOPC   NOT DURING FASTART                           
         BZ    CRAPXXIT                                                         
         SAM31                                                                  
         DROP  RF                                                               
*                                                                               
         L     RF,=V(SYSFAC)                                                    
         USING SYSFACD,RF                                                       
         L     R5,VTCB             CHECK **IOA ON ALL TASKS                     
         USING TCBD,R5                                                          
         LH    R6,0(R5)                                                         
         L     R7,2(R5)                                                         
         LA    R5,6(R5)                                                         
*                                                                               
CRAPX1   L     R1,TCBFILES         CHECK IO AREA BOUNDARIES                     
         SHI   R1,16                                                            
         CLC   0(5,R1),=C'**IOA'                                                
         BNE   CRAPXER1                                                         
         SHI   R1,16                                                            
         CLC   0(5,R1),=C'**IOA'                                                
         BNE   CRAPXER1                                                         
*                                                                               
         L     R1,TCBAZIP          GET A ZIP BUFFER                             
         USING ZIPHDRD,R1                                                       
         OC    ZIPBLEN,ZIPBLEN     CHECK BUFLEN IS NOT ZERO                     
         BZ    CRAPXER2                                                         
         DROP  R1                                                               
*                                                                               
         BXLE  R5,R6,CRAPX1        BACK FOR NEXT TCB ENTRY                      
*                                                                               
CRAPXXIT SAM24                                                                  
         B     EXIT                                                             
*                                                                               
CRAPXER1 L     R1,TCBFILES         RESTORE HEADERS AND DC H'0',$RET             
         SHI   R1,32                                                            
         MVC   0(32,R1),=C'**IOAX****IOAX****IOAX****IOAX**'                    
         B     CRAPWARN                                                         
*                                                                               
CRAPXER2 MVC   ZIPBLEN-ZIPHDRD(4,R1),=A(ZIPBLENM)                               
         B     CRAPWARN                                                         
*                                                                               
CRAPWARN TM    TCBINDS1,TCBFABND                                                
         BO    CRAPWR10            Do not die if in Abend                       
         DC    H'0',C'$RET'                                                     
                                                                                
CRAPWR10 ICM   RF,15,=V(DDWTO)                                                  
         BZ    CRAPXXIT                                                         
         GOTO1 (RF),CRPLIST,CRAPMSG,0                                           
         B     CRAPXXIT                                                         
         DROP  R5,RF                                                            
         EJECT                                                                  
***********************************************************************         
* EXIT POINTS AND HANDY ROUTINES                                      *         
***********************************************************************         
ARSOFF   SAC   0                                                                
         LAM   AR0,ARF,ARZERO                                                   
         BR    RE                                                               
*                                                                               
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALISE TEST BUFFER                                              *         
***********************************************************************         
INIT     NTR1  ,                                                                
         MVI   CRHIT,C'N'          NEW COMPARISON SEQUENCE                      
         XC    CRCOUNT,CRCOUNT                                                  
         XC    CRALET,CRALET                                                    
         XC    CRDSP,CRDSP                                                      
         XC    CRBEFR,CRBEFR                                                    
         XC    CRAFTR,CRAFTR                                                    
*                                                                               
         CLI   CRDSPC,0            DSPACE COMPARISON?                           
         BE    INIT02              NO                                           
*                                                                               
         L     RF,ASSB                                                          
         USING SSBD,RF                                                          
         CLI   CRDSPC,C'T'         TABS ALET                                    
         BNE   *+14                                                             
         MVC   CRALET,SSBTBLET                                                  
         B     INIT02                                                           
         CLI   CRDSPC,C'D'         DMGR ALET                                    
         BNE   *+14                                                             
         MVC   CRALET,SSBALET                                                   
         B     INIT02                                                           
*                                                                               
         MVI   CRDSPC,C'?'         SET UNKNOWN DSPACE                           
         MVC   CRALET,CRQUERY                                                   
         MVI   CRINIT,C'F'         SET FAILED TO INITIALISE                     
         B     EXIT                                                             
         DROP  RF                                                               
*                                                                               
INIT02   SAM31                     GET INTO XA                                  
*                                                                               
         L     R0,CRCORE           CLEAR CORE BUFFER                            
         L     R1,CRCOREL                                                       
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CRAWRK           CLEAR W/S BUFFER                             
         L     R1,CRLWRK                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CRATIA           CLEAR TIA BUFFER                             
         L     R1,CRLTIA                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CRATWA           CLEAR TWA BUFFER                             
         L     R1,CRLTWA                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CRAMAP           CLEAR MAP BUFFER                             
         L     R1,CRLMAP                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CRAPGM           CLEAR PGMS BUFFER                            
         L     R1,CRLPGM                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
         L     R0,CRAUTL           CLEAR UTL BUFFER                             
         L     R1,CRLUTL                                                        
         XR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R2,CRCORE                                                        
         L     R3,CRCOREL                                                       
         LAM   ARE,ARE,CRALET                                                   
         L     RE,CRAWATCH                                                      
         L     RF,CRLWATCH                                                      
         SAC   512                                                              
         MVCL  R2,RE               MOVE DATA INTO CORE BUFFER                   
         BRAS  RE,ARSOFF                                                        
*                                                                               
INITX    MVI   CRINIT,C'Y'         INITIALISED CORE BUFFER OK                   
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* REPORT ON DAMAGED STORAGE                                           *         
***********************************************************************         
REPORT   NTR1  ,                                                                
         L     R2,ASSB             PICK UP SSB                                  
         USING SSBD,R2                                                          
         MVC   MSGH+4(3),SSBSYSNA  SET FACPAK NAME INTO MESSAGE HEADER          
         MVC   MSG1TSK,CRQUERY                                                  
         MVC   MSG1SIN,CRQUERY                                                  
         MVC   MSG1LUID,CRQUERY                                                 
*                                                                               
         ICM   R3,15,SSBTKADR      PICK UP TCB OF CRAPPER                       
         BZ    REP02                                                            
         USING TCBD,R3                                                          
*                                                                               
         SAM31                                                                  
         L     R0,CRAWRK           SAVE W/S                                     
         L     R1,CRLWRK                                                        
         L     RE,TCBWRKA                                                       
         ST    RE,CRCWRK                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CRATIA           SAVE TIA                                     
         L     R1,CRLTIA                                                        
         L     RE,TCBTIA                                                        
         ST    RE,CRCTIA                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CRATWA           SAVE TWA                                     
         L     R1,CRLTWA                                                        
         L     RE,TCBTWA                                                        
         ST    RE,CRCTWA                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CRAMAP           SAVE MAP                                     
         L     R1,CRLMAP                                                        
         L     RE,TCBMAP                                                        
         ST    RE,CRCMAP                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CRAPGM           SAVE PGMS                                    
         L     R1,CRLPGM                                                        
         L     RE,TCBPGMA                                                       
         ST    RE,CRCPGM                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         L     R0,CRAUTL           SAVE UTL                                     
         L     R1,CRLUTL                                                        
         L     RE,TCBUTL                                                        
         ST    RE,CRCUTL                                                        
         LR    RF,R1                                                            
         MVCL  R0,RE                                                            
*                                                                               
         SAM24                                                                  
         MVC   MSG1TSK,TCBID                                                    
         GOTO1 VHEXOUT,CRPLIST,TCBSIN,MSG1SIN,L'TCBSIN,0,0                      
*                                                                               
         ICM   RF,15,TCBUTL                                                     
         BZ    *+10                                                             
         MVC   MSG1LUID,TLUID-UTLD(RF)                                          
*                                                                               
REP02    XR    R0,R0               MUST BE CLEAR FOR MULTILINE WTO              
         WTO   TEXT=((MSGHL,C),(MSG1L,D),(MSG2L,D),(0,E))                       
         B     EXIT                                                             
         DROP  R2,R3                                                            
         EJECT                                                                  
*&&NOP                                                                          
***********************************************************************         
* TEMPORARY CODE TO TRACE UTL VALUES                                  *         
***********************************************************************         
CRAPPERT SAC   0                                                                
         REAR  ARS=OFF                                                          
         L     RF,ASSB                                                          
         USING SSBD,RF                                                          
         ICM   R1,15,SSBTKADR                                                   
         BZ    CRAPERTX                                                         
         USING TCBD,R1                                                          
         CLC   TCBSYM,=C'D1LOXXXT'                                              
         BNE   CRAPERTX                                                         
         ICM   RE,15,TCBUTL                                                     
         BZ    CRAPERTX                                                         
         USING UTLD,RE                                                          
*                                                                               
         LAM   AR2,AR2,SSBTBLET                                                 
         SAM31                                                                  
         SAC   512                                                              
         XR    R2,R2                                                            
         ICM   R1,15,32(R2)        R1=SOT                                       
         BZ    CRAPERTX                                                         
         ICM   R3,15,60(R2)        R3=EOT                                       
         ICM   R2,15,36(R2)        CURRENT                                      
         BZ    CRAPERTX                                                         
         MVC   0(1,R2),TSTAT8                                                   
         LA    R2,1(R2)                                                         
         CR    R2,R3                                                            
         BL    CRAPERT9                                                         
         LR    R2,R1                                                            
*                                                                               
CRAPERT9 LR    R3,R2                                                            
         XR    R2,R2                                                            
         ST    R3,36(,R2)                                                       
         DROP  R1,RE,RF                                                         
*                                                                               
CRAPERTX SAC   0                                                                
         SAM24                                                                  
         B     EXIT                                                             
         EJECT                                                                  
*&&                                                                             
*&&NOP                                                                          
***********************************************************************         
* TEMPORARY CODE TO TRACE CPU VALUES                                  *         
***********************************************************************         
CRAPPERT SAC   0                                                                
         REAR  ARS=OFF                                                          
         L     RF,ASSB                                                          
         USING SSBD,RF                                                          
         ICM   RE,15,SSBTKADR                                                   
         BZ    CRAPERTX                                                         
         USING TCBD,RE                                                          
         CLC   TCBSYM,=C'D1LOXXXT'                                              
         BNE   CRAPERTX                                                         
*                                                                               
         LAM   AR2,AR2,SSBTBLET                                                 
         SAM31                                                                  
         SAC   512                                                              
         XR    R2,R2                                                            
         ICM   R1,15,32(R2)        R1=SOT                                       
         BZ    CRAPERTX                                                         
         ICM   R3,15,60(R2)        R3=EOT                                       
         ICM   R2,15,36(R2)        CURRENT                                      
         BZ    CRAPERTX                                                         
         MVC   0(4,R2),TCBCPUTK    TASK CPU TIME IN MICROSECS                   
         LA    R2,4(R2)                                                         
         CR    R2,R3                                                            
         BL    CRAPERT9                                                         
         LR    R2,R1                                                            
*                                                                               
CRAPERT9 LR    R3,R2                                                            
         XR    R2,R2                                                            
         ST    R3,36(,R2)                                                       
         DROP  RE,RF                                                            
*                                                                               
CRAPERTX SAC   0                                                                
         SAM24                                                                  
         B     EXIT                                                             
*&&                                                                             
         EJECT                                                                  
***********************************************************************         
* LITERALS AND CONSTANTS                                              *         
***********************************************************************         
         LTORG                                                                  
*                                                                               
ARZERO   DC    16F'0'                                                           
ASSB     DC    V(SSB)                                                           
VHEXOUT  DC    V(HEXOUT)                                                        
CRPLIST  DS    6F                                                               
CRQUERY  DC    16C'?'                                                           
*                                                                               
         DS    0D                                                               
         DC    CL16'CRAPBLK*CRAPBLK*'                                           
CRAPBLK  DC    (CRAPPERL)X'00'                                                  
CRAPBLKL EQU   *-CRAPBLK                                                        
*                                                                               
         DS    0D                                                               
         DC    CL16'CRAPBLX*CRAPBLX*'                                           
CRAPBLX  DC    C'YNNN'                                                          
*                                                                               
CRAPMSG  DC    C'CRAPPER IN XA SEE DUMP //'                                     
*                                                                               
MSGALL   DS    0D                                                               
MSGHL    DC    H'25'                                                            
MSGH     DC    CL25'*FACPAK Crapper Trace*'                                     
MSG1L    DC    H'50'                                                            
MSG1     DC    CL50' '                                                          
         ORG   MSG1                                                             
         DC    CL05'Task='                                                      
MSG1TSK  DC    CL08'?'                                                          
         DC    CL02', '                                                         
         DC    CL04'SIN='                                                       
MSG1SIN  DC    CL08'?'                                                          
         DC    CL02', '                                                         
         DC    CL05'LUID='                                                      
MSG1LUID DC    CL08'?'                                                          
         ORG   MSG1+L'MSG1                                                      
MSG2L    DC    H'50'                                                            
MSG2     DC    CL50'Please call systems if this message appears'                
         EJECT                                                                  
***********************************************************************         
* OTHER INCLUDED DSECTS                                               *         
***********************************************************************         
* DDCRAPPERD                                                                    
       ++INCLUDE DDCRAPPERD                                                     
* FASYSFAC                                                                      
       ++INCLUDE FASYSFAC                                                       
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
* FATCB                                                                         
       ++INCLUDE FATCB                                                          
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* DDZIPBLK                                                                      
       ++INCLUDE DDZIPBLK                                                       
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'003DDCRAPPER 01/05/21'                                      
         END                                                                    
