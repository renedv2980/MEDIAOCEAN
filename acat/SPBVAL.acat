*          DATA SET SPBVAL     AT LEVEL 019 AS OF 07/01/04                      
*CATALP SPBVAL                                                                  
         TITLE 'SPBVAL - NETPAK BILL VALUES'                                    
         SPACE 1                                                                
***********************************************************************         
*                                                                               
*   NOTE- SEE SPBVALD FOR PARAMETER LIST AND DESCRIPTION OF                     
*         PROGRAM FUNCTION                                                      
*                                                                               
***********************************************************************         
         SPACE 2                                                                
SPBVAL   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKDL,SPBVAL,CLEAR=YES                                          
*                                                                               
         USING WORKD,RC                                                         
         ST    R1,SAVR1                                                         
         L     R8,0(R1)            A(INPUT)                                     
         L     RA,4(R1)            A(OUTPUT BLOCK)                              
         USING SPBVALD,RA                                                       
*                                                                               
         XC    SPBVALD(SPBVALDL),SPBVALD   INITIALIZE OUTPUT AREA               
         ZAP   SPBVGRSP,=P'0'                                                   
         ZAP   SPBVNETP,=P'0'                                                   
         ZAP   SPBVACTP,=P'0'                                                   
*                                                                               
         CLI   0(R1),C'B'          IS INPUT A BILL HEADER                       
         BE    PRBILL                                                           
         CLI   0(R1),C'E'          OR A BUCKET ELEM                             
         BE    PRELEM                                                           
         CLI   0(R1),C'U'          OR A UNIT BILLING ELEM                       
         BE    UNELEM                                                           
         DC    H'0'                                                             
         SPACE 1                                                                
***********************************************************************         
*   PRBILL - PROCESS BILL HEADER                                                
***********************************************************************         
         SPACE 1                                                                
PRBILL   DS    0H                                                               
         USING BILLREC,R8                                                       
*                                                                               
         ZAP   SPBVACTP,BACTP      ACTUAL                                       
         MVC   SPBVGST,BVATAMT     SET GST                                      
*                                                                               
         SR    R0,R0               FOR PST SUM                                  
         SR    R1,R1               FOR HST SUM                                  
         SR    R6,R6               SUM PST (NOTE-SUM OF ALL PSTS)               
         ICM   R6,1,BILNPVTS       NUMBER OF VAT ELEMS                          
         BZ    PRB04                                                            
         LA    R4,BILPVELD                                                      
*                                                                               
PRB03    DS    0H                                                               
         ICM   RF,15,BILPVAMT-BILPVELD(R4)                                      
         AR    R0,RF               ADD TO PST TOTAL (INCL HST)                  
         CLI   BILPVCOD-BILPVELD(R4),C'H'                                       
         BNE   *+6                                                              
         AR    R1,RF               ADD TO HST TOTAL                             
         LA    R4,BILPVLEN(R4)     NEXT ELEM                                    
         BCT   R6,PRB03                                                         
*                                                                               
PRB04    DS    0H                                                               
         ST    R0,SPBVPST                                                       
         ST    R1,SPBVHST                                                       
*                                                                               
         LA    R5,BGRSP            R5 = A(GROSS)                                
         LA    R6,BNETP            R6 = A(NET)                                  
*                                                                               
         L     R1,SAVR1                                                         
         CLI   4(R1),0             NETPAK SUB-MEDIA FILTER PROVIDED?            
         BE    PRB15               NO                                           
*                                                                               
*                                  SUB-MEDIA "ELEMENTS" ONLY PRESENT            
*                                   ON RECORDS ADDED STARTING APR/2003          
         CLC   BDATE,=X'FAF3F0F4F0F1'   APR01/2003 (Y2K!)                       
         BNL   *+6                                                              
         DC    H'0'                NO SUB-MEDIA DATA AVAILABLE                  
*                                                                               
         SR    R0,R0                                                            
         ICM   R0,1,BILNMEDS       R0 = NUMBER OF SUB-MEDIA "ELEMENTS"          
         BZ    PRB08               NONE PRESENT: RETURN GROSS/NET OF 0          
*                                                                               
         LA    RF,BILSMELD         A(SUB-MEDIA "ELEMENTS")                      
         USING BILSMELD,RF                                                      
         CLC   BILSMCOD,4(R1)      SUB-MEDIA CODE MATCH?                        
         BE    PRB10                                                            
         LA    RF,BILSMLEN(RF)     BUMP TO NEXT "ELEMENT"                       
         BCT   R0,*-14                                                          
*                                                                               
PRB08    DS    0H                                                               
         LA    R5,=PL6'0'          NO DATA PRESENT FOR THIS SUB-MEDIA:          
         LA    R6,=PL6'0'           RETURN GROSS AND NET OF ZERO                
         B     PRB15                                                            
*                                                                               
PRB10    DS    0H                                                               
         LA    R5,BILSMGRS         USE SUB-MEDIA SPECIFIC GROSS                 
         LA    R6,BILSMNET         USE SUB-MEDIA SPECIFIC NET                   
         DROP  RF                                                               
*                                                                               
PRB15    DS    0H                                                               
         TM    BILSTAT,BSTTAORQ    FOR TRUE AOR                                 
         BO    PRB80               DONE                                         
*                                                                               
         TM    BILSTAT,BSTSCOMQ+BSTSNETQ                                        
         BZ    PRB30                                                            
*                                                                               
         TM    BILSTAT,BSTSCOMQ                                                 
         BZ    PRB20                                                            
*                                **COMMISSION BILL**                            
         ZAP   SPBVGRSP,0(6,R5)    GROSS MINUS NET = EFFECTIVE GROSS            
         SP    SPBVGRSP,0(6,R6)                                                 
         B     PRB80               EFF NET AND TAX = 0                          
*                                                                               
PRB20    DS    0H                **NET BILLS**                                  
         ZAP   SPBVGRSP,0(6,R6)    EFF GROSS = TRUE NET                         
         ZAP   SPBVNETP,0(6,R6)    EFF NET = TRUE NET                           
         MVC   SPBVETAX,BTAXAMT    EFF TAX = TRUE TAX                           
         B     PRB80                                                            
*                                                                               
PRB30    DS    0H                  NORMAL BILLS: EFF = TRUE FOR ALL             
         ZAP   SPBVGRSP,0(6,R5)                                                 
         ZAP   SPBVNETP,0(6,R6)                                                 
         MVC   SPBVETAX,BTAXAMT                                                 
*                                                                               
PRB80    DS    0H                                                               
*                                  FOR TEMPORARY COMPATIBILITY, RETURN          
*                                   THE *BINARY* GROSS, NET, AND ACTUAL         
*                                   (IF VALUE WILL FIT IN A REGISTER!)          
*                                                                               
         CP    SPBVGRSP,=P'2147483647'  MAX POSITIVE FULLWORD VALUE             
         BH    PRB85                                                            
         CP    SPBVGRSP,=P'-2147483648' MIN NEGATIVE FULLWORD VALUE             
         BL    PRB85                                                            
         ZAP   DUB,SPBVGRSP                                                     
         CVB   R0,DUB                                                           
         ST    R0,SPBVEGRS                                                      
*                                                                               
PRB85    DS    0H                                                               
         CP    SPBVNETP,=P'2147483647'                                          
         BH    PRB90                                                            
         CP    SPBVNETP,=P'-2147483648'                                         
         BL    PRB90                                                            
         ZAP   DUB,SPBVNETP                                                     
         CVB   R0,DUB                                                           
         ST    R0,SPBVENET                                                      
*                                                                               
PRB90    DS    0H                                                               
         CP    SPBVACTP,=P'2147483647'                                          
         BH    PRBX                                                             
         CP    SPBVACTP,=P'-2147483648'                                         
         BL    PRBX                                                             
         ZAP   DUB,SPBVACTP                                                     
         CVB   R0,DUB                                                           
         ST    R0,SPBVACT                                                       
*                                                                               
PRBX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*   PRELEM - PROCESS STATION BUCKET ELEM                                        
***********************************************************************         
         SPACE 1                                                                
PRELEM   DS    0H                                                               
         USING STABELEM,R8                                                      
*                                                                               
         ICM   R0,15,STABGRS                                                    
         ICM   R1,15,STABNET                                                    
*                                                                               
         SR    R2,R2               NO TAX                                       
         CLI   STABELEM+1,18       IF LENGTH 18 OR LESS                         
         BNH   PRE10                                                            
*                                                                               
         CLI   STABELEM+1,19       IF 19                                        
         BNE   *+14                                                             
         MVC   STAT,STABSTAT       ONLY STATUS                                  
         B     PRE10                                                            
*                                                                               
         CLI   STABELEM+1,21       IF 21                                        
         BNE   *+12                                                             
         ICM   R2,7,STABTAX        ONLY TAX (AT STABTAX)                        
         B     PRE10                                                            
*                                                                               
*                                  IF 22                                        
         MVC   STAT,STABSTAT       HAVE STATUS                                  
         ICM   R2,7,STABTAX+1      AND TAX (AT +1)                              
*                                                                               
PRE10    DS    0H                                                               
         SLL   R2,8                                                             
         SRA   R2,8                                                             
*                                                                               
         ST    R0,TRUEGRS          'TRUE' GROSS                                 
         ST    R1,TRUENET                 NET                                   
         ST    R2,TRUETAX                 AND TAX                               
*                                                                               
         TM    STAT,STABSSCQ+STABSSNQ                                           
         BZ    PRE30                                                            
*                                                                               
         TM    STAT,STABSSCQ                                                    
         BZ    PRE20                                                            
*                                **COMMISSION BILL**                            
         SR    R0,R1               GROSS - NET = EFFECTIVE GRS                  
         ST    R0,SPBVEGRS                                                      
         B     PRE80               EFF NET AND TAX = 0                          
*                                                                               
PRE20    DS    0H                **NET BILLS**                                  
         ST    R1,SPBVEGRS         EFF GROSS = TRUE NET                         
         ST    R1,SPBVENET         EFF NET = TRUE NET                           
         ST    R2,SPBVETAX         EFF TAX = TRUE TAX                           
         B     PRE80                                                            
*                                                                               
PRE30    DS    0H                  NORMAL BILLS: EFF = TRUE FOR ALL             
         MVC   SPBVEGRS,TRUEGRS                                                 
         MVC   SPBVENET,TRUENET                                                 
         MVC   SPBVETAX,TRUETAX                                                 
*                                                                               
PRE80    DS    0H                                                               
         L     R1,SAVR1            SEE IF HAVE BILL FORMULA                     
         SR    R2,R2                                                            
         ICM   R2,7,9(R1)                                                       
         BZ    PRE90               NO, DON'T CALCULATE ACTUAL                   
*                                                                               
         TM    STAT,STABSSNQ       IF NET BILL                                  
         BZ    *+14                                                             
         MVC   SPBVACT,TRUENET     ACTUAL = NET                                 
         B     PRE90                                                            
*                                                                               
         MVC   SPBVACT,SPBVEGRS    ACTUAL = EFFECTIVE GROSS                     
         OC    0(5,R2),0(R2)       IF NO FORMULA                                
         BZ    PRE90                                                            
*                                                                               
         ICM   R1,15,1(R2)         ADJUSTMENT %                                 
         BZ    PRE84               NONE, JUST USE BILL BASIS                    
         L     RF,TRUEGRS          ADJ BASIS IS GROSS                           
         TM    0(R2),X'01'                                                      
         BZ    *+8                                                              
         L     RF,TRUENET          OR NET                                       
         S     RF,TRUETAX          NO TAX IN ADJ CALC                           
         MR    R0,RF                                                            
         L     RF,=F'1000000'                                                   
         DRNDR (R0),(RF)                                                        
*                                                                               
PRE84    DS    0H                                                               
         L     RF,TRUEGRS          BILL BASIS IS GROSS                          
         TM    0(R2),X'10'                                                      
         BZ    *+8                                                              
         L     RF,TRUENET          OR NET                                       
         AR    R1,RF                                                            
*                                                                               
         TM    STAT,STABSSCQ       IF SEP COMMISSION BILL                       
         BZ    *+8                 ACTUAL IS FORMULA LESS NET                   
         S     R1,TRUENET          (TRUE AGENCY COMMISSION)                     
*                                                                               
         ST    R1,SPBVACT                                                       
*                                                                               
PRE90    DS    0H                  REGULAR BILL                                 
*                                  FOR FORWARD COMPATIBILITY, RETURN            
*                                   THE *PACKED* GROSS, NET, AND ACTUAL         
         L     R0,SPBVEGRS                                                      
         CVD   R0,DUB                                                           
         ZAP   SPBVGRSP,DUB                                                     
         L     R0,SPBVENET                                                      
         CVD   R0,DUB                                                           
         ZAP   SPBVNETP,DUB                                                     
         L     R0,SPBVACT                                                       
         CVD   R0,DUB                                                           
         ZAP   SPBVACTP,DUB                                                     
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*   UNELEM - PROCESS UNIT BILLING ELEM                                          
***********************************************************************         
         SPACE 1                                                                
UNELEM   DS    0H                                                               
         USING NUBILD,R8                                                        
*                                                                               
         ICM   R0,15,NUBILGRS                                                   
         ICM   R1,15,NUBILNET                                                   
*                                                                               
         ST    R0,TRUEGRS          'TRUE' GROSS                                 
         ST    R1,TRUENET                 NET                                   
*                                                                               
         TM    NUBILST,NUBILSCQ+NUBILSNQ                                        
         BZ    UNE30                                                            
*                                                                               
         TM    NUBILST,NUBILSCQ                                                 
         BZ    UNE20                                                            
*                                **COMMISSION BILL**                            
         SR    R0,R1               GROSS - NET = EFFECTIVE GRS                  
         ST    R0,SPBVEGRS                                                      
         B     UNE80               EFF NET AND TAX = 0                          
*                                                                               
UNE20    DS    0H                **NET BILLS**                                  
         ST    R1,SPBVEGRS         EFF GROSS = TRUE NET                         
         ST    R1,SPBVENET         EFF NET = TRUE NET                           
         B     UNE80                                                            
*                                                                               
UNE30    DS    0H                  NORMAL BILLS: EFF = TRUE FOR ALL             
         MVC   SPBVEGRS,TRUEGRS                                                 
         MVC   SPBVENET,TRUENET                                                 
*                                                                               
UNE80    DS    0H                                                               
         L     R1,SAVR1            SEE IF HAVE BILL FORMULA                     
         SR    R2,R2                                                            
         ICM   R2,7,9(R1)                                                       
         BZ    UNE90               NO, DON'T CALCULATE ACTUAL                   
*                                                                               
         TM    NUBILST,NUBILSNQ    IF NET BILL                                  
         BZ    *+14                                                             
         MVC   SPBVACT,TRUENET     ACTUAL = NET                                 
         B     UNE90                                                            
*                                                                               
         MVC   SPBVACT,SPBVEGRS    ACTUAL = EFFECTIVE GROSS                     
         OC    0(5,R2),0(R2)       IF NO FORMULA                                
         BZ    UNE90                                                            
*                                                                               
         ICM   R1,15,1(R2)         ADJUSTMENT %                                 
         BZ    UNE84               NONE, JUST USE BILL BASIS                    
         L     RF,TRUEGRS          ADJ BASIS IS GROSS                           
         TM    0(R2),X'01'                                                      
         BZ    *+8                                                              
         L     RF,TRUENET          OR NET                                       
         MR    R0,RF                                                            
         L     RF,=F'1000000'                                                   
         DRNDR (R0),(RF)                                                        
*                                                                               
UNE84    DS    0H                                                               
         L     RF,TRUEGRS          BILL BASIS IS GROSS                          
         TM    0(R2),X'10'                                                      
         BZ    *+8                                                              
         L     RF,TRUENET          OR NET                                       
         AR    R1,RF                                                            
*                                                                               
         TM    NUBILST,NUBILSCQ       IF SEP COMMISSION BILL                    
         BZ    *+8                 ACTUAL IS FORMULA LESS NET                   
         S     R1,TRUENET          (TRUE AGENCY COMMISSION)                     
*                                                                               
         ST    R1,SPBVACT                                                       
*                                                                               
UNE90    DS    0H                  REGULAR BILL                                 
*                                  FOR FORWARD COMPATIBILITY, RETURN            
*                                   THE *PACKED* GROSS, NET, AND ACTUAL         
         L     R0,SPBVEGRS                                                      
         CVD   R0,DUB                                                           
         ZAP   SPBVGRSP,DUB                                                     
         L     R0,SPBVENET                                                      
         CVD   R0,DUB                                                           
         ZAP   SPBVNETP,DUB                                                     
         L     R0,SPBVACT                                                       
         CVD   R0,DUB                                                           
         ZAP   SPBVACTP,DUB                                                     
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
EXIT     XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
SAVR1    DS    F                                                                
TRUEGRS  DS    F                                                                
TRUENET  DS    F                                                                
TRUETAX  DS    F                                                                
STAT     DS    X                                                                
*                                                                               
WORKDL   EQU   *-WORKD                                                          
         EJECT                                                                  
BILLRECD DSECT                                                                  
       ++INCLUDE SPGENBILL                                                      
         EJECT                                                                  
       ++INCLUDE SPGENSTAB                                                      
         EJECT                                                                  
       ++INCLUDE NEGENUNIT                                                      
         EJECT                                                                  
SPBVALDD DSECT                                                                  
       ++INCLUDE SPBVALD                                                        
         SPACE 3                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019SPBVAL    07/01/04'                                      
         END                                                                    
