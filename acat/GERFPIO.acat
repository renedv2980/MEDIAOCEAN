*          DATA SET GERFPIO    AT LEVEL 019 AS OF 05/18/17                      
*CATALP RFPIO                                                                   
RFPIO    TITLE '- I/O CONTROL MODULE FOR RFP GROUP RECORDS'                     
***********************************************************************         
* CALLED FROM  REQUEST PROGRAMS, RLP MAINTENANCE AND EDREQPEEL        *         
***********************************************************************         
                                                                                
RFPIO    CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKL,**RFPIO*,RA,RR=RE,CLEAR=YES                                
         USING WORKD,RC                                                         
                                                                                
         L     R8,0(R1)                                                         
         USING RFPIOD,R8           R8=A(RFP BLOCK)                              
         LA    R9,RFPIOD                                                        
         AHI   R9,RFPBLKLN                                                      
         USING RFPXTNSN,R9         R9=A(RFP BLOCK EXTENSION)                    
         L     R7,RFPAMIN                                                       
         USING MINBLKD,R7          R7=A(MINIO BLOCK)                            
                                                                                
REQ      USING GRPRRNUM,MINEKEY+1  REQUEST ELEMENTS                             
SYM      USING GRPSSYMB,MINEKEY+1  SYMBOLIC ELEMENTS                            
BDA      USING GRPBSYMB,MINEKEY+1  BILLING DATE ADJUSTMENT ELEMENT              
                                                                                
         MVI   RFPERROR,RFPNOERR   RESET ERROR BYTE                             
         MVI   MINRDUP,C'Y'        TEST/SET READ FOR UPDATE                     
         TM    RFPFLAGS,RFPXRDUP                                                
         BZ    *+8                                                              
         MVI   MINRDUP,C'N'                                                     
                                                                                
         MVI   MINDELSW,C'N'       TEST/SET READ FOR DELETES                    
         TM    RFPFLAGS,RFPRDDEL                                                
         BZ    *+8                                                              
         MVI   MINDELSW,C'Y'                                                    
                                                                                
         L     RF,VBINSRCH                                                      
         AR    RF,RE                                                            
         ST    RF,BINSRCH          A(BINSRCH)                                   
         L     RF,VRECUP                                                        
         AR    RF,RE                                                            
         ST    RF,RECUP            A(RECUP)                                     
*&&UK                                                                           
         L     RF,VMINIO           UK USES MINIOUS AND ALWAYS LINKED            
         AR    RF,RE                                                            
         ST    RF,MINIO                                                         
*&&                                                                             
                                                                                
         MVC   COMFACS,RFPACOMF    A(COMFACS)                                   
         ICM   RF,15,COMFACS                                                    
         BZ    INIT02              WE'RE OFFLINE - NO COMFACS                   
                                                                                
         USING COMFACSD,RF                                                      
         MVC   DATAMGR,CDATAMGR    A(DATAMGR)                                   
         MVC   GETFACT,CGETFACT    A(GETFACT)                                   
*&&US*&& MVC   MINIO,CMINIO        A(MINIO)                                     
         MVC   SWITCH,CSWITCH      A(SWITCH)                                    
         MVC   DICTATE,CDICTATE    A(DICTATE)                                   
         MVC   DATCON,CDATCON      A(DATCON)                                    
         DROP  RF                                                               
                                                                                
         OC    SWITCH,SWITCH       IF SWITCH NOT REOLVED WE ARE OFFLINE         
         BNZ   *+12                                                             
         MVI   SAVESENO,RFPFSCTN   AND CALLER MUST HAVE OPENED FILES            
         B     INIT04                                                           
                                                                                
         ICM   RF,8,=X'FE'                                                      
         ICM   RF,7,EFFS                                                        
         GOTOR SWITCH,DMCB,(RF)                                                 
         L     RF,0(R1)            RF=A(SYSFACS)                                
         L     RF,VSSB-SYSFACD(RF)                                              
         ST    RF,ASSB                                                          
         L     RF,SSBTKADR-SSBD(RF)                                             
         L     RF,TCBUTL-TCBD(RF)                                               
         ST    RF,AUTL                                                          
         MVC   SAVEACCS,TACCS-UTLD(RF)                                          
                                                                                
         GOTOR GETFACT,DMCB,0                                                   
         L     R1,0(R1)                                                         
         MVC   SAVESENO,FASYS-FACTSD(R1)                                        
         L     R1,FATIME-FACTSD(R1)                                             
         SRL   R1,4                                                             
         STCM  R1,7,SAVETIME       SAVE TIME (PWOS HHMMSS)                      
         B     INIT04                                                           
                                                                                
INIT02   MVC   DATAMGR,VDMGR                                                    
*&&US*&& MVC   MINIO,VMINIO                                                     
         MVC   DATCON,VDATCON                                                   
         LA    RF,VDMGR                                                         
         ST    RF,COMFACS          A(A(DATAMGR)) TO SIMULATE COMFACS            
         MVI   SAVESENO,RFPFSCTN   REQPEEL ALREADY OPENED GENDIR/GENFIL         
                                                                                
INIT04   LA    RF,SYSTAB           TABLE OF KNOWN SYSTEM LETTERS                
         LHI   R0,SYSTABN                                                       
INIT06   CLC   RFPFSYS,0(RF)       MATCH SYSTEM LETTER TO TABLE                 
         BE    INIT08                                                           
         AHI   RF,L'SYSTAB                                                      
         BCT   R0,INIT06                                                        
         MVI   RFPERROR,RFPNOSYS   SET INVALID SYSTEM                           
         OC    SWITCH,SWITCH       TEST COMFACS RESOLVED                        
         BZ    EXIT                NO - DON'T DIE - WE'RE OFFLINE               
         DC    H'0'                ONLINE YOU'RE DEAD                           
                                                                                
INIT08   MVC   OVSYS,1(RF)         SET OVERLAY SYSTEM NUMBER                    
         LHI   RE,RFPMAXSQ         REGULAR SYMBOL COUNT                         
         LA    RF,RFPVSYMS         REGULAR SYMBOL TABLE                         
         TM    RFPFLAGS,RFPXSYMS   IS BLOCK EXTENSION PRESENT?                  
         BZ    *+12                                                             
         LHI   RE,RFPMAXSX         EXTENDED SYMBOL COUNT                        
         LA    RF,RFPVSYMX         EXTENDED SYMBOL TABLE                        
         STM   RE,RF,MAXSYMS       SAVE MAXIMUM N'SYMBOLS/A(SYMBOLS)            
                                                                                
***********************************************************************         
* GO TO APPROPRIATE ROUTINE                                           *         
***********************************************************************         
                                                                                
         CLI   RFPINIT,0           IS THIS THE FIRST TIME CALL?                 
         BE    INIBLK                                                           
                                                                                
         CLI   SAVESENO,RFPFSCTN   SWITCH TO CONTROL IF NECESSARY               
         BE    INIT10                                                           
         ICM   RF,8,=AL1(RFPFSCTN)                                              
         ICM   RF,7,EFFS                                                        
         GOTOR SWITCH,DMCB,(RF),0                                               
         CLI   4(R1),0                                                          
         BE    INIT10                                                           
         DC    H'0'                                                             
                                                                                
INIT10   CLI   RFPMODE,RFPVALGP    VALIDATE GROUP NAME?                         
         BE    VALGRP                                                           
                                                                                
         CLC   SVMST(SVMSTL),RFPFUID                                            
         BE    *+12                                                             
         MVI   RFPERROR,RFPRKCHA                                                
         B     EXITSW                                                           
                                                                                
         CLI   RFPMODE,RFPSAVGP    SAVE GROUP?                                  
         BE    SAVGRP                                                           
         CLI   RFPMODE,RFPDELGP    DELETE A GROUP?                              
         BE    DELGRP                                                           
                                                                                
         CLI   RFPMODE,RFPRETRQ    RETURN REQUEST CARDS?                        
         BE    RETREQ                                                           
         CLI   RFPMODE,RFPADDRQ    ADD REQUEST CARDS?                           
         BE    ADDREQ                                                           
                                                                                
         CLC   SVREQ(SVREQL),RFPFRQID                                           
         BE    *+12                                                             
         MVI   RFPERROR,RFPRKCHA                                                
         B     EXITSW                                                           
                                                                                
         CLI   RFPMODE,RFPCHARQ    CHANGE REQUEST?                              
         BE    CHAREQ                                                           
         CLI   RFPMODE,RFPDELRQ    DELETE REQUEST?                              
         BE    DELREQ                                                           
                                                                                
         DC    H'0'                ACTION NOT DEFINED                           
                                                                                
EXITSW   CLI   SAVESENO,RFPFSCTN   EXIT IF SWITCHING TO CONTROL SYSTEM          
         BE    EXIT                                                             
         ICM   RF,8,SAVESENO                                                    
         ICM   RF,7,EFFS                                                        
         GOTOR SWITCH,DMCB,(RF),0                                               
         CLI   4(R1),0                                                          
         BE    EXIT                                                             
         DC    H'0'                                                             
                                                                                
EXIT     XIT1  ,                                                                
         EJECT                                                                  
***********************************************************************         
* INITIALIZATION ROUTINE                                              *         
***********************************************************************         
                                                                                
INIBLK   MVI   RFPINIT,1           THE BLOCK IS ABOUT TO BE INITIALIZED         
         LA    RE,RFPFGRP          CLEAR MOST OF BLOCK                          
         LHI   RF,RFPBLKX-RFPFGRP                                               
         TM    RFPFLAGS,RFPXSYMS   IS BLOCK EXTENSION PRESENT?                  
         BZ    *+8                                                              
         LHI   RF,RFPBLXX-RFPFGRP                                               
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
                                                                                
         LA    RE,MINBLKD          CLEAR USER-SUPPLIED MINIO AREA               
         LHI   RF,FIVEK                                                         
         SR    R0,R0                                                            
         SR    R1,R1                                                            
         MVCL  RE,R0                                                            
                                                                                
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,GENFIL       FILE NAME                                    
         MVC   MINDIR,GENDIR       DIR NAME                                     
         MVI   MINFKLEN,L'GRPKEY   KEY LENGTH                                   
         MVI   MINNCTL,L'GRPKSTAT  NUMBER OF CONTROL BYTES                      
         LHI   R0,2000                                                          
         STCM  R0,3,MINFRCLM       MAXIMUM RECORD LENGTH                        
         MVI   MINEKLEN,L'GRPKELEM ELEMENT KEY LENGTH                           
         MVI   MINEKDSP,GRPKELEM-GRPKEYD  DISPLACEMENT TO ELEMENT KEY           
         LA    RF,MINBLKD+ONEK     1K INTO USER-SUPPLIED AREA                   
         ST    RF,MINBUFF          A(RECORD BUFFER)                             
         LA    RF,MINBLKD+MINBLKL  POINT PAST MINIO PARAMETER BLOCK             
         ST    RF,MINELEM          A(AREA FOR MINIO ELEMENT)                    
         AHI   RF,MAXELLNQ                                                      
         C     RF,MINBUFF          ARE WE STILL BELOW THE 1K LINE?              
         BL    *+6                                                              
         DC    H'0'                NO - NEED MORE STORAGE IN RFPAMIN            
         MVC   MINCOMF,COMFACS     A(COMFACS)                                   
         MVC   MINRECUP,RECUP      A(RECUP)                                     
         MVC   MINHOOK,RFPAHOOK    SET A(CALLER'S I/O HOOK)                     
         MVC   MINREGD,RFPARD      SET A(CALLER'S RD VALUE)                     
         MVC   MINMAXEL,=Y(MAXELLNQ) MAX LENGTH OF ELEMENT                      
         MVI   MINFILTL,1          ELEMENT KEY FILTER LENGTH                    
         GOTOR SETRCC              SET REQUESTOR CARD COLUMN                    
                                                                                
INIBLKX  B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* VALIDATE A GROUP                                                    *         
***********************************************************************         
                                                                                
VALGRP   TM    RFPFFLAG,RFPFSYMS   RETURN ALL POTENTIAL SYMBOLS?                
         BNZ   *+12                YES                                          
         TM    RFPFLAGS,RFPNOSYM   TEST CALLER WANTS SYMBOLS                    
         BNZ   VALGRP12                                                         
                                                                                
         LA    R2,DICTLIST         BUILD ESCAPE TABLE HERE                      
         LA    R3,KEY                                                           
         USING GRKEY,R3                                                         
         XC    GRKEY,GRKEY                                                      
         MVI   GRKREC,GRKRECQ      READ RFP GROUP PASSIVES                      
         MVC   GRKSYS,OVSYS        MAJOR SYSTEM NUMBER                          
                                                                                
VALGRP02 MVC   KEYSAVE,GRKEY                                                    
         GOTOR DATAMGR,DMCB,(0,DMRDHI),GENDIR,GRKEY,GRKEY                       
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   GRKEY(GRKMNUM-GRKEY),KEYSAVE                                     
         BNE   VALGRP04                                                         
                                                                                
         MVI   0(R2),DD#ESCL       START OF ESCAPE SEQUENCE                     
         MVC   1(2,R2),GRKMNUM     MESSAGE NUMBER                               
         MVI   3(R2),L'RFPVSYMB    ALWAYS LENGTH 24                             
         MVC   4(1,R2),GRKRFP      RFP RULE NUMBER                              
         AHI   R2,L'DICTLIST       BUMP TO NEXT SLOT IN TABLE                   
                                                                                
         SR    R1,R1                                                            
         IC    R1,GRKMNUM+1                                                     
         AHI   R1,1                                                             
         STC   R1,GRKMNUM+1        SKIP TO NEXT MESSAGE NUMBER                  
         XC    KEY+8(24),KEY+8                                                  
         B     VALGRP02                                                         
         DROP  R3                                                               
                                                                                
VALGRP04 OC    DICTATE,DICTATE     ONLINE?                                      
         BZ    VALGRP12            NO                                           
                                                                                
         SR    R0,R0               SYMBOL COUNTER                               
         LA    R2,DICTLIST         A(ESCAPE SEQUENCES)                          
         LA    R1,SYMBOLS          A(SYMBOL TABLE)                              
         LHI   RE,L'SYMBOLS        L'SYMBOL ENTRY                               
         LA    RF,SYMBOLSX         A(END OF SYMBOL TABLE)                       
VALGRP06 CLI   0(R2),0             ANY MORE ESCAPE SEQUENCES?                   
         BE    VALGRP08            NO                                           
         MVC   0(4,R1),0(R2)       COPY ESCAPE SEQUENCE TO SYMBOL TABLE         
         AHI   R0,1                INCREMENT SYMBOL COUNTER                     
         AHI   R2,L'DICTLIST       BUMP TO NEXT ESCAPE SEQUENCE                 
         BXLE  R1,RE,VALGRP06                                                   
         DC    H'0'                TOO MANY SYMBOLS                             
                                                                                
VALGRP08 LTR   R0,R0                                                            
         BZ    VALGRP12                                                         
         LA    R2,SYMBOLS                                                       
         L     RF,DICTATE                                                       
VALGRP10 LA    R1,DMCB                                                          
         USING DICTATED,R1                                                      
         XC    DICTATED(DICTATEL),DICTATED                                      
         MVI   DDACTN,DDACTNS      SINGLE ESCAPE SEQUENCE TRANSLATE             
         MVI   DDRETN,C' '         DEFAULT CASE                                 
         MVC   DDSYS,OVSYS                                                      
         MVI   DDLANG,C' '         DEFAULT LANGUAGE                             
         STCM  R2,7,DDIADR                                                      
         BASR  RE,RF                                                            
         AHI   R2,L'SYMBOLS        BUMP TO NEXT ENTRY IN SYMBOL TABLE           
         BCT   R0,VALGRP10                                                      
         DROP  R1                                                               
                                                                                
VALGRP12 XC    MINMKEY,MINMKEY     CLEAR MASTER KEY                             
         LA    RF,MINMKEY                                                       
         USING GRPKEYD,RF                                                       
         MVI   GRPKSYS,GRPKSYSQ    RFP GROUP RECORD TYPE AND SUB-TYPE           
         MVI   GRPKSTYP,GRPKSTYQ                                                
         MVC   GRPKSYST,RFPFSYS    SYSTEM (E.G., C'S' = SPOT)                   
         MVC   GRPKAGY,RFPFAGY     AGENCY ALPHA                                 
         MVC   GRPKUSER,RFPFUID    USERID                                       
         MVC   GRPKGRP,RFPFGRP     GROUP NAME                                   
         DROP  RF                                                               
                                                                                
         MVC   SVMST(SVMSTL),RFPFUID                                            
                                                                                
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPHCDQ     LOOK FOR HEADER ELEMENT                      
         GOTOR GOMINIO,MINHI                                                    
         CLI   MINERR,MINESNF                                                   
         BNE   *+12                                                             
         MVI   RFPERROR,RFPNOGRP   GROUP NOT FOUND                              
         B     VALGRPX                                                          
                                                                                
         CLI   MINERR,0            TEST FOR OTHER ERRORS                        
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVC   RFPVAGY,RFPFAGY     AGENCY                                       
         MVC   RFPVUSER,RFPFUID    USERID                                       
         MVC   RFPVSYST,RFPFSYS    SYSTEM                                       
         MVC   RFPVGRP,RFPFGRP     GROUP                                        
         MVI   RFPVGSTA,0                                                       
         TM    MINSTAT,MINDELQ     TEST GROUP IS DELETED                        
         BZ    *+8                                                              
         OI    RFPVGSTA,RFPVGSDQ   YES - SET GROUP IS DELETED                   
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPHD,R4                                                         
         MVC   RFPVDESC,GRPHDESC   DESCRIPTION                                  
         MVC   RFPVFREQ,GRPHFREQ   FREQUENCY                                    
         MVC   RFPVOTYP,GRPHOTYP   OUTPUT TYPE                                  
         MVC   RFPVDEST,GRPHDEST   DESTINATION                                  
         MVC   RFPVNAME,GRPHNAME   NAME                                         
         MVC   RFPVNXTR,GRPHNXTR   NEXT RUN DATE                                
         MVC   RFPVLSTR,GRPHLSTR   LAST RUN DATE                                
         MVC   RFPVENDR,GRPHEND                                                 
         MVC   RFPXFILE,GRPHXFIL   XFILE GROUP KEY                              
         CLC   RFPXFILE,SPACES                                                  
         BH    *+10                                                             
         XC    RFPXFILE,RFPXFILE                                                
         MVC   RFPVHSTA,GRPHSTAT   HEADER STATUS                                
         MVC   RFPVPERS,GRPHPERS   CREATOR PERSON ID                            
                                                                                
         XC    RFPVDSC2,RFPVDSC2   HANDLE EXTENDED DESCRPTION                   
         CLI   GRPHLN,GRPHLNQ                                                   
         BE    VALGRP14                                                         
         MVI   RFPVDSC2,C' '                                                    
         MVC   RFPVDSC2+1(L'RFPVDSC2-1),RFPVDSC2                                
         SR    RE,RE                                                            
         ICM   RE,1,GRPHLN                                                      
         SHI   RE,GRPHLNQ+1                                                     
         BNM   *+6                                                              
         DC    H'0'                                                             
         EX    RE,*+8                                                           
         B     VALGRP14                                                         
         MVC   RFPVDSC2(0),GRPHDSC2                                             
         DROP  R4                                                               
                                                                                
VALGRP14 MVC   SVNXTRUN,RFPVNXTR   SAVE NEXT RUN DATE                           
                                                                                
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPDCDQ     LOOK FOR DATE RANGE ELEMENT                  
         GOTOR GOMINIO,MINHI                                                    
         BE    *+20                                                             
         XC    RFPVRNGE,RFPVRNGE   NO PREVIOUS RANGE ELEMENT                    
         XC    RFPVDAYS,RFPVDAYS                                                
         B     VALGRP16                                                         
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPDD,R4                                                         
         MVC   RFPVRNGE,GRPDTS     RANGE OF BIT/DATES                           
         MVC   RFPVDAYS,GRPDDAY    RUNDAYS                                      
         DROP  R4                                                               
                                                                                
VALGRP16 TM    RFPFFLAG,RFPFSYMS   RETURN ALL POTENTIAL SYMBOLS?                
         BZ    VALGRP20            NO                                           
                                                                                
         SR    R0,R0               SYMBOL COUNTER                               
         LA    R1,DICTLIST                                                      
         LA    RE,SYMBOLS          A(RESOLVED DICTIONARY VALUES)                
         L     RF,ARFPSYMS         A(RETURNED SYMBOL TABLE)                     
         USING RFPVSYME,RF                                                      
                                                                                
VALGRP18 CLI   0(R1),0             ANY MORE ESCAPE SEQUENCES?                   
         BNE   *+12                                                             
         STC   R0,RFPVNUMS         NO - SET N'ESCAPE SEQUENCES & EXIT           
         B     VALGRPX                                                          
                                                                                
         MVC   RFPVSYME,0(R1)      ESCAPE SEQUENCE                              
         MVC   RFPVVRUL,4(R1)      VALIDATION RULE NUMBER                       
         MVC   RFPVSYMB,0(RE)      RESOLVED SYMBOL                              
                                                                                
         AHI   RE,L'RFPVSYMB       BUMP TO NEXT RESOLVED SYMBOL                 
         AHI   R1,5                BUMP TO NEXT ESCAPE SEQUENCE                 
         AHI   RF,RFPVSYML         BUMP TO NEXT ENTRY IN USER TABLE             
         AHI   R0,1                BUMP SYMBOL COUNTER                          
         C     R0,MAXSYMS                                                       
         BNH   VALGRP18                                                         
         DC    H'0'                TOO MANY SYMBOLIC NAMES                      
         DROP  RF                                                               
                                                                                
VALGRP20 MVI   RFPVNUMS,0          NUMBER OF RETURNED SYMBOLS                   
         XC    RFPBVALS,RFPBVALS   CLEAR BILLING DATE ADJUSTMENTS               
         XC    NUMSYMS,NUMSYMS                                                  
         TM    RFPFLAGS,RFPNOREQ   TEST NO INTEREST IN REQUESTS                 
         BNZ   VALGRP44                                                         
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST ELEMENTS                    
         SR    R0,R0               R0=N'REQUESTS IN GROUP                       
         GOTOR GOMINIO,MINHI                                                    
         B     VALGRP24                                                         
                                                                                
VALGRP22 GOTOR GOMINIO,MINSEQ      GET NEXT REQUEST                             
                                                                                
VALGRP24 CLI   MINERR,MINEEOF      ANY MORE REQUEST CARDS?                      
         BE    VALGRP38            NO                                           
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         CLI   GRPRCRDN,GRPRCHDQ   REQUEST HEADER?                              
         BNE   VALGRP26                                                         
         AHI   R0,1                BUMP N'REQUESTS IN GROUP                     
         MVC   REQSTAT,GRPRSTAT    EXTRACT REQUEST STATUS                       
         B     VALGRP22                                                         
                                                                                
VALGRP26 TM    REQSTAT,GRPRDISQ    IS REQUEST DISABLED?                         
         BNZ   VALGRP22            YES                                          
         CLI   GRPRCRDN,GRPRCDSQ   REQUEST DESCRIPTION?                         
         BE    VALGRP22            YES - NOT A REQUEST CARD                     
                                                                                
VALGRP30 LA    R4,GRPCARD          REQUEST CARD                                 
         DROP  R4                                                               
                                                                                
         LHI   R2,1                                                             
         LA    R3,L'GRPCARD-2(R4)                                               
         GOTOR ,BINPARM,,ESCAPES,,L'ESCAPES,(0,3)                               
         L     RF,BINSRCH                                                       
VALGRP32 CLI   0(R4),DD#ESCL2      IS THIS AN ESCAPE CHARACTER?                 
         BL    VALGRP34            NO                                           
         CLI   0(R4),DD#ESUS                                                    
         BH    VALGRP34            NO                                           
         TM    REQSTAT,RFPVSPOF    TEST SPOOF STYLE REQUEST                     
         BZ    *+12                                                             
         TM    3(R4),X'F0'         YES - TEST GOOD ESCAPE SEQUENCE              
         BO    VALGRP34            NO                                           
         ST    R4,0(R1)                                                         
         MVI   0(R1),1                                                          
         BASR  RE,RF                                                            
         OC    1(3,R1),1(R1)       TEST ESCAPE BUFFER FULL                      
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AHI   R4,3                BUMP TO LAST CHARACTER IN SEQUENCE           
VALGRP34 BXLE  R4,R2,VALGRP32      BUMP TO NEXT CHARACTER                       
         B     VALGRP22                                                         
                                                                                
VALGRP38 STC   R0,RFPVNREQ         SET N'REQUESTS IN GROUP                      
         ICM   R0,15,NUMSYMS       NUMBER OF ESCAPE SEQUENCES                   
         STC   R0,RFPVNUMS         NUMBER OF RETURNED SYMBOLS                   
         BZ    VALGRP44            NO SYMBOLICS                                 
                                                                                
         LA    R2,ESCAPES          ESCAPE TABLE                                 
         L     R3,ARFPSYMS         SYMBOL TABLE IN RFPIOD                       
T        USING RFPVSYME,R3                                                      
                                                                                
VALGRP40 XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPSCDQ     LOOK FOR SYMBOLIC NAME ELEMENTS              
         MVC   SYM.GRPSSYMB,0(R2)                                               
         GOTOR GOMINIO,MINRD                                                    
         BE    *+6                                                              
         DC    H'0'                SYMBOLIC ELEMENT VANISHED                    
                                                                                
         L     RF,MINELEM          RF=A(MINIO ELEMENT AREA)                     
         USING GRPSD,RF                                                         
         MVC   T.RFPVSYME,0(R2)    ESCAPE SEQUENCE                              
         MVC   T.RFPVPVAL,SPACES                                                
         MVC   T.RFPVPVAL(L'GRPSPVAL),GRPSPVAL                                  
         MVC   T.RFPVCVAL,SPACES                                                
         MVC   T.RFPVCVAL(L'GRPSCVAL),GRPSCVAL                                  
         DROP  RF                                                               
                                                                                
         LA    R1,DICTLIST         FIND RULE NUMBER                             
VALGRP42 CLI   0(R1),0                                                          
         BNE   *+6                                                              
         DC    H'0'                WE DON'T KNOW ABOUT THIS SYMBOL              
         CLC   T.RFPVSYME(3),0(R1)                                              
         BE    *+12                                                             
         AHI   R1,L'DICTLIST       BUMP TO NEXT SYMBOL                          
         B     VALGRP42                                                         
         MVC   T.RFPVVRUL,4(R1)    VALIDATION RULE NUMBER                       
         CLI   T.RFPVVRUL,RRULBDAT TEST BILLING DATE ADJUSTMENT                 
         BNE   *+10                                                             
         MVC   RFPBSYMB,T.RFPVSYME YES - SET SYMBOLIC                           
                                                                                
         AHI   R2,L'ESCAPES        BUMP TO NEXT ESCAPE SEQUENCE                 
         AHI   R3,RFPVSYML         BUMP TO NEXT ENTRY IN SYMBOL TABLE           
         BCT   R0,VALGRP40                                                      
         DROP  T                                                                
                                                                                
VALGRP44 OC    RFPBSYMB,RFPBSYMB   TEST 'BILLING DATE' SYMBOLIC PRESENT         
         BZ    VALGRPX                                                          
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPBCDQ     LOOK FOR BILLING DATE ADJUSTMENTS            
         MVC   BDA.GRPBSYMB,RFPBSYMB                                            
         GOTOR GOMINIO,MINRD                                                    
         BNE   VALGRPX                                                          
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPBD,R4                                                         
         SR    R1,R1                                                            
         IC    R1,GRPBLN                                                        
         SHI   R1,GRPBLN1Q+1                                                    
         EX    R1,*+8              SET 'BILLING DATE' ADJUSTMENT VALUES         
         B     VALGRPX                                                          
         MVC   RFPBADJS(0),GRPBADJS                                             
                                                                                
VALGRPX  B     EXITSW              SWITCH BACK TO CONNECTED SYSTEM              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* SAVE A GROUP                                                        *         
***********************************************************************         
                                                                                
SAVGRP   LA    RF,RFPVDSC2+L'RFPVDSC2-1                                         
         LHI   R0,L'RFPVDSC2                                                    
         BASR  RE,0                                                             
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCTR  RF,0                                                             
         BCTR  R0,RE                                                            
         STC   R0,WORK             SAVE L'NEW EXTENDED DESCRIPTION              
                                                                                
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPHCDQ     LOOK FOR HEADING ELEMENT                     
                                                                                
         GOTOR GOMINIO,MINHI                                                    
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPHD,R4                                                         
         CLI   MINERR,MINESNF      TEST NOT FOUND (NEW GROUP)                   
         BE    SAVGRP04                                                         
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                THERE MUST BE A HEADING ELEMENT              
                                                                                
         SR    RE,RE               TEST CHANGE OF EXTENDED DESCRIPTION          
         ICM   RE,1,GRPHLN                                                      
         SHI   RE,GRPHLNQ                                                       
         CLM   RE,1,WORK           TEST OLD LENGTH=NEW LENGTH                   
         BNE   SAVGRP04                                                         
         LTR   RE,RE               TEST NO EXTENDED DESCRIPTION                 
         BZ    SAVGRP02                                                         
         BCTR  RE,0                                                             
         EX    RE,*+8              TEST OLD TEXT=NEW TEXT                       
         BNE   SAVGRP04                                                         
         CLC   GRPHDSC2(0),RFPVDSC2                                             
                                                                                
SAVGRP02 CLC   GRPHDESC,RFPVDESC   TEST CHANGE OF DESCRIPTION                   
         BNE   SAVGRP04                                                         
         CLC   GRPHFREQ,RFPVFREQ   ...FREQUENCY                                 
         BNE   SAVGRP04                                                         
         CLC   GRPHOTYP,RFPVOTYP   ...OUTPUT TYPE                               
         BNE   SAVGRP04                                                         
         CLC   GRPHDEST,RFPVDEST   ...DESTINATION                               
         BNE   SAVGRP04                                                         
         CLC   GRPHNAME,RFPVNAME   ...NAME                                      
         BNE   SAVGRP04                                                         
         CLC   GRPHNXTR,RFPVNXTR   ...NEXT RUN DATE                             
         BNE   SAVGRP04                                                         
         CLC   GRPHLSTR,RFPVLSTR   ...LAST RUN DATE                             
         BNE   SAVGRP04                                                         
         CLC   GRPHEND,RFPVENDR    ...END RUN DATE                              
         BNE   SAVGRP04                                                         
         CLC   GRPHXFIL,RFPXFILE   ...XFILE GROUP KEY                           
         BNE   SAVGRP04                                                         
         CLC   GRPHSTAT,RFPVHSTA   ...GROUP HEADER STATUS                       
         BNE   SAVGRP04                                                         
         CLC   GRPHPERS,RFPVPERS   TEST CHANGE OF OWNER                         
         BE    SAVGRP08                                                         
         OC    GRPHPERS,GRPHPERS   YES - TEST OWNER PREVIOUSLY SET              
         BZ    SAVGRP04                                                         
         MVC   GRPHPERS,RFPVPERS   YES - DON'T ALLOW CHANGE                     
                                                                                
SAVGRP04 XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPHCD,GRPHCDQ      HEADING ELEMENT                              
         MVI   GRPHLN,GRPHLNQ      ELEMENT LENGTH                               
         MVC   GRPHDESC,RFPVDESC   DESCRIPTION                                  
         MVC   GRPHFREQ,RFPVFREQ   FREQUENCY                                    
         MVC   GRPHOTYP,RFPVOTYP   OUTPUT TYPE                                  
         MVC   GRPHDEST,RFPVDEST   DESTINATION                                  
         MVC   GRPHNAME,RFPVNAME   NAME                                         
         MVC   GRPHNXTR,RFPVNXTR   NEXT RUN DATE                                
         MVC   GRPHLSTR,RFPVLSTR   LAST RUN DATE                                
         MVC   GRPHEND,RFPVENDR    END RUN DATE                                 
         MVC   GRPHXFIL,RFPXFILE   XFILE GROUP KEY                              
         CLC   GRPHXFIL,SPACES                                                  
         BH    *+10                                                             
         XC    GRPHXFIL,GRPHXFIL                                                
         MVC   GRPHSTAT,RFPVHSTA   GROUP HEADER STATUS                          
         TM    GRPHSTAT,GRPHSPVT   TEST PRIVATE GROUP                           
         BZ    *+10                                                             
         MVC   GRPHPERS,RFPVPERS   YES - SET OWNER                              
                                                                                
         SR    RE,RE                                                            
         ICM   RE,1,WORK           TEST EXTENDED DESCRIPTION                    
         BZ    SAVGRP06                                                         
         SHI   RE,1                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   GRPHDSC2(0),RFPVDSC2                                             
         AHI   RE,GRPHLNQ+1                                                     
         STC   RE,GRPHLN           SET ACTUAL ELEMENT LENGTH                    
         DROP  R4                                                               
                                                                                
SAVGRP06 LHI   R1,MINWRT                                                        
         CLI   MINERR,MINESNF      IS THIS A NEW GROUP?                         
         BNE   *+8                                                              
         LHI   R1,MINADD                                                        
         GOTOR GOMINIO,(R1)                                                     
         BE    SAVGRP08                                                         
         DC    H'0'                                                             
                                                                                
SAVGRP08 SR    R0,R0                                                            
         ICM   R0,1,RFPVNUMS       R0=N'SYMBOL ENTRIES                          
         BZ    SAVGRP14            NONE RESOLVED                                
         L     R2,ARFPSYMS         R2=A(SYMBOL TABLE)                           
         USING RFPVSYME,R2                                                      
                                                                                
SAVGRP10 XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPSCDQ     LOOK FOR SYMBOLIC ELEMENTS                   
         MVC   SYM.GRPSSYMB,RFPVSYME                                            
         GOTOR GOMINIO,MINRD                                                    
         BE    *+6                                                              
         DC    H'0'                WHERE DID THE SYMBOL GO?                     
         L     RF,MINELEM          RF=A(MINIO ELEMENT AREA)                     
         USING GRPSD,RF                                                         
         CLC   GRPSSYMB,RFPVSYME   ESCAPE SEQUENCE                              
         BNE   *+10                                                             
         CLC   GRPSPVAL,RFPVPVAL   PREVIOUS VALUE                               
         BNE   *+10                                                             
         CLC   GRPSCVAL,RFPVCVAL   CURRENT VALUE                                
         BE    SAVGRP12                                                         
                                                                                
         XC    0(MAXELLNQ,RF),0(RF) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPSCD,GRPSCDQ      SYMBOLIC NAME ELEMENT                        
         MVI   GRPSLN,GRPSLNQ      ELEMENT LENGTH                               
         MVC   GRPSSYMB,RFPVSYME   ESCAPE SEQUENCE                              
         MVC   GRPSPVAL,RFPVPVAL   PREVIOUS VALUE                               
         MVC   GRPSCVAL,RFPVCVAL   CURRENT VALUE                                
         DROP  RF                                                               
                                                                                
         GOTOR GOMINIO,MINWRT                                                   
         BE    SAVGRP12                                                         
         DC    H'0'                                                             
                                                                                
SAVGRP12 AHI   R2,RFPVSYML         BUMP TO NEXT SYMBOL                          
         BCT   R0,SAVGRP10                                                      
         DROP  R2                                                               
                                                                                
SAVGRP14 OC    RFPVNXTR,RFPVNXTR   ANY NEXT RUN DATE GIVEN?                     
         BZ    SAVGRP20            NO - GROUP IS NOT BEING SUBMITTED            
**NOOP** CLC   RFPVNXTR,SVNXTRUN   TEST CHANGE OF NEXT RUN DATE                 
**NOOP** BE    SAVGRP20                                                         
                                                                                
         TM    RFPFLAGS,RFPNOREQ   TEST NOT INTERESTED IN REQUESTS              
         BNZ   SAVGRP20                                                         
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST ELEMENTS                    
         GOTOR GOMINIO,MINHI                                                    
                                                                                
SAVGRP16 CLI   MINERR,MINEEOF      ANY MORE REQUEST CARDS?                      
         BE    SAVGRP20            NO                                           
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         CLI   GRPRCRDN,GRPRCHDQ   REQUEST HEADER?                              
         BNE   SAVGRP18                                                         
         TM    GRPRSTAT,GRPRDISQ   TEST REQUEST IS DISABLED?                    
         BNZ   SAVGRP18            YES - IGNORE IT                              
HDR      USING RQHHDR,GRPCARD                                                   
         MVC   RFPCTRL,HDR.RQHCTRL SET CONTROL BYTE FOR REQUEST                 
         DROP  HDR                                                              
                                                                                
         GOTOR BLDHDR,GRPRD        ELSE BUILD REQUEST HEADER VALUES             
                                                                                
         GOTOR GOMINIO,MINWRT      AND WRITE IT BACK                            
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         XC    MINEKEY,MINEKEY     RE-READ THE ONE WE JUST WROTE                
         MVI   MINEKEY,GRPRCDQ                                                  
         MVC   REQ.GRPRRNUM(GRPRSTAT-GRPRRNUM),GRPRRNUM                         
         GOTOR GOMINIO,MINRD                                                    
         BE    SAVGRP18                                                         
         DC    H'0'                WAS THERE JUST A SECOND AGO                  
         DROP  R4                                                               
                                                                                
SAVGRP18 GOTOR GOMINIO,MINSEQ                                                   
         B     SAVGRP16                                                         
                                                                                
SAVGRP20 XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPDCDQ     LOOK FOR DATE RANGE ELEMENT                  
         GOTOR GOMINIO,MINHI                                                    
         L     R4,MINELEM          R4=A(MINIO ELEMENT)                          
         USING GRPDD,R4                                                         
         CLI   MINERR,MINERNF      RECORD NOT FOUND                             
         BE    SAVGRP22                                                         
         CLI   MINERR,MINEEOF                                                   
         BE    SAVGRP22                                                         
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         CLC   GRPDDAY,RFPVDAYS    TEST CHANGE OF RUNDAYS                       
         BNE   SAVGRP22                                                         
         CLC   GRPDTS,RFPVRNGE     ...DATE RANGE (BIT ON->RUN)                  
         BNE   SAVGRP22                                                         
         CLC   GRPDST,RFPVNXTR     ...RANGE START RUN DATE                      
         BNE   SAVGRP22                                                         
         CLC   GRPDEN,RFPVENDR     ...RANGE END RUN DATE                        
         BE    SAVGRP28                                                         
                                                                                
SAVGRP22 XC    0(MAXELLNQ,R4),0(R4)                                             
         MVI   GRPDCD,GRPDCDQ                                                   
         MVI   GRPDLN,GRPDLNQ                                                   
         MVC   GRPDDAY,RFPVDAYS    RUNDAYS                                      
         MVC   GRPDTS,RFPVRNGE     DATE RANGE (BIT ON->RUN)                     
         MVC   GRPDST,RFPVNXTR     RANGE START RUN DATE                         
         MVC   GRPDEN,RFPVENDR     RANGE END RUN DATE                           
         OC    GRPDATA(GRPDATAL),GRPDATA                                        
         BNZ   SAVGRP24                                                         
         XC    RFPBVALS,RFPBVALS   CLEAR BILLING ADJUSTMENT VALUES              
         CLI   MINERR,0            NO DATA IN ELEMENT - TEST ADDING             
         BNE   SAVGRP28            YES - DON'T BOTHER                           
         LHI   R1,MINDEL           NO - MIGHT AS WELL DELETE IT                 
         B     SAVGRP26                                                         
                                                                                
SAVGRP24 LHI   R1,MINWRT                                                        
         CLI   MINERR,0                                                         
         BE    SAVGRP26                                                         
         LHI   R1,MINADD                                                        
                                                                                
SAVGRP26 GOTOR GOMINIO,(R1)        ADD/UPDATE/DELETE ELEMENT                    
         BE    SAVGRP28                                                         
         DC    H'0'                                                             
                                                                                
SAVGRP28 XC    MINEKEY,MINEKEY     LOOK FOR BILLING ADJUSTMENT ELEMENT          
         MVI   MINEKEY,GRPBCDQ                                                  
         GOTOR GOMINIO,MINHI                                                    
         CLI   MINERR,MINEEOF      TEST END OF FILE                             
         BNE   SAVGRP30                                                         
         OC    RFPBADJS,RFPBADJS   TEST ANY BILLING DATE ADJUSTMENTS            
         BZ    SAVGRPX             NO - ALL DONE                                
         B     SAVGRP32                                                         
                                                                                
SAVGRP30 CLI   MINERR,0            TEST FOR OTHER ERRORS                        
         BE    *+6                                                              
         DC    H'0'                                                             
         OC    RFPBADJS,RFPBADJS   TEST ANY BILLING DATE ADJUSTMENTS            
         BNZ   SAVGRP32                                                         
         GOTOR GOMINIO,MINDEL      NO - DELETE THE ADJUSTMENT ELEMENT           
         BE    SAVGRPX                                                          
         DC    H'0'                                                             
                                                                                
SAVGRP32 LA    R1,RFPBADJS+RFPBADJL-L'RFPBADJS                                  
         LHI   R0,RFPBADJM         CALCULATE NUMBER OF ADJUSTMENTS              
                                                                                
SAVGRP34 OC    0(L'RFPBADJS,R1),0(R1)                                           
         BNZ   *+14                                                             
         SHI   R1,L'RFPBADJS                                                    
         BCT   R0,SAVGRP34                                                      
         DC    H'0'                                                             
         LR    R1,R0                                                            
         MHI   R1,L'RFPBADJS                                                    
         SHI   R1,1                R1=L'ADJUSTMENT VALUES-1                     
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPBD,R4                                                         
         CLI   MINERR,MINEEOF      TEST ADJUSTMENT ELEMENT FOUND                
         BE    SAVGRP36                                                         
         CLC   GRPBSYMB,RFPBSYMB   TEST ADJUSTMENT SYMBOLIC GOOD                
         BE    *+6                                                              
         DC    H'0'                                                             
         EX    R1,*+8              TEST ADJUSTMENT VALUES CHANGED               
         BE    SAVGRPX                                                          
         CLC   GRPBADJS(0),RFPBADJS                                             
                                                                                
SAVGRP36 XC    GRPBD(MAXELLNQ),GRPBD                                            
         MVI   GRPBCD,GRPBCDQ      BUILD NEW ADJUSTMENT ELEMENT                 
         LA    R0,GRPBLN1Q+1(R1)                                                
         STC   R0,GRPBLN                                                        
         OC    GRPBSYMB,RFPBSYMB   TEST/SET SYMBOLIC                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         EX    R1,*+8              SET BILLING DATE ADJUSTMENT VALUES           
         B     *+10                                                             
         MVC   GRPBADJS(0),RFPBADJS                                             
         LHI   R1,MINADD                                                        
         CLI   MINERR,MINEEOF                                                   
         BE    *+8                                                              
         LHI   R1,MINWRT                                                        
         GOTOR GOMINIO,(R1)        ADD/UPDATE ADJUSTMENT ELEMENT                
         BE    SAVGRPX                                                          
         DC    H'0'                                                             
                                                                                
SAVGRPX  GOTOR GOMINIO,MINCLS                                                   
         BE    EXITSW                                                           
         DC    H'0'                                                             
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* ADD REQUEST TO AN RFP GROUP                                         *         
***********************************************************************         
                                                                                
ADDREQ   GOTOR DATCON,DMCB,(5,0),(15,WORK)                                      
         TM    RFPFLAGS,RFPALLOW   TEST ADD TO SUB'D GROUP ALLOWED?             
         BNZ   ADDREQ02            YES                                          
         CLC   RFPVNXTR,WORK       IS RUN DATE ON OR AFTER TODAY?               
         BL    ADDREQ02            NO                                           
         MVI   RFPERROR,RFPNOADD   YES - CANNOT ADD REQUEST                     
         B     ADDREQX                                                          
                                                                                
ADDREQ02 XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST CARD ELEMENT                
         MVC   REQ.GRPRRNUM,RFPFRQID                                            
         TM    RFPFFLAG,RFPRUNNR   RUNNER REQUESTS SUPPLY SORT FIELD            
         BNZ   *+10                                                             
         MVC   RFPFSORT,RFPVREQC                                                
         MVC   REQ.GRPRSORT,RFPFSORT                                            
         LHI   R0,1                SEQUENCE NUMBER COUNTER                      
                                                                                
ADDREQ04 STC   R0,REQ.GRPRSEQN     SEQUENCE NUMBER                              
                                                                                
         GOTOR GOMINIO,MINRD                                                    
         CLI   MINERR,MINERNF                                                   
         BNE   *+12                                                             
         STC   R0,RFPFSEQN         THE NEXT AVAILABLE SEQUENCE NUMBER           
         B     ADDREQ06                                                         
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         AHI   R0,1                TRY NEXT SEQUENCE NUMBER                     
         B     ADDREQ04                                                         
                                                                                
ADDREQ06 SR    R2,R2               COUNT REQUESTS IN GROUP                      
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST ELEMENTS                    
         GOTOR GOMINIO,MINHI                                                    
                                                                                
ADDREQ08 CLI   MINERR,MINEEOF      ANY MORE REQUEST CARDS?                      
         BE    ADDREQ10            NO                                           
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         L     R3,MINELEM          R3=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R3                                                         
         CLI   GRPRCRDN,GRPRCHDQ   REQUEST HEADER?                              
         BNE   *+8                                                              
         AHI   R2,1                YES - INCREMENT REQUEST COUNT                
         GOTOR GOMINIO,MINSEQ                                                   
         B     ADDREQ08                                                         
         DROP  R3                                                               
                                                                                
ADDREQ10 CHI   R2,RFPMAXRQ                                                      
         BL    *+12                                                             
         MVI   RFPERROR,RFPNOROO   WE'VE HIT THE MAXIMUM GROUP SIZE             
         B     ADDREQX                                                          
                                                                                
         SR    R2,R2                                                            
         ICM   R2,1,RFPFNUMR       NUMBER OF REQUEST CARDS                      
         BNZ   *+6                                                              
         DC    H'0'                NO REQUEST CARDS TO ADD                      
         SR    R3,R3               CARD NUMBER COUNTER                          
         LA    R5,RFPVREQH         A(REQUEST HEADER)                            
                                                                                
         TM    RFPFFLAG,RFPSPOOF+RFPRUNNR                                       
         BNZ   ADDREQ12                                                         
         GOTOR SETRCC              GET REQUESTOR CARD COLUMN                    
                                                                                
ADDREQ12 L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPRCD,GRPRCDQ      REQUEST CARD ELEMENT                         
         MVC   GRPRRNUM,RFPFRQID   REQUEST ID                                   
         MVC   GRPRSORT,RFPVREQC   START OF 1ST REQUEST CARD                    
         TM    RFPFFLAG,RFPRUNNR   RUNNER REQUESTS SUPPLY SORT FIELD            
         BZ    *+10                                                             
         MVC   GRPRSORT,RFPFSORT                                                
         MVC   GRPRSEQN,RFPFSEQN   SEQUENCE NUMBER                              
         STC   R3,GRPRCRDN         CARD NUMBER (0 = HEADER)                     
                                                                                
         LTR   R3,R3               IF WE ARE DOING HEADER                       
         BNZ   ADDREQ14                                                         
         MVI   GRPRLN,GRPHDRLQ     ELEMENT LENGTH                               
         MVC   GRPCARD,0(R5)                                                    
         GOTOR BLDHDR,GRPRD        BUILD REQUEST HEADER VALUES                  
         TM    RFPFFLAG,RFPSPOOF   IF A SPOOF STYLE REQUEST                     
         JZ    *+8                                                              
         OI    GRPRSTAT,GRPRSPOF   FLAG AS SPOOF STYLE REQUEST                  
         TM    RFPFFLAG,RFPRUNNR   IF A RUNNER STYLE REQUEST                    
         JZ    *+8                                                              
         OI    GRPRSTAT,GRPRRUNR   FLAG AS RUNNER STYLE REQUEST                 
         B     ADDREQ16                                                         
                                                                                
ADDREQ14 MVI   GRPRLN,GRPREQLQ     ELEMENT LENGTH                               
         CLC   GRPRCRDN,RFPVRQSN   IF REQUESTOR ON THIS CARD                    
         BNE   *+10                                                             
         MVC   GRPRRQST,RFPVRQSC   STORE REQUESTOR COLUMN NUM                   
         MVC   GRPCARD,0(R5)       REQUEST CARD                                 
                                                                                
ADDREQ16 GOTOR GOMINIO,MINADD                                                   
         BE    ADDREQ18                                                         
         CLI   MINERR,MINETOVF     TEST OVERFLOW                                
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   RFPERROR,RFPNOROO   WE'VE HIT THE MAXIMUM GROUP SIZE             
         CLM   R2,1,RFPFNUMR       TEST FIRST REQUEST CARD                      
         BNE   DELREQ                                                           
         B     ADDREQX                                                          
                                                                                
ADDREQ18 AHI   R3,1                INCREMENT CARD COUNTER                       
         AHI   R5,L'RFPVREQH       BUMP TO NEXT CARD                            
         BCT   R2,ADDREQ12                                                      
                                                                                
         XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPRCD,GRPRCDQ      REQUEST CARD ELEMENT                         
         MVI   GRPRLN,GRPDSCLQ     ELEMENT LENGTH                               
         MVC   GRPRRNUM,RFPFRQID   REQUEST ID                                   
         MVC   GRPRSORT,RFPVREQC   START OF 1ST REQUEST CARD                    
         TM    RFPFFLAG,RFPRUNNR   RUNNER REQUESTS SUPPLY SORT FIELD            
         BZ    *+10                                                             
         MVC   GRPRSORT,RFPFSORT                                                
         MVC   GRPRSEQN,RFPFSEQN   SEQUENCE NUMBER                              
         MVI   GRPRCRDN,GRPRCDSQ   REQUEST DESCRIPTION ELEMENT                  
         MVC   GRPRDSC,RFPVRDSC    REQUEST DESCRIPTION                          
         MVC   GRPRDSC2,RFPVRDS2   REQUEST DESCRIPTION EXTENSION                
         DROP  R4                                                               
                                                                                
         GOTOR GOMINIO,MINADD                                                   
         BE    ADDREQ20                                                         
         MVI   RFPERROR,RFPNOROO                                                
         CLI   MINERR,MINETOVF     TEST OVERFLOW                                
         BE    DELREQ                                                           
         DC    H'0'                                                             
                                                                                
ADDREQ20 L     R3,ARFPSYMS         SYMBOL TABLE                                 
         USING RFPVSYME,R3                                                      
         SR    R2,R2                                                            
         ICM   R2,1,RFPVNUMS       NUMBER OF SYMBOLS                            
         BZ    ADDREQ24            NO SYMBOL ELEMENTS TO ADD                    
                                                                                
         USING GRPSD,R4                                                         
ADDREQ22 XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPSCD,GRPSCDQ      SYMBOLIC NAME ELEMENT                        
         MVI   GRPSLN,GRPSLNQ      ELEMENT LENGTH                               
         MVC   GRPSSYMB,RFPVSYME   ESCAPE SEQUENCE (WITHOUT LENGTH)             
                                                                                
         GOTOR GOMINIO,MINADD                                                   
         CLI   MINERR,MINEDUP                                                   
         BE    *+14                SYMBOL IS ALREADY THERE                      
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         AHI   R3,RFPVSYML         BUMP TO NEXT CARD                            
         BCT   R2,ADDREQ22                                                      
         DROP  R3,R4                                                            
                                                                                
ADDREQ24 GOTOR GOMINIO,MINCLS                                                   
         BE    ADDREQX                                                          
         DC    H'0'                                                             
                                                                                
ADDREQX  B     EXITSW              SWITCH BACK TO CONNECTED SYSTEM              
         EJECT                                                                  
***********************************************************************         
* RETURN A REQUEST FROM GROUP                                         *         
***********************************************************************         
                                                                                
RETREQ   LA    R0,RFPVREQC                                                      
         LHI   R1,RFPVMAXC*L'RFPVREQC                                           
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         ICM   RF,8,SPACES                                                      
         MVCL  R0,RE                                                            
                                                                                
         SR    R2,R2               CARD COUNTER                                 
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST CARD ELEMENT                
         MVC   REQ.GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                         
         TM    RFPFFLAG,RFPF1STR   RETURN FIRST REQUEST?                        
         BNZ   RETREQ02                                                         
                                                                                
         MVC   REQ.GRPRRNUM(SVREQL),SVREQ                                       
         OC    SVREQRID,SVREQRID                                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         OC    SVREQSRT,SVREQSRT                                                
         BNZ   *+6                                                              
         DC    H'0'                                                             
         OC    SVREQSQN,SVREQSQN                                                
         BNZ   RETREQ02                                                         
         DC    H'0'                                                             
                                                                                
RETREQ02 L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         GOTOR GOMINIO,MINHI                                                    
         CLI   MINERR,MINEEOF      ANY REQUEST CARDS?                           
         BNE   RETREQ04            NO                                           
         TM    RFPFFLAG,RFPF1STR   RETURN FIRST REQUEST?                        
         BNZ   RETREQ30                                                         
         DC    H'0'                NO - BUT WE FOUND IT LAST TIME               
                                                                                
RETREQ04 CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         MVI   RFPVREQD,C' '       RESET DISABLED FLAG                          
         LA    R3,RFPVREQH         CARDS IN RFPIOD                              
         TM    RFPFFLAG,RFPF1STR   RETURN FIRST REQUEST?                        
         BNZ   RETREQ08            YES                                          
                                                                                
RETREQ06 GOTOR GOMINIO,MINSEQ                                                   
         CLI   MINERR,MINEEOF      ANY MORE REQUESTS?                           
         BE    RETREQ30                                                         
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
         CLC   SVREQ(SVREQL),GRPRRNUM                                           
         BE    RETREQ06            READ UNTIL WE HIT NEXT REQUEST               
                                                                                
RETREQ08 GOTOR SETRCC              GET REQUESTOR CARD COLUMN                    
         MVI   RFPVSTAT,0                                                       
                                                                                
RETREQ10 MVI   MINFILTL,L'GRPRCD+L'GRPRRNUM+L'GRPRSORT+L'GRPRSEQN               
         MVC   REQ.GRPRRNUM(GRPRCRDN-GRPRRNUM),GRPRRNUM                         
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         MVC   RFPVAGY,RFPFAGY     AGENCY                                       
         MVC   RFPVUSER,RFPFUID    USERID                                       
         MVC   RFPVSYST,RFPFSYS    SYSTEM                                       
         MVC   RFPVGRP,RFPFGRP     GROUP                                        
         MVC   RFPVRQID(L'GRPRRNUM+L'GRPRSORT+L'GRPRSEQN),GRPRRNUM              
         MVC   SVREQ(SVREQL),GRPRRNUM                                           
                                                                                
         TM    GRPRSTAT,GRPRDISQ   DISABLED?                                    
         BZ    *+8                                                              
         MVI   RFPVREQD,RFPVRDIS   YES - TELL CALLER                            
                                                                                
         CLI   GRPRCRDN,GRPRCHDQ   IF HEADER CARD                               
         BNE   RETREQ12                                                         
         MVC   RFPVSTAT,GRPRSTAT   RETURN STATUS                                
         MVC   RFPVFXID,GRPRFXID   AND FAX ID                                   
HDR      USING RQHHDR,GRPCARD                                                   
         MVC   RFPCTRL,HDR.RQHCTRL SET CONTROL BYTE FOR REQUEST                 
         DROP  HDR                                                              
                                                                                
RETREQ12 CLI   GRPRRQST,0          IF REQUESTOR ON THIS CARD                    
         BE    *+16                                                             
         MVC   RFPVRQSC,GRPRRQST   PASS BACK COLUMN NUMBER                      
         MVC   RFPVRQSN,GRPRCRDN   AND CARD NUMBER                              
                                                                                
         MVC   0(L'RFPVREQH,R3),GRPCARD                                         
         MVI   RFPRET,RFP1REC                                                   
                                                                                
         AHI   R2,1                INCREMENT CARD COUNTER                       
         AHI   R3,L'RFPVREQH       BUMP TO NEXT CARD POSITION                   
                                                                                
         GOTOR GOMINIO,MINSEQ                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
         CLI   GRPRCRDN,GRPRCDSQ   IS THIS THE DESCRIPTION ELEMENT?             
         BNE   RETREQ10                                                         
         MVC   RFPVRDSC,GRPRDSC    YES - SET DESCRIPTION                        
         MVC   RFPVRDS2,GRPRDSC2   AND DESCRIPTION EXTENSION                    
                                                                                
         GOTOR GOMINIO,MINSEQ                                                   
         CLI   MINERR,MINEEOF      ANY MORE CARDS FOR THIS REQUEST?             
         BE    *+6                 BETTER NOT BE ANY                            
         DC    H'0'                                                             
         STC   R2,RFPVNUMC         TELL CALLER HOW MANY CARDS FOUND             
         MVI   MINFILTL,1          RESTORE ELEMENT KEY FILTER LENGTH            
         B     RETREQX                                                          
                                                                                
RETREQ30 XC    SVREQ(SVREQL),SVREQ                                              
         MVI   RFPRET,RFPEOF                                                    
                                                                                
RETREQX  B     EXITSW              SWITCH BACK TO CONNECTED SYSTEM              
         DROP  R4                                                               
         EJECT                                                                  
***********************************************************************         
* UPDATE REQUEST DETAILS                                              *         
***********************************************************************         
                                                                                
CHAREQ   XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST CARD ELEMENT                
         MVC   REQ.GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                         
                                                                                
         GOTOR GOMINIO,MINRD                                                    
         BE    *+6                                                              
         DC    H'0'                IT WAS THERE BEFORE                          
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPRCD,GRPRCDQ      REQUEST CARD ELEMENT                         
         MVI   GRPRLN,GRPHDRLQ     ELEMENT LENGTH                               
         MVC   GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                             
         MVC   GRPRSTAT,RFPVSTAT   SAVE STATUS                                  
         NI    GRPRSTAT,FF-(GRPRDISQ)                                           
         CLI   RFPVREQD,RFPVRDIS   TEST/SET DISABLED?                           
         BNE   *+8                                                              
         OI    GRPRSTAT,GRPRDISQ                                                
         MVC   GRPCARD,RFPVREQH    REQUEST HEADER                               
         MVC   GRPRFXID,RFPVFXID   SET FAX ID                                   
         USING RQHHDRD,GRPCARD                                                  
         NI    RQHFLAG1,FF-(RQHFDEST+RQHFOUT)                                   
         OC    RQHDEST,RQHDEST                                                  
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFDEST   SET REQUEST ADDED WITH DEST ID               
         OC    RQHOUT,RQHOUT                                                    
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFOUT    SET REQUEST ADDED WITH OUTPUT TYPE           
         DROP  R4                                                               
                                                                                
         GOTOR GOMINIO,MINWRT                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST CARD ELEMENT                
         MVC   REQ.GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                         
         MVI   REQ.GRPRCRDN,1      FIRST REQUEST CARD                           
                                                                                
         GOTOR GOMINIO,MINRD                                                    
         BE    *+6                                                              
         DC    H'0'                IT WAS THERE BEFORE                          
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPRCD,GRPRCDQ      REQUEST CARD ELEMENT                         
         MVI   GRPRLN,GRPREQLQ     ELEMENT LENGTH                               
         MVC   GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                             
         MVI   GRPRCRDN,1          CARD NUMBER ONE                              
         CLC   GRPRCRDN,RFPVRQSN   IF REQUESTOR ON THIS CARD                    
         BNE   *+10                                                             
         MVC   GRPRRQST,RFPVRQSC   STORE REQUESTOR COLUMN NUM                   
         MVC   GRPCARD,RFPVREQC    FIRST REQUEST CARD                           
         DROP  R4                                                               
                                                                                
         GOTOR GOMINIO,MINWRT                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST CARD ELEMENT                
         MVC   REQ.GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                         
         MVI   REQ.GRPRCRDN,GRPRCDSQ DESCRIPTION ELEMENT                        
                                                                                
         GOTOR GOMINIO,MINRD                                                    
         BE    *+6                                                              
         DC    H'0'                IT WAS THERE BEFORE                          
                                                                                
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         USING GRPRD,R4                                                         
         XC    0(MAXELLNQ,R4),0(R4) CLEAR ELEMENT BUILD AREA                    
         MVI   GRPRCD,GRPRCDQ      REQUEST CARD ELEMENT                         
         MVI   GRPRLN,GRPDSCLQ     ELEMENT LENGTH                               
         MVC   GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                             
         MVI   GRPRCRDN,GRPRCDSQ   CARD NUMBER ONE                              
         MVC   GRPRDSC,RFPVRDSC    DESCRIPTION                                  
         MVC   GRPRDSC2,RFPVRDS2   DESCRIPTION EXTENSION                        
         DROP  R4                                                               
                                                                                
         GOTOR GOMINIO,MINWRT                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         GOTOR GOMINIO,MINCLS                                                   
         BE    EXITSW                                                           
         DC    H'0'                                                             
         EJECT                                                                  
***********************************************************************         
* DELETE A REQUEST                                                    *         
***********************************************************************         
                                                                                
DELREQ   SR    R0,R0               CARD COUNTER                                 
         MVI   MINFILTL,L'GRPRCD+L'GRPRRNUM+L'GRPRSORT+L'GRPRSEQN               
         L     R4,MINELEM          R4=A(MINIO ELEMENT AREA)                     
         XC    MINEKEY,MINEKEY     CLEAR ELEMENT KEY                            
         MVI   MINEKEY,GRPRCDQ     LOOK FOR REQUEST CARD ELEMENT                
         MVC   REQ.GRPRRNUM(GRPRCRDN-GRPRRNUM),RFPFRQID                         
                                                                                
         GOTOR GOMINIO,MINRD                                                    
         BE    DELREQ02                                                         
         DC    H'0'                IT WAS THERE BEFORE                          
                                                                                
DELREQ02 GOTOR GOMINIO,MINDEL                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         AHI   R0,1                NEXT CARD NUMBER FOR THIS REQUEST            
         STC   R0,REQ.GRPRCRDN                                                  
         GOTOR GOMINIO,MINHI                                                    
         CLI   MINERR,MINEEOF                                                   
         BE    DELREQ04                                                         
         CLI   MINERR,0                                                         
         BE    DELREQ02                                                         
         DC    H'0'                                                             
                                                                                
DELREQ04 GOTOR GOMINIO,MINCLS                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         MVI   MINFILTL,1          RESTORE ELEMENT KEY FILTER LENGTH            
                                                                                
DELREQX  B     EXITSW              SWITCH BACK TO CONNECTED SYSTEM              
         EJECT                                                                  
***********************************************************************         
* DELETE A GROUP                                                                
***********************************************************************         
                                                                                
DELGRP   MVI   MINFILTL,0          SO MINHIGH IS A TRUE HIGH                    
                                                                                
DELGRP02 XC    MINEKEY,MINEKEY     REMOVE ALL SYMBOL/REQUEST ELEMENTS           
         MVI   MINEKEY,GRPSCDQ                                                  
                                                                                
         GOTOR GOMINIO,MINHI                                                    
         CLI   MINERR,MINEEOF                                                   
         BE    DELGRP04                                                         
         CLI   MINERR,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         GOTOR GOMINIO,MINDEL                                                   
         BE    DELGRP02                                                         
         DC    H'0'                                                             
                                                                                
DELGRP04 GOTOR GOMINIO,MINCLS                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
                                                                                
         GOTOR GOMINIO,MINDLF                                                   
         BE    DELGRPX                                                          
         DC    H'0'                                                             
                                                                                
DELGRPX  B     EXITSW              SWITCH BACK TO CONNECTED SYSTEM              
         EJECT                                                                  
***********************************************************************         
* INTERFACE TO MINIO                                                  *         
* NTRY - R1=MINIO ACTION NUMBER                                       *         
* EXIT - CC=EQUAL IF ACTION OKAY, ELSE NOT EQUAL                      *         
***********************************************************************         
                                                                                
GOMINIO  ST    RE,SAVERE                                                        
         STC   R1,DMCB                                                          
         LA    R1,MINBLKD                                                       
         STCM  R1,7,DMCB+1                                                      
         LA    R1,DMCB                                                          
         L     RF,MINIO                                                         
         BASR  RE,RF                                                            
         CLI   MINERR,0                                                         
GOMINIOX L     RE,SAVERE                                                        
         BR    RE                                                               
                                                                                
***********************************************************************         
* SET REQUESTOR FIELD COLUMN NUMBER                                   *         
***********************************************************************         
                                                                                
SETRCC   LA    RF,RCCTAB                                                        
         LHI   R0,RCCTABN                                                       
SETRCC02 CLC   RFPFSYS,0(RF)                                                    
         BE    *+12                                                             
         AHI   RF,L'RCCTAB                                                      
         BCT   R0,SETRCC02                                                      
         MVC   RFPVRQSC,1(RF)                                                   
         BR    RE                                                               
                                                                                
RCCTAB   DS    0XL2                                                             
         DC    AL1(RFPFSAPL,RFPVRCAC)                                           
*&&US*&& DC    AL1(RFPFSSPL,RFPVRCSP)                                           
*&&US*&& DC    AL1(RFPFSRPL,RFPVRCRE)                                           
*&&US*&& DC    AL1(RFPFSPPL,RFPVRCPP)                                           
*&&US*&& DC    AL1(RFPFSNEL,RFPVRCNE)                                           
RCCTABN  EQU   (*-RCCTAB)/L'RCCTAB                                              
         DC    AL1(0,0)                                                         
         EJECT                                                                  
***********************************************************************         
* ROUTINE TO UPDATE REQUEST HEADER WITH CURRENT VALUES                *         
***********************************************************************         
                                                                                
         USING GRPRD,R1                                                         
         USING RQHHDR,GRPCARD                                                   
BLDHDR   XC    RQHHDR(L'RQHISYS+L'RQHITRM),RQHHDR                               
                                                                                
         TM    RFPFLAGS,RFPUSERH   TEST USE EXISTING HEADER                     
         BZ    *+14                                                             
         MVC   RQHHDR,RFPVREQH     YES                                          
         B     BLDHDR02                                                         
                                                                                
         L     RF,ASSB                                                          
         MVC   RQHSYSID,SSBSYSID-SSBD(RF) FACPAK SYSTEM ID                      
         L     RF,AUTL                                                          
         USING UTLD,RF                                                          
         MVC   RQHSIN,TSIN+1       SYSTEM INPUT NUMBER                          
         MVI   RQHFLAG1,RQHFNEW    SET NEW-STYLE REQUEST HEADER                 
         MVI   RQHINFO,RQHIONL     SET REQUEST ADDED ONLINE                     
         TM    TFLAG,TFLAGSEC                                                   
         BZ    *+8                                                              
         OI    RQHINFO1,RQHISEC    SET USERID REQUIRES A PASSWORD               
         TM    TSTATB,TSTATPPS                                                  
         BZ    *+8                                                              
         OI    RQHINFO1,RQHIPPS    SET AGENCY USES PPS SECURITY                 
         MVC   RQHSAGY,TAGYPER     AGENCY                                       
         OC    RQHSAGY,RQHSAGY                                                  
         BNZ   *+10                                                             
         MVC   RQHSAGY,TAGYSEC     SECURITY AGENCY                              
         MVC   RQHACCS,SAVEACCS    LIMIT ACCESS                                 
         MVC   RQHPSWD,TPASSWD     PASSWORD                                     
         MVC   RQHSYS,SAVESENO     SYSTEM SENUM                                 
         MVC   RQHCTRY,TCTRY       COUNTRY CODE                                 
         MVC   RQHLANG,TLANG       LANGUAGE CODE                                
         MVC   RQHAGCTY,TAGCTRY    AGENCY COUNTRY CODE                          
         MVC   RQHOFF,TOFFCODE     OFFICE NUMBER                                
         MVI   RQHCTRL,0           CONTROL FLAGS                                
*&&US                                                                           
         TM    RFPCTRL,RFPCOMSC    TEST IF COMSCORE REQUEST                     
         BZ    *+8                                                              
         MVI   RQHCTRL,1           YES - SET FLAG IN REQUEST CARD HDR           
*&&                                                                             
         MVC   RQHAGY,TAGY         AGENCY ALPHA                                 
         OC    RQHORIG,RQHORIG     REQUEST PROGRAM SET USER ID?                 
         BNZ   *+10                                                             
         MVC   RQHORIG,TUSER       NO - MOVE ORIGINATING USER ID NUM            
         OC    RQHORIG,RQHORIG                                                  
         BNZ   *+6                                                              
         DC    H'0'                STILL DON'T HAVE ORIGIN: WHY NOT?            
         DROP  RF                                                               
         XC    RQHPJDT,RQHPJDT                                                  
         MVC   RQHPHMS,SAVETIME    PWOS TIME (HHMMSS)                           
                                                                                
BLDHDR02 OC    GRPRFXID,GRPRFXID   TEST FAX ID PRESENT                          
         BZ    *+10                                                             
         MVC   RQHDEST,RQHORIG     YES - SET DESTINATION ID FROM ORIGIN         
         NI    RQHFLAG1,FF-(RQHFDEST+RQHFOUT)                                   
         OC    RQHDEST,RQHDEST                                                  
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFDEST   SET REQUEST ADDED WITH DEST ID               
         CLI   RFPVGRP,GRPKDTFQ    TEST DESKTOP FOLDER                          
         BNE   *+10                                                             
         MVC   RQHOUT,DOWNOUT      YES - SET OUTPUT TYPE TO 'DOWN'              
         OC    RQHOUT,RQHOUT                                                    
         BZ    *+8                                                              
         OI    RQHFLAG1,RQHFOUT    SET REQUEST ADDED WITH OUTPUT TYPE           
         BR    RE                                                               
         DROP  R1                                                               
         EJECT                                                                  
FF       EQU   X'FF'                                                            
                                                                                
MAXELLNQ EQU   250                 MAXIMUM ELEMENT SIZE                         
                                                                                
         LTORG                                                                  
                                                                                
ONEK     EQU   1024                                                             
FIVEK    EQU   5*ONEK                                                           
                                                                                
VBINSRCH DC    V(BINSRCH)                                                       
VRECUP   DC    V(RECUP)                                                         
VDMGR    DC    V(DATAMGR)                                                       
VMINIO   DC    V(MINIO)                                                         
VDATCON  DC    V(DATCON)                                                        
                                                                                
DMRDHI   DC    C'DMRDHI  '                                                      
GENDIR   DC    C'GENDIR '                                                       
GENFIL   DC    C'GENFIL '                                                       
                                                                                
DOWNOUT  DC    CL(L'RQHOUT)'DOWN'                                               
                                                                                
EFFS     DC    X'FFFFFFFF'                                                      
SPACES   DC    CL10' '                                                          
                                                                                
SYSTAB   DS    0XL2                SYSTEM KEYS FOR RFP GROUP RECORDS            
         DC    AL1(RFPFSAPL,RFPFSAPN)                                           
         DC    AL1(RFPFSCTL,RFPFSCTN)                                           
*&&US*&& DC    AL1(RFPFSSPL,RFPFSSPN)                                           
*&&US*&& DC    AL1(RFPFSNEL,RFPFSNEN)                                           
*&&US*&& DC    AL1(RFPFSPPL,RFPFSPPN)                                           
*&&US*&& DC    AL1(RFPFSTPL,RFPFSTPN)                                           
*&&US*&& DC    AL1(RFPFSRPL,RFPFSRPN)                                           
*&&US*&& DC    AL1(RFPFSSTL,RFPFSSTN)                                           
SYSTABN  EQU   (*-SYSTAB)/L'SYSTAB                                              
         ORG                                                                    
         EJECT                                                                  
WORKD    DSECT                                                                  
                                                                                
DMCB     DS    6F                                                               
                                                                                
BINPARM  DS    0F                  ** BINSRCH PARAMETER LIST **                 
         DS    2F                                                               
NUMSYMS  DS    F                                                                
         DS    2F                                                               
MAXSYMS  DS    F                   MAXIMUM NUMBER OF SYMBOLS ALLOWED            
BINPARML EQU   *-BINPARM                                                        
                                                                                
ARFPSYMS DS    A                   A(SYMBOL TABLE)                              
                                                                                
ASSB     DS    A                   A(SSB)                                       
AUTL     DS    A                   A(UTL ENTRY)                                 
                                                                                
COMFACS  DS    A                   A(COMFACS)                                   
DATAMGR  DS    A                   A(DATAMGR)                                   
GETFACT  DS    A                   A(GETFACT)                                   
MINIO    DS    A                   A(MINIO)                                     
SWITCH   DS    A                   A(SWITCH)                                    
DICTATE  DS    A                   A(DICTATE)                                   
BINSRCH  DS    A                   A(BINSRCH)                                   
DATCON   DS    A                   A(DATCON)                                    
RECUP    DS    A                   A(RECUP)                                     
                                                                                
WORK     DS    XL64                                                             
KEY      DS    XL64                                                             
KEYSAVE  DS    XL64                                                             
                                                                                
SAVERE   DS    A                   SAVED RE VALUE                               
SAVEACCS DS    CL(L'TACCS)         SAVED LIMIT ACCESS                           
SAVETIME DS    XL3                 PWOS HHMMSS                                  
SAVESENO DS    X                   SYSTEM NUMBER                                
REQSTAT  DS    XL(L'GRPRSTAT)      SAVED REQUEST HEADER STATUS                  
OVSYS    DS    X                   MAJOR SYSTEM NUMBER                          
                                                                                
SYMBOLS  DS    (RFPMAXSX)CL(L'RFPVSYMB)                                         
SYMBOLSX EQU   *-1                                                              
                                                                                
ESCAPES  DS    (RFPMAXSX)XL4       TABLE OF ESCAPE SEQUENCES                    
                                                                                
DICTLIST DS    (RFPMAXSX+1)XL5     TABLE OF ALL ESCAPES AND RULES               
                                                                                
WORKL    EQU   *-WORKD                                                          
         EJECT                                                                  
*DDCOMFACS                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
*DDDDEQUS                                                                       
         PRINT OFF                                                              
       ++INCLUDE DDDDEQUS                                                       
         PRINT ON                                                               
*FAFACTS                                                                        
         PRINT OFF                                                              
       ++INCLUDE FAFACTS                                                        
         PRINT ON                                                               
*FASYSFAC                                                                       
         PRINT OFF                                                              
       ++INCLUDE FASYSFAC                                                       
         PRINT ON                                                               
*FASSB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FASSB                                                          
         PRINT ON                                                               
*FATCB                                                                          
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
*FAUTL                                                                          
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
                                                                                
RQHHDRD  DSECT                                                                  
       ++INCLUDE DMREQHDRA                                                      
         EJECT                                                                  
       ++INCLUDE DDMINBLK                                                       
         EJECT                                                                  
       ++INCLUDE DDDICTATED                                                     
DICTATEL EQU   *-DICTATED                                                       
         EJECT                                                                  
       ++INCLUDE CTGENRFP                                                       
         EJECT                                                                  
       ++INCLUDE GEGENMSG                                                       
         EJECT                                                                  
RFPIOD   DSECT                                                                  
       ++INCLUDE GERFPIOD                                                       
                                                                                
         ORG   RFPRESRV                                                         
SVMST    DS    0X                  ** SAVED MASTER KEY VALUES **                
SVMSTUID DS    XL(L'RFPFUID)       SAVED MASTER KEY USERID NUMBER               
SVMSTAGY DS    CL(L'RFPFAGY)       SAVED MASTER KEY ALPHA AGENCY                
SVMSTSYS DS    CL(L'RFPFSYS)       SAVED MASTER KEY SYSTEM                      
SVMSTGRP DS    CL(L'RFPFGRP)       SAVED MASTER KEY GROUP                       
SVMSTL   EQU   *-SVMST                                                          
                                                                                
SVREQ    DS    0X                  ** SAVED REQUEST VALUES **                   
SVREQRID DS    XL(L'GRPRRNUM)      LAST REQUEST ID                              
SVREQSRT DS    CL(L'GRPRSORT)      LAST REQUEST SORT                            
SVREQSQN DS    XL(L'GRPRSEQN)      LAST REQUEST SEQUENCE NUMBER                 
SVREQL   EQU   *-SVREQ                                                          
                                                                                
SVNXTRUN DS    XL(L'RFPVNXTR)      SAVE GROUP NEXT RUN DATE                     
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019GERFPIO   05/18/17'                                      
         END                                                                    
