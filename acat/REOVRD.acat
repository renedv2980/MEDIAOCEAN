*          DATA SET REOVRD     AT LEVEL 007 AS OF 09/09/83                      
*CATALP OVRD                                                                    
         TITLE 'ADD OVERRIDE ELEMENTS TO REP RECORD'                            
         SPACE 1                                                                
*              PARA 1              BYTE 1-3  A(FIELD HEADER)                    
*              PARA 2              BYTE 1-3  A(RECORD)                          
*              PARA 3              BYTE 1-3  A(SCANNER)                         
*              PARA 4              BYTE 1-3  A(RECUP)                           
*                                  IF ERROR  PARA 1  BYTE 0 = 'FF'              
         SPACE 2                                                                
OVRD     CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 125,OVRD                                                         
         USING OVERD,RC                                                         
         LR    R2,R1               ADDRESS OF PARAMETER LIST                    
         SPACE 1                                                                
         MVC   SCANNER,8(R2)                                                    
         MVC   RECUP,12(R2)                                                     
         EJECT                                                                  
         L     R3,0(R2)       ADDRESS OF FIELD HEADER                           
         CLI   5(R3),0                                                          
         BE    EXIT                                                             
         GOTO1 SCANNER,DMCB,(60,(R3)),(10,BLOCK),0                              
         LA    R8,BLOCK                                                         
         SR    R6,R6                                                            
         IC    R6,DMCB+4                                                        
         LTR   R6,R6                                                            
         BZ    OVERR                                                            
         SPACE 1                                                                
OVER2    LA    R7,TYPE                                                          
         CLI   0(R7),X'FF'                                                      
         BE    OVERR               NO MATCH                                     
         CLC   12(10,R8),0(R7)                                                  
         BE    *+12                                                             
         LA    R7,11(R7)                                                        
         B     *-22                                                             
         SPACE 1                                                                
         MVI   WORK,6              ELEMENT CODE                                 
         SR    RE,RE                                                            
         IC    RE,1(R8)            LENGTH OF SECOND HALF                        
         LTR   RE,RE                                                            
         BZ    OVERR                                                            
         TM    3(R8),X'C0'         CHECK FOR LEADING BLANK                      
         BNZ   *+12                IF DATA IS NOT VALID                         
         CLI   22(R8),C' '         NUMERIC OR ALPHA.                            
         BE    OVERR               ERROR IF ONE IS FOUND                        
         SPACE 1                                                                
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   WORK+3(0),22(R8)    DATA TO ELEMENT                              
         SPACE 1                                                                
         LA    RE,4(RE)                                                         
         STC   RE,WORK+1           ELEMENT LENGTH                               
         MVC   WORK+2(1),10(R7)    TYPE TO ELEMENT                              
         SPACE 1                                                                
         L     R9,4(R2)            A(RECORD)                                    
         LA    R7,WORK             ELEMENT                                      
         MVC   HALF,27(R9)         REC LEN                                      
         LH    R5,HALF                                                          
         LA    R5,0(R5,R9)         REC END                                      
         BCTR  R5,0                FOR BXLE                                     
         SPACE 1                                                                
         SR    R4,R4                                                            
         LA    R3,34(R9)           FIRST ELEMENT                                
         CLC   0(1,R7),0(R3)       NEW ELEM CODE - REC ELEM CODE                
         BL    *+12                                                             
         IC    R4,1(R3)            ELEMENT LENGTH                               
         BXLE  R3,R4,*-14          LOOP THRU ELEMENTS                           
         SPACE 1                                                                
         GOTO1 RECUP,DMCB,(2,(R9)),(R7),(R3)      ADD ELEMENT                   
         SPACE 1                                                                
         LA    R8,82(R8)           NEXT SCAN ENTRY                              
         BCT   R6,OVER2                                                         
EXIT     XMOD1 1                                                                
OVERR    MVI   0(R2),X'FF'         ERROR                                        
         B     EXIT                                                             
         EJECT                                                                  
TYPE     DC    C'DAY       ',X'01'                                              
         DC    C'TIME      ',X'02'                                              
         DC    C'TIM       ',X'02'                                              
         DC    C'PROGRAM   ',X'03'                                              
         DC    C'PRO       ',X'03'                                              
         DC    C'DEMOS     ',X'04'                                              
         DC    C'DEM       ',X'04'                                              
         DC    C'CODES     ',X'05'                                              
         DC    C'COD       ',X'05'                                              
         DC    C'CPM       ',X'06'                                              
         DC    C'BOOK      ',X'07'                                              
         DC    X'FF'                                                            
         SPACE 2                                                                
OVERD    DSECT                                                                  
BLOCK    DS    10CL82                                                           
WORK     DS    CL100                                                            
DMCB     DS    6F                                                               
SCANNER  DS    A                                                                
RECUP    DS    A                                                                
HALF     DS    H                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'007REOVRD    09/09/83'                                      
         END                                                                    
