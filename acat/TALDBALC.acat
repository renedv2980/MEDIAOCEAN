*          DATA SET TALDBALC   AT LEVEL 035 AS OF 02/25/15                      
*CATALP TALDBALC                                                                
         TITLE 'LDBALCK - TALENT - FILE BALANCE CHECK/PRINT'                    
*        PARAMS VIA R1                                                          
*        XL1   COUNT/PRINT  X'01'=COUNT  X'FF'=PRINT                            
*        AL3   A(RECORD)                                                        
*                                                                               
         PRINT NOGEN                                                            
LDBALCK  CSECT                                                                  
         NMOD1 WRKX-WRKD,TALDBALC,R9                                            
         USING WRKD,RC                                                          
*                                                                               
         CLI   0(R1),X'FF'                                                      
         BE    PRINT                                                            
         SPACE 2                                                                
COUNT    L     R2,0(R1)            R2=A(RECORD)                                 
         LA    R3,RECTBL           R3=A(RECORD TYPE TABLE)                      
         SPACE 1                                                                
COUNT1   CLC   0(2,R3),=X'FFFF'    TEST END OF TABLE                            
         BE    COUNT1D                                                          
         CLI   0(R3),TLPUCDQ       X'24' RECORDS                                
         BNE   COUNT1A                                                          
         CLC   0(2,R2),0(R3)       TEST MATCH ON RECORD AND SUBTYPE             
         BE    COUNT2                                                           
         B     COUNT1B                                                          
                                                                                
COUNT1A  CLC   0(1,R2),0(R3)       TEST MATCH ON RECORD TYPE                    
         BE    COUNT2                                                           
                                                                                
COUNT1B  LA    R3,L'RECTBL(R3)                                                  
         B     COUNT1                                                           
         SPACE 1                                                                
COUNT1D  L     RA,=V(CPRINT)                                                    
         USING DPRINT,RA                                                        
         MVC   P(7),=C'UNKNOWN'                                                 
         MVC   P+10(32),0(R2)                                                   
         GOTO1 =V(HEXOUT),DMCB,(R2),P+60,32,=C'TOG'                             
*******  GOTO1 =V(PRINTER)                                                      
         SPACE 1                                                                
COUNT2   CLI   0(R2),TLW4NCDQ      W4 RECORD?                                   
         BNE   COUNT2B                                                          
         USING TLW4PD,R2                                                        
         TM    TLW4NSTA,TLW4NSAK   WANT TO RECORD AKA NAMES                     
         BNO   COUNT2B                                                          
         LA    R3,L'RECTBL(R3)                                                  
         SPACE 1                                                                
COUNT2B  BAS   RE,POST             POST BY RECORD TYPE                          
         LA    R3,RECTBLTT                                                      
         BAS   RE,POST             THEN POST INTO TOTALS                        
         SPACE 1                                                                
         LR    R6,R2               NOW LOOK FOR PAYMENT DETAILS                 
         MVI   ELCODE,TAPDELQ                                                   
         BAS   RE,GETEL                                                         
         BNE   EXIT                                                             
         USING TAPDD,R6                                                         
         L     R1,TAPDGRS          PICK UP GROSS                                
         CVD   R1,DUB                                                           
         AP    FILECASH,DUB        AND ADD INTO FILE TOTAL                      
         USING TLRCD,R2                                                         
         LA    R3,ACTCASH                                                       
         CLI   TLRCCD,TLCKCDQ                                                   
         BNE   *+12                                                             
         TM    TLRCSTAT,TLCKSDEL                                                
         BO    *+12                                                             
         TM    TLRCSTAT,X'C0'                                                   
         BZ    COUNT4                                                           
         LA    R3,DELCASH                                                       
         TM    TLRCSTAT,X'80'                                                   
         BO    COUNT4                                                           
         LA    R3,LPSCASH                                                       
         SPACE 1                                                                
COUNT4   AP    0(8,R3),DUB         ADD INTO SUB CASH ACCUM                      
         SPACE 1                                                                
EXIT     XMOD1 1                                                                
         SPACE 1                                                                
         GETEL (R6),DATADISP,ELCODE                                             
         SPACE 1                                                                
         EJECT                                                                  
*              ROUTINE TO POST INTO ACCUMS                                      
         SPACE 3                                                                
*                                  R2=A(RECORD)                                 
*                                  R3=A(ACCUMS)                                 
         SPACE 1                                                                
POST     NTR1                                                                   
         L     RF,20(R3)           BUMP TOTAL RECORD COUNT                      
         AHI   RF,1                                                             
         ST    RF,20(R3)                                                        
         LA    R4,24(R3)                                                        
         USING TLRCD,R2                                                         
         CLI   TLRCCD,TLCKCDQ                                                   
         BNE   *+12                                                             
         TM    TLRCSTAT,TLCKSDEL                                                
         BO    *+12                                                             
         TM    TLRCSTAT,X'C0'      TEST ACTIVE                                  
         BZ    POST2                                                            
         LA    R4,32(R3)                                                        
         TM    TLRCSTAT,X'80'      TEST DELETED                                 
         BO    POST2                                                            
         LA    R4,28(R3)                                                        
         SPACE 1                                                                
POST2    L     RF,0(R4)            BUMP ACTIVE/LAPSED/DELETED COUNT             
         AHI   RF,1                                                             
         ST    RF,0(R4)                                                         
         B     XIT                                                              
         SPACE 1                                                                
XIT      XIT1                                                                   
         EJECT                                                                  
*              ROUTINE TO PRINT OUT TOTALS AT EOJ                               
         SPACE 3                                                                
PRINT    L     RA,=V(CPRINT)       PRINT HEADING                                
         USING DPRINT,RA                                                        
         ZAP   LINE,=P'99'                                                      
         MVC   MID1,HDRL1                                                       
         MVC   MID2,ULINE                                                       
         LA    R3,RECTBL           R3=A(RECORD TABLE ENTRY)                     
*                                                                               
PRINT1   L     RF,20(R3)           TEST TOTAL RECORD COUNTER                    
         LTR   RF,RF                                                            
         BZ    PRINT3              DONT PRINT IF ZERO                           
         MVC   PLNAME,4(R3)                                                     
*&&DO                                                                           
         CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   PLRTOT,DUB1                                                      
*&&                                                                             
         EDIT  (4,20(R3)),PLRTOT,0,ZERO=NOBLANK,FILL=0                          
         LA    R4,24(R3)           R4=A(RECORD COUNTER)                         
         LA    R5,PLRACT           R5=A(PLINE DISPLAY FIELD)                    
         LA    R0,3                                                             
*                                                                               
PRINT2   L     RF,0(R4)            TEST RECORD COUNTER                          
         LTR   RF,RF                                                            
         BNZ   PRINT2A                                                          
         MVC   0(8,R5),=8C'.'      SET DOTS IF ZERO                             
         B     PRINT2B                                                          
         SPACE 1                                                                
PRINT2A  CVD   RF,DUB                                                           
         UNPK  DUB1,DUB                                                         
         OI    DUB1+7,C'0'                                                      
         MVC   0(8,R5),DUB1                                                     
PRINT2B  LA    R4,4(R4)            BUMP TO NEXT RECORD COUNTER                  
         LA    R5,9(R5)                                                         
         BCT   R0,PRINT2                                                        
PRINT2C  MVC   P,PLINE                                                          
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PRINT3   CLC   0(4,R3),=X'FFFFFFFF' XIT IF END OF TABLE                         
         BE    PRINTX                                                           
         CLC   0(2,R3),=X'FFFF'    SPACE BEFORE TOTAL                           
         BNE   PRINT4                                                           
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PRINT4   LA    R3,L'RECTBL(R3)                                                  
         B     PRINT1                                                           
*                                                                               
PRINTX   GOTO1 =V(PRINTER)         PRINT FILE TOTALS AT END                     
         MVC   P(17),=C'FILE CASH TOTAL ='                                      
         GOTO1 MYEDIT,FILECASH                                                  
         GOTO1 =V(PRINTER)                                                      
         MVC   P(17),=C'ACTIVE CASH     ='                                      
         GOTO1 MYEDIT,ACTCASH                                                   
         GOTO1 =V(PRINTER)                                                      
         MVC   P(17),=C'LAPSED CASH     ='                                      
         GOTO1 MYEDIT,LPSCASH                                                   
         GOTO1 =V(PRINTER)                                                      
         MVC   P(17),=C'DELETED CASH    ='                                      
         GOTO1 MYEDIT,DELCASH                                                   
         GOTO1 =V(PRINTER)                                                      
*                                                                               
         BAS   RE,PRNTPRG          PRINT ADDITIONAL PURGE INFORMATION           
*                                                                               
         BRAS  RE,GOSMF            PUT OUT TOTALS TO SMF RECORD                 
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*              ROUTINE TO PRINT INFO FROM TABLE IN TALDPRGE                     
         SPACE                                                                  
PRNTPRG  NTR1                                                                   
         L     R2,=V(TOTPRGS)      R2=A(TABLE OF COUNTERS)                      
         LTR   R2,R2               IF TABLE RESOLVED                            
         BZ    PRNTPRGX                                                         
*                                                                               
PRNTPRG2 CLI   0(R2),X'FF'         IF NOT END OF TABLE                          
         BE    PRNTPRG6                                                         
         CP    0(8,R2),=P'0'       AND IF COUNT                                 
         BE    PRNTPRG4                                                         
         MVC   P(28),8(R2)         PRINT MESSAGE AND COUNT                      
         EDIT  (P8,(R2)),(12,P+30),ALIGN=LEFT,COMMAS=YES                        
         GOTO1 =V(PRINTER)                                                      
PRNTPRG4 LA    R2,36(R2)           BUMP TO NEXT TABLE ENTRY                     
         B     PRNTPRG2            LOOP                                         
*                                                                               
PRNTPRG6 L     R2,=V(INVCASH)      IF INVOICE CASH RESOLVED                     
         LTR   R2,R2                                                            
         BZ    PRNTPRG8                                                         
         CP    0(8,R2),=P'0'       AND NOT ZERO                                 
         BE    PRNTPRG8                                                         
         MVC   P(28),8(R2)         PRINT MESSAGE AND COUNT                      
         EDIT  (P8,(R2)),(12,P+30),2,ALIGN=LEFT                                 
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PRNTPRG8 L     R2,=V(COMFLAG)                                                   
         CLI   0(R2),C'Y'                                                       
         BNE   PRNTPRGX                                                         
         MVC   P(38),=CL38'COMMERCIAL TABLE FULL - MORE TO DELETE'              
         GOTO1 =V(PRINTER)                                                      
*                                                                               
PRNTPRGX B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
* PUT OUT SMF OUTFILE DEBITS/CREDITS BY COMPANY                      *          
**********************************************************************          
         USING SMFBRECD,R2                                                      
GOSMF    NTR1                                                                   
         L     RE,=V(SMF)                                                       
         TM    0(RE),X'02'         SMF=YES ONLY                                 
         BZ    GOSMFXIT                                                         
*                                                                               
         ZAP   TEMPPCKD,=P'0'                                                   
         LA    R2,SMFREC                                                        
         XC    SMFREC,SMFREC                                                    
         LHI   RF,SMFBRLNQ         LENGTH OF SMF RECORD                         
         STH   RF,SMFBLEN                                                       
         MVC   SMFBDRS(6*L'SMFBMNY),=6PL8'0'                                    
         MVC   SMFBUSER,SPACES                                                  
         MVC   SMFBTXT,SPACES                                                   
         MVC   SMFBTXT1,SPACES                                                  
         MVC   SMFBTXT2,SPACES                                                  
         MVI   SMFBDSPC,C' '                                                    
         MVI   SMFBTYPE,C'M'       MONEY                                        
         MVC   SMFBSRCE,=CL4'DUMP'                                              
*                                                                               
         GOTO1 =V(DATCON),DMCB,(5,0),(15,SMFBDATE)   JULIAN 0CYYDDDF            
*                                                                               
         USING UTLD,RE                                                          
         ICM   RE,15,=V(UTL)                                                    
         BZ    *+10                                                             
         MVC   SMFBSENO,TSYS       SYSTEM NUMBER                                
*                                                                               
         USING SSBOFFD,RE                                                       
         ICM   RE,15,=V(SSB)                                                    
         BZ    *+10                                                             
         MVC   SMFBDSPC,SSODSPAC   DATA SPACE VALUE                             
         DROP  RE                                                               
*----------------------------------------------------------------------         
         L     RE,VOUTPUT          TEST IF LOADING TO DISK                      
         TM    0(RE),DISK                                                       
         BO    GOSMFLD             SAVE LOAD SPECIFIC COUNTER/MONEY             
*                                                                               
* DUMP SPECIFIC COUNTER/MONEY                                                   
*                                                                               
         LA    R3,RECTBLTT         TOTAL RECORDS TABLE ENTRY                    
         AHI   R3,20               BUMP PAST HEADER AND TEXT                    
         AHI   R3,4                BUMP PAST TOTAL RECS                         
*                                                                               
         MVI   SMFBTYPE,C'C'       COUNTER                                      
         MVC   SMFBTXT,=CL8'FIL CNTS'                                           
         ICM   R5,15,0(R3)                                                      
         CVD   R5,TEMPPCKD                                                      
         ZAP   SMFBCNT1,TEMPPCKD             TOTAL ACTIVE RECORDS               
*                                                                               
         AHI   R3,4                                                             
         ICM   R5,15,0(R3)                                                      
         CVD   R5,TEMPPCKD                                                      
         ZAP   SMFBCNT2,TEMPPCKD             TOTAL LAPSE RECORDS                
*                                                                               
         AHI   R3,4                                                             
         ICM   R5,15,0(R3)                                                      
         CVD   R5,TEMPPCKD                                                      
         ZAP   SMFBCNT3,TEMPPCKD             TOTAL DELETED RECORDS              
         BAS   RE,PUTSMF                                                        
*                                                                               
         MVI   SMFBTYPE,C'M'       MONEY                                        
         MVC   SMFBTXT,=CL8'FIL CASH'                                           
         ZAP   SMFBMNY1,ACTCASH              ACTIVE CASH                        
         ZAP   SMFBMNY2,DELCASH              DELETED CASH                       
         ZAP   SMFBMNY3,=P'0'                                                   
         BAS   RE,PUTSMF                                                        
         B     GOSMFXIT                                                         
*                                                                               
* LOAD SPECIFIC COUNTER/MONEY                                                   
*                                                                               
GOSMFLD  DS    0H                                                               
         MVC   SMFBSRCE,=CL4'LOAD'                                              
         LA    R3,RECTBLTT         TOTAL RECORDS TABLE ENTRY                    
         AHI   R3,20               BUMP PAST HEADER AND TEXT                    
         AHI   R3,4                BUMP PAST TOTAL RECS                         
*                                                                               
         MVI   SMFBTYPE,C'C'       COUNTER                                      
         MVC   SMFBTXT,=CL8'FIL CNTS'                                           
         ICM   R5,15,0(R3)                                                      
         CVD   R5,TEMPPCKD                                                      
         ZAP   SMFBCNT1,TEMPPCKD             TOTAL ACTIVE RECORDS               
*                                                                               
         AHI   R3,4                                                             
         ICM   R5,15,0(R3)                                                      
         CVD   R5,TEMPPCKD                                                      
         ZAP   SMFBCNT2,TEMPPCKD                                                
         BAS   RE,PUTSMF                     TOTAL LAPSE RECORDS                
*                                                                               
         MVI   SMFBTYPE,C'M'       MONEY                                        
         MVC   SMFBTXT,=CL8'ACT CASH'        ACTIVE CASH                        
         ZAP   SMFBMNY1,ACTCASH                                                 
         ZAP   SMFBMNY2,=P'0'                                                   
         BAS   RE,PUTSMF                                                        
*                                                                               
GOSMFXIT B     EXIT                                                             
*                                                                               
PUTSMF   NTR1                                                                   
         ICM   RF,15,=V(SMFOUT)                                                 
         BZ    XIT                                                              
         GOTO1 (RF),SMFPARM,12,SMFREC                                           
*                                                                               
         LA    R5,SMFREC                                                        
         LA    R6,SMFBRECX-SMFBRECD                                             
         GOTO1 =V(PRNTBL),DMCB,=C'SMFREC',(R5),C'DUMP',(R6),=C'1D'              
         B     XIT                                                              
                                                                                
         DROP  R2                                                               
         EJECT                                                                  
*              LOCAL EDIT ROUTINE FOR LARGE NUMBERS                             
         SPACE 1                                                                
*                                  R1=A(PL8 FIELD)                              
         SPACE 1                                                                
MYEDIT   MVC   P+18(18),=X'40202020202020202020202020214B202060'                
         ED    P+18(18),0(R1)                                                   
         LA    R0,18                                                            
MYED2    CLI   P+18,C' '                                                        
         BNE   MYEDX                                                            
         MVC   P+18(18),P+18+1                                                  
         MVI   P+18+18-1,C' '                                                   
         BCT   R0,MYED2                                                         
MYEDX    BR    RE                                                               
         EJECT                                                                  
*              CONSTANTS ETC                                                    
*                                                                               
VOUTPUT  DC    V(OUTPUT)                                                        
         SPACE 3                                                                
FILECASH DC    PL8'0'                                                           
ACTCASH  DC    PL8'0'                                                           
DELCASH  DC    PL8'0'                                                           
LPSCASH  DC    PL8'0'                                                           
DATADISP DC    H'40'                                                            
         SPACE 1                                                                
HDRL1    DC   CL132'RECORD TYPE      TOT RECS  ACTIVE   LAPSED   DELETE*        
               D'                                                               
ULINE    DC   CL132'---------------- -------- -------- -------- -------*        
               -'                                                               
         SPACE 2                                                                
PLINE    DC    CL132' '                                                         
         ORG   PLINE                                                            
PLNAME   DS    CL16                                                             
         DS    CL1                                                              
PLRTOT   DS    CL8                                                              
         DS    CL1                                                              
PLRACT   DS    CL8                                                              
         DS    CL1                                                              
PLRLAP   DS    CL8                                                              
         DS    CL1                                                              
PLRDEL   DS    CL8                                                              
         ORG                                                                    
         SPACE 2                                                                
         LTORG                                                                  
         EJECT                                                                  
                                                                                
         DS    0D                                                               
         DC    4CL8'*SMFREC*'                                                   
SMFREC   DS    XL(SMFBRECX-SMFBRECD) SMF BALANCE RECORD                         
         EJECT                                                                  
       ++INCLUDE TALDRECTB                                                      
         EJECT                                                                  
*DDDPRINT                                                                       
*TAGENFILE                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDDPRINT                                                       
       ++INCLUDE TAGENFILE                                                      
* DMLDEQUS                                                                      
       ++INCLUDE DMLDEQUS                                                       
* DDSMFFBAL                                                                     
       ++INCLUDE DDSMFFBAL                                                      
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* FASSBOFF                                                                      
SSBOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
         PRINT ON                                                               
         SPACE 3                                                                
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DUB1     DS    D                                                                
DMCB     DS    6F                                                               
WORK     DS    CL64                                                             
ELCODE   DS    XL1                                                              
SMFPARM  DS    6F                                                               
TEMPPCKD DS    PL8                                                              
WRKX     EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035TALDBALC  02/25/15'                                      
         END                                                                    
