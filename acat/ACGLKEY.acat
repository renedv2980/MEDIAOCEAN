*          DATA SET ACGLKEY    AT LEVEL 004 AS OF 03/13/12                      
*CATALP ACGLKEY                                                                 
         TITLE 'Pass back GL key(s) based on transaction and GLDELD'            
         PRINT NOGEN                                                            
***********************************************************************         
*   When passed a GLDELQ, X'63' and a transaction passes back         *         
*    the GLBRECD key and unit G transaction key                       *         
*   P1    X'80' New file                                              *         
*      -  A(TRNRECD)                                            (R2)  *         
*   P2 -  A(GLDELD)                                             (R3)  *         
*   P3 -  A(GL tranaction key)                                  (R4)  *         
*   P4 -  A(GL bucket pointer key)                              (R5)  *         
*   P5 -  XL1   Length of area                                        *         
*         XL3   A(Saved working area)                           (R6)  *         
*               Stores UL and length of 1st level or zero if n/a      *         
*   P6 -  A(COMFACS)                                            (R7)  *         
***********************************************************************         
         USING WORKD,RC            RC=A(WORK AREA)                              
         USING GLOBALS,RA                                                       
ACGLKEY  CSECT                                                                  
         NMOD1 WORKL,**GKEY**,CLEAR=YES,RR=RE                                   
         MVC   PLIST(PLISTL),0(R1)                                              
         ST    RE,RELO             Save relocation factor                       
         BASR  RA,0                                                             
         AHI   RA,GLOBALS-*                                                     
                                                                                
         USING TRNRECD,R2                                                       
         USING GLDELD,R3                                                        
GL       USING TRNRECD,R4                                                       
         USING GLBRECD,R5                                                       
         USING LV1D,R6                                                          
         LM    R2,R7,0(R1)                                                      
         MVC   HEXCPY,0(R2)        Save off hex company code                    
                                                                                
         USING COMFACSD,R7                                                      
         LTR   R7,R7                                                            
         BNZ   *+6                                                              
         DC    H'00'               Must have COMFACS                            
         MVC   DATCON,CDATCON                                                   
         MVC   DATAMGR,CDATAMGR                                                 
         DROP  R7                                                               
                                                                                
         LA    R6,0(,R6)           Clear HOB                                    
         LTR   R6,R6               Did user pass in ?                           
         BZ    ACGLK006            No                                           
         SR    RF,RF                                                            
         ICM   RF,1,AWORKA         Do we know the size ?                        
         BZ    ACGLK006            No                                           
         CLI   LV1MAX#,0           Yes. Did we initialize ?                     
         BNE   ACGLK008            Yes                                          
         CHI   RF,LV1LNQ           No. Minimum length                           
         BL    ACGLK006            To small, use temp work area                 
         LR    R1,RF                                                            
         BCTR  R1,0                                                             
         EXXC  R1,0(R6),0(R6)      Clear area to initialize                     
         SR    R0,R0                                                            
         SHI   RF,LV1HDRQ          Length less header                           
ACGLK004 CHI   RF,L'LV1ITEM        Count how many we can have                   
         BL    ACGLK008            Done                                         
         AHI   R0,1                                                             
         STC   R0,LV1MAX#          Set max # of entries                         
         SHI   RF,L'LV1ITEM                                                     
         B     ACGLK004                                                         
*                                                                               
ACGLK006 LA    R6,TEMPLIST                                                      
         XC    TEMPLIST,TEMPLIST                                                
         MVI   LV1#OF,1                                                         
*                                                                               
         USING TRNELD,R7                                                        
ACGLK008 LA    R7,TRNRFST          New file, A(TRNELD)                          
         TM    CTRLIND,CTRLNEWF    P4 (parameter passed)                        
         BO    *+8                 New file                                     
         LA    R7,TRNRECD+ACCORFST                                              
                                                                                
         USING TRSELD,R8                                                        
         LR    R8,R7                                                            
         SR    RF,RF                                                            
ACGLK010 CLI   0(R8),EOR           End of record                                
         BNE   *+6                                                              
         DC    H'00'                                                            
         CLI   0(R8),TRSELQ        X'60' status element                         
         BE    ACGLK020                                                         
         IC    RF,1(,R8)                                                        
         AR    R8,RF                                                            
         B     ACGLK010                                                         
                                                                                
ACGLK020 MVC   WORK(L'TRSPMOS),TRSPMOS                                          
         MVI   WORK+L'TRSPMOS,01                                                
*                                                                               
*        Set to last day of month                                               
*                                                                               
         GOTO1 DATCON,DMCB,(X'31',WORK),(1,WORK),(1,0)                          
                                                                                
***********************************************************************         
* Build TRNRECD, GL tranaction                                                  
***********************************************************************         
         MVC   GLBKEY,SPACES                                                    
         MVC   GL.TRNKEY,SPACES                                                 
         CLI   TRNKUNT,C'G'        General ledger                               
         JE    ACGLK300                                                         
         MVC   GL.TRNKCPY,TRNKCPY                                               
         MVC   GL.TRNKULA,GLDULA                                                
         MVC   GL.TRNKOFF,GLDOFFC                                               
         MVC   GL.TRNKCULC,GLDCNTRA                                             
         MVC   GL.TRNKDATE,WORK                                                 
         LA    RF,0                Sequence 0 if DR and 1 if CR                 
         TM    TRNSTAT,TRNSDR                                                   
         BO    *+8                                                              
         LA    RF,1                                                             
         STC   RF,GL.TRNKSBR                                                    
         LA    R9,TRSUPDT                                                       
         ICM   R9,8,=AL1(2)        Compressed date                              
         OC    TRSUPDT,TRSUPDT     Is this set yet ?                            
         BNZ   ACGLK030                                                         
         LA    R9,GLDDATE                                                       
         ICM   R9,8,=AL1(1)        Packed date                                  
         OC    GLDDATE,GLDDATE                                                  
         BNZ   ACGLK030                                                         
         SR    R9,R9                                                            
         ICM   R9,8,=AL1(5)        Today                                        
                                                                                
ACGLK030 GOTOR DATCON,DMCB,(R9),(X'20',GL.TRNKREF)                              
         DROP  R8                  TRSELD                                       
                                                                                
***********************************************************************         
* Build GLBRECD                                                                 
***********************************************************************         
         MVI   GLBKTYP,GLBKTYPQ    X'28'                                        
         MVC   GLBKCPY,TRNKCPY                                                  
         MVC   GLBKGLA,GLDLDG      Ledger/Acount                                
         MVC   GLBKSOFF,SPACES                                                  
         MVC   GLBKGOFF,GLDOFFC    Office code supplied based on prof           
*                                                                               
         CLI   TRNTYPE,X'51'      SPECIAL TYPE 81?                              
         BNE   *+10                                                             
         MVC   GLBKSCA+1(6),=C'RTEARN'                                          
*                                                                               
         CLI   TRNKLDG,C'J'        Production ?                                 
         JE    ACGLK100            Yes                                          
         MVC   GLBKSOFF,TRNOFFC    Use office from element                      
         MVC   GLBKSULA,TRNKUNT    Unit ledger account unit S                   
         CLI   TRNKLDG,C'R'        Recievables                                  
*&&US*&& JNE   ACGLK050            No                                           
*&&UK*&& JNE   ACGLK056            No                                           
***********************************************************************         
* Process recievables                                                           
***********************************************************************         
         LR    RF,R7               R7 = A(TRNELD)                               
ACGLK042 CLI   0(RF),EOR           End of record                                
         BE    ACGLKXIT            Done                                         
         CLI   0(RF),MDTELQ        X'1A', full word version                     
         BE    ACGLK043                                                         
         CLI   0(RF),MDPELQ        X'6A', packed version                        
         BE    ACGLK044                                                         
         LLC   R1,1(,RF)                                                        
         AR    RF,R1                                                            
         J     ACGLK042                                                         
                                                                                
         USING MDTELD,RF                                                        
ACGLK043 LA    RE,MDTSYS           System media                                 
         TM    MDTSTAT,MDTSMIQ     MI record ?                                  
         JZ    ACGLK046                                                         
         CLC   MDTMED2,SPACES                                                   
         JNH   ACGLK046                                                         
         LA    RE,MDTMED2          MI record                                    
         J     ACGLK046                                                         
                                                                                
         USING MDPELD,RF                                                        
ACGLK044 LA    RE,MDPSYS           System media                                 
         TM    MDPSTAT,MDPSMIQ     MI record ?                                  
         JZ    ACGLK046                                                         
         CLC   MDPMED2,SPACES                                                   
         JNH   ACGLK046                                                         
         LA    RE,MDPMED2          MI record                                    
                                                                                
ACGLK046 MVC   GLBKSCA(2),0(RE)    Save off system/media                        
         J     ACGLKXIT                                                         
         DROP  RF                                                               
*&&US                                                                           
***********************************************************************         
* Process media payables                                                        
***********************************************************************         
ACGLK050 BRAS  RE,ISCLRNCE         Is clearance releated transaction            
         JNE   ACGLK056            No                                           
         MVC   GLBKSCNT,SPACES           Reset to spaces                        
         MVC   GLBKSCA(3),TRNKCCPY+12    Set to client                          
         CLC   TRNKCCPY+12(3),SPACES     Was there a client ?                   
         JE    ACGLK052                  No, so check SC for CK or CR           
         CLC   TRNKCCPY(12),SPACES See if true contra                           
         JE    ACGLKXIT            Yes, client is true contra                   
         MVC   GLBKSCUL,=X'4141'   Has client & rep/station, not true           
         J     ACGLKXIT                                                         
                                                                                
ACGLK052 CLI   TRNKCLDG,C'/'       Is media/cli                                 
         BNE   ACGLKXIT                                                         
         MVC   GLBKSCU(5),TRNKCUNT Yes, so save                                 
         B     ACGLKXIT                                                         
*&&                                                                             
***********************************************************************         
* Process everything else                                                       
***********************************************************************         
ACGLK056 GOTOR GETLV1,DMCB,TRNKCUNT,0                                           
         SR    R1,R1                                                            
         ICM   R1,1,DMCB           Get len of 1st level of contra               
         BZ    ACGLKXIT            Not value contra u/l                         
         SHI   R1,1                Addjust for EX instr.                        
         EXMVC R1,GLBKSCU,TRNKCUNT                                              
         J     ACGLKXIT                                                         
                                                                                
***********************************************************************         
* Production transaction                                                        
***********************************************************************         
ACGLK100 MVC   GLBKMEDN,GLDCACT    Media name                                   
         SR    R9,R9                                                            
         ICM   R9,1,LV1SJLV2       Client / Product length (P4+3)               
         BNZ   ACGLK110                                                         
         GOTOR GETLV1,DMCB,TRNKUNT,0      Get SJ info                           
         ICM   R9,1,LV1SJLV2                                                    
         BNZ   *+6                                                              
         DC    H'00'               Something is amiss                           
*                                                                               
ACGLK110 BCTR  R9,0                                                             
         EXMVC R9,GLBKCP,TRNKACT   Move in cli/prod                             
         AHI   R9,1                                                             
         LA    RE,TRNKACT(R9)      RE = Point to media code                     
         MVC   GLBKMEDC,0(RE)      Media code                                   
         J     ACGLKXIT                                                         
         EJECT ,                                                                
***********************************************************************         
* Post of unit G other than type 25                                             
***********************************************************************         
ACGLK300 MVC   GL.TRNKCULA,TRNKCULA                                             
         MVC   GL.TRNKOFF,TRNKOFF                                               
         MVC   GL.TRNKCULC,TRNKCULC                                             
         MVC   GL.TRNKDATE,TRNKDATE                                             
         MVC   GL.TRNKREF,TRNKREF                                               
         MVI   GL.TRNKSBR,0                                                     
                                                                                
         MVI   GLBKTYP,GLBKTYPQ    X'28'                                        
         MVC   GLBKCPY,TRNKCPY                                                  
         MVC   GLBKGLA,TRNKLDG    Ledger/Acount                                 
*        MVC   GLBKGOFF,TRNKOFF                                                 
         MVC   GLBKSOFF,SPACES                                                  
         MVC   GLBKSULA,TRNKULC                                                 
         MVC   GLBKGOFF,TRNOFFC                                                 
                                                                                
ACGLKXIT XIT1                                                                   
         DROP  GL                  TRNRECD (return key)                         
         DROP  R3,R5               GLDELD, GLDRECD                              
         EJECT ,                                                                
***********************************************************************         
* See if tranaction is clearance related                                        
***********************************************************************         
ISCLRNCE NTR1                                                                   
         CLI   TRNKLDG,C'C'        Cash side of clearance                       
         JE    ISCLR10                                                          
         CLI   TRNKLDG,C'S'        Spot / Net                                   
         JE    ISCLR10                                                          
         CLI   TRNKLDG,C'T'        Spot / Net                                   
         JE    ISCLR10                                                          
         CLI   TRNKLDG,C'U'        Net                                          
         JE    ISCLR10                                                          
         CLI   TRNKLDG,C'P'        Print                                        
         JE    ISCLR20                                                          
         CLI   TRNKLDG,C'Q'        Print                                        
         JE    ISCLR20                                                          
         J     ISCLR_NO            No                                           
                                                                                
ISCLR10  CLI   TRNTYPE,33          Clearance                                    
         JE    ISCLR50                                                          
         CLI   TRNTYPE,34          Clearance                                    
         JE    ISCLR50                                                          
         CLI   TRNKLDG,C'C'        Cash side of clearance                       
         JE    ISCLR20                                                          
         CLI   TRNTYPE,129         Check                                        
         JE    ISCLR_YS                                                         
         J     ISCLR30                                                          
                                                                                
ISCLR20  CLI   TRNTYPE,49          Clearance                                    
         JE    ISCLR50                                                          
         CLI   TRNTYPE,50          Clearance                                    
         JE    ISCLR50                                                          
         CLI   TRNKLDG,C'C'        Cash side of clearance                       
         JE    ISCLR30                                                          
         CLI   TRNTYPE,129         Check                                        
         JE    ISCLR_YS                                                         
                                                                                
ISCLR30  CLI   TRNTYPE,13          Online (Input) Clearance                     
         JE    ISCLR_YS                                                         
         CLI   TRNTYPE,37          Void                                         
         JE    ISCLR60                                                          
         CLI   TRNTYPE,36          Manual check                                 
         JNE   ISCLR_NO                                                         
         J     ISCLR60                                                          
                                                                                
ISCLR50  LR    RE,R7               R7 = TRNEL in record                         
ISCLR52  CLI   TRNKLDG,C'C'        Cash                                         
         BE    ISCLR60                                                          
         CLI   0(RE),EOR                                                        
         JE    ISCLR_NO                                                         
         CLI   0(RE),XPYELQ        X'46' extra payment, clearance               
         JE    ISCLR_YS            At this point I say it is                    
         LLC   RF,1(,RE)                                                        
         AR    RE,RF                                                            
         J     ISCLR52                                                          
                                                                                
ISCLR60  CLI   TRNKCLDG,C'/'       Special case                                 
         JE    ISCLR_YS                                                         
         CLC   TRNKCACT+9(3),SPACES                                             
         JNH   ISCLR_NO                                                         
         CLC   TRNKCCPY(12),SPACES                                              
         JE    ISCLR_YS                                                         
         CLI   TRNKCUNT,C'S'       See if unit S                                
         JE    ISCLR_NO            Yes, so not clearance                        
                                                                                
ISCLR_YS SR    RE,RE                                                            
ISCLR_NO LTR   RE,RE                                                            
         J     ACGLKXIT                                                         
         DROP  R2,R7               TRNRECD, TRNELD                              
         EJECT ,                                                                
***********************************************************************         
* Get first level of unit/ledger                                                
*           R6 = A(Saved list area)                          USING LV1D         
*     P1 -  R3 = Unit/ledger to find                                            
*     P2 -  R4 = A(Ledger record)                            optional           
***********************************************************************         
GETLV1   NTR1                                                                   
         LR    R7,R1               Save R1 in R7                                
         LM    R3,R4,0(R1)                                                      
         SR    R5,R5               R5 = zero for none found                     
                                                                                
         LA    R8,LV1ITEM          Point to first item entry                    
         USING LV1ITEM,R8          Off of R6                                    
                                                                                
         SR    R0,R0                                                            
         ICM   R0,1,LV1#OF         Number of entries                            
         JZ    GETLV100                                                         
                                                                                
GETLV040 CLC   LV1UL,0(R3)         Match on unit/ledger                         
         JE    GETLV300                                                         
         LA    R8,L'LV1ITEM(,R8)   Next entry                                   
         BRCT  R0,GETLV040                                                      
                                                                                
         USING LDGRECD,R4                                                       
GETLV100 LTR   R4,R4               Ledger record passed                         
         BNZ   GETLV200            Yes, extract value and save                  
         LA    R4,IOKEY                                                         
         MVC   LDGKEY,SPACES                                                    
         MVC   LDGKCPY,HEXCPY                                                   
         MVC   LDGKUNT(2),0(R3)                                                 
         GOTOR DATAMGR,DMCB,DMREAD,ACCDIR,IOKEY,IOA,IOWRK                       
         BNE   GETLVXIT                                                         
         LA    R4,IOA                                                           
         GOTOR DATAMGR,DMCB,GETREC,ACCMST,LDGKDA,IOA,IOWRK                      
         BE    *+6                                                              
         DC    H'00'                                                            
         LA    R2,IOA+(LDGRFST-LDGRECD)                                         
         B     GETLV210                                                         
*                                                                               
         USING ACLELD,R2                                                        
GETLV200 AHI   R2,ACCORFST         Displacement to 1st elem. old file           
         TM    CTRLIND,CTRLNEWF                                                 
         JZ    *+8                                                              
         AHI   R2,(ACCRFST-ACCRECD)-ACCORFST                                    
                                                                                
GETLV210 CLI   0(R2),EOR           End of record                                
         BE    GETLVXIT            Should never happen                          
         CLI   0(R2),ACLELQ        X'16' Account lengths                        
         BE    GETLV220                                                         
         LLC   RF,1(,R2)                                                        
         AR    R2,RF                                                            
         B     GETLV210                                                         
                                                                                
GETLV220 LLC   RF,LV1#OF           Number of entries                            
         AHI   RF,1                Add one                                      
         CLC   LV1#OF,LV1MAX#      Reached limit ?                              
         BNL   GETLV240            Yes, need to increase LV1MAX#                
         STC   RF,LV1#OF           Increase value                               
         MVC   LV1UL,0(R3)         Set unit ledger                              
         MVI   LV1LEN,2            Set to for unit/ledger                       
                                                                                
GETLV240 CLI   ACLELLVA,L'GLBKSCA                                               
         BH    GETLV300            Can't do it, 1st level too big               
         LLC   RF,ACLELLVA         Get 1st level                                
         AHI   RF,2                Adjust for unit ledger                       
         STC   RF,LV1LEN           Set length                                   
         CLC   =C'SJ',0(R3)        Production ?                                 
         BNE   GETLV300            No                                           
         MVC   LV1SJLV2,ACLELLVB   Set level B for SJ only                      
                                                                                
GETLV300 IC    R5,LV1LEN           Return length of 1st level                   
         DROP  R2                                                               
                                                                                
GETLVXIT STC   R5,0(,R7)           Return value                                 
         J     ACGLKXIT                                                         
         DROP  R6,R8               LV1D, LV1ITEM                                
         DROP  RA,RB                                                            
         EJECT ,                                                                
***********************************************************************         
* GLOBAL values                                                                 
***********************************************************************         
GLOBALS  DS    0D                                                               
SPACES   DC    CL98' '                                                          
DMREAD   DC    CL8'DMREAD'                                                      
GETREC   DC    CL8'GETREC'                                                      
ACCMST   DC    CL8'ACCMST'                                                      
ACCDIR   DC    CL8'ACCDIR'                                                      
         LTORG                                                                  
         EJECT ,                                                                
***********************************************************************         
* EQUATES                                                                       
***********************************************************************         
TURNOFF  EQU   X'FF'                                                            
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
EOT      EQU   X'FF'                                                            
EOR      EQU   0                                                                
K        EQU   1024                                                             
***********************************************************************         
* LOCAL WORK DSECT                                                              
***********************************************************************         
WORKD    DSECT                                                                  
PLIST    DS    0X                                                               
ATRANREC DS    A                                                                
         ORG   ATRANREC                                                         
CTRLIND  DS    XL1                                                              
CTRLNEWF EQU   X'80'               New file                                     
         DS    AL3                                                              
AGLDEL   DS    A                                                                
AGLTKEY  DS    A                                                                
AGLBKEY  DS    A                                                                
AWORKA   DS    A                                                                
         ORG   AWORKA                                                           
AWORKLN  DS    AL1                                                              
         DS    AL3                                                              
ACOMFACS DS    A                                                                
PLISTL   EQU   *-PLIST                                                          
                                                                                
DATAMGR  DS    V                                                                
DATCON   DS    V                                                                
RELO     DS    A                                                                
DUB      DS    D                                                                
WORK     DS    XL256                                                            
DMCB     DS    6F                                                               
FULL     DS    F                                                                
HALF     DS    H                                                                
BYTE     DS    X                                                                
                                                                                
HEXCPY   DS    XL1                                                              
TEMPLIST DS    XL6                                                              
                                                                                
IOKEY    DS    CL(LDGRFST-LDGRECD)                                              
IOWRK    DS    CL96                                                             
IOA      DS    XL(2*K)                                                          
WORKL    EQU   *-WORKD                                                          
         EJECT ,                                                                
LV1D     DSECT                                                                  
LV1#OF   DS    X                   Number of unit/ledgers stored                
LV1MAX#  DS    X                   Max number can store                         
LV1SJLV2 DS    X                   Special for SJ, client/prod length           
LV1HDRQ  EQU   *-LV1D                                                           
LV1ITEM  DS    0CL3                                                             
LV1UL    DS    CL2                 Save Unit/Ledger                             
LV1LEN   DS    AL1                 Length of level 1                            
LV1LNQ   EQU   *-LV1D                                                           
         EJECT ,                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'004ACGLKEY   03/13/12'                                      
         END                                                                    
