*          DATA SET REREPRDWNS AT LEVEL 019 AS OF 05/01/02                      
*CATALP RERDWNA                                                                 
         TITLE 'RERDWN - REREPRDWN - REP OFFLINE DATA DOWNLOAD DRIVER'          
*                                                                               
********************************************************************            
*                                                                  *            
*        REDOWN --- PERFORM DOWNLOAD DATA FUNCTIONS                *            
*                                                                  *            
* CATALOG DATA SET = RERDWN(A)                                     *            
*                                                                  *            
* ---------------------------------------------------------------- *            
* UPDATE HISTORY:                                                  *            
*                                                                  *            
* MAY18/90 (MRR) - INITIAL                                         *            
* JUL11/90          FINISH UP INITIAL VERSION                      *            
*                                                                  *            
* JUL26/90 (MRR) - IF REPORT DOES NOT YET KNOW ABOUT DOWNLOADING,  *            
*                   DON'T DUMP                                     *            
*                                                                  *            
* JAN31/91 (MRR) --- NEAT-O, NIFFTY BUG WHEN LOADING THE CONTROL   *            
*                     BLOCK WITH THE DATA ITEM                     *            
*                                                                  *            
* APR08/97 (BU ) --- FIX SPACE-FILLED FIELD PROBLEM W/RVAN.        *            
*                                                                  *            
* APR09/97 (RHV) --- FIX 1-BYTE EMPTY TEXT FIELD BUG               *            
*                                                                  *            
*                                                                  *            
********************************************************************            
*                                                                               
*        P1     =    A(WORKD)                                                   
*        P2     =    A(COMMAND)                                                 
*                       'START' = START OF REPORT                               
*                       'PRINT' = PRINT LINE                                    
*                       'END'   = END OF REPORT                                 
*                                                                               
********************************************************************            
*                                                                               
         PRINT NOGEN                                                            
RERDWN   CSECT                                                                  
         NMOD1 200,**RDWN**,RR=R2,CLEAR=YES                                     
         USING WORKAREA,RC                                                      
         L     RA,0(R1)                                                         
         USING WORKD,RA                                                         
*                                                                               
         L     RF,=V(PRINT)                                                     
         ST    RF,VPRINT                                                        
*                                                                               
         MVC   DOWNBLK-8(8),=C'RDWNBLK*'                                        
         XC    DOWNBLK(L'DOWNBLK),DOWNBLK                                       
*                                                                               
         MVC   PRNTLN-8(8),=C'RDWNLINE'                                         
         MVI   PRNTLN+0,X'00'      ZERO PRINT LINE CONTROL                      
         MVI   PRNTLN+1,C' '       BLANK THE INTERNAL LINE                      
         MVC   PRNTLN+2(L'PRNTLN-2),PRNTLN+1                                    
*                                                                               
         L     R2,4(R1)            POINT TO COMMAND                             
         CLC   0(5,R2),=C'START'   START OF REPORT                              
         BNE   MAIN10                                                           
         GOTO1 DODOWN,DMCB,0                                                    
         B     DOWNGOOD                                                         
*                                                                               
MAIN10   EQU   *                                                                
         CLC   0(3,R2),=C'END'                                                  
         BNE   MAIN20                                                           
         L     RF,=V(DOWNDONE)                                                  
         CLI   0(RF),0             ANY OUTPUT?                                  
         BNE   MAIN15                                                           
         MVI   PRNTLN+0,X'0C'      NOPE, SO WE NEED A FORM FEED                 
         GOTO1 VPRINT,DMCB,PRNTLN,=C'BC01'                                      
         MVI   PRNTLN+0,0                                                       
         GOTO1 DODOWN,DMCB,4       AND THEN SAY NO DATA                         
MAIN15   EQU   *                                                                
         GOTO1 DODOWN,DMCB,5                                                    
         B     DOWNGOOD                                                         
MAIN20   EQU   *                                                                
         L     R2,=V(DOWNDEF)                                                   
         OC    0(2,R2),0(R2)       AN EMPTY LINE DEF MEANS DON'T PRINT          
         BZ    DOWNGOOD                                                         
*                                                                               
         OC    P(132),PRNTLN+1     CLEAR OUT NULLS                              
         CLC   P(132),PRNTLN+1     BY-PASS BLANK LINES                          
         BE    DOWNGOOD                                                         
*                                                                               
         L     RF,=V(DOWNDONE)                                                  
         CLI   0(RF),0             ANY OUTPUT YET?                              
         BNE   MAIN40                                                           
         MVI   PRNTLN+0,X'0C'      NOPE, SO WE NEED A FORM FEED                 
         GOTO1 VPRINT,DMCB,PRNTLN,=C'BC01'                                      
         MVI   PRNTLN+0,0                                                       
MAIN40   EQU   *                                                                
         LA    R5,P                POINT TO THE START OF OUTPUT                 
         LA    R7,1                                                             
*                                                                               
MAIN50   EQU   *                                                                
         OC    0(2,R2),0(R2)                                                    
         BZ    MAIN100                                                          
         ZIC   R6,1(R2)            GET LENGTH                                   
*                                                                               
         CLI   0(R2),C'T'          TEXT?                                        
         BNE   MAIN60              NO                                           
         GOTO1 DODOWN,DMCB,1                                                    
         B     MAIN90                                                           
MAIN60   EQU   *                                                                
         CLI   0(R2),C'N'          NUMERIC?                                     
         BNE   MAIN70              NO                                           
         GOTO1 DODOWN,DMCB,2                                                    
         B     MAIN90                                                           
MAIN70   EQU   *                                                                
         CLI   0(R2),C'O'          OFFSET?                                      
         BE    MAIN90                                                           
         DC    H'0'                                                             
MAIN90   EQU   *                                                                
         AR    R5,R6               POINT TO THE NEXT FIELD                      
         LA    R2,2(R2)            GET NEXT FIELD DEF                           
         B     MAIN50              LOOP                                         
*                                                                               
MAIN100  EQU   *                                                                
         GOTO1 DODOWN,DMCB,3       END-OF-LINE                                  
         MVI   P,C' '                                                           
         MVC   P+1(131),P                                                       
         B     DOWNGOOD                                                         
         EJECT                                                                  
*                                                                               
*        TRANSLATE AND PRINT ROUTINE                                            
* ---------------------------                                                   
*                                                                               
*        I/P - R5 CONTAINS ADDRESS OF PRINT LINE FIELD                          
*              R6 CONTAINS LENGTH OF PRINT LINE FIELD                           
*                                                                               
*        COMMAND IN P1:                                                         
*                                                                               
*                    0 = START OF REPORT                                        
*                    1 = PRINT TEXT FIELD                                       
*                    2 = PRINT NUMERIC FIELD                                    
*                    3 = END OF LINE                                            
*                    4 = 'NO DATA' LINE                                         
*                    5 = END OF REPORT                                          
*                                                                               
*                                                                               
DODOWN   NTR1                                                                   
*                                                                               
         LA    R4,DOWNBLK          SET ADDR OF THE CONTROL BLOCK                
         USING DLCBD,R4                                                         
         LA    R2,PRNTLN+1                                                      
         ST    R2,DLCBAPL          PRINT LINE                                   
         LA    R2,DOWNHOOK                                                      
         ST    R2,DLCBAPR          PRINT ROUTINE                                
*                                                                               
         LA    RE,DOWNTAB                                                       
DODOWN10 EQU   *                                                                
         OC    0(4,RE),0(RE)                                                    
         BNZ   *+6                                                              
         DC    H'0'                                                             
         CLC   0(1,RE),3(R1)       COMPARE ACTION                               
         BE    DODOWN20                                                         
         LA    RE,4(RE)                                                         
         B     DODOWN10                                                         
DODOWN20 EQU   *                                                                
         ZICM  RF,1(RE),3                                                       
         BR    (RF)                                                             
         SPACE 3                                                                
*                                                                               
*        SUBORDINATE ACTION TRANSFER TABLE                                      
*                                                                               
DOWNTAB  EQU   *                                                                
         DC    AL1(0),AL3(DOWN1ST) START OF REPORT                              
         DC    AL1(1),AL3(DOWNTXT) OUTPUT TEXT                                  
         DC    AL1(2),AL3(DOWNNUM) OUTPUT NUMERIC                               
         DC    AL1(3),AL3(DOWNEOL) MARK END OF LINE                             
         DC    AL1(4),AL3(DOWNNUL) EMPTY LINE                                   
         DC    AL1(5),AL3(DOWNEND) END OF THE REPORT                            
         DC    X'00000000'                                                      
         DS    0H                                                               
         EJECT                                                                  
*                                                                               
*   DOWN1ST - START OF REPORT                                                   
*                                                                               
DOWN1ST  EQU   *                                                                
         MVI   DLCBACT,DLCBSOR     ACTION (START OF REPORT)                     
         B     DLINK                                                            
*                                                                               
*   DOWNTXT - TEXT OUTPUT                                                       
*                                                                               
DOWNTXT  EQU   *                                                                
         MVI   DLCBTYP,DLCBTXT     TYPE (TEXT)                                  
         B     DOWNCOM                                                          
*                                                                               
*   DOWNNUM - NUMERIC OUTPUT                                                    
*                                                                               
DOWNNUM  EQU   *                                                                
         MVI   DLCBTYP,DLCBNUM     TYPE (NUMERIC)                               
         B     DOWNCOM                                                          
*                                                                               
*   DOWNEOL - END OF LINE                                                       
*                                                                               
DOWNEOL  EQU   *                                                                
         MVI   DLCBACT,DLCBEOL     ACTION (END OF PRINT LINE)                   
         LR    R1,R4                                                            
         L     RF,=V(DLFLD)                                                     
         BASR  RE,RF                                                            
         MVI   PRNTLN+1,X'40'                                                   
         MVC   PRNTLN+2(L'PRNTLN-2),PRNTLN+1                                    
         B     DOWNGOOD                                                         
*                                                                               
*   DOWNNUL - NO DATA LINE                                                      
*                                                                               
DOWNNUL  EQU   *                                                                
         MVC   DLCBFLD(7),=C'NO DATA'                                           
         MVI   DLCBLEN,7                                                        
         MVI   DLCBACT,DLCBPUT     ACTION (PUT)                                 
         MVI   DLCBTYP,DLCBTXT     TYPE (TEXT)                                  
         LR    R1,R4                                                            
         L     RF,=V(DLFLD)                                                     
         BASR  RE,RF                                                            
         B     DOWNEOL             ISSUE END-OF-LINE                            
*                                                                               
*   DOWNEND - END OF REPORT                                                     
*                                                                               
DOWNEND  MVI   DLCBACT,DLCBEOR     ACTION (END OF REPORT)                       
         B     DLINK                                                            
*                                                                               
*   DOWNCOM - COMMON FIELD OUTPUT                                               
*                                                                               
DOWNR6   DS    F                   SAVE OFF R6                                  
DOWNCOM  EQU   *                                                                
         MVI   DLCBACT,DLCBPUT     ACTION (PUT)                                 
         CLI   0(R5),C' '                                                       
         BNE   DCOM10                                                           
         BCTR  R6,0                                                             
*                                                                               
         LTR   R6,R6               IF R6=0 THEN THIS IS ONLY A 1 CHAR           
         BZ    DCOM05              FIELD AND IS EMPTY                           
*                                                                               
*   ORIGINAL CODE TREATING A SPACE/NULL-FILLED FIELD WITH DATA                  
*        FOLLOWING IT WAS PRODUCING A DUMP DUE TO WRAPAROUND.                   
*        THE VALUE OF R6 IS SAVED AND RELOADED AFTER THE WRAPAROUND             
*        FIX, AS ITS VALUE WAS ACCEPTABLE IN ALL OTHER AREAS OF CODE,           
*        AND THIS WAS THE MOST EXPEDITIOUS MANNER TO DEAL WITH IT, IF           
*        NOT THE MOST ELEGANT.   BILL UHR (APR97).                              
*                                                                               
         ST    R6,DOWNR6           SAVE OFF R6                                  
         BCTR  R6,0                BACK UP ANOTHER SO WE DON'T RUN              
*                                  INTO NEXT FIELD                              
         EX    R6,DOWNCOM1                                                      
         L     R6,DOWNR6           RESET R6                                     
         B     *+10                                                             
DOWNCOM1 CLC   0(0,R5),1(R5)       TEST IF FIELD IS ALL SPACES                  
         BNE   DCOM10                                                           
DCOM05   LA    R6,1                TRANSMIT MINIMUM LENGTH FIELD                
         CLI   DLCBTYP,DLCBNUM     TEST TYPE (NUMERIC)                          
         BNE   DCOM40              TRANSMIT ONE SPACE (TEXT)                    
         MVI   0(R5),C'0'                                                       
         B     DCOM40              TRANSMIT ONE ZERO (NUMERIC)                  
*                                                                               
DCOM10   EQU   *                                                                
         LA    R7,0(R6,R5)         R7 - LAST BYTE OF FIELD                      
         BCTR  R7,0                                                             
DCOM20   EQU   *                                                                
         CLI   0(R7),C' '          SCAN FOR TRAILING SPACES                     
         BNE   DCOM30                                                           
         BCTR  R7,0                                                             
         BCT   R6,DCOM20                                                        
         DC    H'0'                                                             
*                                                                               
DCOM30   EQU   *                                                                
         CLI   DLCBTYP,DLCBTXT     TEST TYPE (TEXT)                             
         BE    DCOM40                                                           
         CLI   0(R5),C' '          SCAN FOR LEADING SPACES IN NUMERIC           
         BNE   DCOM40                                                           
         LA    R5,1(R5)                                                         
         BCT   R6,DCOM30                                                        
         DC    H'0'                                                             
DCOM40   EQU   *                                                                
         STC   R6,DLCBLEN                                                       
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         MVC   DLCBFLD(0),0(R5)    MOVE PRINT LINE DATA TO DATA FIELD           
DCOM50   EQU   *                                                                
         L     RF,=V(DOWNDONE)                                                  
         MVI   0(RF),1             MARK OUTPUT DONE                             
*                                                                               
*   DOWNLINK - CALL DLFLD                                                       
*                                                                               
DLINK    LR    R1,R4                                                            
         L     RF,=V(DLFLD)                                                     
         BASR  RE,RF                                                            
*                                                                               
         B     DOWNGOOD                                                         
*                                                                               
*    DOWNHOOK - HOOK USED IN DLFDL                                              
*                                                                               
DOWNHOOK NTR1                                                                   
         CLI   PRNTLN+1,C' '       BY-PASS BLANK LINES                          
         BE    DOWNGOOD                                                         
         GOTO1 VPRINT,DMCB,PRNTLN,=C'BL01'                                      
         SPACE 2                                                                
*                                                                               
*        REDOWN EXIT                                                            
*                                                                               
DOWNGOOD EQU   *                                                                
         SR    R0,R0                                                            
DOWNEXIT EQU   *                                                                
         LTR   R0,R0                                                            
         XIT1                                                                   
*                                                                               
         EJECT                                                                  
*                                                                               
*        LITERALS AND CONSTANTS                                                 
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
*                                                                               
*        LOCAL WORK AREA                                                        
*                                                                               
WORKAREA DSECT                                                                  
VPRINT   DS    A                                                                
         DS    0D                                                               
         DS    CL8'RDWNLINE'                                                    
PRNTLN   DS    CL200' '                                                         
         DS    0D                                                               
         DS    CL8'RDWNBLK*'                                                    
DOWNBLK  DS    XL88'00'                                                         
*                                                                               
         EJECT                                                                  
       ++INCLUDE DDDLCB                                                         
         EJECT                                                                  
       ++INCLUDE REREPWORKD                                                     
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'019REREPRDWNS05/01/02'                                      
         END                                                                    
* > > > > > > END OF CATALOGED PROC RERDWN / REREPRDWN < < < < < <              
