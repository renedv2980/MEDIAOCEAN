*          DATA SET SPMEDFLMBY AT LEVEL 006 AS OF 05/01/02                      
*CATALP MEDFLMBY                                                                
         TITLE 'SCAN A BUY RECORD FOR ALL ACTIVE FILMS'                         
*   PARAMETERS                                                                  
* WORD 1 = A(SPWORKC)                                                           
*  WORD 2 = A(FILM LIST)                                                        
*                                                                               
*  LIST FORMAT = 1 BYTE PRODUCT                                                 
*                1 BYTE SPOT LENGTH                                             
*                2 BYTE FILM SEQUENCE NUMBER                                    
*                                                                               
*                                                                               
VMDFLMBY CSECT                                                                  
         NMOD1 0,MEDFLMBY                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         L     R3,ADBUY                                                         
         USING BUYRECD,R3                                                       
         MVC   HLFA,BUYRLEN        SET FOR END OF BXLE LOOP                     
         LH    RF,HLFA                                                          
         LA    RF,0(R3,RF)                                                      
         BCTR  RF,0                ADJUST FOR BXLE                              
         BCTR  RF,0                                                             
         LA    R2,BDELEM                                                        
         USING REGELEM,R2                                                       
FNDEL    CLI   RCODE,6                                                          
         BL    NXTEL                                                            
         CLI   RCODE,X'0D'                                                      
         BH    NXTEL                                                            
         TM    RSTATUS,X'04'       HIATUS                                       
         BO    NXTEL                                                            
         CLI   RCODE,X'0B'         BRAND ELEMENT                                
         BL    BRNDREC                                                          
         CLI   RLEN,12             UNALLOCATED                                  
         BNL   FNDEL1                                                           
         MVI   HLFA,219                                                         
         MVC   HLFA+1(1),BDSEC                                                  
         B     *+10                                                             
FNDEL1   MVC   HLFA,RPPRD          SET PRODUCT AND LENGTH                       
         MVI   FILMDISP,0          SET FOR FIRST FILM                           
         BAS   R9,SETFLM           DIG OUT THE FILM NUMBER                      
         BAS   R9,INSERT                                                        
         CLI   RLEN,15             PIGGYBACK                                    
         BL    NXTEL                                                            
         MVC   HLFA,RPPRD+4        SET PIGGY PRODUCT AND LENGTH                 
         MVI   FILMDISP,2          SET FOR SECOND PRODUCT                       
         BAS   R9,SETFLM                                                        
         BAS   R9,INSERT                                                        
         B     NXTEL                                                            
*                                                                               
BRNDREC  MVC   HLFA(1),BUYKPRD     BRAND RECORD                                 
         MVC   HLFA+1(1),BDSEC     SAVE LENGTH                                  
         CLI   BDTIME,0                                                         
         BE    *+10                                                             
         MVC   HLFA+1(1),BDTIME    ACTIVE SPOT LENGTH                           
         MVI   FILMDISP,0          FILM 1                                       
         BAS   R9,SETFLM                                                        
         BAS   R9,INSERT                                                        
         CLI   BDTIME,0                                                         
         BE    EXIT                                                             
         DROP  R2                                                               
         LA    R2,BDELEM                                                        
         USING PBELEM,R2                                                        
FNDPBEL  CLI   PBCODE,4            GET PIGGYBACK ELEMENT                        
         BNE   NXTEL1                                                           
         SR    RF,RF                                                            
         IC    RF,PBLEN            SET FOR BXLE                                 
         LA    RF,0(R2,RF)                                                      
         LA    RE,7                                                             
FNDPBEL2 MVC   HLFA(1),PBPROD                                                   
         MVC   HLFA+1(1),PBTIME                                                 
         MVI   FILMDISP,2          SECOND FILM                                  
         BAS   R9,SETFLM                                                        
         BAS   R9,INSERT                                                        
         BXLE  R2,RE,FNDPBEL2                                                   
         B     EXIT                                                             
*                                                                               
NXTEL1   SR    RE,RE                                                            
         IC    RE,PBLEN                                                         
         BXLE  R2,RE,FNDPBEL                                                    
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
         USING REGELEM,R2                                                       
NXTEL    SR    RE,RE                                                            
         IC    RE,RLEN                                                          
         BXLE  R2,RE,FNDEL                                                      
*                                                                               
EXIT     XMOD1 1                                                                
*                                                                               
SETFLM   LR    R4,R2               SET TO CURRENT REGELEM                       
         USING FLMELEM,R4                                                       
         ZIC   R4,1(R2)                                                         
         AR    R4,R2                                                            
         XC    FILMSEQ,FILMSEQ                                                  
SETFLM2  CLI   0(R4),X'0F'                                                      
         BLR   R9                                                               
         CLI   0(R4),X'12'         FILM ELEMENT                                 
         BE    SETFLM3                                                          
         ZIC   RE,1(R4)                                                         
         AR    R4,RE                                                            
         B     SETFLM2                                                          
SETFLM3  ZIC   RE,FILMDISP                                                      
         AR    R4,RE                                                            
         MVC   FILMSEQ,FLMNUM                                                   
         BR    R9                                                               
         SPACE 2                                                                
*                                                                               
INSERT   CLI   QPGR,C' '           TEST PRDGRP REQUEST                          
         BE    INSERT4             NO                                           
         CLC   =C'POL',QPRD        TEST POL REQUEST                             
         BNE   INSERT4             NO                                           
* NEED TO CHECK IF PRD IN THIS PRDGRP                                           
         L     R8,SVPRDADR         POINT TO CURRENT PRD                         
INSERT2  CLC   5(1,R8),HLFA        MATCH PRODUCT CODE                           
         BE    INSERT4                                                          
         CLC   0(2,R8),6(R8)       NEXT PRD SAME PRDGRP                         
         BNER  R9                  NO - SKIP PRD                                
         LA    R8,6(R8)            ELSE NEXT ENTRY                              
         B     INSERT2                                                          
*                                                                               
INSERT4  L     R8,4(R1)            GET LIST ADDRESS                             
*                                                                               
INSERT6  CLI   0(R8),0                                                          
         BNE   *+12                                                             
         MVC   0(4,R8),HLFA                                                     
         BR    R9                                                               
         CLC   HLFA(4),0(R8)                                                    
         BER   R9                                                               
         LA    R8,4(R8)                                                         
         B     INSERT6                                                          
*                                                                               
HLFA     DS    CL2                                                              
FILMSEQ  DS    CL2                                                              
FILMDISP DS    C                                                                
         LTORG                                                                  
         PRINT OFF                                                              
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'006SPMEDFLMBY05/01/02'                                      
         END                                                                    
