*          DATA SET PBWIDETXT  AT LEVEL 036 AS OF 05/01/02                      
*CATALP WIDETXT                                                                 
         TITLE 'WIDETXT - 860 WIDELINES TO SCRIPT WIDELINES'                    
WIDETXT  CSECT                                                                  
         PRINT GEN                                                              
*                                                                               
* SAVE REGISTERS - SETUP LINKAGE                                                
*                                                                               
         USING *,15                                                             
         SAVE  (14,12),,*                                                       
         DROP  15                                                               
START    DC    0Y(0)                                                            
         BASR  12,0                                                             
         USING *,12                                                             
         ST    13,SAVEARA1+4                                                    
         LA    13,SAVEARA1                                                      
*                                                                               
* EQUATE REGISTERS                                                              
*                                                                               
R0       EQU   0                                                                
R1       EQU   1                                                                
R2       EQU   2                                                                
R3       EQU   3                                                                
R4       EQU   4                                                                
R5       EQU   5                                                                
R6       EQU   6                                                                
R7       EQU   7                                                                
R8       EQU   8                                                                
R9       EQU   9                                                                
RA       EQU   10                                                               
RB       EQU   11                                                               
RC       EQU   12                                                               
RD       EQU   13                                                               
RE       EQU   14                                                               
RF       EQU   15                                                               
         EJECT                                                                  
*                                                                               
* ROUTINE TO HANDLE CONVERSION AND OUTPUT OF WIDE (132-CHAR) LINES              
*                                                                               
* ROUTINE REQUIRES CALLING PROGRAM TO PLACE THE ADDRESS OF AN I/O               
* PARAMETER LIST IN R1 FOR USE BY THIS ROUTINE. . .                             
*                                                                               
*    PARAMETER LIST - ADDRESS OF DCB FOR INPUT FILE                             
*                     ADDRESS OF INPUT BUFFER                                   
*                     ADDRESS OF DCB FOR OUTPUT FILE                            
*                     ADDRESS OF OUTPUT BUFFER                                  
*                     ADDRESS OF 10,000-BYTE ARRAY                              
*                                                                               
* A 10,000 BYTE ARRAY (TRAY) RECEIVES THE INPUT LINES AFTER THE LINE            
* NUMBER IN THE INPUT RECORD HAS BEEN CONVERTED TO BINARY FOR                   
* ARITHMETIC TO DETERMINE WHERE EACH 66 CHARACTERS OF INPUT SHOULD              
* BE PLACED.  AFTER ALL INPUT IS READ, 66-CHARACTER SEGEMENTS OF THE            
* ARRAY ARE THEN WRITTEN TO THE OUTPUT FILE FOR LATER SCRIPT                    
* PROCESSING.                                                                   
*                                                                               
         SR    R9,R9               INIT REG TO STORE HIGHEST LINE               
*                                                                               
* SAVE PARAMETER LIST POINTER                                                   
*                                                                               
         LR    R2,R1                                                            
*                                                                               
* CLEAR TRAY TO BLANKS                                                          
*                                                                               
         SR    R4,R4                                                            
         SR    R6,R6                                                            
         L     R6,=F'1000'         INIT LOOP CTR                                
         L     R4,16(R2)           LOAD ARRAY POINTER                           
LOOPC    DC    0Y(0)                                                            
         MVC   0(10,R4),=C'          '                                          
         LA    R4,10(0,R4)         BUMP POINTER                                 
         BCT   R6,LOOPC                                                         
         SR    R4,R4                                                            
         SR    R6,R6                                                            
*                                                                               
**                                                                              
*                                                                               
WIDEREAD DC    0Y(0)                                                            
         SR    R4,R4                                                            
         SR    R6,R6                                                            
         L     R4,0(R2)            * A(INFIL)                                   
         L     R6,4(R2)            * A(IBUFF)                                   
         GET   (R4),(R6)                                                        
         CLC   0(8,R6),=C'PAGE=END'    END OF WIDE INPUT?                       
         BE    EWIDETXT            YES, START OUTPUT                            
*                                                                               
* WIDETXT - PART 1...CONVERT LINE NUMBER TO BINARY FOR ARITHMETIC               
*                                                                               
*        R0    WORK                                                             
*        R2    PARAMETER LIST ADDRESS IN CALLING PROGRAM                        
*        R3    A(FIELD) POINTER TO LINE NO IN INPUT                             
*        R4    ACCUM FOR BIT MANIP. IN BINARY CONVERSION                        
*        R5    WORK                                                             
*        R6    WORK                                                             
*        R7    CTR...FIELD LENGTH (2 CHARACTERS)                                
*        R8    A(TRAY)...TEXT ARRAY 10,000 BYTES                                
*                                                                               
         SR    R4,R4               CLEAR                                        
         LA    R7,2                SET CTR                                      
         L     R3,4(R2)            * SET IBUFF POINTER                          
         A     R3,=F'1'            * ADJUST POINTER                             
         SR    R0,R0               CLEAR                                        
         LA    R6,X'0F'            SET MASK                                     
         L     R8,16(R2)           * SET ARRAY POINTER                          
         S     R8,=F'1'            * ADJUST POINTER                             
         B     FIRST                                                            
LOOPA    DC    0Y(0)                                                            
         LR    R0,R4               MOVE IN ACCUM                                
         SLL   R0,1                X 2                                          
         LR    R4,R0               SAVE IN ACCUM                                
         SLL   R0,2                X 4 ...(X 8)                                 
         AR    R0,R4               =((X 8) + (X 2))                             
         LR    R4,R0               SAVE IN ACCUM                                
FIRST    DC    0Y(0)                                                            
         IC    R0,0(R3)            GET CHARACTER                                
         NR    R0,R6               CLEAR BITS                                   
         AR    R0,R4               ADD IN ACCUM                                 
         LR    R4,R0               SAVE IN ACCUM                                
         LA    R3,1(0,R3)          BUMP POINTER                                 
         BCT   R7,LOOPA                                                         
*                                                                               
* R4 NOW CONTAINS THE LINE NUMBER IN BINARY                                     
*                                                                               
         CR    R4,R9               COMPARE CURR. LINE/HIGHEST LINE              
         BL    OFFSET              CURRENT IS LESS                              
         LR    R9,R4               CURRENT HIGHER, SWAP                         
OFFSET   DC    0Y(0)                                                            
         MH    R4,=H'132'          *                                            
         SH    R4,=H'132'          * TO GET TRAY OFFSET                         
         AH    R4,=H'1'            *                                            
         L     R3,4(R2)            * RESET IBUFF POINTER                        
         CLI   0(R3),C'L'          LEFT SIDE OF PAGE?                           
         BE    LEFT                YES, NO LINE OFFSET NEEDED                   
         AH    R4,=H'66'           OFFSET TO RIGHT SIDE OF LINE                 
LEFT     DC    0Y(0)                                                            
         AR    R4,R8               POINT TO TRAY LOCATION                       
         MVC   0(66,R4),3(R3)      MOVE 66 CHARS OF INPUT                       
         B     WIDEREAD            GET NEXT INPUT                               
*                                                                               
* WIDETXT - PART 2 WRITE ARRAY SEGMENTS TO OUTPUT                               
*                                                                               
* USES THE HIGHEST LINE NUMBER (R9) TO STOP THE OUTPUT LOOP                     
*                                                                               
EWIDETXT DC    0Y(0)                                                            
         SR    R6,R6                                                            
         SR    R7,R7                                                            
         SR    R8,R8                                                            
         L     R6,16(R2)           * A(TRAY) INPUT                              
         L     R7,8(R2)            * A(OUTFILE)                                 
         L     R8,12(R2)           * A(OBUFF)                                   
LOOPB    DC    0Y(0)                                                            
         MVC   0(66,R8),0(R6)      LEFT SIDE OF LINE TO OBUFF                   
         MVI   66(R8),C'@'          CONTINUATION CHARACTER TO OBUFF             
****                                                                            
         MVI   PUTFLG,C'1'                                                      
         B     C1CHECK             CHECK FOR '.' IN COL 1                       
PUTFLG1  DC    0Y(0)                                                            
         MVI   PUTFLG,C' '         RESET FLAG                                   
         PUT   (R7),(R8)                                                        
         MVI   0(R8),X'40'         BLANK TO FIRST CHAR OF OBUFF                 
         MVC   1(159,R8),0(R8)     PROPAGATE BLANKS                             
         A     R6,=F'66'           BUMP TRAY POINTER                            
         MVC   0(4,R8),=C'.CT '    CONTINUATION COMMAND                         
         MVC   4(66,R8),0(R6)      RIGHT SIDE OF LINE                           
         MVC   70(4,R8),=C'&&XH.'  HARD LINE END                                
****                                                                            
         MVI   PUTFLG,C'2'                                                      
         B     C1CHECK             CHECK FOR '.' IN COL 1                       
PUTFLG2  DC    0Y(0)                                                            
         MVI   PUTFLG,C' '         RESET FLAG                                   
         PUT   (R7),(R8)                                                        
         MVI   0(R8),X'40'         BLANK TO FIRST CHAR OF OBUFF                 
         MVC   1(159,R8),0(R8)     PROPAGATE BLANKS                             
         A     R6,=F'66'           BUMP TRAY POINTER                            
         BCT   R9,LOOPB                                                         
*                                                                               
* DONE                                                                          
*                                                                               
         L     13,SAVEARA1+4                                                    
         RETURN (14,12),T,RC=0                                                  
         EJECT                                                                  
*                                                                               
* CHECK OBUFF FOR A "." IN COL 1 AND CHANGE TO BACK-SLASH IF NOT                
* SCRIPT COMMAND                                                                
*                                                                               
C1CHECK  DC    0Y(0)                                                            
         CLC   0(4,R8),=C'.CT '    CONTINUATION?                                
         BE    CKFLG                                                            
         CLI   0(R8),X'4B'         PERIOD?                                      
         BNE   CKFLG               NO, GO BACK                                  
         MVI   0(R8),X'E0'         PUT BACK-SLASH IN COL 1                      
CKFLG    DC    0Y(0)                                                            
         CLI   PUTFLG,C'1'                                                      
         BE    PUTFLG1                                                          
         B     PUTFLG2                                                          
*                                                                               
SAVEARA1 DS    18F                                                              
*                                                                               
PUTFLG   DC    C' '                                                             
         LTORG                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'036PBWIDETXT 05/01/02'                                      
         END   WIDETXT                                                          
