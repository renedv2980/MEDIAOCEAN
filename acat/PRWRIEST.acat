*          DATA SET PRWRIEST   AT LEVEL 039 AS OF 08/18/08                      
*CATALP PRWRIEST                                                                
         TITLE 'PRWRIEST - ESTIMATE RECORD KEYWORDS - ENTRY POINTS'             
         ENTRY IESTBUF                                                          
         ENTRY IESTSCHM                                                         
         ENTRY OESTSCHM                                                         
         ENTRY IESTBGT                                                          
         ENTRY IESTPG                                                           
PRWRIEST CSECT                                                                  
         TITLE 'PRWRIEST - CHANGE LOG'                                          
***********************************************************************         
*                                                                               
* SMYE 09/02    ADD IESTPG TO EXTRACT PGEST AND GFEST RECORD DATA               
*               (BOTH IN PGESTREC FORMAT)                                       
*                                                                               
***********************************************************************         
*                                                                     *         
*        PROGRAM CONTAINS THE INPUT & OUTPUT ROUTINES FOR             *         
*          ESTIMATE RECORD KEYWORDS                                   *         
*                                                                     *         
*        IESTBUF  - RETRIEVE DATA FROM ESTIMATE BUFFER - INPUT        *         
*        IESTSCHM - EXTRACT ESTIMATE FILTER SCHEME -     INPUT        *         
*        OESTSCHM - EXTRACT ESTIMATE FILTER SCHEME -     OUTPUT       *         
*        IESTBGT  - EXTRACT ESTIMATE BUDGET $'S    -     INPUT        *         
*        IESTPG   - EXTRACT PGESTREC DATA          -     INPUT        *         
*                                                                     *         
*                                                                     *         
***********************************************************************         
         TITLE 'PRWRIEST - DATA IN ESTIMATE BUFFER - IESTBUF'                   
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT DATA FROM ESTIMATE BUFFER                 *         
*                                                                     *         
*NTRY    GLARGS   = RECORD ID                                         *         
*                   C'E' - ESTIMATE                                   *         
*        GLARGS+1 = DATA TYPE                                         *         
*                   C'F' - FILTER                                     *         
*                   C'L' - LOCKED STATUS                              *         
*                   C'R' - RATE   TYPE                                *         
*                   C'T' - TEST   STATUS                              *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0      -  DATA LENGTH                                         *         
*       1-?    -  DATA                                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
IESTBUF  NMOD1 0,**#IEBF                                                        
*                                                                               
         USING SPOOLD,R8           SPOOL WORKAREA                               
         USING SYSD,R9             PRWRITER WORKAREA                            
         USING GLOBALD,RA          DRIVER AREA                                  
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRNTBLOCK                          
         USING PBLOCK,R7                                                        
*                                                                               
         ICM   R3,15,PBAESTBF      POINT TO ESTIMATE BUFFER                     
         BZ    IESTBUFX            NO BUFFER PRESENT                            
*                                                                               
         ICM   R0,15,PBCESTBF      GET NUMBER OF EST BUFFER ENTRIES             
         BZ    IESTBUFX            EMPTY BUFFER                                 
*                                                                               
         L     R4,AIO1             POINT TO RECORD IN I/O1                      
         SR    RF,RF               INIT ESTIMATE POINTER                        
*                                                                               
         CLI   PBUYKRCD-PBUYKEY(R4),PBUYKIDQ  DETERMINE RECORD TYPE             
         BNE   *+12                                                             
         LA    RF,PBUYKEST-PBUYKEY(R4)  BUY RECORD                              
         B     IEBFESTX                                                         
*                                                                               
         CLI   PBILKRCD-PBILLKEY(R4),PBILKIDQ  DETERMINE RECORD TYPE            
         BNE   *+12                                                             
         LA    RF,PBILKEST-PBILLKEY(R4)  BILL RECORD                            
         B     IEBFESTX                                                         
*                                                                               
         CLI   PBKKRCD-PBKKEY(R4),PBKKIDQ     DETERMINE RECORD TYPE             
         BNE   *+12                                                             
         LA    RF,PBKKEST-PBKKEY(R4)     ESTIMATE BUCKET RECORD                 
         B     IEBFESTX                                                         
*                                                                               
         TM    PBMODE,PBPROCES     IF PROCESSING ESTIMATES                      
         BNO   *+12                                                             
         LA    RF,PBEST               USE CURRENT ESTIMATE                      
         B     IEBFESTX                                                         
*                                                                               
         B     IESTBUFX            NO SUITABLE RECORD AVAILABLE                 
*                                                                               
IEBFESTX DS    0H                  FIND ESTIMATE IN BUFFER                      
*                                                                               
IEBFLOOP DS    0H                  FIND ESTIMATE IN BUFFER                      
*                                                                               
         USING ESTBUFFD,R3         ESTABLISH BUFFER ENTRY                       
*                                                                               
         MVC   WORK,0(R3)          TESTING                                      
         CLC   EBFEST,0(RF)        MUST MATCH ESTIMATE                          
         BNE   IEBFCONT                                                         
*                                                                               
         CLC   EBFPRD,PBPROD       MUST MATCH PRODUCT                           
         BNE   IEBFCONT                                                         
*                                                                               
         CLC   EBFCLT,PBCLT        MUST MATCH CLIENT                            
         BNE   IEBFCONT                                                         
*                                                                               
         CLC   EBFMED,PBMED        MUST MATCH MEDIA                             
         BE    IEBFFND                                                          
*                                                                               
IEBFCONT DS    0H                                                               
*                                                                               
         LA    R3,EBFLEN(R3)       BUMP TO NEXT BUFFER ENTRY                    
         BCT   R0,IEBFLOOP                                                      
*                                                                               
         B     IESTBUFX            NO MATCH                                     
*                                                                               
IEBFFND  DS    0H                  BUFFER ENTRY FOUND                           
*                                                                               
*        FIND DATA TYPE TO BE HANDLED                                           
*                                                                               
         CLI   GLARGS+1,C'F'       ESTIMATE FILTER                              
         BE    IEBFFLT                                                          
*                                                                               
         CLI   GLARGS+1,C'L'       ESTIMATE STATUS - LOCKED                     
         BE    IEBFLK                                                           
*                                                                               
         CLI   GLARGS+1,C'R'       ESTIMATE RATE TYPE                           
         BE    IEBFRT                                                           
*                                                                               
         CLI   GLARGS+1,C'T'       ESTIMATE STATUS - TEST                       
         BE    IEBFTS                                                           
*                                                                               
         B     IESTBUFX            UNKNOWN DATA TYPE                            
*                                                                               
IEBFFLT  DS    0H                  ESTIMATE FILTER                              
*                                                                               
         MVI   0(R2),L'EBFFLTR     SET DATA LENGTH                              
         MVC   1(L'EBFFLTR,R2),EBFFLTR   RETURN ESTIMATE FILTER                 
*                                                                               
         B     IESTBUFX            DONE                                         
*                                                                               
*        ESTIMATE STATUS - LOCKED                                               
*                                                                               
IEBFLK   DS    0H                                                               
*                                                                               
*        TRANSLATE INTERNAL CODE                                                
*                                                                               
         LA    R4,IEBFLKTB         POINT TO TABLE OF VALUES                     
*                                                                               
IEBFLKTL DS    0H                                                               
*                                                                               
         OC    1(2,R4),1(R4)       CHECK FOR END OF TABLE                       
         BE    IEBFLKTD            INVALID VALUE - DON'T TRANSLATE              
*                                                                               
         CLC   EBFESTLK,0(R4)      MATCH TO TABLE                               
         BE    IEBFLKTF                                                         
*                                                                               
IEBFLKTC DS    0H                                                               
*                                                                               
         LA    R4,IEBFLKLN(R4)     BUMP TO NEXT TABLE ENTRY                     
         B     IEBFLKTL                                                         
*                                                                               
IEBFLKTF DS    0H                                                               
*                                                                               
         ICM   RF,1,3(R4)          TRANSLATION LENGTH                           
         BZ    *+6                                                              
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),4(R4)       PASS TRANSLATION TO SORT                     
*                                                                               
IEBFLKTD DS    0H                                                               
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQESTLK),0,0   CHECK IF FILTERING            
*                                         ON LOCKED STATUS                      
         ICM   R6,15,DMCB+8        POINT TO FOUND FILTER                        
         BZ    IEBFLKX                NOT FILTERING                             
         USING FLTCTL,R6           ESTABLISH FILTER AREA                        
*                                                                               
         CLC   1(2,R4),FLTFVAL     IF FILTER VALUES DON'T MATCH                 
         BE    *+8                                                              
         MVI   SORTSW,C'N'            DROP DATA FROM SORT                       
*                                                                               
IEBFLKX  DS    0H                                                               
*                                                                               
         B     IESTBUFX            DONE                                         
*                                                                               
*        TABLE OF ACCEPTABLE VALUES FOR ESTIMATE STATUS - LOCKED                
*                                                                               
*            CL1   - INTERNAL VALUE                                             
*            AL2   - INTERNAL FILTER VALUE                                      
*                    0 INDICATES END OF TABLE                                   
*            CL1   - TRANSLATION LENGTH                                         
*            CL7   - TRANSLATION                                                
*                                                                               
IEBFLKTB DS    0X                                                               
         DC    XL1'00',AL2(PRQELKOP),AL1(4),CL7'OPEN'                           
IEBFLKLN EQU   *-IEBFLKTB          TABLE ENTRY LENGTH                           
         DC    CL1'1',AL2(PRQELKLK),AL1(6),CL7'LOCKED'                          
         DC    CL1'2',AL2(PRQELKPL),AL1(7),CL7'PLOCKED'                         
         DC    XL1'00',AL2(0),AL1(0),CL7' '                                     
*                                                                               
*        ESTIMATE RATE TYPE                                                     
*                                                                               
IEBFRT   DS    0H                  ESTIMATE RATE TYPE                           
*                                                                               
         MVC   1(1,R2),EBFRTETP    RETURN ESTIMATE RATE TYPE                    
*                                                                               
         B     IESTBUFX            DONE                                         
*                                                                               
*        ESTIMATE STATUS - TEST                                                 
*                                                                               
IEBFTS   DS    0H                  ESTIMATE STATUS TEST                         
*                                                                               
*        TRANSLATE INTERNAL CODE                                                
*                                                                               
         LA    R4,IEBFTSTB         DEFAULT TO FIRST ENTRY IN TABLE              
*                                                                               
         TM    EBFESTTS,X'80'      IF TEST ESTIMATE                             
         BNO   *+8                                                              
         LA    R4,IEBFTSLN(R4)         BUMP TO NEXT TABLE ENTRY                 
*                                                                               
         ICM   RF,1,3(R4)          TRANSLATION LENGTH                           
         BZ    *+6                                                              
         BCTR  RF,0                DECREMENT FOR EXECUTE                        
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),4(R4)       PASS TRANSLATION TO SORT                     
*                                                                               
         GOTO1 FNDFLTRA,DMCB,=AL2(PRQESTTS),0,0   CHECK IF FILTERING            
*                                         ON TEST STATUS                        
         ICM   R6,15,DMCB+8        POINT TO FOUND FILTER                        
         BZ    IEBFTSX                NOT FILTERING                             
         USING FLTCTL,R6           ESTABLISH FILTER AREA                        
*                                                                               
         CLC   1(2,R4),FLTFVAL     IF FILTER VALUES DON'T MATCH                 
         BE    *+8                                                              
         MVI   SORTSW,C'N'            DROP DATA FROM SORT                       
*                                                                               
IEBFTSX  DS    0H                                                               
*                                                                               
         B     IESTBUFX            DONE                                         
*                                                                               
*        TABLE OF ACCEPTABLE VALUES FOR ESTIMATE STATUS - LOCKED                
*                                                                               
*            CL1   - INTERNAL VALUE                                             
*            AL2   - INTERNAL FILTER VALUE                                      
*                    0 INDICATES END OF TABLE                                   
*            CL1   - TRANSLATION LENGTH                                         
*            CL7   - TRANSLATION                                                
*                                                                               
IEBFTSTB DS    0X                                                               
         DC    XL1'00',AL2(PRQETSLV),AL1(4),CL7'LIVE'                           
IEBFTSLN EQU   *-IEBFTSTB          TABLE ENTRY LENGTH                           
         DC    XL1'80',AL2(PRQETSTS),AL1(4),CL7'TEST'                           
         DC    XL1'00',AL2(0),AL1(0),CL7' '                                     
*                                                                               
         MVI   0(R2),L'EBFESTTS    SET DATA LENGTH                              
         MVC   1(L'EBFESTTS,R2),EBFESTTS  RETURN TEST STATUS                    
*                                                                               
         B     IESTBUFX            DONE                                         
*                                                                               
IESTBUFX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIEST - REQUESTED EST FILTER SCHEME - IESTSCHM'              
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT REQUESTED ESTIMATE FILTER SCHEME          *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-2    -  REQUESTED ESTIMATE FILTER                           *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
IESTSCHM NMOD1 0,**#IESCHM                                                      
*                                                                               
         USING SPOOLD,R8           SPOOL WORKAREA                               
         USING SYSD,R9             PRWRITER WORKAREA                            
         USING GLOBALD,RA          DRIVER AREA                                  
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRNTBLOCK                          
         USING PBLOCK,R7                                                        
*                                                                               
         MVC   0(L'PBQESTX,R2),PBQESTX   PASS ON FILTER                         
*                                                                               
IESTSCHX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIEST - REQUESTED EST FILTER SCHEME - OESTSCHM'              
***********************************************************************         
*                                                                     *         
*        ROUTINE TO PRINT REQUESTED ESTIMATE FILTER SCHEME            *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  REQUEST SCHEME                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0H                                                               
OESTSCHM NMOD1 0,**#OESCHM                                                      
*                                                                               
         USING SPOOLD,R8           SPOOL WORKAREA                               
         USING SYSD,R9             PRWRITER WORKAREA                            
         USING GLOBALD,RA          DRIVER AREA                                  
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRNTBLOCK                          
         USING PBLOCK,R7                                                        
*                                                                               
         MVC   LABLAREA,SPACES     INIT OUTPUT AREA                             
         MVC   LABLAREA(8),=CL8'ESTIMATE'   SET LABEL                           
*                                                                               
         MVC   CODEAREA(3),PBQEST  REQUESTED ESTIMATES                          
*                                                                               
         CLC   PBQEST,SPACES       IF SPACES PRESENT                            
         BNE   *+10                                                             
         MVC   CODEAREA(3),=CL3'NO'   PRINT 'NO'                                
*                                                                               
         OC    0(L'PBQESTX,R2),0(R2)  SKIP IF NO FILTER ENTERED                 
         BZ    OESTSCHX                                                         
*                                                                               
         MVC   NAMEAREA(10),=CL10'FILTERED ('   SET LEAD-IN WORD                
*                                                                               
         LA    R1,NAMEAREA+9       START OF PRINT AREA MINUS 1                  
         MVC   WORK(L'PBQESTX),0(R2)  MOVE INPUT TO WORK AREA                   
         LA    RE,WORK             INPUT POINTER                                
         LA    RF,1                POSITION COUNTER                             
         LA    R0,3                NUMBER OF POSITIONS                          
*                                                                               
OESCLOOP DS    0H                                                               
*                                                                               
         CLI   0(RE),C' '          SKIP IF NO FILTER                            
         BNH   OESCLPCN                                                         
         CLI   0(RE),C'*'          SKIP IF WILDCARD                             
         BE    OESCLPCN                                                         
*                                                                               
         LA    R1,1(R1)            BUMP OUTPUT PTR                              
*                                                                               
         CVD   RF,DUB              PRINT POSITION POINTER                       
         OI    DUB+7,X'0F'         FORCE SIGN                                   
         UNPK  0(1,R1),DUB         PRINT POSITION OF FILTER                     
         MVI   1(R1),C'='                                                       
         LA    R1,2(R1)            BUMP OUTPUT POINTER                          
*                                                                               
         TM    0(RE),X'40'         SKIP IF 4-BIT PRESENT                        
         BO    OESCLP10                                                         
*                                                                               
         OI    0(RE),X'40'            ELSE RESTORE 4-BIT                        
*                                                                               
         MVI   0(R1),C'-'                  INDICATE NEGATIVE FILTER             
         LA    R1,1(R1)                     BUMP OUTPUT POINTER                 
*                                                                               
OESCLP10 DS    0H                                                               
*                                                                               
         MVC   0(1,R1),0(RE)       PRINT FILTER VALUE                           
         LA    R1,1(R1)            BUMP OUTPUT POINTER                          
         MVI   0(R1),C','          SET DIVIDER                                  
*                                                                               
OESCLPCN DS    0H                                                               
*                                                                               
         LA    RE,1(RE)            BUMP INPUT POINTER                           
         LA    RF,1(RF)            BUMP POSITION COUNTER                        
*                                                                               
         BCT   R0,OESCLOOP                                                      
*                                                                               
         CLI   0(R1),C'('          IF NO PATTERN PRINTED                        
         BNE   *+12                                                             
         MVI   0(R1),C' '             CLEAR PATTERN START PARENS                
         B     *+8                                                              
         MVI   0(R1),C')'          ELSE CLOSE PATTERN DESCRIPTION               
*                                                                               
OESTSCHX DS    0H                                                               
*                                                                               
         GOTO1 GENOUTA             GENERAL OUTPUT ROUTINE                       
*                                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIEST - BUDGET DOLLARS - IESTBGT'                            
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT BUDGET DOLLARS                            *         
*                                                                     *         
*NTRY                                                                 *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-7    -  PL8 - BUDGET DOLLARS                                *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
IESTBGT  NMOD1 0,**#IEBGT                                                       
*                                                                               
         USING SPOOLD,R8           SPOOL WORKAREA                               
         USING SYSD,R9             PRWRITER WORKAREA                            
         USING GLOBALD,RA          DRIVER AREA                                  
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRNTBLOCK                          
         USING PBLOCK,R7                                                        
*                                                                               
         CLI   PBMODE,PBPROCES     EXIT UNLESS PROCESSING ESTIMATES             
         BNE   IESTBGXX                                                         
*                                                                               
         XC    KEY,KEY                                                          
         LA    R4,KEY              ESTABLISH ESTIMATE BUDGET RECORD             
         USING PBUDREC,R4                                                       
*                                                                               
         MVC   PBUDKAGY,PBAGY      SET AGENCY                                   
         MVC   PBUDKMED,PBMED      SET MEDIA                                    
         MVI   PBUDKRCD,PBUDKIDQ   SET RECORD ID                                
         MVC   PBUDKCLT,PBCLT      SET CLIENT                                   
         MVC   PBUDKPRD,PBPROD     SET PRODUCT                                  
         SR    RF,RF                                                            
         ICM   RF,3,PBEST                                                       
         CVD   RF,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  PBUDKEST,DUB        SET ESTIMATE                                 
****     MVC   PBUDKREG,=C'000'    SET REGION                                   
****     MVC   PBUDKDST,=C'000'    SET DISTRICT                                 
*                                                                               
         L     RF,AIO3             POINT TO LAST RECORD READ IN                 
*                                                                               
         CLC   PBUDREC(PBUDKREG-PBUDREC),0(RF)  OKAY IF ALREADY IN CORE         
         BE    IESTBGT1                                                         
*                                                                               
         B     IESTBGTX            DONE IF WRONG RECORD IN CORE                 
*                                                                               
         GOTO1 HIGH                READ BUDGET POINTER                          
*                                                                               
         CLC   PBUDREC(25),KEYSAVE DONE IF RECORD NOT FOUND                     
         BNE   IESTBGTX                                                         
*                                                                               
         ICM   R0,15,AIO           SAVE CURRENT IOAREA PTR                      
         MVC   AIO,AIO3            READ INTO IOA3                               
*                                                                               
         GOTO1 GETREC              READ IN RECORD                               
*                                                                               
         STCM  R0,15,AIO           RESTORE CURRENT IOAREA PTR                   
*                                                                               
IESTBGT1 DS    0H                                                               
*                                                                               
         SR    RF,RF                                                            
         L     R6,AIO3             POINT TO BUDGET RECORD                       
         LA    R6,PBUDELEM-PBUDREC(R6) POINT TO FIRST ELEMENT                   
*                                                                               
         USING PBUDELEM,R6         ESTABLISH AS BUDGET ELEMENT                  
*                                                                               
         CLI   PBUDELEM,0          SKIP IF EOR REACHED                          
         BE    IESTBGTX                                                         
         CLI   PBUDELEM,X'01'      FIND BUDGET ELEMENT                          
         BE    *+16                                                             
         IC    RF,1(R6)            BUMP TO NEXT ELEMENT                         
         LA    R6,0(RF,R6)                                                      
         B     *-24                                                             
*                                                                               
         CLI   GLARGS+1,C'M'       SKIP IF NOT A MONTHLY AMOUNT                 
         BNE   IESTBGMN                                                         
*                                                                               
*        RETURN MONTHLY AMOUNT                                                  
*                                                                               
         GOTO1 DATCON,DMCB,PBUDST,(3,IESTBDST)  CONVERT ESTIMATE START          
         GOTO1 DATCON,DMCB,PBUDEND,(3,IESTBDEN) CONVERT ESTIMATE END            
*                                                                               
*        FIND BUCKET FOR MONTH                                                  
*                                                                               
         LA    R1,PBUDAMTS         FIRST BUCKET                                 
         SR    RF,RF                                                            
         ZAP   0(8,R2),=P'0'       INIT RETURNED VALUE                          
*                                                                               
         LA    R3,PBBDGTST         DEFAULT TO BUDGET MONTH LIST DATES           
*                                                                               
         OC    0(6,R3),0(R3)         IF NO BUDGET MONTH LIST DATES              
         BNZ   *+8                      USE FLOWCHSRT DATES                     
         LA    R3,GLARGS+9            USE BUDGET MONTH LIST DATES               
*                                                                               
         OC    0(6,R3),0(R3)         IF NO DATES                                
         BZ    IESTBDDN                 MAYBE DOING TOTALS                      
*                                                                               
IESTBDLP DS    0H                  ALL COMPARES EXCLUDE DAYS                    
*                                                                               
         CLC   IESTBDST(2),IESTBDEN   STOP AT END OF ESTIMATE                   
         BH    IESTBDDN                                                         
*                                                                               
         CLC   IESTBDST(2),0(R3)      MONTH MUST LIE IN PERIOD                  
         BL    IESTBDCN                                                         
         CLC   IESTBDST(2),3(R3)      MONTH MUST LIE IN PERIOD                  
         BH    IESTBDCN                                                         
*                                                                               
IESTBD10 DS    0H                                                               
*                                                                               
         ICM   RE,15,0(R1)         GET MONTH BUDGET                             
         CVD   RE,DUB                                                           
         AP    0(8,R2),DUB         PASS BACK TOTAL BUDGET DOLLARS               
*                                                                               
IESTBDCN DS    0H                                                               
*                                                                               
         LA    R1,4(R1)            BUMP TO NEXT BUCKET                          
*                                                                               
         ICM   RF,3,IESTBDST       BUMP MONTH                                   
         LA    RF,1(RF)            ADD 1 TO MONTH                               
*                                                                               
         CLM   RF,1,=AL1(13)       IF INTO NEXT YEAR                            
         BL    *+12                                                             
         ICM   RF,1,=AL1(1)           SET TO JANUARY                            
         LA    RF,256(RF)             BUMP YEAR                                 
*                                                                               
         STCM  RF,3,IESTBDST       RESET MONTH                                  
*                                                                               
         B     IESTBDLP                                                         
*                                                                               
IESTBDDN DS    0H                  ALL COMPARES EXCLUDE DAYS                    
*                                                                               
         B     IESTBGTX                                                         
*                                                                               
IESTBGMN DS    0H                                                               
*                                                                               
         OC    PBBDGTST(6),PBBDGTST   SKIP IF BUDGET PERIOD GIVEN               
         BNZ   IESTBGTX                                                         
*                                                                               
         ICM   RF,15,PBUDAMTS+48   GET TOTAL BUDGET                             
         CVD   RF,DUB                                                           
         ZAP   0(8,R2),DUB         PASS BACK TOTAL BUDGET DOLLARS               
*                                                                               
IESTBGTX DS    0H                                                               
*                                                                               
         MVC   KEY,IOKEYSVE        RESTORE FILE POINTER                         
******   GOTO1 HIGH                                                             
*                                                                               
IESTBGXX DS    0H                                                               
*                                                                               
         XIT1                                                                   
*                                                                               
         DS    CL256               FOR NEW Z/OS PROCESSOR                       
IESTBDST DS    XL3                 ESTIMATE START DATE - BINARY                 
IESTBDEN DS    XL3                 ESTIMATE END   DATE - BINARY                 
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE 'PRWRIEST - PGESTREC DATA - IESTPG'                              
***********************************************************************         
*                                                                     *         
*        ROUTINE TO EXTRACT REQUESTED PGESTREC FIELDS                 *         
*                                                                     *         
*NTRY    GLARGS   = C'H' - HEADER                                     *         
*        GLARGS+1 = C'6' - PG ESTIMATE RECORD (PGESTREC - X'0A')      *         
*        GLARGS+2 = ELEMENT "SEQUENCE NUMBER"                         *         
*              (PGSTESEQ)                     LENGTH (SEE GLARGS+3)   *         
*                 X'01' - CHARGE PERIOD        X'03'                  *         
*                 X'02' - ACCOUNT              X'06'                  *         
*                 X'03' - PG BRAND             X'04'                  *         
*                 X'04' - PG EST               X'04'                  *         
*                 X'05' - EVENT CODE           X'06'                  *         
*                 X'06' - MULTI-BRAND          X'01'                  *         
*                 X'07' - NO BRAND             X'01'                  *         
*                 X'08' - BRAND SUFFIX         X'02'                  *         
*        GLARGS+1 = C'7' - GF ESTIMATE RECORD (PGESTREC - X'0B')      *         
*        GLARGS+2 = ELEMENT "SEQUENCE NUMBER"                         *         
*              (PGSTESEQ)                     LENGTH (SEE GLARGS+3)   *         
*                 X'01' - DIVISION/BRAND CODE  X'02'                  *         
*                 X'02' - PRODUCT CODE         X'04'                  *         
*                 X'03' - GF NATURAL           X'03'                  *         
*                 X'04' - GF SUB-NATURAL       X'03'                  *         
*                 X'05' - BILLING AGENCY       X'08'                  *         
*                 X'06' - EXPENSE TYPE         X'06'                  *         
*                 X'07' - PRODUCT ID           X'0A'                  *         
*                 X'08' - CREATIVE AGENCY      X'08'                  *         
*                 X'09' - SOURCE AGENCY        X'08'                  *         
*                 X'0A' - TARGET MARKET        X'01'                  *         
*                 X'0B' - DEAL# (GEVALIA)      X'0A'                  *         
*        GLARGS+3 = X'NN'- FIELD LENGTH (SEE GLARGS+2)                *         
*                                                                     *         
*                                                                     *         
*        R2==>  DRIVER INPUT AREA                                     *         
*                                                                     *         
*EXIT   0-N    -  REQUESTED FIELD DATA (LENGTH FROM GLARGS+3)         *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
IESTPG   NMOD1 0,**#IEPG                                                        
*                                                                               
         USING SPOOLD,R8           SPOOL WORKAREA                               
         USING SYSD,R9             PRWRITER WORKAREA                            
         USING GLOBALD,RA          DRIVER AREA                                  
*                                                                               
         L     RC,GLAWORKD         RESET WORKING STORAGE POINTER                
         USING GEND,RC                                                          
*                                                                               
         LA    R7,PBLOCK           ESTABLISH PRNTBLOCK                          
         USING PBLOCK,R7                                                        
*                                                                               
         L     R4,AIO3             POINT TO IO3                                 
*                                                                               
         CLI   GLARGS+1,C'6'                                                    
         BNE   *+12                                                             
         CLI   3(R4),PGSTKIDQ      PG ESTIMATE RECORD (X'0A') ?                 
         BE    IESTPGE             YES - PROCESS                                
*                                                                               
         CLI   GLARGS+1,C'7'                                                    
         BNE   IESTPGX             GET OUT                                      
         CLI   3(R4),X'0B'         GF ESTIMATE RECORD (X'0B') ?                 
         BNE   IESTPGX             NO - GET OUT                                 
*                                                                               
IESTPGE  DS    0H                                                               
         LA    R4,PGSTKEDQ(R4)     FIRST ELEMENT                                
         USING PGSTELEM,R4                                                      
*                                                                               
IESTPGL  DS    0H                                                               
         CLI   PGSTELEM,0          END-OF-RECORD ?                              
         BE    IESTPGX             YES - NOTHING FOUND                          
         CLC   PGSTESEQ,GLARGS+2   FIELD WE WANT ?                              
         BE    IESTPGO             YES                                          
         LA    R4,PGSTELNQ(R4)     TEST NEXT ELEMENT                            
         B     IESTPGL                                                          
*                                                                               
IESTPGO  DS    0H                                                               
         ZIC   R1,GLARGS+3         LENGTH OF SELECTED FIELD                     
         BCTR  R1,0                PREP FOR EXECUTED MOVE                       
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),PGSTDATA    PASS BACK DATA                               
*                                                                               
IESTPGX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         DROP                                                                   
*                                                                               
         TITLE   'PRWRIEST DSECTS / STORAGE'                                    
* PRWRIWORKD                                                                    
         PRINT   OFF                                                            
       ++INCLUDE PRWRIWORKD                                                     
         PRINT   ON                                                             
* DRGLOBAL                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DRGLOBAL                                                       
         PRINT   ON                                                             
* DRIVETABLE                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRIVETABLE                                                     
         PRINT   ON                                                             
* DRINTRECD2                                                                    
         PRINT   OFF                                                            
       ++INCLUDE DRINTRECD2                                                     
         PRINT   ON                                                             
* DDSPOOLD                                                                      
         PRINT   OFF                                                            
       ++INCLUDE DDSPOOLD                                                       
         PRINT   ON                                                             
* DDSPLWORKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSPLWORKD                                                     
         PRINT   ON                                                             
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT   ON                                                             
* PRGLOBEQUS                                                                    
         PRINT OFF                                                              
       ++INCLUDE PRGLOBEQUS                                                     
         PRINT   ON                                                             
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'039PRWRIEST  08/18/08'                                      
         END                                                                    
