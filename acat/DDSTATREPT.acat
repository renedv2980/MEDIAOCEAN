*          DATA SET DDSTATREPT AT LEVEL 008 AS OF 06/03/14                      
*CATALP STATREPT                                                                
         TITLE 'STATREPT - ADPAK STATISTICS REPORT'                             
         PRINT NOGEN                                                            
STATREPT CSECT                                                                  
         ENTRY SSB                                                              
         ENTRY UTL                                                              
         NBASE WRKX-WRKD,STATREPT,WORK=A(STATWORK)                              
         USING WRKD,RC                                                          
*                                                                               
GETSVP   ST    R1,ACOMRG           SAVE MVS PARM ADDR                           
         MVI   UPSI,0                                                           
         L     R1,0(R1)                                                         
         LH    R2,0(R1)            R2=L'PARM DATA                               
         LTR   R2,R2                                                            
         BZ    GETSVPX                                                          
         CHI   R2,8                                                             
         BNH   *+8                                                              
         LA    R2,8                                                             
         LA    R1,2(R1)            R1=A(PARM DATA)                              
         LA    RF,GETSVPT                                                       
GETSVP1  CLI   0(R1),C','                                                       
         BE    GETSVPX                                                          
         CLI   0(R1),C'0'                                                       
         BE    GETSVP2                                                          
         CLI   0(R1),C'1'                                                       
         BNE   GETSVP2                                                          
         OC    UPSI,0(RF)                                                       
GETSVP2  LA    R1,1(R1)                                                         
         LA    RF,1(RF)                                                         
         BCT   R2,GETSVP1                                                       
         B     GETSVPX                                                          
GETSVPT  DC    X'8040201008040201'                                              
GETSVPX  EQU   *                                                                
*                                                                               
INIT0    TM    UPSI,X'01'          SET TEST SYSTEM DDSIO AND DSPACE             
         BZ    INIT0X                                                           
         LT    RF,=V(DDSIO)                                                     
         BZ    *+10                                                             
*&&UK*&& MVC   0(8,RF),=CL8'DDSIO'                                              
*&&US*&& MVC   0(8,RF),=CL8'DDSION'                                             
         L     RF,=V(SSB)                                                       
         MVI   SSODSPAC-SSOOFF(RF),C'T'                                         
INIT0X   EQU   *                                                                
*                                  OPEN STATS FILE AND BUILD INDEX              
OPEN     LA    R2,DMCB                                                          
         GOTO1 =V(DMOD000),DMCB,V(OPEN),0,0,V(STATS)                            
*                                                                               
OPEN2    XC    DSKADR,DSKADR       READ PROGRAM INDEX                           
         L     R6,=A(PRGNDX)                                                    
         ST    R6,APRGNDX                                                       
         SH    R6,=H'16'                                                        
         LR    R0,R6                                                            
         BAS   R9,SSTATS                                                        
         LR    R1,R6                                                            
         AH    R1,=H'2416'                                                      
         MVC   0(12,R1),=12X'FF'                                                
         LH    R7,2(R6)                                                         
         BCTR  R7,0                R7=NUM OF TRM INDEX RECS                     
         L     R6,=A(TRMNDX)                                                    
         ST    R6,ATRMNDX                                                       
*                                                                               
OPEN4    SH    R6,=H'16'           READ TERMINAL INDEX                          
         MVC   IOBUFF(16),0(R6)                                                 
         LR    R0,R6                                                            
         BAS   R9,SSTATS                                                        
         MVC   0(16,R6),IOBUFF                                                  
         AH    R6,=H'2416'                                                      
         BCT   R7,OPEN4                                                         
         MVC   0(12,R6),=12X'FF'                                                
*                                                                               
OPEN6    LA    R2,IOBUFF           READ GRAND TOTAL REC & SAVE DATE             
         USING STATRECD,R2                                                      
         LR    R0,R2                                                            
         BAS   R9,SSTATS                                                        
         MVC   DATE,SDDATE                                                      
         MVC   COUNT,STCOUNT                                                    
         EJECT                                                                  
NEXT     L     RA,=V(CPRINT)       INITITALIZE FOR START OF NEW REPORT          
         USING DPRINT,RA                                                        
         MVC   TITLE,=CL60'ADPAK STATISTICS REPORT - '                          
         L     RF,APRGNDX                                                       
         SH    RF,=H'4'                                                         
         CLI   0(RF),C'A'          TEST IF FACPAK NAME IN INDEX HDR             
         BL    NEXT1                                                            
         CLI   3(RF),C' '                                                       
         BNE   NEXT0                                                            
         MVI   TITLE,C' '          3 CHR FACPAK NAME                            
         MVI   TITLE+1,C' '                                                     
         MVC   TITLE+2(3),0(RF)                                                 
         B     NEXT1                                                            
NEXT0    MVI   TITLE,C' '          4 CHR FACPAK NAME                            
         MVC   TITLE+1(4),0(RF)                                                 
NEXT1    MVC   MID1,SPACES                                                      
         MVC   MID2,SPACES                                                      
         MVC   MID3,SPACES                                                      
         MVC   MID4,SPACES                                                      
         ZAP   LINE,=P'75'                                                      
         ZAP   PAGE,=P'1'                                                       
*                                                                               
NEXT2    LA    R3,IOBUFF           READ CARD FOR PARAMETERS                     
         GOTO1 =V(CARDS),DMCB,(R3),=C'RE00'                                     
         CLC   0(2,R3),=C'/*'                                                   
         BNE   PARAM1                                                           
         XBASE                                                                  
*                                                                               
PARAM1   CLC   0(3,R3),=C'TRM'     P1 -  1=TRMS 2=PRGS                          
         BNE   PARAM1A                                                          
         MVI   P1,1                                                             
         MVC   MID1+1(15),=C'LINE-ID ADDRESS'                                   
         MVC   MID2+1(15),=C'------- -------'                                   
         B     PARAM2                                                           
PARAM1A  CLC   0(3,R3),=C'PRG'                                                  
         BNE   PARAM1B                                                          
         MVI   P1,2                                                             
         MVC   MID1+1(15),=C'SYSTEM  PROGRAM'                                   
         MVC   MID2+1(15),=C'------- -------'                                   
         B     PARAM2                                                           
PARAM1B  B     ERROR1                                                           
*                                                                               
PARAM2   CLC   4(3,R3),=C'DAY'     P2 - 0=DAILY 1=CUMULATIVE                    
         BNE   PARAM2A                                                          
         MVI   P2,0                                                             
         MVC   TITLE+26(12),=C'DAILY TOTALS'                                    
         LA    R4,SDPRD0-SHDRFLDS                                               
         ST    R4,P2INDEX                                                       
         B     PARAM3                                                           
PARAM2A  CLC   4(3,R3),=C'CUM'                                                  
         BNE   PARAM2B                                                          
         MVC   TITLE+26(14),=C'ONE DAYS TOTAL'                                  
         LH    R0,COUNT                                                         
         CVD   R0,DUB                                                           
         UNPK  TITLE+26(3),DUB                                                  
         OI    TITLE+28,X'F0'                                                   
         LA    R4,STPRD0-SHDRFLDS                                               
         ST    R4,P2INDEX                                                       
         B     PARAM3                                                           
PARAM2B  B     ERROR2                                                           
*                                                                               
PARAM3   EQU   *                                                                
*                                                                               
PARAMX   MVC   MID1+18(88),MID1FLD                                              
         MVC   MID2+18(88),MID2FLD                                              
         MVC   MID3+18(88),MID3FLD                                              
         EJECT                                                                  
SPEC     LA    R2,IOBUFF           TRM AND/OR PRG DISPLAY                       
         XC    LASTHDR,LASTHDR                                                  
         CLI   P1,1                                                             
         BNE   SPEC2                                                            
         L     R3,ATRMNDX          R3&R4 USED FOR TERMINAL INDEX                
         LA    R4,12                                                            
         B     SPEC6                                                            
SPEC2    L     R3,APRGNDX          R3&R4 USED FOR PROGRAM INDEX                 
         LA    R4,21                                                            
         SR    R3,R4                                                            
         B     SPEC6                                                            
*                                                                               
SPEC6    AR    R3,R4               BUMP TO NEXT INDEX ENTRY                     
         LA    R5,0(R3,R4)                                                      
         SH    R5,=H'4'                                                         
         MVC   DSKADR,0(R5)        EXTRACT DISK ADDRESS                         
         CLI   0(R3),X'FF'                                                      
         BE    SPEC20                                                           
         LR    R0,R2                                                            
         BAS   R9,RSTATS           READ RECORD                                  
*                                                                               
SPEC8    CLI   P2,0                CHECK DAYLY FOR DATE                         
         BNE   SPEC10                                                           
         CLC   SDDATE,DATE                                                      
         BNE   SPEC6                                                            
*                                                                               
SPEC10   CLC   SHNAME1,LASTHDR+2   SAME MAJOR ID                                
         BE    SPEC12              YES                                          
         CLI   LASTHDR,0           FIRST TIME                                   
         BE    SPEC22              YES                                          
         B     SPEC20                                                           
*                                                                               
SPEC12   MVC   P+9(7),SHNAME2                                                   
         LA    RE,ROW1                                                          
         ST    RE,ROWADR                                                        
         BAS   RE,BLDROW           BUILD ROW#1                                  
         BAS   RE,ADDROW           ADD ROW#1 TO ROW#0                           
         BAS   RE,EDTROW           EDIT ROW#1                                   
         MVC   SPACING,=C'BL01'                                                 
         GOTO1 =V(PRINTER)                                                      
         MVC   LASTHDR,SHDRFLDS                                                 
         B     SPEC6                                                            
*                                                                               
SPEC20   MVC   P+1(15),=C'        -------'                                      
         MVC   P+18(88),PFLD                                                    
         GOTO1 =V(PRINTER)                                                      
         MVC   P+9(7),=C'TOT/AVG'                                               
         LA    RE,ROW0                                                          
         ST    RE,ROWADR                                                        
         BAS   RE,AVGROW                                                        
         BAS   RE,EDTROW                                                        
         MVC   SPACING,=C'BL02'                                                 
         GOTO1 =V(PRINTER)         PRINT TOTALS/AVERAGES                        
*                                                                               
SPEC22   MVC   P+1(7),SHNAME1                                                   
         XC    ADDCNT,ADDCNT       CLEAR ROW#0                                  
         XC    ROW0(60),ROW0                                                    
         CLI   0(R3),X'FF'                                                      
         BE    SPECX               CONTROL BREAK CAUSED BY END OF INDEX         
         MVC   LASTHDR,SHDRFLDS                                                 
         B     SPEC10                                                           
SPECX    B     NEXT                                                             
         EJECT                                                                  
ERROR1   MVC   P+1(15),=C'PARAM#1 INVALID'                                      
         B     ERRORX                                                           
ERROR2   MVC   P+1(15),=C'PARAM#2 INVALID'                                      
         B     ERRORX                                                           
*                                                                               
ERRORX   MVC   P+17(80),IOBUFF                                                  
         GOTO1 =V(PRINTER)                                                      
         B     NEXT                                                             
         EJECT                                                                  
RSTATS   LA    R8,DMRDIR           STATISTICS FILE I/O                          
         B     IOSTATS                                                          
SSTATS   LA    R8,DMRSEQ                                                        
*                                                                               
IOSTATS  GOTO1 =V(DATAMGR),DMCB,(R8),STATS,DSKADR,(R0)                          
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                DIE IF DISK ERROR                            
         BR    R9                                                               
         EJECT                                                                  
BLDROW   NTR1                      BUILD ROW IN PRINT LINE SEQUENCE             
         L     RE,ROWADR                                                        
         LA    R3,IOBUFF                                                        
         A     R3,P2INDEX                                                       
         USING STATPRDD,R3                                                      
*                                                                               
         MVC   0(4,RE),SPTRCTR                                                  
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPIQLEN                                                  
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPOQLEN                                                  
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPIOCTR                                                  
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPOVCTR                                                  
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPCPUTM                                                  
         LA    RE,4(RE)                                                         
         L     RF,SPTRTM                                                        
         S     RF,SPCPUTM                                                       
         ST    RF,0(RE)                                                         
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPTRTM                                                   
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPIQTM                                                   
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPIQFREQ                                                 
         LA    RE,4(RE)                                                         
         MVC   0(4,RE),SPOQFREQ                                                 
         LA    RE,4(RE)                                                         
         DROP  R3                                                               
*                                                                               
BLDROWX  XIT1                                                                   
         EJECT                                                                  
ADDROW   NTR1                      ADD ROW#1 TO ROW#0                           
         L     RE,ADDCNT                                                        
         LA    RE,1(RE)                                                         
         ST    RE,ADDCNT                                                        
         L     R2,ROW1             R2=NUM OF TRANS                              
         L     R0,ROW0                                                          
         AR    R0,R2                                                            
         ST    R0,ROW0             UPDATE TRANS TOTAL                           
         LA    RE,ROW0+4                                                        
         LA    RF,ROW1+4                                                        
         LA    R3,10                                                            
ADDROW1  L     R1,0(RF)            WEIGHT BY NUM OF TRANS                       
         MR    R0,R2                                                            
         A     R1,0(RE)                                                         
         ST    R1,0(RE)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R3,ADDROW1                                                       
ADDROWX  XIT1                                                                   
*                                                                               
AVGROW   NTR1                      AVERAGE ROW#0                                
         L     R0,ROW0             DIVIDE BY TRANS TOTAL                        
         LTR   R0,R0                                                            
         BZ    AVGROWX                                                          
         LA    R5,ROW0+4                                                        
         LA    R1,10                                                            
AVGROW1  L     RE,0(R5)                                                         
         SRDA  RE,31                                                            
         DR    RE,R0                                                            
         AH    RF,=H'1'                                                         
         SRA   RF,1                                                             
         ST    RF,0(R5)                                                         
         LA    R5,4(R5)                                                         
         BCT   R1,AVGROW1                                                       
AVGROWX  XIT1                                                                   
         EJECT                                                                  
EDTROW   NTR1                      EDIT ROW @ ROWADR INTO PRINT LINE            
         LA    R3,P                                                             
         USING PLINED,R3                                                        
         L     R4,ROWADR                                                        
*                                                                               
         L     R6,0(R4)                                                         
         EDIT  (R6),(7,PNOT-1),COMMAS=YES,ZERO=NOBLANK                          
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMIL),ZERO=NOBALNK                                       
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMOL),COMMAS=YES,ZERO=NOBLANK                            
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMNIO),1                                                 
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMOC),1                                                  
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMCT),COMMAS=YES,ZERO=NOBLANK                            
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMIOT),COMMAS=YES,ZERO=NOBLANK                           
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMTT),COMMAS=YES,ZERO=NOBLANK                            
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PMIQT),COMMAS=YES,ZERO=NOBLANK                           
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
         EDIT  (R6),(6,PIQF),2                                                  
         LA    R4,4(R4)                                                         
         L     R6,0(R4)                                                         
*NOP*    EDIT  (R6),(6,POQF),2                                                  
*                                                                               
EDTROWX  XIT1                                                                   
         EJECT                                                                  
MID1FLD  DC    CL88'  NUMBER    MEAN    MEAN    MEAN    MEAN    MEAN   X        
                MEAN    MEAN    MEAN   INPUT  OUTPUT'                           
MID2FLD  DC    CL88'      OF   INPUT  OUTPUT  NUMBER  OVRLAY     CPU   X        
                 I/O   TRANS   INPUT   QUEUE   QUEUE'                           
MID3FLD  DC    CL88'   TRANS  LENGTH  LENGTH  OF I/O   CALLS    TIME   X        
                TIME    TIME  Q-TIME    FREQ    FREQ'                           
PFLD     DC    CL88'  ------  ------  ------  ------  ------  ------  -X        
               -----  ------  ------  ------  ------'                           
         SPACE 1                                                                
DMRSEQ   DC    CL8'DMRSEQ'                                                      
DMRDIR   DC    CL8'DMRDIR'                                                      
STATS    DC    CL8'STATS'                                                       
         SPACE 1                                                                
         LTORG                                                                  
         SPACE 1                                                                
         DS    4F                                                               
PRGNDX   DS    110CL21             MAX 110 PROGRAMS                             
         DS    XL90                SPARE AT END OF RECORD                       
         DS    XL12                                                             
         SPACE 1                                                                
         DS    4F                                                               
TRMNDX   DS    12000CL12           MAX 12000 TERMINALS                          
         DS    XL12                                                             
         SPACE 1                                                                
STATWORK DS    0D                                                               
         DS    1024D                                                            
*                                                                               
SSB      DS    0D                                                               
         DC    X'00000002',XL28'00'                                             
*                                                                               
UTL      DS    0D                                                               
         DC    F'0',X'01',XL27'00'                                              
         EJECT                                                                  
WRKD     DSECT                                                                  
DUB      DS    D                                                                
DMCB     DS    6F                                                               
DSKADR   DS    F                                                                
ACOMRG   DS    A                                                                
UPSI     DS    X                                                                
         DS    XL3                                                              
P1       DS    F                                                                
P2       DS    F                                                                
P2INDEX  DS    F                                                                
APRGNDX  DS    A                                                                
ATRMNDX  DS    A                                                                
ROWADR   DS    A                                                                
ADDCNT   DS    F                                                                
DATE     DS    CL6                                                              
COUNT    DS    H                                                                
LASTHDR  DS    CL16                                                             
WORK     DS    CL20                                                             
*                                                                               
ROW0     DS    15F                                                              
ROW1     DS    15F                                                              
*                                                                               
IOBUFF   DS    2416C                                                            
*                                                                               
WRKX     DS    0C                                                               
         EJECT                                                                  
PLINED   DSECT                                                                  
         DS    CL20                                                             
PNOT     DS    CL6                                                              
         DS    CL2                                                              
PMIL     DS    CL6                                                              
         DS    CL2                                                              
PMOL     DS    CL6                                                              
         DS    CL2                                                              
PMNIO    DS    CL6                                                              
         DS    CL2                                                              
PMOC     DS    CL6                                                              
         DS    CL2                                                              
PMCT     DS    CL6                                                              
         DS    CL2                                                              
PMIOT    DS    CL6                                                              
         DS    CL2                                                              
PMTT     DS    CL6                                                              
         DS    CL2                                                              
PMIQT    DS    CL6                                                              
         DS    CL2                                                              
PIQF     DS    CL6                                                              
         DS    CL2                                                              
POQF     DS    CL6                                                              
         DS    CL4                                                              
         EJECT                                                                  
       ++INCLUDE DDSTATREC                                                      
                                                                                
       ++INCLUDE DDDPRINT                                                       
                                                                                
* FASSBOFF                                                                      
SSOOFFD  DSECT                                                                  
       ++INCLUDE FASSBOFF                                                       
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'008DDSTATREPT06/03/14'                                      
         END                                                                    
