*          DATA SET PZADDINSV  AT LEVEL 054 AS OF 05/01/02                      
*CATALP PZADDINV                                                                
*        TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE'                            
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - CHANGE LOG'               
***********************************************************************         
*                                                                     *         
*        USED WITH PPZ502 - LINK WITH LKPPZ502                        *         
*                                                                     *         
***********************************************************************         
*                                                                     *         
*        CHANGE LOG                                                   *         
*                                                                     *         
*  BOBY NOV/94    CREATION                                            *         
*                                                                     *         
***********************************************************************         
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - INIT'                     
***********************************************************************         
*                                                                     *         
*        INITIALIZATION                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         PRINT NOGEN                                                            
PZADDINV CSECT                                                                  
         NMOD1 PZADIDL,**PZADI                                                  
*                                                                               
         LA    RA,COMSUBS          ESTABLISH COMMON SUBROUTINES                 
         USING COMSUBS,RA                                                       
*                                                                               
         USING PZADID,RC           ESTABLISH WORKING STORAGE                    
*                                                                               
         L     R7,0(R1)            A(EZBLOCK)                                   
         USING EZBLOCKD,R7         ESTABLISH INCOMING EZBLOCK                   
*                                                                               
         L     R8,4(R1)            A(PPWORKD)                                   
         USING PPWORKD,R8          ESTABLISH REPORT WORKING STORAGE             
         L     R9,PPWORK2C         POINT TO SECOND PART OF STORAGE              
         USING PPWORK2D,R9         ESTABLISH SECOND REPORT WORKING STG          
*                                                                               
         MVI   EZERRCD,0           CLEAR ERROR                                  
*                                                                               
         CLI   ADSCTL,ADS1STQ      SKIP IF NOT FIRST TIME                       
         BNE   *+12                                                             
         BAS   RE,MININIT             INIT STORAGE AREAS                        
         MVI   ADSCTL,0               CLEAR FIRST TIME SWITCH                   
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - MODE'                     
***********************************************************************         
*                                                                     *         
*        DETERMINE MODE                                               *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         CLI   EZMODE,EZINVP       PROC INV                                     
         BE    PRINV                                                            
*                                                                               
         CLI   EZMODE,EZSPTP       PROCESS PRINT DETAIL                         
         BE    PRANN                                                            
*                                                                               
         CLI   EZMODE,EZINVL       LAST FOR INVOICE                             
         BE    LINV                                                             
*                                                                               
EXIT     DS    0H                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRINV'                    
***********************************************************************         
*                                                                     *         
*        PROCESS INVOICE HEADER                                       *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRINV    DS    0H                                                               
*                                                                               
*        SAVE HEADER RECORD FOR INVOICE LAST PROCESSING                         
*                                                                               
         LA    R0,SAVINVH          A(INVOICE HEADER SAVE AREA)                  
         L     RE,EZWKRREC         A(INVOICE HEADER RECORD)                     
         LH    R1,0(RE)            INVOICE HEADER LENGTH                        
         CH    R1,=Y(L'SAVINVH)                                                 
         BNH   *+6                                                              
         DC    H'0'                INVOICE HEADER RECORD TOO LONG               
         LR    RF,R1               LENGTH OF SAVEAREA TO USE                    
         MVCL  R0,RE               SAVE INVOICE HEADER RECORD                   
*                                                                               
*        SAVE WORKER BUFFER FOR INVOICE LAST PROCESSING                         
*                                                                               
         L     R0,=A(SAVWKBUF)     A(WORKER BUFFER SAVE AREA)                   
         LH    R1,=Y(L'SAVWKBUF)   SAVE AREA LENGTH                             
         L     RE,EZWKRBUF         A(WORKER BUFFER)                             
         LR    RF,R1               LENGTH OF SAVEAREA TO USE                    
         MVCL  R0,RE               SAVE WORKER BUFFER AREA                      
*                                                                               
         LA    RF,PRDBUFF          RESET PRODUCT BUFFER                         
         XC    0(L'PINVPRD,RF),0(RF)                                            
*                                                                               
PRINVX   DS    0H                                                               
         XIT1                                                                   
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRANN'                    
***********************************************************************         
*                                                                     *         
*        PROCESS ANNOUNCEMENT                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRANN    DS    0H                                                               
*                                                                               
         OC    EZIHLADC,EZIHLADC   TEST HAVE ADVERTISER                         
         BZ    PRERRANF                                                         
*                                                                               
         TM    EZIHLKST,EZIHLANF   AND IS OK PRINTPAK CODE                      
         BO    PRERRANF                                                         
*                                                                               
         MVI   HDRSQ,0             INIT HEADER SEQUENCE NUMBER                  
         MVI   HDRSQOLD,0          INIT OLD HEADER SEQUENCE NUMBER              
*                                                                               
*        BUILD MASTER INVOICE KEY                                               
*                                                                               
         LA    R5,MINBLK                                                        
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         MVI   MINOPEN,C'N'        FORCE NEW RECORD                             
*                                                                               
         XC    MINMKEY,MINMKEY                                                  
         LA    R4,MINMKEY          ESTABLISH NEW INVOICE RECORD KEY             
         USING PINVKEY,R4                                                       
*                                                                               
         MVC   PINVAGY,EZAGY       AGENCY ALPHA CODE                            
         MVC   PINVMED,EZMED       MEDIA                                        
         MVI   PINVTYPE,PINVTYPQ   RECORD TYPE                                  
*                                                                               
         MVC   PINVCLT,EZIHCVAD    USE OVERRIDE CLIENT                          
         CLC   PINVCLT,SPACES      IF CLIENT IS MISSING                         
         BH    *+10                                                             
         MVC   PINVCLT,EZIHLADC       USE LOOKED UP CLIENT                      
*                                                                               
         CLC   PINVCLT,SPACES      IF CLIENT IS MISSING                         
         BNH   PRERRANF               ERROR                                     
*                                                                               
         MVC   PINVPRD,EZIHCVPR    SET TO HEADER OVERRIDE PRODUCT               
         CLC   PINVPRD,SPACES      IF PRODUCT IS MISSING                        
         BH    *+10                                                             
         MVC   PINVPRD,EZIHLPRC       SET TO HEADER LOOKED UP PRODUCT           
         CLC   PINVPRD,SPACES      IF PRODUCT IS MISSING                        
         BH    *+10                                                             
         MVC   PINVPRD,EZSPCVPR       USE DETAIL OVERRIDE PRODUCT               
         CLC   PINVPRD,SPACES      IF PRODUCT IS MISSING                        
         BH    *+10                                                             
         MVC   PINVPRD,EZSPLPRC       SET TO LOOKED UP PRODUCT                  
*                                                                               
         CLC   PINVPRD,SPACES      IF PRODUCT IS MISSING                        
         BNH   PRERRPNF               ERROR                                     
*                                                                               
         MVC   PINVPUB,EZSNPUB     PUB/ZONE/EDN                                 
*                                                                               
         MVC   PINVYS,EZIHDMOS+5   YEAR OF SERVICE                              
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         LA    R1,PINVKEY             DUMP KEY                                  
         BAS   RE,DMPREC                                                        
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINOPN',MINBLKD)    OPEN FILE MINIO BLCK         
*                                                                               
         CLI   MINERR,MINESNF      SKIP IF RECORD NOT FOUND                     
         BE    PRHDRDN                                                          
*                                                                               
         CLI   MINERR,0            NO OTHER ERRORS TOLERTED                     
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         TM    MINSTAT,MINDELQ     IF RECORD SET IS DELETED                     
         BNO   PRANOLDX                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINRSF',MINBLKD)  RESTORE MINIO SET              
*                                                                               
PRANOLDX DS    0H                                                               
*                                                                               
         EJECT                                                                  
*                                                                               
*        FIND NEXT AVAILABLE HEADER SEQUENCE NUMBER                             
*        OR CURRENT SEQUENCE NUMBER                                             
*                                                                               
PRHDRSQ  DS    0H                                                               
*                                                                               
         XC    DTLSQ,DTLSQ         INIT DETAIL SEQUENCE NUMBER                  
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          ESTABLISH ELEMENT WORKAREA                   
         USING PIMHDREL,R2         AS INVOICE HEADER ELEMENT                    
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   SET ELEMENT IDENTIFIER                       
         MVI   PIMHDRLN,PIMHDRSQ-PIMHDREL  LOOK AT ALL HEADER ELMS              
*                                                                               
PRHDRLP  DS    0H                                                               
*                                                                               
         GOTO1 GETEL,DMCB,MINBLKD,PINVELM,0 FIND HEADER ELM                     
*                                                                               
         ICM   R2,15,8(R1)         POINT TO FOUND ELEMENT                       
         BZ    PRHDRDN             EOF - NEW INVOICE                            
*                                                                               
         CLI   HDRSQ,0             IF NO HEADER SEQUENCE FOUND YET              
         BNE   *+10                                                             
         MVC   HDRSQ,PIMHDRSQ         SAVE THE ONE FROM FOUND ELEMENT           
*                                                                               
         CLI   EZESTOPT,C'Y'       SKIP IF NOT COPYING EST NUMS.                
         BNE   PRHDRESX                                                         
*                                                                               
         OC    EZIHBEST,EZIHBEST   IF HEADER ESTIMATE AVAILABLE                 
         BZ    *+18                                                             
         CLC   PIMEST,EZIHBEST        MATCH TO HEADER ESTIMATE                  
         BE    PRHDRESX                                                         
         B     PRHDRCN                ELSE NO MATCH                             
*                                                                               
         CLC   PIMEST,EZSPBEST     ELSE MATCH TO DETAIL ESTIMATE                
         BE    PRHDRESX                                                         
*                                                                               
         B     PRHDRCN             NO MATCH                                     
*                                                                               
PRHDRESX DS    0H                                                               
*                                                                               
         MVC   WKINVNO,PIMINVNO       SPACE FILL INVOICE NUMBER                 
         OC    WKINVNO,SPACES                                                   
*                                                                               
         CLC   WKINVNO(10),EZIHINV    MATCH ON INVOICE NUMBER                   
         BE    PRHDRFD                                                          
*                                                                               
PRHDRCN  DS    0H                                                               
*                                                                               
         SR    RF,RF               BUMP SEQUENCE NUMBER                         
         IC    RF,PIMHDRSQ                                                      
         LA    RF,1(RF)                                                         
         STC   RF,PIMHDRSQ-PIMHDREL+PINVELM                                     
*                                                                               
         CLI   PIMHDRSQ-PIMHDREL+PINVELM,0 IF ZERO THEN ONLY 1 HDR (FF)         
         BE    PRHDRDN                                                          
*                                                                               
         B     PRHDRLP                                                          
*                                                                               
         EJECT                                                                  
*                                                                               
*        HEADER ALREADY EXISTS                                                  
*           ALLOWED IF REPLACING INVOICES OR NOT FIRST TIME FOR PRODUCT         
*                                                                               
PRHDRFD  DS    0H                                                               
*                                                                               
         MVC   HDRELMSV,PIMHDREL   SAVE HEADER ELEMENT FOR LATER                
*                                                                               
         MVC   HDRSQOLD,PIMHDRSQ      SAVE SEQUENCE NUMBER                      
*                                                                               
         LA    RF,PRDBUFF          POINT TO PRODUCT BUFFER                      
*                                                                               
         OC    0(L'PINVPRD,RF),0(RF) IF AT END OF BUFFER                        
         BZ    *+22                     THEN FIRST TIME FOR PRODUCT             
         CLC   PINVPRD,0(RF)       OKAY IF NOT 1ST TIME FOR PRODUCT             
         BE    PRHDROLD               NOT FIRST TIME                            
         LA    RF,L'PINVPRD(RF)    NEXT PRODUCT IN BUFFER                       
         B     *-24                                                             
*                                                                               
*        HEADER ALREADY EXISTS AND 1ST TIME FOR PRODUCT                         
*                                                                               
         CLI   EZRPLOPT,C'Y'       ERROR IF NOT ALLOWED TO REPLACE INV          
         BNE   PRERRDUP                                                         
*                                                                               
*        DELETE INVOICE FROM FILE                                               
*                                                                               
PRANDEL  DS    0H                                                               
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         LA    R1,MINMKEY             DUMP KEY                                  
         BAS   RE,DMPREC                                                        
*                                                                               
         LA    R2,PINVELM          ESTABLISH AS HEADER ELEMENT                  
         USING PIMHDREL,R2                                                      
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   SET KEY FOR THIS INVOICE                     
         MVC   PIMHDRSQ,HDRSQOLD   SET SEQUENCE NUMBER                          
*                                                                               
         GOTO1 GETEL,DMCB,MINBLKD,PINVELM,0 READ FOR HEADER                     
*                                                                               
         ICM   RF,15,8(R1)         POINT TO FOUND ELEMENT                       
         BZ    PRANDELX            NONE FOUND                                   
*                                                                               
         GOTO1 DELEL,DMCB,MINBLKD,PINVELM,0   DELETE HEADER                     
*                                                                               
*        DELETE ALL DETAILS AND COMMENTS FOR INVOICE                            
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY                             
*                                                                               
         MVI   MINEKEY,PIMDTLEQ       SET DETAIL ELEMENT ID                     
         MVC   MINEKEY+1(1),HDRSQOLD  SET HEADER SEQUENCE NUMBER                
*                                                                               
         MVI   MINFILTL,PIMDTLS2-PIMDTLEL  IGNORE DTL SQN IN COMPARE            
*                                                                               
         LA    R0,MINHI            FIRST READ IS HIGH                           
*                                                                               
PRANDTLP DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)   READ NEXT ELEMENT                 
*                                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END OF TYPE              
         BNE   PRANDTDN                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)   DELETE ELEMENT                
*                                                                               
PRANDTCN DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           NEXT READ IS SEQUENTIAL                      
         B     PRANDTLP                                                         
*                                                                               
PRANDTDN DS    0H                                                               
*                                                                               
*        DELETE COMMENTS                                                        
*                                                                               
         MVI   MINEKEY,PIMCOMEQ       SET COMMENT ELEMENT ID                    
         MVC   MINEKEY+1(1),HDRSQOLD  SET HEADER SEQUENCE NUMBER                
*                                                                               
         MVI   MINFILTL,PIMCOMS2-PIMCOMEL  IGNORE DTL SQN IN COMPARE            
*                                                                               
         LA    R0,MINHI            FIRST READ IS HIGH                           
*                                                                               
PRANCMLP DS    0H                                                               
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)   READ NEXT ELEMENT                 
*                                                                               
         CLI   MINERR,0            TREAT ALL ERRORS AS END OF TYPE              
         BNE   PRANCMDN                                                         
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)   DELETE ELEMENT                
*                                                                               
PRANCMCN DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           NEXT READ IS SEQUENTIAL                      
         B     PRANCMLP                                                         
*                                                                               
PRANCMDN DS    0H                                                               
*                                                                               
PRANDELX DS    0H                                                               
*                                                                               
*        NEW INVOICE GROUP                                                      
*                                                                               
*        ADD PRODUCT TO BUFFER                                                  
*                                                                               
PRHDRDN  DS    0H                                                               
*                                                                               
         LA    RF,PRDBUFF          POINT TO PRODUCT BUFFER                      
*                                                                               
         OC    0(L'PINVPRD,RF),0(RF) TEST FOR END OF BUFFER                     
         BZ    *+12                                                             
         LA    RF,L'PINVPRD(RF)    NEXT PRODUCT IN BUFFER                       
         B     *-14                                                             
*                                                                               
         MVC   0(L'PINVPRD,RF),PINVPRD ADD PRODUCT TO BUFFER                    
         XC    L'PINVPRD(L'PINVPRD,RF),L'PINVPRD(RF) CLEAR NXT ENTRY            
*                                                                               
         OC    HDRSQOLD,HDRSQOLD   IF INVOICE ALREADY ON FILE                   
         BZ    *+14                                                             
         MVC   HDRSQ,HDRSQOLD         USE ITS SEQUENCE NUMBER                   
         B     PRHDRSQX                                                         
*                                                                               
         SR    RF,RF                                                            
         IC    RF,HDRSQ            ELSE DECREMENT FIRST ON FILE                 
         BCTR  RF,0                                                             
         STC   RF,HDRSQ                                                         
*                                                                               
PRHDRSQX DS    0H                                                               
*                                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - PRPIMHDR'                 
***********************************************************************         
*                                                                     *         
*        ADD INVOICE HEADER ELEMENT                                   *         
*        ADD ACTIVITY       ELEMENT                                   *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
PRPIMHDR DS    0H                                                               
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          BUILD INVOICE HEADER ELEMENT                 
         USING PIMHDREL,R2                                                      
*                                                                               
         MVI   PIMHDREL,PIMHDREQ   ELEMENT ID                                   
         MVI   PIMHDRLN,PIMHDRLQ   ELEMENT LENGTH                               
         MVC   PIMHDRSQ,HDRSQ      HEADER SEQUENCE NUMBER                       
*                                                                               
         CLI   EZESTOPT,C'Y'       SKIP IF NOT COPYING EST NUMS.                
         BNE   PRESX                                                            
*                                                                               
         MVC   PIMEST,EZIHBEST     USE HEADER ESTIMATE                          
*                                                                               
         OC    EZIHBEST,EZIHBEST   IF NO HEADER ESTIMATE                        
         BNZ   *+10                                                             
         MVC   PIMEST,EZSPBEST        USE DETAIL ESTIMATE                       
*                                                                               
PRESX    DS    0H                                                               
*                                                                               
         MVC   PIMINVDT,EZIHBIDT   INVOICE DATE                                 
*                                                                               
         MVC   PIMINVNO(10),EZIHINV  INVOICE NUMBER                             
*                                                                               
*        NULL FILL END OF INVOICE NUMBER                                        
*                                                                               
         LA    R0,L'PIMINVNO       NUMBER OF BYTES IN INVOICE NUMBER            
         LA    RF,PIMINVNO+L'PIMINVNO-1  LAST BYTE OF INVOICE NO.               
*                                                                               
         CLI   0(RF),C' '          IF NOT A SPACE                               
         BH    *+14                   DONE                                      
         MVI   0(RF),0             ELSE MAKE NULLS                              
         BCTR  RF,0                BACK UP A BYTE                               
         BCT   R0,*-14                                                          
*                                                                               
         CLC   EZIHDISD,SPACES     IF EITHER INVOICE START                      
         BNH   *+10                                                             
         CLC   EZIHDIED,SPACES     OR END DATE MISSING                          
         BH    PRHDRIDT                                                         
*                                                                               
*        USE MONTH OF SERVICE START AND END AS INVOICE DATES                    
*                                                                               
         XC    WORK,WORK                                                        
         LA    R4,WORK                                                          
         USING PERVALD,R4          ESTABLISH PERVAL WORKAREA                    
*                                                                               
         GOTO1 VPERVAL,DMCB,(6,EZIHDMOS),(R4) VALIDATE PERIOD                   
*                                                                               
         CLI   4(R1),0             CHECK FOR ERRORS                             
         BNE   PRHDRIDX                                                         
*                                                                               
         MVC   PIMSTDT,PVALBSTA   SAVE START AND END DATES                      
         MVC   PIMENDDT,PVALBEND                                                
*                                                                               
         B     PRHDRIDX                                                         
*                                                                               
PRHDRIDT DS    0H                                                               
*                                                                               
         GOTO1 DATCON,DMCB,(0,EZIHISDT),(3,PIMSTDT)  INVOICE START DATE         
*                                                                               
         GOTO1 DATCON,DMCB,(0,EZIHIEDT),(3,PIMENDDT) INVOICE END   DATE         
*                                                                               
PRHDRIDX DS    0H                                                               
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(3,PIMCREDT) DATE INVOICE ADDED                
*                                                                               
         MVC   PIMACTDT,PIMCREDT   DATE OF LAST ACTIVITY                        
         ZAP   PIMAMT,=P'0'        INIT INVOICE AMOUNT                          
*                                                                               
*****    GOTO1 ADDEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
         MVC   HDRELMSV,PIMHDREL   SAVE HEADER ELEMENT FOR LATER                
*                                                                               
         DROP  R2                                                               
*                                  SET ACTIVITY ELEM                            
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM          BUILD ACTIVITY ELEMENT                       
         USING ACTVD,R2                                                         
*                                                                               
         MVI   ACTVEL,X'F1'                                                     
         MVI   ACTVLEN,20                                                       
         GOTO1 DATCON,DMCB,(5,0),(3,ACTVADDT)   TODAY'S DATE                    
         MVC   ACTVADID,RCORIGID   USER ID                                      
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
*                                                                               
*        CREATE AND ADD DETAIL ELEMENT                                          
*                                                                               
PRHDROLD DS    0H                                                               
*                                                                               
         XC    PINVELM,PINVELM                                                  
         LA    R2,PINVELM                                                       
         USING PIMDTLEL,R2         ESTABLISH DETAIL ELEMENT                     
*                                                                               
*        BUILD DETAIL ELEMENT KEY                                               
*                                                                               
         MVI   PIMDTLEL,PIMDTLEQ   SET DETAIL ELEMENT CODE                      
         MVI   PIMDTLLN,PIMDTLLQ   SET DETAIL ELEMENT LENGTH                    
         MVC   PIMDTLS1,HDRSQ      SET HEADER SEQUENCE NUMBER                   
         SR    RF,RF                                                            
         ICM   RF,3,DTLSQ          BUMP TO NEXT SEQUENCE NUMBER                 
         LA    RF,1(RF)                                                         
         STCM  RF,3,PIMDTLS2                                                    
*                                                                               
*        BUILD DETAIL ELEMENT                                                   
*                                                                               
         MVC   PIMIDATE,EZIHBIDT   INVOICE DATE                                 
*                                                                               
         CLI   EZESTOPT,C'Y'       SKIP IF NOT COPYING EST NUMS.                
         BNE   PRANESX                                                          
*                                                                               
         MVC   PIMIEST,EZIHBEST    INVOICE ESTIMATE NUMBER                      
*                                                                               
PRANESX  DS    0H                                                               
*                                                                               
         MVC   PIMSPACE,EZSPSPCE   SPACE DESCRIPTION                            
*                                                                               
         ICM   RF,15,EZSPBLNS      IF LINES PRESENT                             
         BZ    *+12                                                             
         MVI   PIMUIND,C'L'           SET UNITS AS LINES                        
         B     PRANUNT                GO FORMAT UNITS                           
*                                                                               
         ICM   RF,15,EZSPBICS      IF INCHES PRESENT                            
         BNE   PRANUNTX                 (LOWERCASE I)                           
         MVI   PIMUIND,X'89'          SET UNITS AS INCHES                       
*                                                                               
PRANUNT  DS    0H                  SPACE DESCRIPTION IN UNITS                   
*                                                                               
         ICM   RE,15,EZSPBCLS      IF NUMBER OF COLUMNS PRESENT                 
         BZ    *+6                                                              
         MR    RE,RE                  CALCULATE TOTAL UNITS                     
*                                                                               
         CVD   RF,DUB              RF HAS TOTAL UNITS                           
         ZAP   PIMUNITS,DUB        SET UNITS TOTAL                              
*                                                                               
PRANUNTX DS    0H                                                               
*                                                                               
         ICM   RF,15,EZSPBGRS      SET COST                                     
         CVD   RF,DUB                                                           
         ZAP   PIMCOST,DUB                                                      
*                                                                               
         AP    PIMAMT-PIMHDREL+HDRELMSV,DUB  UPDATE INV TOTAL                   
*                                                                               
*        ACCUMULATE PREMIUM CHARGES                                             
*                                                                               
         LA    R0,3                MAX THREE PREMIUM CHARGES                    
         LA    R1,EZSPBPM1         FIRST PREMIUM                                
         SR    RF,RF               INIT ACCUMULATOR                             
*                                                                               
         ICM   RE,15,0(R1)         NEXT PREMIUM                                 
         AR    RF,RE               ACCUMULATE                                   
         LA    R1,4(R1)            NEXT CHARGE                                  
         BCT   R0,*-10                                                          
*                                                                               
         CVD   RF,DUB                                                           
         ZAP   PIMPREM,DUB         SET PREMIUM CHARGE                           
*                                                                               
         MVI   PIMCSIND,C' '       COST IS GROSS                                
*                                                                               
         ICM   RF,15,EZSPBCLS                                                   
         CVD   RF,DUB                                                           
         ZAP   PIMCLMS,DUB         SET NUMBER OF COLUMNS                        
*                                                                               
         GOTO1 ADDEL,DMCB,MINBLKD,PINVELM,0 ADD ELEMENT TO RECORD               
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,HDRELMSV,0 UPDATE HEADER ELEMENT              
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINCLS',MINBLK)    CLOSE FILE   RECORD           
*                                                                               
PRAX     DS    0H                                                               
*                                                                               
         B     EXIT                                                             
*                                                                               
         DROP  R2                                                               
*                                                                               
PRERRANF DS    0H                  CLIENT NOT FOUND                             
*                                                                               
         MVI   EZERRCD,EZERRANF                                                 
         B     PRERRX                                                           
*                                                                               
PRERRPNF DS    0H                  PRODUCT NOT FOUND                            
*                                                                               
         MVI   EZERRCD,EZERRPNF                                                 
         B     PRERRX                                                           
*                                                                               
PRERRDUP DS    0H                  DUPLICATE INVOICE                            
*                                                                               
         MVI   EZERRCD,EZERRDUP                                                 
         B     PRERRX                                                           
*                                                                               
PRERRX   DS    0H                                                               
*                                                                               
         BAS   RE,GETERRM                                                       
         MVI   EZMODE,EZINVL       SET TO SKIP TO NEXT INV                      
*                                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - LINV'                     
***********************************************************************         
*                                                                     *         
*        LAST FOR INVOICE                                             *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
LINV     DS    0H                                                               
*                                                                               
         LA    R5,MINBLK           ESTABLISH INV FILE MINIO BLOCK               
         USING MINBLKD,R5                                                       
*                                                                               
LINV90   DS    0H                  MARK INVOICE AS CONVERTED                    
*                                                                               
         CLI   EZTEST,C'Y'         TEST TO SUPPRESS MARKING                     
         BE    LINV96                                                           
*                               SET CONVERSION DATA IN INVOICE HEADER           
         LA    R3,SAVINVH+5        SAVED INVOICE HEADER                         
*                                  SKIP RECLEN(2),RECCODE(2),DELIM(1)           
         OI    0(R3),EZIHCVQ       INDICATE A CONVERTED INVOICE                 
         NI    0(R3),X'FF'-EZIHRCVQ  TURN OFF ANY RECONVERTED INDICATOR         
*                                                                               
         GOTO1 DATCON,DMCB,(5,0),(1,1(R3)) TODAY PWOS                           
*                                                                               
         MVC   4(2,R3),EZIHLADC    CLIENT                                       
         MVC   6(1,R3),EZIHLPRC    PRODUCT                                      
         OI    11(R3),X'80'        STOP TRAILING BLANK DELETION                 
*                                                                               
*                               NOTE- THE RDBLOCK IS NEEDED TO ENSURE           
*                                     WE WRITE BACK THE LATEST VERSION          
*                                     OF THE BLOCK                              
*                                                                               
         MVC   DMCB+16(4),=A(SAVWKBUF)                                          
*                                                                               
         CLI   EZWRITE,C'Y'        TEST IF WRITE ALLOWED                        
         BNE   LINV96                                                           
*                                                                               
*        FIND BLOCK POINTER FOR SAVED INVOICE HEADER                            
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'RDBLOCK',EZWKRFIL,EZWKRIND,SAVINVH               
*                                                                               
*        WRITE BACK UPDATED INVOICE HEADER                                      
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'WRITE',EZWKRFIL,EZWKRIND,SAVINVH                 
*                                                                               
LINV96   DS    0H                                                               
*                                                                               
LINVX    DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         TITLE 'PZADDINV - PROGRAM INITIALIZATION - MININIT'                    
***********************************************************************         
*                                                                     *         
*        MININIT - PROGRAM INITIALIZATION                             *         
*              INIT PRINT INVOICE RECORD MINIO BLOCK                  *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
MININIT  NTR1                                                                   
*                                                                               
         GOTO1 LOADER,DMCB,=CL8'T00A74',0,0                                     
         MVC   VMINIO,DMCB+4       SET MINIO ADDRESS                            
*                                                                               
         L     RF,=V(PERVERT)                                                   
         ST    RF,VPERVERT                                                      
*                                                                               
*        INITIALIZE MINIO VALUES                                                
*                                                                               
         LA    R0,MINBLK           CLEAR MINBLOCK                               
         LA    R1,MINBLKL                                                       
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         LA    R5,MINBLK           ESTABLISH MINIO BLOCK                        
         USING MINBLKD,R5                                                       
*                                                                               
         MVC   MINRECUP,=V(RECUP)  A(RECUP)                                     
         MVC   MINCOMF,EZCOMFCS    A(COMFACS)                                   
         MVI   MINOPEN,C'N'        SET NOT OPEN                                 
         MVC   MINFIL,INVFILE      FILE NAME                                    
         MVC   MINDIR,INVDIR       DIR NAME                                     
         MVI   MINFKLEN,L'PINVKEY  KEY LENGTH                                   
         MVI   MINEKLEN,L'PINVMINI ELEMENT KEY LENGTH                           
         MVI   MINEKDSP,L'PINVMAST DISPLACEMENT TO ELEMENT KEY                  
         MVI   MINNCTL,L'PINVSTAT  NUMBER OF CONTROL BYTES                      
         MVC   MINFRCLM,=AL2(L'PINVMBF1) MAXIMUM RECORD LENGTH                  
*                                                                               
         L     RF,=A(PINVMBF1)     POINT TO START OF MINIO BUFFERS              
         ST    RF,MINBUFF          A(FIRST BUFFER)                              
*                                                                               
         MVI   MINNBUF,2           USE TWO BUFFERS                              
*                                                                               
         L     RF,=A(PINVMRTB)                                                  
         ST    RF,MINRTAB          A(AREA FOR RECORD TABLE)                     
*                                                                               
         MVC   MINRTABL,=Y(L'PINVMRTB) LENGTH OF RECORD TABLE                   
*                                                                               
         L     RF,=A(PINVMELM)      A(AREA FOR ELEM OR CLUSTER)                 
         ST    RF,MINELEM                                                       
*                                                                               
         XC    0(L'PINVMELM,RF),0(RF)  CLEAR MINELEM AREA                       
*                                                                               
         MVC   MINMAXEL,=Y(L'PINVMELM)   MAX LENGTH OF ELEM OR CLUSTER          
*                                                                               
         MVI   MINDELSW,C'Y'       PROCESS DELETED RECORDS                      
*                                                                               
         CLI   EZWRITE,C'Y'        IF WRITES NOT ALLOWED                        
         BE    *+8                                                              
         MVI   MINWRITE,C'N'          DISABLE WRITES                            
*                                                                               
MININITX DS    0H                                                               
         XIT1                                                                   
*                                                                               
         DROP  R5                                                               
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - COMELM'                   
***********************************************************************         
*                                                                     *         
*        COMELM - FIND/SET CMML ELEMENTS IN BUFFER                    *         
*                                                                     *         
*NTRY    R5 ==>  A(MINBLK)                                            *         
*                                                                     *         
*EXIT                                                                 *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
         DS    0D                                                               
COMELM   NTR1                                                                   
*                                                                               
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         CLC   EZICCOM,=CL256' '   SKIP IF NO COMMENT AVAILABLE                 
         BNH   COMELMX                                                          
*                                                                               
         XC    PINVCELM,PINVCELM                                                
         LA    R2,PINVCELM         ESTABLISH COMMENT ELEMENT                    
         USING PIMCOMEL,R2                                                      
*                                                                               
         MVI   PIMCOMEL,PIMCOMEQ   SET ELEMENT ID                               
         MVI   PIMCOMLN,PIMCOMOV   SET DEFAULT MINIMUM LENGTH                   
****     MVC   PIMCOMS1,           SET HEADER SEQUENCE NUMBER                   
****     MVC   PIMCOMS2,           DETAIL SEQUENCE NUMBER                       
*                                                                               
         LA    R0,PIMCOMEL         MINIMUM COMMENT ELM LENGTH                   
         LA    R1,PIMCOML1         POINT TO FIRST COMMENT LENGTH BYTE           
         LA    R4,PIMCOMTX         POINT TO START OF COMMENTS IN ELM            
         LA    R5,3                MAX 3 COMMENT SEGMENTS                       
         LA    RE,EZICCOM          POINT TO START OF COMMENT                    
*                                                                               
COMELMLP DS    0H                                                               
*                                                                               
         CLC   0(39,RE),=CL256' '  SKIP IF SEGMENT NOT PRESENT                  
         BNH   COMELMCN                                                         
*                                                                               
         LA    RF,40               MAX POSITIONS TO CHECK                       
         LA    R3,39(RE)           POINT TO END+1 OF COMMENT SEGMENT            
*                                                                               
         CLI   0(R3),C' '          FIND LAST SPACE                              
         BNH   *+18                                                             
         BCTR  R3,0                BACK UP A POSITION                           
         BCT   RF,*-10                                                          
         LA    R3,39(RE)           NO SPACES IN COMMENT - RESET POINTER         
         B     COMELML1                                                         
*                                                                               
         BCTR  R3,0                BACK UP A POSITION                           
         BCTR  RF,0                RECTIFY THE COUNT                            
*                                                                               
         CLI   0(R3),C' '          FIND PREVIOUS NON-SPACE                      
         BH    *+12                                                             
         BCTR  R3,0                BACK UP A POSITION                           
         BCT   RF,*-10                                                          
         DC    H'0'                SHOULD NOT BE HERE                           
*                                                                               
COMELML1 DS    0H                                                               
*                                                                               
         SR    R3,RE               LENGTH OF COMMENT SEGMENT                    
*                                                                               
         STC   R3,0(R1)            SET COMMENT LENGTH                           
         LA    R1,1(R1)            BUMP COMMENT LENGTH POINTER                  
*                                                                               
         AR    R0,R3               INCREMENT ELEMENT LENGTH COUNTER             
*                                                                               
         BCTR  R3,0                DECREMENT FOR EXECUTE                        
         EX    R3,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R4),0(RE)       MOVE COMMENT SEGMENT TO ELEMENT              
*                                                                               
COMELMCN DS    0H                                                               
*                                                                               
         LA    R4,1(R3,RE)         BUMP TO NEXT SEGMENT IN ELEMENT              
         LA    RE,1(R3,RE)         BUMP TO NEXT INCOMING SEGMENT                
*                                                                               
         CLI   0(RE),C' '          IF SEGMENT STARTS WITH SPACE                 
         BH    *+8                                                              
         LA    RE,1(RE)               BUMP PAST IT                              
*                                                                               
         BCT   R5,COMELMLP                                                      
*                                                                               
COMELMDN DS    0H                                                               
*                                                                               
         GOTO1 PUTEL,DMCB,MINBLKD,PINVCELM,0  ADD TO RECORD                     
*                                                                               
COMELMX  DS    0H                                                               
         XIT1                                                                   
*                                                                               
HDRSQ    DC    X'00'               HEADER SEQUENCE NUMBER                       
HDRSQOLD DC    X'00'               HEADER SEQUENCE NUMBER FROM FILE             
DTLSQ    DC    XL2'00'             DETAIL SEQUENCE NUMBER                       
*                                                                               
         LTORG                                                                  
*                                                                               
         TITLE 'PZADDINV - ADD INVOICES TO PRNTFILE - COMSUBS'                  
***********************************************************************         
*                                                                     *         
*        COMSUBS - CODE AND DATA ADDRESSABLE FROM ALL CSECTS          *         
*                                                                     *         
***********************************************************************         
         SPACE 2                                                                
COMSUBS  DS    0H                                                               
*                                                                               
*        DMPREC - HEX DUMP OF ELEMENT WORK AREA                                 
*                                                                               
*NTRY    R1 ==>  ELEMENT TO BE ADDED                                            
*                                                                               
         DS    0D                                                               
DMPREC   NTR1                                                                   
*                                                                               
         LR    R5,R1               POINT TO ELEMENT WORKAREA                    
         SR    R2,R2                                                            
         ICM   R2,1,1(R5)          ELEMENT LENGTH                               
*                                                                               
         CLI   3(R5),PINVTYPQ      IF DUMPING KEY                               
         BNE   *+8                                                              
         LA    R2,L'PINVKEY           RESET DUMP LENGTH                         
*                                                                               
         LA    R3,0(R5,R2)         EOR                                          
*                                                                               
DMPREC2  DS    0H                                                               
         LR    R4,R3                                                            
         SR    R4,R5                                                            
         BNP   DMPRECX                                                          
         CH    R4,=H'32'                                                        
         BNH   *+8                                                              
         LA    R4,32                                                            
         XC    WORK,WORK                                                        
         MVC   P,SPACES                                                         
         GOTO1 HEXOUT,DMCB,(R5),WORK,(R4),=C'N'                                 
*                                                                               
         MVC   P+01(8),WORK+00                                                  
         MVC   P+10(8),WORK+08                                                  
         MVC   P+19(8),WORK+16                                                  
         MVC   P+28(8),WORK+24                                                  
         MVC   P+37(8),WORK+32                                                  
         MVC   P+46(8),WORK+40                                                  
         MVC   P+55(8),WORK+48                                                  
         MVC   P+64(8),WORK+56                                                  
*                                                                               
         MVC   WORK(32),0(R5)                                                   
         TR    WORK(32),TRTAB                                                   
         BCTR  R4,R0                                                            
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   P+75(0),WORK                                                     
         LA    R4,1(R4)                                                         
         GOTO1 EZPRINT,DMCB,P,=C'BL01'  PRINT LINE                              
         LA    R5,0(R5,R4)                                                      
         B     DMPREC2                                                          
*                                                                               
DMPRECX  DS    0H                                                               
         XIT1                                                                   
         SPACE 3                                                                
TRTAB    DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     00-0F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     10-1F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     20-2F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     30-3F                    
         DC    X'404B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     40-4F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B5B5C5D4B4B'     50-5F                    
         DC    X'60614B4B4B4B4B4B4B4B4B6B6C6D4B6F'     60-6F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B7B4B7D7E4B'     70-7F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     80-8F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     90-9F                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     A0-AF                    
         DC    X'4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B4B'     B0-BF                    
         DC    X'4BC1C2C3C4C5C6C7C8C94B4B4B4B4B4B'     C0-CF                    
         DC    X'4BD1D2D3D4D5D6D7D8D94B4B4B4B4B4B'     D0-DF                    
         DC    X'4B4BE2E3E4E5E6E7E8E94B4B4B4B4B4B'     E0-EF                    
         DC    X'F0F1F2F3F4F5F6F7F8F94B4B4B4B4B4B'     F0-FF                    
         EJECT                                                                  
* CHECK STATION FOR VALIDITY - 1ST 4 MUST NOT BE - (MINUS) *                    
*       UNLESS ALL NUMERIC                                 *                    
         SPACE                                                                  
         DS    0D                                                               
CKSTA    NTR1                                                                   
         LA    R0,4                                                             
         LA    R1,EZBTSTN                                                       
         CLI   EZBTSTN,C'Z'                                                     
         BH    CKSTA20                                                          
CKSTA10  CLI   0(R1),C'-'          MINUS NOT ALLOWED ANYWHERE                   
         BE    CKSTA40              ERROR                                       
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA10                                                       
         B     CKSTA30             GO CHECK MEDIA                               
         SPACE                                                                  
CKSTA20  CLI   0(R1),C'0'                                                       
         BL    CKSTA24                                                          
         CLI   0(R1),C'9'                                                       
         BH    CKSTA40             ERROR                                        
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA20                                                       
         B     CKSTA30             GO CK MEDIA                                  
         SPACE                                                                  
CKSTA24  CLI   0(R1),C' '          TRAILING BLANKS OK                           
         BNE   CKSTA40              ERROR                                       
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA24                                                       
         SPACE                                                                  
* CHECK VAKLID MEDIA - AM/FM(RADIO), T/L(TV), C/S/N (NETWORK) *                 
         SPACE                                                                  
CKSTA30  LA    R0,9                                                             
         LA    R1,=C'AFRTLXCSN'                                                 
CKSTA34  CLC   EZBTSTN+5(1),0(R1)                                               
         BE    CKSTAX                                                           
         LA    R1,1(,R1)                                                        
         BCT   R0,CKSTA34                                                       
CKSTA40  MVI   EZERRCD,EZERRSTA                                                 
         BAS   RE,GETERRM                                                       
         MVI   EZMODE,EZINVL       SET TO SKIP TO NEXT INV                      
         SPACE                                                                  
CKSTAX   B     EXIT                                                             
         EJECT                                                                  
*        GETERRM - GET ERROR MESSAGE AND LEVEL                                  
         SPACE 2                                                                
         DS    0D                                                               
GETERRM  NTR1                                                                   
         L     R3,=A(ERRTAB)                                                    
         MVC   EZERRMSG,SPACES                                                  
         MVI   EZERRLEV,0                                                       
*                                                                               
GERM2    DS    0H                                                               
         CLI   0(R3),X'FF'         EOL                                          
         BE    GERMX                                                            
*                                                                               
         CLC   EZERRCD,0(R3)                                                    
         BE    GERM4                                                            
         LA    R3,ERRTABL(R3)                                                   
         B     GERM2                                                            
*                                                                               
GERM4    DS    0H                                                               
         MVC   EZERRLEV,1(R3)                                                   
         MVC   EZERRMSG,2(R3)                                                   
         OC    EZERRLVR,1(R3)      'OR' UP ERROR - RECORD                       
         OC    EZERRLVI,1(R3)                    - INVOICE                      
         OC    EZERRLVB,1(R3)                    - BATCH                        
*                                                                               
GERMX    DS    0H                                                               
         XIT1                                                                   
         SPACE 2                                                                
SAVBUF   DS    0H                  SAVE WRKR BUFFER                             
         L     RF,EZWKRBUF                                                      
         L     R1,=A(SAVWKBUF)                                                  
*                                                                               
         LA    R0,16               16 X 256 BYTES = 4096                        
         MVC   0(256,R1),0(RF)                                                  
         LA    R1,256(R1)                                                       
         LA    RF,256(RF)                                                       
         BCT   R0,*-14                                                          
         BR    RE                                                               
         EJECT                                                                  
         SPACE 2                                                                
*                                  ERROR TABLE                                  
*                                  CODE(1),LEVEL(1),MESSAGE(40)                 
*                                                                               
ERRTAB   DS    0C                                                               
         DC    AL1(EZERRDUP)       DUPLICATE INVOICE                            
         DC    AL1(0)              LEVEL                                        
         DC    CL40'INVOICE ALREADY ON FILE'                                    
*                                                                               
         DC    AL1(EZERROVF)       OVERFLOW                                     
         DC    AL1(0)              LEVEL                                        
         DC    CL40'TOO MANY PRINTS IN INVOICE'                                 
*                                                                               
         DC    AL1(EZERRANF)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'CLIENT NOT ON FILE'                                         
*                                                                               
         DC    AL1(EZERRPNF)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRODUCT NOT ON FILE'                                        
*                                                                               
         DC    AL1(EZERRSPL)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'PRINT DETAIL PRINT LENGTH ZERO'                             
*                                                                               
         DC    AL1(EZERRMUL)                                                    
         DC    AL1(0)              LEVEL                                        
         DC    CL40'COMBINED INVOICES, NO RECONVERT'                            
*                                                                               
         DC    X'FFFF'                                                          
*                                                                               
INVDIR   DC    CL8'PRTDIR'         INVOICE DIRECTORY NAME                       
INVFILE  DC    CL8'PRTFILE'        INVOICE FILE NAME                            
MAXRECSZ DC    H'1900'                                                          
ERRTABL  EQU   42                                                               
*                                                                               
VMINIO   DS    A                   A(MINIO)                                     
VPERVAL  DC    V(PERVAL)           A(PERVAL)                                    
VPERVERT DS    A                   A(PERVERT)                                   
*                                                                               
ADSCTL   DC    C'1'                CONTROL SWITCH                               
ADS1STQ  EQU   C'1'                FIRST TIME TO PROGRAM                        
*                                                                               
         SPACE 2                                                                
*                                                                               
         TITLE 'ROUTINE TO ADD ELEMENT TO A MINIO BLOCK - ADDEL'                
***********************************************************************         
* ROUTINE TO ADD AN ELEMENT TO A RECORD                               *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
ADDEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         ICM   R0,15,MINELEM       SAVE ELEMENT AREA POINTER                    
*                                                                               
         MVC   MINELEM,4(R1)       POINT BLOCK TO NEW ELEMENT                   
*                                                                               
         TM    EZTRACE,X'10'       IF TRACING                                   
         BNO   *+12                                                             
         L     R1,4(R1)               POINT TO ELEMENT TO BE ADDED              
         BAS   RE,DMPREC              GO DUMP ELEMENT                           
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINADD',MINBLKD)                                 
*                                                                               
         STCM  R0,15,MINELEM       RESTORE ELEMENT AREA POINTER                 
*                                                                               
         CLI   MINERR,0            NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
ADDELX   XIT1                                                                   
         DROP  R5                                                               
         TITLE 'ROUTINE TO DELETE AN ELEMENT IN A MINIO BLOCK - ADDEL'          
***********************************************************************         
* ROUTINE TO DELETE AN ELEMENT FROM A RECORD                          *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    NOT USED                                            *         
*        PARM3    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
DELEL    NTR1  0H                                                               
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         ICM   R0,15,MINELEM       SAVE ELEMENT AREA POINTER                    
*                                                                               
         L     R2,8(R1)            POINT TO ELEMENT TO BE DELETED               
*                                                                               
         ST    R2,MINELEM          POINT BLOCK TO ELEMENT                       
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY AREA                        
*                                                                               
         MVC   MINEKEY(1),0(R2)    BUILD ELEMENT KEY                            
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET KEY LENGTH                               
*                                                                               
         CLM   RF,1,MINEKLEN       DEFAULT TO MINIO KEY LENGTH                  
         BZ    DELELX              NO ELEMENT                                   
         BNH   *+8                                                              
         IC    RF,MINEKLEN                                                      
*                                                                               
         SH    RF,=H'2'            DECREMENT FOR EXECUTE                        
*                                                                               
         EX    RF,DELELMV          SET REST OF KEY                              
*                                                                               
         GOTO1 VMINIO,MNPRMS,('MINDEL',MINBLKD)                                 
*                                                                               
         STCM  R0,15,MINELEM       RESTORE ELEMENT AREA POINTER                 
*                                                                               
         CLI   MINERR,0            NO ERRORS ALLOWED                            
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         B     DELELX                                                           
*                                                                               
DELELMV  MVC   MINEKEY+1(0),2(R2)   SETS REST OF ELEMENT KEY                    
*                                                                               
*                                                                               
DELELX   XIT1                                                                   
*                                                                               
         DROP  R5                                                               
*                                                                               
         TITLE 'ROUTINE TO GET AN ELEMENT IN A MINIO BLOCK - GETEL'             
***********************************************************************         
* ROUTINE TO GET AN ELEMENT IN A RECORD                               *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*        ELEMENT  CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR        *         
*        ELEMENT+1 CONTAINS LENGTH TO BE COMPARED INCLUDING LENGTH    *         
*                 BYTE WHICH IS NOT INCLUDED IN COMPARE               *         
* EXIT - 8(R1)  CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND    *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
GETEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         L     R2,4(R1)            POINT O ELEMENT KEY                          
         LR    R4,R1               SAVE PARAMETER REGISTER                      
*                                                                               
         ZIC   R6,MINFILTL         SAVE CURRENT FILTER VALUE                    
*                                                                               
         MVI   MINFILTL,0          INIT FILTER LENGTH                           
*                                                                               
         XC    MINEKEY,MINEKEY     INIT ELEMENT KEY AREA                        
*                                                                               
         MVC   MINEKEY(1),0(R2)    BUILD ELEMENT KEY                            
*                                                                               
         LA    R0,MINRD            DEFAULT TO DIRECT READ                       
*                                                                               
         CLI   1(R2),0             USE DEFAULT IF NO LENGTH GIVEN               
         BE    GETEL10                                                          
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET SEARCH LENGTH                            
*                                                                               
         CLM   RF,1,MINEKLEN       USE READ IF GREATER THAN KEY LENGTH          
         BNL   GETEL10                                                          
*                                                                               
         LA    R0,MINHI            SET FOR READ HI/EQUAL                        
         STC   RF,MINFILTL         SET FILTER LENGTH                            
*                                                                               
GETEL10  DS    0H                                                               
*                                                                               
         ZIC   RF,MINEKLEN         GET KEY LENGTH                               
         SH    RF,=H'2'            DECREMENT FOR EXECUTE                        
         EX    RF,GETELMV          SET REST OF KEY                              
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)                                     
*                                                                               
         STC   R6,MINFILTL         RESTORE CURRENT FILTER VALUE                 
*                                                                               
         XC    8(4,R4),8(R4)       INIT RETURNED PARAMETER                      
*                                                                               
         CLI   MINERR,0            NO MATCH                                     
         BNE   GETELX                                                           
*                                                                               
         MVC   8(4,R4),MINELEM     GET RETURNED ELEMENT ADDRESS                 
         B     GETELX                                                           
*                                                                               
GETELMV  MVC   MINEKEY+1(0),2(R2)  SETS REST OF ELEMENT KEY                     
*                                                                               
GETELX   XIT1                                                                   
         DROP  R5                                                               
         TITLE 'ROUTINE TO GET NEXT ELEMENT IN A MINIO BLOCK - SEQEL'           
***********************************************************************         
* ROUTINE TO GET NEXT SEQUENTIAL ELEMENT IN A MINIO RECORD            *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*        ELEMENT  CONTAINS ELEMENT CODE AND DATA TO SEARCH FOR        *         
*        ELEMENT+1 CONTAINS LENGTH TO BE COMPARED INCLUDING LENGTH    *         
*                 BYTE WHICH IS NOT INCLUDED IN COMPARE               *         
*                 ASSUMES LAST ACTION WAS A GETEL AND MINIO BLOCK     *         
*                  IS ALREADY SET UP FOR SEQUENTIAL READ              *         
*                                                                     *         
* EXIT - 8(R1)  CONTAINS ADDRESS OF ELEMENT OR ZEROES IF NOT FOUND    *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
SEQEL    NTR1                                                                   
*                                                                               
         L     R5,0(R1)            A(MINBLKD)                                   
         USING MINBLKD,R5          ESTABLISH MINIO BLOCK                        
*                                                                               
         L     R2,4(R1)            POINT TO ELEMENT KEY                         
         LR    R4,R1               SAVE PARAMETER REGISTER                      
*                                                                               
         ZIC   R6,MINFILTL         SAVE CURRENT FILTER VALUE                    
*                                                                               
         MVI   MINFILTL,0          INIT FILTER LENGTH                           
*                                                                               
         CLI   1(R2),0             USE DEFAULT IF NO LENGTH GIVEN               
         BE    SEQEL10                                                          
*                                                                               
         ZIC   RF,1(R2)            GET ELEMENT LENGTH                           
         BCTR  RF,0                GET SEARCH LENGTH                            
*                                                                               
         CLM   RF,1,MINEKLEN       USE READ IF GREATER THAN KEY LENGTH          
         BNL   SEQEL10                                                          
*                                                                               
         STC   RF,MINFILTL         SET FILTER LENGTH                            
*                                                                               
SEQEL10  DS    0H                                                               
*                                                                               
         LA    R0,MINSEQ           SET FOR READ READ SEQUENTIAL                 
*                                                                               
         GOTO1 VMINIO,MNPRMS,((R0),MINBLKD)                                     
*                                                                               
         STC   R6,MINFILTL         RESTORE CURRENT FILTER VALUE                 
*                                                                               
         XC    8(4,R4),8(R4)       INIT RETURNED PARAMETER                      
*                                                                               
         CLI   MINERR,0            NO MATCH                                     
         BNE   SEQELX                                                           
*                                                                               
         MVC   8(4,R4),MINELEM     GET RETURNED ELEMENT ADDRESS                 
         B     SEQELX                                                           
*                                                                               
SEQELX   XIT1                                                                   
         DROP  R5                                                               
*                                                                               
         TITLE 'ROUTINE TO PUT AN ELEMENT TO A MINIO BLOCK - PUTEL'             
***********************************************************************         
* ROUTINE TO PUT AN ELEMENT IN A RECORD                               *         
*       DELETES AND ADDS NEW ELEMENT WILL NOT GET UPSET IF THERE      *         
*         IS NO PRIOR ELEMENT                                         *         
*                                                                     *         
* NTRY - PARM1    A(MINIO BLOCK)                                      *         
*        PARM2    A(ELEMENT)                                          *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
         DS    0D                                                               
PUTEL    NTR1  0H                                                               
*                                                                               
         GOTO1 GETEL,(R1)          FIND ELEMENT                                 
*                                                                               
         OC    8(4,R1),8(R1)       ADD ELEMENT IF NONE ALREADY                  
         BZ    PUTELADD            ELSE DELETE OLD ELEMENT                      
*                                                                               
         GOTO1 DELEL,(R1)          DELETE ELEMENT                               
*                                                                               
PUTELADD DS    0H                                                               
*                                                                               
         GOTO1 ADDEL,(R1)          ADD ELEMENT TO RECORD                        
*                                                                               
PUTELX   XIT1                                                                   
*                                                                               
         LTORG                                                                  
*                                                                               
         EJECT                                                                  
*                                                                               
SAVINVH  DS    XL512               SAVE AREA FOR INVOICE HEADER                 
         DS    0D                                                               
MINBLK   DS    XL(MINBLKL)         MINIO BLOCK FOR INVOICE RECORD               
*                                                                               
         DS    0D                                                               
HDRELMSV DS    XL128               HEADER ELEMENT SAVEAREA                      
PINVMELM DS    XL128               ELEMENT RETRIEVAL AREA                       
PINVMRTB DS    XL(200*(6+L'PINVMINI))  RECORD TABLE                             
*                                                                               
         DS    0D                                                               
PINVMBF1 DS    XL4096              FIRST  MINIO BUFFER                          
PINVMBF2 DS    XL4096              SECOND MINIO BUFFER                          
*                                                                               
SAVWKBUF DS    XL4096              WORKER FILE BUFFER SAVEAREA                  
*                                                                               
         TITLE 'PZADDINV - ADD INVOICE TO FILES - PZADID'                       
***********************************************************************         
*                                                                     *         
*        WORKAREAS                                                    *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PZADID   DSECT                                                                  
PINVELM  DS    XL256               ELEMENT WORKAREA                             
PINVCELM DS    XL256               COMMENT ELM WORKAREA                         
ERR      DS    XL1                                                              
         DS    0D                                                               
*                                                                               
*        MINIO ROUTINES WORKAREAS                                               
*                                                                               
MNPRMS   DS    6A                  PARAMETER BLOCK                              
*                                                                               
WKINVNO  DS    CL(L'PIMINVNO)      INVOICE NUMBER WORKAREA                      
INVSAVE  DS    XL(L'EZIHINV)       INVOICE NUMBER SAVEAREA                      
PRDBUFF  DS    200XL(L'PINVPRD)    PRODUCT BUFFER                               
*                                                                               
PZADIDL  EQU   *-PZADID                                                         
*                                                                               
         TITLE 'PZADDINV - ADD INVOICE TO FILES - PZADID'                       
***********************************************************************         
*                                                                     *         
*        OTHER INCLUDED DSECTS                                        *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
* PZBLOCK                                                                       
         PRINT OFF                                                              
       ++INCLUDE PZBLOCK                                                        
         PRINT ON                                                               
* DDPERVALD                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDPERVALD                                                      
         PRINT ON                                                               
* PRGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE PRGENFILE                                                      
         PRINT ON                                                               
* DDACTIVD                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDACTIVD                                                       
         PRINT ON                                                               
* DDCOMFACSD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACSD                                                     
         PRINT ON                                                               
* DDMINBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE DDMINBLK                                                       
         PRINT ON                                                               
* DMWRKRD                                                                       
         PRINT OFF                                                              
       ++INCLUDE DMWRKRD                                                        
         PRINT ON                                                               
* PPREPWORK                                                                     
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK                                                      
         PRINT ON                                                               
* PPREPWORK2                                                                    
         PRINT OFF                                                              
       ++INCLUDE PPREPWORK2                                                     
         PRINT ON                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'054PZADDINSV 05/01/02'                                      
         END                                                                    
