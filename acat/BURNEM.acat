*          DATA SET BURNEM     AT LEVEL 002 AS OF 12/02/85                      
*CATALP BURNEM                                                                  
         TITLE 'BURNEM - VALIDATE NUMERIC FIELD'                                
***********************************************************************         
* VALIDATE A NUMERIC STRING AND CONVERT INTO SCALED DECIMAL FORMAT    *         
*                                                                     *         
*        INPUT STRINGS MAY CONTAIN LEADING OR TRAILING SIGN (+ OR -)  *         
*        AND ANY NUMBER OF DECIMAL PLACES. ONLY SIGNIFICANT DIGITS    *         
*        ARE HELD IN THE RETURNED VALUE (LEADING AND TRAILING ZEROES  *         
*        ARE DROPPED).                                                *         
*                                                                     *         
* NTRY - R1 POINTS TO PARAMETER LIST AS FOLLOWS                       *         
*                                                                     *         
* P1     B0    L'INPUT STRING                                         *         
*        B1-3  A(INPUT STRING)                                        *         
*                                                                     *         
* P2     B0    N/D                                                    *         
*        B1-3  A(8 BYTE OUTPUT VALUE) - 1 BYTE SCALE/EXPONENT         *         
*                                                                     *         
* EXIT - PARAMETER LIST CHANGED AS FOLLOWS                            *         
*                                                                     *         
* P1     B0    ZERO=INPUT STRING IS VALID                             *         
*              N/Z =INPUT STRING IS INVALID                           *         
*                                                                     *         
* P2     B0    INDEX TO INVALID CHARACTER IF ERROR SET                *         
***********************************************************************         
         SPACE 1                                                                
BURNEM   CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,**BURNEM                                             
         USING WORKD,RC            RC=A(W/S)                                    
*                                                                               
         LR    R2,R1               R2=A(PARAMETER LIST)                         
         L     R3,0(R2)                                                         
         LA    R3,0(R3)            R3=A(INPUT STRING)                           
         SR    R4,R4                                                            
         ICM   R4,1,0(R2)          R4=L'INPUT STRING                            
         BZ    BURNEME1                                                         
         STC   R4,LENGTH                                                        
         L     R5,4(R2)            R5=A(OUTPUT VALUE)                           
         ZAP   0(8,R5),=P'0'                                                    
         SR    R6,R6               R6=N'SIGNIFICANT DIGITS                      
         SR    R7,R7               R7=N'DECIMAL PLACES                          
         SR    R8,R8               R8=N'TRAILING ZEROES                         
         LA    R9,WORK             R9=A(INTERMEDIATE VALUE)                     
         MVI   SIGN,NO                                                          
         MVI   DECS,NO                                                          
         MVI   SIGNIF,NO                                                        
         MVI   NUMBER,NO                                                        
*                                                                               
BURNEM2  CLI   0(R3),PLUS          TEST SIGN                                    
         BE    *+12                                                             
         CLI   0(R3),MINUS                                                      
         BNE   BURNEM4                                                          
         CLM   R4,1,0(R2)          TEST LEADING SIGN                            
         BE    *+12                                                             
         CH    R4,=H'1'            TEST TRAILING SIGN                           
         BNE   BURNEME2                                                         
         CLI   SIGN,NO             TEST SIGN ALREADY SET                        
         BNE   BURNEME7                                                         
         MVC   SIGN,0(R3)                                                       
         B     BURNEM14            BUMP TO NEXT CHARACTER                       
*                                                                               
BURNEM4  CLI   0(R3),POINT         TEST DECIMAL POINT                           
         BNE   BURNEM6                                                          
         CLI   DECS,NO             TEST ALREADY SET                             
         BNE   BURNEME3                                                         
         MVI   DECS,YES                                                         
         B     BURNEM14            BUMP TO NEXT INPUT CHARACTER                 
*                                                                               
BURNEM6  CLI   0(R3),C'0'          TEST NUMERIC ZERO                            
         BL    BURNEME4                                                         
         BH    BURNEM8                                                          
         MVI   NUMBER,YES                                                       
         CLI   SIGNIF,NO                                                        
         BE    BURNEM12            BUMP TO NEXT CHARACTER                       
         LA    R8,1(R8)            BUMP TRAILING ZEROES                         
         B     BURNEM10                                                         
*                                                                               
BURNEM8  CLI   0(R3),C'9'          TEST NUMERIC 1 THRU 9                        
         BH    BURNEME4                                                         
         MVI   NUMBER,YES                                                       
         MVI   SIGNIF,YES          SET START OF SIGNIFICANCE                    
         SR    R8,R8               SET NO TRAILING ZEROES                       
*                                                                               
BURNEM10 LA    R6,1(R6)            BUMP SIGNIFICANT DIGITS                      
         MVC   0(1,R9),0(R3)       MOVE CHARACTER TO WORK                       
         LA    R9,1(R9)                                                         
*                                                                               
BURNEM12 CLI   DECS,NO             BUMP DECIMAL PLACES                          
         BE    *+8                                                              
         LA    R7,1(R7)                                                         
*                                                                               
BURNEM14 LA    R3,1(R3)            BUMP TO NEXT INPUT CHARACTER                 
         BCT   R4,BURNEM2                                                       
*                                                                               
         CLI   NUMBER,NO                                                        
         BE    BURNEME5                                                         
         CLI   SIGNIF,NO           TEST ANY SIGNIFICANT DIGITS                  
         BE    BURNEM16            NO - EXIT WITH ZERO (SCALE=INTEGER)          
         SR    R6,R8               SUBTRACT TRAILING ZEROES                     
         LA    R0,MAXLEN                                                        
         CR    R6,R0               TEST GR MAXIMUM SIGNIFICANCE                 
         BH    BURNEME6                                                         
         BCTR  R6,0                                                             
         EX    R6,*+8                                                           
         B     *+10                                                             
         PACK  1(7,R5),WORK(0)     PACK NUMBER INTO OUTPUT AREA                 
         CLI   SIGN,MINUS          TEST NEGATIVE SIGN                           
         BNE   *+10                                                             
         MP    1(7,R5),=P'-1'      YES - SET NEGATIVE VALUE                     
         SR    R7,R8               SET EXPONENT SIGN & SCALE                    
         BZ    BURNEM16                                                         
         BP    *+10                                                             
         LPR   R7,R7                                                            
         B     *+8                                                              
         LA    R7,X'80'(R7)                                                     
         STC   R7,0(R5)                                                         
*                                                                               
BURNEM16 B     BURNEMOK                                                         
         EJECT                                                                  
BURNEMOK MVI   0(R2),0             INPUT VALID                                  
         MVI   4(R2),0                                                          
         B     BURNEMX                                                          
BURNEME1 MVI   0(R2),1             INPUT LENGTH OF ZERO                         
         B     BURNEMEX                                                         
BURNEME2 MVI   0(R2),2             SIGN NOT LEADING OR TRAILING                 
         B     BURNEMEX                                                         
BURNEME3 MVI   0(R2),3             MORE THAN ONE DECIMAL POINT                  
         B     BURNEMEX                                                         
BURNEME4 MVI   0(R2),4             INVALID CHARACTER                            
         B     BURNEMEX                                                         
BURNEME5 MVI   0(R2),5             NO NUMERIC CHARACTERS FOUND                  
         B     BURNEMEX                                                         
BURNEME6 MVI   0(R2),6             TOO MANY SIGNIFICANT DIGITS                  
         B     BURNEMEX                                                         
BURNEME7 MVI   0(R2),7             MORE THAN ONE SIGN FOUND                     
         B     BURNEMEX                                                         
         SPACE 2                                                                
BURNEMEX ZIC   R0,LENGTH           SET DISPLACEMENT TO ERROR                    
         SR    R0,R4                                                            
         STC   R0,4(R2)                                                         
         SPACE 2                                                                
BURNEMX  XIT1                                                                   
         EJECT                                                                  
         LTORG                                                                  
         SPACE 2                                                                
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
PLUS     EQU   C'+'                                                             
MINUS    EQU   C'-'                                                             
POINT    EQU   C'.'                                                             
MAXLEN   EQU   13                  MAXIMUM SIGNIFICANT LENGTH                   
         SPACE 2                                                                
WORKD    DSECT                                                                  
SIGN     DS    C                                                                
DECS     DS    C                                                                
SIGNIF   DS    C                                                                
NUMBER   DS    C                                                                
LENGTH   DS    X                                                                
WORK     DS    CL24                                                             
WORKX    EQU   *                                                                
         SPACE 2                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'002BURNEM    12/02/85'                                      
         END                                                                    
