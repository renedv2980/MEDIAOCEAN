*          DATA SET UNLDCPTR   AT LEVEL 047 AS OF 12/06/11                      
*CATALP UNLDCPTR                                                                
         TITLE 'LDCPTR - UNIT - CREATE DIRECTORY POINTERS '                     
*                                                                               
* UNLDCPTR - FOR UNIT RECORD: CREATES 2 PASSIVE AND 1 REGULAR POINTER           
*            FOR OTHER RECORDS(PACKAGE) : CREATES REGULAR POINTER               
*                              TRAFFIC PROGRAM EQUIVALENCY RECORD               
*                                          1 PASSIVE                            
*                              TRAFFIC PATTERN RECORD                           
*                                          1 PASSIVE                            
*                                                                               
* INPUTS: PARAMETER1 - ADDRESS OF UNIT (OR PACKAGE) RECORD                      
*         PARAMETER2 - ADDRESS OF LIST TO RETURN POINTERS IN                    
*                                                                               
* OUTPUTS: LIST POINTED TO BY PARAM2 - THE PASSIVE, REGULAR POINTERS TO         
*                   BE ADDED. MUST BE 0 TERMINATED.                             
*                                                                               
* CALLED FROM: DMLDMOD - GENERAL LOAD/DUMP MODULE                               
*                                                                               
* GLOBALS : R7 - ADDRESS OF INPUT RECORD                                        
*           R8 - NEXT AVAILABLE ADDRESS IN RETURN-AREA                          
**********************************************************************          
*                                                                    *          
* 04/MAR/12 BGRI ADD NET PATTERN PASSIVE PTR                         *          
*                                                                    *          
**********************************************************************          
         PRINT NOGEN                                                            
LDCPTR   CSECT                                                                  
         NMOD1 0,UNLDCPTR                                                       
*                                                                               
         L     R7,0(R1)            INITIALIZE R7,R8                             
         L     R8,4(R1)                                                         
*                                                                               
         CLI   0(R7),X'04'         IF A UNIT RECORD                             
         BNE   CKNEXT                                                           
         BAS   RE,PASSUNIT            GENERATE PASSIVE POINTER                  
         B     REGPTR                                                           
*                                                                               
CKNEXT   EQU   *                   IN CASE OTHER RECORDS IN FUTURE              
         CLI   0(R7),X'23'         IF A TRAFFIC PATTERN RECORD                  
         B     CKNEXT10            BYPASS CREATING PASSIVES FOR PAT             
*NOP     BNE   CKNEXT10                                                         
*        BAS   RE,PASSTPTN            GENERATE PASSIVE POINTER(S)               
******   B     REGPTR                                                           
*                                                                               
CKNEXT10 EQU   *                   IN CASE OTHER RECORDS IN FUTURE              
         CLI   0(R7),X'24'         IF A TRAFFIC PROG EQUIV                      
         BNE   CKNEXT20                                                         
         BAS   RE,PASSTEQV            GENERATE PASSIVE POINTER(S)               
         B     REGPTR                                                           
*                                                                               
CKNEXT20 EQU   *                   IN CASE OTHER RECORDS IN FUTURE              
         CLI   0(R7),X'0C'         IF A NETWORK COMMENT RECORD                  
         BNE   CKNEXT30                                                         
         BAS   RE,PASSCOMM            GENERATE PASSIVE POINTER(S)               
         B     REGPTR                                                           
*                                                                               
CKNEXT30 EQU   *                   IN CASE OTHER RECORDS IN FUTURE              
*                                                                               
         USING NURECD,R8                                                        
REGPTR   MVC   0(L'NUKEY,R8),0(R7)    MOVE KEY                                  
         MVC   NUKSTAT,NURSTAT-NUKEY(R7)   MOVE STATUS                          
         XC    NUDA,NUDA              SET DISK ADDRESS TO 0'S                   
         MVI   L'NUKEY+L'NUKSTAT+L'NUDA(R8),X'00'    MARK END O LIST            
*                                                                               
         XMOD1 1                                                                
         DROP  R8                                                               
**********************************************************************          
         EJECT                                                                  
**********************************************************************          
*  PASSUNIT - CREATE PASSIVE POINTER FOR UNIT RECORD.                           
*                                                                               
* INPUTS - R7 - ADDRESS OF UNIT RECORD                                          
*          R8 - WHERE TO PUT PASSIVE POINTER (POINTER LIST)                     
*                                                                               
* OUTPUTS - R8 - ADDRESS FOR NEXT POINTER                                       
*           POINTER LIST - PASSIVE POINTER + X'00' TERMINATION                  
**********************************************************                      
PASSUNIT EQU   *                                                                
*                                                                               
         USING NUKPKEY,R8                                                       
         XC    NUKPKEY,NUKPKEY     CLEAR PASSIVE POINTER FIELD                  
         MVI   NUKPTYPE,X'84'      TYPE PASSIVE POINTER                         
         MVC   NUKPAM,NUKAM-NUKEY(R7)       AGENCY/MEDIA                        
         MVC   NUKPCLT,NUKCLT-NUKEY(R7)     CLIENT                              
         MVC   NUKPNET,NUKNET-NUKEY(R7)     NETWORK                             
         MVC   NUKPPROG,NUKPROG-NUKEY(R7)   PROGRAM CODE                        
         MVC   NUKPDATE,NUKDATE-NUKEY(R7)   AIR DATE                            
         MVC   NUKPEST,NUKEST-NUKEY(R7)     ESTIMATE                            
         MVC   NUKPSUB,NUKSUB-NUKEY(R7)     SUB-LINE                            
         MVC   NUKPDP,NUKDP-NUKEY(R7)       DAYPART                             
*                                                                               
         USING NURECD,R8             USE POINTER STATUS AND ADDRESS,            
*                                      NOT THAT OF RECORD!!!!!!!                
         MVC   NUKSTAT,NURSTAT-NUKEY(R7)    MOVE STATUS                         
         XC    NUDA,NUDA              SET DISK ADDRESS TO 0'S                   
         LA    R8,L'NUKEY+L'NUKSTAT+L'NUDA(R8)    NEXT LOCATION                 
*                                                                               
         USING NUKDKEY,R8          NOW DO DAY-TIME PASSIVE POINTER              
         XC    NUKDKEY,NUKDKEY     CLEAR PASSIVE POINTER FIELD                  
         MVI   NUKDTYPE,X'94'      TYPE PASSIVE POINTER                         
         MVC   NUKDAM,NUKAM-NUKEY(R7)       AGENCY/MEDIA                        
         MVC   NUKDCLT,NUKCLT-NUKEY(R7)     CLIENT                              
         MVC   NUKDEST,NUKEST-NUKEY(R7)     ESTIMATE                            
         MVC   NUKDNET,NUKNET-NUKEY(R7)     NETWORK                             
*                                                                               
         LA    R2,DAYTABL              CONVERT TO REP DAY                       
PSLOOP   CLI   0(R2),X'FF'         CK FOR END OF TABLE                          
         BE    GOTONE                                                           
         CLC   NUDAY-NUKEY(1,R7),0(R2)                                          
         BE    GOTONE                                                           
         LA    R2,2(R2)                                                         
         B     PSLOOP                                                           
*                                                                               
GOTONE   MVC   NUKDDAY,1(R2)                                                    
         MVC   NUKDTIME,NUKTIME-NUKEY(R7)   START QRTR HOUR                     
         MVC   NUKDPROG,NUKPROG-NUKEY(R7)   PROGRAM CODE                        
         MVC   NUKDDATE,NUKDATE-NUKEY(R7)   AIR DATE                            
         MVC   NUKDSUB,NUKSUB-NUKEY(R7)     SUB-LINE                            
*                                                                               
         USING NURECD,R8             USE POINTER STATUS AND ADDRESS,            
*                                      NOT THAT OF RECORD!!!!!!!                
         MVC   NUKSTAT,NURSTAT-NUKEY(R7)    MOVE STATUS                         
         XC    NUDA,NUDA              SET DISK ADDRESS TO 0'S                   
*                                                                               
         LA    R8,L'NUKEY+L'NUKSTAT+L'NUDA(R8)    NEXT LOCATION                 
*                                                                               
         MVI   0(R8),X'00'         MARK END OF LIST                             
         BR    RE                                                               
         DROP  R8                                                               
         EJECT                                                                  
**********************************************************************          
*  PASSTEQV - CREATE PASSIVE POINTER FOR TRAFFIC EQUIVALENCY RECORD             
*                                                                               
* INPUTS - R7 - ADDRESS OF TRAFFIC EQUIVALENT PROGRAM RECORD                    
*          R8 - WHERE TO PUT PASSIVE POINTER (POINTER LIST)                     
*                                                                               
* OUTPUTS - R8 - ADDRESS FOR NEXT POINTER                                       
*           POINTER LIST - PASSIVE POINTER + X'00' TERMINATION                  
**********************************************************************          
PASSTEQV DS   0H                                                                
*                                                                               
         XC    BASPRGTB,BASPRGTB                                                
         LR    R3,R7                                                            
         LA    R3,PGEDTAEL-PGEKEY(,R3)                                          
PASST10  CLI   0(R3),X'10'               THIS AN ELEMENT                        
         BNE   PASST30                                                          
         LA    R1,BASPRGTB                                                      
         LA    RF,20                                                            
PASST20  OC    0(L'PGEPROG,R1),0(R1)     EMPTY SPACE                            
         BZ    PASST24                                                          
         CLC   0(L'PGEPROG,R1),PGEPROG-PGEDTAEL(R3) BYPASS EQ ENTRIES           
         BE    PASST26                                                          
         LA    R1,L'PGEPROG(,R1)                                                
         BCT   RF,PASST20                                                       
         DC    H'0'                                                             
         SPACE                                                                  
PASST24  MVC   0(L'PGEPROG,R1),PGEPROG-PGEDTAEL(R3)                             
         LA    R1,L'PGEPROG(,R1)                                                
         SPACE                                                                  
PASST26  LA    R3,PGEDTAX-PGEDTAEL(,R3)                                         
         B     PASST10                                                          
         SPACE                                                                  
         USING PGEPID,R8                                                        
PASST30  LA    R1,BASPRGTB                                                      
         LA    RF,20                                                            
         SPACE                                                                  
PASST50  OC    0(L'PGEPROG,R1),0(R1) EMPTY SPACE                                
         BZ    PASST60                                                          
         MVC   0(L'PGEKEY,R8),0(R7)  MOVE IN OLD KEY                            
         SPACE                                                                  
         MVI   PGEPID,X'A4'          TYPE PASSIVE POINTER                       
         MVC   PGEPEPRG,PGEPBPRG     MOVE EQUIV                                 
         MVC   PGEPBPRG,0(R1)        MOVE BASE                                  
*                                                                               
         LA    R8,L'NUKEY+L'NUKSTAT+L'NUDA(R8)    NEXT LOCATION                 
*                                                                               
         LA    R1,L'PGEPROG(,R1)   NEXT TABLE ENTRY                             
         BCT   RF,PASST50                                                       
*                                                                               
PASST60  MVI   0(R8),X'00'         MARK END OF LIST                             
         BR    RE                                                               
         DROP  R8                                                               
**********************************************************************          
         EJECT                                                                  
**********************************************************************          
*  PASSTPTN - CREATE PASSIVE POINTER FOR TRAFFIC PATTERN RECORD                 
*                                                                               
* INPUTS - R7 - ADDRESS OF TRAFFIC PATTERN RECORD                               
*          R8 - WHERE TO PUT PASSIVE POINTER (POINTER LIST)                     
*                                                                               
* OUTPUTS - R8 - ADDRESS FOR NEXT POINTER                                       
*           POINTER LIST - PASSIVE POINTER + X'00' TERMINATION                  
**********************************************************************          
         SPACE                                                                  
*&&DO                                                                           
PASSTPTN DS   0H                                                                
         SPACE                                                                  
         OC    NPTKNET-NPTKEY(NPTKREF+1-NPTKNET,R7),NPTKNET-NPTKEY(R7)          
         BZR   RE                  BYPASS SEQ NO RECD                           
         SPACE                                                                  
         LR    R3,R7                                                            
         LA    R3,27(R3)           POINT TO FIRST ELEMENT                       
         SPACE                                                                  
* ANY PATTERN WITHOUT A 10 ELEM IS VERY BAD                                     
         SPACE                                                                  
         CLI   0(R3),X'10'               THIS THE MASTER ELEM                   
         BNER  RE                         BYPASS BAD RECD                       
         SPACE                                                                  
* ANY PATTERN WITHOUT A PATTERN SEQ # MUST BE BYPASSED                          
         SPACE                                                                  
         OC    NPTSEQNO-NPTDTAEL(,R3),NPTSEQNO-NPTDTAEL(R3)                     
         BZR   RE                   BYPASS BAD RECD                             
         SPACE                                                                  
         XC    0(20,R8),0(R8) CLEAR KEY AREA                                    
         SPACE                                                                  
         MVI   NPTPKID-NPTKEY(R8),X'A3'         TYPE PASSIVE POINTER            
         MVC   NPTPKAM-NPTPKID(3,R8),1(R7)      MOVE AGYMD & BCLT               
         MVC   NPTPKSEQ-NPTPKID(2,R8),NPTSEQNO-NPTDTAEL(R3)                     
         SPACE                                                                  
         LA    R8,L'NUKEY+L'NUKSTAT+L'NUDA(R8)    NEXT LOCATION                 
         SPACE                                                                  
         MVI   0(R8),X'00'         MARK END OF LIST                             
         BR    RE                                                               
*&&                                                                             
**********************************************************************          
         EJECT                                                                  
**********************************************************************          
*  PASSCOMM - CREATE PASSIVE POINTER FOR NETWORK COMMENT RECORD                 
*                                                                               
* INPUTS - R7 - ADDRESS OF COMMENT RECORD                                       
*          R8 - WHERE TO PUT PASSIVE POINTER (POINTER LIST)                     
*                                                                               
* OUTPUTS - R8 - ADDRESS FOR NEXT POINTER                                       
*           POINTER LIST - PASSIVE POINTER + X'00' TERMINATION                  
**********************************************************                      
PASSCOMM EQU   *                                                                
*                                                                               
* IF NO 04 ELEMENT NO PASSIVE POINTER                                           
         USING NSUPEL,R3                                                        
         LR    R3,R7                                                            
         LA    R3,NCOMELEM-NCOMKEY(R3)                                          
PASSCM20 CLI   0(R3),0                                                          
         BE    PASSCMEX                                                         
         CLI   0(R3),4                                                          
         BE    PASSCM30                                                         
         ZIC   R2,1(R3)                                                         
         AR    R3,R2                                                            
         B     PASSCM20                                                         
*                                                                               
PASSCM30 DS    0H                                                               
         USING NCOMPKEY,R8                                                      
         MVC   NCOMPKEY,=20X'FF'   INITIALIZE PASSIVE POINTER                   
         MVI   NCOMPTYP,X'8C'      TYPE PASSIVE POINTER                         
         MVC   NCOMPAM,NCOMKAM-NCOMKEY(R7)     AGENCY/MEDIA                     
         MVC   NCOMPID,NCOMKID-NCOMKEY(R7)     COMMENT ID GROUP                 
         MVC   NCOMPIDA,NCOMKIDA-NCOMKEY(R7)   COMMENT SUB GROUP                
         MVC   NCOMPIDB,NCOMKIDB-NCOMKEY(R7)   ID PRINT CONTROL                 
         MVC   NCOMPCLT,NCOMKCLT-NCOMKEY(R7)   CLIENT                           
         MVC   NCOMPPRD,NSUPPROD               PRODUCT                          
         OC    NCOMPPRD,=3X'40'                                                 
         MVC   NCOMPEST,NCOMKEST-NCOMKEY(R7)   ESTIMATE                         
         MVC   NCOMPNET,NCOMKNET-NCOMKEY(R7)   NETWORK                          
         MVC   NCOMPDPT,NCOMKDPT-NCOMKEY(R7)   DAYPART                          
         MVC   NCOMPPKG,NCOMKPKG-NCOMKEY(R7)   PACKAGE                          
*                                                                               
         USING NURECD,R8             USE POINTER STATUS AND ADDRESS,            
*                                      NOT THAT OF RECORD!!!!!!!                
         MVC   NUKSTAT,NURSTAT-NUKEY(R7)    MOVE STATUS                         
         XC    NUDA,NUDA              SET DISK ADDRESS TO 0'S                   
         LA    R8,L'NUKEY+L'NUKSTAT+L'NUDA(R8)    NEXT LOCATION                 
*                                                                               
         MVI   0(R8),X'00'         MARK END OF LIST                             
PASSCMEX BR    RE                                                               
         DROP  R8,R3                                                            
         EJECT                                                                  
*                                                                               
DAYTABL  DC    X'4001'           MON                                            
         DC    X'2002'           TUE                                            
         DC    X'1003'           WED                                            
         DC    X'0804'           THU                                            
         DC    X'0405'           FRI                                            
         DC    X'0206'           SAT                                            
         DC    X'0107'           SUN                                            
         DC    X'7C08'           M-F                                            
         DC    X'7F09'           M-S                                            
         DC    X'FF0A'           VAR                                            
*                                                                               
BASPRGTB DS    CL120             UP TO 20 6 BYTE ENTRIES                        
*                                                                               
RECCNT   DS    F                                                                
DMCB     DS    6F                                                               
*                                                                               
         LTORG                                                                  
*                                                                               
       ++INCLUDE NEGENUNIT                                                      
       ++INCLUDE NEGENPACK                                                      
       PRINT GEN                                                                
       ++INCLUDE NEGENCOM                                                       
       PRINT NOGEN                                                              
       ++INCLUDE SPTRNEQPRG                                                     
       ++INCLUDE SPTRNPAT                                                       
*                                                                               
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'047UNLDCPTR  12/06/11'                                      
         END                                                                    
