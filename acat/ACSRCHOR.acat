*          DATA SET ACSRCHOR   AT LEVEL 012 AS OF 01/11/16                      
*CATALP ACSRCHOR                                                                
                                                                                
ACSRCHOR TITLE '- ACCOUNT SEARCH SUBROUTINES FOR ORDERS'                        
                                                                                
* TKLU 001 02Dec15 <RD009622> Order name search support                         
                                                                                
         PRINT NOGEN                                                            
ACSRCHOR CSECT                                                                  
         NMOD1 WORKL,ACSRCHOR,RA                                                
         USING WORKD,RC            RC=A(LOCAL W/S)                              
         ST    R1,APARMS                                                        
         L     R8,0(R1)                                                         
         USING SEARCHD,R8          R8=A(SEARCH CONTROL BLOCK)                   
         USING GETTXTD,SBTXTBLK                                                 
         LHI   R9,SVBLK-TWAD                                                    
         A     R9,SBATWA                                                        
         USING SVBLKD,R9           R9=A(SAVE AREA IN TWA)                       
         USING OFFALD,SVOFFAL                                                   
                                                                                
         MVI   SBRETURN,0          CLEAR ERROR FLAG                             
         CLI   0(R1),SDSSYPPQ                                                   
         JE    *+2                                                              
         OC    SBASYS,SBASYS                                                    
         JNZ   INIT2                                                            
         MVI   SBRETURN,SBENITQ    SEARCH BLOCK NOT INITIALIZED                 
         J     EXITN                                                            
                                                                                
         USING COMFACSD,RF                                                      
INIT2    L     RF,SBCOMFAC                                                      
         MVC   CUREDIT,CCUREDIT                                                 
         MVC   VDATAMGR,CDATAMGR                                                
         MVC   VGETFACT,CGETFACT                                                
         MVC   VCALLOV,CCALLOV                                                  
         MVC   VDICTATE,CDICTATE                                                
         MVC   VSCANNER,CSCANNER                                                
         MVC   VSWITCH,CSWITCH                                                  
         DROP  RF                                                               
                                                                                
         CLI   0(R1),ROUTSN                                                     
         JNL   EXITY               UNKNOWN ROUTINE                              
         LLC   RF,0(R1)                                                         
         SLL   RF,2                                                             
         B     ROUTS(RF)                                                        
                                                                                
ROUTS    DS    0XL4                                                             
         J     EXITY               SYSTEM KEY SET                               
         J     SETKEY              SET KEY                                      
         J     SEC                 SECURITY CHECK                               
         J     FLT                 FILTER                                       
         J     DIS                 DISPLAY                                      
ROUTSN   EQU   (*-ROUTS)/L'ROUTS                                                
                                                                                
***********************************************************************         
* SECURITY ROUTINES                                                   *         
***********************************************************************         
                                                                                
SEC      CLI   SBMODE,1            DIRECTORY LEVEL SECURITY                     
         JE    EXITY                                                            
         CLI   SBMODE,2            RECORD LEVEL SECURITY                        
         JE    EXITY                                                            
         CLI   SBMODE,0            INITIALIZE                                   
         JE    INITSEC                                                          
         J     EXITY               UNKNOWN MODE                                 
                                                                                
***********************************************************************         
* FILTER ROUTINES                                                     *         
***********************************************************************         
                                                                                
FLT      CLI   SBMODE,1            DIRECTORY LEVEL FILTERS                      
         JE    DIRFLT                                                           
         CLI   SBMODE,2            RECORD LEVEL FILTERS                         
         JE    RECFLT                                                           
         CLI   SBMODE,0            INITIALIZE                                   
         JE    INITFLT                                                          
         J     EXITY               UNKNOWN MODE                                 
                                                                                
***********************************************************************         
* DISPLAY ROUTINES                                                    *         
***********************************************************************         
                                                                                
DIS      CLI   SBMODE,1            DISPLAY RECORD                               
         JE    RECDIS                                                           
         CLI   SBMODE,0            DISPLAY HEADERS                              
         JE    INITDIS                                                          
         J     EXITY               UNKNOWN MODE                                 
                                                                                
***********************************************************************         
* EXITS                                                               *         
***********************************************************************         
                                                                                
EXITH    CLI   *,0                 CC=HIGH                                      
         J     EXIT                                                             
*                                                                               
EXITN    DS    0H                                                               
EXITL    CLI   *,FF                CC=LOW                                       
         J     EXIT                                                             
*                                                                               
EXITY    CR    RB,RB               CC=EQUAL                                     
EXIT     XIT1  ,                                                                
                                                                                
***********************************************************************         
* SET UP SEARCH RECORD                                                *         
***********************************************************************         
                                                                                
SETKEY   L     R2,4(R1)                                                         
         USING SONRECD,R2                                                       
         XC    SONKEY,SONKEY                                                    
         MVI   SONKTYP,SONKTYPQ                                                 
         MVC   SONKCPY,SBAGYB                                                   
         MVI   SONKSRC,SONKSRCQ                                                 
                                                                                
SETKEYY  J     EXITY                                                            
         DROP  R2                                                               
                                                                                
***********************************************************************         
* INITIALIZE SECURITY                                                 *         
***********************************************************************         
                                                                                
INITSEC  LA    RE,SVBLKD           CLEAR SAVE AREA                              
         LA    RF,SVBLKL                                                        
         XR    R1,R1                                                            
         MVCL  RE,R0                                                            
                                                                                
         MVC   DMCB,EFFS                                                        
         GOTO1 VGETFACT,DMCB,(X'80',0),F#UTLD  REPLACE UTLD BY FA               
         L     RF,0(R1)                                                         
         USING F@UTLD,RF           R4=A(UTL)                                    
         MVC   SVSECY+1(1),F@TAUTH+1                                            
         MVC   SVSYS,F@TSYS                                                     
         DROP  RF                                                               
                                                                                
         TM    SBCALLER,SBCALSOQ   TEST STAND ALONE CALL                        
         JZ    INITSEC2                                                         
         OI    SVINDS,SVIALONE                                                  
                                                                                
INITSEC2 JAS   RE,SETOFF                                                        
                                                                                
INITSECX B     EXITY                                                            
                                                                                
***********************************************************************         
* INITIALIZE FILTERS                                                  *         
***********************************************************************         
                                                                                
INITFLT  L     R2,4(R1)                                                         
         USING FHD,R2              R2=A(INPUT FIELD)                            
                                                                                
         OI    SBSTYLO,SBSTDFLQ    FORCE DIRECTORY FILTERING                    
                                                                                
IFLT04   CLI   FHIL,0              TEST FOR NO INPUT                            
         JNE   IFLT06                                                           
         MVI   SBRETURN,SBEMIFQ                                                 
         J     IFLTER3                                                          
                                                                                
IFLT06   XC    FTVALS,FTVALS                                                    
         MVI   SBLFILT,FTVALSL                                                  
         MVI   SBERRFLD,2          INDICATE KEY FIELD                           
         MVI   SBNNAMES,0          INDICATE NO NAMES FOUND YET                  
         GOTO1 DICTATE,DMCB,C'LU  ',DI,DO                                       
                                                                                
         LA    R5,IOREC                                                         
         USING SCANBLKD,R5         R5=A(SCAN BLOCK)                             
         GOTO1 VSCANNER,DMCB,(L'SCFLD2,FHD),('SCANBLKN',SCANBLKD),     *        
               C',=  '                                                          
         MVC   SCANN,4(R1)                                                      
         CLI   SCANN,SCANBLKN                                                   
         JNH   IFLT08                                                           
         MVI   SBRETURN,SBETMNQ                                                 
         J     IFLTER2                                                          
                                                                                
IFLT08   MVI   INDX,1              SET FIELD INDEX NO.                          
         XC    BTNOFILT,BTNOFILT                                                
                                                                                
IFLT10   CLI   SC1STLEN,0          TEST NO 1ST FIELD                            
         JE    IFLT28                                                           
         CLI   SC2NDLEN,0          TEST NO 2ND FIELD                            
         JNE   IFLT14                                                           
                                                                                
         CLI   SCFLD1,C'?'         TEST HELP WANTED                             
         JE    HELPFLT                                                          
         GOTO1 CMPWORD,DMCB,(SC1STLEN,SCFLD1),(L'AC@HELP,AC@HELP)               
         JE    HELPFLT                                                          
                                                                                
         CLI   SC1STLEN,SBNALENQ   PARSE NAME                                   
         JNH   IFLT12                                                           
         MVI   SBRETURN,SBEFTLQ    NAME TOO LONG                                
         J     IFLTER2                                                          
                                                                                
IFLT12   GOTO1 SBPARSER,DMCB,('SBPARSKQ',SEARCHD),(SC1STLEN,SCFLD1)             
         JNE   IFLTER2                                                          
         J     IFLT28                                                           
                                                                                
IFLT14   CLI   SC1STLEN,FLTLMINQ   TEST FIELD LONG ENOUGH                       
         JNL   IFLT16                                                           
         MVI   SBRETURN,SBEFTSQ                                                 
         J     IFLTER1                                                          
                                                                                
IFLT16   XR    R7,R7                                                            
                                                                                
         USING FLTTABD,R7          R7=A(FILTER TABLE ENTRY)                     
IFLT18   JAS   RE,NXTFLT                                                        
         JE    IFLT20                                                           
         MVI   SBRETURN,SBEIKWQ    E-O-T SO INVALID KEYWORD                     
         J     IFLTER1                                                          
                                                                                
IFLT20   XR    RF,RF               COMPARE LHS                                  
         ICM   RF,3,FLTLNAM                                                     
         GOTO1 CMPWORD,DMCB,(SC1STLEN,SCFLD1),(FLTLMAX,DO(RF))                  
         JNE   IFLT18                                                           
                                                                                
         TM    FLTINDS1,FLTIDUP    TEST DUPLICATES ALLOWED                      
         JNZ   IFLT22                                                           
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         JZ    IFLT22                                                           
         MVI   SBRETURN,SBEDKWQ                                                 
         J     IFLTER1                                                          
                                                                                
IFLT22   GOTO1 SETBIT,DMCB,(FLTCODE,FTUSED)                                     
                                                                                
         TM    FLTINDS1,FLTINOT    TEST NOTS ALLOWED                            
         JZ    IFLT24                                                           
         CLI   SCFLD2,C'*'                                                      
         JNE   IFLT24                                                           
         GOTO1 SETBIT,DMCB,(FLTCODE,FTNOT)                                      
         MVC   SCFLD2(L'SCFLD2-1),SCFLD2+1                                      
         MVI   SCFLD2+L'SCFLD2-1,C' '                                           
         IC    RF,SC2NDLEN                                                      
         BCTR  RF,0                                                             
         STC   RF,SC2NDLEN                                                      
                                                                                
IFLT24   CLC   SC2NDLEN,FLTRMIN    TEST FIELD TOO SHORT                         
         JNL   IFLT26                                                           
         MVI   SBRETURN,SBEFTSQ                                                 
         J     IFLTER1                                                          
                                                                                
IFLT26   CLC   SC2NDLEN,FLTRMAX    TEST FIELD TOO LONG                          
         JNH   IFLT27                                                           
         MVI   SBRETURN,SBEFTLQ                                                 
         J     IFLTER1                                                          
                                                                                
IFLT27   XR    RF,RF               TEST FOR VALIDATION ROUTINE                  
         ICM   RF,3,FLTVAL                                                      
         JZ    IFLT28                                                           
         B     ACSRCHOR(RF)        GO TO VALIDATION ROUTINE                     
                                                                                
VFX      JE    VFYES               RETURN POINTS FOR ROUTINES                   
VFNO     J     IFLTER1                                                          
VFYES    DS    0H                                                               
                                                                                
IFLT28   CLC   INDX,SCANN          TEST ALL FIELDS DONE                         
         JNL   IFLT30                                                           
         IC    RE,INDX             INCREMENT INDEX NO.                          
         LA    RE,1(RE)                                                         
         STC   RE,INDX                                                          
         LA    R5,SCANBLKL(R5)                                                  
         J     IFLT10                                                           
                                                                                
IFLT30   XR    R7,R7               SET ANY DEFAULT VALUES                       
IFLT32   JAS   RE,NXTFLT                                                        
         JNE   IFLT40                                                           
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         JO    IFLT32              FILTER HAS BEEN USED                         
         XR    RF,RF                                                            
         ICM   RF,3,FLTDEF                                                      
         JZ    IFLT32                                                           
         B     ACSRCHOR(RF)        GO TO SET DEFAULT ROUTINE                    
DFYES    GOTO1 SETBIT,DMCB,(FLTCODE,FTUSED)                                     
DFNO     J     IFLT32                                                           
                                                                                
IFLT40   XC    FTUSED,BTNOFILT     TURN OFF NO FILTERS                          
                                                                                
IFLTYES  J     EXITY                                                            
                                                                                
IFLTER1  DS    0H                  INSERT LHS INTO ERROR MESSAGE                
         LA    RF,SBTXTBLK                                                      
         USING GETTXTD,RF          RF=A(GETTXT BLOCK)                           
         CLI   GTLTXT,0                                                         
         JNE   IFLTER1A                                                         
         MVC   GTLTXT,SC1STLEN                                                  
         MVC   SBOPTTXT(L'SCFLD1),SCFLD1                                        
IFLTER1A LA    RE,SBOPTTXT                                                      
         STCM  RE,7,GTATXT                                                      
                                                                                
IFLTER2  DS    0H                  DISPLAY INDEX # AFTER ERROR MESSAGE          
         MVC   SBTXTBLK+(GTINDX-GETTXTD)(L'INDX),INDX                           
                                                                                
IFLTER3  CLI   SBRETURN,0                                                       
         JNE   EXITN                                                            
         MVI   SBRETURN,SBEIFVQ                                                 
         J     EXITN                                                            
         DROP  RF                                                               
                                                                                
***********************************************************************         
* DISPLAY HELP FOR FILTERS                                            *         
***********************************************************************         
                                                                                
HELPFLT  MVC   WORK,SPACES                                                      
         LA    R4,WORK                                                          
         XR    R7,R7                                                            
                                                                                
HFLT02   JAS   RE,NXTFLT           GET NEXT TABLE ENTRY                         
         JNE   HFLT10                                                           
         XR    RE,RE                                                            
         ICM   RE,3,FLTLNAM        COPY KEYWORD                                 
         LA    RE,DO(RE)                                                        
         LLC   RF,FLTLMAX                                                       
         BCTR  RF,0                                                             
         MVC   0(0,R4),0(RE)                                                    
         EX    RF,*-6                                                           
         AR    R4,RF                                                            
HFLT04   CLI   0(R4),C' '          FIND END OF KEYWORD                          
         JH    HFLT06                                                           
         JCT   R4,HFLT04                                                        
HFLT06   MVI   1(R4),C','          APPEND COMMA                                 
         LA    R4,2(R4)                                                         
         J     HFLT02                                                           
                                                                                
HFLT10   BCTR  R4,0                REMOVE LAST COMMA                            
         MVI   0(R4),C' '                                                       
         LLC   RF,FHLN             COPY LIST TO OUTPUT                          
         SHI   RF,FHDAD+FHDAD+1                                                 
         MVC   FHDA(0),WORK                                                     
         EX    RF,*-6                                                           
                                                                                
HELPFLTX MVI   SBRETURN,SBEHLPQ                                                 
         J     EXITN                                                            
                                                                                
***********************************************************************         
* VALIDATE FILTER ROUTINES                                            *         
***********************************************************************         
                                                                                
VFRNAM   XR    RF,RF               ** = KEYWORD **                              
         ICM   RF,3,FLTRNAM                                                     
         GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(FLTRMAX,DO(RF))                  
         J     VFX                                                              
*                                  ** =YES/ONLY/NO **                           
VFYON    GOTO1 CMPWORD,DMCB,(SC2NDLEN,SCFLD2),(L'AC@YES,AC@YES)                 
         JNE   VFYON02                                                          
         GOTO1 SETBIT,(R1),(FLTCODE,BTNOFILT)                                   
         J     VFYES                                                            
VFYON02  GOTO1 (RF),(R1),,(L'AC@ONLY,AC@ONLY)                                   
         JE    VFYES                                                            
         GOTO1 (RF),(R1),,(L'AC@NO,AC@NO)                                       
         JNE   VFNO                                                             
         GOTO1 SETBIT,(R1),(FLTCODE,FTNOT)                                      
         J     VFYES                                                            
*                                  ** NAME **                                   
VFNAME   GOTO1 SBPARSER,DMCB,('SBPARSKQ',SEARCHD),(SC2NDLEN,SCFLD2)             
         J     VFX                                                              
                                                                                
***********************************************************************         
* SET DEFAULT FILTER VALUES                                           *         
***********************************************************************         
                                                                                
*                                  ** LOCKED=NO **                              
DFLOCKED GOTO1 SETBIT,DMCB,(FLTCODE,FTNOT)                                      
         J     DFYES                                                            
         DROP  R2,R7,R5                                                         
                                                                                
***********************************************************************         
* INITIALIZE DISPLAY - SET UP HEADLINES                               *         
***********************************************************************         
                                                                                
INITDIS  L     RF,SBATWA           TEST FOR A REPORT                            
         ICM   R1,15,REPACOM-TWAD(RF)                                           
         JZ    *+8                                                              
         OI    SVINDS,SVIREP                                                    
                                                                                
         XC    SBEXHEAD,SBEXHEAD                                                
         MVI   SBEXHNHD,2          SET NO. OF HEADLINES                         
                                                                                
         LA    R6,SBEXHTIT         R6=A(TITLE)                                  
         MVCDD 0(22,R6),AC#ORDER                                                
         GOTO1 DICTATE,DMCB,C'SL  ',(R6)                                        
         LR    R0,R6                                                            
         LA    R6,21(R6)                                                        
         CLI   0(R6),C' '                                                       
         JH    *+8                                                              
         JCT   R6,*-8                                                           
         LA    R6,1(R6)                                                         
                                                                                
         LR    RE,R6                                                            
         SR    RE,R0                                                            
         STC   RE,SBEXHTIL                                                      
                                                                                
         XR    R5,R5               R5=ITEM DISPLACEMENT                         
         XR    R7,R7                                                            
         USING CLMTABD,R7          R7=A(COLUMN TABLE ENTRY)                     
                                                                                
IDIS02   JAS   RE,NXTCLM                                                        
         JNE   IDIS10                                                           
         SHI   RF,CLMWDTH-CLMTABD+1                                             
         MVC   2(0,R6),CLMWDTH                                                  
         EX    RF,*-6                                                           
         LA    RF,3(RF)                                                         
         STC   RF,0(R6)            SET ENTRY LENGTH                             
         STC   R4,1(R6)            SET ITEM DISPLACEMENT                        
                                                                                
         TM    CLMINDS1,CLMIKEY+CLMINAME                                        
         JZ    IDIS08                                                           
         LA    RE,SBEXHEAD                                                      
         SR    RE,R6                                                            
         LPR   RE,RE                                                            
         TM    CLMINDS1,CLMIKEY                                                 
         JZ    *+12                                                             
         STC   RE,SBEXHDKY         SET DISPLACEMENT TO KEY                      
         J     *+8                                                              
         STC   RE,SBEXHDNA         SET DISPLACEMENT TO NAME                     
                                                                                
IDIS08   JXH   R6,RF,IDIS02                                                     
         DROP  R7                                                               
                                                                                
IDIS10   MVI   0(R6),EOT                                                        
                                                                                
INITDISX J     EXITY                                                            
                                                                                
***********************************************************************         
* FILTER ON SEARCH DIRECTORY RECORD (CHECK OFFICE IS OK)              *         
***********************************************************************         
                                                                                
DIRFLT   L     R2,4(R1)                                                         
         L     RF,SBATWA                                                        
         USING SONRECD,R2                                                       
         USING OFLPASD,IOKEY                                                    
         CLI   TWAACCS-TWAD(RF),0  GLOBAL LOGON CAN VIEW EVERYTHING             
         JE    DIRFLTY                                                          
                                                                                
         CLC   SONKSTA+ORDSOFF-ORDKSTAT(2),SPACES                               
         JNH   DIRFLTY                                                          
         TM    COMPSTA4,CPYSOFF2   2 CHARACTER OFFICES                          
         JZ    DIRFLT06                                                         
         CLC   SONKSTA+ORDSOFF-ORDKSTAT(2),TWAACCS+2-TWAD(RF)                   
         JE    DIRFLTY            CHECK MATCH ON OFFICE LIST FIRST              
                                                                                
         XC    IOKEY,IOKEY                                                      
         XC    OFLPAS,OFLPAS       CHECK WHETHER OFFICE IS PART OF              
         MVI   OFLPTYP,OFLPTYPQ    OFFICE LIST                                  
         MVC   OFLPCPY,SBAGYB                                                   
         MVI   OFLPSUB,OFLPSUBQ                                                 
         MVC   OFLPOFF,TWAACCS+2-TWAD(RF)                                       
         MVC   SVIOKEY,IOKEY                                                    
         GOTO1 VDATAMGR,DMCB,DMRDHI,ACCDIR,IOKEY,IOKEY                          
         J     DIRFLT04                                                         
                                                                                
DIRFLT02 GOTO1 VDATAMGR,DMCB,DMRSEQ,ACCDIR,IOKEY,IOKEY                          
                                                                                
DIRFLT04 CLC   SVIOKEY(OFLPOFL-OFLPASD),IOKEY                                   
         JNE   DIRFLT06                                                         
         CLC   OFLPOFF,SONKSTA+ORDSOFF-ORDKSTAT                                 
         JE    DIRFLTY               CHECK MATCH ON OFFICE LIST                 
         J     DIRFLT02                                                         
                                                                                
DIRFLT06 MVI   OFFAACT,OFFAVAL     TEST OFFICE IS VALID FOR THIS LOGON          
         XR    RF,RF                                                            
         TM    COMPSTA4,CPYSOFF2   2 CHARACTER OFFICES                          
         JZ    *+8                                                              
         LA    RF,1                                                             
         MVC   OFFAOFFC(0),SONKSTA+ORDSOFF-ORDKSTAT                             
         EX    RF,*-6                                                           
         GOTO1 VOFFAL,OFFALD                                                    
         JE    DIRFLTY                                                          
         J     DIRFLTN                                                          
                                                                                
DIRFLTY  J     EXITY                                                            
DIRFLTN  J     EXITN                                                            
         DROP  R2                                                               
                                                                                
***********************************************************************         
* FILTER ON ACCMST RECORD                                             *         
***********************************************************************         
                                                                                
RECFLT   L     R2,4(R1)                                                         
         USING ORDRECD,R2          R2=A(ORDER RECORD)                           
         LA    R3,ORDRFST                                                       
         XR    R7,R7                                                            
                                                                                
         USING FLTTABD,R7          R7=FILTER TABLE ENTRY                        
RFLT02   JAS   RE,NXTFLT                                                        
         JNE   RECFLTY             E-O-T                                        
                                                                                
         GOTO1 TESTBIT,DMCB,(FLTCODE,FTUSED)                                    
         JZ    RFLT02                                                           
         XR    RF,RF                                                            
         ICM   RF,3,FLTREC                                                      
         JZ    RFLT02                                                           
         B     ACSRCHOR(RF)        GO TO FILTER RECORD ROUTINE                  
                                                                                
RFX      JNE   RFNO                RETURN POINTS FOR ROUTINES                   
RFYES    GOTO1 TESTBIT,DMCB,(FLTCODE,FTNOT)                                     
         JZ    RFLT02              MATCH FOR '=' FILTER                         
         J     RECFLTN                                                          
                                                                                
RFNO     GOTO1 TESTBIT,DMCB,(FLTCODE,FTNOT)                                     
         JO    RFLT02              NO MATCH FOR 'NOT=' FILTER                   
                                                                                
RECFLTN  MVI   SBRETURN,SBRFLTQ                                                 
         J     EXITN                                                            
RECFLTY  J     EXITY                                                            
         DROP  R7                                                               
                                                                                
***********************************************************************         
* DISPLAY RECORD                                                      *         
***********************************************************************         
                                                                                
RECDIS   L     R2,8(R1)                                                         
         USING ORDRECD,R2          R2=A(ESTIMATE RECORD)                        
         L     R6,4(R1)            R6=A(OUTPUT AREA)                            
         MVI   0(R6),C' '                                                       
         MVC   1(255,R6),0(R6)                                                  
                                                                                
         XR    R5,R5               R5=ITEM DISPLACEMENT                         
         XR    R7,R7                                                            
         USING CLMTABD,R7          R7=COLUMN TABLE ENTRY                        
                                                                                
RDIS02   JAS   RE,NXTCLM                                                        
         JNE   RDIS10                                                           
         AR    R4,R6               R4=A(OUTPUT AREA)                            
         XR    RF,RF                                                            
         ICM   RF,3,CLMRTN                                                      
         B     ACSRCHOR(RF)                                                     
                                                                                
DISX     J     RDIS02                                                           
                                                                                
RDIS10   J     EXITY                                                            
                                                                                
***********************************************************************         
* DISPLAY ORDER NUMBER                                                *         
***********************************************************************         
                                                                                
DISCODE  DS    0H                                                               
         MVC   0(L'ORDKORD,R4),ORDKORD                                          
         J     DISX                                                             
                                                                                
***********************************************************************         
* DISPLAY ORDER NAME                                                  *         
***********************************************************************         
                                                                                
DISNAM   GOTO1 GETEL,DMCB,('ENMELQ',ORDRECD)                                    
         JNE   DISX                                                             
         SHI   RF,ENMLNQ+1        *************************************         
         JM    DISX               * LIMITED TO 50 CHARS FOR ESTIMATES *         
         CHI   RF,L'ENMNAME+1     * AND FOR ORDER (WHICH CAN BE UP TO *         
         JNH   *+8                * 100 BYTES LONG)                   *         
         LHI   RF,L'ENMNAME+1     *************************************         
         MVC   0(0,R4),ENMNAME-ENMELD(R1)                                       
         EX    RF,*-6                                                           
         J     DISX                                                             
                                                                                
***********************************************************************         
* DISPLAY ORDER OFFICE                                                *         
***********************************************************************         
                                                                                
*DISOFF  MVC   0(2,R4),ORDROFF                                                  
*        J     DISX                                                             
                                                                                
***********************************************************************         
* STATUS                                                              *         
***********************************************************************         
                                                                                
DISSTAT  MVC   0(6,R4),SPACES                                                   
         TM    ORDRSTA2,ORDSEXEX                                                
         JZ    DISX                                                             
         MVC   0(6,R4),AC@ESLDL   STCOLDLT                                      
         TM    ORDRSTAT,ORDSLDEL                                                
         JNZ   DISX                                                             
         MVC   0(6,R4),AC@DRAFT   STCODRFT                                      
         TM    ORDRSTA2,ORDSDRFT                                                
         JNZ   DISX                                                             
         MVC   0(6,R4),AC@ESSUB   STCOSUBM                                      
         TM    ORDRSTA2,ORDSSUBM                                                
         JNZ   DISX                                                             
         MVC   0(6,R4),AC@ESPAP   STCOPAPP                                      
         TM    ORDRSTA2,ORDSPAPP                                                
         JNZ   DISX                                                             
         MVC   0(6,R4),AC@APRVD   STCOFAPP                                      
         TM    ORDRSTA2,ORDSAPPR                                                
         JNZ   DISX                                                             
         MVC   0(6,R4),AC@ESREJ   STCOREJ                                       
         TM    ORDRSTA2,ORDSOREJ                                                
         JNZ   DISX                                                             
         MVC   0(6,R4),SPACES                                                   
         J     DISX                                                             
         DROP  R2,R7                                                            
                                                                                
***********************************************************************         
* ROUTINE TO GET A(NEXT COLUMN TABLE ENTRY)                           *         
*                                                                     *         
* NTRY: R7=A(COLUMN CODE ENTRY)                                       *         
*       R5=CURRENT DISPLACEMENT IN LIST LINE                          *         
* EXIT: IF END OF LIST, CC=NOT EQUAL                                  *         
*       R7=A(COLUMN TABLE ENTRY), RF=L(COLUMN TABLE ENTRY)            *         
*       R4=OLD LINE DISPLACEMENT                                      *         
*       R5=NEW LINE DISPLACEMENT                                      *         
***********************************************************************         
                                                                                
         USING CLMTABD,R7                                                       
NXTCLM   LTR   R7,R7                                                            
         JNZ   NCLM02                                                           
         LA    R7,CLMTAB                                                        
         J     NCLM04                                                           
                                                                                
NCLM02   XR    RF,RF                                                            
         ICM   RF,1,CLMLEN                                                      
         AR    R7,RF                                                            
                                                                                
NCLM04   XR    RF,RF                                                            
         ICM   RF,1,CLMLEN                                                      
         JZ    NXTCLMN                                                          
                                                                                
         LR    R4,R5               R4=OLD DISPLACEMENT                          
         IC    R5,CLMWDTH                                                       
         AR    R5,R4               R5=NEW DISPLACEMENT                          
                                                                                
NXTCLMY  CR    RB,RB                                                            
         BR    RE                                                               
                                                                                
NXTCLMN  LTR   RB,RB                                                            
         BR    RE                                                               
         DROP  R7                                                               
                                                                                
***********************************************************************         
* ROUTINE TO GO TO NEXT ENTRY OF THE FILTER TABLE                     *         
*                                                                     *         
* NTRY: R7=A(CURRENT ENTRY) OR 0 TO START                             *         
* EXIT: R7=A(NEXT ENTRY)                                              *         
*       CC=LOW IF END-OF TABLE REACHED                                *         
***********************************************************************         
                                                                                
         USING FLTTABD,R7                                                       
NXTFLT   LR    R0,RE                                                            
         LTR   R7,R7                                                            
         JNZ   NFLT08                                                           
         LA    R7,FLTTAB                                                        
                                                                                
NFLT02   CLI   FLTTABD,EOT         TEST E-O-T                                   
         JE    NXTFLTN                                                          
                                                                                
         GOTO1 TESTCTRY,FLTCTRY    MATCH ON COUNTRY                             
         JE    NXTFLTY                                                          
                                                                                
NFLT08   LA    R7,FLTTABL(R7)                                                   
         J     NFLT02                                                           
                                                                                
NXTFLTN  LTR   RB,RB                                                            
         J     *+6                                                              
NXTFLTY  CR    RB,RB                                                            
         LR    RE,R0                                                            
         BR    RE                                                               
         DROP  R7                                                               
                                                                                
***********************************************************************         
* ROUTINE TO PARTITION INPUT                                          *         
*                                                                     *         
* NTRY: P1=(L'INPUT,A(INPUT))                                         *         
*                                                                     *         
***********************************************************************         
         SPACE 1                                                                
PARTINP  NTR1  ,                                                                
         MVI   PARTALN,0                                                        
         XC    PARTA,PARTA                                                      
         MVI   PARTBLN,0                                                        
         MVI   PARTB,FF                                                         
         MVC   PARTB+1(L'PARTB-1),PARTB                                         
         XR    R2,R2                                                            
         ICM   R2,7,1(R1)          R2=A(INPUT)                                  
                                                                                
         LLC   RE,0(R1)            RE=L(INPUT)                                  
         LR    RF,RE                                                            
         LR    R1,R2               R1=A(INPUT)                                  
         CLI   0(R1),C'-'                                                       
         JE    *+12                                                             
         LA    R1,1(R1)                                                         
         JCT   RE,*-12                                                          
                                                                                
         SR    RF,RE               COPY PART A                                  
         JZ    PINP02                                                           
         STC   RF,PARTALN                                                       
         BCTR  RF,0                                                             
         MVC   PARTA(0),0(R2)                                                   
         EX    RF,*-6                                                           
         LTR   RE,RE               TEST PARTITION CHARACTER FOUND               
         JNZ   PINP02                                                           
         MVC   PARTBLN,PARTALN     NO SO PARTB=PARTA                            
         MVC   PARTB(0),0(R2)                                                   
         EX    RF,*-6                                                           
         J     PARTINPX                                                         
                                                                                
PINP02   JCT   RE,*+8                                                           
         J     PARTINPX                                                         
         STC   RE,PARTBLN                                                       
         BCTR  RE,0                                                             
         MVC   PARTB(0),1(R1)                                                   
         EX    RE,*-6                                                           
                                                                                
PARTINPX J     EXIT                                                             
                                                                                
***********************************************************************         
* ROUTINE TO COMPARE INPUT WITH WORD                                  *         
*                                                                     *         
* NTRY: P1=(L'INPUT, A(INPUT))                                        *         
*       P2=(L'WORD, A(WORD))                                          *         
***********************************************************************         
                                                                                
CMPWORD  STM   RE,R3,12(RD)                                                     
         LM    R2,R3,0(R1)                                                      
         IC    RF,0(R1)                                                         
         CLC   0(1,R1),4(R1)                                                    
         JNH   *+8                                                              
         IC    RF,4(R1)                                                         
         BCTR  RF,0                                                             
         EX    RF,CMPWORDC                                                      
CMPWORDX LM    RE,R3,12(RD)                                                     
         BR    RE                                                               
                                                                                
CMPWORDC CLC   0(0,R2),0(R3)                                                    
                                                                                
***********************************************************************         
* ROUTINE TO SET A BIT OF A BIT TABLE                                 *         
*                                                                     *         
* NTRY: P1=(BIT CODE, A(BIT TABLE))                                   *         
***********************************************************************         
                                                                                
SETBIT   LLC   RF,0(R1)                                                         
         BCTR  RF,0                                                             
         L     R0,0(R1)                                                         
         LA    R1,X'07'                                                         
         NR    R1,RF                                                            
         LA    R1,MASKS(R1)        R1=A(MASK)                                   
         SRL   RF,3                                                             
         AR    RF,R0               RF=A(BYTE OF BIT)                            
         OC    0(1,RF),0(R1)                                                    
         BR    RE                                                               
                                                                                
***********************************************************************         
* ROUTINE TO TEST A BIT OF A BIT TABLE                                *         
*                                                                     *         
* NTRY: P1=(BIT CODE, A(BIT TABLE))                                   *         
***********************************************************************         
                                                                                
TESTBIT  LLC   RF,0(R1)                                                         
         BCTR  RF,0                                                             
         L     R0,0(R1)                                                         
         LA    R1,X'07'                                                         
         NR    R1,RF                                                            
         IC    R1,MASKS(R1)        R1=MASK                                      
         SRL   RF,3                                                             
         AR    RF,R0               RF=A(BYTE OF BIT)                            
         EX    R1,TESTBITM         EX A TM                                      
         BR    RE                                                               
                                                                                
TESTBITM TM    0(RF),0                                                          
                                                                                
MASKS    DC    X'8040201008040201'                                              
                                                                                
***********************************************************************         
* TEST COUNTRY FILTER                                                 *         
*                                                                     *         
* NTRY: R1=A(COUNTRY)                                                 *         
***********************************************************************         
                                                                                
TESTCTRY CLI   0(R1),ALL           TEST ALL COUNTRIES                           
         JE    TCTRYYES                                                         
         CLC   SBCTRY,0(R1)        MATCH ON COUNTRY                             
         JE    TCTRYYES                                                         
         TM    0(R1),NOT                                                        
         JZ    TCTRYNO                                                          
         MVC   BYTE,0(R1)          MATCH ON NOT A COUNTRY                       
         XI    BYTE,NOT                                                         
         CLC   BYTE,SBCTRY                                                      
         JE    TCTRYNO                                                          
                                                                                
TCTRYYES CR    RE,RE                                                            
         BR    RE                                                               
TCTRYNO  LTR   RE,RE                                                            
         BR    RE                                                               
                                                                                
***********************************************************************         
* CALL DICTATE                                                        *         
***********************************************************************         
                                                                                
DICTATE  NTR1  ,                                                                
         MVI   2(R1),ACCSYSQ       SET ACCOUNT SYSTEM                           
         GOTO1 VDICTATE                                                         
         XR    RE,RE                                                            
         ICM   RE,7,EFFS                                                        
         GOTO1 VSWITCH,(R1),(RE)   TEST DICTATE SWITCHED                        
         L     RE,0(R1)                                                         
         CLC   SVSYS,TCBSEN-TCBD(RE)                                            
         JE    EXIT                SWITCH BACK IF IT DID                        
         GOTO1 (RF),(R1),(SVSYS,0),0                                            
         JE    EXIT                                                             
         DC    H'0'                SWITCH FAILED                                
                                                                                
***********************************************************************         
* GET ELEMENT ROUTINE                                                 *         
*                                                                     *         
* NTRY: P1=(ELEMENT CODE, A(RECORD))                                  *         
* EXIT: R1=A(ELEMENT), RF=L(ELEMENT)                                  *         
***********************************************************************         
                                                                                
GETEL    ST    RE,12(RD)                                                        
         LR    RE,R1                                                            
                                                                                
         L     R1,0(RE)                                                         
         LA    R1,ACCRFST-ACCRECD(R1)                                           
         LHI   R0,100                                                           
         XR    RF,RF                                                            
                                                                                
GETEL02  CLI   0(R1),0                                                          
         JE    GETELNO                                                          
         IC    RF,1(R1)                                                         
         CLC   0(1,R1),0(RE)                                                    
         JE    GETELYES                                                         
         AR    R1,RF                                                            
         JCT   R0,GETEL02                                                       
                                                                                
GETELNO  LTR   RB,RB                                                            
         J     *+6                                                              
                                                                                
GETELYES CR    RB,RB                                                            
         L     RE,12(RD)                                                        
         BR    RE                                                               
                                                                                
***********************************************************************         
* ROUTINE TO INITIALIZE OFFICE SECURITY FOR USER                      *         
***********************************************************************         
                                                                                
SETOFF   NTR1  ,                                                                
         LA    R2,IOKEY            READ THE COMPANY RECORD                      
         USING CPYRECD,R2                                                       
         MVC   CPYKEY,SPACES                                                    
         MVC   CPYKCPY,SBAGYB                                                   
         GOTO1 VDATAMGR,DMCB,DMREAD,ACCMST,CPYKEY,IOREC,WORK                    
         JNE   *+2                                                              
         DROP  R2                                                               
         XR    RF,RF                                                            
         LA    R3,IOREC+(CPYRFST-CPYRECD)                                       
         USING CPYELD,R3           R3=A(COMPANY ELEMENT)                        
         CLI   CPYEL,CPYELQ                                                     
         JE    *+12                                                             
         IC    RF,CPYLN                                                         
         JXH   R3,RF,*-12                                                       
                                                                                
         MVC   DMCB,EFFS                                                        
         GOTO1 VGETFACT,DMCB,(X'80',0),F#UTLD  REPLACE UTLD BY FA               
         L     R4,0(R1)                                                         
         USING F@UTLD,R4           R4=A(UTL)                                    
         MVC   SVSECY+1(1),F@TAUTH+1                                            
         MVC   SVSYS,F@TSYS        EXTRACT                                      
                                                                                
         MVI   OFFACTRL,OFFACNEW                                                
         MVC   OFFACOMF,SBCOMFAC                                                
         MVC   OFFAALPH,SBAGY                                                   
         MVC   OFFACPY,SBAGYB                                                   
         MVC   OFFACST1,CPYSTAT1                                                
         MVC   OFFACST2,CPYSTAT2                                                
         MVC   OFFACST3,CPYSTAT3                                                
         MVC   OFFACST4,CPYSTAT4                                                
         MVC   COMPSTA4,CPYSTAT4                                                
         MVC   OFFACST5,CPYSTAT5                                                
         MVC   OFFACST6,CPYSTAT6                                                
         MVC   OFFACST7,CPYSTAT7                                                
         MVC   OFFACST8,CPYSTAT8                                                
         MVC   OFFALIMA,F@TACCS                                                 
         MVC   OFFAAUTH,F@TAUTH                                                 
         MVI   OFFAINDS,OFFAIOFF   OFFICE SECURITY CHECK                        
         MVI   OFFAACT,OFFAINI     INITIALIZE                                   
         GOTO1 VCALLOV,DMCB,0,X'D9000A62'                                       
         MVC   VOFFAL,0(R1)                                                     
         GOTO1 VOFFAL,OFFALD                                                    
         JE    EXIT                                                             
         DROP  R3,R4                                                            
                                                                                
***********************************************************************         
* CONSTANTS AND LITERALS                                              *         
***********************************************************************         
                                                                                
FF       EQU   X'FF'                                                            
EOT      EQU   X'00'               END-OF-TABLE                                 
ALL      EQU   X'00'               ALL                                          
NOT      EQU   X'80'               NOT                                          
ACCSYSQ  EQU   6                   ACCOUNT SYSTEM                               
                                                                                
         LTORG                                                                  
                                                                                
SPACES   DC    CL80' '                                                          
EFFS     DC    12X'FF'                                                          
                                                                                
DMREAD   DC    C'DMREAD  '                                                      
DMRSEQ   DC    C'DMRSEQ  '                                                      
DMRDHI   DC    C'DMRDHI  '                                                      
ACCMST   DC    C'ACCMST  '                                                      
ACCDIR   DC    C'ACCDIR  '                                                      
TITLEDIV DC    C' / '                                                           
                                                                                
DI       DS    0X                  ** DICTIONARY IN **                          
         DCDDL AC#NAME,8                                                        
         DCDDL AC#STT,8                                                         
         DCDDL AC#YES,4                                                         
         DCDDL AC#NO,4                                                          
         DCDDL AC#ONLY,8                                                        
         DCDDL AC#HELP,4                                                        
         DCDDL AC#ALL,4                                                         
         DCDDL AC#ESLDL,6                                                       
         DCDDL AC#DRAFT,6                                                       
         DCDDL AC#ESSUB,6                                                       
         DCDDL AC#ESPAP,6                                                       
         DCDDL AC#APRVD,6                                                       
         DCDDL AC#ESREJ,6                                                       
DIX      DC    AL1(0)                                                           
                                                                                
***********************************************************************         
* COLUMN TABLE                                                        *         
***********************************************************************         
                                                                                
CLMTABD  DSECT                                                                  
CLMLEN   DS    XL1                 LENGTH OF TABLE ENTRY                        
CLMINDS1 DS    XL1                 * INDICATORS *                               
CLMIKEY  EQU   X'80'               COLUMN IS KEY                                
CLMINAME EQU   X'40'               COLUMN IS NAME                               
CLMRTN   DS    XL2                 DISPLACEMENT TO DISPLAY ROUTINE              
CLMWDTH  DS    XL1                 COLUMN WIDTH                                 
CLMNHD   DS    XL1                 NO. OF HEADINGS                              
CLMHEADS DS    0X                  LIST OF COLUMN HEADINGS                      
CLMHWDTH DS    XL1                 WIDTH OF HEADING                             
CLMHEAD  DS    0X                  HEADING                                      
                                                                                
ACSRCHOR CSECT                                                                  
CLMTAB   DS    0X                                                               
                                                                                
CCODE    DC    AL1(CCODEX-CCODE,CLMIKEY)                                        
         DC    AL2(DISCODE-ACSRCHOR)                                            
         DC    AL1(L'ORDKORD,1)                                                 
         DC    AL1(4)                                                           
         DCDD  AC#CODE,5,PAD=NONE                                               
CCODEX   DS    0X                                                               
                                                                                
CNAM     DC    AL1(CNAMX-CNAM,CLMINAME)                                         
         DC    AL2(DISNAM-ACSRCHOR)                                             
         DC    AL1(L'ENMNAME,1)                                                 
         DC    AL1(4)                                                           
         DCDD  AC#NAME,L'ENMNAME,PAD=NONE                                       
CNAMX    DS    0X                                                               
                                                                                
CSTAT    DC    AL1(CSTATX-CSTAT,0)                                              
         DC    AL2(DISSTAT-ACSRCHOR)                                            
         DC    AL1(6,1)                                                         
         DC    AL1(4)                                                           
         DCDD  AC#STT,6,PAD=NONE                                                
CSTATX   DS    0X                                                               
                                                                                
* not active as =Search doesn't allow for < 3 columns ...                       
*OFF     DC    AL1(COFFX-COFF,0)                                                
*        DC    AL2(DISOFF-ACSRCHOR)                                             
*        DC    AL1(2,1)                                                         
*        DC    AL1(4)                                                           
*        DCDD  AC#OFF,3,PAD=NONE                                                
*OFFX    DS    0X                                                               
                                                                                
CLMTABX  DC    AL1(EOT)                                                         
                                                                                
***********************************************************************         
* FILTER TABLE                                                        *         
***********************************************************************         
                                                                                
FLTTABD  DSECT                                                                  
FLTCODE  DS    XL1                 A NUMBER (1-32)                              
FLTCTRY  DS    XL1                 COUNTRY                                      
FLTINDS1 DS    XL1                 INDICATORS BYTE 1                            
FLTINOT  EQU   X'80'               'NOT EQUAL' IS VALID                         
FLTIDUP  EQU   X'40'               FILTER CAN BE DUPLICATED                     
FLTINDS2 DS    XL1                 INDICATORS BYTE 2                            
FLTLNAM  DS    AL2                 DISPLACEMENT TO LHS NAME                     
FLTLMINQ EQU   2                   MINIMUM INPUT LENGTH FOR LHS                 
FLTLMAX  DS    XL1                 MAXIMUM INPUT LENGTH FOR LHS                 
FLTRNAM  DS    AL2                 DISPLACEMENT TO RHS (IF KEYWORD)             
FLTRMIN  DS    XL1                 MINIMUM INPUT LENGTH FOR RHS                 
FLTRMAX  DS    XL1                 MAXIMUM INPUT LENGTH FOR RHS                 
FLTVAL   DS    AL2                 DISP. TO VALIDATION ROUTINE                  
FLTDEF   DS    AL2                 DISP. TO SET DEFAULT VALUE ROUTINE           
FLTREC   DS    AL2                 DISP. TO FILTER RECORD ROUTINE               
FLTTABL  EQU   *-FLTTABD                                                        
                                                                                
ACSRCHOR CSECT                                                                  
                                                                                
FLTTAB   DS    0H                                                               
                                                                                
         DC    AL1(((*-FLTTAB)/FLTTABL)+1)                                      
         DC    AL1(ALL,FLTIDUP,0)                                               
         DC    AL2(AC@NAME-DO),AL1(L'AC@NAME)                                   
         DC    AL2(0),AL1(1,SBNALENQ)                                           
         DC    AL2(VFNAME-ACSRCHOR,0,0)                                         
                                                                                
FLTTABX  DC    AL1(EOT)                                                         
                                                                                
***********************************************************************         
* LOCAL WORKING STORAGE                                               *         
***********************************************************************         
                                                                                
WORKD    DSECT                                                                  
DUB      DS    D                                                                
FULL     DS    F                                                                
HALF     DS    H                                                                
DMCB     DS    6F                                                               
                                                                                
CUREDIT  DS    A                                                                
VDATAMGR DS    A                                                                
VCALLOV  DS    A                                                                
VDICTATE DS    A                                                                
VGETFACT DS    V                                                                
VSCANNER DS    A                                                                
VSWITCH  DS    A                                                                
                                                                                
APARMS   DS    A                   ADDRESS CALLERS PARAMETER LIST               
WORK     DS    XL256                                                            
BYTE     DS    X                                                                
BTNOFILT DS    XL(L'FTUSED)        NO FILTERS                                   
                                                                                
PARTALN  DS    XL1                                                              
PARTA    DS    XL32                                                             
PARTBLN  DS    XL1                                                              
PARTB    DS    XL32                                                             
                                                                                
SCANN    DS    XL1                 NO. OF ENTRIES IN SCAN BLOCK                 
INDX     DS    XL1                 CURRENT FIELD INDEX NO.                      
SCANBLKN EQU   (L'IOREC)/SCANBLKL  MAX. NO OF ENTRIES IN SCAN BLOCK             
                                                                                
KEYMIN   DS    XL42                MINIMUM KEY FOR BIT SET                      
KEYMAX   DS    XL42                MAXIMUM KEY FOR BIT SET                      
                                                                                
IOKEY    DS    XL64                                                             
SVIOKEY  DS    XL(L'IOKEY)         SAVED IOKEY                                  
IOREC    DS    XL2048              IO AREA                                      
                                                                                
WORKL    EQU   *-WORKD                                                          
                                                                                
* GESRCHBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHBLKD                                                     
         PRINT ON                                                               
                                                                                
***********************************************************************         
* FILTERS/SAVED STORAGE                                               *         
***********************************************************************         
                                                                                
FTVALS   DS    0XL(FTVALSL)        ** FILTERS **                                
FTUSED   DS    XL4                 BIT TABLE OF FILTERS USED                    
FTNOT    DS    XL4                 BIT TABLE OF FILTERS WITH 'NOT ='            
FTVALSL  EQU   *-FTUSED                                                         
*                                                                               
         ORG   SBASAVE+L'SBASAVE                                                
                                                                                
***********************************************************************         
* TWAD                                                                *         
***********************************************************************         
                                                                                
* FATWA                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATWA                                                          
         PRINT ON                                                               
         DS    0X                                                               
* SRSRCFFD                                                                      
         PRINT OFF                                                              
       ++INCLUDE SRSRCFFD                                                       
         PRINT ON                                                               
         DS    0X                                                               
* FAREPBLK                                                                      
         PRINT OFF                                                              
       ++INCLUDE FAREPBLK                                                       
         PRINT ON                                                               
                                                                                
         ORG   TWAD+X'3B00'        SAVE AREA                                    
SVBLK    DS    XL(SVBLKL)                                                       
         ORG   TWAD+X'4000'                                                     
                                                                                
***********************************************************************         
* TWA SAVE AREA                                                       *         
***********************************************************************         
                                                                                
SVBLKD   DSECT                                                                  
SVINDS   DS    XL1                 INDICATORS BYTE                              
SVIALONE EQU   X'80'               STAND-ALONE SEARCH ($SEARCH ENTERED)         
SVIREP   EQU   X'40'               OUTPUT IS FOR A REPORT                       
SVIADR   EQU   X'20'               DISPLAY ADDRESS                              
SVSECY   DS    XL2                 USER SECURITY LEVEL                          
SVSYS    DS    XL1                 CONNECTED SYSTEM                             
SVLAOFFC DS    CL1                 LIMITED ACCESS OFFICE CODE                   
VOFFAL   DS    A                   A(OFFAL)                                     
SVOFFAL  DS    XL(OFFALLEN)        OFFAL BLOCK                                  
                                                                                
COMPSTA4 DS    XL1                 SAVED COMPANY STATUS BYTE 4                  
                                                                                
DO       DS    0X                  ** DICTIONARY OUT **                         
         DSDDL PRINT=YES                                                        
DOX      DS    0X                                                               
                                                                                
SVBLKL   EQU   *-SVBLKD                                                         
                                                                                
* ACOFFALD                                                                      
       ++INCLUDE ACOFFALD                                                       
                                                                                
* GESRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE GESRCHDIRD                                                     
         PRINT ON                                                               
                                                                                
* ACSRCHDIRD                                                                    
         PRINT OFF                                                              
       ++INCLUDE ACSRCHDIRD                                                     
         PRINT ON                                                               
                                                                                
* ACGENFILE                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACGENFILE                                                      
         PRINT ON                                                               
                                                                                
* DDCOMFACS                                                                     
         PRINT OFF                                                              
       ++INCLUDE DDCOMFACS                                                      
         PRINT ON                                                               
                                                                                
* FAGETTXTD                                                                     
         PRINT OFF                                                              
       ++INCLUDE FAGETTXTD                                                      
         PRINT ON                                                               
                                                                                
* DDSCANBLKD                                                                    
         PRINT OFF                                                              
       ++INCLUDE DDSCANBLKD                                                     
         PRINT ON                                                               
         ORG   SCONEFLD                                                         
SCFLDS   DS    0XL(L'SCFLD1+L'SCFLD2)                                           
SCFLD1   DS    XL10                                                             
SCFLD2   DS    XL(SBNALENQ)                                                     
SCANBLKL EQU   *-SCANBLKD                                                       
                                                                                
* ACDDEQUS                                                                      
         PRINT OFF                                                              
       ++INCLUDE ACDDEQUS                                                       
         PRINT ON                                                               
                                                                                
* ACMSGEQUS                                                                     
         PRINT OFF                                                              
       ++INCLUDE ACMSGEQUS                                                      
         PRINT ON                                                               
                                                                                
* DDFH                                                                          
         PRINT OFF                                                              
       ++INCLUDE DDFH                                                           
         PRINT ON                                                               
                                                                                
* FAUTL                                                                         
         PRINT OFF                                                              
       ++INCLUDE FAUTL                                                          
         PRINT ON                                                               
       ++INCLUDE FAFACTS                                                        
                                                                                
* FATCB                                                                         
         PRINT OFF                                                              
       ++INCLUDE FATCB                                                          
         PRINT ON                                                               
                                                                                
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'012ACSRCHOR  01/11/16'                                      
         END                                                                    
