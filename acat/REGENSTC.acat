*          DATA SET REGENSTC   AT LEVEL 035 AS OF 05/02/11                      
*CATALP REGENSTC                                                                
***********************************************************************         
*                                                                     *         
*         REGENSTC --- STORED COMMENTS VALIDATION/DISPLAY ROUTINES    *         
*                                                                     *         
* ------------------------------------------------------------------- *         
* UPDATE HISTORY:                                                     *         
*                                                                     *         
* 19NOV91 (SKU) --- DATE OF BIRTH                                     *         
*                                                                     *         
* 12MAY92 (SKU) --- FAKE LIABILITY POSITION AS AN ELEMENT             *         
*                                                                     *         
* 02MAY11 (SKU) --- SUPPORT FORCED COMMENTS                           *         
***********************************************************************         
         TITLE 'REGENSTC - CONTRACT COMMENT VALIDATION/DISPLAY'                 
***********************************************************************         
*  INPUT:                                                                       
*              PARAMETER 1 =       A(SCREEN CMT FIELD)                          
*                                      BYTE 1 X'00' = VALIDATION                
*                                             X'01' = DISPLAY                   
*                                  A(ELEMENT)                                   
*                                             X'02' = BUILD CMT BLOCK           
*                                  A(FIELD)                                     
*                                             X'03' = BUILD CMT BLOCK           
*                                  A(FIELD) - LIABILITY POSITION                
*                                             X'04' = BUILD CMT BLOCK           
*                        2 =       A(OUTPUT AREA)                               
*                        3 =       A(DATAMGR)                                   
*                        4 =       A(CONREC)                                    
*                        5 =       A(GETTXT)                                    
*                                                                               
*  OUTPUT:                                                                      
*              ON ERROR SET ^0 CC AND/OR ERROR EXIT                             
*                                                                               
***********************************************************************         
REGENSTC CSECT                                                                  
         PRINT NOGEN                                                            
         NMOD1 WORKX-WORKD,REGENSTC                                             
         USING WORKD,RC                                                         
         L     R2,0(R1)                                                         
         MVC   AIOAREA,4(R1)                                                    
         MVC   VDATAMGR,8(R1)                                                   
         MVC   ACONREC,12(R1)                                                   
         L     R5,12(R1)                                                        
         USING RCOND,R5                                                         
         MVC   VGETTXT,16(R1)                                                   
         MVC   DATADISP,=H'34'                                                  
         MVI   DMINBTS,0           DO NOT PASS DELETED RECORDS                  
*                                                                               
         CLI   0(R1),0             VALIDATE COMMENT                             
         BE    VCMT                                                             
         CLI   0(R1),1             DISPLAY COMMENT                              
         BE    DISPCOM                                                          
         CLI   0(R1),2             VALIDATE AND PUT REC INTO IOAREA             
         BE    VGETCMT                                                          
         CLI   0(R1),3             VALIDATE AND PUT REC INTO IOAREA             
         BE    VGETCMT0                                                         
         CLI   0(R1),4             VALIDATE AND PUT REC INTO IOAREA             
         BE    VLIABPOS            AGENCY/STATION LIABILITY POSITION            
*                                                                               
EXIT     SR    R1,R1                                                            
         B     *+8                                                              
ERRXIT   LA    R1,1                SET CONDITION CODE AT EXIT                   
         LTR   R1,R1                                                            
         XMOD1                                                                  
*                                                                               
*********************************************************************           
* VALIDATE FILE COMMENTS, SFM COMMENTS AND/OR FREE FORM COMMENTS                
*********************************************************************           
VCMT     DS    0H                                                               
         CLC   =C'C=',8(R2)        FILE COMMENTS                                
         BNE   VCMT20                                                           
         CLI   5(R2),2                                                          
         BH    *+12                                                             
         LA    R3,278              CMT CODE MISSING                             
         B     ERROR                                                            
*                                                                               
         OC    8(14,R2),SPACES     BLANK PADDED                                 
         CLC   10(8,R2),SPACES                                                  
         BNE   *+12                                                             
         LA    R3,278              CMT CODE MISSING                             
         B     ERROR                                                            
*                                                                               
         CLC   18(4,R2),SPACES                                                  
         BE    *+12                                                             
         LA    R3,279              CMT CDE MUST BE FIRST AND ONLY ENTRY         
         B     ERROR                                                            
*                                                                               
         MVI   5(R2),10            KEEP ONLY COMMENT CODE                       
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RCMTD,R6                                                         
         MVI   RCMTKTYP,X'2E'      RECORD TYPE                                  
         MVC   RCMTKREP,RCONKREP   REP CODE                                     
         MVC   RCMTKOFF,=X'FFFF'   OFFICE CODE                                  
         MVC   RCMTKCDE,10(R2)     COMMENT CODE                                 
         DROP  R6                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(L'RCMTKEY),KEYSAVE                                           
         BE    EXIT                                                             
         LA    R3,277              FILE COMMENT REC NOT FOUND                   
         B     ERROR                                                            
*                                                                               
VCMT20   CLC   =C'SC=',8(R2)       SFM COMMENTS                                 
         BNE   EXIT                                                             
         CLI   5(R2),3                                                          
         BH    *+12                                                             
         LA    R3,278              CMT CODE MISSING                             
         B     ERROR                                                            
*                                                                               
         OC    8(14,R2),SPACES                                                  
         CLC   11(2,R2),SPACES                                                  
         BNE   *+12                                                             
         LA    R3,278              CMT CODE MISSING                             
         B     ERROR                                                            
*                                                                               
         CLC   13(9,R2),SPACES                                                  
         BE    *+12                                                             
         LA    R3,279              CMT CDE MUST BE FIRST AND ONLY ENTRY         
         B     ERROR                                                            
*                                                                               
         LA    R3,280               MUST BE ALPHANUMERIC                        
         CLI   11(R2),C'A'                                                      
         BL    ERROR                                                            
         CLI   11(R2),C'9'                                                      
         BH    ERROR                                                            
         CLI   11(R2),C'}'                                                      
         BE    ERROR                                                            
         CLI   11(R2),C'\'                                                      
         BE    ERROR                                                            
         CLI   12(R2),C'A'                                                      
         BL    ERROR                                                            
         CLI   12(R2),C'9'                                                      
         BH    ERROR                                                            
         CLI   12(R2),C'}'                                                      
         BE    ERROR                                                            
         CLI   12(R2),C'\'                                                      
         BE    ERROR                                                            
*                                                                               
         MVI   5(R2),5             KEEP ONLY OFFICE CODE                        
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING ROCMD,R6                                                         
         MVI   ROCMKTYP,X'34'      RECORD TYPE                                  
         MVC   ROCMKREP,RCONKREP   REP CODE                                     
         MVC   ROCMKOFC,RCONKOFF   OFFICE CODE                                  
         MVC   ROCMKNUM,11(R2)     NUMBER                                       
*                                                                               
* CHECK FOR SPECIAL FORCED COMMENT                                              
*                                                                               
         CLI   11(R2),C'*'                                                      
         BNE   VCMT40                                                           
         MVC   ROCMKOFC,=C'* '     OFFICE CODE                                  
         MVC   ROCMKNUM,=C'01'     NUMBER                                       
*                                                                               
VCMT40   DS    0H                                                               
         MVI   ROCMKPAG,C'0'       PAGE NOT USED YET                            
         DROP  R6                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(L'ROCMKEY),KEYSAVE                                           
         BE    EXIT                                                             
         LA    R3,276              SFM COMMENT REC NOT FOUND                    
         B     ERROR                                                            
         EJECT                                                                  
***********************************************************************         
* GET FILE COMMT REC AND MOVE A NON-BLANK LINE INTO CONCOM1/2 FIELD             
***********************************************************************         
DISPCOM  DS    0H                                                               
         CLC   =C'C=',8(R2)                                                     
         BNE   DISPSFC                                                          
DISPFC   DS    0H                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RCMTD,R6                                                         
         MVI   RCMTKTYP,X'2E'      RECORD TYPE                                  
         MVC   RCMTKREP,RCONKREP   REP CODE                                     
         MVC   RCMTKOFF,=X'FFFF'   OFFICE CODE                                  
         MVC   RCMTKCDE,10(R2)     COMMENT CODE                                 
         OC    RCMTKCDE,SPACES     BLANK PADDED                                 
         DROP  R6                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(L'RCMTKEY),KEYSAVE                                           
         BE    DISPFC10                                                         
         B     ERRXIT                                                           
*                                                                               
DISPFC10 DS    0H                                                               
         BAS   RE,GETREC                                                        
         L     R6,AIOAREA                                                       
         USING RCMTELM2,R6                                                      
         MVI   ELCODE,2            COMMENT TEXT ELEMENT                         
         BAS   RE,GETEL                                                         
         BNE   EXIT                                                             
*                                                                               
DISPFC20 DS    0H                                                               
         CLI   RCMT2LEN,3          GET FIRST NON-BLANK COMMT LINE               
         BH    DISPFC30                                                         
         CLI   RCMT2TXT,C' '                                                    
         BNE   DISPFC30                                                         
         BAS   RE,NEXTEL                                                        
         BE    DISPFC20                                                         
         B     EXIT                                                             
*                                                                               
DISPFC30 DS    0H                                                               
         ZIC   R4,0(R2)            FIND MAX OUTPUT LENGTH                       
         SH    R4,=H'8'            SUBTRACT HEADER FROM TOTAL LENGTH            
         SH    R4,=H'14'           FIRST 14 BYTES FOR COMMENT CODE DISP         
         TM    1(R2),X'02'         EXTENDED HEADER??                            
         BZ    DISPFC35                                                         
         SH    R4,=H'8'            SUBTRACT EXTENSION                           
*                                                                               
DISPFC35 ZIC   R1,RCMT2LEN                                                      
         CR    R1,R4               COMMT FIELD HAS THIS MUCH ROOM               
         BH    DISPFC40                                                         
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     EXIT                                                             
         MVC   22(0,R2),RCMT2TXT                                                
*                                                                               
DISPFC40 BCTR  R4,0                MOVE MAX LENGTH OF DATA ALLOWED              
         EX    R4,*+8                BY OUPUT FIELD                             
         B     *+10                                                             
         MVC   22(0,R2),RCMT2TXT                                                
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
***********************************************************************         
* GET SFM COMMT REC AND MOVE A NON-BLANK LINE INTO CONCOM1/2 FIELD              
***********************************************************************         
DISPSFC  DS    0H                                                               
         CLC   =C'SC=',8(R2)                                                    
         BNE   EXIT                                                             
*                                                                               
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING ROCMD,R6                                                         
         MVI   ROCMKTYP,X'34'      RECORD TYPE                                  
         MVC   ROCMKREP,RCONKREP   REP CODE                                     
         MVC   ROCMKOFC,RCONKOFF   OFFICE CODE                                  
         MVC   ROCMKNUM,11(R2)     COMMENT CODE                                 
*                                                                               
* CHECK FOR SPECIAL FORCED COMMENT                                              
*                                                                               
         CLI   11(R2),C'*'                                                      
         BNE   DISSFC05                                                         
         MVC   ROCMKOFC,=C'* '     OFFICE CODE                                  
         MVC   ROCMKNUM,=C'01'     NUMBER                                       
*                                                                               
DISSFC05 DS    0H                                                               
         MVI   ROCMKPAG,C'0'       PAGE NOT USED YET                            
         DROP  R6                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(L'ROCMKEY),KEYSAVE                                           
         BE    DISSFC10                                                         
         B     ERRXIT                                                           
*                                                                               
DISSFC10 DS    0H                                                               
         BAS   RE,GETREC                                                        
         L     R6,AIOAREA                                                       
         USING ROCMDSEL,R6                                                      
         MVI   ELCODE,1            COMMENT DESCRIPTION                          
         BAS   RE,GETEL                                                         
         BNE   DISSFC20                                                         
*                                                                               
         ZIC   R1,ROCMDSLN                                                      
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     EXIT                                                             
         MVC   22(0,R2),ROCMDSDS                                                
         DROP  R6                                                               
*                                                                               
DISSFC20 DS    0H                  DESCRIPTION NOT FOUND, GET COMMT TXT         
         L     R6,AIOAREA                                                       
         USING ROCMCMEL,R6                                                      
         MVI   ELCODE,2            COMMENT TEXT                                 
         BAS   RE,GETEL                                                         
         BNE   EXIT                                                             
*                                  FIRST NON-BLANK COMMENT LINE FOUND           
         CLI   ROCMCMLN,46                                                      
         BH    DISSFC30                                                         
         ZIC   R1,ROCMCMLN                                                      
         SH    R1,=H'3'                                                         
         EX    R1,*+8                                                           
         B     EXIT                                                             
         MVC   22(0,R2),ROCMCMCM                                                
*                                                                               
DISSFC30 MVC   22(46,R2),ROCMCMCM                                               
         B     EXIT                                                             
         DROP  R6                                                               
         EJECT                                                                  
*********************************************************************           
* START WITH LIABILITY CODE AND PASS TO VGETCMT AS AN ELEMENT                   
*********************************************************************           
VLIABPOS DS    0H                  BUILD A FAKE ELEMENT FIELD                   
         XC    COMELEM,COMELEM                                                  
         MVC   COMELEM+2(6),=C'C=LIAB'                                          
         LA    R4,COMELEM+8                                                     
         EDIT  (1,0(R2)),(2,(R4)),FILL=0                                        
         OC    COMELEM+2(60),SPACES                                             
         LA    R2,COMELEM          R2 POINTS TO COMMENT ELEMENT                 
         MVI   COMELEM+1,10        ELEMENT LENGTH                               
         B     VGETCMT                                                          
*********************************************************************           
* VALIDATE FILE/SFM COMMENTS AND BUILDS A COMMENT BLOCK IN IOAREA.              
* EACH ENTRY STARTS WITH AN 1-BYTE LENGTH FOLLOWED BY A COMMENT.                
* BLOCK ENDS WITH X'FF'.                                                        
*********************************************************************           
VGETCMT0 DS    0H                  BUILD A FAKE ELEMENT FIELD                   
         XC    COMELEM,COMELEM                                                  
         MVC   COMELEM+2(60),0(R2) COMMENT TEXT                                 
         OC    COMELEM+2(60),SPACES                                             
         MVI   COMELEM+1,62        LENGTH OF FAKE ELEMENT                       
         LA    R2,COMELEM          R2 POINTS TO COMMENT ELEMENT                 
         CLC   =C'C=',COMELEM+2    LENGTH IS 12 FOR FILE COMMENTS               
         BNE   VGETCMT5                                                         
         MVI   COMELEM+1,12                                                     
         B     VGETCMT                                                          
VGETCMT5 CLC   =C'SC=',COMELEM+2   LENGTH IS 7 FOR SFM COMMENTS                 
         BNE   VGETCMT                                                          
         MVI   COMELEM+1,7                                                      
*                                                                               
VGETCMT  DS    0H                                                               
         CLC   =C'C=',2(R2)        FILE COMMENTS                                
         BNE   VGCMT20                                                          
         XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING RCMTD,R6                                                         
         MVI   RCMTKTYP,X'2E'      RECORD TYPE                                  
         MVC   RCMTKREP,RCONKREP   REP CODE                                     
         MVC   RCMTKOFF,=X'FFFF'   OFFICE CODE                                  
         ZIC   R1,1(R2)                                                         
         SH    R1,=H'5'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   RCMTKCDE(0),4(R2)      COMMENT CODE                              
         OC    RCMTKCDE,SPACES                                                  
         DROP  R6                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(L'RCMTKEY),KEYSAVE                                           
         BNE   ERRXIT                                                           
*                                                                               
         LA    R4,CBLOCK                                                        
         BAS   RE,GETREC           STORE REC INTO IOAREA                        
         L     R6,AIOAREA                                                       
         USING RCMTELM2,R6                                                      
         MVI   ELCODE,2            COMMENT TEXT ELEMENT                         
         BAS   RE,GETEL                                                         
         BNE   ERRXIT              NO TEXT FOUND                                
*                                                                               
VGCMT10  DS    0H                                                               
         CLI   RCMT2LEN,3          GET NON-BLANK COMMT LINE                     
         BH    VGCMT15                                                          
         CLI   RCMT2TXT,C' '                                                    
         BNE   VGCMT15                                                          
VGCMT13  BAS   RE,NEXTEL                                                        
         BE    VGCMT10                                                          
         MVI   0(R4),X'FF'         END-OF-BLOCK                                 
         B     VGCMTX                                                           
*                                                                               
VGCMT15  DS    0H                                                               
         ZIC   R1,RCMT2LEN         MOVE IN LENGTH OF COMMENT                    
         BCTR  R1,0                                                             
         STC   R1,0(R4)                                                         
         SH    R1,=H'2'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),RCMT2TXT    MOVE IN COMMENT                              
         LA    R4,2(R1,R4)                                                      
         B     VGCMT13                                                          
*                                                                               
VGCMT20  CLC   =C'SC=',2(R2)       SFM COMMENTS                                 
         BE    VGCMT25                                                          
         L     R6,AIOAREA                                                       
         MVI   0(R6),0             PRINT FREE FORM COMMENT                      
         B     EXIT                                                             
*                                                                               
VGCMT25  XC    KEY,KEY                                                          
         LA    R6,KEY                                                           
         USING ROCMD,R6                                                         
         MVI   ROCMKTYP,X'34'      RECORD TYPE                                  
         MVC   ROCMKREP,RCONKREP   REP CODE                                     
         MVC   ROCMKOFC,RCONKOFF   OFFICE CODE                                  
         MVC   ROCMKNUM,5(R2)      NUMBER                                       
*                                                                               
* CHECK FOR SPECIAL FORCED COMMENT                                              
*                                                                               
         CLI   5(R2),C'*'                                                       
         BNE   VGCMT30                                                          
         MVC   ROCMKOFC,=C'* '     OFFICE CODE                                  
         MVC   ROCMKNUM,=C'01'     NUMBER                                       
*                                                                               
VGCMT30  DS    0H                                                               
         MVI   ROCMKPAG,C'0'       PAGE NOT USED YET                            
         DROP  R6                                                               
         BAS   RE,HIGH                                                          
         CLC   KEY(L'ROCMKEY),KEYSAVE                                           
         BNE   ERRXIT                                                           
*                                                                               
         LA    R4,CBLOCK                                                        
         BAS   RE,GETREC           STORE REC INTO IOAREA                        
*                                                                               
VGCMT40  DS    0H                  DESCRIPTION NOT FOUND, GET COMMT TXT         
         L     R6,AIOAREA                                                       
         USING ROCMCMEL,R6                                                      
         MVI   ELCODE,2            COMMENT TEXT                                 
         BAS   RE,GETEL                                                         
         BNE   ERRXIT              NO TEXT FOUND                                
*                                                                               
VGCMT50  DS    0H                                                               
         ZIC   R1,ROCMCMLN                                                      
         BCTR  R1,0                                                             
         STC   R1,0(R4)            MOVE IN COMMENT LENGTH                       
         SH    R1,=H'2'                                                         
         EX    R1,*+8                                                           
         B     *+10                                                             
         MVC   1(0,R4),ROCMCMCM    MOVE IN COMMENT                              
         LA    R4,2(R1,R4)                                                      
         BAS   RE,NEXTEL                                                        
         BE    VGCMT50                                                          
         MVI   0(R4),X'FF'         END-OF-BLOCK                                 
*                                                                               
VGCMTX   DS    0H                                                               
         L     RF,AIOAREA                                                       
         LA    R1,L'CBLOCK                                                      
         LA    RE,CBLOCK                                                        
         MOVE  ((RF),(R1)),(RE)    MOVE CMNT TEXT BLOCK INTO IO4                
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
* ERROR HANDLING                                                                
***********************************************************************         
ERROR    GOTO1 VGETTXT,DMCB+12,(R3),0,(C'E',DMCB),0,0,0                         
         LTR   R3,R3               IF DATAMGR ERROR, DON'T SET CURPOS           
         BZ    ERRXIT                                                           
         OI    6(R2),X'40'                                                      
         B     ERRXIT                                                           
***********************************************************************         
* DATAMGR CALLS                                                                 
***********************************************************************         
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'DMRDHI'),=C'REPDIR',          X        
               KEYSAVE,KEY,0                                                    
         XIT1                                                                   
*                                                                               
GETREC   NTR1                                                                   
         GOTO1 VDATAMGR,DMCB,(DMINBTS,=C'GETREC'),=C'REPFILE',KEY+28,  X        
               AIOAREA,DMWORK                                                   
         XIT1                                                                   
*                                                                               
         DROP  R5                                                               
         GETEL R6,DATADISP,ELCODE                                               
         EJECT                                                                  
SPACES   DC    80C' '                                                           
WORKD    DSECT                                                                  
DMWORK   DS    12D                                                              
KEY      DS    CL32                                                             
KEYSAVE  DS    CL32                                                             
DMCB     DS    6F                                                               
DUB      DS    D                                                                
DMINBTS  DS    X                                                                
VDATAMGR DS    A                   A(DATAMGR)                                   
VGETTXT  DS    A                   A(GETTXT)                                    
ACONREC  DS    A                                                                
AIOAREA  DS    A                                                                
DATADISP DS    H                                                                
ELCODE   DS    X                                                                
WORK     DS    CL17                FOR EDIT                                     
CMTLEN   DS    0CL70                                                            
CBLOCK   DS    CL(13*L'CMTLEN+1)   BLOCK OF COMMENT TEXTS                       
*                                  +1 FOR X'00' MARKER                          
*                                  MUST BE LESS THAN L'IOAREA                   
WORKX    EQU   *                                                                
*                                                                               
         ORG   CBLOCK                                                           
COMELEM  DS    CL80                FAKE A COMMENT ELEMENT                       
*                                                                               
RCMTD    DSECT                                                                  
       ++INCLUDE REGENCMT                                                       
ROCMD    DSECT                                                                  
       ++INCLUDE REGENOCM                                                       
RCOND    DSECT                                                                  
       ++INCLUDE REGENCON                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'035REGENSTC  05/02/11'                                      
         END                                                                    
