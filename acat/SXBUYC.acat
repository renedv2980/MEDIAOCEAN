*          DATA SET SXBUYC     AT LEVEL 030 AS OF 05/01/02                      
*CATALP SXBUYC                                                                  
         TITLE 'SXBUYC - SPOT EXTRACT - BUY RECORD'                             
         SPACE 1                                                                
         PRINT NOGEN                                                            
SXBUYC   CSECT                                                                  
         NMOD1 0,*SXBUYC*                                                       
         L     RC,0(R1)                                                         
         LA    RA,2048(RC)                                                      
         LA    RA,2048(RA)                                                      
         USING SPWORKD,RC,RA                                                    
         L     R2,4(R1)                                                         
         USING SXBUYD,R2           R2=A(EXTRACT RECORD)                         
         L     R3,ADBUY                                                         
         USING BUYRECD,R3          R3=A(SPOT RECORD)                            
*                                                                               
SXBLEN   DS    0H                                                               
         XC    SXBUYLEN,SXBUYLEN   RECORD LENGTH/TYPE/ENDOFREC                  
         MVC   SXBUYLEN(2),=AL2(SXBUYDL)                                        
         MVC   SXBUYTYP,=AL2(SXBUYDQ)                                           
         MVI   SXBUYDX,SXTRTQ                                                   
         MVI   SXBUYDX+1,SXTRTQ                                                 
*                                                                               
         BAS   RE,INIT             INITIALIZATION                               
*                                                                               
         BAS   RE,SETKEY           SET DATA FROM KEY                            
*                                                                               
         BAS   RE,FNDPG            FIND PIGGYBACK PRODUCT                       
*                                                                               
         BAS   RE,SETBDEL          GET INFO FROM BDELEM                         
*                                                                               
         BAS   RE,SETNDEL          GET INFO FROM NDELEM                         
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
*                                                                               
* INITIALIZATION                                                                
*                                                                               
INIT     NTR1                                                                   
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
* CONVERT DATA FROM KEY                                                         
*                                                                               
SETKEY   NTR1                                                                   
         BAS   RE,FNDMED           FIND CORRECT MEDIA CODE                      
         MVI   SXBUYMED-1,SXTRTQ   BUY MEDIA                                    
         MVC   SXBUYMED,FULL                                                    
         MVI   SXBUYCLT-1,SXTRTQ       CLIENT                                   
         GOTO1 CLUNPK,DMCB,BUYKCLT,SXBUYCLT                                     
         BAS   RE,GCLT             GET CLT RECORD                               
         MVI   SXBUYPRD-1,SXTRTQ       PRODUCT                                  
         LA    R1,BUYKPRD                                                       
         BAS   RE,GPRD             GET PRD CODE                                 
         MVC   SXBUYPRD,FULL                                                    
         MVI   SXBUYMKT-1,SXTRTQ       MARKET                                   
         MVC   WORK(8),SPACES                                                   
         GOTO1 MSUNPK,DMCB,(X'80',BUYMSTA),SXBUYMKT,WORK                        
         MVI   SXBUYSTN-1,SXTRTQ   STATION                                      
         MVC   SXBUYSTN,WORK                                                    
*****    MVI   SXBUYSTS-1,SXTRTQ   NETWORK                                      
         MVC   SXBUYSTS,WORK+5                                                  
         CLI   SXBUYSTS,C' '       IF NO NETWORK                                
         BH    SK10                                                             
         MVC   SXBUYSTS(1),WORK+4  SET BAND                                     
         CLI   SXBUYSTS,C' '       IF NO BAND                                   
         BH    SK10                                                             
         MVI   SXBUYSTS,C'T'       SET TO TV                                    
*                                                                               
SK10     MVI   SXBUYEST-1,SXTRTQ   ESTIMATE                                     
         EDIT  (1,BUYKEST),(3,SXBUYEST),FILL=0                                  
         MVI   SXBUYLIN-1,SXTRTQ   LINE NUMBER                                  
         EDIT  (1,BUYKBUY),(3,SXBUYLIN),FILL=0                                  
*                                                                               
         B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        SET INFO IN EXTRACT RECORD FROM BDELEM                                 
*                                                                               
SETBDEL  NTR1                                                                   
         LA    R4,BDELEM           R4=A(FIRST ELEMENT)                          
         USING BDELEM,R4                                                        
*                                                                               
         MVI   SXBUYSTR-1,SXTRTQ                                                
         GOTO1 DATCON,DMCB,(3,BDSTART),(10,SXBUYSTR)                            
****     MVC   SXBUYSTR(2),=C'19'                                               
****     CLI   SXBUYSTR+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXBUYSTR(2),=C'20'  SET CENTURY TO 20                            
         MVI   SXBUYEND-1,SXTRTQ                                                
         GOTO1 DATCON,DMCB,(3,BDEND),(10,SXBUYEND)                              
****     MVC   SXBUYEND(2),=C'19'                                               
****     CLI   SXBUYSTR+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXBUYEND(2),=C'20'  SET CENTURY TO 20                            
*                                                                               
         MVI   SXBUYNWK-1,SXTRTQ      NUMBER OF WEEKS                           
         EDIT  (1,BDWKS),(3,SXBUYNWK),FILL=0                                    
         MVI   SXBUYWIN-1,SXTRTQ   WEEK INDICATOR                               
         MVC   SXBUYWIN,BDWKIND                                                 
         MVI   SXBUYROT-1,SXTRTQ   ROTATION                                     
         CLI   BDDAY,0                                                          
         BNE   *+8                                                              
         MVI   BDDAY,X'7C'         GET AROUND PROGRAM DUMPS                     
         GOTO1 CODAY,(R1),(BDSEDAY,BDDAY),WORK   DAYUNPK                        
         MVC   SXBUYROT,WORK                                                    
         MVI   SXBUYSPW-1,SXTRTQ   SPOTS PER WEEK                               
         EDIT  (1,BDNOWK),(3,SXBUYSPW),FILL=0                                   
         MVI   SXBUYSLN-1,SXTRTQ   SPOT LENGTH                                  
         EDIT  (1,BDSEC),(3,SXBUYSLN),FILL=0                                    
         MVI   SXBUYSTY-1,SXTRTQ   SPOT LENGTH TYPE                             
         MVI   SXBUYSTY,C'S'                                                    
         MVI   SXBUYDPT-1,SXTRTQ   DAYPART                                      
         MVC   SXBUYDPT,BDDAYPT                                                 
         MVI   SXBUYSTM-1,SXTRTQ   START TIME                                   
         EDIT  (2,BDTIMST),(4,SXBUYSTM),FILL=0                                  
         MVI   SXBUYETM-1,SXTRTQ   END TIME                                     
         EDIT  (2,BDTIMEND),(4,SXBUYETM),FILL=0                                 
         MVI   SXBUYPGM-1,SXTRTQ   PROGRAM                                      
         MVC   SXBUYPGM,BDPROGRM                                                
         MVI   SXBUYSFL-1,SXTRTQ   SPECIAL FLAG                                 
         CLI   BDPROG+17,X'00'                                                  
         BNE   *+8                                                              
         MVI   SXBUYSFL,C'S'                                                    
         MVI   SXBUYADJ-1,SXTRTQ   ADJACENCY CODE                               
         EDIT  (1,BDPROGT),(2,SXBUYADJ),FILL=0                                  
         BAS   RE,SETIND           SET INDICATOR                                
         MVI   SXBUYREP-1,SXTRTQ   SPECIAL REP                                  
         OC    BDREP,BDREP                                                      
         BZ    SB20                                                             
         MVC   FULL(2),BDREP                                                    
         LH    R0,FULL                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  SXBUYREP(3),DUB                                                  
*                                                                               
SB20     MVI   SXBUYCDT-1,SXTRTQ   CHANGED DATE                                 
         OC    BDCHG,BDCHG                                                      
         BZ    SB30                                                             
         XC    WORK,WORK                                                        
         GOTO1 DATCON,DMCB,(3,BDCHG),(10,SXBUYCDT)                              
****     MVC   SXBUYCDT(2),=C'19'                                               
****     CLI   SXBUYSTR+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXBUYCDT(2),=C'20'  SET CENTURY TO 20                            
*                                                                               
SB30     MVI   SXBUYMDT-1,SXTRTQ   MISSED DATE                                  
         OC    BDMGDATE,BDMGDATE                                                
         BZ    SB40                                                             
         GOTO1 DATCON,DMCB,(2,BDMGDATE),(10,SXBUYMDT)                           
****     MVC   SXBUYMDT(2),=C'19'                                               
****     CLI   SXBUYSTR+2,C'8'     IF THE YEAR IS LESS THAN 8X                  
****     BNL   *+10                                                             
****     MVC   SXBUYMDT(2),=C'20'  SET CENTURY TO 20                            
*                                                                               
SB40     MVI   SXBUYRSO-1,SXTRTQ   RATING SERVICE                               
         MVC   SXBUYRSO,BDRTG                                                   
*                                                                               
         B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*        SET INFO IN EXTRACT RECORD FROM NDELEM                                 
*                                                                               
SETNDEL  NTR1                                                                   
         LA    R4,BDELEM           R4=A(FIRST ELEMENT)                          
         USING NDELEM,R4                                                        
         MVI   ELCODE,X'02'                                                     
         BAS   RE,NEXTEL                                                        
         BNE   NDX                                                              
         BAS   RE,GEST             GET EST RECORD                               
         MVI   SXBUYBMM-1,SXTRTQ   BOOK MONTH                                   
         EDIT  (1,NDBOOK+1),(2,SXBUYBMM),FILL=0                                 
         MVI   SXBUYBYY-1,SXTRTQ   BOOK YEAR                                    
         EDIT  (1,NDBOOK),(2,SXBUYBYY),FILL=0                                   
         MVI   SXBUYBPG-1,SXTRTQ   BOOK PROGRAM                                 
         MVC   SXBUYBPG,NDPROG                                                  
*                                                                               
         MVI   SXBUYD01-1,SXTRTQ   DEMO 1                                       
         MVI   SXBUYV01-1,SXTRTQ   DEMO VALUE 1                                 
         MVI   SXBUYD02-1,SXTRTQ   DEMO 2                                       
         MVI   SXBUYV02-1,SXTRTQ   DEMO VALUE 2                                 
         MVI   SXBUYD03-1,SXTRTQ   DEMO 3                                       
         MVI   SXBUYV03-1,SXTRTQ   DEMO VALUE 3                                 
         MVI   SXBUYD04-1,SXTRTQ   DEMO 4                                       
         MVI   SXBUYV04-1,SXTRTQ   DEMO VALUE 4                                 
*&&DO                                                                           
         MVI   SXBUYD05-1,SXTRTQ   DEMO 5                                       
         MVI   SXBUYV05-1,SXTRTQ   DEMO VALUE 5                                 
         MVI   SXBUYD06-1,SXTRTQ   DEMO 6                                       
         MVI   SXBUYV06-1,SXTRTQ   DEMO VALUE 6                                 
         MVI   SXBUYD07-1,SXTRTQ   DEMO 7                                       
         MVI   SXBUYV07-1,SXTRTQ   DEMO VALUE 7                                 
         MVI   SXBUYD08-1,SXTRTQ   DEMO 8                                       
         MVI   SXBUYV08-1,SXTRTQ   DEMO VALUE 8                                 
         MVI   SXBUYD09-1,SXTRTQ   DEMO 9                                       
         MVI   SXBUYV09-1,SXTRTQ   DEMO VALUE 9                                 
         MVI   SXBUYD10-1,SXTRTQ   DEMO 10                                      
         MVI   SXBUYV10-1,SXTRTQ   DEMO VALUE 10                                
         MVI   SXBUYD11-1,SXTRTQ   DEMO 11                                      
         MVI   SXBUYV11-1,SXTRTQ   DEMO VALUE 11                                
         MVI   SXBUYD12-1,SXTRTQ   DEMO 12                                      
         MVI   SXBUYV12-1,SXTRTQ   DEMO VALUE 12                                
         MVI   SXBUYD13-1,SXTRTQ   DEMO 13                                      
         MVI   SXBUYV13-1,SXTRTQ   DEMO VALUE 13                                
         MVI   SXBUYD14-1,SXTRTQ   DEMO 14                                      
         MVI   SXBUYV14-1,SXTRTQ   DEMO VALUE 14                                
*&&                                                                             
*                                                                               
         L     R5,ADEST                                                         
         USING ESTHDRD,R5                                                       
         XC    DEMTBL,DEMTBL                                                    
         XC    DBLOCK,DBLOCK                                                    
         MVC   DBCOMFCS,ACOMFACS                                                
         MVC   DBFILE,=C'TP '                                                   
         MVI   DBSELMED,C'T'                                                    
         CLI   SXBUYMED,C'R'                                                    
         BNE   *+8                                                              
         MVI   DBSELMED,C'R'                                                    
         GOTO1 DEMOCON,DMCB,(14,EDEMLST),(13,DEMTBL),(C'S',DBLOCK),    X        
               EUSRNMS                                                          
*                                                                               
         LA    RF,EDEMLST                                                       
****     LA    R1,14                                                            
         LA    R1,4                                                             
         LA    R5,DEMTBL                                                        
         LA    R6,SXBUYD01     PUT DEMOS                                        
*                                                                               
ND10     CLI   0(R5),C' '                                                       
         BNH   ND20                LAST DEMO                                    
         MVC   0(7,R6),0(R5)                                                    
         LA    R5,11(R5)                                                        
         LA    R6,SXBUYDLN(R6)     NEXT DEMO                                    
         LA    RF,3(RF)            NEXT DEMO                                    
         BCT   R1,ND10                                                          
*                                                                               
ND20     L     R5,ADEST                                                         
         USING ESTHDRD,R5                                                       
         LA    R5,EDEMLST          R5=A(DEMO LIST)                              
         ZIC   R1,NDLEN                                                         
         SH    R1,=Y(NDEMNO-NDELEM)                                             
         BNP   NDX                                                              
         SRL   R1,3                                                             
         LR    RF,R1               RF=N'DEMOS                                   
         LA    R6,SXBUYV01         PUT DEMOS VALUES                             
*                                                                               
ND30     ZIC   R1,NDLEN                                                         
         SH    R1,=Y(NDEMNO-NDELEM)                                             
         BNP   NDX                                                              
         SRL   R1,3                                                             
         LA    RE,NDEMNO                                                        
*                                                                               
ND40     CLC   0(3,R5),0(RE)                                                    
         BE    ND50                                                             
         LA    RE,8(RE)                                                         
         BCT   R1,ND40                                                          
         B     ND60                                                             
*                                                                               
ND50     L     R0,4(RE)                                                         
         N     R0,=X'7FFFFFFF'     TURN OFF OVERRIDE BIT                        
         ZIC   R1,3(RE)            GET HUT ADJUSTMENT VALUE                     
         MR    R0,R0                                                            
         AH    R1,=H'50'                                                        
         D     R0,=F'100'                                                       
         MH    R1,=H'10'           IN RECORD WITH 1 DECIMAL - NOT 2             
         EDIT  (R1),(7,0(R6)),FILL=0                                            
*                                                                               
ND60     LA    R6,SXBUYDLN(R6)     NEXT DEMO VALUE                              
         LA    R5,3(R5)            NEXT DEMO                                    
         BCT   RF,ND30                                                          
*                                                                               
NDX      B     XIT                                                              
         DROP  R4,R5                                                            
         EJECT                                                                  
*                                                                               
*        FIND PIGGYBACK PRODUCT ONLY FOR NON POL P/B                            
*                                                                               
FNDPG    NTR1                                                                   
         MVC   SXBUYPPR,=C'000'    SET DEFAULT (KEY <> NULL)                    
         LA    R4,BDELEM           R4=A(FIRST ELEMENT)                          
         USING PBELEM,R4                                                        
         MVI   ELCODE,X'04'        NON - POL WILL HAVE 04 ELEM                  
         BAS   RE,NEXTEL                                                        
         BNE   PGX                                                              
         MVC   SXBUYPPR,PBPRD                                                   
*                                                                               
PGX      B     XIT                                                              
         DROP  R4                                                               
         EJECT                                                                  
*                                                                               
*        SET COST INDICATOR                                                     
*                                                                               
SETIND   NTR1                                                                   
         MVI   SXBUYCIN-1,SXTRTQ   COST INDICATOR                               
         MVI   SXBUYCIN,C' '                                                    
*                                                                               
         TM    BDCIND2,X'04'       BDCIND IS A CHARACTER                        
         BZ    SI10                                                             
         TM    BDCIND2,BDC2NEG     TEST NEGATIVE COST                           
         BZ    SIX                                                              
         B     SI20                                                             
SI10     TM    BDCIND,X'01'        TEST NEGATIVE COST                           
         BZ    *+8                                                              
SI20     MVI   SXBUYCIN,C'-'                                                    
*                                                                               
SIX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        FIND CORRECT MEDIA CODE                                                
*                                                                               
FNDMED   NTR1                                                                   
         MVI   FULL,C' '                                                        
         MVC   BYTE,BUYKAM         AGY/MED                                      
         NI    BYTE,X'0F'                                                       
         ZIC   R1,BYTE                                                          
         BCTR  R1,0                                                             
         LA    R1,MEDTAB(R1)                                                    
         MVC   FULL(1),0(R1)                                                    
*                                                                               
FMX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET CLIENT RECORD INTO ADCLT                                           
*                                                                               
GCLT     NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING CLTHDRD,R4                                                       
         MVC   CKEYAM,BUYKAM       SET AGY/MED                                  
         MVC   CKEYCLT,BUYKCLT     SET CLIENT                                   
         L     R4,ADCLT            CLEAR IO AREA                                
         CLC   0(L'CKEY,R4),KEY                                                 
         BE    GCX                                                              
         LA    R1,8                                                             
*                                                                               
GC10     XC    0(250,R4),0(R4)                                                  
         LA    R4,250(R4)                                                       
         BCT   R1,GC10                                                          
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'CKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,ADCLT                                                       
         GOTO1 GET                                                              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GCX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET PRODUCT CODE                                                       
*        R1 - A(PRD CODE)                                                       
*                                                                               
GPRD     NTR1                                                                   
         L     R4,ADCLT                                                         
         USING CLTHDRD,R4                                                       
         LA    R4,CLIST-CLTHDRD(R4)                                             
         MVC   FULL,SPACES                                                      
*                                                                               
GP10     CLI   0(R4),C'A'                                                       
         BL    GPX                                                              
         CLC   3(1,R4),0(R1)       MATCH PRD CODE                               
         BE    GP20                                                             
         LA    R4,4(R4)                                                         
         B     GP10                                                             
*                                                                               
GP20     MVC   FULL(3),0(R4)       SET PROD CODE                                
*                                                                               
GPX      B     XIT                                                              
         EJECT                                                                  
*                                                                               
*        GET ESTIMATE RECORD INTO ADEST                                         
*                                                                               
GEST     NTR1                                                                   
         XC    KEY,KEY                                                          
         LA    R4,KEY                                                           
         USING ESTHDRD,R4                                                       
         MVC   EKEYAM,BUYKAM       SET AGY/MED                                  
         MVC   EKEYCLT,BUYKCLT     SET CLIENT                                   
         MVC   EKEYPRD,SXBUYPRD    SET PRODUCT                                  
         MVC   EKEYEST,BUYKEST     SET ESTIMATE                                 
         L     R4,ADEST            CLEAR IO AREA                                
         CLC   0(L'EKEY,R4),KEY                                                 
         BE    GEX                                                              
         LA    R1,4                                                             
*                                                                               
GE10     XC    0(250,R4),0(R4)                                                  
         LA    R4,250(R4)                                                       
         BCT   R1,GE10                                                          
*                                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(L'EKEY),KEYSAVE                                              
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   AREC,ADEST                                                       
         GOTO1 GET                                                              
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GEX      B     XIT                                                              
         EJECT                                                                  
NEXTEL   CLI   0(R4),0                                                          
         BE    NEXTELX                                                          
         ZIC   R0,1(R4)                                                         
         LTR   R0,R0                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         AR    R4,R0                                                            
*                                                                               
NEXTEL2  CLI   0(R4),0                                                          
         BE    NEXTELX                                                          
         CLC   ELCODE,0(R4)                                                     
         BNE   NEXTEL                                                           
         CR    RB,RB                                                            
         B     *+6                                                              
*                                                                               
NEXTELX  LTR   RB,RB                                                            
         BR    RE                                                               
         EJECT                                                                  
*                                                                               
XIT      XIT1                                                                   
EXIT     XMOD1 1                                                                
*                                                                               
         EJECT                                                                  
SXTRTQ   EQU   X'5E'               FIELD SEPARATOR CHAR                         
*                                                                               
MEDTAB   DC    C'TRNX'                                                          
*                                                                               
ELCODE   DS    XL1                                                              
DEMTBL   DS    CL220               USED BY DEMOCON                              
       ++INCLUDE DEDBLOCK                                                       
         LTORG                                                                  
         EJECT                                                                  
       ++INCLUDE SXBUYD                                                         
         EJECT                                                                  
BUYRECD  DSECT                                                                  
       ++INCLUDE SPGENBUY                                                       
         EJECT                                                                  
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
         EJECT                                                                  
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
*SPMEDBLOCK                                                                     
*SPREWORKD                                                                      
         PRINT OFF                                                              
       ++INCLUDE SPMEDBLOCK                                                     
       ++INCLUDE SPREPWORKD                                                     
         EJECT                                                                  
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'030SXBUYC    05/01/02'                                      
         END                                                                    
