*          DATA SET PPREPB12F  AT LEVEL 031 AS OF 05/01/02                      
*CATALP PPB12F                                                                  
         TITLE 'PPB12F - PRINTPAK BILLING/SOURCE BOOK F'                        
***********************************************************************         
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                 *** THIS MODULE IS DEAD ***                                   
*                                                                               
*                                                                               
*              IT HAS BEEN REPLACED BY PPREPB102                                
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
*                                                                               
***********************************************************************         
***********************************************************************         
*                                                                               
*        CONTAINS VARIOUS MODULES FOR BILLING EDI.                              
*                                                                               
***********************************************************************         
*                                                                               
*                                                                               
*        CHANGE LOG                                                             
*                                                                               
*  BPLA   08/00    COPY OF PPB12F AT LEVEL 26 5/04/99                           
*                  COPY MADE 8/16/00                                            
*                  CHANGES TO CATALP AND PPREPB1WKN                             
*                  (WHICH INCLUDES PNNEWFILE IN PLACE OF                        
*                  PPNEWFILE)                                                   
*                  PNNEWFILE INCLUDES PNBILREC IN PLACE                         
*                  OF PBILLREC                                                  
*                                                                               
*   BPLA  5/99   CHECK FOR ERRORS WHEN CALLING TSAROFF                          
*               +CHANGE WLAGEDD TO WLUDATA (SAME FIELD)                         
*                                                                               
*   BPLA  5/98   CD SEPERATE CODE                                               
*                                                                               
*   BPLA  5/98   EDI "CLIENT" NOW IN PROFB1A (+10(3))                           
*                                                                               
*   BPLA 5/98    CHANGES IN MFHDR - EDI CONTROLS                                
*                                                                               
*   BPLA 1/98    CHANGES FOR WORKER FILE, ETC.                                  
*                                                                               
*   BPLA 8/97    MODIFICATIONS FOR PRINTPAK                                     
*                                                                               
*   BPLA 8/97    COPY FROM SPB12E                                               
*                                                                               
         SPACE 2                                                                
         PRINT NOGEN                                                            
BILLEDI  CSECT                                                                  
         NMOD1 0,**EDI***,(R5)                                                  
***                                                                             
***      NOTE R5 IS SECOND BASE REGISTER                                        
***                                                                             
         USING PPWORKD,RA                                                       
         L     R9,PPWORK2C                                                      
         USING PPWORK2D,R9                                                      
         LA    RC,SPACEND                                                       
         USING BILWRKD,RC                                                       
         L     R8,=A(COMSUBS)      COMMON AREA                                  
         USING COMSUBS,R8                                                       
         LA    R7,BREC                                                          
         USING BILLEDID,R7                                                      
         LA    R6,TSARB                                                         
         USING TSARD,R6                                                         
*                                                                               
         MVC   EDPARMS(24),0(R1)   AND PARMS                                    
         MVC   EDMODE,0(R1)        MODE IS HOB OF PARM1                         
*                                                                               
         CLI   DUESW,C'R'          DON'T PROCESS AOR                            
         BE    EXIT                                                             
         CLI   DUESW,C'A'          SEP ADJ BILLS ARE OK                         
         BE    MP03                                                             
***                                                                             
***  WHAT ABOUT SEPERATE CD INV?                                                
***                                                                             
         CLI   PASSNO,1            DON'T PROCESS DETAIL BACKUP                  
         BH    EXIT                                                             
*                                                                               
MP03     DS    0H                                                               
         CLI   FRSTSW,0                                                         
         BNE   *+8                                                              
         BAS   RE,INIT                                                          
*                                                                               
         BAS   RE,SETGN                                                         
*                                  FIND MODE                                    
         LA    RF,MODLIST                                                       
         LA    R0,MODLISTN                                                      
*                                                                               
MP04     DS    0H                                                               
         CLC   EDMODE,0(RF)                                                     
         BE    MP04D                                                            
         LA    RF,5(RF)                                                         
         BCT   R0,MP04                                                          
         DC    H'0'                                                             
*                                                                               
MP04D    DS    0H                                                               
         ICM   RF,15,1(RF)                                                      
         BASR  RE,RF                                                            
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        INITIALIZATION                                                         
**********************************************************************          
*                                                                               
INIT     NTR1                                                                   
         CLI   EDMODE,EDMCLS       IF CLOSING, NO INIT                          
         BE    INITX                                                            
         MVI   FRSTSW,1                                                         
*                                                                               
*              NOTE- IF WORKER FILES USED, THEY ARE OPENED AT MMFHDR            
*                                                                               
         CLI   OUTMODE,C'D'        DATASET OUTPUT                               
         BNE   INIT4                                                            
         OPEN  (EDIOUT,OUTPUT)                                                  
         B     INIT5                                                            
*                                                                               
INIT4    DS    0H                                                               
INIT5    DS    0H                                                               
         MVC   WORK(8),=CL8'T00A7D'    LOAD TSAROFF                             
         GOTO1 LOADER,DMCB,WORK,0,0                                             
         OC    DMCB+4(4),DMCB+4                                                 
         BNZ   *+6                                                              
         DC    H'0'                                                             
         MVC   ATSAROFF,4(R1)                                                   
*                                                                               
         L     R0,=A(BESIZE)                                                    
         GETMAIN RU,LV=(0),LOC=(ANY,ANY)                                        
         LTR   R1,R1                                                            
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    R1,TSABUF                                                        
*                                                                               
         MVC   TSACOM,VCOMFACS                                                  
         MVI   TSKEYL,BEKLN                                                     
         OI    TSRECI,TSRVAR       SET FOR VARIABLE RECS                        
         MVC   TSRECL,=Y(BERECL)                                                
         MVI   TSOFFACT,TSAINI                                                  
         MVC   TSAREC,=A(BESIZE)                                                
         GOTO1 ATSAROFF,(R6)                                                    
*                                                                               
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         LA    RF,BREC             SET ADDR'S OF REC AREAS                      
         ST    RF,ABREC                                                         
         LA    RF,ABREC+L'ABREC                                                 
         LA    RF,FREC                                                          
         ST    RF,AFREC                                                         
         LA    RF,4(RF)                                                         
         ST    RF,AFRECP4          REC+4 IS START OF DATA                       
         LA    RF,AFREC+L'AFREC                                                 
         ST    RF,AFRECX                                                        
*                                                                               
INITX    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        PRODUCT GROUP                                                          
**********************************************************************          
*                                                                               
EDPGR    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEPGRLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDPGRQ       RECORD TYPE                                  
*                                                                               
         C     R3,APGR3BRK                                                      
         BNE   EDPGR4                                                           
         MVI   BEPGRL,C'3'                                                      
         MVC   BEPGRC,PGR3                                                      
         MVC   BEPGRD,PGR3BK                                                    
         MVC   BEPGRN,PGR3NM                                                    
         BAS   RE,EDPUT                                                         
         B     EDPGRX                                                           
*                                                                               
EDPGR4   DS    0H                                                               
         C     R3,APGR2BRK                                                      
         BNE   EDPGR6                                                           
         MVI   BEPGRL,C'2'                                                      
         MVC   BEPGRC,PGR2                                                      
         MVC   BEPGRD,PGR2BK                                                    
         MVC   BEPGRN,PGR2NM                                                    
         BAS   RE,EDPUT                                                         
         B     EDPGRX                                                           
*                                                                               
EDPGR6   DS    0H                                                               
         MVI   BEPGRL,C'1'                                                      
         MVC   BEPGRC,PGR1                                                      
         MVC   BEPGRD,PGR1BK                                                    
         MVC   BEPGRN,PGR1NM                                                    
         BAS   RE,EDPUT                                                         
*                                                                               
EDPGRX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        PRODUCT                                                                
**********************************************************************          
*                                                                               
EDPRD    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEPRDLN)   PRODUCT RECORD                            
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDPRDQ       RECORD CODE                                  
         MVC   BEPRD,PRD                                                        
         MVC   BEPRDN,PRDNM                                                     
         OC    BEPRDN,SPACES                                                    
*                                                                               
         MVC   BEPRICD,SPACES                                                   
         L     RF,ADPRD            PRD NUMBER                                   
         LA    RF,PPRDACCT-PPRDREC(RF)                                          
         MVC   BEPRICD(4),0(RF)                                                 
         CLI   0(RF),X'FF'                                                      
         BNE   EDPRD4                                                           
         MVC   FULL,0(RF)                                                       
         MVI   FULL,0                                                           
         EDIT  (B4,FULL),(5,BEPRICD),ALIGN=LEFT                                 
*                                                                               
EDPRD4   DS    0H                                                               
         OC    BEPRICD,SPACES                                                   
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        ESTIMATE                                                               
**********************************************************************          
*                                                                               
EDEST    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEESTLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDESTQ                                                    
         MVC   BEEST,EST                                                        
         MVC   BEESTN,ESTNM                                                     
         OC    BEESTN,SPACES                                                    
         MVC   BEESTN2,ESTNAM2                                                  
         OC    BEESTN2,SPACES                                                   
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        MARKET GROUP                                                           
**********************************************************************          
*                                                                               
EDMGR    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEMGRLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDMGRQ       RECORD TYPE                                  
*                                                                               
         C     R3,AMGR3BRK                                                      
         BNE   EDMGR4                                                           
         MVI   BEMGRL,C'3'                                                      
         MVC   BEMGRC,MGR3                                                      
         MVC   BEMGRD,MGR3BK                                                    
         MVC   BEMGRN,MGR3NM                                                    
         BAS   RE,EDPUT                                                         
         B     EDMGRX                                                           
*                                                                               
EDMGR4   DS    0H                                                               
         C     R3,AMGR2BRK                                                      
         BNE   EDMGR6                                                           
         MVI   BEMGRL,C'2'                                                      
         MVC   BEMGRC,MGR2                                                      
         MVC   BEMGRD,MGR2BK                                                    
         MVC   BEMGRN,MGR2NM                                                    
         BAS   RE,EDPUT                                                         
         B     EDMGRX                                                           
*                                                                               
EDMGR6   DS    0H                                                               
         MVI   BEMGRL,C'2'                                                      
         MVC   BEMGRC,MGR1                                                      
         MVC   BEMGRD,MGR1BK                                                    
         MVC   BEMGRN,MGR1NM                                                    
         BAS   RE,EDPUT                                                         
*                                                                               
EDMGRX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        MARKET                                                                 
**********************************************************************          
*                                                                               
EDMKT    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEMKTLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDMKTQ       RECORD TYPE                                  
         L     RF,AMKT                                                          
         MVC   BEMKTN(20),0(RF)                                                 
         OC    BEMKTN,SPACES                                                    
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        PUBLICATION                                                            
**********************************************************************          
*                                                                               
EDPUB    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEPUBLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDPUBQ       RECORD TYPE                                  
         MVC   BEPUBN,PPUBL1                                                    
         MVC   BEPUBN2,PPUBL2                                                   
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        JOB OR AD                                                              
**********************************************************************          
*                                                                               
EDJOB    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEJOBLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDJOBQ       RECORD TYPE                                  
         MVC   BEJOB1,JOBL1                                                     
         MVC   BEJOB2,JOBL2                                                     
         MVC   BEJOB3,JOBL3                                                     
         MVC   BEJOB4,JOBL4                                                     
         MVC   BEJOB5,JOBL5                                                     
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
         EJECT                                                                  
**********************************************************************          
*        MONTHLY TOTAL AMOUNTS                                                  
**********************************************************************          
*                                                                               
EDMTA    NTR1                                                                   
*                                                                               
         L     R3,SAVBRK                                                        
         CLI   3(R3),X'34'          SKIP LEVELS HIGHER THAN DESC                
         BNL   EXIT                 (NO INSERTION LEVEL (FOR NOW))              
*                                                                               
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEDETLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDMTAQ                                                    
*                                                                               
         L     R3,SAVBRK           BREAK TABLE ENTRY                            
         MVC   BEDLEV,3(R3)        BREAK LEVEL CODE                             
         C     R3,AINVBRK          IF AND END OF INVOICE                        
         BNE   *+8                                                              
         MVI   BEDLEV,X'FF'        SAY SO                                       
*                                                                               
         L     R3,PERLIND          PERIOD (PERLIST INDEX)                       
         LA    R3,PERLIST(R3)                                                   
*                                                                               
         CLI   0(R3),X'FF'                                                      
         BNE   EDMTA12                                                          
         MVC   BEDMOS(5),=C'TOTAL'                                              
         B     EDMTA14                                                          
*                                                                               
EDMTA12  DS    0H                                                               
**SPOT** CLI   SPOTPROF+2,0        TEST CALENDAR TYPE                           
**SPOT** BNE   EDMTA13                                                          
         GOTO1 DATCON,DMCB,(3,0(R3)),(6,BEDMOS)  NORMAL MMMYY                   
         B     EDMTA14                                                          
*                                                                               
EDMTA13  DS    0H                                                               
         GOTO1 DATCON,DMCB,(2,2(R3)),(5,BEDMOS)  OTHER MMMDD/YY                 
*                                                                               
EDMTA14  DS    0H                                                               
*                                                                               
*****                                                                           
*****    NOTE-I'M NOT SET-UP TO HANDLE BILLING AGY COMM.                        
*****                                                                           
         L     RF,EDPARMS                                                       
         MVC   BEDPINV,0(RF)                                                    
*                                                                               
         TM    GNCTL,X'80'         GROSS                                        
         BZ    EDMTA16                                                          
         EDIT  (B4,00(R2)),(11,BEDOGRS),FLOAT=-,ALIGN=LEFT,            X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,ROWHL+00(R2)),(11,BEDPGRS),FLOAT=-,ALIGN=LEFT,      X        
               ZERO=NOBLANK                                                     
*                                                                               
EDMTA16  DS    0H                                                               
         TM    GNCTL,X'40'         NET                                          
         BZ    EDMTA18                                                          
         EDIT  (B4,04(R2)),(11,BEDONET),FLOAT=-,ALIGN=LEFT,            X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,ROWHL+04(R2)),(11,BEDPNET),FLOAT=-,ALIGN=LEFT,      X        
               ZERO=NOBLANK                                                     
*                                                                               
EDMTA18  DS    0H                                                               
         EDIT  (B4,08(R2)),(11,BEDOCD),FLOAT=-,ALIGN=LEFT,             X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,ROWHL+08(R2)),(11,BEDPCD),FLOAT=-,ALIGN=LEFT,       X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,16(R2)),(11,BEDOTAX),FLOAT=-,ALIGN=LEFT,            X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,ROWHL+16(R2)),(11,BEDPTAX),FLOAT=-,ALIGN=LEFT,      X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,20(R2)),(11,BEDOINS),FLOAT=-,ALIGN=LEFT,            X        
               ZERO=NOBLANK                                                     
         EDIT  (B4,ROWHL+20(R2)),(11,BEDPINS),FLOAT=-,ALIGN=LEFT,      X        
               ZERO=NOBLANK                                                     
*                                                                               
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        DETAIL AMOUNTS, ETC.  NO-OPED (REPLACED BY EDMTA)                      
*                              IF RESTORING, CONTINUATION NEEDED FOR            
*                              EDIT STATEMENTS                                  
**********************************************************************          
*                                                                               
**DET    NTR1                                                                   
**       XC    BEKEY,BEKEY                                                      
**       MVC   BEREC(2),=Y(BEDETLN)                                             
**       MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
**       MVI   BEKRCD,EDDETQ                                                    
**                                                                              
**       L     R3,SAVBRK           BREAK TABLE ENTRY                            
**       MVC   BEDLEV,3(R3)        BREAK LEVEL CODE                             
**       C     R3,AINVBRK          IF AND END OF INVOICE                        
**       BNE   *+8                                                              
**       MVI   BEDLEV,X'FF'        SAY SO                                       
**                                                                              
**SPOT** L     R3,PERLIND          PERIOD (PERLIST INDEX)                       
**SPOT** LA    R3,PERLIST(R3)                                                   
**                                                                              
**       LA    R3,PERLIST                                                       
**       CLI   0(R3),X'FF'                                                      
**       BNE   EDD20                                                            
**       MVC   BEDMOS(5),=C'TOTAL'                                              
**       B     EDD30                                                            
**                                                                              
**D20    DS    0H                                                               
**SPOT** CLI   SPOTPROF+2,0        TEST CALENDAR TYPE                           
**SPOT** BNE   EDD25                                                            
**       GOTO1 DATCON,DMCB,(3,0(R3)),(6,BEDMOS)  NORMAL MMMYY                   
**       B     EDD30                                                            
**                                                                              
**D25    DS    0H                                                               
**       GOTO1 DATCON,DMCB,(2,2(R3)),(5,BEDMOS)  OTHER MMMDD/YY                 
**                                                                              
**D30    DS    0H                                                               
**       MVI   BEDTYP,C'T'         T=TIME COSTS                                 
**                                                                              
**       L     RF,EDPARMS                                                       
**       MVC   BEDPINV,0(RF)                                                    
**                                                                              
**       EDIT  (B4,00(R2)),(11,BEDOGRS),FLOAT=-,ALIGN=LEFT,                     
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,04(R2)),(11,BEDONET),FLOAT=-,ALIGN=LEFT,                     
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,08(R2)),(11,BEDOCD),FLOAT=-,ALIGN=LEFT,                      
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,16(R2)),(11,BEDOTAX),FLOAT=-,ALIGN=LEFT,                     
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,20(R2)),(11,BEDOINS),FLOAT=-,ALIGN=LEFT,                     
**             ZERO=NOBLANK                                                     
**                                                                              
**       EDIT  (B4,ROWHL+00(R2)),(11,BEDPGRS),FLOAT=-,ALIGN=LEFT,               
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,ROWHL+04(R2)),(11,BEDPNET),FLOAT=-,ALIGN=LEFT,               
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,ROWHL+08(R2)),(11,BEDPCD),FLOAT=-,ALIGN=LEFT,                
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,ROWHL+16(R2)),(11,BEDPTAX),FLOAT=-,ALIGN=LEFT,               
**             ZERO=NOBLANK                                                     
**       EDIT  (B4,ROWHL+20(R2)),(11,BEDPINS),FLOAT=-,ALIGN=LEFT,               
**             ZERO=NOBLANK                                                     
**                                                                              
**       BAS   RE,EDPUT                                                         
**                                                                              
**       B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        COMMENTS                                                               
**********************************************************************          
*                                                                               
EDCOM    NTR1                                                                   
         L     R4,ACOMREC                                                       
         CLI   0(R4),0             TEST COMMENT PRESENT                         
         BE    EDCMX                                                            
*                                                                               
         LA    R4,33(R4)           FIRST ELEM                                   
*                                                                               
EDCM2    DS    0H                                                               
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BECOMLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDCOMQ                                                    
*                                                                               
         L     R3,SAVBRK           BREAK TABLE ENTRY                            
         MVC   BECOMLEV,3(R3)      BREAK LEVEL CODE                             
         C     R3,AINVBRK          IF AND END OF INVOICE                        
         BNE   *+8                                                              
         MVI   BECOMLEV,X'FF'      SAY SO                                       
*                                                                               
         CLI   0(R4),0             EOR                                          
         BE    EDCM10                                                           
         CLI   0(R4),X'40'         COMMENT ELEM                                 
         BNE   EDCM4                                                            
         ZIC   RE,1(R4)            ELEM LENGTH                                  
         SH    RE,=H'3'            SET FOR EX                                   
         BM    EDCM4                                                            
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BECOMTXT(0),2(R4)                                                
         BAS   RE,EDPUT                                                         
*                                                                               
EDCM4    DS    0H                                                               
         ZIC   R0,1(R4)                                                         
         AR    R4,R0                                                            
         B     EDCM2                                                            
*                                                                               
EDCM10   DS    0H                                                               
*                                                                               
EDCMX    DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        BILLING HEADER                                                         
**********************************************************************          
*                                                                               
EDHDR    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVI   BEKRCLS,EDRSRTHQ    HEADER CLASS                                 
         MVI   BEKRCD,EDHDRQ                                                    
         L     RF,ADBILL                                                        
         SR    RE,RE                                                            
         ICM   RE,3,25(RF)                                                      
         STH   RE,BEREC            SET RECORD LENGTH                            
         BCTR  RE,0                                                             
         EX    RE,*+8                                                           
         B     *+10                                                             
         MVC   BEHDRC(0),0(RF)                                                  
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        BILLING ADDRESS                                                        
**********************************************************************          
*                                                                               
EDADD    NTR1                                                                   
         TM    EDNEWSW,X'80'       TEST HAVE ALAREADY DONE ADDRESS              
         BNZ   EDADDX                                                           
         OI    EDNEWSW,X'80'                                                    
*                                                                               
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEADDLN)                                             
         MVI   BEKRCLS,EDRSRTAQ    ADDRESS SORT GROUP                           
         MVI   BEKRCD,EDADDQ                                                    
         MVC   BEADDL1,HEAD7+46                                                 
         MVC   BEADDL2,HEAD8+46                                                 
         MVC   BEADDL3,HEAD9+46                                                 
         MVC   BEADDL4,HEAD10+46                                                
         MVC   BEADDL5,HEAD11+46                                                
*                                                                               
         BAS   RE,EDPUT                                                         
*                                                                               
EDADDX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        USER DEFINITION FIELDS                                                 
**********************************************************************          
*                                                                               
EDUSR    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEUSRLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDUSRQ                                                    
         L     RF,EDPARMS                                                       
         MVC   BEUSRTXT(2),0(RF)                                                
*                                                                               
         MVC   BEUSRTXT+2(L'BEUSRTXT-2),W     (W FOR PRINT)                     
*                                  (MFUSR ROUTINE WILL SORT IT OUT)             
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        USER COMMENT FIELDS                                                    
**********************************************************************          
*                                                                               
EDUCM    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEUCMLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDUCMQ                                                    
         L     RF,EDPARMS                                                       
         MVC   BEUCMTXT(2),0(RF)                                                
*                                                                               
         MVC   BEUCMTXT+2(L'BEUCMTXT-2),W     (W FOR PRINT)                     
*                                  (MFUCM ROUTINE WILL SORT IT OUT)             
         BAS   RE,EDPUT                                                         
*                                                                               
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        LIST OF PREVIOUS BILLS                                                 
**********************************************************************          
*                                                                               
EDLST    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BDLSTLN)                                             
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDLSTQ                                                    
*                                                                               
         L     RF,EDPARMS                                                       
         MVC   BDLSTIN,0(RF)    INVOICE NUMBER                                  
         L     RF,EDPARMS+4                                                     
         EDIT  (B4,0(RF)),(11,BDLSTG),FLOAT=-,ALIGN=LEFT   GROSS                
         L     RF,EDPARMS+8                                                     
         EDIT  (B4,0(RF)),(11,BDLSTN),FLOAT=-,ALIGN=LEFT   NET                  
         L     RF,EDPARMS+12                                                    
         EDIT  (B4,0(RF)),(11,BDLSTCD),FLOAT=-,ALIGN=LEFT  CD                   
*                                                                               
         BAS   RE,EDPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        AMOUNT DUE                                                             
*          P1=A(FORMULA)  R4 IN PRTADJ                                          
*          P2=A(TOTAL)    R7 IN PRTADJ                                          
*  (SPOT)  P3=INDEX TO CURRENCY (0=AGENCY, 4=CLIENT)   R6 IN PRTADJ             
*                                                                               
**********************************************************************          
*                                                                               
EDDUE    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BDUELN)                                              
         MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
         MVI   BEKRCD,EDDUEQ                                                    
*                                                                               
         L     R3,SAVBRK                                                        
         MVC   BDULEV,3(R3)        BREAK LEVEL CODE                             
         C     R3,AINVBRK          IF END OF INVOICE                            
         BNE   *+8                                                              
         MVI   BDULEV,X'FF'        SAY SO                                       
*                                                                               
         L     R4,EDPARMS          A(BFORM)                                     
         USING BFORMD,R4                                                        
         MVI   BDUBB,C'G'          BILL BASIS - GROSS                           
         CLI   BFBASA,X'01'                                                     
         BE    EDD4                                                             
         MVI   BDUBB,C'N'          BILL BASIS - NET                             
         CLI   BFBASA,X'02'                                                     
         BE    EDD4                                                             
         MVI   BDUBB,C'1'          GROSS-CD                                     
         CLI   BFBASA,X'05'        GROSS-CD                                     
         BE    EDD4                                                             
         MVI   BDUBB,C'2'          NET-CD                                       
         CLI   BFBASA,X'06'        NET-CD                                       
         BE    EDD4                                                             
         MVI   BDUBB,C'A'          AGY COMM                                     
         CLI   BFBASA,X'08'        AGY COMM                                     
         BE    EDD4                                                             
         MVI   BDUBB,C'M'          AGY COMM                                     
         CLI   BFBASA,X'FF'                                                     
         BE    EDD4                                                             
         DC    H'0'                BAD BILLING FORMULA                          
*                                                                               
EDD4     DS    0H                  COMMISSION BASIS                             
         MVI   BDUCB,C'G'          BILL BASIS - GROSS                           
         CLI   BFBASB,X'01'                                                     
         BE    EDD6                                                             
         MVI   BDUCB,C'N'          BILL BASIS - NET                             
         CLI   BFBASB,X'02'                                                     
         BE    EDD6                                                             
         MVI   BDUCB,C'1'          GROSS-CD                                     
         CLI   BFBASB,X'05'        GROSS-CD                                     
         BE    EDD6                                                             
         MVI   BDUCB,C'2'          NET-CD                                       
         CLI   BFBASB,X'06'        NET-CD                                       
         BE    EDD6                                                             
         MVI   BDUCB,C'A'          AGY COMM                                     
         CLI   BFBASB,X'08'        AGY COMM                                     
         BE    EDD6                                                             
         MVI   BDUCB,C'M'          AGY COMM                                     
         CLI   BFBASB,X'FF'                                                     
         BE    EDD6                                                             
         DC    H'0'                BAD BILLING FORMULA                          
*                                                                               
EDD6     DS    0H                                                               
         CLC   BFCOMP,FFS          MIXED %'S                                    
         BE    EDD8                                                             
         EDIT  (B3,BFCOMP),(11,BDUCP),FLOAT=-,ALIGN=LEFT                        
*                                                                               
EDD8     DS    0H                                                               
         L     R3,EDPARMS+4        A(TOTALS)                                    
**SPOT** A     R3,EDPARMS+8        CURRENCY INDEX                               
*                                                                               
         CLI   DUESW,C'C'          SEE IF DOING CD SEPERATE INV                 
         BE    EDD15                                                            
*                                                                               
         CLI   ADJSEP,C'Y'                                                      
         BE    EDD10                                                            
*                                                                               
         L     RE,DUEDSP(R3)       AMOUNT DUE                                   
         S     RE,ADJDSP(R3)       LESS ADJUSTMENT = BASIS AMOUNT               
         EDIT  (RE),(11,BDUBBA),FLOAT=-,ALIGN=LEFT                              
*                                                                               
         L     RE,ADJDSP(R3)       ADJUSTMENT                                   
         EDIT  (RE),(11,BDUCBA),FLOAT=-,ALIGN=LEFT                              
*                                                                               
         L     RE,DUEDSP(R3)       AMOUNT DUE                                   
         EDIT  (RE),(11,BDUAMT),FLOAT=-,ALIGN=LEFT                              
         B     EDD18                                                            
*                                                                               
EDD10    DS    0H                  SEP ADJUSTMENT                               
         CLI   DUESW,C'A'          ACTUAL ADJUSTMENT BILL?                      
         BNE   EDD12                                                            
*                                  YES, SPECIAL AMOUNTS                         
         MVI   BDUBBA,C'0'         BASE IS $0??                                 
*                                                                               
         L     RE,ADJDSP(R3)       ADJUSTMENT                                   
         EDIT  (RE),(11,BDUCBA),FLOAT=-,ALIGN=LEFT                              
*                                                                               
         MVC   BDUAMT,BDUCBA       ADJUSTMENT IS AMOUNT DUE                     
*                                                                               
         B     EDD18                                                            
*                                                                               
EDD12    DS    0H                  BASE BILL IN SEP ADJ SITUATION               
         L     RE,DUEDSP(R3)       AMOUNT DUE                                   
         S     RE,ADJDSP(R3)       LESS ADJUSTMENT = BASIS AMOUNT               
         EDIT  (RE),(11,BDUBBA),FLOAT=-,ALIGN=LEFT                              
*                                                                               
         MVI   BDUCBA,C'0'         ADJUSTMENT IS $0.                            
*                                                                               
         MVC   BDUAMT,BDUBBA       DUE IS BASE AMOUNT                           
         B     EDD18                                                            
*                                                                               
EDD15    DS    0H              CD SEPERATE INVOICE                              
         MVI   BDUBBA,C'0'     BASIS IS 0                                       
         MVI   BDUCBA,C'0'     ADJUSTMENT IS 0                                  
         L     RE,RCDD(R3)     CD                                               
         S     RE,RBCDD(R3)    LESS PREV. BILLED CD                             
*                              AMOUNT DUE IS CD                                 
         EDIT  (RE),(11,BDUAMT),FLOAT=-,ALIGN=LEFT                              
*                                                                               
EDD18    DS    0H                                                               
*                                                                               
         BAS   RE,EDPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
**********************************************************************          
*        REMITTANCE ADDRESS                                                     
*        NOT AVAILABLE FOR PRINTPAK                                             
**********************************************************************          
*                                                                               
*DRMA    NTR1                                                                   
*        MVC   BEREC(2),=Y(BERMALN)                                             
*        MVI   BEKRCLS,EDRSRTMQ    MAIN SORT GROUP                              
*        MVI   BEKRCD,EDRMAQ                                                    
*        L     R4,ADSTATAD                                                      
*        USING ADDRREC,R4                                                       
*        MVC   BERMNAM,ANAME                                                    
*        MVC   BERMADR,A1LINE                                                   
*        MVC   BERMCTY,A2LINE                                                   
*        MVC   BERMSTA,A3LINE                                                   
*        MVC   BERMZIP,ABIGZIP                                                  
*                                                                               
*        OC    BERMNAM,SPACES                                                   
*        OC    BERMADR,SPACES                                                   
*        OC    BERMCTY,SPACES                                                   
*        OC    BERMSTA,SPACES                                                   
*        OC    BERMZIP,SPACES                                                   
*                                                                               
*        BAS   RE,EDPUT                                                         
*        DROP  R4                                                               
*                                                                               
*        B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        END OF INVOICE                                                         
**********************************************************************          
*                                                                               
EDEND    NTR1                                                                   
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BECLTLN)   CLIENT RECORD                             
         MVI   BEKRCLS,EDRSRTHQ    HEADER CLASS                                 
         MVI   BEKRCD,EDADVQ                                                    
         MVC   BECLT,CLIENT                                                     
         L     RF,ADCLT                                                         
         MVC   BECLTN(L'PCLTNAME),PCLTNAME-PCLTREC(RF)                          
         OC    BECLTN,SPACES                                                    
EDENDB   DS    0H                                                               
         CLI   PCLTNUM-PCLTREC(RF),X'FF'                                        
         BNE   EDENDC                       PWOS                                
         UNPK  WORK(5),PCLTNUM+1-PCLTREC(3,RF)                                  
         B     EDENDD                                                           
*                                                                               
EDENDC   MVC   BYTE,PCLTNUM-PCLTREC(RF)                                         
         NI    BYTE,X'F0'                                                       
         CLI   BYTE,X'80'                                                       
         BNE   EDENDC8                                                          
         XC    FULL,FULL                                                        
         MVC   FULL+1,PCLTNUM-PCLTREC(RF)                                       
         NI    FULL+1,X'7F'              SET-OFF X'80'                          
         L     R0,FULL                                                          
         CVD   R0,DUB                                                           
         OI    DUB+7,X'0F'                                                      
         UNPK  WORK(5),DUB                                                      
         CLC   QAGENCY,=C'KA'        SPECIAL FOR KA                             
         BNE   EDENDC4                                                          
         MVC   WORK+10(5),WORK                                                  
         MVC   WORK(5),SPACES                                                   
         MVC   WORK(4),WORK+1       NOTE - DROPS HIGH DIGIT                     
         B     EDENDD                                                           
*                                                                               
EDENDC4  DS    0H                                                               
         B     EDENDD                                                           
*                                                                               
EDENDC8  MVC   WORK(3),PCLTNUM-PCLTREC(RF)                                      
         OC    WORK(5),SPACES                                                   
EDENDD   MVC   BECLTICD(5),WORK                                                 
         OC    BECLTICD,SPACES                                                  
         BAS   RE,EDPUT                                                         
*                                                                               
         XC    BEKEY,BEKEY                                                      
         MVC   BEREC(2),=Y(BEAGMLN)    AGENCY/MEDIA RECORD                      
         MVI   BEKRCLS,EDRSRTHQ    HEADER CLASS                                 
         MVI   BEKRCD,EDAGMQ                                                    
         MVI   BEAGMMED,C'P'           FOR PRINTPAK                             
         MVC   BEAGMMED+1(1),MEDIA     MEDIA                                    
         MVC   BEAGMMN,MEDNM           MEDIA NAME                               
         MVC   BEAGMAN,AGYNM           AGENCY NAME                              
         MVC   BEAGMAD,AGYADR          AGENCY ADDRESS                           
         BAS   RE,EDPUT                                                         
*                                  AT END OF INVOICE                            
         BAS   RE,EDTRC            TRACE                                        
*                                                                               
         GOTO1 =A(MAKEFILE),DMCB,SPACEND   CREATE OUTPUT FILE                   
*                                                                               
         MVI   TSOFFACT,TSAINI     RE-INITIALIZE TSAROFF                        
         MVC   TSAREC,=A(BESIZE)                                                
         GOTO1 ATSAROFF,(R6)                                                    
*                                                                               
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVI   EDNEWSW,X'00'       CLEAR ED ROUTINES NEWSW                      
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        CLOSE                                                                  
**********************************************************************          
*                                                                               
EDCLS    NTR1                                                                   
         CLI   FRSTSW,0            TEST EDI OPEN                                
         BE    ECLX                                                             
*                                                                               
         CLI   OUTMODE,C'D'        DATASET OUTPUT                               
         BNE   EDCLS8                                                           
*                                                                               
         CLOSE EDIOUT                                                           
*                                TRACE OUTPUT FILE                              
         L     RF,APATCH                                                        
         TM    0(RF),X'04'         TEST NEED FINAL TRACE                        
         BZ    ECLX                                                             
*                                                                               
         MVI   FORCEHED,C'Y'                                                    
*                                                                               
         OPEN  (EDITRC,INPUT)                                                   
*                                                                               
EDCLS2   DS    0H                                                               
         BAS   RE,CLRFREC                                                       
         LA    R2,EDITRC                                                        
         GET   (R2),FREC                                                        
         MVC   P(132),FREC                                                      
         MVI   SKIPSPEC,C'Y'                                                    
         GOTO1 APRNT                                                            
         B     EDCLS2                                                           
*                                                                               
EDCLS4   DS    0H                                                               
         B     EXIT                                                             
*                                  CLOSE FOR WORKER FILE                        
EDCLS8   DS    0H                                                               
         GOTO1 DATAMGR,DMCB,=C'CLOSE',WRKRFIL,0,FREC,AWRKBUFF                   
*                                NO TRACE OF OUTPUT FILE                        
         B     EXIT                                                             
*                                                                               
EDTRCEOF DS    0H                                                               
         CLOSE EDITRC                                                           
*                                                                               
ECLX     DS    0H                                                               
         B     EXIT                                                             
*                                                                               
         LTORG                                                                  
         EJECT                                                                  
**********************************************************************          
*        MAKEFILE - CREATE OUTPUT FILE FROM TSAR BUFFER                         
**********************************************************************          
*                                                                               
MAKEFILE DS    0D                                                               
         NMOD1 0,**MKF***                                                       
         LA    RC,SPACEND                                                       
*                                  READ THRU TSAR BUFFER                        
         ST    R7,TSAREC                                                        
         XC    BREC(BEKLN),BREC                                                 
         MVI   TSOFFACT,TSARDH                                                  
         B     MKF4B                                                            
*                                                                               
MKF4     DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
*                                                                               
MKF4B    DS    0H                                                               
         GOTO1 ATSAROFF,(R6)                                                    
         TM    TSERRS,TSEEOF                                                    
         BO    MKF20                                                            
*                                                                               
         TM    TSERRS,X'FF'-TSERNF (RECORD NOT FOUND IS OK)                     
         BZ    *+6                                                              
         DC    H'0'                NO OTHER ERROR IS ACCEPTABLE                 
*                                                                               
*                                  FIND ROUTINE                                 
         LA    RF,MKFROUTS                                                      
         LA    R0,MKFRTSN                                                       
*                                                                               
MKF5     DS    0H                                                               
         CLC   BREC+5(1),0(RF)                                                  
         BE    MKF6                                                             
         LA    RF,5(RF)                                                         
         BCT   R0,MKF5                                                          
         DC    H'0'                                                             
*                                                                               
MKF6     DS    0H                                                               
         ICM   RF,15,1(RF)                                                      
         BASR  RE,RF                                                            
         B     MKF4                NEXT TSAR RECORD                             
*                                                                               
MKF20    DS    0H                                                               
         MVI   MFNEWSW,0             CLEAR NEWSW BITS                           
*                                                                               
MKFX     DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
***********************************************************************         
*        MFHDR - INVOICE HEADER                                                 
***********************************************************************         
*                                                                               
MFHDR    NTR1                                                                   
         TM    MFNEWSW,X'80'         NEW INVOICE?                               
         BO    MFHDR4                                                           
         OI    MFNEWSW,X'80'                                                    
*                                  YES, START NEW RECORD                        
         CLI   OUTMODE,C'W'        FOR WORKER FILES DO OPEN                     
         BNE   MFHDR08                                                          
*                                                                               
         L     R0,=A(WRKRBUFF)                                                  
         ST    R0,AWRKBUFF                                                      
         L     R1,=A(WRKRBEND-WRKRBUFF)                                         
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
         XC    WRKRIND,WRKRIND                                                  
         LA    R4,FREC                                                          
         USING WLHDRD,R4                                                        
         XC    WLHDRD(WLSOFEND-WLHDRD),WLHDRD                                   
         MVC   WLUSRID,RCORIGID    USER ID                                      
         MVC   WLSYSPRG(1),MED     MEDIA                                        
         MVC   WLSYSPRG+1(3),CLT   CLIENT                                       
         CLI   CLT+2,C' '          SPACE TO DOT                                 
         BH    *+8                                                              
         MVI   WLSYSPRG+3,C'.'                                                  
         MVI   WLDAY,X'97'         EDI BILLING IDENTIFIER                       
         MVI   WLCLASS,C'P'        SYSTEM = PRINT                               
*                                                                               
         MVC   WLAGELD,TODAYP                                                   
         MVC   WLUDATA,=X'FFFF'                                                 
         MVC   WLAGELT,=X'FFFF'                                                 
         MVC   WLRETNL,=X'FFFF'                                                 
         MVC   WLRETND,=X'FFFF'                                                 
*                                                                               
         MVC   WLDESC(10),PINVNO   COMMENT                                      
         DROP  R4                                                               
*                                                                               
         GOTO1 DATAMGR,DMCB,(0,=C'GFILE'),=C'WRKFIL',                  X        
               WRKRIND,FREC,AWRKBUFF                                            
         LA    R1,WRKRIND                                                       
         USING UKRECD,R1                                                        
         MVC   WRKRFIL,UKUSRINF                                                 
         DROP  R1                                                               
*                                                                               
         GOTO1 DATAMGR,DMCB,=C'OPEN',WRKRFIL,0,FREC,AWRKBUFF                    
         CLI   DMCB+8,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
MFHDR08  DS    0H                                                               
         L     RF,ADCLT            FIRST PUT EDI CONTROLS                       
         MVC   FREC+4(45),SPACES                                                
         MVI   FREC+04,C'H'                                                     
******   MVC   FREC+05(3),PCLTKCLT-PCLTREC(RF)                                  
         MVC   FREC+05(3),PROFB1A+10   "CLIENT"  FROM PROFILE                   
         OC    FREC+05(3),SPACES                                                
         MVI   FREC+08,C'P'    SYSTEM = PRINTPAK                                
         MVC   FREC+09(2),PCLTKAGY-PCLTREC(RF)                                  
         MVI   FREC+20,C'X'        STANDARD                                     
         MVC   FREC+21(4),PROFB1A+6    VERSION NUMBER                           
         MVC   FREC+25(3),=C'810'     DOCUMENT TYPE                             
         MVI   FREC+28,C'P'       PRODUCTION RUN                                
         CLI   TEST,C'T'           DRAFT BILLING (TEST)                         
         BNE   *+8                                                              
         MVI   FREC+28,C'T'        OR TEST                                      
*                                                                               
***      MVC   FREC+04(03),=C'HMR'                                              
***      MVC   FREC+07(20),PCLTBNAM-PCLTREC(RF)  BILL RECEIPT NAME              
***      OC    FREC+07(20),SPACES                                               
***      MVC   FREC+27(18),=C'X          810   T'                               
***      MVC   FREC+28(06),PCLTATTN-PCLTREC(RF) FIRST 6 OF ATTENTION            
*                                                                               
         LA    RF,FREC+45+4        MFPUT WILL SET LENGTH                        
         ST    RF,ANXTPOS                                                       
         XC    FREC+2(2),FREC+2                                                 
*                                                                               
         BAS   RE,MFPUT                                                         
*                                                                               
         MVC   ANXTPOS,AFRECP4     YES, START NEW RECORD                        
         GOTO1 BLDREC,DMCB,(2,=C'IH')                                           
         GOTO1 (RF),(R1),(10,PINVNO)                                            
         LA    R4,BEDATA                                                        
         USING PBILLREC,R4                                                      
         GOTO1 DATCON,DMCB,(3,PBILINVD),(17,DUB)                                
         GOTO1 BLDREC,DMCB,(8,DUB)                                              
         GOTO1 DATCON,DMCB,(3,PBILDUED),(17,DUB)                                
         GOTO1 BLDREC,DMCB,(8,DUB)                                              
         MVI   WORK,C'A'           SEP ADJUSTMENT BILL                          
         CLI   PBILSEP,C'A'                                                     
         BE    MFHDR3                                                           
         MVI   WORK,C'D'           SEP CASH DISCOUNT BILL                       
         CLI   PBILSEP,C'C'                                                     
         BE    MFHDR3                                                           
         MVI   WORK,C'C'           SEP COMMISSION BILL                          
         TM    PBILCMSW,BSTSCOMQ                                                
         BNZ   MFHDR3                                                           
         MVI   WORK,C'N'           NORMAL BILL                                  
*                                                                               
MFHDR3   DS    0H                                                               
         GOTO1 BLDREC,DMCB,(1,WORK)                                             
         DROP  R4                                                               
*                                                                               
         BAS   RE,MFPUT                                                         
*                                                                               
MFHDR4   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFAGM - AGENCY/MEDIA                                                   
***********************************************************************         
*                                                                               
MFAGM    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4          START NEW RECORD                        
         GOTO1 BLDREC,DMCB,(2,=C'AG')   AGENCY                                  
         GOTO1 (RF),(R1),(33,BEAGMAN)   NAME                                    
         GOTO1 (RF),(R1),(33,BEAGMAD)   ADDRESS                                 
         BAS   RE,MFPUT                                                         
*                                                                               
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'ME')    MEDIA                                  
         GOTO1 (RF),(R1),(2,BEAGMMED)    MEDIA CODE                             
         GOTO1 (RF),(R1),(10,BEAGMMN)    MEDIA NAME                             
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFADV - ADVERTISER                                                     
***********************************************************************         
*                                                                               
MFADV    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'AV')                                           
         GOTO1 (RF),(R1),(3,BECLT)      CODE                                    
         GOTO1 (RF),(R1),(20,BECLTN)    NAME                                    
         GOTO1 (RF),(R1),(5,BECLTICD)   INTERFACE CODE                          
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFADD - BILLING ADDRESS                                                
***********************************************************************         
*                                                                               
MFADD    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'BT')                                           
         GOTO1 (RF),(R1),(36,BEADDL1)                                           
         GOTO1 (RF),(R1),(36,BEADDL2)                                           
         GOTO1 (RF),(R1),(36,BEADDL3)                                           
         GOTO1 (RF),(R1),(36,BEADDL4)                                           
         GOTO1 (RF),(R1),(36,BEADDL5)                                           
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFPGR - PRODUCT GROUP                                                  
***********************************************************************         
*                                                                               
MFPGR    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         MVI   WORK,C'P'           PN IS PGR LEVEL N                            
         MVC   WORK+1(1),BEPGRL                                                 
         GOTO1 BLDREC,DMCB,(2,WORK)                                             
         GOTO1 (RF),(R1),(05,BEPGRC)    CODE                                    
         GOTO1 (RF),(R1),(12,BEPGRD)    DESC                                    
         GOTO1 (RF),(R1),(24,BEPGRN)    NAME                                    
*                                                                               
         BAS   RE,MFPUT                                                         
*                                                                               
MFPGRX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFPRD - PRODUCT                                                        
***********************************************************************         
*                                                                               
MFPRD    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'PR')                                           
         GOTO1 (RF),(R1),(3,BEPRD)      CODE                                    
         GOTO1 (RF),(R1),(20,BEPRDN)    NAME                                    
         GOTO1 (RF),(R1),(5,BEPRICD)    INTERFACE CODE                          
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFEST - ESTIMATE                                                       
***********************************************************************         
*                                                                               
MFEST    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'ES')                                           
         GOTO1 (RF),(R1),(3,BEEST)      CODE                                    
         GOTO1 (RF),(R1),(20,BEESTN)    NAME                                    
         GOTO1 (RF),(R1),(20,BEESTN2)   NAME                                    
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFMGR - MARKET GROUP                                                   
***********************************************************************         
*                                                                               
MFMGR    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         MVI   WORK,C'M'           MN IS MGR LEVEL N                            
         MVC   WORK+1(1),BEMGRL                                                 
         GOTO1 BLDREC,DMCB,(2,WORK)                                             
         GOTO1 (RF),(R1),(05,BEMGRC)    CODE                                    
         GOTO1 (RF),(R1),(12,BEMGRD)    DESC                                    
         GOTO1 (RF),(R1),(24,BEMGRN)    NAME                                    
*                                                                               
         BAS   RE,MFPUT                                                         
*                                                                               
MFMGRX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFMKT - MARKET                                                         
***********************************************************************         
*                                                                               
MFMKT    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'MK')                                           
         GOTO1 (RF),(R1),(20,BEMKTN)   NAME                                     
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFPUB - PUBLICATION                                                    
***********************************************************************         
*                                                                               
MFPUB    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'PB')                                           
         GOTO1 (RF),(R1),(60,BEPUBN)     NAME                                   
         GOTO1 (RF),(R1),(20,BEPUBN2)    NAME - PART 2                          
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFJOB - JOB (OR AD)                                                    
***********************************************************************         
*                                                                               
MFJOB    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'JB')                                           
         GOTO1 (RF),(R1),(25,BEJOB1)     JOB INFO LINE 1                        
         GOTO1 (RF),(R1),(25,BEJOB2)     JOB INFO LINE 2                        
         GOTO1 (RF),(R1),(30,BEJOB3)     JOB INFO LINE 3                        
         GOTO1 (RF),(R1),(30,BEJOB4)     JOB INFO LINE 4                        
         GOTO1 (RF),(R1),(25,BEJOB5)     JOB INFO LINE 5                        
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFUSR - USER DEF RECORDS                                               
***********************************************************************         
*                                                                               
MFUSR    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'UD')                                           
         GOTO1 BLDREC,DMCB,(2,BEUSRTXT)   DATA POSITION- P1,P2...               
*                                                                               
*              USER LINE IS 8 SPACES, DESC: NAME                                
*              (DESC CAN BE UP TO 20, NAME TO 32)                               
*                                                                               
         LA    R4,BEUSRTXT+10                                                   
         LA    R0,22+1                                                          
*                                                                               
MFUSR04  DS    0H                                                               
         CLI   0(R4),C':'                                                       
         BE    MFUSR06                                                          
         LA    R4,1(R4)                                                         
         BCT   R0,MFUSR04                                                       
         B     MFUSRX              SHOULD HAVE FOUND A :                        
*                                                                               
MFUSR06  DS    0H                                                               
         LR    R2,R4               SAVE A(:)                                    
         LA    R0,BEUSRTXT+11                                                   
         SR    R4,R0             R4 = DESC LENGTH                               
*                                                                               
         MVC   X(20),SPACES                                                     
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   X(0),BEUSRTXT+10                                                 
*                                                                               
         GOTO1 (RF),(R1),(20,X)                                                 
         GOTO1 (RF),(R1),(32,2(R2))                                             
*                                                                               
         BAS   RE,MFPUT                                                         
*                                                                               
MFUSRX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFUCM - USER COM RECORDS                                               
***********************************************************************         
*                                                                               
MFUCM    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'UC')                                           
         GOTO1 BLDREC,DMCB,(2,BEUCMTXT)   DATA POSITION- P1,P2...               
*                                                                               
*              USER LINE IS 8 SPACES, DESC: NAME                                
*              (DESC CAN BE UP TO 20, NAME TO 32)                               
*                                                                               
         LA    R4,BEUCMTXT+10                                                   
         LA    R0,22+1                                                          
*                                                                               
MFUCM04  DS    0H                                                               
         CLI   0(R4),C':'                                                       
         BE    MFUCM06                                                          
         LA    R4,1(R4)                                                         
         BCT   R0,MFUCM04                                                       
         B     MFUCMX              SHOULD HAVE FOUND A :                        
*                                                                               
MFUCM06  DS    0H                                                               
         LR    R2,R4               SAVE A(:)                                    
         LA    R0,BEUCMTXT+11                                                   
         SR    R4,R0             R4 = DESC LENGTH                               
*                                                                               
         MVC   X(20),SPACES                                                     
         EX    R4,*+8                                                           
         B     *+10                                                             
         MVC   X(0),BEUCMTXT+10                                                 
*                                                                               
         GOTO1 (RF),(R1),(20,X)                                                 
         GOTO1 (RF),(R1),(32,2(R2))                                             
*                                                                               
         BAS   RE,MFPUT                                                         
*                                                                               
MFUCMX   DS    0H                                                               
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFCOM - COMMENT LINE RECORD                                            
***********************************************************************         
*                                                                               
MFCOM    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'CM')                                           
*                                                                               
         LA    R2,LEVNAMS          GET LEVEL NAME                               
         LA    R0,LEVNAMSN                                                      
*                                                                               
MFCOM4   DS    0H                                                               
         CLC   BECOMLEV,0(R2)                                                   
         BE    MFCOM5                                                           
         LA    R2,5(R2)                                                         
         BCT   R0,MFCOM4                                                        
         DC    H'0'                                                             
*                                                                               
MFCOM5   DS    0H                                                               
         GOTO1 (RF),(R1),(4,1(R2))  LEVEL NAME                                  
*                                                                               
         GOTO1 (RF),(R1),(80,BECOMTXT)                                          
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFDET - DETAIL                                                         
***********************************************************************         
*                                                                               
MFDET    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'MT')                                           
*                                                                               
         LA    R2,LEVNAMS          GET LEVEL NAME                               
         LA    R0,LEVNAMSN                                                      
*                                                                               
MFDET4   DS    0H                                                               
         CLC   BEDLEV,0(R2)                                                     
         BE    MFDET5                                                           
         LA    R2,5(R2)                                                         
         BCT   R0,MFDET4                                                        
         DC    H'0'                                                             
*                                                                               
MFDET5   DS    0H                                                               
         GOTO1 (RF),(R1),(4,1(R2))  LEVEL NAME                                  
*                                                                               
         GOTO1 (RF),(R1),(8,BEDMOS)                                             
         GOTO1 (RF),(R1),(10,BEDPINV)                                           
*                                                                               
         GOTO1 (RF),(R1),(11,BEDOGRS)                                           
         GOTO1 (RF),(R1),(11,BEDONET)                                           
         GOTO1 (RF),(R1),(11,BEDOCD)                                            
         GOTO1 (RF),(R1),(11,BEDOTAX)                                           
         GOTO1 (RF),(R1),(11,BEDOINS)                                           
         GOTO1 (RF),(R1),(11,BEDPGRS)                                           
         GOTO1 (RF),(R1),(11,BEDPNET)                                           
         GOTO1 (RF),(R1),(11,BEDPCD)                                            
         GOTO1 (RF),(R1),(11,BEDPTAX)                                           
         GOTO1 (RF),(R1),(11,BEDPINS)                                           
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFRMA - REMITTANCE ADDRESS                                             
**********************************************************************          
*                                                                               
MFRMA    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'RA')                                           
         GOTO1 (RF),(R1),(20,BERMNAM)   NAME                                    
         GOTO1 (RF),(R1),(24,BERMADR)   ADDRESS                                 
         GOTO1 (RF),(R1),(24,BERMCTY)   CITY                                    
         GOTO1 (RF),(R1),(03,BERMSTA)   STATE                                   
         GOTO1 (RF),(R1),(10,BERMZIP)   ZIP                                     
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
*                                                                               
         SPACE 2                                                                
***********************************************************************         
*        MFLST - PREVIOUS BILL LIST                                             
**********************************************************************          
*                                                                               
MFLST    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'PI')                                           
         GOTO1 (RF),(R1),(10,BDLSTIN)   INVOICE NUMBER                          
         GOTO1 (RF),(R1),(11,BDLSTG)    GROSS                                   
         GOTO1 (RF),(R1),(11,BDLSTN)    NET                                     
         GOTO1 (RF),(R1),(11,BDLSTCD)   CD                                      
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
         SPACE 2                                                                
***********************************************************************         
*        MFDUE - FORMULA ADJUSTMENTS AND AMOUNT DUE                             
**********************************************************************          
*                                                                               
MFDUE    NTR1                                                                   
         MVC   ANXTPOS,AFRECP4     NEW RECORD                                   
         GOTO1 BLDREC,DMCB,(2,=C'AD')                                           
*                                                                               
         LA    R2,LEVNAMS          GET LEVEL NAME                               
         LA    R0,LEVNAMSN                                                      
*                                                                               
MFDUE4   DS    0H                                                               
         CLC   BDULEV,0(R2)                                                     
         BE    MFDUE5                                                           
         LA    R2,5(R2)                                                         
         BCT   R0,MFDUE4                                                        
         DC    H'0'                                                             
*                                                                               
MFDUE5   DS    0H                                                               
         GOTO1 (RF),(R1),(4,1(R2))  LEVEL NAME                                  
*                                                                               
         GOTO1 (RF),(R1),(1,BDUBB)   BILL BASIS (G,N,M)                         
         GOTO1 (RF),(R1),(1,BDUCB)   COMM ADJUST BASIS (G,N,M)                  
         GOTO1 (RF),(R1),(11,BDUCP)   COMM ADJUST % SNNVNNNN                    
         GOTO1 (RF),(R1),(11,BDUBBA)  BILL BASIS AMOUNT                         
         GOTO1 (RF),(R1),(11,BDUCBA)  COMM ADJST AMOUNT                         
         GOTO1 (RF),(R1),(11,BDUAMT)  AMOUNT DUE                                
         BAS   RE,MFPUT                                                         
         B     EXIT                                                             
*                                                                               
         LTORG                                                                  
*                                                                               
BSTCMONQ EQU   X'02'                                                            
BSTCAORQ EQU   X'10'                                                            
BSTTAORQ EQU   X'20'                                                            
BSTSADJQ EQU   X'80'        NOT SET IN PRINTPAK                                 
BSTNTXAQ EQU   X'04'                                                            
BSTSCOMQ EQU   X'01'                                                            
BSTSNETQ EQU   X'08'                                                            
         EJECT                                                                  
**********************************************************************          
*        COMMON SUBROUTINES AND DATA AREAS                                      
**********************************************************************          
*                                                                               
COMSUBS  DS    0D                                                               
*                                                                               
MODLIST  DS    0X                  MODE LIST                                    
         DC    AL1(EDMPGR),AL4(EDPGR)                                           
         DC    AL1(EDMPRD),AL4(EDPRD)                                           
         DC    AL1(EDMEST),AL4(EDEST)                                           
         DC    AL1(EDMMGR),AL4(EDMGR)                                           
         DC    AL1(EDMMKT),AL4(EDMKT)                                           
         DC    AL1(EDMPUB),AL4(EDPUB)                                           
         DC    AL1(EDMJOB),AL4(EDJOB)                                           
         DC    AL1(EDMMTA),AL4(EDMTA)   (REPLACED EDDET)                        
         DC    AL1(EDMCOM),AL4(EDCOM)                                           
         DC    AL1(EDMEND),AL4(EDEND)                                           
         DC    AL1(EDMLST),AL4(EDLST)                                           
         DC    AL1(EDMDUE),AL4(EDDUE)                                           
         DC    AL1(EDMADD),AL4(EDADD)                                           
         DC    AL1(EDMHDR),AL4(EDHDR)                                           
         DC    AL1(EDMUSR),AL4(EDUSR)                                           
         DC    AL1(EDMUCM),AL4(EDUCM)                                           
**SPOT** DC    AL1(EDMRMA),AL4(EDRMA)   NOT FOR PRINTPAK                        
         DC    AL1(EDMCLS),AL4(EDCLS)                                           
MODLISTN EQU   (*-MODLIST)/5                                                    
*                                  TSAR RECORD TYPE EQUATES                     
*                                                                               
MKFROUTS DS    0X                  MAKEFILE RECORD ROUTINE LIST                 
         DC    AL1(EDAGMQ),AL4(MFAGM)                                           
         DC    AL1(EDADVQ),AL4(MFADV)                                           
         DC    AL1(EDADDQ),AL4(MFADD)                                           
         DC    AL1(EDPGRQ),AL4(MFPGR)                                           
         DC    AL1(EDPRDQ),AL4(MFPRD)                                           
         DC    AL1(EDESTQ),AL4(MFEST)                                           
         DC    AL1(EDMGRQ),AL4(MFMGR)                                           
         DC    AL1(EDMKTQ),AL4(MFMKT)                                           
         DC    AL1(EDPUBQ),AL4(MFPUB)                                           
         DC    AL1(EDJOBQ),AL4(MFJOB)                                           
         DC    AL1(EDHDRQ),AL4(MFHDR)                                           
         DC    AL1(EDMTAQ),AL4(MFDET)                                           
         DC    AL1(EDUSRQ),AL4(MFUSR)                                           
         DC    AL1(EDUCMQ),AL4(MFUCM)                                           
         DC    AL1(EDCOMQ),AL4(MFCOM)                                           
         DC    AL1(EDLSTQ),AL4(MFLST)                                           
         DC    AL1(EDDUEQ),AL4(MFDUE)                                           
**SPOT** DC    AL1(EDRMAQ),AL4(MFRMA)   NOT FOR PRINTPAK                        
MKFRTSN  EQU   (*-MKFROUTS)/5                                                   
*                                                                               
*                                  BREAK LEVEL NAMES                            
*                                                                               
LEVNAMS  DS    0X                                                               
         DC    X'04',C'MED '       MEDIA                                        
         DC    X'08',C'PGR1'       PGR1                                         
         DC    X'0C',C'PGR2'       PGR2                                         
         DC    X'10',C'PGR3'       PGR3                                         
         DC    X'14',C'PRD '       PRD                                          
         DC    X'18',C'EST '       EST                                          
         DC    X'1C',C'MGR1'       MGR1                                         
         DC    X'20',C'MGR2'       MGR2                                         
         DC    X'24',C'MGR3'       MGR3                                         
         DC    X'28',C'MKT '       MKT                                          
         DC    X'2C',C'JOB '       JOB                                          
         DC    X'30',C'PUB '       PUBLICATION                                  
         DC    X'34',C'DESC'       DESCRIPTION                                  
         DC    X'38',C'PER '       PERIOD                                       
         DC    X'3C',C'PERG'       PERIOD GROUP                                 
         DC    X'40',C'TYPE'       TYPE - DEBIT/CREDIT (SPECIAL)                
         DC    X'FF',C'INV '       INVOICE                                      
LEVNAMSN EQU   (*-LEVNAMS)/5                                                    
*                                                                               
*                                  TSAR RECORD TYPE EQUATES                     
EDHDRQ   EQU   10                                                               
EDAGMQ   EQU   20                                                               
EDADVQ   EQU   30                                                               
EDADDQ   EQU   32                                                               
EDPGRQ   EQU   40                                                               
EDPRDQ   EQU   50                                                               
EDESTQ   EQU   60                                                               
EDJOBQ   EQU   61                  ONLY FOR PRINTPAK                            
EDPUBQ   EQU   62                                                               
EDCOMQ   EQU   64                                                               
EDUSRQ   EQU   66                                                               
EDUCMQ   EQU   68                                                               
EDMGRQ   EQU   70                                                               
EDMKTQ   EQU   80                                                               
EDDUEQ   EQU   81                                                               
EDLSTQ   EQU   83                                                               
EDRMAQ   EQU   85                                                               
EDMTAQ   EQU   90                                                               
*                                  SORT SEQUENCE GROUPS                         
EDRSRTHQ EQU   10                  HEADER                                       
EDRSRTAQ EQU   15                  BILLING ADDR                                 
EDRSRTMQ EQU   20                  MAIN                                         
*                                                                               
FDELIM   EQU   X'5E'               FIELD DELIMITER  (SEMI-COLON)                
*                                                                               
EXIT     DS    0H                                                               
         XIT                                                                    
*                                                                               
*********************************************************************           
*        EDPUT - RECORD TO TSAR                                                 
*********************************************************************           
*                                                                               
EDPUT    NTR1                                                                   
         MVC   BEKSEQ,SEQNO        SET SEQUENCE NUMBER                          
         LH    RF,SEQNO            AND BUMP                                     
         LA    RF,1(RF)                                                         
         STH   RF,SEQNO                                                         
*                                                                               
         LA    R1,BREC                                                          
         ST    R1,TSAREC                                                        
         MVI   TSOFFACT,TSAADD                                                  
         GOTO1 ATSAROFF,(R6)                                                    
*                                                                               
         CLI   TSERRS,0                                                         
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         BAS   RE,CLRBREC          SET BUFFER REC TO SPACES                     
         B     EXIT                                                             
*                                                                               
*********************************************************************           
*        MFPUT - RECORD TO OUTPUT FILE                                          
*********************************************************************           
*                                                                               
MFPUT    NTR1                                                                   
         L     R2,ANXTPOS          SET RECORD LENGTH                            
         S     R2,AFREC                                                         
         STCM  R2,3,FREC                                                        
*                                                                               
         CLI   OUTMODE,C'D'        DATASET OUTPUT                               
         BE    MFPUT4                                                           
*                                  ELSE WORKER OTUPUT                           
         GOTO1 DATAMGR,DMCB,=C'ADD',WRKRFIL,WRKRIND,FREC,AWRKBUFF               
         TM    DMCB+8,0                                                         
         BE    MFPUT6                                                           
         DC    H'0'                                                             
*                                                                               
MFPUT4   DS    0H                                                               
         LA    R2,EDIOUT                                                        
         PUT   (R2),FREC                                                        
*                                                                               
MFPUT6   DS    0H                                                               
         B     EXIT                                                             
*                                                                               
*********************************************************************           
*        TRACE TSAROFF BUFFER                                                   
*********************************************************************           
*                                                                               
EDTRC    NTR1                                                                   
         L     RF,APATCH                                                        
         TM    0(RF),X'08'         TEST NEED TRACE                              
         BZ    EDTRX                                                            
*                                                                               
         MVC   P(7),=C'**EDI**'                                                 
         GOTO1 APRNT                                                            
*                                                                               
         LA    R4,BREC                                                          
         ST    R4,TSAREC                                                        
         XC    BREC(BEKLN),BREC                                                 
         MVI   TSOFFACT,TSARDH                                                  
         B     EDTR4B                                                           
*                                                                               
EDTR4    DS    0H                                                               
         MVI   TSOFFACT,TSANXT                                                  
*                                                                               
EDTR4B   DS    0H                                                               
         GOTO1 ATSAROFF,(R6)                                                    
         TM    TSERRS,TSEEOF                                                    
         BO    EDTRX                                                            
*                                                                               
         TM    TSERRS,X'FF'-TSERNF (RECORD NOT FOUND IS OK)                     
         BZ    *+6                                                              
         DC    H'0'                NO OTHER ERROR IS ACCEPTABLE                 
*                                                                               
         SR    R2,R2                                                            
         ICM   R2,3,0(R4)                                                       
         CH    R2,=H'133'                                                       
         BNH   *+8                                                              
         LA    R2,133                                                           
         BCTR  R2,0                                                             
         EX    R2,*+8                                                           
         B     *+10                                                             
         MVC   P(0),0(R4)                                                       
*                                                                               
         LA    R2,1(R2)                                                         
         GOTO1 HEXOUT,DMCB,0(R4),P4,(R2),=C'N'                                  
*                                                                               
         LR    R0,R2                                                            
         LA    R3,P2                                                            
         LA    R2,P4                                                            
*                                                                               
EDTR5    DS    0H                                                               
         MVC   0(1,R3),0(R2)                                                    
         MVC   132(1,R3),1(R2)                                                  
         LA    R3,1(R3)                                                         
         LA    R2,2(R2)                                                         
         BCT   R0,EDTR5                                                         
*                                                                               
         MVC   P4,SPACES                                                        
         MVC   P5,SPACES                                                        
*                                                                               
         MVI   SPACING,2                                                        
         GOTO1 APRNT                                                            
         B     EDTR4                                                            
*                                                                               
EDTRX    DS    0H                                                               
         B     EXIT                                                             
*                                                                               
**********************************************************************          
*        CLEAR RECORD AREAS                                                     
**********************************************************************          
*                                                                               
CLRBREC  NTR1                                                                   
         LA    R0,BREC             SET BUFFER REC TO SPACES                     
         LA    R1,L'BREC                                                        
         SR    RE,RE                                                            
         IC    RF,=X'40'                                                        
         SLL   RF,24                                                            
         MVCL  R0,RE                                                            
         B     EXIT                                                             
*                                                                               
CLRFREC  NTR1                                                                   
         LA    R0,FREC+4           SET FILE REC TO SPACES                       
         LA    R1,L'FREC-4                                                      
         SR    RE,RE                                                            
         IC    RF,=X'40'                                                        
         SLL   RF,24                                                            
         MVCL  R0,RE                                                            
         XC    FREC(4),FREC                                                     
         B     EXIT                                                             
         EJECT                                                                  
**********************************************************************          
*        SETGN - SET GROSS/NET CONTROL                                          
**********************************************************************          
*                                                                               
SETGN    DS    0H                                                               
         MVI   GNCTL,0                                                          
         CLI   FORMAT,C'Y'          GROSS                                       
         BE    SETGN5                                                           
         CLI   FORMAT+3,C'Y'        OR GROSS-CD                                 
         BE    SETGN5                                                           
         B     SETGN8                                                           
*                                                                               
SETGN5   OI    GNCTL,X'80'         GROSS SHOWN                                  
*                                                                               
SETGN8   CLI   FORMAT+1,C'Y'       NET                                          
         BE    SETGN10                                                          
         CLI   FORMAT+4,C'Y'       OR NET-CD                                    
         BE    SETGN10                                                          
         B     SETGNX                                                           
*                                                                               
SETGN10  OI    GNCTL,X'40'        NET SHOWN                                     
*                                                                               
SETGNX   DS    0H                                                               
         BR    RE                                                               
         EJECT                                                                  
***********************************************************************         
*        BLDREC -  ADD DATA ITEM TO RECORD                                      
*                  P1 POINTS TO ITEM, BYTE 0 = LENGTH                           
***********************************************************************         
         SPACE 2                                                                
BLDREC   NTR1                                                                   
         SR    R2,R2                                                            
         ICM   R2,7,1(R1)          A(DATA ITEM)                                 
         SR    RF,RF                                                            
         ICM   RF,1,0(R1)          LENGTH                                       
         BNZ   BR02                                                             
*                                                                               
         CLI   OUTMODE,C'W'        VARIABLE LENGTH FLDS FOR WKR FILES           
         BE    BR03                                                             
*                                  ZERO LENGTH GIVEN                            
         CLI   PROFB1A+5,C'F'      FIXED FIELD FORMAT                           
         BNE   BR03                NO, OK                                       
*                                                                               
         LA    RF,1                ASSUME 1                                     
         LA    R2,SPACES           SPACE                                        
         B     BR03                                                             
*                                                                               
BR02     DS    0H                  NON-ZERO LENGTH                              
         CLI   OUTMODE,C'W'        VARIABLE LENGTH FLDS FOR WKR FILES           
         BE    BR02B                                                            
*                                                                               
         CLI   PROFB1A+5,C'F'      FIXED FIELD FORMAT                           
         BE    BR03                SKIP STRIP OFF                               
*                                                                               
BR02B    DS    0H                                                               
         LA    RF,0(R2,RF)         STRIP ANY TRAILING BLANKS                    
         BCTR  RF,0                                                             
         CLI   0(RF),C' '                                                       
         BH    *+8                                                              
         BCT   RF,*-8                                                           
         SR    RF,R2                                                            
         BNM   *+10                                                             
         SR    RF,RF                                                            
         B     BR03                                                             
         LA    RF,1(RF)                                                         
*                                                                               
BR03     DS    0H                                                               
         L     R3,ANXTPOS          WHERE TO PUT                                 
         LA    R4,1(RF,R3)                                                      
         C     R4,AFRECX                                                        
         BL    BR04                                                             
         DC    H'0'                REC OVERFLOW                                 
*                                                                               
BR04     DS    0H                                                               
         LTR   RF,RF                                                            
         BNP   BR06                                                             
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R3),0(R2)                                                    
         LA    R3,1(R3,RF)                                                      
*                                                                               
BR06     DS    0H                                                               
         CLI   OUTMODE,C'W'        VARIABLE LENGTH FLDS FOR WKR FILES           
         BE    BR06B               NO FIELD DELIMITER                           
         CLI   PROFB1A+5,C'F'      IF FIXED FIELD FORMAT                        
         BE    BR07                NO FIELD DELIMITER                           
*                                                                               
BR06B    DS    0H                                                               
         MVI   0(R3),FDELIM                                                     
         LA    R3,1(R3)                                                         
*                                                                               
BR07     DS    0H                                                               
         ST    R3,ANXTPOS                                                       
*                                                                               
BRX      DS    0H                                                               
         XIT1                                                                   
         EJECT                                                                  
EDIOUT   DCB   DDNAME=EDIOUT,                                          X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               MACRF=PM                                                         
*                                                                               
EDITRC   DCB   DDNAME=EDIOUT,                                          X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               MACRF=GM,                                               X        
               EODAD=EDTRCEOF                                                   
*                                                                               
FRSTSW   DC    X'00'                                                            
SEQNO    DC    H'1'                                                             
MFNEWSW  DC    X'00'                                                            
EDNEWSW  DC    X'00'                                                            
GNCTL    DC    X'00'           GROSS/NET - X'80'=GROSS, X'40'=NET               
*                                                                               
EDMODE   DS    XL1                                                              
EDPARMS  DS    6F                                                               
ATSAROFF DS    A                                                                
TSARB    DS    XL(TSARDL)                                                       
*                                                                               
BREC     DS    XL300               TSAR BUFFER RECORD                           
FREC     DS    XL300               OUTUT FILE RECORD                            
*                                                                               
ABREC    DS    A                                                                
AFREC    DS    A                                                                
AFRECP4  DS    A                                                                
ABRECX   DS    A                                                                
AFRECX   DS    A                                                                
ANXTPOS  DS    A                                                                
*                                                                               
AWRKBUFF DS    A                                                                
WRKRIND  DS    XL44                                                             
WRKRFIL  DS    CL8                                                              
*                                                                               
         LTORG                                                                  
         SPACE 2                                                                
         DS    0D                                                               
         DC    CL8'*WKRBUF*'                                                    
WRKRBUFF DS    14336X                                                           
WRKRBEND EQU   *                                                                
         EJECT                                                                  
*                                                                               
         EJECT                                                                  
       ++INCLUDE PPREPB1WRK                                                     
*                                                                               
       ++INCLUDE DDTSARD                                                        
*                                                                               
       ++INCLUDE DMWRKFK                                                        
*                                                                               
       ++INCLUDE DMWRKFL                                                        
*                                                                               
       ++INCLUDE DMWRKFD                                                        
*                                                                               
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'031PPREPB12F 05/01/02'                                      
         END                                                                    
