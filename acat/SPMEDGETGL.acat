*          DATA SET SPMEDGETGL AT LEVEL 024 AS OF 03/04/21                      
*CATALP MEDGETGL                                                                
         TITLE 'MEDIA SUMMARY - EXTRACT GOAL DATA'                              
***********************************************************************         
* USER    JIRA       DATE                  CHANGE LOG                 *         
* ---- ----------  -------- ----------------------------------------- *         
* AKAT SPEC-52654  01/29/21 2-DECIMAL RADIO RATINGS FOR CANADA        *         
***********************************************************************         
*                                                                               
* PARAMETERS                       REGISTER USAGE                               
*  1 = A(WORKRC)                   RA-RC   A(WORKC)                             
*                                  R2      GOAL RECORD                          
*                                  R3      MEDBLOCK                             
*                                  R4      VARIABLE DSECTS                      
*                                  R5      DISPLACEMENT TO NEXT ENTRY           
*                                                                               
         PRINT NOGEN                                                            
VMDGETGL CSECT                                                                  
         NMOD1 0,MEDGETGL                                                       
         L     RA,0(R1)                                                         
         LA    RC,2048(RA)                                                      
         LA    RC,2048(RC)                                                      
         USING SPWORKD,RA,RC                                                    
         MVC   GGKEY,KEY                                                        
         L     R2,ADGOAL                                                        
         USING GOALREC,R2                                                       
         L     R3,MEDBUFF                                                       
         USING MEDBLOCK,R3                                                      
         L     R5,MEDAFRST                                                      
         MVC   MGSTART,0(R5)                                                    
         L     R5,MEDALAST                                                      
         MVC   MGEND,2(R5)                                                      
         LA    R6,MEDTOTAL                                                      
         L     R5,MEDAFRST                                                      
*                                                                               
CLRBUFF  ICM   R0,15,4(R5)         CLEAR EACH ACTIVE ENTRY                      
         BZ    CLRBUFF2                                                         
         L     R1,MEDLCHNK                                                      
         SR    RE,RE                                                            
         SR    RF,RF                                                            
         MVCL  R0,RE                                                            
*                                                                               
CLRBUFF2 LA    R5,12(R5)                                                        
         CR    R5,R6                                                            
         BNH   CLRBUFF                                                          
         EJECT                                                                  
* GET MEDBLOCK KEY INFORMATION                                                  
*                                                                               
         MVC   SVADBUY,ADBUY                                                    
         ST    R2,ADBUY                                                         
         GOTO1 MEDEQUIV,MYPARAM,(RA)                                            
         MVC   ADBUY,SVADBUY                                                    
         MVC   MGEQU,MEDEQFAC                                                   
         OC    MGEQU,MGEQU                                                      
         BNZ   *+10                                                             
         MVC   MGEQU(2),=H'1000'                                                
         MVI   MEDPRIMY,0                                                       
         MVI   MGSECOND,0                                                       
*                                                                               
         MVI   TWODEC,C'N'                                                      
         NI    RQOPTS,X'FF'-RQOPTS_2DEC-RQOPTS_2DECIMP                          
         TM    RQOPTS,RQOPTS_1DEC  REPORT REQUIRE 1 DEC?                        
         BO    GETGLF6                                                          
*                                                                               
         L     RF,ADAGY            A(AGENCY RECORD)                             
         USING AGYHDRD,RF          AGENCY RECORD DSECT                          
         CLI   AGYPROF+7,C'C'      CANADIAN AGENCY?                             
         BE    GETGLF1             YES - THEY GET 2 DEC RADIO RATINGS           
         DROP  RF                  DROP AGENCY RECORD USING                     
*                                                                               
         CLI   QMED,C'R'                                                        
         BE    GETGLF6                                                          
                                                                                
* TEST FOR CHANGE OF 00 PROFILE KEY AND READ PROFILE IF NEEDED                  
* READ AGENCY LEVEL PROFILE AND NO-USERID PROFILES                              
                                                                                
GETGLF1  LA    R4,MYBIG            WORK AREA                                    
         XC    0(64,R4),0(R4)                                                   
         MVC   0(4,R4),=C'S000'                                                 
         MVC   4(2,R4),QAGY                                                     
         GOTO1 GETPROF,MYPARAM,(X'90',(R4)),32(R4),DATAMGR                      
*                                                                               
         CLI   32+9(R4),C'Y'      TEST 2-DEC RTGS ACTIVE                        
         BNE   GETGLF1A                                                         
         OI    RQOPTS,RQOPTS_2DEC  TURN REPORT OPTION ON                        
*                                                                               
GETGLF1A MVC   0(4,R4),=C'S00A'                                                 
         NI    0(R4),X'BF'         MAKE S LOWERCASE                             
         GOTO1 GETPROF,(R1)                                                     
*                                                                               
         NI    RQOPTS,X'FF'-RQOPTS_2DECIMP                                      
         CLI   32+6(R4),C'Y'      TEST 2-DEC IMPS ACTIVE                        
         BNE   GETGLF1B                                                         
         CLI   QMED,C'R'                                                        
         JE    GETGLF1B                                                         
         OI    RQOPTS,RQOPTS_2DEC+RQOPTS_2DECIMP                                
                                                                                
* NOW SEE IF WE NEED TWO DECIMALS FOR THIS TARGET DEMO                          
                                                                                
GETGLF1B SR    RE,RE                                                            
         IC    RE,MEDBRAND                                                      
         CLI   MEDBRAND,X'FF'                                                   
         BNE   *+8                                                              
         LA    RE,220                                                           
         BCTR  RE,0                                                             
         MH    RE,PRDBUFLN                                                      
         A     RE,PRDBUFF          KEY INTO BRAND BUFFER                        
         LA    RE,28(RE)                                                        
*                                                                               
         CLI   2(RE),0             TEST NON-T DEMO                              
         JE    GETGLF2             NO                                           
*                                                                               
         CLI   1(RE),C'R'          CHECK DEMO MODIFIER FOR AN R                 
         BE    GETGLF2X                                                         
         CLI   1(RE),C'E'                                                       
         BE    GETGLF2X                                                         
         J     GETGLF4                                                          
*                                                                               
GETGLF2  LLC   R0,1(RE)            GET NONT DEMO INDEX                          
         BCTR  R0,0                                                             
         SLL   R0,3                X 8                                          
         L     RE,VNONTNMS                                                      
         AR    RE,R0                                                            
         CLI   0(RE),C'R'                                                       
         JE    GETGLF2X                                                         
         CLI   0(RE),C'E'                                                       
         JE    GETGLF2X                                                         
         J     GETGLF4                                                          
*                                                                               
GETGLF2X TM    RQOPTS,RQOPTS_2DEC  TEST 2-DEC RATINGS                           
         JZ    *+8                                                              
         MVI   TWODEC,C'Y'                                                      
         J     GETGLF6                                                          
*                                                                               
GETGLF4  TM    RQOPTS,RQOPTS_2DECIMP  TEST 2-DEC IMPS                           
         JZ    *+8                                                              
         MVI   TWODEC,C'Y'                                                      
         J     GETGLF6                                                          
*                                                                               
GETGLF6  DS    0H                                                               
         CLI   QFILTER,C'F'        TEST FILM TYPE FILTERING                     
         BNE   GETGLFX                                                          
         L     RE,CMLPTR           GET POINTER TO FILM TYPE/CML LIST            
         CLC   0(1,RE),GDCATCC     COMPARE TO GOAL REC TYPE                     
         BNE   EXIT                                                             
*                                                                               
GETGLFX  LA    RE,GDELEM           SET ELEMENT ADDRESS                          
         CLI   MODE,PROCNGOL       TEST NEW GOAL RECORD                         
         BNE   *+8                                                              
         LA    RE,GOALREC+(GXDATA-GXKEY)                                        
         DROP  R2                                                               
*                                                                               
         MVI   MGBYTE,X'21'                                                     
         CLI   RQLKGLS,C'Y'        TEST REPORT LOCKED GOALS                     
         BNE   *+8                                                              
         MVI   MGBYTE,X'A1'                                                     
*                                                                               
         USING GLEMENT,R2                                                       
         LR    R2,RE                                                            
         L     R6,MEDAFRST                                                      
         L     R4,4(R6)            SET BUFFER ADDRESS                           
         USING MEDGLD,R4                                                        
GETGL2   CLI   GLCODE,0            END                                          
         BE    DOTOTAL                                                          
         CLC   GLCODE,MGBYTE       CORRECT ELEMENT                              
         BE    GETGL6                                                           
GETGL4   SR    RE,RE                                                            
         IC    RE,GLEN                                                          
         AR    R2,RE                                                            
         B     GETGL2                                                           
                                                                                
* CHECK IF WITHIN BEFORE PERIOD                                                 
                                                                                
GETGL6   CLI   MEDNYBEF,C'Y'                                                    
         BNE   GETGL8                                                           
         CLC   2(2,R6),MEDBFORE+2                                               
         BNL   GETGL8                                                           
         CLC   MEDBFORE(2),2(R6)                                                
         BH    GETGL4                                                           
         LA    R6,MEDBFORE                                                      
         B     GETGL20                                                          
*                                                                               
GETGL8   CLC   GLWEEK,MGSTART      CHECK START                                  
         BL    GETGL4                                                           
         L     R6,MEDAFRST         RESET TO START OF LIST                       
                                                                                
* CHECK IF WITHIN AFTER PERIOD                                                  
                                                                                
         CLI   MEDNYAFT,C'Y'                                                    
         BNE   GETGL10                                                          
         CLC   GLWEEK,MEDAFTER                                                  
         BL    GETGL10                                                          
         CLC   GLWEEK,MEDAFTER+2                                                
         BH    GETGL4                                                           
         LA    R6,MEDAFTER                                                      
         B     GETGL20                                                          
*                                                                               
GETGL10  CLC   GLWEEK,MGEND        CHECK END DATE                               
         BH    GETGL4                                                           
*                                                                               
GETGL12  CLC   GLWEEK,2(R6)        FIND PROPER SLOT                             
         BL    GETGL20                                                          
         BE    GETGL20                                                          
*                                                                               
GETGL14  LA    R6,12(R6)                                                        
         CLI   0(R6),0                                                          
         BE    GETGL14                                                          
         B     GETGL12                                                          
*                                                                               
GETGL20  L     R4,4(R6)            SET SLOT                                     
*                                                                               
         L     R1,GLGRP            GET POINTS FROM ELEM (DFLT 1-DEC)            
         N     R1,=X'3FFFFFFF'                                                  
         CLI   TWODEC,C'Y'         TEST REPORTING 2-DEC                         
         JE    GETGL21             YES                                          
* REPORTING 1-DEC                                                               
         TM    GLGRP,GLGRP2DEC     TEST ELEM IS 2-DEC                           
         JZ    *+8                 NO - DONE                                    
         BAS   RE,GETROUND                                                      
         ST    R1,MGGRP                                                         
*                                                                               
         L     R1,GLGRP2                                                        
         TM    GLGRP2,GLGRP2DEC   TEST ELEM IS 2-DEC                            
         JZ    *+8                                                              
         BAS   RE,GETROUND                                                      
         ST    R1,MGGRP2                                                        
         J     GETGL22                                                          
* REPORTING 2-DEC                                                               
GETGL21  L     R1,GLGRP                                                         
         N     R1,=X'3FFFFFFF'                                                  
         TM    GLGRP,GLGRP2DEC     TEST ELEM IS 2-DEC                           
         JO    *+8                                                              
         MHI   R1,10                                                            
         ST    R1,MGGRP                                                         
*                                                                               
         L     R1,GLGRP2                                                        
         N     R1,=X'3FFFFFFF'                                                  
         TM    GLGRP2,GLGRP2DEC    TEST ELEM IS 2-DEC                           
         JO    *+8                                                              
         ST    R1,MGGRP2                                                        
         J     GETGL22                                                          
*                                                                               
GETROUND N     R1,=X'3FFFFFFF'     DROP 2-DEC FLAG                              
         M     R0,=F'2'            ROUND TO 1-DEC                               
         AHI   R1,1                                                             
         D     R0,=F'10'                                                        
         AHI   R1,1                                                             
         SRL   R1,1                                                             
         BR    RE                                                               
*                                                                               
GETGL22  MVC   MGBUDGET,GLBUDGET                                                
         TM    RCOPTS,RCOPT_NOCOST TEST SUPPRESS COSTS                          
         BZ    GETGL24                                                          
         XC    MGBUDGET,MGBUDGET                                                
         B     GETGL26                                                          
*                                                                               
GETGL24  OC    MGGRP,MGGRP         CHECK FOR CPP GUIDE REQUIRED                 
         BZ    GETCPP                                                           
         OC    MGBUDGET,MGBUDGET                                                
         BZ    GETCPP                                                           
*                                                                               
GETGL26  L     RF,MEDGL1                                                        
         A     RF,MGGRP                                                         
         ST    RF,MEDGL1                                                        
*                                                                               
         L     RF,MGGRP                                                         
         BAS   R9,EQUIVG2                                                       
*                                                                               
         A     RF,MEDGL1EQ                                                      
         ST    RF,MEDGL1EQ                                                      
*                                                                               
         CLI   GLEN,12             SECONDARY GOAL                               
         BE    GETGL30                                                          
*                                                                               
         CLC   MGSECOND,MGGRP2                                                  
         BNE   GETGL30                                                          
*                                                                               
         L     RF,MEDGL2                                                        
         A     RF,MGGRP2                                                        
         ST    RF,MEDGL2                                                        
*                                                                               
         L     RF,MGGRP2                                                        
         BAS   R9,EQUIVG2                                                       
         A     RF,MEDGL2EQ                                                      
         ST    RF,MEDGL2EQ                                                      
*                                                                               
GETGL30  MVC   MYFULL,MGBUDGET     GOAL DOLLARS                                 
*                                                                               
         L     RE,ADEST                                                         
         USING ESTHDRD,RE                                                       
*                                                                               
         OC    ECOST2,ECOST2       TEST COS2 ESTIMATE                           
         BZ    GETGL31                                                          
         CLI   MEDEXTPW,C'Y'       TEST REQUEST FOR CLT DOLLARS                 
         BE    GETGL32             YES - DO NOT ADJUST                          
* ADJUST GOAL DOLLARS BY COS2 FACTOR                                            
         ICM   R0,15,MYFULL                                                     
         CVD   R0,MYDUB            MAKE AMOUNT PACKED                           
         ZAP   MYBIG(16),MYDUB                                                  
         SRP   MYBIG(16),6,0       SCALE UP BY 10**6                            
*                                                                               
         ICM   R0,15,ECOST2        GET COS2 FACTOR                              
         CVD   R0,MYDUB            MAKE IT PACKED                               
         DP    MYBIG(16),MYDUB     GOAL DOLS/FACTOR                             
*                                                                               
         ZAP   MYDUB,MYBIG(8)      GET QUOTIENT                                 
         CVB   R0,MYDUB                                                         
         ST    R0,MYFULL                                                        
         B     GETGL32                                                          
*                                                                               
GETGL31  OC    EPWPCT,EPWPCT       TEST PW ESTIMATE                             
         BZ    GETGL32                                                          
         CLI   MEDEXTPW,C'Y'       TEST REQUEST FOR CLT DOLLARS                 
         BE    GETGL32             YES - DO NOT ADJUST                          
         BAS   RE,GETPW                                                         
*                                                                               
GETGL32  L     RE,ADBUY            GET GOALREC ADDRESS                          
         CLI   MODE,PROCNGOL       TEST NEW GOAL RECORD                         
         BE    GETGL40             (NO PIGGYBACKS FOR NETWORK)                  
         CLI   GKEYPRD2-GKEY(RE),0 TEST PIGGYBACK                               
         BE    GETGL40             NO                                           
         TM    GKEYAGY-GKEY(RE),X'40'   TEST PRD2 = SUBREF                      
         BO    GETGL40                                                          
         CLI   MEDNOPIG,C'Y'       TEST IGNORE PIGGYBACK SPLITS                 
         BE    GETGL40                                                          
                                                                                
* NEED TO APPORTION DOLLARS BETWEEN BRANDS *                                    
                                                                                
         ZIC   R1,GKEYSLN-GKEY+GGKEY GET BRAND LEN                              
         AR    R1,R1               X 2                                          
         M     R0,MYFULL                                                        
         ZIC   RE,GKEYSEC-GKEY+GGKEY GET TOTAL LEN                              
         DR    R0,RE                                                            
         AHI   R1,1                                                             
         SRL   R1,1                                                             
         ST    R1,MYFULL                                                        
*                                                                               
GETGL40  L     RF,MEDGLD                                                        
         A     RF,MYFULL                                                        
         ST    RF,MEDGLD                                                        
*                                                                               
         L     RF,MYFULL                                                        
         BAS   R9,EQUIVG                                                        
         A     RF,MEDGLDEQ                                                      
         ST    RF,MEDGLDEQ                                                      
*                                                                               
         LA    RE,MEDBFORE         SET DATE POINTER                             
         CR    R6,RE                                                            
         BNE   *+8                                                              
         L     R6,MEDAFRST                                                      
         LA    RE,MEDAFTER                                                      
         CR    R6,RE                                                            
         BNE   *+8                                                              
         L     R6,MEDAFRST                                                      
         B     GETGL4                                                           
         SPACE 2                                                                
EQUIVG   M     RE,=F'2000'         X 1000 X 2                                   
         LH    R1,MGEQU                                                         
         DR    RE,R1                                                            
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         BR    R9                                                               
         SPACE 2                                                                
EQUIVG2  LH    R1,MGEQU                                                         
         MR    RE,R1                                                            
         SLDA  RE,1                                                             
         D     RE,=F'1000'                                                      
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         BR    R9                                                               
         EJECT                                                                  
GETCPP   CLI   MODE,PROCNGOL       NO CPP IF NEW GOAL RECORD                    
         BE    GETGL26                                                          
*                                                                               
         L     RF,ADGOAL           GET CPP GUIDE                                
         MVC   KEY(13),0(RF)       BUILD KEY                                    
         MVI   KEY+4,X'FF'                                                      
         XC    KEY+9(4),KEY+9                                                   
         CLC   KEY(9),GLIO         DO WE HAVE IT ALREADY                        
         BE    GETCPP2              YES - TEST HAVE CPP EQ FACTOR               
         L     RE,ADCLT                                                         
         USING CLTHDRD,RE                                                       
         CLI   CPROF+8,C'0'                                                     
         BH    GETCPPX                                                          
         DROP  RE                                                               
         SPACE 1                                                                
         MVI   CPPEQLEN,0          FORCE NEW CPP EQ FACTOR                      
         L     RE,ADGOAL                                                        
         LA    RE,24(RE)                                                        
         USING GDELEM,RE                                                        
         CLI   GDCPPES,0           TEST NEW CPP DATA                            
         BNE   GETCPP10                                                         
         DROP  RE                                                               
*                                                                               
         LA    RF,GLIO                                                          
         ST    RF,AREC                                                          
         GOTO1 HIGH                                                             
         CLC   KEY(9),KEYSAVE      CPP GUIDE FOUND                              
         BNE   GETCPPX              NO - LEAVE VALUES AT ZERO                   
*                                                                               
         GOTO1 GET                 READ CPP GUIDE                               
         EJECT                                                                  
*                                                                               
         SPACE 1                                                                
* GET CPP GUIDE EQUIVALENCE FACTOR *                                            
* BASED ON TOTAL SPOT LENGTH       *                                            
         SPACE 1                                                                
GETCPP2  CLC   CPPEQLEN,GGKEY+GKEYSEC-GKEY  TEST RIGHT CPP EQ FACTOR            
         BE    GETCPP2A                                                         
         MVC   CPPEQLEN,GGKEY+GKEYSEC-GKEY  GET TOTAL LENGTH                    
         MVC   SVEQFAC,MEDEQFAC             SAVE GOAL VALUES                    
         MVC   SVSPTLN,MEDSPTLN                                                 
         MVC   MEDSPTLN,CPPEQLEN            SET REQUIRED LEN                    
         SPACE 1                                                                
         GOTO1 MEDEQUIV,MYPARAM,(RA)        GET CPP EQUIV FACTOR                
         SPACE 1                                                                
         MVC   CPPEQFAC,MEDEQFAC                                                
         OC    CPPEQFAC,CPPEQFAC            HANDLE INVALID LENGTHS              
         BNZ   *+10                                                             
         MVC   CPPEQFAC(2),=H'1000'                                             
         MVC   MEDEQFAC,SVEQFAC             RESTORE GOAL VALUES                 
         MVC   MEDSPTLN,SVSPTLN                                                 
*                                                                               
GETCPP2A LA    RF,GLIO             SET CPP GUIDE ADDRESS                        
*                                                                               
         STM   R2,R9,SAVE29                                                     
         MVC   CURRDTE,GLWEEK                                                   
         LA    R2,GLIO+24                                                       
         SR    RF,RF                                                            
*                                                                               
GETCPP3  CLI   GLCODE,0                                                         
         BE    GETCPP5A            END                                          
         CLI   GLCODE,X'21'                                                     
         BNE   GETCPP4             CORRECT ELEMENT                              
         CLC   GLWEEK,CURRDTE      MATCH AGAINST GOAL DATE                      
         BNL   GETCPP5                                                          
         LR    RF,R2               SAVE CURRENT ELEMENT                         
*                                                                               
GETCPP4  SR    RE,RE               NEXT ELEMENT                                 
         IC    RE,GLEN                                                          
         AR    R2,RE                                                            
         B     GETCPP3                                                          
*                                                                               
GETCPP5  CLC   CURRDTE,GLWEEK      SAME START DATE IS OK                        
         BE    *+12                                                             
GETCPP5A LTR   RF,RF               NO PREV = NO GUIDE                           
         BZ    GETCPP7                                                          
         LR    R2,RF           OTHERWISE INFER THAT PREV DATE APPLIES           
*                                                                               
         MVC   MYFULL,GLBUDGET                                                  
         SR    RE,RE                                                            
         L     RF,MYFULL                                                        
         SLDA  RE,1                                                             
         D     RE,=F'100'                                                       
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
         ST    RF,MYFULL                                                        
*                                                                               
         OC    MGGRP,MGGRP         HAVE DEMOS FROM GOAL RECORD                  
         BNZ   GETCPP6              YES - GET DOLLARS                           
*                                                                               
* CALCULATE POINTS  (DOLLARS/CPP)                                               
         L     RF,MGBUDGET                                                      
         SR    RE,RE                                                            
         M     RE,=F'10'                                                        
         SLDA  RE,1                                                             
         D     RE,MYFULL                                                        
         A     RF,=F'1'                                                         
         SRA   RF,1                HAVE POINTS                                  
         SR    RE,RE               NOW EQUIVALENCE POINTS                       
         M     RE,=F'1000'                                                      
         SLDA  RE,1                                                             
         LH    R1,CPPEQFAC                                                      
         DR    RE,R1                                                            
         A     RF,=F'1'                                                         
         SRA   RF,1                                                             
         ST    RF,MGGRP                                                         
         B     GETCPP7                                                          
                                                                                
* CALCULATE DOLLARS (POINTS X CPP)                                              
                                                                                
GETCPP6  SR    RE,RE                                                            
         L     RF,MGGRP                                                         
         M     RE,MYFULL                                                        
         SR    RE,RE                                                            
         SLDA  RE,1                                                             
         LHI   R0,10               SET TO SCALE POINTS                          
         CLI   TWODEC,C'Y'                                                      
         BNE   *+8                                                              
         LHI   R0,100                                                           
         DR    RE,R0                                                            
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         LH    R1,CPPEQFAC                                                      
         MR    RE,R1                                                            
         SLDA  RE,1                EQUIVALENCE                                  
         D     RE,=F'1000'                                                      
         AHI   RF,1                                                             
         SRL   RF,1                                                             
         ST    RF,MGBUDGET                                                      
*                                                                               
GETCPP7  LM    R2,R9,SAVE29                                                     
GETCPPX  DC    0H'0'                                                            
         MVC   KEY(13),GGKEY       RESTORE GOAL KEY                             
         GOTO1 HIGH                RE-ESTABLISH SEQ                             
         B     GETGL26                                                          
         SPACE 2                                                                
* READ ONE OR 2 CPP GUIDES INTO GLIO                                            
*                                                                               
GETCPP10 STM   R2,R9,SAVE29                                                     
         LA    RF,GLIO                                                          
         ST    RF,AREC                                                          
         L     R2,ADGOAL                                                        
         USING GOALREC,R2                                                       
         MVC   KEY+2(2),GDCPPCL                                                 
         MVC   KEY+7(1),GDCPPES                                                 
         GOTO1 HIGH                                                             
         LA    R2,GLIO+24                                                       
         CLC   KEY(9),KEYSAVE                                                   
         BE    *+8                                                              
         B     GETCPP20                                                         
         L     R2,ADGOAL                                                        
*                                                                               
         GOTO1 GET                                                              
*                                                                               
         CLI   GDCPPES2,0          CHECK FOR SECOND CPP EST                     
         BE    GETCPP2X             NO - DONE                                   
*                                                                               
         MVC   KEY+7(1),GDCPPES2                                                
         GOTO1 HIGH                                                             
         XC    GLIO+1024(10),GLIO+1024                                          
         CLC   KEY(9),KEYSAVE                                                   
         BNE   GETCPP11                                                         
         LA    RF,GLIO+1000                                                     
         ST    RF,AREC                                                          
         GOTO1 GET                                                              
* REMOVE KEY AND UNNEEDED ELEMENTS                                              
GETCPP11 LA    R2,GLIO+24                                                       
         SR    R0,R0                                                            
GETCPP12 CLI   0(R2),0             FIND END OF FIRST CPP GUIDE                  
         BE    GETCPP14                                                         
         IC    R0,1(R2)                                                         
         AR    R2,R0                                                            
         B     GETCPP12                                                         
GETCPP14 LA    RE,GLIO+1024                                                     
         SR    R0,R0                                                            
GETCPP16 CLI   0(RE),0                                                          
         BE    GETCPP20                                                         
         CLI   0(RE),X'21'                                                      
         BNE   GETCPP18                                                         
         SR    RF,RF                                                            
         IC    RF,1(RE)                                                         
         BCTR  RF,0                                                             
         EX    RF,*+8                                                           
         B     *+10                                                             
         MVC   0(0,R2),0(RE)  *EXECUTED*                                        
         LA    R2,1(RF,R2)                                                      
GETCPP18 IC    R0,1(RE)                                                         
         AR    RE,R0                                                            
         B     GETCPP16                                                         
*                                                                               
GETCPP20 XC    0(4,R2),0(R2)                                                    
GETCPP2X LA    RE,GLIO                                                          
         ST    RE,AREC                                                          
         LM    R2,R9,SAVE29                                                     
         B     GETCPP2                                                          
         EJECT                                                                  
*                                                                               
* DO TOTALS                                                                     
DOTOTAL  L     R6,MEDAFRST                                                      
         LA    RE,MEDTOTAL                                                      
         ST    RE,MYDUB                                                         
DOTOTAL1 C     R6,MYDUB                                                         
         BE    EXIT                                                             
         L     RE,4(R6)                                                         
         L     RF,8(R6)                                                         
         LTR   RF,RF                                                            
         BZ    DOTOTAL5                                                         
         LA    R4,6                                                             
DOTOTAL3 L     R7,0(RE)                                                         
         A     R7,0(RF)                                                         
         ST    R7,0(RF)                                                         
         LA    RE,4(RE)                                                         
         LA    RF,4(RF)                                                         
         BCT   R4,DOTOTAL3                                                      
DOTOTAL5 LA    R6,12(R6)                                                        
         C     R6,MYDUB                                                         
         BNL   EXIT                                                             
         CLI   0(R6),0                                                          
         BE    DOTOTAL5                                                         
         B     DOTOTAL1                                                         
*                                                                               
EXIT     MVC   AREC,ADGOAL                                                      
         XIT1                                                                   
         EJECT                                                                  
*===============================================================*               
* SUBROUTINE TO LOOK UP PW PERCENTAGE AND RETURN WIM GOAL       *               
* ON ENTRY R2 POINTS TO GOAL ELEMENT                            *               
* NOTE THAT GOALS ARE ALWAYS ADJUSTED BY ESTIMATED PW PERCENT   *               
* -- NOT THE WEEKLY PERCENTS IN THE MARKET LEVEL PW RECORDS     *               
* UNFORTUNATELY HAVE TO READ THE PW RECORD ANYWAY TO GET THE    *               
* ESTIMATED TAX RATE                                            *               
*===============================================================*               
         SPACE 1                                                                
GETPW    NTR1                                                                   
         L     RE,ADCONLST         EXTRACT PWREC ADDRESS FROM                   
         USING SPADCONS,RE         EXTENDED ADCONS                              
         L     R4,ADPWREC                                                       
         DROP  RE                                                               
*                                                                               
         XC    PWKEY,PWKEY                                                      
         LA    R5,PWKEY                                                         
         USING PWRECD,R5                                                        
*                                                                               
         L     R2,ADGOAL                                                        
         USING GOALREC,R2                                                       
         MVC   PWKTYP,=X'0D7A'                                                  
         MVC   PWKAGMD(3),GKEYAM   AGMD/CLT                                     
         MVC   PWKPRD,GKEYPRD      PRD                                          
         MVC   PWKEST,GKEYEST      ESTIMATE                                     
         MVC   PWKMKT,GKEYMKT      MARKET                                       
         DROP  R2,R5                                                            
*                                                                               
         CLC   0(13,R4),PWKEY      TEST SAME AS PREVIOUS                        
         BE    GETPW10             YES - SKIP READ                              
*                                                                               
         XC    0(256,R4),0(R4)     CLEAR PW RECORD AREA                         
         MVC   0(13,R4),PWKEY      SAVE CURRENT KEY                             
         GOTO1 DATAMGR,DMCB,DMRDHI,SPTDIR,(R4),PWKEY                            
         CLC   0(13,R4),PWKEY      NO PW REC                                    
         BNE   GETPW10                                                          
*                                                                               
         GOTO1 DATAMGR,DMCB,GETREC,SPTFILE,PWKEY+14,(R4),DMWORK                 
         CLI   8(R1),0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
GETPW10  L     RE,ADEST                                                         
         USING ESTHDRD,RE                                                       
*                                                                               
         SR    R7,R7                                                            
         ICM   R7,7,EPWPCT         GET DEFAULT PERCENTAGE                       
         N     R7,=X'007FFFFF'     IAAFI                                        
         DROP  RE                                                               
*                                                                               
GETPW22  BC    0,GETPW24                                                        
         MVI   *-3,X'F0'                                                        
         GOTO1 LOADER,DMCB,=CL8'T00A79',0                                       
         ICM   RF,15,4(R1)         GET LOAD ADDRESS                             
         BNZ   *+6                                                              
         DC    H'0'                                                             
         ST    RF,APWCALC                                                       
*                                                                               
GETPW24  DS    0H                                                               
         XC    PWBLOCK,PWBLOCK                                                  
         LA    R2,PWBLOCK                                                       
         USING PWBLKD,R2                                                        
*                                                                               
         MVI   PWACT,PWGETGOL                                                   
         MVC   PWACTGOL,MYFULL                                                  
         ST    R7,PWPCT                                                         
*                                                                               
         USING PWRECD,R4                                                        
         MVC   PWTAXRT+2(2),PWGNTAX       PASS TAX RATE                         
         DROP  R4                                                               
*                                                                               
         GOTO1 APWCALC,DMCB,(R2)                                                
         CLI   PWERR,0                                                          
         BE    *+6                                                              
         DC    H'0'                                                             
         MVC   MYFULL,PWVAL        CHANGE THE GROSS DOLLARS                     
*                                                                               
         MVC   KEY(13),GGKEY       RESTORE GOAL KEY                             
         GOTO1 HIGH                RE-ESTABLISH SEQ                             
*                                                                               
GETPWX   XIT1                                                                   
*                                                                               
APWCALC  DS    A                                                                
         DS    0D                                                               
         DC    CL8'*PWBLOCK'                                                    
PWBLOCK  DS    XL48                                                             
         DS    0D                                                               
         DC    CL8'**PWKEY*'                                                    
PWKEY    DS    XL18                                                             
         XMOD1 1                                                                
         LTORG                                                                  
         EJECT                                                                  
MYDUB    DS    D                                                                
MYBIG    DS    PL16                                                             
         DS    XL48                MORE SPACE TO READ PROFILES                  
MYPARAM  DS    16F                                                              
*                                                                               
MYFULL   DS    F                                                                
CURRDTE  DS    H                                                                
MGSTART  DS    H                                                                
MGEND    DS    H                                                                
CPPEQFAC DS    H                 CPP RECORD EQUIV. FACTOR (TOT LENGTH)          
MGEQU    DS    H                 GOAL RECORD EQUIV. FACTOR (MEDSPTLN)           
MGHALF   DS    H                                                                
MGGRP    DS    F                                                                
MGGRP2   DS    F                                                                
MGBUDGET DS    F                                                                
SAVE29   DS    8F                                                               
SVADBUY  DS    F                                                                
SVEQFAC  DS    H                                                                
TWODEC   DS    C                                                                
SVSPTLN  DS    C                                                                
MGSECOND DS    C                                                                
CPPEQLEN DS    C                                                                
MGBYTE   DS    C                                                                
GGKEY    DS    XL(L'GXKEY)                                                      
         DS    0D                                                               
GLIO     DS    2000C                                                            
         PRINT OFF                                                              
         EJECT                                                                  
* SPMEDBLOCK                                                                    
       ++INCLUDE SPMEDBLOCK                                                     
         EJECT                                                                  
* SPREPWORKD                                                                    
       ++INCLUDE SPREPWORKD                                                     
* SPREPMODES                                                                    
       ++INCLUDE SPREPMODES                                                     
         EJECT                                                                  
* SPGENGOAL                                                                     
       ++INCLUDE SPGENGOAL                                                      
CLTHDRD  DSECT                                                                  
       ++INCLUDE SPGENCLT                                                       
ESTHDRD  DSECT                                                                  
       ++INCLUDE SPGENEST                                                       
         EJECT                                                                  
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
         EJECT                                                                  
       ++INCLUDE SPGENWIPW                                                      
       ++INCLUDE SPPWBLOCK                                                      
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'024SPMEDGETGL03/04/21'                                      
         END                                                                    
