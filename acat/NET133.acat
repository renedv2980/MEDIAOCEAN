*          DATA SET NET133     AT LEVEL 005 AS OF 12/12/13                      
*CATALP NET133                                                                  
                                                                                
         TITLE 'NET133 - NETFILE DAILY ACTIVITY REPORT'                         
                                                                                
*****************************************************                           
*  13/10/11 NEW RECV FILE,DDSIO=,DSPACE=,UPDID=     *                           
*                                                   *                           
*  9/29/08  WRITE=NO OPENS FILE FOR NO UPDATE       *                           
*                                                   *                           
*  4/19/07  PASSES NBACTAM AND NBDM TO NEW EXTENDED *                           
*  BRNADS NETVALUE - NOT NEEDED IN OLD NETVALUE     *                           
*                                                   *                           
*  3/6/96   (PXZ)  NO-SHOW TREATED AS DELETES       *                           
*  3/6/96   (PXZ)  OFF LINE CHANGES INCLUDED IN DFA *                           
*                                                   *                           
*  STAND ALONE PROGRAM TO READ RECOVERY FILE        *                           
*  PRGRM GOES TO NETIO TO FILL IN NETBLOCK          *                           
*  AND SO NEEDS TO GET A(NETVALUE) AND V(LOADER)    *                           
*  ALSO NOTE THE DUMMY COMFACS IT MUST SET UP       *                           
*****************************************************                           
                                                                                
NET133   CSECT                                                                  
         ENTRY UTL                                                              
         ENTRY SSB                                                              
         PRINT NOGEN                                                            
         NBASE MYWRKDLE,**NTDL**,=A(WORKA),R9,CLEAR=YES                         
*                                                                               
         USING MYWORKD,RC                                                       
*                                                                               
         L     R7,=A(DBLOCK)       R7 - NETBLOCK (2000CL ALLOTTED)              
         USING NEBLOCKD,R7                                                      
                                                                                
**********************************************************************          
* SET UP ADDS FOR NETIO                                                         
**********************************************************************          
         LA    R8,IOAREA1                                                       
         USING RECD,R8                                                          
         LA    RE,28(R8)           POINT PAST RECV HEADER                       
         ST    RE,AUNITREC         UNIT RECORD POINTER                          
         MVC   NBACOM,ACOMFACS     DUMMY COMFACS LIST                           
         MVC   NBLOADER,=V(LOADER)             A(LOADER)                        
         MVC   NBDM,=V(DATAMGR)  NEEDED FOR EXTENDED BRANDS NETVALUE            
         MVC   NBHELLO,=V(HELLO)                                                
         GOTO1 NBLOADER,DMCB,=CL8'T00A28',0    GET A(NETVALUE)                  
         MVC   NBNETVAL,DMCB+4                                                  
         OC    NBNETVAL,NBNETVAL   TEST FOR LOADER ERROR                        
         BNZ   *+6                                                              
         DC    H'0'                NO-TAKE A HIT                                
*                                                                               
         L     RA,=V(CPRINT)       RA - DPRINT                                  
         USING DPRINT,RA                                                        
         EJECT ,                                                                
**********************************************************************          
* MAIN EVENT                                                                    
**********************************************************************          
         BAS   RE,INIT                                                          
*                                                                               
         XC    WORK,WORK                                                        
         CLI   DUMPOS,YES                                                       
         BE    MAIN10                                                           
         ST    RB,WORK                                                          
         L     R4,=A(WORKA)                                                     
         A     R4,=F'20000'                                                     
         ST    R4,WORK+4                                                        
         OI    WORK+4,X'80'                                                     
         GOTO1 =V(STXITER),DMCB,WORK                                            
*                                                                               
MAIN10   BAS   RE,NET1         READ RECOVERY /SET UP-PASS RECS TO SORT          
         CLOSE (RCVTAPE)                                                        
*                                                                               
         BAS   RE,NET2         GET RECS FROM SORT/  AGYHEADER-PRINT             
**********************************************************************          
* CLOSE SPOT FILE                                                               
**********************************************************************          
         GOTO1 =V(DATAMGR),DMCB,=C'DMCLSE',=C'SPOT'                             
         GOTO1 =V(SORTER),DMCB,=C'END'                                          
XIT      XBASE                                                                  
EXIT     XIT1                                                                   
         EJECT ,                                                                
***********************************************                                 
*  INITIALIZATION: GET DATES                  *                                 
*                  ZAP TOTALS FIELDS          *                                 
*                  SET UP SORTER              *                                 
*                  OPEN FILES                 *                                 
***********************************************                                 
                                                                                
INIT     NTR1                                                                   
         GOTO1 =V(DATCON),DMCB,(5,0),(3,TODAYB)                                 
         GOTO1 (RF),(R1),,(2,TODAYP2)                                           
         GOTO1 (RF),(R1),,(1,TODAYP3)                                           
         LA    R2,TOTCTR                                                        
         LA    RE,6                                                             
ZAPTOTS  ZAP   0(4,R2),=P'0'                                                    
         LA    R2,4(R2)                                                         
         BCT   RE,ZAPTOTS                                                       
**       ZAP   TOTCTR,=P'0'                                                     
**       ZAP   CHGCTR,=P'0'                                                     
**       ZAP   ADDCTR,=P'0'                                                     
**       ZAP   CPYCTR,=P'0'                                                     
**       ZAP   CPYCHCTR,=P'0'                                                   
**       ZAP   UNKCTR,=P'0'                                                     
                                                                                
**********************************************************************          
* ZAP TOTS TO PACKED FIELDS                                                     
**********************************************************************          
         MVI   ENDTOTS,X'FF'                                                    
         MVI   ENDAGYAL,X'FF'                                                   
         LA    R2,PKGTOT                                                        
INIT0    ZAP   0(L'PKGTOT,R2),=P'0'                                             
         LA    R2,8(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         BNE   INIT0                                                            
         LA    R2,ALLAGY                                                        
INIT00   ZAP   0(L'ALLAGY,R2),=P'0'                                             
         LA    R2,8(R2)                                                         
         CLI   0(R2),X'FF'                                                      
         BNE   INIT00                                                           
                                                                                
         ZAP   TOTAL,=P'0'                                                      
         MVI   WRITEFLG,C'Y'                                                    
         XC    ONEAGY,ONEAGY                                                    
***********************************************************************         
* SET UP SORTER AND PROCESS SYSIN CARDS                                         
***********************************************************************         
INIT1    XC    DMCB(12),DMCB                                                    
         GOTO1 =V(SORTER),DMCB,SORTCARD,RECCARD                                 
*                                                                               
INIT2    GOTO1 =V(CARDS),DMCB,CARD,=C'RE00'                                     
         CLC   =C'/*',CARD                                                      
         BE    INIT10                                                           
         CLI   CARD,C'*'                                                        
         BE    INIT2                                                            
*                                                                               
INIT3    CLC   =C'DSPACE=',CARD                                                 
         BNE   INIT4                                                            
         L     RE,=A(SSB)                                                       
         MVC   SSODSPAC-SSOOFF(1,RE),CARD+7                                     
         B     INIT2                                                            
*                                                                               
INIT4    CLC   =C'DDSIO=',CARD                                                  
         BNE   INIT5                                                            
         L     RE,=V(DDSIO)                                                     
         MVC   0(8,RE),CARD+6                                                   
         B     INIT2                                                            
*                                                                               
INIT5    CLC   =C'DUMP=OS',CARD                                                 
         BNE   *+12                                                             
         MVI   DUMPOS,YES  '                                                    
         B     INIT2                                                            
*                                                                               
         CLC   =C'WRITE=NO',CARD                                                
         BNE   *+12                                                             
         MVI   WRITEFLG,NO '                                                    
         B     INIT2                                                            
*                                                                               
         CLC   =C'ZERO=NO',CARD                                                 
         BNE   *+12                                                             
         OI    ZEROFLG,X'80'                                                    
         B     INIT2                                                            
*                                                                               
         CLC   =C'AGY=',CARD                                                    
         BNE   INIT6                                                            
         MVI   WRITEFLG,NO                                                      
         MVC   ONEAGY,CARD+4                                                    
         B     INIT2                                                            
*                                                                               
INIT6    CLC   =C'UPDID=',CARD     THIS ALLOWS CONC FILE UPDATE                 
         BNE   INIT7                                                            
         GOTO1 =V(DATAMGR),DMCB,=C'UPDID'                                       
         L     RF,12(R1)                                                        
         MVC   0(2,RF),CARD+6                                                   
         B     INIT2                                                            
*                                                                               
INIT7    LA    R3,CARD+4                                                        
         CLC   =C'SPOT',CARD                                                    
         BE    INIT7C                                                           
         LA    R3,CARD+3                                                        
         CLC   =C'SPT',CARD                                                     
         BNE   INIT8                                                            
*                                                                               
INIT7C   MVC   SPTTXT+4(2),0(R3)                                                
         MVC   SPTSYSC,0(R3)                                                    
         LA    R3,SPTSYS                                                        
         B     INIT9                                                            
*                                                                               
INIT8    CLC   =C'NET',CARD                                                     
         BNE   ERROR               WHAT IS IT THEN?                             
         MVC   NETTXT(5),CARD                                                   
         MVC   NETSYSC,0(R3)                                                    
         LA    R3,NETSYS                                                        
*                                                                               
INIT9    GOTO1 =V(DMDDNAME),DMCB,DDNAME,(R3),0                                  
         TM    8(R1),X'10'         SYSTEM NOT FOUND                             
         JO    *+2                                                              
         LT    RE,8(,R1)           GET A(DDNADATA)                              
         JZ    *+2                                                              
*                                                                               
         USING DDNAMED,RE                                                       
         USING UTLD,RF                                                          
         L     RF,=A(UTL)                                                       
         MVC   TSYS,DDNASENO                                                    
         MVC   SPTTXT(4),=C'SPOT'                                               
         MVC   NETTXT(3),=C'NET'                                                
         MVC   SPTTXT+4(2),DDNASEID       SPOT ID?                              
         MVC   NETTXT+3(2),DDNASYID       NET  ID?                              
         CLI   DDNASECH,C'S'                                                    
         BNE   INIT9B              DONE                                         
         CLI   DDNASYCH,C'N'                                                    
         JNE   *+2                                                              
         B     INIT2                                                            
*                                                                               
INIT9B   CLI   DDNASECH,C'N'                                                    
         JNE   *+2                 MUST BE NET SYSTEM                           
         MVC   NETTXT+3(2),DDNASEID                                             
         MVC   NETTXT+4(2),DDNASYID                                             
         B     INIT2                                                            
         DROP  RE,RF                                                            
***********************************************************************         
* OPEN FILES                                                                    
***********************************************************************         
         USING UTLD,RF                                                          
INIT10   L     RF,=A(UTL)          MUST HAVE DEFINED A SYSTEM                   
         CLI   TSYS,0                                                           
         BNE   INIT11                                                           
         MVC   P(30),=C'** ERROR * SYSTEM NOT VALID **'                         
         B     CANCEL                                                           
         DROP  RF                                                               
*                                                                               
INIT11   CLI   WRITEFLG,NO                                                      
         BNE   INIT12                                                           
         MVI   FLIST,C'N'                                                       
         MVI   FLIST+8,C'N'                                                     
*                                                                               
INIT12   GOTO1 =V(DATAMGR),DMCB,=C'DMOPEN',=C'SPOT',FLIST,REC                   
*                                                                               
         OPEN  (RCVTAPE,(INPUT))                                                
         B     EXIT                                                             
***********************************************************************         
* ERROR / CANCEL                                                                
***********************************************************************         
ERROR    MVC   P(30),=C'*** UNKNOWN PARAMETER CARD ***'                         
*                                                                               
CANCEL   GOTO1 =V(LOGIO),DMCB,1,(30,P)                                          
         GOTO1 =V(PRINTER)                                                      
         MVC   P(80),CARD                                                       
         GOTO1 =V(PRINTER)                                                      
         ABEND 999                                                              
         EJECT ,                                                                
***********************************************************************         
* CONSTANT FOR INITIALIZATION                                                   
***********************************************************************         
DDNAME   DC    CL8'DDNAME'                                                      
*                                                                               
SPTSYS   DC    C'S=S  '                                                         
         ORG   SPTSYS+3                                                         
SPTSYSC  DS    CL2                                                              
*                                                                               
NETSYS   DC    C'S=N  '                                                         
         ORG   NETSYS+3                                                         
NETSYSC  DS    CL2                                                              
*                                                                               
FLIST    DS    0H                                                               
         DC    CL8'USPTFILE'                                                    
         DC    CL8' SPTDIR '                                                    
         DC    CL8'X       '                                                    
*                                                                               
* ROUTINE FOR LOADING REC FILE ONTO DISK *                                      
*        OPEN  (RCVOUT,OUTPUT)                                                  
*GET1    LA    R1,RCVTAPE                                                       
*        LA    R0,IOAREA1                                                       
*        GET   (1),(0)                                                          
*        LA    R0,IOAREA1                                                       
*        PUT   RCVOUT,(0)                                                       
*        B     GET1                                                             
*GETX    CLOSE (RCVTAPE)                                                        
*        CLOSE (RCVOUT)                                                         
*                                                                               
*        B     EXIT                                                             
*                                                                               
SORTCARD DC    CL80'SORT FIELDS=(1,21,A),FORMAT=BI,WORK=1'                      
RECCARD  DC    CL80'RECORD TYPE=F,LENGTH=68'                                    
                                                                                
         EJECT                                                                  
********************************************************                        
* THIS ROUTINE GETS THE RECORDS FROM THE RECOVERY FILE *                        
* IT PROCESSES THEM ACCORDING TO WHETHER THEY ARE      *                        
* COPY/CHA/ADD AND THEN PASSES THEM TO NETIO TO FILL   *                        
* IN NETBLOCK.  THE PROGRAM THEN SETS UP A SORTREC     *                        
* WITH KEY AND $ FIELDS AND PASSES THIS REC TO SORTER. *                        
*                                                      *                        
*  INPUT: REC FROM RECOVERY FILE                       *                        
*         NETBLOCK FROM NETIO WITH RECOVERY FILE REC   *                        
*                                                      *                        
* OUTPUT: SORT REC PASSED TO SORTER WITH KEY + $FIELDS *                        
********************************************************                        
                                                                                
NET1     NTR1                                                                   
GET      LA    R1,RCVTAPE                                                       
         LA    R0,IOAREA1                                                       
         GET   (1),(0)                                                          
*                                                                               
         CLI   RTASKID,X'FF'       IGNORE DELETED RECORDS                       
         BE    GET                                                              
         TM    RRECTY,X'80'        IGNORE POINTER COPIES/CHANGES                
         BO    GET                                                              
*                                                                               
         CLI   RFILTY,QUNTFIL      TEST UNTFILE                                 
         BNE   GET                                                              
         CLI   RPRG,X'01'          TEST PFM                                     
         BE    GET                                                              
         L     RE,AUNITREC         POINT TO UNIT RECORD                         
         USING NURECD,RE                                                        
         CLI   NUKTYPE,X'04'       TEST UNIT RECORD                             
         BNE   GET                                                              
**************************************                                          
* BAD RECORD CATCHER                                                            
         B     SKIPTHIS                                                         
         CLC   NUKDATE,=X'C4B6'                                                 
         BNE   SKIPTHIS                                                         
         CLI   NUKEST,68                                                        
         BNE   SKIPTHIS                                                         
         CLI   NUKSUB,1                                                         
         BNE   SKIPTHIS                                                         
         CLC   =C'8HOMIC',NUKPROG                                               
         BE    GET                                                              
SKIPTHIS DS    0H                                                               
*******************************************                                     
* - TREAT DELETED LOCKED PKG AS TRUE DELETES                                    
         TM    NUPACKST,X'01'      TEST NO-SHOW DELETE                          
         BO    NET8                IF YES/TREAT AS DELETED                      
*                                                                               
* - UPDATIVE SOON MEANS WE NEED TO INCLUDE OFF-LINE CHANGES IN                  
*   DAILY ACTIVITY NO?                                                          
*****    OC    RSIN,RSIN           TEST IF ON-LINE                              
*****    BZ    GET                 IF NO/SKIP                                   
         MVI   DELETFLG,0                                                       
         TM    NURSTAT,X'80'       TEST IF DELETED                              
         BNO   *+8                                                              
NET8     MVI   DELETFLG,C'Y'                                                    
                                                                                
         DROP  RE                                                               
                                                                                
* SET ZERO ELEMENT CODE AT E-O-R                                                
NET10    LH    R1,REC-4                                                         
         LA    R1,REC-4(R1)                                                     
         XC    0(2,R1),0(R1)                                                    
*                                                                               
         CLI   ONEAGY,0            TEST IF ONE AGENCY RUN                       
         BE    NET12                                                            
         BAS   RE,FILLDBLK         IF YES/FILL DBLOCK                           
         CLC   NBALPHA,ONEAGY      AND TEST AGYALPHA                            
         BNE   GET                                                              
                                                                                
NET12    AP    TOTCTR,=P'1'                                                     
                                                                                
         CLI   RRECTY,COPY         TEST IF COPY                                 
         BE    CHK1                                                             
         CLI   RRECTY,CHANGE       TEST IF CHA                                  
         BE    CHK2                                                             
         CLI   RRECTY,ADD          TEST IF ADD                                  
         BE    CHK1                                                             
         MVC   P(27),=C'*** UNKNOWN RRECTY TYPE ***'                            
         AP    UNKCTR,=P'1'                                                     
         GOTO1 =V(PRINTER)                                                      
         B     GET                                                              
         EJECT                                                                  
* REC IS ADD/COPY - PREV MUST NOT BE COPY *                                     
* IF PREV=COPY, PRINT OUT PREV AS ERROR   *                                     
* THEN PROCESS REC AS NORMAL ADD/COPY     *                                     
CHK1     DS    0H                                                               
         CLI   PREVFLG,COPY                                                     
         BNE   CHK1A                                                            
*                                                                               
         MVC   P(19),=C'COPY WITH NO CHANGE'                                    
         AP    CPYCTR,=P'1'                                                     
         LA    R2,RCVRYSV                                                       
         LA    R3,P                                                             
         BAS   RE,ERRPRNT                                                       
                                                                                
CHK1A    DS    0H                                                               
         CLI   RRECTY,ADD          IS THIS AN ADD                               
         BNE   CHK1B                                                            
         AP    ADDCTR,=P'1'                                                     
         B     NET15               YES/GOTO NET15                               
                                                                                
* NO/IT'S A COPY                                                                
CHK1B    DS    0H                                                               
         CLI   DELETFLG,C'Y'       TEST DELETE                                  
         BNE   CHK1C                                                            
         XC    COPYDOL1,COPYDOL1   YES/MUST BE REUSE OF KEY                     
         XC    COPYDOL2,COPYDOL2   CLEAR DOLS SO THAT CHA ADDS ITS DOLS         
         B     CHK1D                                                            
CHK1C    BAS   RE,FILLDBLK                                                      
         MVC   COPYDOL1,NBBILTGR       8 FULL WORDS OF $                        
         MVC   COPYDOL2,NBASSIGN       3 FULL WORDS OF $                        
         MVC   PAYCPYGR,NBPAYTGR                                                
         MVC   PAYCPYGI,NBPAYIGR                                                
         MVC   PAYCPYNE,NBPAYTNT                                                
         MVC   PAYCPYNI,NBPAYINT                                                
CHK1D    MVC   RCVRYSV,0(R1)           SAVE 60CHARS OF RECOVERY REC             
         MVC   PREVFLG,RRECTY                                                   
         B     GET                                                              
                                                                                
* REC IS A CHA / PREV MUST = COPY                     *                         
* IF PREV IS NOT COPY, PRINT OUT CURRENT REC AS ERROR *                         
* THEN GET NEXT REC                                   *                         
CHK2     DS    0H                                                               
         CLI   PREVFLG,COPY                                                     
         BE    NET15               PREV=COPY/PROCESS                            
                                                                                
*        L     RE,AUNITREC                                                      
*        USING NURECD,RE                                                        
*        CLI   NUKTYPE,X'04'       ..IF IT'S UNIT                               
*        BNE   CHK2A                                                            
*        TM    NUPACKST,X'01'      ..AND IF NO-SHOW DELETE                      
*        BNO   CHK2A               ELSE/DO NORMAL                               
*        BAS   RE,FILLDBLK         ..FUDGE COPY $$$                             
*        MVC   COPYDOL1,NBBILTGR   ..8 FULL WORDS OF $                          
*        MVC   COPYDOL2,NBASSIGN   ..3 FULL WORDS OF $                          
*        MVC   PAYCPYGR,NBPAYTGR                                                
*        MVC   PAYCPYGI,NBPAYIGR                                                
*        MVC   PAYCPYNE,NBPAYTNT                                                
*        MVC   PAYCPYNI,NBPAYINT                                                
*        BNO   NET15                ..ACCEPT WITH NO PREVIOUS                   
*        DROP   RE                                                              
                                                                                
*                                                                               
* PREV NOT COPY/ERROR                                                           
CHK2A    MVI   PREVFLG,CHANGE                                                   
                                                                                
* ERROR MESSAGE REMOVED 3/14/96 - PROGRAM LOOKS AT OFFLINE CHANGES              
*  TO GET UPDATIVE SOONS - PICKS UP A LOT OF JUNK ALONG WITH IT                 
                                                                                
**       MVC   P(28),=C'CHANGE WITH NO PREVIOUS COPY'                           
**       AP    CHGCTR,=P'1'                                                     
**       LA    R2,REC                                                           
**       LA    R3,P                                                             
**       BAS   RE,ERRPRNT                                                       
         B     GET                 GET NEXT REC                                 
         EJECT                                                                  
* NO ERRORS/ PROCESS CHA/ADD RECS *                                             
                                                                                
NET15    DS    0H                                                               
         CLI   RRECTY,ADD                                                       
         BE    NET19                                                            
                                                                                
* PROCESS CHANGE REC *                                                          
         BAS   RE,FILLDBLK                                                      
         MVC   CHADOL1,NBBILTGR                                                 
         MVC   CHADOL2,NBASSIGN                                                 
         MVC   PAYCHAGR(8),NBACTUAL                                             
         L     RF,NBACTUAL                                                      
         SR    RE,RE                                                            
         M     RE,=F'85'                                                        
         D     RE,=F'100'                                                       
         CH    RE,=H'50'                                                        
         BL    *+8                                                              
         LA    RF,1(RF)                                                         
         ST    RF,PAYCHANE                                                      
         L     RF,NBINTEG                                                       
         SR    RE,RE                                                            
         M     RE,=F'85'                                                        
         D     RE,=F'100'                                                       
         CH    RE,=H'50'                                                        
         BL    *+8                                                              
         LA    RF,1(RF)                                                         
         ST    RF,PAYCHANI                                                      
         CLI   DELETFLG,C'Y'       TEST IF DELETED RECORD                       
         BNE   NET16                                                            
         XC    CHADOL1,CHADOL1   IF YES/CLEAR DOLS TO ZERO                      
         XC    CHADOL2,CHADOL2      TO GET NEG DOLS AFTER SUBTRACTION           
*NET16    MVC   PREVFLG,RRECTY                                                  
NET16    MVI   PREVFLG,CHANGE                                                   
                                                                                
* SUBTRACT COPYDOLS FROM CHADOLS AND STORE IN CHADOLS  *                        
         MVC   ENDCHA,=C'END'      SET END OF CHADOLS                           
         LA    R1,COPYDOL1         R1-ADRS OF COPY $                            
         LA    R2,CHADOL1          R2-ADRS OF CHA REC $                         
NET17    L     R3,0(R2)            PUT CHA$ IN R3                               
         S     R3,0(R1)            SUBTRACT COPY$                               
         ST    R3,0(R2)            STORE DIFF BACK TO R2                        
         LA    R2,4(R2)            INCREMENT CHA$                               
         CLC   0(3,R2),=C'END'       EOF                                        
         BE    NET20               YES/GOTO PASTOSRT                            
         LA    R1,4(R1)            INCREMENT COPY                               
         B     NET17                                                            
                                                                                
                                                                                
* PROCESS ADD REC *                                                             
NET19    DS    0H                                                               
         BAS   RE,FILLDBLK                                                      
         MVC   CHADOL1,NBBILTGR   8 FULL WORDS OF $                             
         MVC   CHADOL2,NBASSIGN   3 FULL WORDS OF $                             
         XC    PAYDLRS,PAYDLRS                                                  
         MVC   PREVFLG,RRECTY                                                   
         B     NET20                                                            
                                                                                
NET20    DS    0H                                                               
         BAS   RE,PASTOSRT                                                      
         B     GET                                                              
         EJECT                                                                  
*                                                                               
FILLDBLK NTR1                                                                   
         MVC   NBAIO,AUNITREC       SET NBAIO TO START OF UNIT REC              
         L     R1,NBAIO                                                         
         MVC   NBACTAM,1(R1)        NEEDED FOR EXTENDED BRANDS NETVAL           
*        MVI   NBFUNCT,NBFVAL                     FOR NETBLOCK FILL.            
*        GOTO1 NETIO,DMCB,NETBLOCK                                              
         OI    NBSBKEND,NBNODPT2    DON'T READ FOR 2 CHAR DPT                   
         MVI   NBESTOPT,0                                                       
         MVI   NBACTOPT,0                                                       
         GOTO1 NBNETVAL,DMCB,NETBLOCK                                           
         CLI   NBERROR,NBGOOD                                                   
         BE    *+6                                                              
         DC    H'0'                                                             
*                                                                               
         MVC   DATADISP,=X'001B'                                                
         L     R6,AUNITREC                                                      
         MVI   ELCODE,X'03'        SPECIAL RATE ELEMENT                         
         BAS   RE,GETEL                                                         
         BNE   FL1                                                              
         USING NUSPRD,R6                                                        
FL0      ICM   R1,15,NUSPRAMT                                                   
         A     R1,NBACTUAL                                                      
         ST    R1,NBACTUAL                                                      
         BAS   RE,NEXTEL                                                        
         BE    FL0                                                              
FL1      MVI   ELCODE,X'10'        BILLING ELEMENT                              
         L     R6,AUNITREC                                                      
         USING NUBILD,R6                                                        
         BAS   RE,GETEL                                                         
         BNE   FL5                                                              
FL2      CLI   NUBILTYP,C'T'                                                    
         BE    FL3                                                              
         CLI   NUBILTYP,C'I'                                                    
         BE    FL3                                                              
         L     R1,NBBILTGR                                                      
         A     R1,NUBILGRS                                                      
         ST    R1,NBBILTGR                                                      
         L     R1,NBBILTNT                                                      
         A     R1,NUBILNET                                                      
         ST    R1,NBBILTNT                                                      
FL3      BAS   RE,NEXTEL                                                        
         BE    FL2                                                              
FL5      MVI   ELCODE,X'12'        PAYING ELEMENT                               
         L     R6,AUNITREC                                                      
         BAS   RE,GETEL                                                         
         BNE   FL9                                                              
         USING NUPAYD,R6                                                        
FL6      CLI   NUPAYTYP,C'T'                                                    
         BE    FL7                                                              
         CLI   NUPAYTYP,C'I'                                                    
         BE    FL7                                                              
         L     R1,NBPAYTGR                                                      
         A     R1,NUPAYGRS                                                      
         ST    R1,NBPAYTGR                                                      
         L     R1,NBPAYTNT                                                      
         A     R1,NUPAYNET                                                      
         ST    R1,NBPAYTNT                                                      
FL7      BAS   RE,NEXTEL                                                        
         BE    FL6                                                              
*                                                                               
FL9      TM    NBUNITST,X'42'      IF PREEMPTED OR MISSED SET TO ZERO           
         BZ    FILLX                                                            
         XC    NBASSIGN,NBASSIGN                                                
         XC    NBACTUAL,NBACTUAL                                                
         XC    NBINTEG,NBINTEG                                                  
FILLX    B     EXIT                                                             
*                                                                               
         GETEL R6,DATADISP,ELCODE                                               
*                                                                               
         EJECT                                                                  
*                                                                               
* PUT RECORDS TO SORT                                                           
* CHADOLS CONTAINS DIFF OF COPY AND CHANGE                                      
                                                                                
PASTOSRT NTR1                                                                   
         LA    R5,IOAREA2                                                       
         USING SORTRECD,R5                                                      
         MVI   SRTRTY,1                                                         
         MVC   SRTAGY,NBALPHA                                                   
         MVI   SRTRBA,1                                                         
         MVC   SRTCLT,NBACTCLI                                                  
         MVC   SRTNET,NBACTNET                                                  
         MVC   SRTEST,NBACTEST                                                  
         MVC   SRTPKG,NBPACK                                                    
         MVC   SRTPRG,NBACTPRG          **NO BREAK ON PROGRAM ****              
         MVC   SRTSUB,NBACTSUB                                                  
         MVC   SRTAIRDT,NBACTDAT                                                
         MVC   SRTBTGR(SRTRCLNE-SRTKLNE),CHADOL1                                
                                                                                
         GOTO1 =V(SORTER),DMCB,=C'PUT',(R5)                                     
                                                                                
         AP    CPYCHCTR,=P'2'                                                   
                                                                                
         OC    PAYCPYGR(8),PAYCPYGR IF COPY 0 NO CHANGE NEW PAY                 
         B     PS100                                                            
         BZ    PS100                                                            
         CLC   PAYCPYGR(8),PAYCHAGR IF DIF ADD SORT RECORD                      
         BE    PS100                                                            
         XC    RCCNTR,RCCNTR                                                    
                                                                                
* ADD SECOND SORT RECORD IF DIFFERENCE IN PAY/ASSIGNED                          
* KEEP VALUES SET ABOVE EXCEPT WHERE DIFFERENT                                  
         USING SORT2RCD,R5                                                      
         MVI   SRTRTY2,2                                                        
         MVC   SRTUSER2,RUSER                                                   
         MVC   SRTPYGRO(32),PAYCPYGR                                            
         XC    SRTEND2(SRTSPAR2),SRTEND2                                        
                                                                                
         GOTO1 =V(SORTER),DMCB,=C'PUT',(R5)                                     
                                                                                
PS100    XC    COPYDOL1,COPYDOL1                                                
         XC    COPYDOL2,COPYDOL2                                                
         XC    CHADOL1,CHADOL1                                                  
         XC    CHADOL2,CHADOL2                                                  
         B     EXIT                                                             
         DROP  R5                                                               
*                                                                               
ERRPRNT  NTR1                                                                   
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),DMCB,(R2),(R3),60                                     
*        GOTO1 =V(PRNTBL),DMCB,0,REC,C'DUMP',(R0),=C'1D'                        
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         EJECT                                                                  
*****************************************************                           
* NET2 TAKES RECS FROM SORTER AND PROCESSES THEM    *                           
*                                                                               
* A REC IS PUT INTO IOAREA1/ANOTHER IN IOAREA2      *                           
* THEY ARE COMPARED FOR BREAK IN SORTKEY            *                           
* EACH REC IS PRINTED                                                           
* AT KEYBREAK TOTS ARE PRINTED AND ROLLED           *                           
*****************************************************                           
                                                                                
NET2     NTR1                                                                   
         BAS   RE,PAGETOP                                                       
         MVI   FRST,C'Y'                                                        
                                                                                
NET22    GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         L     R6,4(R1)                                                         
         LTR   R6,R6                                                            
         BNZ   NET23                                                            
                                                                                
         CLI   FRST,C'Y'           TEST FIRST TIME                              
         BE    ERR                                                              
         MVI   EOFSRTRC,C'Y'       SET EOF FLAG                                 
         MVI   BRKBYTE,C'5'        SET BREAKLEVEL TO AGY                        
         B     NET29                                                            
ERR      MVC   P(29),=C'NO RECORDS RECEIVED FROM SORT'                          
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
                                                                                
NET23    DS    0H                                                               
         LA    R2,IOAREA2                    PUT REC IN IOAREA2                 
         USING SORTRECD,R2                                                      
         MVC   0(SRTRCLNE,R2),0(R6)                                             
         MVC   COPYDOL1,SRTBTGR                                                 
         MVC   COPYDOL2,SRTASGN                                                 
         CLI   FRST,C'Y'                     TEST FIRST TIME                    
         BNE   *+12                                                             
         MVI   FRST,C'N'                                                        
         B     NET25                                                            
         CLC   0(SRTKLNE,R2),IOAREA1          COMPARE REC1/REC2                 
         BNE   NET27                                                            
         DROP  R2                                                               
                                                                                
* RECS ARE EQUAL                                                                
NET25    BAS   RE,PRNTREC                     PRINT THE REC                     
         BAS   RE,ADDTOPKG                    ADD TO PKG TOTS                   
         MVC   IOAREA1(SRTRCLNE),IOAREA2      MOVE TO IOAREA1                   
         B     NET22                          GET NEXT REC                      
         EJECT                                                                  
* REC ARE UNEQUAL                                                               
NET27    DS    0H                                                               
                                                                                
* CHECK WHERE BREAK IN KEY *                                                    
NET28    CLI   IOAREA2,2                                                        
         BE    *+14                                                             
         CLC   IOAREA1+1(L'SRTAGY),IOAREA2+1                                    
         BE    *+12                                                             
         MVI   BRKBYTE,C'5'        AGY BREAK LEVEL                              
         B     NET29                                                            
         CLC   IOAREA1+4(L'SRTCLT),IOAREA2+4                                    
         BE    *+12                                                             
         MVI   BRKBYTE,C'4'        CLT BREAK LEVEL                              
         B     NET29                                                            
         CLC   IOAREA1+6(L'SRTNET),IOAREA2+6                                    
         BE    *+12                                                             
         MVI   BRKBYTE,C'3'        NET BREAK LEVEL                              
         B     NET29                                                            
         CLC   IOAREA1+10(L'SRTEST),IOAREA2+10                                  
         BE    *+12                                                             
         MVI   BRKBYTE,C'2'        EST BREAK LEVEL                              
         B     NET29                                                            
         CLC   IOAREA1+11(L'SRTPKG),IOAREA2+11                                  
         BE    *+12                                                             
         MVI   BRKBYTE,C'1'        PKG BREAK LEVEL                              
         B     NET29                                                            
         DC    H'0'                                                             
                                                                                
NET29    DS    0H                                                               
         GOTO1 =V(PRINTER)         PRINT SPACE BEFORE TOTALS                    
         LA    R2,BRKTBL                                                        
NET29A   CLC   BRKBYTE,0(R2)                                                    
         BL    NET29B              NO MORE LEVEL BREAKS                         
         MVC   FULL,1(R2)          FULL GETS DISPLACEMENT TO TOTFLD             
         LA    R3,MYWORKD                                                       
         A     R3,FULL             ADD DISP TO START OF WORKAREA                
         BAS   RE,PRNTROLL         TO GET ADRS OF TOTFLD.                       
         LA    R2,20(R2)                                                        
         CLI   0(R2),X'FF'         EOF BRKTABLE                                 
         BNE   NET29A                                                           
         EJECT                                                                  
                                                                                
NET29B   DS    0H                                                               
         CLI   EOFSRTRC,C'Y'       TEST EOFSORT                                 
         BE    NET29C              IF YES/THEN NO MORE RECS/EXIT                
                                                                                
         CLI   IOAREA2,1           TEST END OF REPORT                           
         BNE   RPT2                IF YES/THEN DO NEXT REPORT                   
                                                                                
         GOTO1 =V(PRINTER)                   PRINT SPACE AFTER TOTS             
         BAS   RE,PRNTREC          ELSE/     PRINT THE NEW REC                  
         BAS   RE,ADDTOPKG                   ADD IT TO PKGTOTS                  
         MVC   IOAREA1(SRTRCLNE),IOAREA2     MOVE IT TO IO1                     
                                                                                
         B     NET22                         GET NEXT REC                       
                                                                                
NET29C   BAS   RE,NET3         PRINT AGY TOTS/ CLOSE FILES                      
NET2X    DS    0H                                                               
         B     EXIT                                                             
                                                                                
************************************************                                
*        BRKTABLE FOR DOLLAR TOTALS            *                                
*        BRKBYTE/DISP OF FULL WORD TOTS/HEADER *                                
************************************************                                
BRKTBL   DS    0H                                                               
         DC    C'1',AL4(PKGTOT-MYWORKD),C'PACKAGE TOTAL  '                      
         DC    C'2',AL4(ESTTOT-MYWORKD),C'ESTIMATE TOTAL '                      
         DC    C'3',AL4(NETTOT-MYWORKD),C'NETWORK TOTAL  '                      
         DC    C'4',AL4(CLTTOT-MYWORKD),C'CLIENT TOTAL   '                      
         DC    C'5',AL4(AGYTOT-MYWORKD),C'AGENCY TOTAL   '                      
         DC    X'FF'                                                            
         EJECT                                                                  
                                                                                
* PRINT THE REC *                                                               
PRNTREC  NTR1                                                                   
         CP    LINE,=P'50'                                                      
         BNH   *+8                                                              
         BAS   RE,PAGETOP                                                       
         LA    R2,P                                                             
         USING PLINED,R2                                                        
         LA    R3,IOAREA2                                                       
         USING SORTRECD,R3                                                      
         MVC   PLNAGY,SRTAGY                                                    
         GOTO1 =V(CLUNPK),DMCB,SRTCLT,PLNCLT                                    
         EDIT  (B1,SRTEST),(3,PLNEST)                                           
         MVC   PLNNET,SRTNET                                                    
         EDIT  (B1,SRTPKG),(3,PLNPKG)                                           
         LA    R5,PLNBILGR         **                                           
         MVC   0(6,R5),SRTPRG      **                                           
         EDIT  (B1,SRTSUB),(2,9(R5)) **                                         
         GOTO1 =V(DATCON),DMCB,(2,SRTAIRDT),(X'20',13(R5)) **                   
         B     PRT5                **                                           
                                                                                
** NOTE THAT PROGRAM HERE PUTS PRGRAM,SUBLINE,AIRDATE IN BILLED                 
** FIELDS.  THIS GIVES MORE DETAIL TO GET ERRORS.  BILLED DOLS                  
** WERE COMING UP BLANK ANYWAY SINCE PRGRM SKIPS OFF-LINE BILLING.              
** TOTAL BILLED DOLS ARE STILL SHOWN.                                           
                                                                                
         LA    R4,PLNBILGR                                                      
         L     R3,CPDBTG           BILLED TIME GROSS                            
         A     R3,CPDITG           INTG GROSS                                   
         BAS   RE,PEDT                                                          
         L     R3,CPDBTN           BILLED TIME NET                              
         A     R3,CPDITN           INTG NET                                     
         BAS   RE,PEDT                                                          
PRT5     NI    ZEROFLG,X'80'       RESET ZERO CHECK FLAG                        
         LA    R4,PLNPAYGR                                                      
         L     R3,CPDPTG           PAY TIME GROSS                               
         A     R3,CPDPIG           INTG GROSS                                   
         C     R3,=F'0'                                                         
         BE    *+8                                                              
         OI    ZEROFLG,X'40'       SET DATA ON TIS LINE                         
         BAS   RE,PEDT                                                          
         L     R3,CPDPTN           PAY TIME NET                                 
         A     R3,CPDPIN           INTG NET                                     
         C     R3,=F'0'                                                         
         BE    *+8                                                              
         OI    ZEROFLG,X'40'       SET DATA ON TIS LINE                         
         BAS   RE,PEDT                                                          
         L     R3,CPDOASGN         ORDERED ASSIGNED                             
         C     R3,=F'0'                                                         
         BE    *+8                                                              
         OI    ZEROFLG,X'40'       SET DATA ON TIS LINE                         
         BAS   RE,PEDT                                                          
         L     R3,CPDOACT          ORDERED ACTUAL                               
         C     R3,=F'0'                                                         
         BE    *+8                                                              
         OI    ZEROFLG,X'40'       SET DATA ON TIS LINE                         
         BAS   RE,PEDT                                                          
         L     R3,CPDINTG          ORDERED INTEGRATION                          
         C     R3,=F'0'                                                         
         BE    *+8                                                              
         OI    ZEROFLG,X'40'       SET DATA ON TIS LINE                         
         BAS   RE,PEDT                                                          
                                                                                
         TM    ZEROFLG,X'80'       IS BYPASS ZERO SET                           
         BZ    PRT7                                                             
         TM    ZEROFLG,X'40'       IS ANY DATA ON THIS LINE                     
         BZ    EXIT                NO BYPASS THE PRINT                          
PRT7     GOTO1 =V(PRINTER)                                                      
                                                                                
         B     EXIT                                                             
         DROP  R2,R3                                                            
*                                                                               
PEDT     DS    0H                                                               
         EDIT  (R3),(15,0(R4)),2,CR=YES                                         
         LA    R4,16(R4)                                                        
         BR    RE                                                               
         EJECT                                                                  
* ADD COPYDOLS TO PKGTOT *                                                      
* ALL TOTS ARE PL8       *                                                      
ADDTOPKG NTR1                                                                   
         LA    R1,PKGTOT                                                        
         SR    R2,R2                                                            
         A     R2,CPDBTG           BILLED TIME GROSS                            
         A     R2,CPDITG           BILLED INTG GROSS                            
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         LA    R1,8(R1)                                                         
         SR    R2,R2                                                            
         A     R2,CPDBTN           BILLED TIME NET                              
         A     R2,CPDITN           BILLED INTG NET                              
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         LA    R1,8(R1)                                                         
         SR    R2,R2                                                            
         A     R2,CPDPTG           PAID  TIME GROSS                             
         A     R2,CPDPIG           PAID  INTG GROSS                             
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         LA    R1,8(R1)                                                         
         SR    R2,R2                                                            
         A     R2,CPDPTN           PAID  TIME NET                               
         A     R2,CPDPIN           PAID  INTG NET                               
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         LA    R1,8(R1)                                                         
         L     R2,CPDOASGN         ORDERED ASSIGNED                             
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         LA    R1,8(R1)                                                         
         L     R2,CPDOACT          ORDERED ACTUAL                               
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         LA    R1,8(R1)                                                         
         L     R2,CPDINTG          ORDERED INTEGRATION                          
         CVD   R2,DUB                                                           
         AP    0(8,R1),DUB                                                      
         B     EXIT                                                             
         EJECT                                                                  
****************************                                                    
* PRINT TOTALS AT BREAK    *                                                    
* INPUT:R2 - BRKTBL ENTRY  *                                                    
*       R3 - ADDR OF $F'S  *                                                    
****************************                                                    
PRNTROLL NTR1                                                                   
                                                                                
         MVC   P(15),5(R2)       TOTALS HEADER TO P LINE                        
                                                                                
         LA    R2,P              - NOW R2 IS FREE                               
         USING PLINED,R2                                                        
                                                                                
         MVI   ENDTOTS,X'FF'                                                    
         LA    R5,7                                                             
         ST    R3,SAVEADR                                                       
         LA    R6,PLNBILGR                                                      
PRT10    MVC   TOTAL,0(R3)                              $AMT TO TOTAL           
         EDIT  (P8,TOTAL),(15,0(R6)),2,CR=YES                                   
         LA    R6,16(R6)                        INCREMENT P LINE DISP           
         LA    R3,8(R3)                         INCREMENT $FIELD                
         BCT   R5,PRT10                                                         
         GOTO1 =V(PRINTER)                                                      
                                                                                
* ROLL UP TOTAL *                                                               
         CLI   0(R3),X'FF'         IS IT AN AGY BREAK                           
         BE    PRT30               IF YES/GOTO ALL AGY TOTS                     
                                                                                
         L     R3,SAVEADR          R3-THIS LEVEL OF D'S                         
         LA    R6,56(R3)           R6-NEXT LEVEL OF D'S (7FXCL8)                
         LA    R5,7                R5-BCT LIMIT OF NUM OF FIELDS                
PRT20    AP    0(8,R6),0(8,R3)                                                  
         LA    R3,8(R3)                                                         
         LA    R6,8(R6)                                                         
         BCT   R5,PRT20                                                         
                                                                                
* CLEAR ROLLED UP TOTAL *                                                       
         L     R3,SAVEADR                                                       
         LA    R5,7                                                             
PRT21    ZAP   0(8,R3),=P'0'                                                    
         LA    R3,8(R3)                                                         
         BCT   R5,PRT21                                                         
         B     PRTX                                                             
                                                                                
* ROLL TO ALL AGY SUMMARY *                                                     
PRT30    DS    0H                                                               
         MVI   ENDAGYAL,X'FF'                                                   
         LA    R3,AGYTOT                                                        
         LA    R4,ALLAGY                                                        
PRT32    AP    0(8,R4),0(8,R3)                                                  
         LA    R3,8(R3)                                                         
         LA    R4,8(R4)                                                         
         CLI   0(R4),X'FF'                                                      
         BNE   PRT32                                                            
                                                                                
* DISPLAY:CLEARED=(NET PAID)         FOR CLEARANCE REPORT COORDINATION          
*         ORDERED=(ORD ACT+ORD INTG) FOR AGY SUMMARY COORDINATION               
         LA    R3,AGYTOT                                                        
         MVC   TOTAL,24(R3)        GET NET PAID                                 
         MVC   P(8),=C'CLEARED='                                                
         LA    R4,P+8                                                           
         EDIT  (P8,TOTAL),(15,0(R4)),2,CR=YES                                   
         GOTO1 =V(PRINTER)                                                      
         MVC   TOTAL,40(R3)          MOVE ORD ACT                               
         AP    TOTAL,48(8,R3)          ADD ORD INTG                             
         MVC   P(8),=C'ORDERED='                                                
         LA    R4,P+8                                                           
         EDIT  (P8,TOTAL),(15,0(R4)),2,CR=YES                                   
         GOTO1 =V(PRINTER)                                                      
                                                                                
*                                  * WRITE TO AGY HEADER *                      
         CLI   WRITEFLG,C'Y'                                                    
         BNE   PRT35                                                            
         LA    R3,AGYTOT                                                        
         LA    R3,24(R3)           NET PAID                                     
         LA    R1,IOAREA1                                                       
         USING SORTRECD,R1                                                      
         XC    KEY,KEY                                                          
         MVI   KEY,X'06'           AGY HEADER ID                                
         MVC   KEY+1(2),SRTAGY                                                  
         DROP  R1                                                               
         GOTO1 HIGH                                                             
         CLC   KEY(3),KEYSAVE                                                   
         BNE   READERR                                                          
         GOTO1 GETREC                                                           
         BNE   READERR                                                          
         LA    R1,24(R7)           R7 POINTS TO REC                             
         CLI   0(R1),X'01'         ELCODE                                       
         BNE   READERR                                                          
         USING AGYEL,R1                                                         
*        CLC   AGYCLDT,TODAYP3     CHK IF UPDATED BY SPOT                       
*        BE    *+14                                                             
         MVC   AGYCLAMT,2(R3)      NO/SO MOVE IT IN (8PL TO 6PL)                
*        B     *+10                                                             
*        AP    AGYCLAMT,2(6,R3)    YES/SO ADD IT                                
         MVC   AGYCLDT,TODAYP3     SET TODAY'S DATE                             
         DROP  R1                                                               
         GOTO1 PUTREC                                                           
         B     PRT35                                                            
*                                                                               
READERR  DS    0H                                                               
         MVC   P+4(24),=C'ERROR IN AGY HEADER READ'                             
         GOTO1 =V(PRINTER)                                                      
         MVC   P+4(5),=C'AGY ='                                                 
         LA    R1,IOAREA1+1                                                     
         MVC   P+10(2),0(R1)                                                    
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(HEXOUT),DMCB,(R7),P,50                                        
         GOTO1 =V(PRINTER)                                                      
*                                                                               
*  SEND ALERT VIA E-MAIL TO TELL OF CONDITION                                   
*                                                                               
         XC    WORK,WORK                                                        
         LA    R1,WORK                                                          
         MVC   0(L'NOTEMSG,R1),NOTEMSG                                          
         LA    R1,L'NOTEMSG(,R1)                                                
         MVC   0(2,R1),IOAREA1+1    FROM ABOVE                                  
         OC    WORK,SPACES                                                      
         GOTO1 =V(DATAMGR),DMCB,=C'OPMSG',(L'NOTEMSG+11,WORK)                   
         B     PRT35                                                            
*                                                                               
NOTEMSG  DC    C'AUTONOTE*PZIR,SCHT,NSIL:NET133 AGY READ ERROR ='               
         EJECT                                                                  
*                                                                               
* CLEAR AGYTOT *                                                                
PRT35    LA    R3,AGYTOT                                                        
         LA    R5,7                                                             
PRT37    ZAP   0(8,R3),=P'0'                                                    
         LA    R3,8(R3)                                                         
         BCT   R5,PRT37                                                         
         BAS   RE,PAGETOP          NEW PAGE FOR NEW AGY                         
         B     PRTX                                                             
                                                                                
PRTX     B     EXIT                                                             
         DROP  R2                                                               
*                                                                               
         EJECT                                                                  
                                                                                
*********************************                                               
* NET3 PRINTS ALL AGENCY TOTALS *                                               
*********************************                                               
NET3     NTR1                                                                   
         LA    R5,P                                                             
         USING PLINED,R5                                                        
         LA    R2,ALLAGY                                                        
         LA    R3,PLNBILGR                                                      
NET32    MVC   TOTAL,0(R2)                                                      
         EDIT  (P8,TOTAL),(15,0(R3)),2,CR=YES                                   
         LA    R2,8(R2)                                                         
         LA    R3,16(R3)                                                        
         CLI   0(R2),X'FF'                                                      
         BNE   NET32                                                            
                                                                                
         MVC   P(16),=C'ALL AGENCY TOTAL'                                       
         MVI   SPACING,2                                                        
         GOTO1 =V(PRINTER)                                                      
         DROP  R5                                                               
                                                                                
* PRINT COUNT TOTALS *                                                          
         MVC   P(10),=C'RECS READ='                                             
         LA    R3,P+11                                                          
         MVC   TOTAL(4),TOTCTR                                                  
         BAS   R5,NET38                                                         
         MVC   P(10),=C'RECS ADD= '                                             
         MVC   TOTAL(4),ADDCTR                                                  
         BAS   R5,NET38                                                         
         MVC   P(10),=C'RECS CPCH='                                             
         MVC   TOTAL(4),CPYCHCTR                                                
         BAS   R5,NET38                                                         
         MVC   P(10),=C'ERROR CHA='                                             
         MVC   TOTAL(4),CHGCTR                                                  
         BAS   R5,NET38                                                         
         MVC   P(10),=C'ERROR CPY='                                             
         MVC   TOTAL(4),CPYCTR                                                  
         BAS   R5,NET38                                                         
         MVC   P(10),=C'ERROR UNK='                                             
         MVC   TOTAL(4),UNKCTR                                                  
         BAS   R5,NET38                                                         
         B     NET3X                                                            
*                                                                               
NET38    DS    0H                                                               
         EDIT  (P4,TOTAL),(7,0(R3))                                             
         GOTO1 =V(PRINTER)                                                      
         BR    R5                                                               
                                                                                
NET3X    DS    0H                                                               
         B     EXIT                                                             
         EJECT                                                                  
* SET TO PRINT HEADLINES *                                                      
PAGETOP  NTR1                                                                   
         ZAP   LINE,=P'100'        FORCE PAGE BREAK                             
         MVC   TITLE+10(22),=C'NETWORK DAILY ACTIVITY'                          
         MVC   MID1+49(5),NETTXT                                                
         MVI   MID1+54,C'-'                                                     
         MVC   MID1+55(6),SPTTXT                                                
         MVC   MID2+49(12),=15C'-'                                              
                                                                                
         LA    R2,P                                                             
         USING PLINED,R2                                                        
         MVC   PLNAGY(3),=C'AGY'                                                
         MVC   PLNCLT(3),=C'CLT'                                                
         MVC   PLNEST(3),=C'EST'                                                
         MVC   PLNNET(3),=C'NET'                                                
         MVC   PLNPKG(3),=C'PKG'                                                
         MVC   PLNBILGR(10),=C'BILL GROSS'                                      
         MVC   PLNBILNT(8),=C'BILL NET'                                         
         MVC   PLNPAYGR(10),=C'PAID GROSS'                                      
         MVC   PLNPAYNT(8),=C'PAID NET'                                         
         MVC   PLNOASGN(12),=C'ORD ASSIGNED'                                    
         MVC   PLNOACT(10),=C'ORD ACTUAL'                                       
         MVC   PLNOINTG(15),=C'ORD INTEGRATION'                                 
                                                                                
         GOTO1 =V(PRINTER)                                                      
                                                                                
         MVC   PLNAGY(3),=15C'-'                                                
         MVC   PLNCLT(3),=15C'-'                                                
         MVC   PLNEST(3),=15C'-'                                                
         MVC   PLNNET(3),=15C'-'                                                
         MVC   PLNPKG(3),=15C'-'                                                
         MVC   PLNBILGR(15),=15C'-'                                             
         MVC   PLNBILNT(15),=15C'-'                                             
         MVC   PLNPAYGR(15),=15C'-'                                             
         MVC   PLNPAYNT(15),=15C'-'                                             
         MVC   PLNOASGN(15),=15C'-'                                             
         MVC   PLNOACT(15),=15C'-'                                              
         MVC   PLNOINTG(15),=15C'-'                                             
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         DROP  R2                                                               
         EJECT                                                                  
RPT2     DS    0H                                                               
         BAS   RE,NET3             PRIN FINAL TOTALS FROM LAST REPORT           
         MVI   FRST,C'Y'                                                        
         B     RP120               1ST RECORD ALEADY IN IOAREA2                 
                                                                                
RP100    GOTO1 =V(SORTER),DMCB,=C'GET'                                          
         MVI   EOFSRTRC,C'Y'                                                    
         ICM   R6,15,4(R1)                                                      
         BZ    RPT3                                                             
         MVI   EOFSRTRC,C'N'                                                    
                                                                                
RP120    DS    0H                                                               
         LA    R2,IOAREA2          PUT REC IN IOAREA2                           
         USING SORT2RCD,R2                                                      
         MVC   SORTRC2,0(R6)                                                    
         CLI   FRST,C'Y'                                                        
         BNE   *+8                                                              
         BAS   RE,PRNTLOGS         PRINT AGY LOGO                               
*                                                                               
RP140    CLI   SRTRTY2,2                                                        
         BNE   RPT3                                                             
         CLI   FRST,C'Y'                                                        
         BE    RP150                                                            
         CLC   IOAREA2+1(3),IOAREA1+1 TEST FOR AGY/RPT BREAK                    
         BE    RP160                                                            
         CLI   IOAREA2+3,1         REPORT 1                                     
         BNE   RPDUMP                                                           
         BAS   RE,ENDRPT1                                                       
         BAS   RE,PRNTLOGS         PRINT AGY LOGO                               
         B     RP150                                                            
*                                                                               
RPDUMP   DC    H'0'                NO REPORT MATCH                              
*                                                                               
RP150    ZAP   LINE,=P'100'                                                     
*                                                                               
RP160    CLI   IOAREA2+3,1         REPORT 1                                     
         BNE   RPDUMP                                                           
         CLC   IOAREA1(10),IOAREA2 COMPARE THRU NET FOR PAGE BREAK              
         BE    RP180                                                            
         ZAP   LINE,=P'100'                                                     
RP180    BAS   RE,PROCRPT1                                                      
         MVI   FRST,C'N'                                                        
         B     RP300                                                            
*                                                                               
         DROP  R2                                                               
                                                                                
* SAVE LAST RECORD READ NEXT RECORD                                             
RP300    MVC   IOAREA1(68),IOAREA2                                              
         B     RP100                                                            
         EJECT                                                                  
* PRINT THE REC *                                                               
PROCRPT1 NTR1                                                                   
         CP    LINE,=P'50'                                                      
         BNH   *+8                                                              
         BAS   RE,PAGETOP2                                                      
         LA    R2,P                                                             
         USING PLINED2,R2                                                       
         LA    R5,IOAREA2                                                       
         USING SORT2RCD,R5                                                      
         EDIT  (B1,SRTEST2),(3,PLNEST2)                                         
         EDIT  (B1,SRTPKG2),(3,PLNPKG2)                                         
         MVC   PLNPRG2(6),SRTPRG2                                               
         GOTO1 =V(DATCON),DMCB,(2,SRTAIRD2),(5,PLNDTE2)                         
         EDIT  (B1,SRTSUB2),(3,PLNSUB2)                                         
                                                                                
         MVC   PLNLIT(4),=C'FROM'                                               
         LA    R4,PLNPYD1                                                       
         L     R3,SRTPYGRO         OLD GROSS                                    
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD2                                                       
         L     R3,SRTPYGIO         OLD INTG GROSS                               
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD3                                                       
         L     R3,SRTPYGRO         OLD GROSS TOTAL                              
         A     R3,SRTPYGIO                                                      
         BAS   RE,PEDT                                                          
*                                                                               
         LA    R4,PLNPYD4                                                       
         L     R3,SRTPYNEO         OLD NET                                      
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD5                                                       
         L     R3,SRTPYNIO         OLD INTG NET                                 
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD6                                                       
         L     R3,SRTPYNEO         OLD NET                                      
         A     R3,SRTPYNIO                                                      
         BAS   RE,PEDT                                                          
                                                                                
         GOTO1 =V(PRINTER)                                                      
                                                                                
         MVC   PLNLIT(2),=C'TO'                                                 
         LA    R4,PLNPYD1                                                       
         L     R3,SRTPYGRN         NEW GROSS                                    
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD2                                                       
         L     R3,SRTPYGIN         NEW INTG GROSS                               
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD3                                                       
         L     R3,SRTPYGRN         NEW GROSS TOTAL                              
         A     R3,SRTPYGIN                                                      
         BAS   RE,PEDT                                                          
*                                                                               
         LA    R4,PLNPYD4                                                       
         L     R3,SRTPYNEN         NEW NET                                      
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD5                                                       
         L     R3,SRTPYNIN         NEW INTG NET                                 
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD6                                                       
         L     R3,SRTPYNEN         NEW NET TOTAL                                
         A     R3,SRTPYNIN                                                      
         BAS   RE,PEDT                                                          
                                                                                
         GOTO1 =V(PRINTER)                                                      
                                                                                
         MVC   PLNLIT-1(5),=C'*DIF*'                                            
         LA    R4,PLNPYD1                                                       
         L     R3,SRTPYGRN         DIF GROSS                                    
         S     R3,SRTPYGRO                                                      
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD2                                                       
         L     R3,SRTPYGIN         DIF INTG GROSS                               
         S     R3,SRTPYGIO                                                      
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD3                                                       
         L     R3,SRTPYGRN         DIF GROSS TOTAL                              
         A     R3,SRTPYGIN                                                      
         S     R3,SRTPYGRO                                                      
         S     R3,SRTPYGIO                                                      
         BAS   RE,PEDT                                                          
*                                                                               
         LA    R4,PLNPYD4                                                       
         L     R3,SRTPYNEN         DIF NET                                      
         S     R3,SRTPYNEO                                                      
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD5                                                       
         L     R3,SRTPYNIN         DIF INTG NET                                 
         S     R3,SRTPYNIO                                                      
         BAS   RE,PEDT                                                          
         LA    R4,PLNPYD6                                                       
         L     R3,SRTPYNEN         DIF NET TOTAL                                
         A     R3,SRTPYNIN                                                      
         S     R3,SRTPYNEO                                                      
         S     R3,SRTPYNIO                                                      
         BAS   RE,PEDT                                                          
                                                                                
         GOTO1 =V(PRINTER)                                                      
         GOTO1 =V(PRINTER)                                                      
                                                                                
         B     EXIT                                                             
         DROP  R2,R5                                                            
         EJECT                                                                  
ENDRPT1  CLC   IOAREA1+1(2),IOAREA2+1  AGY BREAK PRINT LOGO                     
         BER   RE                                                               
         LR    R0,RE                                                            
         BAS   RE,PRNTLOGE                                                      
         LR    RE,R0                                                            
         BR    RE                                                               
         EJECT                                                                  
* PRINT AN AGENCY LOGO                                                          
PRNTLOGS NTR1                                                                   
         LA    R3,IOAREA2                                                       
         USING SORT2RCD,R3                                                      
*                                                                               
         MVI   NOLOGO,C'N'                                                      
         XC    KEY,KEY                                                          
         MVI   KEY,X'06'           READ AGY HEADER                              
         MVC   KEY+1(2),SRTAGY2                                                 
         GOTO1 HIGH                                                             
         CLC   KEY(13),KEYSAVE                                                  
         BNE   EXIT                                                             
         MVI   NOLOGO,C'Y'                                                      
*                                                                               
         GOTO1 GETREC                                                           
         L     R4,=A(DBLOCK)                                                    
         USING AGYHDRD,R4                                                       
*                                                                               
         L     R6,=V(LOGOC)                                                     
         USING LOGOD,R6                                                         
         MVC   LOGOPRNT,=V(PRINT)                                               
         MVC   LOGOXPND,=V(EXPAND)                                              
         MVI   LOGOEND,C'X'                                                     
         MVI   LOGOTYPE,C'S'                                                    
         MVC   LOGONAME,AGYNAME                                                 
         MVC   LOGOADD,AGYADDR                                                  
         GOTO1 =V(GETLOGO),DMCB,(0,SRTUSER2),(R6),V(DATAMGR)                    
         MVC   LOGOJOB(4),DMCB                                                  
         CLI   LOGOJOB+3,X'40'                                                  
         BH    *+8                                                              
         MVI   LOGOJOB+3,C'X'                                                   
         MVC   LOGOJOB+4(3),=C'NCA'                                             
         GOTO1 =V(LOGO),DMCB,(R6)                                               
         B     EXIT                                                             
                                                                                
* PRINT AN AGENCY LOGO                                                          
PRNTLOGE NTR1                                                                   
         CLI   NOLOGO,C'Y'                                                      
         BNE   EXIT                                                             
         L     R6,=V(LOGOC)                                                     
         USING LOGOD,R6                                                         
         MVI   LOGOTYPE,C'E'                                                    
         GOTO1 =V(LOGO),DMCB,(R6)                                               
         B     EXIT                                                             
         DROP  R3,R4,R6                                                         
         EJECT                                                                  
* SET TO PRINT HEADLINES *                                                      
PAGETOP2 NTR1                                                                   
         ZAP   LINE,=P'100'        FORCE PAGE BREAK                             
         XC    MID1,MID1                                                        
         XC    MID2,MID2                                                        
         LA    R5,IOAREA2                                                       
         USING SORT2RCD,R5                                                      
         XC    TITLE,TITLE                                                      
         MVC   TITLE+10(20),=C'NETWORK PAY ACTIVITY'                            
         MVC   MID1(6),=C'AGENCY'                                               
         MVC   MID1+9(2),SRTAGY2                                                
         MVC   MID1+42(25),=C'* CHANGES TO PAID UNITS *'                        
         MVC   MID1+98(18),=C'REPORT NUMBER NECA'                               
         MVC   MID2(6),=C'CLIENT'                                               
         GOTO1 =V(CLUNPK),DMCB,SRTCLT2,MID2+9                                   
         MVC   MID3(7),=C'NETWORK'                                              
         MVC   MID3+9(4),SRTNET2                                                
                                                                                
         LA    R2,P                                                             
         USING PLINED2,R2                                                       
         MVC   PLNPYD1+5(5),=C'GROSS'                                           
         MVC   PLNPYD1+20(5),=C'GROSS'                                          
         MVC   PLNPYD4+5(3),=C'NET'                                             
         MVC   PLNPYD4+22(3),=C'NET'                                            
                                                                                
         GOTO1 =V(PRINTER)                                                      
                                                                                
         MVC   PLNEST2(3),=C'EST'                                               
         MVC   PLNPKG2(3),=C'PKG'                                               
         MVC   PLNPRG2(7),=C'PROGRAM'                                           
         MVC   PLNDTE2(4),=C'DATE'                                              
         MVC   PLNSUB2(3),=C'LN#'                                               
         MVC   PLNPYD1+6(4),=C'TIME'                                            
         MVC   PLNPYD2+1(11),=C'INTEGRATION'                                    
         MVC   PLNPYD3+5(5),=C'TOTAL'                                           
         MVC   PLNPYD4+6(4),=C'TIME'                                            
         MVC   PLNPYD5+1(11),=C'INTEGRATION'                                    
         MVC   PLNPYD6+5(5),=C'TOTAL'                                           
                                                                                
         GOTO1 =V(PRINTER)                                                      
                                                                                
         MVC   PLNEST2(3),=31C'-'                                               
         MVC   PLNPKG2(3),=31C'-'                                               
         MVC   PLNPRG2(7),=31C'-'                                               
         MVC   PLNDTE2(8),=31C'-'                                               
         MVC   PLNSUB2(3),=31C'-'                                               
         MVC   PLNPYD1(15),=31C'-'                                              
         MVC   PLNPYD2(15),=31C'-'                                              
         MVC   PLNPYD3(15),=31C'-'                                              
         MVC   PLNPYD4(15),=31C'-'                                              
         MVC   PLNPYD5(15),=31C'-'                                              
         MVC   PLNPYD6(15),=31C'-'                                              
         GOTO1 =V(PRINTER)                                                      
         B     EXIT                                                             
         DROP  R2,R5                                                            
         EJECT                                                                  
RPT3     CLI   FRST,C'Y'                                                        
         BE    *+8                                                              
         BAS   RE,PRNTLOGE                                                      
         CLI   EOFSRTRC,C'Y'                                                    
         BE    EXIT                                                             
         DC    H'0'                                                             
         EJECT                                                                  
HIGH     NTR1                                                                   
         MVC   KEYSAVE,KEY                                                      
         GOTO1 =V(DATAMGR),DMCB,=C'DMRDHI',=C'SPTDIR',KEYSAVE,KEY               
         B     PUTRECX                                                          
*                                                                               
GETREC   NTR1                                                                   
         LA    RF,=C'GETREC'                                                    
         B     PUTREC2                                                          
*                                                                               
PUTREC   NTR1                                                                   
         LA    RF,=C'PUTREC'                                                    
         B     PUTREC2                                                          
*                                                                               
WRITE    NTR1                                                                   
         LA    RF,=C'DMWRT'                                                     
*                                                                               
PUTREC2  ST    RF,DMCB            NOTE R7=NETBLOCK/NOW DESTROYED                
         GOTO1 =V(DATAMGR),DMCB,,=C'SPTFILE',KEY+14,(R7),DMWORK                 
*                                                                               
PUTRECX  CLI   8(R1),0             SET CC ON EXIT                               
         B     EXIT                                                             
                                                                                
RCVTAPE  DCB   DDNAME=RECVIN,                                          X        
               DSORG=PS,                                               X        
               RECFM=VB,                                               X        
               MACRF=GM,                                               X        
               EODAD=EXIT                                                       
                                                                                
*CVOUT   DCB   DDNAME=RECVOUT,                                                  
*              DSORG=PS,                                                        
*              RECFM=VB,                                                        
*              LRECL=2024,                                                      
*              BLKSIZE=2028,                                                    
*              MACRF=PM                                                         
                                                                                
         DS    0D                                                               
         DC    C'*SSB*SSB*SSB*SSB'                                              
SSB      DC    XL(SSOOFFX-SSOOFF)'00'                                           
         ORG   SSB+(SSOXTND-SSBD)                                               
         DC    X'FF'               SET EXTENDED OFFLINE SSB                     
         ORG   SSB+(SSOSTAT2-SSBD)                                              
         DC    AL1(SSOSNRCV)       SET RECOVERY OFF                             
         ORG                                                                    
*                                                                               
         DS    0D                                                               
         DC    CL16'*UTL*UTL*UTL*UTL'                                           
UTL      DC    (TUTLXALN)X'00'                                                  
         ORG   UTL+(TSYS-UTLD)                                                  
         DC    X'01'                                                            
         ORG                                                                    
         EJECT ,                                                                
***********************************************************************         
* LITERAL POOL                                                                  
***********************************************************************         
         LTORG                                                                  
                                                                                
***********************************************************************         
* EQUATES                                                                       
***********************************************************************         
YES      EQU   C'Y'                                                             
NO       EQU   C'N'                                                             
*                                                                               
COPY     EQU   1                                                                
CHANGE   EQU   2                                                                
ADD      EQU   3                                                                
*                                                                               
QUNTFIL  EQU   X'2A'                                                            
***********************************************************************         
* CONSTANTS                                                                     
***********************************************************************         
ACOMFACS DC    A(COMFACS)                                                       
*                                                                               
COMFACS  DS    0F                  DUMMY COMFACS LIST                           
         DC    V(DATAMGR)                                                       
         DC    8A(0)                                                            
         DC    V(HEXOUT)                                                        
         DC    A(0)                                                             
         DC    V(DATVAL)                                                        
         DC    V(DATCON)                                                        
         DC    10A(0)                                                           
         DC    17A(0)                                                           
                                                                                
CNDATA   DS    XL14                                                             
                                                                                
FLIST2   DC    CL8' SPTFILE'                                                    
         DC    CL8' SPTDIR '                                                    
         DC    CL8'X       '                                                    
         EJECT                                                                  
         DS    0D                                                               
         DC    CL8'**AGYR**'                                                    
AGYHDRA  DS    2000C               AGENCY RECORD                                
AGYHDRAX EQU   *-1                                                              
*                                                                               
         DS    0D                                                               
         DC    CL8'*DBLOCK*'                                                    
DBLOCK   DC    2000X'00'                                                        
*                                                                               
         DS    0D                                                               
         DC    CL8'**WORK**'                                                    
WORKA    DS    0D                   **** NMOD CHAIN ****                        
         DC    10000D'0'                                                        
         DC    CL8'**WORX**'                                                    
         EJECT                                                                  
RECD     DSECT                                                                  
RECLN    DS    XL2                                                              
         DS    CL2                                                              
REC      DS    0C                                                               
       ++INCLUDE DMRCVRHDR                                                      
       ++INCLUDE NEGENUNIT                                                      
         EJECT                                                                  
***********************************************************************         
* DSECT TO COVER NETBLOCK                                                       
***********************************************************************         
NEBLOCKD DSECT                                                                  
       ++INCLUDE NETBLOCKN                                                      
         EJECT                                                                  
SORTRECD DSECT                                                                  
SRTRTY   DS    CL1                 REPORT TYPE                                  
SRTAGY   DS    CL2                 AGY/MED                                      
SRTRBA   DS    CL1                 REPORT BY AGENCY                             
SRTCLT   DS    CL2                 CLIENT                                       
SRTNET   DS    CL4                 NETWORK                                      
SRTEST   DS    CL1                 ESTIMATE                                     
SRTPKG   DS    CL1                 PACKAGE                                      
SRTKLNE  EQU   *-SORTRECD                                                       
SRTPRG   DS    CL6                 PROGRAM                                      
SRTSUB   DS    CL1                 SUBLINE                                      
SRTAIRDT DS    CL2                 AIR DATE                                     
SRTBTGR  DS    F                   BILL TIME GROSS                              
SRTBTNT  DS    F                   BILL TIME NETS                               
SRTBIGR  DS    F                   BILL INTEGRATION GROSS                       
SRTBINT  DS    F                   BILL INTEGRATION NET                         
SRTPTGR  DS    F                   PAY TIME GROSS                               
SRTPTNT  DS    F                   PAY TIME NET                                 
SRTPIGR  DS    F                   PAY INTEGRATION GROSS                        
SRTPINT  DS    F                   PAY INTEGRATION NET                          
SRTASGN  DS    F                   ORDERED ASSIGNED     (CENTS)                 
SRTACT   DS    F                   ORDERED ACTUAL       (CENTS)                 
SRTINTG  DS    F                   ORDERED INTEGRATION  (CENTS)                 
SRTRCLNE EQU   *-SORTRECD                                                       
*                                                                               
SORT2RCD DSECT                                                                  
SORTRC2  DS    0CL68                                                            
SRTRTY2  DS    CL1                 REPORT TYPE                                  
SRTAGY2  DS    CL2                 AGY/MED                                      
SRTRBA2  DS    CL1                 REPORT BY AGENCY                             
SRTCLT2  DS    CL2                 CLIENT                                       
SRTNET2  DS    CL4                 NETWORK                                      
SRTEST2  DS    CL1                 ESTIMATE                                     
SRTPKG2  DS    CL1                 PACKAGE                                      
SRTKLNE2 EQU   *-SORT2RCD                                                       
SRTPRG2  DS    CL6                 PROGRAM                                      
SRTSUB2  DS    CL1                 SUBLINE                                      
SRTAIRD2 DS    CL2                 AIR DATE                                     
SRTUSER2 DS    CL2                 USER ID                                      
SRTPYGRO DS    F                   OLD PAY GROSS                                
SRTPYGIO DS    F                   OLD PAY GROSS INTEGRATION                    
SRTPYNEO DS    F                   OLD PAY NET                                  
SRTPYNIO DS    F                   OLD PAY NET INTEGRATION                      
SRTPYGRN DS    F                   NEW PAY GROSS                                
SRTPYGIN DS    F                   NEW PAY GROSS INTEGRATION                    
SRTPYNEN DS    F                   NEW PAY NET                                  
SRTPYNIN DS    F                   NEW PAY NET INTEGRATION                      
SRTEND2  DS    0H                                                               
SRTRCLN2 EQU   SRTRCLNE-SORTRECD                                                
SRTSPAR2 EQU   *-SRTEND2                                                        
         EJECT                                                                  
PLINED   DSECT                                                                  
PLNAGY   DS    CL2                 AGENCY                                       
         DS    CL1                                                              
PLNCLT   DS    CL3                 CLIENT                                       
         DS    CL1                                                              
PLNNET   DS    CL4                 NETWORK                                      
         DS    CL1                                                              
PLNEST   DS    CL3                 ESTIMATE                                     
         DS    CL1                                                              
PLNPKG   DS    CL3                 PACKAGE                                      
         DS    CL1                                                              
PLNBILGR DS    CL15                BILL GROSS                                   
         DS    CL1                                                              
PLNBILNT DS    CL15                BILL NET                                     
         DS    CL1                                                              
PLNPAYGR DS    CL15                PAY GROSS                                    
         DS    CL1                                                              
PLNPAYNT DS    CL15                PAY NET                                      
         DS    CL1                                                              
PLNOASGN DS    CL15                ORDERED ASSIGNED                             
         DS    CL1                                                              
PLNOACT  DS    CL15                ORDERED ACTUAL                               
         DS    CL1                                                              
PLNOINTG DS    CL15                ORDERED INTEGRATION                          
         EJECT                                                                  
PLINED2  DSECT                                                                  
PLNEST2  DS    CL3                 ESTIMATE                                     
         DS    CL1                                                              
PLNPKG2  DS    CL3                 PACKAGE                                      
         DS    CL1                                                              
PLNPRG2  DS    CL7                 PROGRAM                                      
         DS    CL1                                                              
PLNDTE2  DS    CL8                 UNIT DATE                                    
         DS    CL1                                                              
PLNSUB2  DS    CL3                 UNIT SUB LINE NUMBER                         
         DS    CL1                                                              
PLNLIT   DS    CL4                 LITERAL                                      
         DS    CL1                                                              
PLNPYD1  DS    CL15                                                             
         DS    CL1                                                              
PLNPYD2  DS    CL15                                                             
         DS    CL1                                                              
PLNPYD3  DS    CL15                                                             
         DS    CL1                                                              
PLNPYD4  DS    CL15                                                             
         DS    CL1                                                              
PLNPYD5  DS    CL15                                                             
         DS    CL1                                                              
PLNPYD6  DS    CL15                                                             
         EJECT                                                                  
MYWORKD  DSECT                                                                  
* 3200 BYTES REQUESTED IN NBASE *                                               
DUB      DS    D                                                                
TOTAL    DS    D                                                                
NETIO    DS    V                                                                
RELO     DS    F                                                                
AIO      DS    A                   IO AREA POINTER                              
AUNITREC DS    A                   UNIT RECORD POINTER                          
FULL     DS    F                                                                
DMCB     DS    6F                                                               
DMWORK   DS    F                                                                
WORK     DS    CL64                                                             
ELCODE   DS    CL1                                                              
DATADISP DS    H                                                                
*                                                                               
COPYDOL1 DS    0CL32                                                            
CPDBTG   DS    F         1         BILL TIME GROSS                              
CPDBTN   DS    F         2         BILL TIME NET                                
CPDITG   DS    F         3         INTEGRATION TIME GROSS                       
CPDITN   DS    F         4         INTEGRATION TIME NET                         
CPDPTG   DS    F         5         PAY TIME GROSS                               
CPDPTN   DS    F         6         PAY TIME NET                                 
CPDPIG   DS    F         7         PAY INTEGRATION GROSS                        
CPDPIN   DS    F         8         PAY INTEGRATION NET                          
*                                                                               
COPYDOL2 DS    0CL12                                                            
CPDOASGN DS    F         9         ORDERED ASSIGNED                             
CPDOACT  DS    F         10        ORDRERED ACTUAL                              
CPDINTG  DS    F         11        ORDERED INTEGRATION                          
*                                                                               
CHADOL1  DS    0CL32                                                            
CHADL1   DS    F                                                                
CHADL2   DS    F                                                                
CHADL3   DS    F                                                                
CHADL4   DS    F                                                                
CHADL5   DS    F                                                                
CHADL6   DS    F                                                                
CHADL7   DS    F                                                                
CHADL8   DS    F                                                                
*                                                                               
CHADOL2  DS    0CL12                                                            
CHADL9   DS    F                                                                
CHADL10  DS    F                                                                
CHADL11  DS    F                                                                
ENDCHA   DS    CL3                                                              
*                                                                               
         DS    0D                                                               
PKGTOT   DS    7PL8                PACKAGE TOTALS                               
*              1(BILL GROSS) 2(BILL NET) 3(PAY GROSS) 4(PAY NET)                
*              5(ORD ASGND)  6(ORD ACT)  7(ORD INTEG)                           
ESTTOT   DS    7PL8                ESTIMATE TOTALS                              
NETTOT   DS    7PL8                NETWORK TOTALS                               
CLTTOT   DS    7PL8                CLIENT TOTALS                                
AGYTOT   DS    7PL8                AGENCY TOTALS                                
ENDTOTS  DS    CL1                 EOF TOTS                                     
SAVEADR  DS    F                                                                
ALLAGY   DS    7PL8                ALL AGENCY TOTALS                            
ENDAGYAL DS    CL1                 EOF ALL AGENCY TOTS                          
*                                                                               
RCVRYSV  DS    CL60                                                             
TODAYB   DS    CL3                                                              
TODAYP2  DS    CL2                                                              
TODAYP3  DS    CL3                                                              
CARD     DS    CL80                                                             
TOTCTR   DS    CL4                                                              
ADDCTR   DS    CL4                                                              
CHGCTR   DS    CL4                                                              
UNKCTR   DS    CL4                                                              
CPYCTR   DS    CL4                                                              
CPYCHCTR DS    CL4                                                              
PREVFLG  DS    CL1                                                              
ZEROFLG  DS    CL1                 BIT 0=ZERO CHEK IS ACTIVE                    
*                                  BIT 1=BYPASS PRINT                           
DELETFLG DS    CL1                                                              
KEY      DS    CL20                                                             
KEYSAVE  DS    CL20                                                             
BRKBYTE  DS    CL1                                                              
EOFSRTRC DS    CL1                                                              
FRST     DS    CL1                                                              
NETTXT   DS    CL5                                                              
SPTTXT   DS    CL6                                                              
WRITEFLG DS    CL1                                                              
DUMPOS   DS    CL1                                                              
ONEAGY   DS    CL2                                                              
NOLOGO   DS    CL1                                                              
         DS    0F                                                               
PAYDLRS  DS    0CL32                                                            
PAYCPYGR DS    F                   GROSS DOLLARS      -  COPY                   
PAYCPYGI DS    F                   GROSS INTEG                                  
PAYCPYNE DS    F                   NET DOLLARS                                  
PAYCPYNI DS    F                   NET INTEG                                    
PAYCHAGR DS    F                   GROSS              -  CHANGE                 
PAYCHAGI DS    F                   GROSS INTEG                                  
PAYCHANE DS    F                   NET DOLLARS                                  
PAYCHANI DS    F                   NET INTEG                                    
RCCNTR   DS    F                                                                
IOAREA1  DS    CL1000                                                           
IOAREA2  DS    CL1000                                                           
         DS    CL2048     ** NEEDED FOR EXPANDED RECS ON RECOVERY FILE          
         DS    CL2048     ** NEEDED FOR EXPANDED RECS ON RECOVERY FILE          
MYWRKDLE EQU   *-MYWORKD                                                        
         EJECT                                                                  
       ++INCLUDE DDDPRINT                                                       
         EJECT                                                                  
       ++INCLUDE DDLOGOD                                                        
         EJECT                                                                  
       ++INCLUDE DDCNTRL                                                        
* DDNAMED                                                                       
DDNAMED DSECT                                                                   
       ++INCLUDE DMDDNAMED                                                      
* FAUTL                                                                         
       ++INCLUDE FAUTL                                                          
* FASSB                                                                         
       ++INCLUDE FASSB                                                          
         ORG   SSBD                                                             
       ++INCLUDE FASSBOFF                                                       
         ORG                                                                    
         EJECT                                                                  
AGYHDRD  DSECT                                                                  
       ++INCLUDE SPGENAGY                                                       
**PAN#1  CSECT                                                                  
**PAN#1  DC    CL21'005NET133    12/12/13'                                      
         END                                                                    
